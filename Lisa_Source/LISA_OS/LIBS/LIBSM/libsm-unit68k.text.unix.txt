;-----------------------------------------------------------------------
; UNIT68K       LOTUS ASSEMBLY ROUTINES
;-----------------------------------------------------------------------

; Copyright 1983, Apple Computer Inc.

        ;------------
        .MACRO  BIAS
        ;------------
        MOVE.L  (SP)+,(SP)
        .ENDM

        ;----------------------------------
        .MACRO   ADJSP           ;ADJUST SP
        ;----------------------------------
        MOVE.L  (SP),%1(SP)
        ADD     #%1,SP
        .ENDM

        ;---------------------------------
        .FUNC   WHERE           ;Where: TC
        ;---------------------------------
        ADJSP   2
        LEA     *,A0
        MOVE    A0,4(SP)
        RTS

        ;----------------------------------------
        .FUNC   WNOT,1          ;WNot(w: TC): TW;
        ;----------------------------------------
        MOVE.L  (SP)+,A0
        MOVE    (SP)+,(SP)
        NOT     (SP)
        JMP     (A0)


        ;-------------------------------------------
        .FUNC   WXOR,2          ;WXor(w1,w2: TW): TW
        ;-------------------------------------------
        MOVE.L  (SP)+,A0
        MOVE    (SP)+,D0
        MOVE    (SP)+,D1
        EOR     D1,D0
        MOVE    D0,(SP)
        JMP     (A0)

        ;-------------------------------------------
        .FUNC   PRegGlobal          ;PRegGlobal : TP
        ;-------------------------------------------
        MOVE.L  (SP)+,A0
        MOVE.L  A5,(SP)
        JMP     (A0)

        ;-------------------------------------------
        .FUNC   WAND,2          ;WAnd(w1,w2: TW): TW
        ;-------------------------------------------
        MOVE.L  (SP)+,A0
        MOVE    (SP)+,D0
        MOVE    (SP)+,D1
        AND     D1,D0
        MOVE    D0,(SP)
        JMP     (A0)

        ;-------------------------------------------------
        .FUNC   FINSET,2        ;FInSet(el, elSet: TW): TF
        ;-------------------------------------------------
        MOVE.L  (SP)+,A0
        MOVE    (SP)+,D0        ;elSet
        MOVE    (SP)+,D1        ;el
        MOVE    #0,(SP)
        AND     D1,D0
        BEQ     @1
        MOVE.B    #1,(SP)
@1:
        JMP     (A0)


        ;---------------------------------------------
        .FUNC   WSHLFT,2        ;WShLft(w; cbitSh): TW
        ;---------------------------------------------
        MOVE.L  (SP)+,A0
        MOVE    (SP)+,D0        ;cbitSh
        MOVE    (SP)+,D1        ;w
        LSL     D0,D1
        MOVE    D1,(SP)
        JMP     (A0)

        ;--------------------------------------------
        .FUNC   WSHRT,2         ;WShRt(w; cbitSh): TW
        ;--------------------------------------------
        MOVE.L  (SP)+,A0
        MOVE    (SP)+,D0        ;cbitSh
        MOVE    (SP)+,D1        ;w
        LSR     D0,D1
        MOVE    D1,(SP)
        JMP     (A0)

        ;---------------------------------------------
        .PROC   DSHRT,2         ;DShRt(VAR dw; cbitSh)
        ;---------------------------------------------
        MOVE.L  (SP)+,A0
        MOVE    (SP)+,D0        ;cbitSh
        MOVE.L  (SP)+,A1
        MOVE.L  (A1),D1
        ASR.L   D0,D1
        MOVE.L  D1,(A1)
        JMP     (A0)

        ;----------------------------------------------------------
        .PROC   ASGNFIELD,5     ;AsgnField(p,ib,mask,val,cbitSh)
        ;----------------------------------------------------------
        MOVE.L  (SP)+,A0
        MOVE    (SP)+,D0        ;cbitSh
        MOVE    (SP)+,D1        ;val
        LSL     D0,D1

        MOVE    (SP)+,D2        ;mask
        MOVE    (SP)+,A1        ;ib
        ADD.L   (SP)+,A1        ;p

        MOVE    (A1),D0
        AND     D2,D0           ;mask
        OR      D1,D0           ;val shifted
        MOVE    D0,(A1)

        JMP     (A0)

        ;-----------------------------------------------------
        .FUNC   VALOFFIELD,4    ;ValOfField(p,ib,mask,cbitSh):w
        ;-----------------------------------------------------
        MOVE.L  (SP)+,A0
        MOVE    (SP)+,D1        ;cbitSh
        MOVE    (SP)+,D0        ;mask
        NOT     D0

        MOVE    (SP)+,A1        ;ib
        ADD.L   (SP)+,A1        ;p

        AND     (A1),D0
        LSR     D1,D0
        MOVE    D0,(SP)

        JMP     (A0)

        ;---------------------------------------------
        .PROC   CallSeq,6         ;CallSeq(lpd, VAR achad, proc)
        ;---------------------------------------------
        MOVE.L  (SP)+,A0
        MOVE.L  (SP),A1        ;proc
        MOVE.L  A0, (SP)       ; restore return address
        JMP     (A1)

        ;---------------------------------------------
        .PROC   CallFree,3         ;CallSeq(ics, proc)
        ;---------------------------------------------
        MOVE.L  (SP)+,A0
        MOVE.L  (SP),A1        ;proc
        MOVE.L  A0, (SP)       ; restore return address
        JMP     (A1)

        ;---------------------------------------------
        .PROC   PxHcs,4         ;PxHcs(hcs, proc)
        ;---------------------------------------------
        MOVE.L  (SP)+,A0
        MOVE.L  (SP),A1        ;proc
        MOVE.L  A0, (SP)       ; restore return address
        JMP     (A1)

        ;---------------------------------------------
        .PROC   CallFindLpFixed,7  ;FindLpFixed(ics, lp, VAR lpFixed, proc)
        ;---------------------------------------------
        MOVE.L  (SP)+,A0
        MOVE.L  (SP),A1        ;proc
        MOVE.L  A0, (SP)       ; restore return address
        JMP     (A1)

        ;---------------------------------------------
        .PROC   CallGenCrd,5  ;CallGenCrd(ics, lpFst, proc)
        ;---------------------------------------------
        MOVE.L  (SP)+,A0
        MOVE.L  (SP),A1        ;proc
        MOVE.L  A0, (SP)       ; restore return address
        JMP     (A1)

        ;---------------------------------------------
        .PROC   CallGenImg,5  ;CallGenImg(crd, oboxPane, proc)
        ;---------------------------------------------
        MOVE.L  (SP)+,A0
        MOVE.L  (SP),A1        ;proc
        MOVE.L  A0, (SP)       ; restore return address
        JMP     (A1)

        ;---------------------------------------------
        .PROC   CallHilight,8  ;CallHilight(crd, oboxPane, oint, tyHi, fShow, proc)
        ;---------------------------------------------
        MOVE.L  (SP)+,A0
        MOVE.L  (SP),A1        ;proc
        MOVE.L  A0, (SP)       ; restore return address
        JMP     (A1)

        ;---------------------------------------------
        .FUNC   CallFSelLpBounds,9; (lp, ics, VAR lpFstSel,
        ;                            VAR lpLimSel, proc) : TF
        ;---------------------------------------------
        MOVE.L  (SP)+,A0
        MOVE.L  (SP),A1        ;proc
        MOVE.L  A0, (SP)       ; restore return address
        JMP     (A1)

        ;---------------------------------------------
        .FUNC   CallCbMore,5; (hz, cb, proc) : TF
        ;---------------------------------------------
        MOVE.L  (SP)+,A0
        MOVE.L  (SP),A1        ;proc
        MOVE.L  A0, (SP)       ; restore return address
        JMP     (A1)

        ;---------------------------------------------
        .FUNC   CbOfN,4; (n, proc) : TC
        ;---------------------------------------------
        MOVE.L  (SP)+,A0
        MOVE.L  (SP),A1        ;proc
        MOVE.L  A0, (SP)       ; restore return address
        JMP     (A1)

        ;---------------------------------------------
        .FUNC   FSwapInN,6; (n, rgch, proc) : TF
        ;---------------------------------------------
        MOVE.L  (SP)+,A0
        MOVE.L  (SP),A1        ;proc
        MOVE.L  A0, (SP)       ; restore return address
        JMP     (A1)

        ;---------------------------------------------
        .PROC   SwapOutN,6; (n, rgch, proc)
        ;---------------------------------------------
        MOVE.L  (SP)+,A0
        MOVE.L  (SP),A1        ;proc
        MOVE.L  A0, (SP)       ; restore return address
        JMP     (A1)

        ;---------------------------------------------
        .FUNC   CallHeur,3; (lfntid, VAR charstyle, VAR str30, proc):TF
        ;---------------------------------------------
        MOVE.L  (SP)+,A0       ; save return address
        MOVE.L  (SP),A1        ; proc
        MOVE.L  A0, (SP)       ; restore return address
        JMP     (A1)

        ;---------------------------------------------
        .PROC   CallErr,1; (cErr, procErr);
        ;---------------------------------------------
        MOVE.L  (SP)+,A0       ; save return address
        MOVE.L  (SP),A1        ; proc
        MOVE.L  A0, (SP)       ; restore return address
        JMP     (A1)


;---------------------------------------------------------------------------------
;---------------------------------------------------------------------------------
                .FUNC   IpnMapN                 ;(hz: THz; nSrc: TN): TC
;---------------------------------------------------------------------------------
;
; FUNCTION IpnMapN (hz: THz; nSrc: TN): TC;
;
; relocatable, clobbers only A0
;
; external dependencies: variable declarations-specifically, mskLpnLst must be
;        30 bytes from front of TAhz record, rgpnob must be 26 bytes from front
;        of TAhz record; NIL must equal zero
;
; registers assigned as follows:
;
;       D0 scratch (long index) A0 scratch
;       D1 index                A1 rgpnob
;       D2 nSrc                 A2 address of name
;       D3 mskLpnLst
;
;
;
; offsets from A6
;
PARAMSIZE       .EQU    8
hz              .EQU    PARAMSIZE + 8 - 4
nSrc            .EQU    hz - 4
;
; record offsets: depend on type declarations
;
mskLpnLst       .EQU    30
hrgpnob         .EQU    26
NIL             .EQU    0

                LINK    A6, #0
                MOVEM.L D0-D3/A1-A2, -(SP)

;
; set up registers
;
                MOVE.L  hz (A6), A0             ; base address of record
                CLR.L   D3                      ; clear top of D3
                MOVE    mskLpnLst (A0), D3      ; get mskLpnLst
                MOVE.L  nSrc (A6), D2           ; get nSrc
                MOVE.L  D2, D1                  ; copy nSrc

;
; hashing function equals half-rotated, exclusive-or of swapped halves of name
;
                SWAP    D1                      ; arbitrary hashing function:
                EOR     D2, D1                  ;   returns word-sized index
                ROR     #4, D1                  ;   however, index times four (for
                AND.L   D3, D1                  ;   long names) can be long

;
; search loop: roll is beginning of rollover part-if index overflows
;
ROLL            MOVE.L  D1, D0                  ; compute address of table entry
                ASL.L   #2, D0                  ; index times four
                MOVE.L  hrgpnob (A0), A1        ; offset from record base
                MOVE.L  (A1), A1                ;
LOOP            CMP.L   #NIL, 0 (A1, D0.L)      ; check if spot is empty
                BEQ     ASSIGN                  ; if so, return that index
                MOVE.L  0 (A1, D0.L), A2        ; if not, check if it matches
                CMP.L   (A2), D2                ; check nSrc against spot
                BEQ     ASSIGN                  ; if they match return that index
                ADD     #1, D1                  ; if not, check the next one
                AND     D3, D1                  ;     and mask it
                BEQ     ROLL                    ; if rollover, start at base + 0
                ADD.L   #4, D0                  ; index to next entry
                BRA     LOOP

;
; assignment of result to function and return
;
ASSIGN          MOVE    D1, PARAMSIZE + 8 (A6)  ; push function result on stack
                MOVEM.L (SP)+, D0-D3/A1-A2      ; restore registers
                UNLK    A6                      ; restore old frame pointer
                MOVE.L  (SP)+, A0               ; get return address
                ADD     #PARAMSIZE, SP          ; clean up stack
                JMP     (A0)                    ; return


;---------------------------------------------------------------------------------
;---------------------------------------------------------------------------------
                .FUNC   IBinSearch
;---------------------------------------------------------------------------------
;
; Function IBinSearch (rgc: TRgc; icLst: TC; c: TC): TC
;
; Clobbers only A0. Position independent.
;
; No external dependencies aside from variable declarations
;
; The following register assignments are made:
;
;                  D0 SCRATCH                   A0 SCRATCH
;                  D1 icFst (Local variable)    A1 rgc   (Parameter)
;                  D2 icMid (Local variable)    A6 FRAME POINTER (Standard)
;                  D3 icLst (Parameter)         A7 STACK POINTER (Standard)
;                  D4 c     (Parameter)
;
;
;---------------------------------------------------------------------------------
;
; A6 offsets after link.
;
PARAMSIZE       .EQU    8                       ; size of parameters
rgc             .EQU    PARAMSIZE + 8 - 4       ; long, parameter
icLst           .EQU    rgc - 2                 ; word, parameter
c               .EQU    icLst - 2               ; word, parameter

;----------------------------------------------------------------------------------
;
; begin here. Sets up stack and saves old registers.
;
                LINK    A6, #0                  ; set up new frame pointer
                MOVEM.L D0-D7/A1-A5, -(SP)      ; save old registers
;
; move data to registers for convenience and speed
;
                CLR     D1                      ; icFst := 0
                MOVE    icLst (A6), D3          ; D3 gets icLst
                MOVE.L  rgc (A6), A1            ; A1 gets address of rgc
                MOVE    c (A6), D4              ; D4 gets c (key)

;---------------------------------------------------------------------------------
;
; icMid := (icFst + icLst) div 2
;
LOOP            MOVE    D1, D2                  ; D2 := icFst
                ADD     D3, D2                  ; D2 := D2 + icLst
                ASR     #1, D2                  ; D2 := D2/2

; if icLst - icFfst < 2 then goto ASSIGN; i.e., if they differ by less than two,
then
; (icFst + icLst) div 2 = icFst (rounding down to the smaller one) and we branch
;
                CMP     D1, D2                  ; compare icFst and the midpoint
                BEQ     ASSIGN                  ; if they're equal, get out
                MOVE    D2, D0                  ; otherwise continue
                ASL     #1, D0                  ; new midpoint
;
; if c < rgc^[icMid] then goto MIDLST
;
                CMP     0 (A1, D0), D4          ; compare to c
                BLT     MIDLST                  ; if number < key, midpoint := last
                MOVE    D2, D1                  ; else icFst := icMid
                BRA     LOOP
MIDLST          MOVE    D2, D3                  ;      icLst := icMid
                BRA     LOOP

;---------------------------------------------------------------------------------
;
; return a value for the function. if c < rgc^[icLst] then goto RTNFST
;
ASSIGN          MOVE    D3, D0                  ; put icLst in D0
                ASL     #1, D0                  ; multiply by two
                CMP     0 (A1, D0), D4          ; compare to c
                BLT     RTNFST                  ; if less than the key, return
icFst
                MOVE    D3, PARAMSIZE + 8 (A6)  ; else return icLst
                BRA     RETURN
RTNFST          MOVE    D1, PARAMSIZE + 8 (A6)  ;        return icFst
                BRA     RETURN

;---------------------------------------------------------------------------------
;
; unlink and return to calling program
;
RETURN          MOVEM.L (SP)+, D0-D7/A1-A5      ; restore old registers
                UNLK    A6                      ; restore old frame pointer
                MOVE.L  (SP)+, A0               ; set up return address
                ADD     #PARAMSIZE, SP          ; clean up stack
                JMP     (A0)                    ; return


                .PROC   MoveOvRgch,3
;----------------------------------------------------------------------
;
; BlockMove(rgchDst, rgchSrc: Ptr; nBytes: INTEGER);
;
; This version will move in either direction
; modified by AJH to move by longs if it can
;
                MOVE.L  (SP)+,D0                ;SAVE RETURN ADDRESS
                CLR.L   D1                      ;to avoid sign extension
                MOVE    (SP)+,D1                ;GET BYTE COUNT
                MOVE.L  (SP)+,A0                ;GET SRC POINTER
                MOVE.L  (SP)+,A1                ;GET DST POINTER
                MOVE.L  D0,-(SP)                ;PUT BACK RETURN ADDRESS
                TST     D1                      ;CHECK FOR ZERO LENGTH
                BEQ.S   MOVEDONE                ;YES, RETURN
;
; if both address are even or both are odd, we have a chance of optimizing
;
                MOVE    D2,-(SP)                ;preserve D2
                MOVE    A0,D0
                MOVE    A1,D2
                EOR     D0,D2                   ;do the low bits match?
                LSR     #1,D2                   ;test the low bit
                BCC.S   MOVEFAST                ;if they match, go move fast
                MOVE    (SP)+,D2                ;restore D2
;
; we couldn't optimize so move a byte at a time
;
MOVENORMAL      CMP.L   A1,A0                   ;SRC < DST?
                BLT.S   MOVERIGHT               ;YES, MOVERIGHT
MOVENOR1        SUB     #1,D1                   ;ELSE MOVELEFT
@1              MOVE.B  (A0)+,(A1)+
                DBRA    D1,@1
                RTS
;
MOVERIGHT       ADD     D1,A0                   ;POINT TO END OF BLOCKS
                ADD     D1,A1
MOVENOR2        SUB     #1,D1
@1              MOVE.B  -(A0),-(A1)             ;MOVERIGHT
                DBRA    D1,@1
MOVEDONE        RTS
;
; here is the move by longs part.  First make sure we are trying to move at least
; 5 bytes (5 instead of 4 in case of odd boundary)
;
MOVEFAST        MOVE    (SP)+,D2                ;restore D2
                CMP     #5,D1
                BLT.S   MOVENORMAL              ;if <5, go move slow
;
; check if moving up or down
;
                CMP.L   A1,A0                   ;source<destination?
                BLT.S   FMOVERIGHT
;
; make sure we start on an even address
;
                MOVE    A0,D0
                LSR     #1,D0
                BCC.S   FINISHLEFT
;
                MOVE.B  (A0)+,(A1)+             ;get on an even address
                SUBQ    #1,D1                   ;adjust the count
;
FINISHLEFT      MOVE    D1,D0                   ;save count for clean-up
                LSR     #2,D0                   ;divide by 4
                SUBQ    #1,D0                   ;adjust for DBRA
;
; here's the actual loop
;
@1              MOVE.L  (A0)+,(A1)+             ;move 4 bytes
                DBRA    D0,@1
;
; now handle the remainder
;
                AND     #3,D1
                BNE.S   MOVENOR1                ;if there's any left, go move em
MOVEFIN1        RTS
;
; handle moving right to left
;
FMOVERIGHT      ADD.L   D1,A0
                ADD.L   D1,A1                   ;bump out to the last byte
;
; make sure we start on an even address
;
                MOVE    A0,D0
                LSR     #1,D0
                BCC.S   FINISHRIGHT                  ;skip if on even address
;
                MOVE.B  -(A0),-(A1)             ;get on an even address
                SUBQ    #1,D1                   ;we moved one so subtract
;
FINISHRIGHT     MOVE    D1,D0                   ;save count for clean-up
                LSR     #2,D0                   ;divide by 4
                SUBQ    #1,D0                   ;adjust for DBRA
;
; heres the actual loop
;
@2              MOVE.L  -(A0),-(A1)             ;move 4 bytes
                DBRA    D0,@2                   ;loop till done
;
; finish up the remaining 0-3 bytes
;
                AND     #3,D1
                BEQ.S   MOVEFIN1
                BRA.S   MOVENOR2                ;go move a byte at a byte

        .END



ÿ