
(*$S SegHz*)
(*$G+*)

UNIT UnitStd;   INTRINSIC;

(* Copyright 1983, Apple Computer Inc. *)

{ edited by bas 9/10/81 }

INTERFACE

{$Setc fDbgOK   := FALSE}
{$Setc fOS      := TRUE}
{$Setc fSymOK   := FALSE}
{$Setc fstd1    := FALSE}
{$Setc fTrace   := FALSE}
{$SETC doTraceSM := TRUE}
{$SETC fTraceSM := doTraceSM AND fTrace}
{$IFC fTrace}
USES
    {$U libos/syscall.obj} Syscall;
{$ENDC}
(*$R-*)

CONST

chLst = 127;
chSp = 32;
chCr = 13;
chTab = 9;
olleNil = 0;
procNil = 1;
{$IFC NOT fDbgOk} fTstStd = FALSE; {$ENDC}
{$IFC NOT fstd1} fTstStd1 = FALSE; {$ENDC}

TYPE

TC = INTEGER;
TB = -128..127;
TA = 0..127;             {7-bit Ascii.}
TP = ^TB;
TPc = ^TC;
TPa = LONGINT;
TF = BOOLEAN;
TX = TC;
TY = TC;
TL = LONGINT;
TW = INTEGER;
TCh = TB;
TCd = TC;   { coordinate }
TN = TL;
TPn = ^TN;

TFd = RECORD iFil: TB; bhi: TB; iblo: TW END;

TPFd = ^TFd;

TNOrFd = PACKED RECORD
      CASE TC OF
      1: (fd: TFd);
      2: (n: TN);
      END;



TArgch = ARRAY [0..0] OF TCh;
TRgch = ^TArgch;
TAchad = RECORD
      rgch:   TRgch;
      ichFst: TC;
      ichLim: TC;
      END;
TChad = ^TAchad;

TArgb = ARRAY [0..0] OF TB;
TRgb = ^TArgb;
THrgb = ^TRgb;
TAbad = RECORD
      rgb:   TRgb;
      ichFst: TC;
      ichLim: TC;
      END;
TBad = ^TAbad;

TArgc = ARRAY [0..0] OF TC;
TRgc = ^TArgc;

TArgx = ARRAY [0..0] OF TX;
TRgx = ^TArgx;

TCmp = (cmpLs, cmpEq, cmpGr);

TAlld = RECORD
      olleFreeFst:    TC;
      olleLim:        TC;
      END;
TLld = ^TAlld;
TAlle = RECORD
      olleNxt:        TC;
      ollePrv:        TC;
      END;
TLle = ^TAlle;

TSp = STRING[80];
TPsp  = ^TSp;
THsp  = ^TPsp;


TARgCHAR = PACKED ARRAY[0..0] OF CHAR;
TRgCHAR = ^TARgCHAR;

TProc = TP;

VAR
  Temp: Integer;
{$IFC fDbgOk}
  fTstStd: TF;
{$ENDC}
{$IFC fstd1}
  fTstStd1: TF;
{$ENDC}


{$IFC NOT fSymOk} {$D-} {$ENDC}

FUNCTION CMin(c1: TC; c2: TC) : TC;

FUNCTION CMax(c1: TC; c2: TC) : TC;

PROCEDURE MoveAchad(VAR achadDst: TAchad; VAR achadSrc: TAchad);

{PROCEDURE MoveRvsAchad(VAR achadDst: TAchad; VAR achadSrc: TAchad);}

PROCEDURE MoveRgch(rgchDst: TRgch; rgchSrc: TRgch; cb: TC);

PROCEDURE MoveOvRgch(rgchDst: TRgch; rgchSrc: TRgch; cb: TC);

PROCEDURE DivMod(num: TC; denom: TC; VAR quotient: TC; VAR rem: TC);

FUNCTION IBinSearch(rgc: TRgc; icLst: TC; c: TC) : TC;

PROCEDURE Break(bkcd: TC);

PROCEDURE MoveOlle(lld: Tlld; olle: TC; VAR olleDstFst: TC; VAR olleSrcFst: TC);

PROCEDURE PxLld(lld: TLld);

PROCEDURE InitOlleFree(lld: TLld; olleFreeFst: TC; cbAlle: TC);

FUNCTION IchCr(VAR achad: TAchad; fFwd: TF) : TC;

FUNCTION WAnd(w1,w2: TW): TW;

FUNCTION WXor(w1,w2: TW): TW;

FUNCTION WNot(w: TW): TW;

FUNCTION WShLft(w: TW; cbitSh: TW) : TW;

FUNCTION WShRt(w: TW; cbitSh: TW) : TW;

PROCEDURE AsgnField(p: TP; ib: TW; mskNotFld: TW; val: TW; cbitSh: TC);

FUNCTION ValOfField(p: TP; ib: TW; mskNotFld: TW; cbitSh: TC) : TW;

FUNCTION UpperCh(ch: CHAR): CHAR;

PROCEDURE MakeSpUpper(VAR sp: TSp);

{$IFC fTrace}
Procedure InitTrCalls;

Procedure LogCall;

Procedure ResetTrace(LogNamesAndSegs:Boolean);

Procedure StartCallLog;

Procedure StopCallLog;

Procedure ListCalls;

Procedure SetCallTrace(Traceit:Boolean);
{$ENDC}

IMPLEMENTATION

{$IFC fTrace}
CONST
     TabLength = 5000;   {Hash Table length - 1}
     InitValue = 123456;
     OpenValue = 7654321;
     KeyLength = 9;      {name + seg}

TYPE


    Byte = -128..127;
    S8 = String[8];
    USB = 0..255;
    BA = Array [0..10] of Byte;
    BA8 = Array [0..7] of Byte;
    PBA = ^BA;

    TPint  = ^INTEGER;
    TPPint = ^TPint;


    TabEntry = Record
               name: BA8;
               seg: usb;
               pid: usb;
               next: integer;
               end;
     PTabEntry = ^TabEntry;

     Globals = Record
                 Initialized: longint;
                 LogCallsAndSegs: Boolean;
                 Logging: Boolean;
                 TraceProcs:Boolean;
               End;
     PtrGlobals = ^Globals;

VAR
    PBuf: PBA;  {DS pointer}
    PGlob : PtrGlobals;
    DSRefnum : Longint;
    OpenCalled: Longint;
{$ENDC}

FUNCTION WAnd; EXTERNAL;

FUNCTION WXor; EXTERNAL;

FUNCTION WNot; EXTERNAL;

FUNCTION WShLft; EXTERNAL;

FUNCTION WShRt; EXTERNAL;

PROCEDURE AsgnField; EXTERNAL;

FUNCTION ValOfField; EXTERNAL;



{$S SMclose}
FUNCTION UpperCh (* (ch: CHAR): CHAR *);
VAR up: CHAR;
BEGIN
{$IFC fTraceSM}   LogCall;   {$ENDC}
IF ch >= 'a'
 THEN up := CHR(ORD(ch)-32)
 ELSE up := ch;
UpperCh := up;
END;

{$S SMclose}
PROCEDURE MakeSpUpper (* (VAR sp: TSp) *);
VAR c: TC;
BEGIN
{$IFC fTraceSM}   LogCall;   {$ENDC}
c := 1;
WHILE c <= LENGTH(sp) DO
 BEGIN
 sp[c] := UpperCh(sp[c]);
 c := c + 1;
 END;
END;

{$S SMlwwrk}
FUNCTION IchCr;
VAR
dchT:   TC;
rgchScan: TRgCHAR;
BEGIN
{$IFC fTraceSM}   LogCall;   {$ENDC}
WITH achad DO BEGIN
IF fFwd THEN
      BEGIN
      rgchScan := @rgch^[ichFst];
      dchT := SCANEQ(ichLim-ichFst, CHR(chCr), rgchScan^);
      IchCr := ichFst+dchT;
      END
ELSE    BEGIN
      rgchScan := @rgch^[ichLim-1];
      dchT := SCANEQ(ichFst-ichLim, CHR(chCr), rgchScan^);
      IchCr := ichLim-1+dchT;
      END;
IF fTstStd THEN WRITELN('IchCr - fFwd: ', ORD(fFwd), ', ichFst: ', ichFst,
        ', ichLim: ', ichLim, ', dchT: ', dchT);
END;
END;

{$S SMlwwrk}
PROCEDURE MoveOlle;
VAR
lleT:   TLle;
lle:    TLle;
lleDstFst:    TLle;
BEGIN
{$IFC fTraceSM}   LogCall;   {$ENDC}
IF fTstStd THEN
      WRITELN('MoveOlle(', ORD(lld), ', ', olle, ', ', olleDstFst,
        ', ', olleSrcFst, ')');
lle := POINTER(ORD(lld)+olle);
IF olleSrcFst <> olleNil THEN
      BEGIN
      IF olle = olleSrcFst THEN olleSrcFst := lle^.olleNxt;
      IF lle^.olleNxt <> olleNil THEN
              BEGIN
              lleT := POINTER(ORD(lld)+lle^.olleNxt);
              lleT^.ollePrv := lle^.ollePrv;
              END;
      IF lle^.ollePrv <> olleNil THEN
              BEGIN
              lleT := POINTER(ORD(lld)+lle^.ollePrv);
              lleT^.olleNxt := lle^.olleNxt;
              END;
      END;
lle^.olleNxt := olleDstFst;
IF olleDstFst <> olleNil THEN
      BEGIN
      lleDstFst := POINTER(ORD(lld)+olleDstFst);
      lle^.ollePrv := lleDstFst^.ollePrv;
      IF lleDstFst^.ollePrv <> olleNil THEN
              BEGIN
              lleT := POINTER(ORD(lld)+lleDstFst^.ollePrv);
              lleT^.olleNxt := olle;
              END;
      lleDstFst^.ollePrv := olle;
      END
ELSE    lle^.ollePrv := olleNil;
olleDstFst := olle;
END;

{$S SMlwwrk}
PROCEDURE InitOlleFree;
VAR
olleNilT:       TC;
BEGIN
{$IFC fTraceSM}   LogCall;   {$ENDC}
olleNilT := olleNil;
WHILE (olleFreeFst+cbAlle) <= lld^.olleLim DO
 BEGIN
 MoveOlle(lld, olleFreeFst, lld^.olleFreeFst, olleNilT);
 olleFreeFst := olleFreeFst+cbAlle;
 END;
END;

{PROCEDURE FreeOlle(olle, lld, olleSrcFst) =
 MoveOlle(lld, olle, lld^.olleFreeFst, olleSrcFst)}

{FUNCTION OlleAlloc(lld: TLld; VAR olleDstFst: TC) : TC;
VAR
olle:   TC;
lleOrPa:        TLleOrPa;
lle:    TLle;
BEGIN
WRITELN('OlleAlloc');
PxLld(lld);
olle := lld^.olleFreeFst;
IF olle <> olleNil THEN
      BEGIN
      MoveOlle(lld, olle, olleDstFst, lld^.olleFreeFst);
      END;
OlleAlloc := olle;
PxLld(lld);
END;
}

{$S SMwork}
FUNCTION CMin;
BEGIN
{$IFC fTraceSM}   LogCall;   {$ENDC}
IF c1 < c2 THEN CMin := c1 ELSE CMin := c2;
END;

{$S SMwork}
FUNCTION CMax;
BEGIN
{$IFC fTraceSM}   LogCall;   {$ENDC}
IF c2 < c1 THEN CMax := c1 ELSE CMax := c2;
END;

{$S SMwork}
PROCEDURE MoveOvRgch; EXTERNAL;
(*
VAR fRt: TF;
  src,dst: TC;
BEGIN
src := ORD(rgchSrc); dst := ORD(rgchDst);
fRt := src < dst;
IF src < 0 THEN
 BEGIN
 IF dst > 0 THEN fRt := TRUE;
 END
ELSE
 BEGIN
 IF dst < 0 THEN fRt := FALSE;
 END;
IF fRt
 THEN MOVERIGHT(rgchSrc^, rgchDst^, cb)
 ELSE MOVELEFT(rgchSrc^, rgchDst^, cb);
END;
*)

{$S SMwork}
PROCEDURE MoveRgch;

BEGIN
{$IFC fTraceSM}   LogCall;   {$ENDC}
IF fTstStd1 THEN
   WRITELN('MoveRgch:   rgchDst = ', ORD(rgchDst):1,
           '   rgchSrc = ', ORD(rgchSrc):1,'   cb = ', cb:1);

MoveOvRgch(rgchDst, rgchSrc, cb);
{ MOVELEFT(rgchSrc^, rgchDst^, cb); }
END;

{$S SMlwwrk}
PROCEDURE MoveAchad;
VAR rgchSrc, rgchDst: TRgch;
BEGIN
{$IFC fTraceSM}   LogCall;   {$ENDC}
rgchSrc := @achadSrc.rgch^[achadSrc.ichFst];
rgchDst := @achadDst.rgch^[achadDst.ichFst];
MOVELEFT(rgchSrc^, rgchDst^, achadDst.ichLim-achadDst.ichFst);
END;

{PROCEDURE MoveRvsAchad;
VAR
ichDst: TC;
ichSrc:    TC;
rgchSrc:        TRgch;
rgchDst:        TRgch;
BEGIN
ichSrc := achadSrc.ichLim; rgchSrc := achadSrc.rgch; rgchDst := achadDst.rgch;
FOR ichDst := achadDst.ichFst TO achadDst.ichLim DO
      BEGIN
      ichSrc := ichSrc-1;
      rgchDst^[ichDst] := rgchSrc^[ichSrc];
      END;
END;
}

{$S SMlwwrk}
PROCEDURE DivMod;
BEGIN
{$IFC fTraceSM}   LogCall;   {$ENDC}
quotient := num DIV denom; rem := num MOD denom;
END;

FUNCTION IBinSearch; EXTERNAL;
(* LABEL   100;
VAR
icFst:  TC;
icMid:  TC;
BEGIN
icFst := 0;
WHILE TRUE DO
      BEGIN
      IF icLst-icFst < 2 THEN
              GOTO 100;
      icMid := (icFst+icLst) DIV 2;
      IF c < rgc^[icMid] THEN
              icLst := icMid
      ELSE    icFst := icMid;
      END;
100:
IF c < rgc^[icLst] THEN IBinSearch := icFst ELSE IBinSearch := icLst;
END;
*)

{$S SegPxHz}
PROCEDURE Break {bkcd: TC};
VAR bPainless: TB;
    pPainless: TP;
BEGIN
{$IFC fTraceSM}   LogCall;   {$ENDC}
{$IFC fstd1}
   WRITELN('Break: ', CHR(7), bkcd);
{$ENDC}
   pPainless := POINTER(ORD(0));
   bPainless := pPainless^; (* Sayonara, friends... *)
END;

(*$S SegPxHz*)

PROCEDURE PxLld;
VAR
olle:   TC;
lle:    TLle;
BEGIN
{$IFC fTraceSM}   LogCall;   {$ENDC}
{$IFC fstd1}
WRITELN('lld: ', ORD(lld), ', olleLim: ', lld^.olleLim, ', olleFreeFst: ',
 lld^.olleFreeFst);
olle := lld^.olleFreeFst;
WHILE olle <> olleNil DO
      BEGIN
      lle := POINTER(ORD(lld)+olle);
      WRITELN('  olle: ', olle, ', lle: ', ORD(lle),
        ', olleNxt: ', lle^.olleNxt, ', ollePrv: ',
        lle^.ollePrv);
      olle := lle^.olleNxt;
      END;
{$ENDC}
END;

{$IFC fTrace}
{$S TRCALLS}
Procedure InitTrCalls;
     BEGIN
     OpenCalled := 0;
     END;

Procedure OpenDs;
VAR
    dsname : pathname;
    errnum,
    refnum : integer;
    dsaddr,
    memsize,
    disksize : longint;
    BEGIN
    dsname := 'TrCalls';
    Open_DataSeg(errnum,dsname,refnum,dsaddr,11);
    IF Errnum = 0 then
       BEGIN
       PBuf := POINTER(ORD(dsaddr));
       PGlob := POINTER(ORD(dsaddr));
       DSRefnum := refnum;
       OpenCalled := OpenVAlue;
       SetAccess_DataSeg(errnum,refnum,FALSE);
       IF errnum <> 0 then writeln(chr(7),'SetAccess_DataSeg returns ',errnum);
       END
    ELSE IF errnum = 313 then
       BEGIN
       {dataseg not there so create}
       memsize := (ORD4((Tablength+1))*SizeOf(TabEntry))+SizeOf(Globals);
       disksize := memsize;
       make_DataSeg ( errnum, dsname, memsize, disksize, refnum, dsaddr, 11, ds_shared);
       if errnum <> 0 then writeln(chr(7), 'make_DataSeg returns ', errnum);

       {now initialize the segment}
       PBuf := POINTER(ORD(dsaddr));
       PGlob := POINTER(ORD(dsaddr));
       DSRefnum := refnum;
       OpenCalled := OpenVAlue;

       ResetTrace(TRUE);
       END
    ELSE
       BEGIN
       Writeln(Chr(7),'Open_DataSeg returns', errnum);
       END;
    END;

Procedure SetCallTrace(*Traceit:Boolean*);
    BEGIN
    IF OpenCalled <> OpenValue then OpenDS;
    PGlob^.TraceProcs := Traceit;
    END;

Procedure StartCallLog;
    BEGIN
    IF OpenCalled <> OpenValue then OpenDS;
    If PGlob^.Initialized <> InitValue then ResetTrace(TRUE);
    PGlob^.Logging := True;
    END;

Procedure StopCallLog;
    BEGIN
    PGlob^.Logging := False;
    END;

Function Match(name:s8; bname:BA8):Boolean;
VAR
    i : integer;
    BEGIN
    For i := 0 to 7 do
       BEGIN
       if ORD(name[i+1]) <> bname[i] then
          begin
          match := false;
          exit(match);
          end;
       END;
    Match := True;
    END;

Procedure ListCalls;
VAR  i,j: Integer;
     s: s8;
     p: PBA;
     PTab:PtabEntry;
     c: Char;
     path : pathname;
     f:text;
     t: string[40];
     err:integer;


     BEGIN
     Write(chr(7),'Output goes to ?  CR for console, name for file [t/]file[.text]');
     Readln(t);
     IF (length(t) = 0) then
        begin
        path := '-CONSOLE';
        Reset (f,path);
        err := ioresult;
        if err <> 0 then writeln('reset ioresult ',err);
        end
     else
        begin
        path := concat('t/',t,'.text');
        writeln('rewriting ', path);
        rewrite(f,path);
        err := ioresult;
        if err <> 0 then writeln('rewrite ioresult ',err);
        end;
(*
     Writeln(f,' ');
     Writeln(f,' ');
     Writeln(f,' Procedures  Segments  Process id ');
     Writeln(f,' ');
*)
     Writeln (f,chr(9),'aaaaaaaa',chr(9), path,chr(9), '0');
     For i := 0 to TabLength do
        begin
        PTab := POINTER(ORD(Pbuf)+SizeOf(Globals)+ORD4(i)*SizeOf(TabEntry));
        IF (Ptab^.seg  <> 0) then with Ptab^ do
           BEGIN
           s:= '        ';
           p := @s;
           For j := 0 to 7 do p^[j+1] := name[j];
           Writeln (f,chr(9),s,chr(9), seg:10,chr(9), pid:10);
           END;
        end;
     Close(f,lock);
     END;

Procedure Insert(name:s8; seg:usb; pid:usb);
VAR
   sum:longint;
   i : integer;
   index : integer;
   Empty: integer;
   Found : boolean;
   lastindex: integer;
   newindex : integer;
   Ptab, PtabL, PtabF : PTabEntry;
   BEGIN
   With PGlob^ do
      BEGIN
      sum := seg;
      IF LogCallsAndSegs then For i := 1 to 8 do sum := sum+ORD(name[i]) ;
      index := (sum*1009) MOD (Tablength+1);;
      (*writeln('after hash  index = ', index);*)
      Ptab := POINTER(ORD(PBuf)+SizeOf(Globals)+ord4(index)*SizeOf(TabEntry));
      IF (PTab^.seg  = 0) THEN
         BEGIN  {no collision .. just insert}
         IF LogCallsAndSegs then For i := 0 to 7 do PTab^.name[i] := ORD(name[i+1])
         Else PTab^.name[0] := ORD('?');
         PTab^.seg := seg;
         PTab^.pid := Pid;
         PTab^.next := 0;
         EXIT(Insert);
         END
      ELSE
         BEGIN {match or overflow condition}
         {check first entry}
         IF (seg = PTab^.seg) then
            BEGIN
            IF NOT LogCallsAndSegs then EXIT(Insert);
            IF (*LogCallsAndSegs and*) Match(name,PTab^.name) then
               begin
               {entry already there so just return}
               Exit(insert);
               end;
            END;
         { it has a different name or the name is the same and the seg is different}
         {check entries on collision chain}
         lastIndex := index;
         (*writeln('before loop, lastindex = ', lastindex);*)
         PTabL := POINTER(ORD(Pbuf)+SizeOf(Globals)+ord4(lastindex)*SizeOf(TabEntry));
         (*with PTabl^ do writeln( seg, pid, next );*)
         While PTabL^.next <> 0  do
            begin
            LastIndex := PTabL^.Next;
            (*writeln('in loop, lastindex = ', lastindex);*)
   (***)    PTabL := POINTER(ORD(Pbuf)+SizeOf(Globals)+ord4(lastindex)*SizeOf(TabEntry));
            (*with PTabl^ do writeln( seg, pid, next );*)
            IF (seg = PTabL^.seg) then
               BEGIN
               IF NOT LogCallsAndSegs then EXIT(Insert);
               IF (*LogCallsAndSegs and *) Match(name,PTabL^.name) then
                  begin
                  {entry already there so just return}
                  Exit(insert);
                  end;
               END;
            end;

         {no matches found, so we have a collision .. now just need to find free entry and insert}
         found := false;
         i := index+1;
         While (i<=TabLength) and not found do
            begin
            PTabF := POINTER(ORD(Pbuf)+SizeOf(Globals)+ord4(i)*SizeOf(TabEntry));
            IF PTabF^.seg = 0 then
               BEGIN
               newIndex := i;
               found := true;
               END;
            i := i + 1;
            end;
         IF NOT found then
            begin
            i := 0;
            while (i <= index-1) and not found do
               begin
               PTabF := POINTER(ORD(Pbuf)+SizeOf(Globals)+ord4(i)*SizeOf(TabEntry));
               IF PtabF^.seg = 0 then
                  BEGIN
                  newIndex := i;
                  found := true;
                  END;
               i := i + 1;
               end;
            end;
         (*writeln('index, lastindex, newindex', index,lastindex,newindex);*)
         IF NOT Found then Writeln(CHR(7), 'hash table full')
         ELSE
            BEGIN
            IF LogCallsAndSegs then For i := 0 to 7 do PTabF^.name[i] := ORD(name[i+1])
            Else PTabF^.name[0] := ORD('?');
            PTabF^.seg := seg;
            PTabF^.pid := Pid;
            PTabF^.next := 0;
            PTabL^.next := newIndex;
            END;
         END;
      END;
   END;


Procedure ResetTrace(*LogNamesAndSegs:Boolean*);

VAR
   i,j,k : integer;
   PTab : PTabEntry;
   BEGIN
   IF OpenCalled <> OpenValue then
      BEGIN
      OpenDS;
      END;
   With PGlob^ do
      BEGIN
      if LogNamesAndSegs then  LogCallsAndSegs := True
      Else  LogCallsAndSegs := FALSE;
      For i := 0 to TabLength do
         BEGIN
         PTab := POINTER(ORD4(Pbuf)+SizeOf(Globals)+(ORD4(i)*SizeOf(TabEntry)));
         With PTab^ do
            BEGIN
            for j := 0 to 7 do name[j] := ORD(' ');
            seg := 0;
            pid := 0;
            next := 0;
            END;
         END;
      Initialized := initvalue;
      Logging := False;
      TraceProcs := False;
      END;
   END;

PROCEDURE GetDollarD(pFrame: TPPint; VAR (*nameOfClass,*) nameOfMethod: S8; var Seg:usb);
    VAR pname:  ^Byte;
        pPC:    TPPint;
        pc:     TPint;
        pcl:    ^LONGINT;
        pS8:    ^S8;
        pi: TPint;
        l: longint;
        fBothClassAndProc: BOOLEAN;

    PROCEDURE SwapIn(valueString: S8);
        BEGIN
        END;

    PROCEDURE CopyName(VAR anyName: S8);
        VAR j: INTEGER;
    BEGIN
        anyName := '12345678';
        FOR j := 1 TO 8 DO
            BEGIN
            anyName[j] := CHR(Wand(pname^, 127));
            pname := POINTER(ORD(pname)+1);
            END;
    END;

BEGIN
    pPC := POINTER(ORD(pFrame) + 4);
    pc := pPC^;
    (*nameOfClass := '';*)
    l := ord(pc);
    seg := l div 131072;
    nameOfMethod := '';

    IF ORD(pc) <> 0 THEN
        BEGIN
        pS8 := POINTER(ORD(pc));
    {$R-} SwapIn(pS8^); {$R+}  {Be sure the code is swapped in}


        WHILE pc^ <> $4E5E DO                           {search for UNLK A6}
            IF pc^  = $4E41 THEN                        {TRAP #1 instruction -- inside PASLIB so no $D}
                EXIT(GetDollarD)
            ELSE
                pc := POINTER(ORD(pc)+2);
        WHILE (pc^ <> $4E75) AND (pc^ <> $4ED0) DO      {search for RTS or JMP (A0)}
            pc := POINTER(ORD(pc)+2);

        pname := POINTER(ORD(pc)+3);
        fBothClassAndProc := pname^ < 0;
        pname := POINTER(ORD(pname)-1);
        CopyName(nameOfMethod);
        (*
        IF fBothClassAndProc THEN
            CopyName(nameOfClass)
        ELSE
            nameOfClass := '';
        *)
        END;
END;


Procedure LogCall;
TYPE PL = ^Longint;
VAR dummy : longint;
    caller: PL;
    seg: usb;
    pid: usb;
    procname: S8;
    BEGIN
    IF OpenCalled <> OpenValue then OpenDS;
    With PGlob^ do
       BEGIN
       If Initialized = initvalue then
         If Logging then
            BEGIN
            caller := POINTER(ORD(@Dummy)+4);
            GetDollarD(POINTER(ORD(Caller)),Procname,seg);
            pid := My_Id;
            If TraceProcs then Writeln(procname, ' Seg#', seg:3, ' P#', pid:3);
            Insert(procname,seg,pid);
            END;
       END;
    END;
{$ENDC}

END.





ÿ