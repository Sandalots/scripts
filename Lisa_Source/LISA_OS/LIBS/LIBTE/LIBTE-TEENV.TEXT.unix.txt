UNIT TEenv;     {LIBTE/L:teenv.text    4/6/82}
{Copyright 1983, Apple Computer Inc.}
Intrinsic;


INTERFACE

uses {$U libsm/UnitStd }    UnitStd,
     {$U libsm/UnitHz }     UnitHz,
     {$U libqd/Storage }    Storage,
     {$U libqd/QuickDraw }  QuickDraw,
     {$U libfm/FontMgr   }  FontMgr,
     {$U libdb/dbenv }      dbenv,
     {$U libfe/fedec}       FEDec,
     {$U libfe/fld }        fieldedit;

{$SETC teProduction := NOT FDbgOK }
{$SETC teSym := FSymOK}
{$SETC TEDEBUG := NOT teProduction  }

{$SETC doTraceTE := TRUE}
{$SetC fTraceTE := doTraceTE AND fTRACE}

const
        OK              = 0;
        OutOfMem        = 1;
        InvalidCell     = 2;
        rmDel           = 3;
        rmOk            = 4;
        rmReName        = 5;
        parseErr        = 6;
        undoErr         = 7;
        badField        = 8;
        cvnotenuff      = 9;
        crnotenuff      = 10;

        nullSnip =      0;   {The snip in the null range }
        pInfSnip =     -2;   {+Infinity Snip             }
        mInfSnip =     -1;   {-Infinity Snip             }

        nullTimeout =   0;
        dfltTimeout =  -1;


        cFolderpnl = 0;
        cCellPnl = 1;
        cTblPnl = 2;
        cWidePnl = 3;
        cDialogPnl = 4;
        cScrapPnl = 5;



        nullPnl = -1;

        cBitmapH = 720;
        cBitmapV = 20;

{$IFC NOT TEDEBUG}

        TraceFmgr = FALSE;
{$ENDC }


type

     bitptr = ^Bitmap;

     PnlIndex = INTEGER;


     Panel =   RECORD
               PnlPort : GrafPtr;
               Offset : Point;
               PnlRect : Rect;
               END;

     panelptr = ^ Panel;
     pnlHandle = ^panelptr;

     STR40 = string[40];
     str9  = string[9];
     str100 = string[100];


        pixel = integer;

        idType = integer;

        {a table range: }

                              { field meanings for each type}

                            {  loRow   loCol   hiRow   hiCol   }
                            {  -----   -----   -----   -----   }
        rgKind = (aNullRg,  {  null    null     null    null   }
                  a1CellRg, { cel-row cel-col  cel-row cel-col }
                  aRectRg,  {  loRow   loCol   hiRow   hiCol   }
                  aRowRg,   {  loRow   minf    hiRow   pinf    }
                  aColRg,   {  minf    loCol   pinf    hiCol   }
                  aRowGrid, { prev-row minf   nxt-row  pinf    }
                  aColGrid, {  minf   prev-col pinf    nxt-col }
                  aRowHedRg,{ hedRow   minf   hedRow   minf    }
                  aColHedRg,{  minf   hedCol   minf    hedCol  }
                  aWTBoxRg, {  minf    minf    pinf    pinf    }
                  aTblHedRg {  minf    x-col   minf    x-col   }
                  );
                          {Note: "null" means "nullSnip"; prev- and
                          nxt- (for grids) is m/p-inf for edge-of-
                          table grids; x-col means whatever col
                          the mouse was above at m-down time}

        rgJust = (topJ, botJ, leftJ, rightJ, botLeftJ, topLeftJ,
                  topRightJ, nearJ);

        rgX =  ^range;
        range = record
                rKind:           rgKind;
                loRow:          idType;
                loCol:          idType;
                hiRow:          idType;
                hiCol:          idType;
             END;

        fract = INTEGER;        {an approximate fractional position within
                                any potentially large linear space: 0 is
                                the beginning,1000 is the END}


        {a row or column, as shown in some table split}
        snipKind =      (rowKind, colKind);


        SelKind = (
                   aNullSl,
                   aCellTxtSl,
                   a1CellSl,
                   aRectRgSl,
                   aRowRgSl,
                   aColRgSl,
                   aRowGrdSl,
                   aColGrdSl,
                   aRowHedSl,
                   aColHedSl,
                   aTblHedSl,
                   aWTBoxSl );

        TblPart =  (
                   outOfTbl,
                   cellArea,
                   rowGrid,
                   colGrid,
                   rowHandl,
                   colHandl,
                   rowHed,
                   colHed,
                   whTbBox,
                   tblHed);



     TableRecord = RECORD
                   SplitTable: BOOLEAN;
                   RectBorder: BOOLEAN;
                   Marquee: BOOLEAN;
                   ColHandles: BOOLEAN;
                   ColGrids: BOOLEAN;
                   ColGridHandles: BOOLEAN;
                   ColRangeOk: BOOLEAN;
                   EditColTitle: BOOLEAN;
                   InsertCols: BOOLEAN;
                   EdBlankCol: BOOLEAN;
                   RowHandles: BOOLEAN;
                   RowGrids: BOOLEAN;
                   RowRangeOk: BOOLEAN;
                   RowGridHandles: BOOLEAN;
                   EditRowTitle: BOOLEAN;
                   InsertRows: BOOLEAN;
                   EdBlankRow: BOOLEAN;
                   EditTable: BOOLEAN;
                   ShoFormulas : BOOLEAN;
                   Preview : BOOLEAN;
                   RectRgOk : BOOLEAN;
                   FieldPad:INTEGER;
                   scrollincr: ARRAY [rowkind..colkind] of INTEGER;
                   END;

      tmBand = RECORD
               tmbKind:    SnipKind;    {kind of band}
               tmbLoP:     pixel;       {panel-relative band origin}
               tmbHiP:     pixel;       {panel-relative band END}
               tmbHideP:   pixel;       {amt hidden of first snip}
               tmbLoId:    idtype;     {initial snip id }
               END;


      cellParType = (cRecord );
      idModeType  = (mmfirst, mmlast, mmnext, mmprior, mmfraction);
      mcType      = (mmRight, mmLeft, mmUp, mmDown, mmNone, mmRange, mmBegOfRow,
                     mmBegOfCol,mmRightPeg,mmLeftPeg);


      IOModeType  = (mmRead, mmWrite, mmIONull);

      errRecord   = record
                       errSpot  : interval;
                       mess1    : str40;
                       mess2    : str40;
                       status   : integer;

                    END;

      cellRecord  = record
                       align     : integer;
                       font      : Tlfntid;
                       protected : boolean;
                       hiLite    : boolean;
                       NumRuns   : integer;
                       dim       : boolean;
                    END;


      BGrowResult =
              (BSame,         {band left alone}
               BGrew,         {band was resized}
               BWent,         {band was deleted}
               nxtBWent);     {next band was deleted}


      ScrollType =
              (incrScroll,    {scroll one small unit}
               pageScroll,    {scroll one large unit}
               jmpScroll);    {thumb to spec. location}

      ScrollDir =
              (ScrollBak,     {scroll backwards}
               ScrollFwd);    {scroll forwards}

      TMstate = Record
                  marqsaved : boolean;
                  rg:             range;    {the range currently hi-lited}
                  isSel:          boolean;  {is this selection (not just
                                                 cur. rg) hilite?}
                  isOn:           boolean;  {is hilite currently displayed?}
                  isBorder:       boolean;  {is hilite a border (instead of area)?}
                end;

  {*************************************************************************}


VAR
{$IFC TEDEBUG}
     traceFMGR : BOOLEAN ;
{$ENDC }
     dfltNbrhood: Rect;
     nullNbrhood: Rect;
     TblPars: TableRecord;


     FolderPnl : PnlIndex;
     CellPnl   : PnlIndex;
     TblPnl    : PnlIndex;
     WidePnl   : PnlIndex;
     DialogPnl : PnlIndex;
     ScrapPnl  : PnlIndex;


PROCEDURE AdjRect(Pnl:PnlIndex; VAR r:rect);
PROCEDURE AdjToPnl (Pnl:PnlIndex; P:Point; VAR relPoint:Point);
PROCEDURE allocPnl(thePnl:PnlIndex);
PROCEDURE ChgPnlSize(P:PnlIndex;Width,Height:Pixel);
PROCEDURE FreePnl(thePnl:PnlIndex);
PROCEDURE GetPnlBits(P:pnlIndex; VAR b:bitptr);
PROCEDURE GetPnloffset(Pnl:pnlIndex; VAR offset:point);
PROCEDURE GetPnlRect (PNL :PnlIndex; VAR aPnlRect : Rect) ;
PROCEDURE InitPnls(var status : integer);
PROCEDURE MovePnl(P:PnlIndex;Offset:point);
{$IFC TEDEBUG}
procedure prntClip;
PROCEDURE PRNTPORT;
procedure prntRect ( aRect: rect);
PROCEDURE PrntRg(s:str40;rg:range);
{$ENDC}
FUNCTION ptInPnl(portptr:grafptr;loc:point;hipnl:pnlindex):PnlIndex;
function rgEq (aRg, bRg: range): boolean;
Function SectPnl(Pnl:pnlIndex; portptr:grafptr;updrgn: rgnhandle;
                 resultrgn:rgnhandle) : BOOLEAN;
PROCEDURE SetPnlPort(P:PnlIndex);
procedure setRg (var aRg: range; aKind: rgKind; aLoRow, aLoCol, aHiRow, aHiCol: idType);
PROCEDURE SetUpPnl(P:PnlIndex; portptr: grafptr;width, Height: pixel; loc:point);





IMPLEMENTATION

{$R-}

{$IFC teSym}
{$D+}
{$ELSEC }
{$D-}
{$ENDC}


CONST

   maxpnls = 20;



VAR
   PArray : Array [0..Maxpnls] of PnlHandle;
   CellPort : GrafPort;
   selbitmap : Bitmap;
   ArrayOfBits : Array [ 0..899] of INTEGER;



PROCEDURE AbsAdjPnl (Pnl:PnlIndex; P:Point; VAR relPoint:Point);forward;

{$s TEwork1}
PROCEDURE AbsAdjPnl {Pnl:PnlIndex; P:Point; VAR relPoint:Point};
   {-------------------------------------------------------------------------}
   {P is folder rel pt.  set relpt to be relative to panel                   }
   {                                                                         }
   {-------------------------------------------------------------------------}
   BEGIN
{$IFC fTraceTE}    LogCall;     {$ENDC}
   With PArray[pnl]^^ do
      begin
      relpoint.h := P.h - offset.h;
      relpoint.v := P.v - offset.v;
      end;

   END;


{$s TEwork1}
PROCEDURE AdjRect{Pnl:PnlIndex; VAR r:rect};
   BEGIN
{$IFC fTraceTE}    LogCall;     {$ENDC}
      with PArray[Pnl]^^ do offsetrect(r,offset.h,offset.v);
   END;


{$s TEwork1}
PROCEDURE AdjToPnl {Pnl:PnlIndex; P:Point; VAR relPoint:Point};
   {-------------------------------------------------------------------------}
   {P is folder rel pt.  set relpt to be relative to panel                   }
   {                                                                         }
   {-------------------------------------------------------------------------}
   BEGIN

{$IFC fTraceTE}    LogCall;     {$ENDC}
   IF Pnl <> nullPnl then
      BEGIN
      With PArray[Pnl]^^ do
         begin
         relpoint.h := P.h - offset.h;
         relpoint.v := P.v - offset.v;

         With {PArray[Pnl]^^.}pnlrect do
            BEGIN
            IF relpoint.h < left THEN relpoint.h := left;
            IF relpoint.v < top THEN relpoint.v := top;
            IF relpoint.h > right THEN relpoint.h := right;
            IF relpoint.v > bottom THEN relpoint.v := bottom;
            END;
         END;
      END;
   END;



{$s TEinit}
PROCEDURE AllocPnl{thePnl:PnlIndex};
   BEGIN
{$IFC fTraceTE}    LogCall;     {$ENDC}
   PArray[thepnl] := POINTER(ORD(hAllocate(theHeap,sizeOf(panel))));

   END;

{$s TEwork1}
PROCEDURE ChgPnlSize{P:PnlIndex;Width,Height:Pixel};
   VAR
      aPnlRect : Rect;
      pnlhnd :pnlhandle;
   BEGIN
{$IFC fTraceTE}    LogCall;     {$ENDC}
   pnlhnd := PArray[P];
   if width < 0 then width := 0;
   if height < 0 then height := 0;
   With pnlhnd^^.pnlrect do
      BEGIN
      bottom := height;
      right := width;
      END;
   aPnlRect := pnlhnd^^.pnlrect;
   END;


{$s TEcold}
PROCEDURE FreePnl{thePnl:PnlIndex};
   BEGIN

{$IFC fTraceTE}    LogCall;     {$ENDC}
   if thepnl <> cCellPnl then Freeh(theheap,POINTER(ORD(Parray[thePnl])));
   END;


{$s TEwork1}
PROCEDURE GetPnlBits{P:pnlIndex; VAR b:bitptr};
   {-------------------------------------------------------------------------}
   {Returns the bitmap for the port associated with the panel                }
   {                                                                         }
   {-------------------------------------------------------------------------}

   BEGIN
{$IFC fTraceTE}    LogCall;     {$ENDC}
   b := @PArray[P]^^.pnlport^.portbits;
   END;

{$s TEwork1}
PROCEDURE GetPnloffset{(PNL :PnlIndex; VAR offset:point)};
   {-------------------------------------------------------------------------}
   {      This Procedure returns the Panel Rectangle of the Panel Identified }
   {      by PNL. PnlRect is in panel  relative coords.                      }
   {-------------------------------------------------------------------------}
   BEGIN
{$IFC fTraceTE}    LogCall;     {$ENDC}
   offset := PArray[Pnl]^^.offset;
   END;


{$s TEwork1}
PROCEDURE GetPnlRect{(PNL :PnlIndex, VAR aPnlRect:Rect)};
   {-------------------------------------------------------------------------}
   {      This Procedure returns the Panel Rectangle of the Panel Identified }
   {      by PNL. PnlRect is in panel  relative coords.                      }
   {-------------------------------------------------------------------------}
   BEGIN
{$IFC fTraceTE}    LogCall;     {$ENDC}
   aPnlRect := PArray[Pnl]^^.pnlrect;
   END;



{$S TEinit}

PROCEDURE InitPnls{var status : integer };
   {-------------------------------------------------------------------------}
   {      Initialize the panels                                              }
   {                                                                         }
   {-------------------------------------------------------------------------}
   VAR loc : point;
   BEGIN

{$IFC fTraceTE}    LogCall;     {$ENDC}
   FolderPnl := cFolderPnl;
   CellPnl := cCellPnl;
   TblPnl := cTblPnl;
   WidePnl := cWidePnl;
   DialogPnl := cDialogPnl;
   ScrapPnl := cScrapPnl;



   Openport(@CellPort);
   PortSize(cBitMapH,cBitmapV);

   {set up portbits}
   With selbitmap do
      BEGIN
      baseaddress := @arrayOfBits;
      rowbytes := 90;
      setrect(bounds,0,0,cBitmapH,cBitMapV);
      END;
   SetPortBits(selbitmap);


   {set up cellPnl}

   setpt(loc,0,0);
   allocpnl(ccellpnl);
   SetUpPnl(ccellPnl,@CellPort,cBitmapH,cBitMapV,loc);
   allocpnl(cFolderpnl);
   allocpnl(cTblPnl);
   allocpnl(cWidepnl);
   allocpnl(cDialogPnl);
   allocpnl(cScrapPnl);

   END;

{$s TEwork1}
PROCEDURE MovePnl{P:PnlIndex;Offset:point};
   BEGIN
{$IFC fTraceTE}    LogCall;     {$ENDC}
   PArray[P]^^.offset := offset;
   END ;


{$IFC tedebug }
{$s TEcold }
procedure prntClip ;
         {If traceFlg, print out the current clipping (assuming
         it's rectangular)}

   BEGIN
{$IFC fTraceTE}    LogCall;     {$ENDC}
   with ThePort^.Cliprgn^^ do
      begin
      writeln ('actually clipping at:');
      prntrect(rgnbbox);
      END;

   END;


{$s TEcold }
PROCEDURE PRNTPORT;
   VAR tempport: grafptr;
   BEGIN
{$IFC fTraceTE}    LogCall;     {$ENDC}
   getport(tempport);
   writeln('current port rect');
   prntrect(tempport^.portrect);
   END;

{$s TEcold }
procedure prntRect {( aRect: rect)};
         {If traceFlg, print out the coordinates of aRect in the order
         left,top,right,bottom}

   BEGIN
{$IFC fTraceTE}    LogCall;     {$ENDC}
   with arect do
      begin
      writeln (' left:', left,
               '  top:', top);
      writeln ('  right:', right,
               '  bottom:', bottom);
      end;

   END;



{$s TEcold }
PROCEDURE PrntRg{s:str40;rg:range};
   {-------------------------------------------------------------------------}
   {debug procedure to print out a range.                                    }
   {                                                                         }
   {-------------------------------------------------------------------------}

   BEGIN
{$IFC fTraceTE}    LogCall;     {$ENDC}
   WITH rg do
      BEGIN
      WRITELN(s,' ',ord(rkind));
      WRITELN('lr: ',lorow,'lc: ',locol,'hr: ',hirow,'hc: ',hicol);
      END;
   END;
{$ENDC }
{$s TEwork1}
FUNCTION ptInPnl{portptr:grafptr;loc:point;hipnl:pnlindex}{:PnlIndex};
   {-------------------------------------------------------------------------}
   {returns the panel index of the panel in which the the point loc is.      }
   {                                                                         }
   {-------------------------------------------------------------------------}
   VAR i : INTEGER;
       found : boolean;
       relloc: point;
       hndpnl : pnlhandle;
   BEGIN
{$IFC fTraceTE}    LogCall;     {$ENDC}
   i := 2;
   found := false;
   while not found and (i <= hipnl)  do
      BEGIN
      AbsAdjPnl(i, loc, relloc);
      hndpnl := PArray[i];
      IF hndPnl^^.pnlport = portptr then
           found := ptInRect(relloc, hndPnl^^.pnlrect)
      ELSE found := false;
      i := I + 1;
      END;
   if found then ptInPnl := i - 1
   ELSE ptInPnl := nullPnl;

   END;


{$s TEwork1}
function rgEq {(aRg, bRg: range): boolean};
        {aRg = bRg ?}
     begin
{$IFC fTraceTE}    LogCall;     {$ENDC}
        rgEq := (aRg.rKind = bRg.rKind) and
                (aRg.loRow = bRg.loRow) and
                (aRg.loCol = bRg.loCol) and
                (aRg.hiRow = bRg.hiRow) and
                (aRg.hiCol = bRg.hiCol);
     end;
{$s TEwork1}
Function SectPnl{Pnl:pnlIndex; portptr: grafptr;updrgn: rgnHandle; resultrgn:rgnhandle
                           ) : BOOLEAN};
  { ----------------------------------------------------------------------}
  {        Function to see if update region intersects the panel          }
  {        Also calculates intersection of regions. Assumes rgnend primed }
  {        with the address of the last cell in the region.               }
  { ----------------------------------------------------------------------}
  VAR
      xrect : rect;
      trect : rect;

  BEGIN
{$IFC fTraceTE}    LogCall;     {$ENDC}
  {adjust panel rect to folder panel}

  {get the rectangle of the panel and adjust it to folder coords}

  getPnlRect(pnl,xrect);
  adjrect(Pnl,xrect);


  {if the panel rectange intersects the update regions bounding box continue on }

  IF  sectrect(xrect,updrgn^^.rgnbbox,trect) AND (PArray[Pnl]^^.pnlport = portptr) THEN
     BEGIN

     {set up a rectangular region which is the panel rectange in folder coords}

     RectRgn(ResultRgn,xrect);
     {intersect the regions and put result region in resultrgn}

     sectrgn(resultRgn,updrgn,resultrgn);

     {adjust region to panel coords}

     offsetrgn(resultrgn,-(PArray[Pnl]^^.offset.h),-(PArray[Pnl]^^.offset.v));

     sectPnl := true;
     END
  ELSE
     begin
     sectPnl :=false;
     end;

  END;

{$s TEwork1}
PROCEDURE SetPnlPort{P:PnlIndex};
   {-------------------------------------------------------------------------}
   { Set port to bitmap for the selection                                    }
   {                                                                         }
   {-------------------------------------------------------------------------}
   VAR
       pnlhnd : pnlhandle;

   BEGIN
{$IFC fTraceTE}    LogCall;     {$ENDC}
   pnlhnd := PArray[P];
   setPort(pnlhnd^^.pnlport);
   setOrigin(-pnlhnd^^.offset.h,-pnlhnd^^.offset.v);
   ClipRect(pnlhnd^^.pnlrect);
   END;



{$s TEwork1}
procedure setRg {var aRg: range; aKind: rgKind;
                aLoRow, aLoCol, aHiRow, aHiCol: idType};
         {set aRg to the indicated values}
   begin
{$IFC fTraceTE}    LogCall;     {$ENDC}
      aRg.rKind := aKind;
      aRg.loRow := aLoRow;
      aRg.loCol := aLoCol;
      aRg.hiRow := aHiRow;
      aRg.hiCol := aHiCol;
   end;


{$s TEopen2}
PROCEDURE SetUpPnl{P:pnlIndex; portptr:grafptr;width, Height: pixel; loc:point};
   {-------------------------------------------------------------------------}
   {set up the grafport within pnlport and set up cliprgn for the port.      }
   {size port to width and height.  loc relative to folder.                  }
   {-------------------------------------------------------------------------}

   BEGIN
{$IFC fTraceTE}    LogCall;     {$ENDC}
   with PArray[P]^^ do
      BEGIN
      pnlport := portptr;
      setrect(pnlrect,0,0,width,height);
      offset := loc;
      END;

   END;


END.{unit}



ÿ