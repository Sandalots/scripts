{tMgr:Sel Mgr Procs -- LIBTE/tmSProcs 7/21/82}
{ ----------------------------------------------------------------------}
{$R-}

{$S TEcold}
PROCEDURE BoldFlds{status:integer};

   VAR
       errnum : integer;
   BEGIN
{$IFC fTraceTE}    LogCall;     {$ENDC}
   if tracesmgr then writeln('%tmsprocs boldflds');
   SetRunFormat(selFieldH, selFStateH,p12tile,setbold,errnum);
   if fwriteln then if errnum <> 0 then writeln('bolding selfield : ',errnum);
   SetRunFormat(wavFieldH, wavFStateH, p12tile,setbold,errnum);
   if fwriteln then if errnum <> 0 then writeln('bolding wavfield : ',errnum);
   status := errnum;
   END;



{$S TEwork2}
FUNCTION CellChanged{: Boolean};
   {-------------------------------------------------------------------------}
   {Returns true if editing on the cell has occurred.                        }
   {                                                                         }
   {-------------------------------------------------------------------------}
   BEGIN
{$IFC fTraceTE}    LogCall;     {$ENDC}
   if tracesmgr then writeln('tmsprocs CellChanged');
   IF (WavActive AND WavFstateH^^.changed) OR selFstateH^^.changed THEN
           CellChanged := TRUE
   ELSE CellChanged := FALSE;
   END;


{$S TEcold}
FUNCTION cellLogPos {(mouseLoc:point): Integer};
   {-------------------------------------------------------------------------}
   {Translates the location of the mouse in the wide angle view to           }
   {a logical position in the cell                                           }
   {-------------------------------------------------------------------------}
   VAR
       wideE, cellE : Integer;
       cellLp, wideLp : Integer;
       valid: boolean;
   BEGIN
{$IFC fTraceTE}    LogCall;     {$ENDC}
   IF TraceSMGR then Writeln( 'tmsprocs cellLogPos');
   wideLP := LpFromX(mouseLoc.h, wavFieldH, wavFstateH, wavFstateh^^.select.tysel);      {#lm185}
   wideLP := compressedLp(wideLP, wavFieldH, wavFstateH, valid);
   if fwriteln then if not valid then begin
      writeln('tmsprocs    cellLogPos    Bad compressedLP');
      writeln('wideLP = ', wideLP);
      end;
   cellLP := cMin(selFieldH^^.curlen, WideLP);
   cellLogPos := ExpandedLp(cellLP, selFieldH, selFstateH);
   END;


{$S TEwork2}
PROCEDURE ChangeCellFont;
   VAR
        CellInf: cellrecord;
        i : integer;
        xrect:rect;

   BEGIN
{$IFC fTraceTE}    LogCall;     {$ENDC}
   if tracesmgr then writeln('%tmsprocs changeCellFont');

   {this routine called after mxmgr can return revised cell widths and fonts}
   {note : panels should also have been resized if they are on a font change}
   {it is the responsibility of the caller to redraw the selection sometime}
   { after this routine is called}

   IF (TypeofSel = a1CellSl) OR
      (TypeOfSel = aCellTxtSl) OR
      (TypeOfSel = aRowHedSl) OR
      (TypeOfSel = aColHedSl) THEN
      BEGIN

      {unhighlight selection}
      setpnlport(cellpnl);
      hilightsel(selfieldh,selfstateh,false);
      setpnlport(widepnl);
      hilightsel(wavfieldh,wavfstateh,false);

      {fix up new cell rectangle}
      setRect(xrect,0,0,snipwidth(colkind,CurrRg.locol),
                        snipwidth(rowkind,CurrRg.lorow));
      SetFCoords(selfieldH,nil,xrect);
      getPnlRect(widePnl,xrect);
      setFcoords(wavFieldH,nil,xRect);

      {put the new font in the field data structures}
      CellInfo( CRecord, MMRead,CurrRg.lorow,CurrRg.locol,cellinf);
      for i := 0 to selfieldh^^.curfmts-1 do
         begin
         selfieldh^^.fmtinfo^^[i].font := cellinf.font.fam;
         wavfieldh^^.fmtinfo^^[i].font := cellinf.font.fam;
         end;
      selfstateh^^.validlp := 0;
      wavfstateh^^.validlp := 0;

      END;
   END;

{$S TEwork2}
Procedure CircleCell{var timeout : integer};

   VAR
       xrect : rect;
       t : integer;
       saveport : grafptr;


   BEGIN
{$IFC fTraceTE}    LogCall;     {$ENDC}
   if tracesmgr then Writeln('%tmsprocs circlecell' );
   if (typeOfSel = aCellTxtSl) or
      (typeofSel = aRowHedSl ) or
      (typeofSel = aColHedSl ) then
      begin
      SetPnlPort(CellPnl);
      ExitField(selFieldH,selFstateH);
      ErrorSelect(selFieldH,selFstateH,selfieldH^^.curlen,selfieldH^^.curlen,timeout);
      SetPnlPort(WidePnl);
      ExitField(wavFieldH,wavFstateH);
      ErrorSelect(wavFieldH,wavFstateH,wavfieldH^^.curlen,wavfieldH^^.curlen,t);
      end;

   {put a round rect around the Field}
   setpnlport(cellpnl);
   getport(saveport);
   xrect := selfieldH^^.coords;
   pennormal;
   pensize(3,2);
   frameroundrect(xrect,15,10);
   insetrect(xrect,3,2);
   pennormal;
   penmode(-1);
   openrgn;
   frameroundrect(xrect,15,10);
   closergn(saveport^.cliprgn);
   pennormal;
   selcircled := true;

   {force an enter to reset round rect on exit}
   selFstateH^^.changed := true;
   wavFstateH^^.changed := true;
   end;


{$S TEwork2}
PROCEDURE ClearSel{status : integer};
VAR errnum : integer;
BEGIN
{$IFC fTraceTE}    LogCall;     {$ENDC}
   IF TraceSMGR then Writeln('%tmsprocs ClearSel' );
   selcircled := false;
   IF (selFieldH^^.curlen > 0 ) then BEGIN
      If TypeOfSel = a1CellSl THEN BEGIN
         TurnOffMarquee;
         TypeOfSel := aCellTxtSl;
      END;
      ClearField(wavFieldH,wavFstateH,errnum);
      if tracesmgr then begin
         writeln('errnum ',errnum);
         {$IFC FldDebug }
         DumpFldInfo(wavFieldH,wavFstateH);
         {$ENDC }
      end;

      ClearField(selFieldH,selFstateH,errnum);
      if tracesmgr then begin
         writeln('errnum ',errnum);
         {$IFC FldDebug }
         DumpFldInfo(selFieldH,selFstateH);
         {$ENDC }
      end;

      if errnum = 0 then BEGIN
         selfieldH^^.curfmts := 1;
         wavfieldH^^.curfmts := 1;
         SetRunFormat(wavFieldH, nil, p12tile, setnormal, errnum);
         SetRunFormat(selFieldH, nil, p12tile, setnormal, errnum);
      END;
      redrawsel(true);
      status := errnum;
   END
   ELSE status := 0;
END;

{$S TEwork2}
PROCEDURE ColGridSel{colLo, colHi : IdType;
                     handRect:Rect;
                     P : Point;
                     VAR nbh : Rect};
      {-------------------------------------------------------------------------}
      {Mouse down on col grid;                                                  }
      {                                                                         }
      {-------------------------------------------------------------------------}
   VAR
            Status : INTEGER;

   BEGIN
{$IFC fTraceTE}    LogCall;     {$ENDC}
      IF TraceSMGR then Writeln( 'tmsprocs colgridsel' );
      TypeOfSel := aColGrdSl;
      setRg(currRg, acolgrid, mInfSnip, colLo, pInfSnip, colHi);
      Chgrgborders(currRg,true);
      initGrdH := P.H;
      GridColLo := colLo;  {remember where to put arrows}
      nbh := handRect;
      InsetRect(nbh, -hGridTol, -vGridTol);
      tGridH := -1;

   END;



{$S TEwork2}
PROCEDURE ColSel{Col : IdType;
                 H : INTEGER;
                 VAR nbh : Rect;
                 scrnRect: rect;
                 VAR timeout: integer};
      {-------------------------------------------------------------------------}
      { Mouse down in col handle                                                }
      {                                                                         }
      {-------------------------------------------------------------------------}
   BEGIN
{$IFC fTraceTE}    LogCall;     {$ENDC}
      IF TraceSMGR then Writeln( 'tmsprocs colsel' );
      setRg(currRg, aColRg  , mInfSnip, col, pInfSnip, col);
      EndCR;
      TypeOfSel := aColRgSl;
      newRgBorders(currRg, true);
      timeout := dflttimeout;
      nbh := scrnrect;
      stcol := col;
      strow := minfSnip;
   END;



{$S TEwork2}
PROCEDURE CopySel{status : integer};
   VAR errnum : integer;
   BEGIN
{$IFC fTraceTE}    LogCall;     {$ENDC}
   IF TraceSMGR then Writeln('%tmsprocs CopySel' );
   if (typeofSel = aCellTxTSl) or
   (tblPars.EditColTitle and (typeOfSel = aColHedSl)) or
   (tblPars.EditRowTitle and (typeOfSel = aRowHedSl)) then
      Begin
      SetPnlPort(WidePnl);
      CutCopyField(wavFieldH,wavFstateH,false,true,errnum);
      Status := errnum;
      CutCopyField(selFieldH,selFstateH,false,false,errnum);
      END;
   END;

{$S TEwork2}
PROCEDURE CUTSel{status : integer};
   VAR errnum : integer;
   BEGIN
{$IFC fTraceTE}    LogCall;     {$ENDC}
   IF TraceSMGR then Writeln('%tmsprocs Cutsel' );
   if (typeofSel = aCellTxTSl) or
   (tblPars.EditColTitle and (typeOfSel = aColHedSl)) or
   (tblPars.EditRowTitle and (typeOfSel = aRowHedSl)) then
      begin

      SetPnlPort(CellPnl);
      CutCopyField(selFieldH,selFstateH,true,false, errnum);
      if selcircled then
         begin
         selfstateH^^.validlp := 0;
         selcircled := false;
         end;
      SetPnlPort(WidePnl);
      CutCopyField(wavFieldH,wavFstateH,true,true,errnum);
      Status := errnum;
      redrawsel(true);
      END;
   END;


{$S TEwork2}
procedure doresetrun;

   VAR  startlp : integer;
        i : integer;
        errornum : integer;
   BEGIN
{$IFC fTraceTE}    LogCall;     {$ENDC}
   IF TraceSMGR then Writeln('%tmsprocs doresetrun' );
   {see if the char typed in would  be bolded}
   startlp :=  selfstateh^^.select.int.lpfst;
   {figure out what run applies}
   i := 0;

   While (i <= selfieldH^^.curfmts-1) and
         (selfieldh^^.fmtinfo^^[i].lpfst < startlp ) do i := i + 1;

   {if it would be bold then we need to reset}

   if (bold in selfieldh^^.fmtinfo^^[i-1].face ) THEN
      begin
      SetRunFormat(selFieldH, selFstateH,p12tile,
                   setnormal, errornum);
      if tracesmgr then writeln('errornum from setrunfmt = ',errornum:4);
      if wavactive then
         SetRunFormat(wavFieldH, wavFstateH, p12tile,
                      setnormal, errornum);
      if tracesmgr then writeln('errornum from setrunfmt = ',errornum:4);
      end;
   if tracesmgr then writeln('doresetrun',bold in
            selfieldh^^.fmtinfo^^[i-1].face);

   END;

{$S TEcold}
Procedure DoSelAct {Activate: boolean};
        {If Activate is true, then re-activate any selection controlled by
         tmsProcs, otherwise, de-activate any selection. }
      begin
{$IFC fTraceTE}    LogCall;     {$ENDC}
         if traceSMgr then
            begin
            if Activate then Writeln('%tmsprocs ', 'DoSelAct ', 'Activate')
            else Writeln('%tmsprocs ', 'DoSelAct ', 'DeActivate');
            end;

         if (typeOfSel = aCellTxtSl) or
            (typeOfSel = aColHedSl) or
            (typeOfSel = aRowHedSl)then
            begin
            SetPnlPort(CellPnl);
            ChangeSelHilight(selFieldH, selFstateH, not Activate);
            NewRgImg(currRg);
            SetPnlPort(WidePnl);
            ChangeSelHilight(wavFieldH, wavFstateH, not Activate);
            end;
      end;



{$S TEwork2}
PROCEDURE DoSelCR{aRg : Range;
                  VAR timeout : INTEGER;
                  hilite : cellseltype};
      {-------------------------------------------------------------------------}
      {Sets Selection based upon the current range  Highlight cell as           }
      {Designated by the hilite param                                           }
      {-------------------------------------------------------------------------}
   VAR
             row : idtype;
             col : idtype;
             fullrect:rect;
             t:integer;
             xrect:rect;
             saveport : grafptr;

   BEGIN
{$IFC fTraceTE}    LogCall;     {$ENDC}
      IF TraceSMGR then
         BEGIN
         Writeln( 'tmsprocs', 'doselcr',' ');
         {$IFC tedebug}
         prntRg( 'currRg is:', currRg);
         prntRg( 'New CR to select', aRg);
         {$ENDC}
         END;
      timeout := nullTimeout;
      frstCellRect := aNullRect;
      frstScrnRect := aNullRect;
      case arg.rkind of
         a1CellRg:
              BEGIN
              row := aRg.lorow;
              col := arg.locol;
              setRect(fullrect,0,0,snipwidth(colkind,col),snipwidth(rowkind,row));
              SetCellField(arg.loRow, arg.loCol, fullRect, SelFieldH, selFstateH);

              CASE hilite of
                 SelectContents,
                 PutCaretAtEnd:
                    BEGIN
                    TypeOfSel := aCelltxtSl;
                    END;
                 SelectCell: (*ELSE IF (hilite = selectCell)  THEN*)
                    BEGIN
                    { 'select whole cell'}
                    TypeOfSel := a1CellSl;
                    END;

                 END; {case}

              redrawsel(false);

              case hilite of
                 SelectContents:
                    BEGIN
                    ChgRgBorders(arg,false);
                    SelAllTxt(timeout);
                    END;
                 SelectCell:
                    BEGIN
                    {LisaCAlc/BusGraphics 'select whole cell'}
                    ChgRgBorders(aRg,true);
                    timeout := dflttimeout;
                    END;
                PutCaretAtEnd:
                    BEGIN
                    ChgRgBorders(aRg,false);
                    CircleCell(timeout);
                    END

                 END; {case}
              newRgImg(aRg);
              END;
         aColHedRg,
         aRowHedRg:
              BEGIN
              IF NOT (rgEq(currRg, aRg)) THEN
                 BEGIN
                 IF aRg.rkind = aColHedRg then TypeOfSel := aColHedSl
                 ELSE IF aRg.rkind = aRowHedRg then TypeOFSel := aRowHedSL;
                 ChgRgBorders(aRg,False);
                 row := aRg.lorow;
                 col := arg.locol;
                 IF row = minfSnip then
                    BEGIN {col header}
                    gethedrect(row,col,fullrect);
                    SetCellField(row, col, fullRect,selFieldH, selFstateH);
                    END
                 ELSE IF col = minfSnip then
                    BEGIN
                    gethedrect(row,col,fullrect);
                    SetCellField(row, col, fullRect,selFieldH, selFstateH);
                    END;
                 redrawsel(false);
                 END
             ELSE
                BEGIN
                SetPnlPort(CellPnl);
                hiLightSel(selFieldH,selFstateH,False);
                SetPnlPort(WidePnl);
                hiLightSel(wavFieldH,wavFstateH,False);
                END;

              CASE hilite of
                 SelectContents,
                 SelectCell:
                    BEGIN
                    SelAllTxt(timeout);
                    END;
                 PutCaretAtENd:
                    BEGIN
                    CircleCell(timeout);
                    END
                 END; {case}
              newRgImg(aRg);
              END;
         aRectRg,
         aRowRg,
         aColRg,
         aWTBoxRg:
              BEGIN
              case arg.Rkind of
                 aRectRg: typeofsel := aRectRgSl;
                 aRowRg: typeofsel := aRowRgSl;
                 aColRg: typeofsel := aColRgSl;
                 aWTBoxRg: typeofsel := aWTBoxSl;
                 end;
              chgRgBorders(aRg,true);
              timeout := dflttimeout;
              END;

         aRowGrid:
              BEGIN
              TypeOfSel := aRowGrdSl;
              chgrgborders(arg,true);
              timeout := dflttimeout;
              END;

         aColGrid:
              BEGIN
              TypeOfSel := aColGrdSl;
              chgrgborders(arg,true);
              timeout := dflttimeout;
              END;

         END;
   currRg := aRg;
   END;



{$S TEwork2}
PROCEDURE DoTblSel{P : point;
                   VAR Nbh : Rect;
                   VAR Timeout : INTEGER};
      {-------------------------------------------------------------------------}
      {Called when mouse goes down in a cell with a text selection.  Sets up a  }
      {new selection and returns a new neighborhood and timeout.                }
      {-------------------------------------------------------------------------}

   BEGIN
{$IFC fTraceTE}    LogCall;     {$ENDC}
      IF TraceSMGR then Writeln( 'tmsprocs dotblsel' );
      if traceSmgr then writeln('Called with point: ', P.h, '/', P.v);
      selMode := selText;
      SelInTbl(P, selectContents, Nbh, Timeout);
   END;{DoTblSel}

{$S TEwork2}
PROCEDURE DoWavSel{P:Point; VAR Nbh :Rect; VAR Timeout: Integer};
   {-------------------------------------------------------------------------}
   {Called when mouse goes down in the wide angle view panel.  P is relative }
   {to upper left of the wide panel.  This routine sets up the selection     }
   {iff the current range is a single cel.  It produces visual feedback in   }
   {both the wide panel and the cell(s), and it returns a new nbh and timeout}
   {-------------------------------------------------------------------------}
   BEGIN
{$IFC fTraceTE}    LogCall;     {$ENDC}
   IF TraceSMGR then Writeln( 'tmsprocs doswavsel' );

   IF WavActive and ((CurrRg.rkind = a1CellRg) OR  (CurrRg.rkind = aRowHedrg)
                OR (CurrRg.rkind = aColHedRg)) THEN
      BEGIN
      TypeOfSel := aCellTxtSl;

      SetPnlPort(widePnl);
      ExitField(wavFieldH,wavFstateH);
      SetPnlPort(CellPnl);
      ExitField(selFieldH,selFstateH);

      {if circled, clear it}
      if selcircled then
         begin
         selfstateH^^.validlp := 0;
         selcircled := false;
         redrawsel(false);
         end;

      SetPnlPort(widePnl);
      Select(P,wavFieldH,wavFstateH,nbh, timeout);

      SetPnlPort(CellPnl);
      repSelect(cellLogPos(P), selFieldH, selFstateH);

      TurnOffMarquee;
      NewRgImg(CurrRg);
      END;
   END;



{$S TEwork2}
PROCEDURE EndCR;
      {-------------------------------------------------------------------------}
      {Remove selection and current range along with visual feedback            }
      {                                                                         }
      {-------------------------------------------------------------------------}
   VAR xrect : Rect;
   BEGIN
{$IFC fTraceTE}    LogCall;     {$ENDC}
      IF TraceSMGR then Writeln( 'tmsprocs EndCR' );
      CASE TypeOfSel of


         aRowHedSl,
         aColHedSl,
         aCellTxtSl:
              BEGIN
              endRgBorders;
              EraseWav;
              END;

         a1CellSl,
         aRectRgSl,
         aRowRgSl,
         aColRgSl,
         aWTBoxSl:
              BEGIN
              endRgBorders;
              EraseWav;
              END;

         aRowGrdSl,
         aColGrdSl:
              BEGIN
              chgrgborders(nullrg,true);
              END;


         OtherWise
              BEGIN
              END;
         END;{case}


      TypeOfSel := aNullSl;
   END;

{$S TEwork2}
PROCEDURE EnterSelection{VAR Status:Integer};
   {-------------------------------------------------------------------------}
   {Enters Selection into the matrix                                         }
   {                                                                         }
   {-------------------------------------------------------------------------}

   BEGIN
{$IFC fTraceTE}    LogCall;     {$ENDC}
   IF TraceSMGR then Writeln('tmsprocs EnterSelection' );

   (*
   IF traceSmgr then
      BEGIN
      writeln('wavField before enter');
      {$IFC FldDebug }
      DumpFldInfo(wavFieldH,wavFstateH);
      {$ENDC }
      END;
   *)

   if NOT( selfstateh^^.valid)  or  NOT (wavfstateh^^.valid) then
      begin
      redrawsel(true);
      end;
   SetPnlPort(CellPnl);
   ExitField(selFieldH,selFstateH);
   SetPnlPort(widePnl);
   ExitField(wavFieldH,wavFstateH);
   NewRgImg(currRg);

   IF traceSmgr then
      BEGIN
      writeln('selField after exitfield');
      {$IFC FldDebug }
      DumpFldInfo(selFieldH,selFstateH);
      {$ENDC }
      END;

   EnterCellVal(status);
   if (status = 0) and growfield and (selfieldh^^.maxlen > maxwav) then
      begin
      selfieldh^^.curlen := maxwav;
      wavfieldh^^.curlen := maxwav;
      if tracesmgr then writeln('compacting field length ',selfield^^.maxlen:5);
      Compactfield(selfieldh);
      if wavactive then compactfield(wavfieldh);
      if tracesmgr then writeln('after compacting length ',selfield^^.maxlen:5);
      end;
   END;

{$S TEwork2}
PROCEDURE EraseWav;
   {-------------------------------------------------------------------------}
   {Erases the wide angle view, but leaves the wavField and wavFstate set up.}
   {Returns with pnl set to widePnl                                          }
   {-------------------------------------------------------------------------}
   VAR xrect:rect;
   BEGIN
{$IFC fTraceTE}    LogCall;     {$ENDC}
   IF TraceSMGR then Writeln( 'tmsprocs EraseWav' );
   SetPnlPort(widePnl);
   getPnlRect(widePnl,xrect);
   EraseRect(xrect);
   (*CaretErased(wavfieldh,wavfstateh);*)
   END;

{$S TEwork2}
PROCEDURE ExitSel;
   BEGIN
{$IFC fTraceTE}    LogCall;     {$ENDC}
   IF TraceSMGR then Writeln('tmsprocs Exitsel' );
   case TypeOfSel of
      aCellTxtSl,
      aRowHedSl,
      aColHedSl:
          BEGIN
          SetPnlPort(widePnl);
          ExitField(wavFieldH,wavFstateH);
          InitFstate(wavFieldH,wavFstateH);
          SetPnlPort(cellPnl);
          ExitField(selFieldH,selFstateH);
          InitFstate(selFieldH,selFstateH);
          END;

      a1CellSl,
      aRectRgSl,
      aRowRgSl,
      aColRgSl,
      aWTBoxSl:
          BEGIN
          TurnOffMarquee;
          If TypeOfSel = a1CellSl then TypeOfSel := aCellTxtSl;
          END;

      otherwise
          BEGIN
          END;

      END;

   END;

{$S TEwork2}
PROCEDURE GetNewRectRg{ strowid : IdType;
                        stcolid : IdType;
                        rowid : IdType;
                        colid : IdType;
                        cellPt: point;
                        TakeCell: boolean;
                        virtRect: rect;
                        scrnRect: rect;
                        VAR rRg : Range
                        VAR newNbh: Rect};
   {-------------------------------------------------------------------------}
   {Mouse went down in strow,stcol and moved to point cellPt within cell     }
   {rowid/colid within rectangle virtRect.  Set the new range rRg and the    }
   {new nbh for further dragging of the mouse.  If TakeCell is true, then    }
   {unconditionally include the cell that the mouse is in within the range.  }
   {-------------------------------------------------------------------------}
   VAR

           endColId : Idtype;
           endrowid : Idtype;
           rRgType: rgKind;
           lr,hr,lc,hc: Idtype;

   PROCEDURE boundRg (aKind: snipKind; mouseP: pixel; snipId, stSnipId: idType;
                     VAR endSnipId: idType);
                {set endSnipId to the endRowId (if rowKind) or the endColId
                (if colKind) of the new range - ie, the new range extends from
                stRowId/stColId through endRowId/endColId.  Also, adjust the
                bounds of newNbh (in the h direction if colKind, or the v direction
                if rowKind) to be the new nbh for further dragging of the mouse.
                Assume that newNbh is <= the scrnRect upon entry}
                VAR
                   bufDist:   pixel;
                   contour:   pixel;
                   visrg: range;
                   toppt : point;
                   r:rect;

                BEGIN


{$IFC fTraceTE}    LogCall;     {$ENDC}
                   IF TraceSmgr then
                      BEGIN
                      writeln('kind',ord(akind),'mouseP',mouseP);
                      writeln('snipid,stsnipid',snipid, stsnipid,'virtrect');
                      {$IFC tedebug}
                      prntrect(virtRect);
                      {$ENDC}
                      END;

                   tmcurvwpt(toppt);
                   visrgofview(toppt,visrg);
                   {$IFC tedebug}
                   if tracesmgr then prntrg('visrg',visrg);
                   {$ENDC}
                   if (snipId = stSnipId) then endSnipId := snipId
                   else
                      BEGIN
                      if TakeCell then bufDist := 0
                      else
                         begin
                         bufdist := (virtrect.botRight.vh[axisOfSKind[aKind]]-
                                     virtRect.topLeft.vh[axisOfSKind[aKind]])
                                      div dragTol;

                         end;
                      if snipLT(aKind, stSnipId, snipId) then
                         BEGIN
                         contour := virtrect.topLeft.vh[axisOfSKind[aKind]] +
                                    bufDist;


                         if (mouseP < contour) or (snipId = pInfSnip) then
                            BEGIN

                            newnbh.botRight.vh[axisOfSKind[aKind]] :=
                             cmin(contour,newnbh.botRight.vh[axisOfSKind[aKind]]);
                            IF snipid = PinfSnip then
                               begin
                               if akind = rowkind then
                                   endsnipid := visrg.hirow
                               else endsnipid := visrg.hicol;
                               end
                            ELSE GetSnipId(aKind, MMPrior, 0, snipId, endSnipId);
                            END
                         else
                            BEGIN
                            Newnbh.topLeft.vh[axisOfSKind[aKind]] :=
                            cMax(contour, newnbh.topLeft.vh[axisOfSKind[aKind]]);

                            endSnipId := snipId;
                            END;
                         END
                      else
                         BEGIN
                         contour := Virtrect.botRight.vh[axisOfSKind[aKind]]
                                   - bufDist;
                         if (mouseP > contour) or (snipId = mInfSnip) then
                            BEGIN
                            newnbh.topLeft.vh[axisOfSKind[aKind]] :=
                             cMax(contour, newnbh.topLeft.vh[axisOfSKind[aKind]]);

                            if snipid = minfsnip then
                               begin
                               if akind = rowkind then
                                   endsnipid := visrg.lorow
                               else endsnipid := visrg.locol;
                               end
                            ELSE GetSnipId(aKind, MMNext, 0, snipId, endSnipId);
                            END
                         else
                            BEGIN
                            newnbh.botRight.vh[axisOfSKind[aKind]] :=
                             cMin(contour, newnbh.botRight.vh[axisOfSKind[aKind]]);

                            endSnipId := snipId;
                            END;
                         END;
                      END;

                      IF TraceSmgr Then
                         BEGIN
                         (*
                         Writeln('bufdist, contour',bufdist, contour);
                         writeln('new nbh ');
                         {$IFC tedebug}
                         Prntrect(newNbh);
                         {$ENDC}
                         *)
                         writeln('endsnipid',endsnipid:4);
                         END;

                END;


   BEGIN
{$IFC fTraceTE}    LogCall;     {$ENDC}
      IF TraceSMGR then Writeln( 'tmsprocs GetNewRectRg' );
      {Set the new nbh and the endColId and the endRowId}
      newNbh := virtRect;
         IF stColId = minfSnip Then
            BEGIN
            boundRg (rowKind, cellPt.v, rowId, stRowId, endRowId);
            EndcolId := pinfSnip;
            END
         ELSE IF stRowId = MinfSnip THEN
            BEGIN
            boundRg (colKind, cellPt.h, colId, stColId, endColId);
            EndrowId := pinfSnip;
            END
         ELSE
            BEGIN
            boundRg (colKind, cellPt.h, colId, stColId, endColId);
            boundRg (rowKind, cellPt.v, rowId, stRowId, endRowId);
            END;



      {Now make sure the range has valid kind and ordering of id's}

      IF (stRowId = endRowId) and (stColId = endColId) THEN
          BEGIN
          rRgType := a1CellRg;
          END
      ELSE IF (stRowId = minfSnip) THEN rRgType := aColRg
      ELSE IF (stColId = minfSnip) THEN rRgType := aRowRg
      ELSE
          BEGIN
          rRgType := aRectRg;
          END;
      If SnipLT(rowkind,strowid,endrowid) THEN
         BEGIN
         lr := strowid;
         hr := endrowid;
         END
      ELSE
         BEGIN
         lr := endrowid;
         hr := strowid;
         END;
      IF SnipLT(colkind, stcolid,endcolid) THEN
         BEGIN
         lc := stcolid;
         hc := endcolid;
         END
      ELSE
         BEGIN
         lc := endcolid;
         hc := stcolid;
         END;


      SetRg(rRg, rRgtype, lr,lc, hr, hc);
      {$IFC tedebug}
      IF TraceSMGR then PrntRg('rRg',rRg);
      {$ENDC}

   END;

{$S TEcold}
PROCEDURE ExtendSel{P : point;
                    VAR nbh : Rect;
                    VAR delta : INTEGER};
        {STUB}
     begin
{$IFC fTraceTE}    LogCall;     {$ENDC}
        if traceSMgr then Writeln( '%tmsprocs ExtendSel' );
        case TypeOfSel of
           aCellTxtSl,
           a1CellSl,
           aRowHedSl,
           aColHedSl,
           aRowRgSl,
           aColRgSl,
           aRectRgSl: begin
               NewNbh(P, nbh, delta);
               end;
           aWTBoxSl,
           aNullSl,
           aRowGrdSl,
           aColGrdSl: begin
               NullCR;
               DoTblSel(P, nbh, delta);
               end;
           end;

     end;


{$S TEwork2 }
procedure GetSelKind {var theSelKind: selKind};
        {Returns the current selection kind}
     begin
{$IFC fTraceTE}    LogCall;     {$ENDC}
     IF TraceSMGR then Writeln('tmsprocs getselkind' );
     theSelKind := typeOfSel;
     end;



{$S TEinit}
PROCEDURE InitSelMgr{wavActFlg : boolean;maxLenWav: integer;
                     growdata:integer;growrun:integer};
   {-------------------------------------------------------------------------}
   {Initialize selection manager                                             }
   {InitPnls must be called first                                            }
   {maxLenWav is the max no. of chars allowed in the Wide-Angle View         }
   {-------------------------------------------------------------------------}

   VAR
       nullRect:     rect;
   BEGIN
{$IFC fTraceTE}    LogCall;     {$ENDC}
   IF TraceSMGR then Writeln('tmsprocs initselmgr' );
   WavActive := wavActFlg;
   if growdata>0 then growfield := true;
   maxWav := maxLenWav;
   SetRect(nullRect, 0, 0, 0, 0);
   SelFieldH := CreateField(nullRect, maxWav, growdata, left, 4,
                            maxrun, growrun, p12tile, true);
   SelFstateH := pointer(ord(newHandle(sizeof(fieldstate))));
   WavFieldH := CreateField(nullRect, maxWav, growdata, left, 4, maxrun,
                            growrun, p12tile, true);
   WavFstateH := pointer(ord(newHandle(sizeof(fieldstate))));
   reinitselmgr;


   END;




{$I libte/tmsprocs2.text}
{$I libte/tmsprocs3.text}



ÿ