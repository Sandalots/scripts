
                       {This file included from tmsprocs}


{$S TEwork2}
   PROCEDURE InKey{ch : char;
                ShiftFlag:Boolean;
                cmdflag : Boolean;
                resetrun:boolean;
                VAR Timeout : INTEGER;
                VAR Status : INTEGER};
      {-------------------------------------------------------------------------}
      { A key was typed.  Update selection.                                     }
      {                                                                         }
      {-------------------------------------------------------------------------}
   CONST
          BKSP = 8;
   VAR
             errornum : integer;
             newRange : range;

   BEGIN
{$IFC fTraceTE}    LogCall;     {$ENDC}
   IF TraceSMGR then Writeln( 'tmsprocs InKey' );
   obscureCursor;
   If TypeOfSel = a1CellSl THEN
      BEGIN
      TurnOffMarquee;
      SelAllTxt(Timeout);
      TypeOfSel := aCellTxtSl;
      END;

   SetPnlPort(CellPnl);
   CaretOn(selFieldH,selFstateH);  {stop blinking the caret          }


   IF ORD(ch) = BKSP then
      BEGIN
      If Shiftflag and not cmdflag then
                ForwardSpace(selFieldH,selFstateH, errornum)
      ELSE IF not shiftflag and cmdflag then
                BackWord(selfieldH,selFstateH,errornum)
      ELSE IF shiftflag and cmdflag then
                ForwardWord(selfieldH,selFstateH,errornum)
      ELSE
         begin
         Backspace(selFieldH,selFstateH,errornum);
         end;
      END
   ELSE
      BEGIN
      IF resetrun then doresetrun;
      InsCh(ch, selFieldH, selFstateH,errornum);
      END;

   IF wavActive THEN
      BEGIN
      SetPnlPort(widePnl);
      CaretOn(wavFieldH,wavFstateH);  {stop blinking the caret          }
      IF ORD(ch) = BKSP then
         BEGIN
         If Shiftflag and not cmdflag  then
                   ForwardSpace(wavFieldH,wavFstateH, errornum)
         ELSE IF not shiftflag and cmdflag then
                   BackWord(wavFieldH,wavFstateH, errornum)
         ELSE IF shiftflag and cmdflag then
                   ForwardWord(wavFieldH,wavFstateH, errornum)
         ELSE begin
            Backspace(wavFieldH,wavFstateH,errornum);
            end;
         END

      ELSE
         BEGIN
         {run already reset}
         InsCh(ch, wavFieldH, wavFstateH,errornum);
         END;

     END;
   if selcircled then
      begin
      selfstateH^^.validlp := 0;
      selcircled := false;
      end;



   Status := errornum;
   IF traceSmgr then WRITELN ('errornum ', errornum, 'status ', status);
   TimeOut := nullTimeout;

   END;



{$S TEwork2}
PROCEDURE ldSmgr;
   BEGIN
{$IFC fTraceTE}    LogCall;     {$ENDC}
   IF TraceSMGR then Writeln('tmsprocs ldsmgr' );
   END;


{$S TEcold}
Procedure MovePointer{P: Point; var pRg: range; var nbh: rect};
        {The mouse went down (with the option key pressed) and has now moved
         to P.  The last range was pRg.  Return the new range designation and
         the nbh for further mouse dragging.  Do not change any selections or
         current ranges.}
      var
         pTPart: TblPart;
         pCellRect, pScrnRect: rect;
         newRg: range;
         doGetNewRect : boolean;
      begin
{$IFC fTraceTE}    LogCall;     {$ENDC}
      IF TraceSMGR then Writeln('%tmsprocs movepointer' );
         FindTblPt(P, pTPart, newRg, pCellRect, pScrnRect);
         doGetNewRect := false;
         case pRg.rKInd of
            aRowRg,
            aColRg : if not ((newRg.rKind = aRowGrid) or (newRg.rKind = aColGrid)) then doGetNewRect := true;
           a1CellRg,
           aRectRg : doGetNewRect := true;
          {aRowGrid,
           aColGrid,
           aWTBoxRg: Don't change pRg or nbh
          }
        end;
        if doGetNewRect then
           GetNewRectRg(pStRow, pStCol, newRg.loRow, newRg.loCol, P, (pTPart = outOfTbl),
                        pCellRect, pScrnRect, pRg, nbh);

      end;


{$S TEwork2}
PROCEDURE MseUp{  P : Point;
                  VAR delta : INTEGER};
      {-------------------------------------------------------------------------}
      {Mouse up.  Update selection.                                             }
      {                                                                         }
      {-------------------------------------------------------------------------}


   VAR nbh : Rect;
   BEGIN
{$IFC fTraceTE}    LogCall;     {$ENDC}
   IF TraceSMGR then Writeln( 'tmsprocs MseUp' );
   With currRg do
      BEGIN
      if (rkind = aColGrid) and (TgridH <> -1)then
         BEGIN
         dragGrid(loCol, tgridh, -1, nbh);
         endGridArrows(LoCol);
         moveGrid(loCol, initGrdH, P.h);
         tgridh := -1;
         END
      ELSE
         if rKind = aRectRg then EraseWav;
      END;
   Case TypeofSel of
      aCellTxtSl,
      aColHedSl,
      aRowHedSl:

         BEGIN
         obscureCursor;
         StartBlink(delta);
         END;
      Otherwise
         delta := dflttimeout;
      end;

   END;


{$S TEwork2}
PROCEDURE MsSel{TblPoint : point;
                    Tpart:tblpart;
                    rg:range;
                    cellRect : Rect;
                    scrnrect : Rect;
                    VAR nbh : Rect;
                    VAR timeout : INTEGER};
   VAR
           inCell : BOOLEAN;
           relPoint : point;
           xrect : rect;



   BEGIN

{$IFC fTraceTE}    LogCall;     {$ENDC}
      {set up selected field }

      IF TraceSMGR then Writeln('tmsprocs MsSel' );
      Case tpart of
         colHed: typeofSel := aColHedSl;
         rowhed: typeofsel := aRowHedSl;
         cellarea: typeofsel := aCellTxtSl;
         {$ifc tmdebug}
         otherwise if fwriteln then writeln('badtpart to mssel');
         {$endc}
         end;


      case typeofSel of
         aCellTxtSl,
         aRowHedSl,
         aColHedSl:
            BEGIN
            IF  NOT ((currRg.rkind = a1CellRg ) or (CurrRg.rkind = aColHedRg) or
                     (CurrRg.rkind = aColHedRg)
                AND ((currRg.lorow = rg.lorow) AND (currRg.locol = rg.locol))) THEN
               BEGIN
               SetCellField(rg.loRow, rg.loCol, cellRect, SelFieldH, selFstateH);
               redrawsel(false);
               END
            ELSE
               BEGIN
               SetPnlPort(CellPnl);
               hiLightSel(selFieldH,selFstateH,False);
               SetPnlPort(WidePnl);
               hiLightSel(wavFieldH,wavFstateH,False);
               END;
            end;
         {$ifc tmdebug}
         Otherwise if fwriteln then writeln('not a row or col title');
         {$endc}
         end; {case}
      {Set up other selection data structures}

      {if circled, clear it}
      if selcircled then
         begin
         selfstateH^^.validlp := 0;
         selcircled := false;
         redrawsel(false);
         end;

      {Make point relative to lower left corner of first character}
      xrect := cellRect;
      TransPoint(TblPoint, xRect, inCell, relPoint);

            {Determine new current range}


      CurrRg := rg;


      SetPnlPort(CellPnl);
      Select(relPoint, selFieldH, selFstateH, Nbh, Timeout);

      {Relocate Nbh}

      offsetrect(nbh,frstcellrect.left,frstcellrect.top);


      {DrawBorder}

      Case TypeofSel of
         aCellTxtSl,
         aRowHedSl,
         aColHedSl:
            chgRgBorders(currRg, false);
         Otherwise
            chgRgBorders(currRg, true);
         end; {case}

      {Tell TblMgr to display}

      NewRgImg(currRg);
      IF wavActive THEN
          BEGIN
          SetPnlPort(widePnl);
          repSelect(wideLogPos(relPoint), wavFieldH, wavFstateH);
          END;
      END;

{$S TEwork2}
PROCEDURE NewNbh{P : point;
                 VAR nbh : Rect;
                 VAR delta : INTEGER};
      {-------------------------------------------------------------------------}
      {Called when mouse goes out of neighborhood.  Updates selection and       }
      {gives visual feedback.                                                   }
      {-------------------------------------------------------------------------}
   VAR
           scrnrect:rect;
           TPart : TblPart;
           rg, newCurrRg : range;
           inCell: boolean;
           relpoint: point;
           endrow: idtype;
           endcol:idtype;
           cellrect:rect;
           lr,hr,lc,hc : Idtype;
           xrect:rect;
           lastCellRect, lastScrnRect: rect;



   PROCEDURE reSetTxt;
        {We've re-entered the anchor cell and want to re-setup the info about its
         text fields.  If we've lost the fact that it was under
         edit, then we'll set the initial selection at the left end.}
          BEGIN
{$IFC fTraceTE}    LogCall;     {$ENDC}
                 frstCellRect := cellRect;
                 frstScrnRect := scrnRect;
                 TransPoint(P, frstCellRect, inCell, relPoint);
                 if (EditRow = nullSnip) and (EditCol = nullSnip) then begin
                    SetCellField(rg.loRow, rg.loCol, frstCellRect,
                                 selFieldH, selFstateH);
                    SetSel(selFieldH, selFstateH, 0, 0, tySelPt);
                    SetSel(wavFieldH, wavFstateH, 0, 0, tySelPt);
                    end;
          END;

   BEGIN
{$IFC fTraceTE}    LogCall;     {$ENDC}
      IF TraceSMGR then Writeln('tmsprocs NewNbh' );
      lastCellRect := frstCellRect;
      lastScrnRect := frstScrnRect;
      FindTblPt(P,Tpart,rg,cellRect,scrnrect);
      {$IFC tedebug}
      if TraceSmgr then prntRg('FindTblPt returned: ', rg);
      {$ENDC}
      (*
      IF TraceSmgr then
         BEGIN
         Writeln('cellrect, scrnrect');
         {$IFC tedebug}
         prntrect(cellrect);
         prntrect(scrnrect);
         {$ENDC}
         writeln ('Tpart , P.h, p.v ',ORD(Tpart),P.h,P.v);
         {$IFC tedebug}
         prntrg('rg',rg);
         {$ENDC}
         END;
      *)
      newCurrRg := currRg;

      CASE TypeOfSel of
        aColHedSl,
        aRowHedSl,
        aCellTxtSl:
              BEGIN
              if (TypeOfSel = aCellTxtSl) then
                           GetNewRectRg(strow,stcol,rg.lorow,rg.locol, P,
                           (Tpart = outOfTbl), cellRect, scrnRect, newCurrRg, nbh);
              IF (newCurrRg.rKind = a1CellRg) or (newCurrRg.rKind = aRowHedRg)
                 or (newCurrRg.rKind = aColHedRg) then
                 BEGIN
                 if not rgEq(rg, newCurrRg) then
                    {Just dragged outside of cell with text, but not enough to
                    go to rect rg.  So, reset rects back to the cell with the
                    text, not the one with the mouse.}
                    BEGIN
                    cellRect := lastCellRect;
                    scrnRect := lastScrnRect;
                    END;
                 ReSetTxt;
                 {extend text selection.}
                 SetPnlPort(CellPnl);
                 growSel(relPoint,selFieldH, selFstateH,nbh);
                 NewRgImg(newCurrRg);

                 offsetrect(nbh,frstcellrect.left,frstcellrect.top);
                 delta := dflttimeout;
                 IF wavActive THEN
                    BEGIN
                    SetPnlPort(widePnl);
                    repGrowSel(WideLogPos(relPoint),wavFieldH, wavFstateH);
                    END;
                 END
              ELSE IF TblPars.RectRgOk then {should check to see that rect ranges
                                             are ok}
                 BEGIN
                 IF (TypeOfSel = aCellTxtSl) Then
                    BEGIN
                    {change text selection to range selection}
                    TypeOfSel := aRectRgSl;
                    SetPnlPort(widePnl);
                    ExitField(wavFieldH,wavFstateH);
                    SetPnlPort(CellPnl);
                    ExitField(selFieldH,selFstateH);
                    NewRgImg(currRg);
                    chgRgBorders(newCurrRg, tblpars.marquee);
                    delta := dflttimeout;

                    END;
                 END
              ELSE {rectrg not ok}
                 BEGIN
                 if not rgEq(rg, newCurrRg) then
                    {Just dragged outside of cell with text, but not enough to
                    go to rect rg.  So, reset rects back to the cell with the
                    text, not the one with the mouse.}
                    BEGIN
                    cellRect := lastCellRect;
                    scrnRect := lastScrnRect;
                    END;

                 NewCurrRg := CurrRg;

                 ReSetTxt;

                 {extend text selection.}
                 SetPnlPort(CellPnl);
                 growSel(relPoint,selFieldH, selFstateH,nbh);
                 NewRgImg(newCurrRg);
                 offsetrect(nbh,frstcellrect.left,frstcellrect.top);
                 delta := dflttimeout;
                 IF wavActive THEN
                    BEGIN
                    SetPnlPort(widePnl);
                    repGrowSel(WideLogPos(relPoint),wavFieldH, wavFstateH);
                    END;
                 END;
              END;

        a1CellSl,
        aRectRgSl:
              BEGIN
              if tblpars.rectrgok then
                 begin
                 GetNewRectRg(stRow, stcol, rg.lorow, rg.loCol, P,
                              (Tpart = outOfTbl), cellRect, scrnRect, newCurrRg, nbh );
                 {GetNewRectRg sets newCurrRg and nbh }
                 if (newCurrRg.rKind = aRectRg) then
                    BEGIN
                    {Extend range selection}
                    if TypeOfSel = a1CellSl then
                       begin
                       DumpSel;
                       newRgImg(currRg);
                       end;
                    TypeOfSel := aRectRgSl;
                    chgRgBorders(newCurrRg, tblpars.marquee);
                    delta := dflttimeout;
                    END
                 ELSE
                    BEGIN
                    {We're back within a 1-cell range}
                    if selMode = selText then
                       BEGIN
                       {go back to a text selection.  restore newCurrRg and selection}
                       { and wide panel to last text selection        }
                       if rgEq(rg, newCurrRg) then
                          BEGIN
                          ReSetTxt;
                          IF Tpart = ColHed then TypeOfSel := aColHedSl
                          ELSE IF Tpart = RowHed then TypeOfSel := aRowHedSl
                          ELSE TypeOfSel := aCellTxtSl;

                          SetPnlPort(CellPnl);
                          drawfield(selFieldH,selFstateH,TRUE, true);
                          growsel(relPoint,selFieldH, selFstateH,nbh);
                          offsetrect(nbh,scrnrect.left,scrnrect.top);
                          chgRgBorders(newCurrRg,false);
                          NewRgImg(newCurrRg);
                          delta := dflttimeout;
                          IF wavActive THEN
                             BEGIN
                             SetPnlPort(widePnl);
                             drawfield(wavFieldH,wavFstateH,TRUE, true);
                             repGrowSel(WideLogPos(relPoint), wavFieldH, wavFstateH);
                             END;
                          END;
                       END
                    else
                       BEGIN
                       {go back to a one-cell selection}
                       if currRg.rKind = aRectRg then
                          BEGIN
                          nbh := frstScrnRect;
                          DoSelCR(newCurrRg, delta, lastHilite);
                          END;
                       END;
                    END;
                 end;

              END;


        aRowGrdSl:
              BEGIN
              {do nothing}
              END;

        aColGrdSl:
              BEGIN
              if (tgridh = -1) then shoGridArrows(GridColLo);
              {move col grid line}
              dragGrid(newCurrRg.loCol,tGridH,P.H,nbh);
              tGridH := P.H;
              delta := NullTimeout;
              END;

        aRowRgSl:
              BEGIN
              {extend row range selection}
              IF (rg.rKind <> aRowGrid) then
                 BEGIN
                 GetNewRectRg(stRow, stcol, rg.lorow, rg.loCol, P,
                              (Tpart = outOfTbl),
                               cellRect, scrnRect, newCurrRg, nbh );
                 {GetNewRectRg sets newCurrRg and nbh }

                chgRgBorders(newCurrRg, tblpars.marquee);
                delta := dflttimeout;
                {$IFC tedebug}
                IF TraceSMGR then prntrg('newCurrRg',newCurrRg);
                {$ENDC}
                END
             ELSE
                BEGIN
                NewCurrRg := CurrRg;
                END;
             END;

       aColRgSl:

             BEGIN
             {extend col range selection}
             IF (rg.rKind <> aColGrid) then
                BEGIN
                GetNewRectRg(stRow, stcol, rg.lorow, rg.loCol, P,
                           (Tpart = outOfTbl), cellRect, scrnRect,
                           newCurrRg, nbh );
                {GetNewRectRg sets newCurrRg and nbh }
                {$IFC tedebug}
                if traceSMgr then
                   BEGIN
                   writeln('nbh:');
                   prntRect(nbh);
                   end;
                {$ENDC}

               chgRgBorders(newCurrRg, tblpars.marquee);
               delta := dflttimeout;
               {$IFC tedebug}
               IF TraceSMGR then prntrg('newCurrRg',newCurrRg);
               {$ENDC}
               END
            ELSE
               BEGIN
               NewCurrRg := CurrRg;
               END;
            END;
      aWTBoxSl:
            BEGIN
            END;
      END; {case}
      (*
      IF TraceSmgr then
         BEGIN
         Writeln('nbh');
         {$IFC tedebug}
         prntrect(nbh);
         {$ENDC}
         END;
      *)

      currRg := newCurrRg;

   END;


{$S TEwork2}
PROCEDURE NewNbhWav{P:Point; VAR nbh:Rect; VAR delta : Integer};
   {-------------------------------------------------------------------------}
   {The mouse has gone down out of nbh and it started in the wav panel.      }
   {Updates selection and gives visual feedback.                             }
   {-------------------------------------------------------------------------}
   BEGIN
{$IFC fTraceTE}    LogCall;     {$ENDC}
   IF TraceSMGR then Writeln('tmsprocs NewNbhWav' );
   if (currRg.rkind = a1cellRg) or (CurrRg.rkind = aColHedRg) or
           (CurrRg.rkind = aRowHedRg) then
      begin
      SetPnlPort(widePnl);
      growSel(P, wavFieldH, wavFstateH, nbh);
      delta := dfltTimeout;
      SetPnlPort(CellPnl);
      repGrowSel(cellLogPos(P), selFieldH, selFstateH);
      NewRgImg(currRg);
      END;
   END;




ÿ