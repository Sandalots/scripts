{
cl:pmm



gl:pmm
obj:pmm
fdl:=.i
yq
{{}

{$R-}

{ Copyright 1983,Apple Computer Inc. }

{$S PMMSeg}
UNIT PMM;

(* Original version for Release 1 of Lisa Office System *)
(* 12/21/83 Wendell Henry - Update for new parameter memory format *)
(*                          to support configurable devices.       *)


INTRINSIC SHARED;


INTERFACE

USES

    {$U libhw/hwint}       HWInt,
    {$U libos/Syscall}     SysCall,
    {$U libsm/UnitStd}     UnitStd,
    {$U libpm/PMDecl}      PMDecl;

{$SETC DOPXPM := FALSE}

VAR
    PMBeep_Flash: TNibble;

Procedure PmInit(var error:integer);
{ Function: Read in PM. Set defaults if needed and set the global vars.
  Errors: -622 - Both PM and the snapshot are bad. PM is set to defaults defined
               in PMdecl.
          -621 - PM is good snapshot is bad. PM returned.
          -620 - PM is bad snapshot is good. PM returned.
 }

Procedure PMReadConfig(var error: integer; var NextEntry: longint;
                       var Config: ConfigDev);
{Function: Returns configuration information for the next device in pm}

Procedure PMWriteConfig(var error: integer; var NextEntry: longint;
                        var Config: ConfigDev);
{Function: Stores configuration information for the next device in pm }

Procedure PMReadInput(Var InputInfo: TInputStuff);
{ Function: Sets the keyboard record. }

Procedure PMWriteInput(InputInfo: TInputStuff);
{ Function: Writes the keyboard record. Sets the command type ahead global.
            Sets hardware driver repeat rate.
            See the declaration of TInputStuff for valid input ranges. }


Procedure PMReadOutPut(Var OutputInfo: TOutputStuff);
{ Function: Sets the screen record }

Procedure PMWriteOutPut(OutputInfo: TOutputStuff);
{ Function: Writes the screen record. Sets the Flash menu bar and mouse type
            ahead globals. Sets hardware driver normal contrast and beep volume.
            See the declaration of TOutputStuff for valid input ranges. }

Procedure PMReadBoot(Var BootInfo: TBootStuff);
{ Function: Sets the boot record }

Procedure PMWriteBoot(BootInfo: TBootStuff);
{ Function: Writes the Boot record.
            See the declaration of TBootStuff for valid input ranges. }

Procedure PMWrite;
{ Function: Writes PM to memory and snapshot }

Function DimConvert(Contrast:Integer):Integer;
{ Function: Takes an value, typically between 0 and 16, and converts it into
            a pratical byte value to be used by SetContrast.
  Assumes:  Contrast is between 0 and 16. }

Function ConvertCont(Contrast:Integer):Integer;
{ Function: Takes an value, typically between 0 and 16, and converts it into
            a pratical byte value to be used by SetContrast.
  Assumes:  Contrast is between 0 and 16. }

Procedure PMReadDevolp(Var IntrnlInfo: TInternalStuff);
{ Function: Sets development record }

Procedure PMWriteDevolp(IntrnlInfo: TInternalStuff);
{ Function: Writes development record. }

Procedure DefaultPM;
{ Function: Sets all non configuration fields to default values. }

Procedure DefaultConfig;
{ Function: Sets all configuration information to default values. }


{$IFC DOPXPM}
Procedure PxPm;
{ Function: Prints out PM and globals}
{$ENDC}

IMPLEMENTATION

{$SETC PMMDEBUG := FDbgOk }
{$SETC PMMSYMBOLS := FSymOk }

{$IFC PMMSYMBOLS}
   {$D+}
{$ELSEC}
   {$D-}
{$ENDC}

{$SETC doTracePM := TRUE}
{$SetC fTracePM := doTracePM AND fTRACE}

CONST

  { These are the defaults values used to reset PM }
    DfltMemTest = False;
    DfltNormCont = 6;
    DfltDimCont = 12;
    DfltBeepVol = 1;
    DfltFadeDelay = 2;
    DfltDoubleClick = 4;
    DfltMouseOn = true;
    DfltScaling = false;
    DfltExtenMem = false;
    DfltInitRepeat = 4;
    DfltSubRepeat = 1;
    DfltMEM_LOSS = 0;
    DfltBootVol = 0;

TYPE

 {This record defines the layout of PM in memory. The numbers to the right indicate
  the byte offset of that field }
   TPM = packed record                                            {Byte     Bits}
          pm_Version:      integer;                               { 0 - 1 }
          pm_TimeStamp:    integer;                               { 2 - 3 }
          pm_BootVol:      TNibble;                               { 4       7-4 }
          pm_NormCont:     TNibble;                               { 4       3-0 }
          pm_DimCont:      TNibble;                               { 5       7-4 }
          pm_BeepVol:      TNibble;                               { 5       3-0 }
          pm_MouseOn:      boolean; {.............................  6       7 }
          pm_ExtendMem:    boolean;                               { 6       6 }
          pm_ScaleMouse:   boolean;                               { 6       5 }
          pm_Pad1:         boolean;                               { 6       4 }
          pm_DoubleClick:  TNibble; {...............................6       3-0 }
          pm_FadeDelay:    TNibble;                               { 7       7-4 }
          pm_BeginRepeat:  TNibble;                               { 7       3-0 }
          pm_SubRepeat:    TNibble;                               { 8       7-4 }
          pm_pad2:         TNibble;                               { 8       3-0 }
          pm_cdCount:      PMByte;                                { 9  }
          pm_DevConfig:    packed array[1..50] of PMByte;         { 10 - 59 }
          pm_MEM_LOSS:     integer;                               { 60 - 61 }
          pm_Checksum:     integer;                               { 62 - 63 }
        end;

Var
    PMRec: record         { Variant to convert 64 bytes into PM and vice versa }
       case boolean of
       true: (InternalPM: TPM);
       false:(OS_Rec: pMemRec);
       end;

{ ---------------------------------------------------------------------- }

{$S PMMSeg}
function FadeConvert(Indepedant: Integer):Integer;
{function: Converts the value stored in PM into the appropriate number of minutes.}
begin
{$IFC fTracePM}    LogCall;     {$ENDC}
   Case Indepedant of
      1: FadeConvert := 1;
      2: FadeConvert := 2;
      3: FadeConvert := 5;
      4: FadeConvert := 10;
      5: FadeConvert := 15;
      6: FadeConvert := 30;
      otherwise FadeConvert := 2;
      end
end;

{-------------------------------------------------------------------------------}

{$S PMMSeg}
Function DimConvert{Contrast:Integer):Integer};
{ Function: Converts Dim Contrast into a pratical byte value to be used by
            SetContrast.
  Assumes:  Contrast is between 0 and 16. }

begin
{$IFC fTracePM}    LogCall;     {$ENDC}
DimConvert := ConvertCont(Contrast + 5)
end;

{-------------------------------------------------------------------------------}

{$S PMMSeg}
Function ConvertCont{Contrast:Integer):Integer};
{ Function: Converts Contrast into a pratical byte value to be used by
            SetContrast.
  Assumes:  Contrast is between 0 and 16. }

begin
{$IFC fTracePM}    LogCall;     {$ENDC}
ConvertCont := (Contrast * 5) + 90;
end;

{-------------------------------------------------------------------------------}


{$S PMMSeg}
Procedure DefaultPM;
{ Set PM to the default constants }
begin
{$IFC fTracePM}    LogCall;     {$ENDC}
  { set entire PM to 0 }
  With PMRec.internalPM do
    begin
      pm_version := 4;
      pm_bootvol:= DfltBootVol;
      pm_normcont:= DfltNormCont;
      pm_dimcont := DfltDimCont;
      pm_beepvol := DfltBeepVol;
      pm_fadedelay := DfltFadeDelay;
      pm_DoubleClick := DfltDoubleClick;
      pm_MouseOn := DfltMouseOn;
      pm_ScaleMouse := DfltScaling;
      pm_ExtendMem := DfltExtendMem;
      pm_beginrepeat := DfltInitRepeat;
      pm_subrepeat := DfltSubRepeat;
      pm_MEM_LOSS := DfltMem_Loss;
    end;
end;

{-------------------------------------------------------------------------------}
PROCEDURE DefaultConfig;
VAR
  error:        INTEGER;
  config:       ConfigDev;
  nextEntry:    LONGINT;

BEGIN
{$IFC fTracePM}    LogCall;     {$ENDC}
    With PMRec.internalPM do
        BEGIN
        nextEntry:=0;
        PutNxtConfig(error,nextEntry,PMRec.OS_Rec,config);
        END;
END;
{-------------------------------------------------------------------------------}


{$S pminitseg}
Procedure PMInit{var error: integer};
VAR
  temp: integer;

BEGIN
{$IFC fTracePM}    LogCall;     {$ENDC}
  Read_PMem(error, PMRec.OS_Rec);
  if error <> 0 then
     begin
     if error = -622 then
        begin
        DefaultPM;
        DefaultConfig;
        Write_Pmem(temp,PMRec.OS_Rec);
        end;
     end;

  with PMRec.internalPM do
     begin
     { Set the global variable }
     PMBeep_Flash := pm_BeepVol;
     end;
END;
{-------------------------------------------------------------------------------}

{$S PMMSeg}
Procedure PMReadConfig{var error: integer; var NextEntry: longint;
                       var Config: ConfigDev};
{Function: Returns configuration information for the next device in pm}
Begin
{$IFC fTracePM}    LogCall;     {$ENDC}
  GetNxtConfig(error,NextEntry,PMRec.OS_Rec,Config);
end;

{-------------------------------------------------------------------------------}

{$S PMMSeg}
Procedure PMWriteConfig{var error: integer; var NextEntry: longint;
                        var Config: ConfigDev};
{Function: Stores configuration information for the next device in pm}
Begin
{$IFC fTracePM}    LogCall;     {$ENDC}
  PutNxtConfig(error,NextEntry,PMRec.OS_Rec,Config);
end;

{-------------------------------------------------------------------------------}

{$S PMMSeg}
Procedure PMReadInput{Var InputInfo: TInputStuff};
{ Function: Sets the keyboard record }

begin
{$IFC fTracePM}    LogCall;     {$ENDC}
  with PMRec.InternalPM, InputInfo do
    begin
      BeginRepeat := pm_BeginRepeat;
      SubRepeat := pm_SubRepeat;
      DoubleClick := pm_DoubleClick;
    end;
end;

{-------------------------------------------------------------------------------}

{$S PMMSeg}
Procedure PMWriteInput{InputInfo: TInputStuff};
{ Function: Writes the keyboard record. Sets the command type ahead global.
            Sets hardware driver repeat rate. }

begin
{$IFC fTracePM}    LogCall;     {$ENDC}
  with PMRec.InternalPM, InputInfo do
    begin
      pm_BeginRepeat := BeginRepeat;
      pm_SubRepeat := SubRepeat;
      pm_DoubleClick := DoubleClick;

   {Inform the drivers}
      SetRepeatRate(pm_BeginRepeat * 100, pm_SubRepeat * 100);
    end;

end;

{-------------------------------------------------------------------------------}

{$S PMMSeg}
Procedure PMReadOutPut{Var OutputInfo: TOutputStuff};
{ Function: Sets the screen record }

begin
{$IFC fTracePM}    LogCall;     {$ENDC}
  with PMRec.internalPM, OutputInfo do
    begin
      NormCont := pm_NormCont;
      DimCont := pm_DimCont;
      BeepVol := pm_BeepVol;
      FadeDelay := pm_FadeDelay;
    end;
end;

{-------------------------------------------------------------------------------}

{$S PMMSeg}
Procedure PMWriteOutPut{OutputInfo: TOutputStuff};
{ Function: Writes the screen record. Sets the Flash menu bar and mouse type
            ahead globals. Sets hardware driver normal contrast and beep volume. }

begin
{$IFC fTracePM}    LogCall;     {$ENDC}
  with PMRec.internalPM, OutputInfo do
    begin
      pm_NormCont := NormCont;
      pm_DimCont := DimCont;
      pm_BeepVol := BeepVol;
      pm_FadeDelay := FadeDelay;

      {Reset globals}
      PMBeep_Flash := pm_BeepVol;

      {Inform the drivers}
      SetContrast(ConvertCont(pm_NormCont));
      SetDimContrast(DimConvert(pm_DimCont));
      SetFadeDelay(FadeConvert(pm_FadeDelay) * 60000);
      SetVolume(pm_BeepVol);
    end;
end;

{-------------------------------------------------------------------------------}

{$S PMMSeg}
Procedure PMReadBoot{Var BootInfo: TBootStuff};
{ Function: Reads the boot record }

begin
{$IFC fTracePM}    LogCall;     {$ENDC}
  with PMRec.internalPM, BootInfo do
    begin
      ExtendMem := pm_ExtendMem;
      BootVol:=pm_BootVol;
    end;
end;

{-------------------------------------------------------------------------------}

{$S PMMSeg}
Procedure PMWriteBoot{BootInfo: TBootStuff};
{ Function: Writes the boot record }

begin
{$IFC fTracePM}    LogCall;     {$ENDC}
  with PMRec.internalPM, BootInfo do
    begin
      pm_ExtendMem := ExtendMem;
      pm_BootVol:=BootVol;
    end;
end;

{-------------------------------------------------------------------------------}

{$S PMMSeg}
Procedure PMWrite;
{ Function: Writes PM to memory }
Var
   error: integer;

begin
{$IFC fTracePM}    LogCall;     {$ENDC}
  {Write Parameter memory}
  Write_PMem(error,PMRec.OS_Rec);
end;

{-------------------------------------------------------------------------------}

{$S PMMSeg}
Procedure PMReadDevolp{Var IntrnlInfo: TInternalStuff};
{ Function: Sets development record }
begin
{$IFC fTracePM}    LogCall;     {$ENDC}
  with PMRec.internalPM, IntrnlInfo do
    begin
      MouseOn := pm_MouseOn;
      mem_loss := pm_mem_loss;
      ScaleMouse := pm_ScaleMouse;
    end;
end;

{$S PMMSeg}
Procedure PMWriteDevolp{IntrnlInfo: TInternalStuff};
{ Function: Writes development record }
begin
{$IFC fTracePM}    LogCall;     {$ENDC}
  with PMRec.internalPM, IntrnlInfo do
    begin
      pm_MouseOn := MouseOn;
      pm_mem_loss := mem_loss;
      pm_ScaleMouse := ScaleMouse;
      MouseScaling(pm_ScaleMouse);
    end;
end;

{-------------------------------------------------------------------------------}
{$IFC DOPXPM}
{$S PMMSeg}
Procedure PxPm;
{ Function: Prints out PM and globals}
var
  error: integer;
  config: ConfigDev;
  nextEntry: longint;

begin
{$IFC fTracePM}    LogCall;     {$ENDC}
  writeln('PARAMETER MEMORY is ',sizeof(TPM),' bytes long');
  With PMRec.internalPM do
    begin
      writeln('version is ',pm_version);
      writeln('time stamp is ',pm_timestamp);
      writeln('bootvol is  ',pm_bootvol);
      writeln('normcont is ',pm_normcont);
      writeln('dimcont is  ',pm_dimcont);
      writeln('beepvol is  ',pm_beepvol);
      writeln('mouse on is', pm_mouseon);
      writeln('mouse scaling is ',pm_ScaleMouse);
      writeln('extendtest is ',pm_extendmem);
      writeln('DoubleClick is ',pm_DoubleClick);
      writeln('Fadedelay is ',pm_fadedelay);
      writeln('beginrepeat is ',pm_beginrepeat);
      writeln('subrepeat is ',pm_subrepeat);
      writeln('mem_loss is ',pm_mem_loss);
    end;
  nextEntry:=0;
  repeat
    GetNxtConfig(error,nextEntry,PMRec.OS_Rec,config);
    if error = 0 then
      with config do
        begin
          writeln('Device at slot',pos.slot,' chan',pos.chan,' dev',pos.dev);
          writeln('  DriverID is ',DriverID,' Device name is ',DevName);
          Writeln('  NumExtWords and values are',nExtWords,ExtWords[1],
                     ExtWords[2],ExtWords[3]);
          writeln;
        end;
  until error > 0;
end;
{$ENDC}
END.
ÿ