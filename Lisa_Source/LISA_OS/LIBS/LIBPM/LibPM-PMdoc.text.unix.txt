

====== D O C U M E N T ==========================================================

Subject:  The Parameter Memory Manager Document

From:     Peter Young

Date:     October 4, 1982

=================================================================================

Parameter memory covers a large expanse of Lisa.
        The actual data in memory and on disk.
        The startup code in the loader.
        The read and write routines in the OS.
        The Parameter Memory Manager.
        The Preference Window.
        The Filer
        The Print Shop

Parameter Memory - The Data

Parameter memory is in two places when things are going well. One
copy is in memory and another, the snapshot, is on disk(it is not a file but an area
in the volume header). When the OS boots it first looks for a system.config file
to configure the system from, if the file is not there then it uses parameter memory.
There four different states the OS can encounter when it tries to read parameter
memory( see syscall interface, procedure read_PMem):

1) Both the copy in memory and the snapshot are good. Memory copy is used.
2) The copy in memory is bad and the snapshot is good. Memory is restored from the snapshot.
3) The copy in memroy is good but the snapshot is bad. Snapshot is restored from memory.
4) Both the copy in memory and the snapshot are bad. OS uses defaults.

The copy in memory is bad if the checksum or the version number is bad.
The snapshot is bad if the checksum or the version is bad, or if the timestamp
differs from the one in memory, or if the serial number doesn't match the
machine. That is if you have a version of PM in memory and
then boot from a DIFFERENT disk the snapshot on that disk will be restored from
memory. This is the way it is supposed to work so reporting this as a bug will not
get you into the contest.

Parameter Memory - The Code
The Rom
The first code to look at parameter memory at system boot is the boot rom. If the
memory copy is valid it will use it to determine boot volume, contrast levels,
beep volume, and degree of memory test. If the memory copy is not valid the boot
rom does not go to disk but instead uses its defaults. The Actual device used
to boot is passed to the OS and obtainable through the routine OSBootVol in the
Priviledged OS interface.
Note: The Rom uses a different port enumeration than everone else.

The OS
The OS has two separate responsiblities for PM. The Loader which initializes the
system according to the contents of PM and the PM read and write routines.
The Loader is the second piece of code to look at parameter memory. It goes through the
sequence described above checking parameter memory and the snapshot for validity and
restoring when necessary. It also initializes all needed device drivers, and leaves a status for
the first read operation done.
The read routine Read_PMem blindly passes 62 bytes
to the caller with no internal error checking. If this is the first read since the
loader has been run Read_PMem will return the status left by the loader and reset it.
Write_PMem writes takes the 62 bytes passed in sets the timestamp and version fields,
tacks the 2 byte checksum on to the end and writes to memory. The same 64 bytes, plus a
serial number, is written to disk.

The Parameter Memory Manager

The Preferences Window
The Preference Window is the application that lets users modify the contents
of PM. It is the only upper level Lisa code that can modify PM.
Note: The filer is modifying the boot field in special cases.

Filer
The Filer calls PmInit upon initialization and depending on the status received may
put up an alert. It masks out the Monitor Working device against the list of devices in PM and
assumes the rest are filer devices.


The Parameter Memory Manager
The applications access Parameter Memory solely through two low level units,
PMdecl and PMM, that make up the Parameter Memory Manager. It is possible to
use the OS read and write routines but anyone caught doing so will be forced to
work as a data entry specialist at Gemco. The layout of PM and the data structures
used to read and write parts of PM are defined in unit PMdecl. Unit PMM provides
initiaization, print, and read and write routines for PM. The Parameter Memory Manager
must be initialized by a call to PMinit before any other PMM calls are made.
PmInit reads parameter memory into an interval variable, receives the status left
by the loader and passes the status back to the caller. There are five pairs of
read write routines. One pair to read and write each of the four groups of PM fields
defined in PMdecl, and a fifth pair to read and write device information for a given
port. All changes made to PM through the PMM are made to the internal copy.
PMWrite writes the internal copy out to memory and disk and must be called to
make any changes permanent.


The Fields of PM and there byte offsets are:
 Version:      integer;                                  { 0 }
      The version number is read, written, and checked by the OS.
 TimeStamp:    integer;                                  { 2 }
      The Timestamp in the snapshot and must match the one in memory or the
      snapshot is invalid.
 BootVol:      TNibble;                                   { 4       7-4 }
      The first 14 ports in TPorts from upper twiggy to slot3 port4 are bootable.
      BootVol contains the ord of the port instance.
 NormCont:     TNibble;                                   { 4       3-0 }
      Normal Contrast is stored as a value from 0 to 15 and converted by ConvertCont
      to a number usable by the drivers.
 DimCont:      TNibble;                                   { 5       7-4 }
      Dim Contrast is stored as a value from 0 to 15 and converted by DimConvert
      to a number usable by the drivers.
 BeepVol:      TNibble;                                   { 5       3-0 }
      Beep Volume is stored as a value from 0 to 5 which is directly usable by the
      drivers.
 MouseOn:      boolean; {.................................  6       7 }
      MouseOn = true means that a mouse is currently connected to the Lisa. MouseOn
      should always be true.
 ExtendMem:    boolean;                                   { 6       6 }
      ExtendMem is read by the boot rom. ExtendMem = true invokes the long memory test.
 ScaleMouse:   boolean;                                   { 6       5 }
      ScaleMouse = true means that a mouse scaling is enable. otherwise it is not
      enabled.
 DoubleClick:  TNibble; {.................................  6       3-0 }
      Double Click Delay is stored from 0 to 5. The WM var clickdelay is initially read
      and set in WMinit.
 FadeDelay:    TNibble;                                   { 7       7-4 }
      Fade Delay is stored as a value from 1 to 6 which can be converted into a number of
      minutes to fade by fadeconvert. The drivers want miliseconds: fadeconvert(value)*60000.
 BeginRepeat:  TNibble;                                   { 7       3-0 }
      initial repeat delay is a value from 1 to 6 which is interpreted as tenths of a
      second.  The drivers take miliseconds.
 SubRepeat:    TNibble;                                   { 8       7-4 }
      Subsequent repeat delay is a value from 1 to 6 which is interpreted as tenths of
      a second. The drivers take miliseconds.
 Time:         pmByte;                                    { 8       3-0 }
      The time offset of greenwich mean time is stored in quarter hours from 0 to 96.
 DeviceType:   packed array [Parallel..Serialb] of PMbyte; { 10-24 25 pad}
      Array of all currently user accessable ports.  Each port element should have the device
      constant in it for the device currently connected to that port.
 Deviceinfo:   array [Parallel..Serialb] of TDeviceInfo;   { 26-55 }
      Array of all currently user accessable ports.  Each port that has a configurable
      device attached to it must have a valid configuration word in this array.
 MEM_LOSS:     integer;                                   { 58 }
      Number of bytes to remove when running.
 OSDS:         PMByte;                                    { 60 }
      See varaint record above for fields of OSDS.
 MON_WD:       TNibble;                                   { 61 }
      Contains the ord of the Tport to which is connected the monitor working device.

UNIT PMDecl;
INTRINSIC;

INTERFACE

USES
{$U obj:syscall } syscall;

{$SETC PMMDEBUG = FALSE }
{$SETC PMMSYMBOLS = TRUE }

CONST

   DevNone = 0;   { no device }
   Typer = 1;     { Daisy Wheel Printer }
   DMPrinter = 2; { Dot Matrix Printer }
   Profile = 3;   { ProFile }
   Modem = 4;     { Guess }
   Marksmen = 5;  { This will change to Priam }
   AppleNet = 6;  { Guess again }
   Corvus = 7;    { or maybe this will change to Priam }
   Laser = 8;     { Not used yet }
   Cfgdrvr = 9;   { configurable driver - also not used yet }
   Upper = 10;    { upper twiggy - may get new meaning in 1.5 }
   Lower = 11;    { lower twiggy }
   Sys_Screen = 12;
   Sys_Keyboard = 13;
   Sys_Mouse = 14;
   MaxDev = 14;

   ThreshHold = 8;      {Mouse movement threshold for scaling}

TYPE

   {Four bit type}
   TNibble = 0..15;
   {eight bit type}
   PMbyte = 0..255;

   TDeviceType = PMbyte;
   TDeviceInfo = integer;

   {A Type for each packet}
   {Boot info - 4 bits}
   TBootStuff = record
         ExtendMem: Boolean;    { false means short boot test }
         case boolean of        { conversion variant }
         true: (iboot: TNibble);
         false: (eboot: TPorts);{ BootPort is stored as a TPort but the boot rom }
         end;                   { reads the field from parameter memory in }
                                { a different form so a conversion is done }
                                { on every read and write. }



   {Screen info - 18 bits}
   TOutPutStuff = record                { Valid Values}
                     NormCont:   TNibble; { 0..15 }
                     DimCont:    TNibble; { 0..15 }
                     BeepVol:    TNibble; { 0..7 }
                     FadeDelay:  TNibble; { 1..6  See fadeconvert in PMM for
                                                                      translatioin }
                  end;

   {Keyboard/Mouse info - 5 bits}
   TInputStuff = record               { Valid Values}
                    BeginRepeat:  TNibble; { 1..6 }
                    SubRepeat:    TNibble; { 1..6 }
                    DoubleClick:  TNibble; { 2..5 }
                 end;

   TInternalStuff = packed record               { Valid Values}
                    MON_WD:       TNibble;      { 3..5 this will go away }
                    MEM_LOSS:     integer;      { 0, 256, 512, 768 }
                    Mouseon:      Boolean;      { true if mouse is connected }
                    MouseScal:    Boolean;      { true if mouse scaling enable }
                    TimeOffSet:   PmByte;       { Time offset from Greenwich }
           { This Variant provided by Yu Ying to interpret the console stuff byte }
                    case boolean of
                    true:  (OSDS: pmbyte);
                    false: (osconsole: 0..7;
                            apconsole: 0..7;
                            pad1: 0..3);
          end;

 {This record defines the layout of PM in memory. The numbers to the right indicate
  the byte offset of that field }
   TPM = packed record                                            {Byte     Bits}
          Version:      integer;                                   { 0 }
          TimeStamp:    integer;                                   { 2 }
          BootVol:      TNibble;                                   { 4       7-4 }
          NormCont:     TNibble;                                   { 4       3-0 }
          DimCont:      TNibble;                                   { 5       7-4 }
          BeepVol:      TNibble;                                   { 5       3-0 }
          MouseOn:      boolean; {.................................  6       7 }
          ExtendMem:    boolean;                                   { 6       6 }
          ScaleMouse:   boolean;                                   { 6       5 }
          Pad2:         boolean;                                   { 6       4 }
          DoubleClick:  TNibble; {.................................  6       3-0 }
          FadeDelay:    TNibble;                                   { 7       7-4 }
          BeginRepeat:  TNibble;                                   { 7       3-0 }
          SubRepeat:    TNibble;                                   { 8       7-4 }
          pad3:         TNibble;                                   { 8       3-0 }
          Time:         PMByte;                                    { 9 }
          DeviceType:   packed array [Parallel..Serialb] of PMbyte; { 10-24 25 pad}
          Deviceinfo:   array [Parallel..Serialb] of TDeviceInfo;   { 26-55 }
          { Find the mystery byte }
          Configpad:    Packed array [1..2] of PMbyte;  {Padding} { 56-57 }
          MEM_LOSS:     integer;                                   { 58 }
          OSDS:         PMByte;                                    { 60 }
          MON_WD:       TNibble;                                   { 61 }
          pad:          TNibble;                                   { 61 }
        end;
IMPLEMENTATION
End.

UNIT PMM;
INTRINSIC SHARED;

INTERFACE

USES
    {$U obj:hwint}       HWInt,
    {$U obj:Syscall}     SysCall,
    {$U Obj:PMDecl}      PMDecl;


CONST

  { These are the defaults values used to reset PM }
    DfltMemTest = False;
    DfltNormCont = 6;
    DfltDimCont = 12;
    DfltBeepVol = 3;
    DfltFadeDelay = 2;
    DfltDoubleClick = 4;
    DfltMouseOn = true;
    DfltScaling = false;
    DfltExtenMem = false;
    DfltInitRepeat = 4;
    DfltSubRepeat = 1;
    DfltTime = 0;
    DfltMON_WD = 0;
    DfltMEM_LOSS = 0;

VAR
    PMBeep_Flash: TNibble;
    PMBootVolume: TPorts;

Procedure PmInit(var error:integer);
{ Function: Read in PM. Set defaults if needed and set the global vars.
  Errors: -622 - Both PM and the snapshot are bad. PM is set to defaults defined
               in PMdecl.
          -621 - PM is good snapshot is bad. PM returned.
          -620 - PM is bad snapshot is good. PM returned.
 }

Procedure PMReadConfig(Port: TPorts; Var DeviceType: TDevicetype;
                                                Var DeviceInfo: TDeviceInfo);
{Function: Returns the device type and device info word for the port passed. }

Procedure PMWriteConfig(Port: TPorts; DeviceType: TDevicetype;
                                                    DeviceInfo: TDeviceInfo);
{Function: Sets the device type and device info word for the port passed.
           DeviceType must be in the range of valid device constants.
           DeviceInfo is typically 0 unless you know what you are doing. }

Procedure PMReadInput(Var InputInfo: TInputStuff);
{ Function: Sets the keyboard record. }

Procedure PMWriteInput(InputInfo: TInputStuff);
{ Function: Writes the keyboard record. Sets the command type ahead global.
            Sets hardware driver repeat rate.
            See the declaration of TInputStuff for valid input ranges. }


Procedure PMReadOutPut(Var OutputInfo: TOutputStuff);
{ Function: Sets the screen record }

Procedure PMWriteOutPut(OutputInfo: TOutputStuff);
{ Function: Writes the screen record. Sets the Flash menu bar and mouse type
            ahead globals. Sets hardware driver normal contrast and beep volume.
            See the declaration of TOutputStuff for valid input ranges. }

Procedure PMReadBoot(Var BootInfo: TBootStuff);
{ Function: Sets the boot record }

Procedure PMWriteBoot(BootInfo: TBootStuff);
{ Function: Writes the Boot record.
            See the declaration of TBootStuff for valid input ranges. }

Procedure PMWrite;
{ Function: Writes PM to memory and snapshot }

Function DimConvert(Contrast:Integer):Integer;
{ Function: Takes an value, typically between 0 and 16, and converts it into
            a pratical byte value to be used by SetContrast.
  Assumes:  Contrast is between 0 and 16. }

Function ConvertCont(Contrast:Integer):Integer;
{ Function: Takes an value, typically between 0 and 16, and converts it into
            a pratical byte value to be used by SetContrast.
  Assumes:  Contrast is between 0 and 16. }

Procedure PMReadDevolp(Var IntrnlInfo: TInternalStuff);
{ Function: Sets development record }

Procedure PMWriteDevolp(IntrnlInfo: TInternalStuff);
{ Function: Writes development record. }

{$IFC PMMDEBUG}
Procedure PxPm;
{ Function: Prints out PM and globals}
{$ENDC}

ÿ