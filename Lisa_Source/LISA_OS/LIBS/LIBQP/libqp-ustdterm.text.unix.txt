                                   {Copyright 1983, 1984, Apple Computer Inc.}
{$SETC fTrace := TRUE}             {FALSE if for performance measurement}

UNIT UStdTerm; INTRINSIC;


INTERFACE
USES
    {$U tklib}             UObject,
    {$U sys1lib}           QuickDraw,
    {$U tklib}             UDraw,
    {$U tklib}             UABC,
    {$U tk2lib}            UTKUniversalText,

    {$U tk2lib}            UText,
    {$U tk2lib}            UDialog,

    {$U libos/Syscall}     Syscall,
    {$U iospaslib}         UClascal,
    {$U iospaslib}         Blkiointr,               {paslib's unit, should not be before UObject}

    {$U libqp/UQPPrint}    UQPPrint,
    {$U libqp/QP/Hardware} Hardware;              {we can not use HWINT because of name conflict.}


{$SETC fRngQP := fDbgOK}
{$SETC fSymQP := fSymOK}



  CONST
    maxPosLines    = 50;                {max possible lines for any terminal emulator}
    maxPosColumns  = 132;
    maxtextline    = 133;               {132 + 1 for CR if in clipboard}

     {clearkind definition for ClearScreen procedure}
    sclearScreen  = 1;                  {clear the whole screen}
    sclearEScreen = 2;                  {clear to the end of the screen}
    sclearBScreen = 3;                  {clear from the beginning of the screen to the cursor position}
    sclearLine    = 4;                  {clear the whole line}
    sclearELine   = 5;                  {clear to end of line}
    sclearBLine   = 6;                  {clear from the beginning of the line to the cursor position}



  TYPE

    TQPConfigRec = RECORD
                     tosaveBuffer    : BOOLEAN;             {save lines in buffer}
                     passApplePeriod : BOOLEAN;             {pass apple '.' to main program}
                     showInputPanel  : BOOLEAN;             {display input panel}
                     CASE twoPanels  : BOOLEAN OF           {have both text and graphic panels}
                       TRUE : (vhs : VHSelect;              {vertical or horizontal split}
                               grPanelSize : INTEGER);      {initial width or height in pixels, if < 0, text
                                                             panel is below or right of the graph panel}
                   END;

    Tcursorshape = (blockshape, underscoreshape, invisibleshape);

    TTermPara = RECORD
                  rowsize          : 1..maxPosLines;
                  columnsize       : 1..maxPosColumns;
                  toWraparound     : BOOLEAN;
                  keytoStopOutput  : CHAR;
                  keytoStartOutput : CHAR;
                  tmcursorShape    : Tcursorshape;
                END;

    TMouseEvent = RECORD
                    mouseLoc : Point;
                    clicknum : INTEGER;                               {max 3 for triple clicks}
                    mouseDown, meShift, meApple, meOption : BOOLEAN;
                  END;



    TTermProcess = SUBCLASS OF TProcess
        FUNCTION  {TTermProcess.}CREATE: TTermProcess;
       {FUNCTION   TTermProcess. newDocManager(volumePrefix: TFilePath; openAsTool : BOOLEAN): TDocManager;}
        END;

    TTermDocManager = SUBCLASS OF TDocManager
        FUNCTION  {TTermDocManager.}CREATE(object: TObject; heap: THeap; itsPathPrefix: TFilePath): TTermDocManager;
       {FUNCTION   TTermDocManager. newWindow(heap: THeap; wmgrID: TWindowID): TWindow;}
       {PROCEDURE  TTermDocManager. Open(VAR error: INTEGER; wmgrID: TWindowID;
                                     Var OpenedSuspended:Boolean);}
        END;

    TTermView = SUBCLASS OF TView
        minViewLRect : LRect;
        lines        : TArray; {OF TTermLines}              {for clipview in the clipboard only}
        firstLineStartX : INTEGER;                          {for clipview in the clipboard only}

        FUNCTION  {TTermView.}CREATE(object: TObject; heap: THeap; itsPanel: TPanel; itsExtent: LRect;
                                     itsLines : TArray): TTermView;
       {PROCEDURE  TTermView. Draw;}
       {PROCEDURE  TTermView. MousePress (mouseLPt : LPoint);}
       {PROCEDURE  TTermView. SetMinViewSize (VAR minLRect : LRect);}
       {PROCEDURE  TTermView. CreateUniversalText;}
        END;

    TInputView = SUBCLASS OF TView
        FUNCTION  {TInputView.}CREATE(object: TObject; heap: THeap; itsPanel: TPanel; itsExtent: LRect): TInputView;
       {PROCEDURE  TInputView. Draw;}
        END;

    TTermCutCopyCmd = SUBCLASS OF TCutCopyCommand
        termSelection:  TTermSelection;

        FUNCTION  {TTermCutCopyCmd.}CREATE(object: TObject; heap: THeap; itsCmdNumber: TCmdNumber; itsView: TView;
                                    itsSelection: TTermSelection): TTermCutCopyCmd;
       {PROCEDURE  TTermCutCopyCmd. DoCutCopy(clipSelection: TSelection; deleteOriginal: BOOLEAN;
                                            cmdPhase: TCmdPhase);}
        END;

    TTermPasteCmd = SUBCLASS OF TPasteCommand

        FUNCTION  {TTermPasteCmd.}CREATE (object: TObject; heap: THeap; itsCmdNumber: TCmdNumber;
                                          itsView: TView): TTermPasteCmd;
       {PROCEDURE  TTermPasteCmd. DoPaste (clipSelection: TSelection; pic: PicHandle; cmdPhase: TCmdPhase);}
        END;

    TCoordinate = RECORD
                     Case BOOLEAN OF
                       TRUE  : (y, x   : INTEGER);          {so that we can compare xyCood as a lingint}
                       FALSE : (xyCoord : LONGINT);
                   END;

    TSelRange = RECORD
                  startXY : TCoordinate;              {first char selected}
                  endXY   : TCoordinate;              {next to last char selected}
                END;

    TTermSelection = SUBCLASS OF TSelection
        selRange  : TSelRange;
        anchorXY  : TCoordinate;

        FUNCTION  {TTermSelection.}CREATE (object: TObject; heap: THeap; itsView: TView; itsKind: INTEGER;
                                        itsCharPos : TCoordinate; itsAnchorLPt: LPoint): TTermSelection;
       {PROCEDURE  TTermSelection. DoKey (ascii: CHAR; keycap : Byte; shiftKey, appleKey, optionKey: BOOLEAN);}
        PROCEDURE {TTermSelection.}QPHighlight (highTransit: THighTransit; cursorpoint : Point);
       {PROCEDURE  TTermSelection. Highlight (highTransit: THighTransit);}
       {PROCEDURE  TTermSelection. MouseMove (mouseLPt: LPoint);}
       {FUNCTION   TTermSelection. CanDoCommand (cmdNumber: TCmdNumber; VAR checkIt: BOOLEAN): BOOLEAN;}
       {FUNCTION   TTermSelection. NewCommand (cmdNumber: TcmdNumber) : TCommand;}
        PROCEDURE {TTermSelection.}SetRange (sXY, eXY : TCoordinate; startIsAnchor : BOOLEAN);
        END;

    TGraphView = SUBCLASS OF TView
        minViewLRect : LRect;
        qdpPicture   : PicHandle;
        scrolldh     : INTEGER;
        scrolldv     : INTEGER;

        FUNCTION  {TGraphView.}CREATE (object: TObject; heap: THeap; itsPanel: TPanel; itsExtent: LRect): TGraphView;
       {PROCEDURE  TGraphView. Draw;}
       {PROCEDURE  TGraphView. MousePress (mouseLPt: LPoint);}
       {PROCEDURE  TGraphView. MouseMove (mouseLPt: LPoint);}
       {PROCEDURE  TGraphView. MouseRelease;}
       {PROCEDURE  TGraphView. SetMinViewSize (VAR minLRect : LRect);}
        END;

    TGraphCutCopyCmd = SUBCLASS OF TCutCopyCommand
        graphSelection:  TGraphSelection;

        FUNCTION  {TGraphCutCopyCmd.}CREATE (object: TObject; heap: THeap; itsCmdNumber: TCmdNumber; itsView: TView;
                                    itsSelection: TGraphSelection): TGraphCutCopyCmd;
       {PROCEDURE  TGraphCutCopyCmd. DoCutCopy (clipSelection: TSelection; deleteOriginal: BOOLEAN;
                                            cmdPhase: TCmdPhase);}
        END;

    TGraphSelection = SUBCLASS OF TSelection

        FUNCTION  {TGraphSelection.}CREATE (object: TObject; heap: THeap; itsView: TView; itsKind: INTEGER;
                                                  itsAnchorLPt: LPoint): TGraphSelection;
       {PROCEDURE  TGraphSelection. Highlight (highTransit: THighTransit);}
       {PROCEDURE  TGraphSelection. DoKey (ascii: CHAR; keycap : Byte; shiftKey, appleKey, optionKey: BOOLEAN);}
       {FUNCTION   TGraphSelection. CanDoCommand (cmdNumber: TCmdNumber; VAR checkIt: BOOLEAN): BOOLEAN;}
       {FUNCTION   TGraphSelection. NewCommand (cmdNumber: TcmdNumber) : TCommand;}
        END;

    TInputBuffer = SUBCLASS OF TObject
        inputRun  : TString;                 {contain characters}
        combKeys  : TArray;                  {nil if from paste, else array of TCombKeys}

        FUNCTION  {TInputBuffer.}CREATE(object: TObject; heap: THeap; fromKeyboard : BOOLEAN): TInputBuffer;
       {PROCEDURE  TInputBuffer. Free;}
        END;

    TVanWindow = SUBCLASS OF TWindow
        typeAhead  : TArray;                 {of TInputBuffer}
        inputcount : INTEGER;                {total # of characters in all input buffers}
        termPanel  : TPanel;
        inputPanel : TPanel;
        graphPanel : TPanel;
        stdterm    : TStdTerm;               {so that Consterm object will be saved when put away}
        dialogWindow :   TDialogWindow;
        termDialog : TTermDialog;

        FUNCTION  {TVanWindow.}CREATE(object: TObject; heap: THeap; itsWmgrID: TWindowID): TVanWindow;
       {PROCEDURE  TVanWindow. BlankStationery;}
        PROCEDURE  TVanWindow. DoKey (ascii: CHAR; keycap : Byte; shiftKey, appleKey, optionKey: BOOLEAN);
       {FUNCTION   TVanWindow. WantMenu (menuID: INTEGER; inClipboard : BOOLEAN) : BOOLEAN;}
       {FUNCTION   TVanWindow. CanDoCommand (cmdNumber: TCmdNumber; VAR checkIt: BOOLEAN): BOOLEAN;}
       {FUNCTION   TVanWindow. NewCommand (cmdNumber: TcmdNumber) : TCommand;}
       {PROCEDURE  TVanWindow. StashPicture (highTransit : THighTransit);}
       {PROCEDURE  TVanWindow. AbortEvent;}
        END;

    TTermWriteUnivText = SUBCLASS OF TTKWriteUnivText
        termView     : TTermView;
        linePosition : INTEGER;
        charPosition : INTEGER;

        FUNCTION  {TTermWriteUnivText.}CREATE (object: TObject; heap: THeap; itsDataArray: TString;
                                               itsDataSize: INTEGER; itsView : TTermView): TTermWriteUnivText;
       {PROCEDURE  TTermWriteUnivText. FillRun (howmany : INTEGER);} { Writes one run of text each time called }
        END;

    TTermDialog = SUBCLASS OF TDialog
        orgtabindex   : INTEGER;             {tab index first shown in the dialog box}
        orgtabstr     : STRING [5];          {tab index string first shown in the dialog box}

        FUNCTION  {TTermDialog.}CREATE (object: TObject; heap: THeap; itsView: TView): TTermDialog;
        PROCEDURE {TTermDialog.}SetupDialog;
       {PROCEDURE  TTermDialog. ButtonPushed (button: TButton);}
        END;

    TTabPosition = RECORD
                     Case TabSet : BOOLEAN OF
                       TRUE  : (TabStop : packed array [0..maxPosColumns] of BOOLEAN);
                       FALSE : (All : array [1..5] of LONGINT);
                   END;

    TRAttributes = RECORD
                     charStyle : Style;
                     charFont  : INTEGER;
                   END;

    Tfontinfo = RECORD
                  leading, ascent, descent, widmax, lineheight : INTEGER;
                END;

    TTermLine = SUBCLASS OF TObject
        size       : INTEGER;                          {with max possible charattr, byte is not big enough}
        ch         : PACKED ARRAY [0..maxtextline] OF CHAR;         {termstr, can be shrunk down}
                                                       {charattr follows this, has to start on even byte}
        FUNCTION  {TTermLine.}CREATE : TTermLine;
        END;

    TStdTerm = SUBCLASS OF TObject           {All ToolKit classes should have been initiaialized by now}
                                             {Standard Terminal, can be subclassed by terminal emulator}
                                             {program or emulator can access to these variables}
        window            : TVanWindow;      {connection to ToolKit stuff}
        buffer            : TArray;          {OF TTermLine}
        screen            : ARRAY [0..maxPosLines] OF TTermLine;
        maxLines          : INTEGER;         {size of active view}
        maxColumns        : INTEGER;
        viewSize          : INTEGER;         {# lines in total view, might be changed if Save Buffer}
        cursorPt          : Point;
        cursorXY          : TCoordinate;     {relative to home position, 0, 0}
        cursorshape       : Tcursorshape;
        saveBuffer        : BOOLEAN;         {to save lines scrolled off the top of screen in buffer area}
        wrapAround        : BOOLEAN;
        stopOutputKey     : CHAR;
        startOutputKey    : CHAR;
        tabPosition       : TTabPosition;
        dfontInfo         : TFontinfo;       {cash of default font info}
        defltAttr         : TRAttributes;    {default type style and font, for the screen column size,
                                              changed by ChangeMaxColumns only }
        currAttr          : TRAttributes;    {current type style and font}
        handleApplePeriod : BOOLEAN;         {QP will handle apple .}
        haveInputPanel    : BOOLEAN;         {show input panel}
        haveTwoPanels     : BOOLEAN;         {both text and graphic panel}
        txpanelonly       : BOOLEAN;
        grpanelonly       : BOOLEAN;         {these three are exclusive of one another}
        alreadyConfigured : BOOLEAN;         {if QPConfig already called or reopen a put-away document}
        bufhasroom        : BOOLEAN;         {if buffer has room for more lines scrolled up}
                                             {terminal emulators can have additional fields}

               {only the methods that need to be overridable by terminal emulators, others are just procedures}
        FUNCTION  {TStdTerm.}CREATE (object : TObject; heap : Theap) : TStdTerm;
                                                            {terminal emulator subclass need these two args too.}
        PROCEDURE {TStdTerm.}VWrite (VAR str : Tstr255);    {Paslib will call this, terminal emulator must override}
        PROCEDURE {TStdTerm.}CtrKeyWrite (ctrch : CHAR);    {terminal emulators can override if handles
                                                             different control keys.}
        PROCEDURE {TStdTerm.}VRead (VAR ach: char; VAR keycap : Byte; VAR applekey, shiftkey, optionkey : BOOLEAN);
                                                            {Paslib will call this, terminal emulator must override}
        END;


    TPasTerm = SUBCLASS of TTERMINAL                    {in Blkiointr unit, linkage to paslib}
                                                        {not on heap, no fileds allowed, not to be subclassed}
        FUNCTION  {TPasTerm.}CREATE : TPasTerm;
       {PROCEDURE  TPasTerm. VWrite (VAR str : Tstr255);}
       {FUNCTION   TPasTerm. VRead : CHAR;}
       {PROCEDURE  TPasTerm. VGotoxy (x, y : INTEGER);}
       {FUNCTION   TPasTerm. VKeyPress : BOOLEAN;}
       {FUNCTION   TPasTerm. VAbortKey : BOOLEAN;}
       {PROCEDURE  TPasTerm. VScreenCtr (contrfun : INTEGER);}
       {FUNCTION   TPasTerm. OpenPrinter : INTEGER;}
       {FUNCTION   TPasTerm. PrWrite (VAR str : Tstr255) : INTEGER;}
       {FUNCTION   TPasTerm. ClosePrinter : INTEGER;}
       {PROCEDURE  TPasTerm. CleanUp;}
        END;



    TPMouseEvent = ^TMouseEvent;                        {used in UQPGraph unit too}




  VAR
    ConsTerm : TStdTerm;
    qdpUsed  : BOOLEAN;       {to be set by UQPGraph}
    qdpGPtr  : grafptr;       {for graphic panel}
    qdpGPort : grafport;
    qdpPicOpen : BOOLEAN;
    qdpViewRect : Rect;

    qMouseEvents : TArray; {of TMouseEvent}



 {Procedures to be called by programs, or terminal emulators}

  PROCEDURE QPConfig (config : TQPConfigRec);     {main program should call this before any screen i/o}
  PROCEDURE SetupTermPara (termpara : TTermPara); {set up terminal parameters from program or terminal emulator}
                                                  {main program or terminal emulator should call this before any
                                                   screen i/o.}
  PROCEDURE VRead (VAR ch: CHAR; VAR keycap : Byte; VAR applekey, shiftkey, optionkey: BOOLEAN);
                                                  {program can call this to get control keys, instead of READ,
                                                   But this call will not echo the character read, nor do mapping.}
  PROCEDURE ClearScreen (clearkind : INTEGER);
  PROCEDURE VGotoxy (x, y : INTEGER);
  PROCEDURE MoveCursor (scroll : BOOLEAN; xdistance, ydistance : INTEGER);
                                                  {scroll is only meanful in vertical direction.}
                                                  {xdistance, ydistance: positive means to the right or down.}
  PROCEDURE SetTab (column : INTEGER);            {use cursor if column < 0}
  PROCEDURE ClearTab (clearAll : BOOLEAN; column : INTEGER);    {use cursor if column < 0}

  PROCEDURE StopInput;                            {lock keyboard from program or terminal emulator}
  PROCEDURE StartInput;                           {unlock keyboard}

    {Character attributes change}
  PROCEDURE ChangeCharStyle (newstyle : Style);

  PROCEDURE QPYield_CPU;                          {Programs should include this call in the long compute-bound
                                                   loop. This call will handle user's menu and mouse actions
                                                   as well. If programs do not want to give up being the active
                                                   window, they may call Yield_CPU.}



 {Procedures to be called by terminal emulators only}

  PROCEDURE VStrWrite (VAR str : Tstr255);        {write the str from cursor position, not the entire line, to be
                                                   called by terminal emulators after interpreting esc sequences.}
                                                  {No control characters allowed in str, handles wraparound}
    {for manipulating output lines and scrolling margin feature}     {Note that cursor position does not change}
  PROCEDURE VGetLine (lineNo : INTEGER; VAR line : Tstr255; delete : BOOLEAN);
  PROCEDURE VPutLine (lineNo : INTEGER; VAR line : Tstr255; insert : BOOLEAN);    {insert as lineNo}
                                   {lineNo is limited to -1 to maxLines-1 for both GetLine and PutLine}
                                   {Possible to have active view size > maxlines after insertion, caller
                                    should make sure this does not happen.}

  PROCEDURE VScrollLines (topRegion, bottomRegion : INTEGER; scrollhowmanylines : INTEGER);
                                   {scrollhowmanylines : positive means to scroll down}
                                   {just scroll output on screen, does not change data structure}
  PROCEDURE RedrawScreen;          {repaint entire screen after lines or screen size changes}
  PROCEDURE RedrawLine (lineNo : INTEGER);       {repaint line after attributes changes}

  PROCEDURE ChangeMaxColumns (newColumns : INTEGER);
                        {Note that this call always go to the default char font. So if this is called when in
                         grphic font, the new font will be non-graphic font.}
     {for VT100 terminal emulator only, to switch to graphic character font and back}
  PROCEDURE ChangeFont (newfont : INTEGER);       {same character width, fixed pitch font only}




 {QP internal routines, to be called by UQPGraph too}
  PROCEDURE BindDocheap;
  PROCEDURE CkPutAway;              {to be called after process.ObeyEvents call to check put away}
  FUNCTION NotToWait : BOOLEAN;     {for ObeyEvents until idle, used in VStrWrite, Vread, WaitMouseEvent, WaitEvent}






IMPLEMENTATION

{$I libqp/USTDTERM2.TEXT}
{$I libqp/USTDTERM3.TEXT}

END.
ÿ