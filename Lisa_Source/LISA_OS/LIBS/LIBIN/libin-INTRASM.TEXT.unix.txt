DEBUGF    .EQU      1
          .SEG      'INTRCold'
;=======================================================================
;                                  HEAD
;-----------------------------------------------------------------------
     .MACRO    HEAD
       .IF       '%1' = ''
       LINK      A6,#0
       .ELSE
       LINK      A6,#-%1
       .ENDC
     .ENDM
;
;=======================================================================
;                                  TAIL
;-----------------------------------------------------------------------
     .MACRO    TAIL
     UNLK      A6
     .IF       %1 = 0
       RTS
     .ELSE
       .IF       %1 = 4
         MOVE.L    (SP)+,(SP)
         RTS
       .ELSE
         MOVE.L    (SP)+,A0
         ADD.W     #%1,SP
         JMP       (A0)
       .ENDC
     .ENDC
     .IF       DEBUGF
       .ASCII    %2
     .ENDC
     .ENDM
;=======================================================================
;
;                                       Constant declarations
;
;-----------------------------------------------------------------------
OneStart  .EQU      32
TwoStart  .EQU      32
UpStart   .EQU      97
DownStart .EQU      65
WeakStart .EQU      97

CAbsolute .EQU      0
CStrong   .EQU      1
CWeak     .EQU      2
CVeryWeak .EQU      3

MaxChrMat .EQU      10
;=======================================================================

          .FUNC     CompChrMagnitude
;=======================================================================
;
;                                       Compare Character Array Magnitude
;
;FUNCTION  CompChrMagnitude(char1, char2: CompChrP;
;                           length1, length2: INTEGER; veryWeak: BOOLEAN): INTEGER;
;
;-----------------------------------------------------------------------
          .DEF      MagniEval
          .REF      xFirst,xSecond,IdentEval,Expand,ExpChar

VERYWEAK  .EQU      8
LENGTH2   .EQU      VERYWEAK+2
LENGTH1   .EQU      LENGTH2+2
CHAR2     .EQU      LENGTH1+2
CHAR1     .EQU      CHAR2+4
RETURN    .EQU      CHAR1+4
POINTER1  .EQU      -4
POINTER2  .EQU      POINTER1-4
LEN1      .EQU      POINTER2-2
LEN2      .EQU      LEN1-2

;
;-----------------------------------------------------------------------
;
          HEAD      12
          MOVE.B    VERYWEAK(A6),D2     ;Is it a very weak or absolute identity compare
          MOVE.L    CHAR2(A6),A0        ;Pointer to array 2
          MOVE.L    CHAR1(A6),A1        ;Pointer to array 1
          MOVE.W    LENGTH2(A6),D0      ;Length of array 2
          MOVE.W    LENGTH1(A6),D1      ;Length of array 1
          BSR       MagniEval           ;evaluate
          MOVE.W    D2,RETURN(A6)       ;Put D2 as returned value of function
          TAIL      14,'COMPCHRM'       ;
;
;---------------------------------------
;
          .MACRO    Fetch               ;%1=pointer, %2=length, %3=next expanded, %4=result, %5=next label
          MOVE.W    %3,%4               ;Any expanded character in vector?
          BNE       @1                  ;If so use it
          SUBQ.W    #1,%2               ;Else decrement the length
          BLT       @1                  ;End of vector?
          MOVE.B    (%1)+,%4            ;Fetch next char
          CMPI.W    #TwoStart,%4        ;Less than start of Second table?
          BLT       @1                  ;If so, can't expand
          CLR.W     D7                  ;zap high byte
          MOVE.B    0(A3,%4),D7         ;Se if it can expand
          CMPI.W    #MaxChrMatch,D7
          BLE       @1                  ;Less than the limit  ;BLE or BLT ???????????
          MOVE.L    A5,D7               ;Calculate D7 := @ExpChar-@Expand-1
          SUB.L     A2,D7
          SUBQ.W    #1,D7               ;We now that D7 is now < 1 Word
@0        CMP.B     0(A2,D7),%4         ;Test if hit
          DBEQ      D7,@0               ;We know this will allways hit soner or later
          MOVE.B    0(A5,D7),%3         ;Fetch exanding character for next time
          BRA       %5                  ;Skip out of macro
@1        CLR.W     %3                  ;Make shure next expanded character is 0
          .ENDM
;
;---------------------------------------
;
MagniEval MOVE.L    A0,POINTER2(A6)     ;Save pointer to vector 2
          MOVE.L    A1,POINTER1(A6)     ;Save pointer to vector 1
          MOVE.W    D0,LEN2(A6)         ;Save length of vector 2
          MOVE.W    D1,LEN1(A6)         ;Save length of vector 1

          TST.B     D2                  ;Is D2 FALSE?
          BEQ       @1                  ;If so skip Very weak compare
          MOVEQ     #CVeryWeak,D2       ;Else set up call
          JSR       IdentEval           ;and evaluate identity
          TST.B     D2                  ;Is result true?
          BEQ       @1                  ;Skip it
          CLR.W     D2                  ;Return 0
          RTS                           ;and RTS

@1        MOVEM.L   A2-A5/D3-D7,-(SP)   ;Prepare to evaluate magnitude
          LEA       Expand,A2           ;Set up pointer to expand table
          LEA       xSecond,A3
          LEA       xFirst,A4
          LEA       ExpChar,A5

          MOVE.L    POINTER2(A6),A0     ;Restore pointer to vector 2
          MOVE.L    POINTER1(A6),A1     ;Restore pointer to vector 1
          MOVE.W    LEN2(A6),D0         ;Restore length of vector 2
          MOVE.W    LEN1(A6),D1         ;Restore length of vector 1
          CLR.W     D2                  ;No expanded character yet in vector 2
          CLR.W     D3                  ;No expanded character yet in vector 1

ME1       Fetch     A0,D0,D2,D4,ME2
ME2       Fetch     A1,D1,D3,D5,ME21
ME21      TST.W     D0
          BLT       ME3
          TST.W     D1
          BLT       ME3

          CMPI.W    #OneStart,D4
          BLT       @10
          MOVE.B    0(A4,D4),D4
@10       CMPI.W    #OneStart,D5
          BLT       @11
          MOVE.B    0(A4,D5),D5
@11       CMP.W     D4,D5
          BGT       MEGT
          BLT       MELT
          BRA       ME1

ME3       CMP.W     D0,D1
          BGT       MEGT
          BLT       MELT

          MOVE.L    POINTER2(A6),A0     ;Restore pointer to vector 2
          MOVE.L    POINTER1(A6),A1     ;Restore pointer to vector 1
          MOVE.W    LEN2(A6),D0         ;Restore length of vector 2
          MOVE.W    LEN1(A6),D1         ;Restore length of vector 1
          CLR.W     D2                  ;No expanded character yet in vector 2
          CLR.W     D3                  ;No expanded character yet in vector 1

ME4       Fetch     A0,D0,D2,D4,ME5
ME5       Fetch     A1,D1,D3,D5,ME51
ME51      TST.W     D0
          BLT       MEEQ

          CMPI.W    #TwoStart,D4
          BLT       @10
          MOVE.B    0(A3,D4),D4
@10       CMPI.W    #TwoStart,D5
          BLT       @11
          MOVE.B    0(A3,D5),D5
@11       CMP.W     D4,D5
          BGT       MEGT
          BLT       MELT
          BRA       ME4

MEEQ      CLR.W     D2
          BRA       MERTS
MEGT      MOVEQ     #1,D2
          BRA       MERTS
MELT      MOVEQ     #-1,D2
MERTS     MOVEM.L   (SP)+,A2-A5/D3-D7   ;Restore registers
          RTS



          .FUNC     CompStrMagnitude
;=======================================================================
;
;                                       Compare string magnitude
;
;FUNCTION  CompStrMagnitude(string1, string2: CompStrP; veryWeak: BOOLEAN): INTEGER;
;
;-----------------------------------------------------------------------
          .REF      MagniEval
VERYWEAK  .EQU      8
STRING2   .EQU      VeryWeak+2
STRING1   .EQU      STRING2+4
RETURN    .EQU      STRING1+4
;
;-----------------------------------------------------------------------
;
          HEAD      12
          MOVE.B    VERYWEAK(A6),D2     ;Is it a very weak or absolute identity compare
          MOVE.L    STRING2(A6),A0      ;Pointer to string2
          MOVE.L    STRING1(A6),A1      ;Pointer to string1
          CLR.W     D0                  ;zap high byte
          MOVE.B    (A0)+,D0            ;Length to D0 and A0 points to first char of STRING2
          CLR.W     D1                  ;zap high byte
          MOVE.B    (A1)+,D1            ;Length to D1 and A1 points to first char of STRING1
          JSR       MagniEval           ;use same evaluation as CompChrIdentity
          MOVE.W    D2,RETURN(A6)       ;Put D2 as returned value of function
          TAIL      10,'COMPSTRM'

          .FUNC     CompChrIdentity
;=======================================================================
;
;                                       Compare Character Array Identity
;
;FUNCTION  CompChrIdentity(char1, char2: CompChrP;
;                          length1, length2: INTEGER; kind: KindOfCompare): BOOLEAN;
;
;-----------------------------------------------------------------------
          .DEF      IdentEval
          .REF      xDownTbl,xWeakTbl

KIND      .EQU      8
LENGTH2   .EQU      KIND+2
LENGTH1   .EQU      LENGTH2+2
CHAR2     .EQU      LENGTH1+2
CHAR1     .EQU      CHAR2+4
RETURN    .EQU      CHAR1+4
;
;-----------------------------------------------------------------------
;
          HEAD
          MOVE.B    KIND(A6),D2         ;Kind of compare
          MOVE.L    CHAR2(A6),A0        ;Pointer to array 2
          MOVE.L    CHAR1(A6),A1        ;Pointer to array 1
          MOVE.W    LENGTH2(A6),D0      ;Length of array 2
          MOVE.W    LENGTH1(A6),D1      ;Length of array 1
          BSR       IdentEval           ;Evaluate
          MOVE.B    D2,RETURN(A6)       ;Put D2 as returned value of function
          TAIL      14,'COMPCHRI'

;---------------------------------------
IdentEval ANDI.W    #$FF,D2             ;Mask away high byte
;         CHK       #CVeryWeak,D2       ;Make shure we have a valid KIND
          CMP.W     D0,D1               ;First test the length
          BNE       FALSE               ;Fail
          SUBQ.W    #1,D0               ;Adjust counter
          BLT       TRUE                ;If < 0 then Succed
          CMP.W     #CVeryWeak,D2
          BEQ       VeryWeak            ;If so test Very Weak
          CMP.W     #CWeak,D2
          BEQ       Weak                ;If so test Weak
          CMP.W     #CStrong,D2
          BEQ       Strong              ;If so test Strong
;---------------------------------------;Else assume Absolute
;
Abs       MOVE.B    (A0)+,D1
          CMP.B     (A1)+,D1            ;Test the characters and advance pointers
          DBNE      D0,Abs              ;Decrement counter and go back if still equal
TestIt    BEQ       TRUE                ;All characters were equal
FALSE     CLR.W     D2                  ;Set D2 to FALSE
          RTS                           ;and return
TRUE      MOVEQ     #1,D2               ;Set D2 to TRUE
          RTS                           ;and return
;---------------------------------------
;
Strong    MOVE.L    A2,-(SP)            ;Save A2 for a moment
          LEA       xDownTbl,A2         ;Fetch start of DownTbl into A2
          CLR.W     D2                  ;Clear high bytes
          CLR.W     D1                  ;of D1 and D2
;
Strong1   MOVE.B    (A0)+,D2            ;Get next byte of CH2
          CMPI.W    #DownStart,D2       ;Less than 'A'?
          BLT       @1                  ;If less then no need to DownShift
          MOVE.B    0(A2,D2),D2         ;Fetch byte from Downshift table
;
@1        MOVE.B    (A1)+,D1            ;Get next byte of CH1
          CMPI.W    #DownStart,D1       ;Less than 'A'?
          BLT       @2                  ;If less then no need to DownShift
          MOVE.B    0(A2,D1),D1         ;Fetch byte from Downshift table
;
@2        CMP.W     D1,D2               ;Test assumption
          DBNE      D0,Strong1          ;Test and decremet loop counter
          MOVE.L    (SP)+,A2            ;Restore A2
          BRA       TestIt              ;Evaluate Result
;---------------------------------------
;
Weak      MOVEM.L   A2-A3/D3-D4,-(SP)   ;Save A2-A3 and D3-D4 for a moment
          LEA       xDownTbl,A2         ;Fetch start of DownTbl into A2
          LEA       xWeakTbl,A3         ;Fetch start of WeakTbl into A3
          CLR.W     D2                  ;Clear high bytes
          CLR.W     D1                  ;of D1 and D2
;
Weak1     MOVE.B    (A0)+,D2            ;Get next byte of CH2
          CMPI.W    #DownStart,D2       ;Less than 'A'?
          BLT       @1                  ;If less then no need to DownShift
          MOVE.B    0(A2,D2),D2         ;Fetch byte from Downshift table
;
@1        MOVE.B    (A1)+,D1            ;Get next byte of CH1
          CMPI.W    #DownStart,D1       ;Less than 'A'?
          BLT       @2                  ;If less then no need to DownShift
          MOVE.B    0(A2,D1),D1         ;Fetch byte from Downshift table
;
@2        CMP.W     D1,D2               ;Test assumption
          BEQ       Weak2               ;If result goto next character pair
;
          CMPI.W    #WeakStart,D2       ;Less than 'a'?
          BLT       WeakF               ;If less then Weak fails
          MOVE.B    0(A3,D2),D2         ;Fetch byte from Weak table
          MOVE.W    D2,D4               ;Save a copy in D4
;
          CMPI.W    #WeakStart,D1       ;Less than 'a'?
          BLT       WeakF               ;If less then Weak fails
          MOVE.B    0(A3,D1),D1         ;Fetch byte from Weak table
          MOVE.W    D1,D3               ;Save a copy in D3
;
          AND.B     D2,D1               ;Compute if they have the same accents
          ANDI.B    #$1F,D1             ;and look only at accents
          BEQ       WeakF               ;If no match then weak fails
;
          ANDI.B    #$E0,D3             ;Mask away all but character family
          ANDI.B    #$E0,D4
          CMP.B     D3,D4               ;Are they of the same family?
          BNE       WeakF
Weak2     DBF       D0,Weak1

          MOVEM.L   (SP)+,A2-A3/D3-D4   ;Restore regidsters
          BRA       TRUE                ;Exit with the result TRUE
WeakF     MOVEM.L   (SP)+,A2-A3/D3-D4   ;Restore regidsters
          BRA       FALSE               ;Exit with the result TRUE
;---------------------------------------
;
VeryWeak  MOVEM.L   A2-A3,-(SP)         ;Save A2-A3 for a moment
          LEA       xDownTbl,A2         ;Fetch start of DownTbl into A2
          LEA       xWeakTbl,A3         ;Fetch start of WeakTbl into A3
          CLR.W     D2                  ;Clear high bytes
          CLR.W     D1                  ;of D1 and D2
;
VeryW1    MOVE.B    (A0)+,D2            ;Get next byte of CH2
          CMPI.W    #DownStart,D2       ;Less than 'A'?
          BLT       @1                  ;If less then no need to DownShift
          MOVE.B    0(A2,D2),D2         ;Fetch byte from Downshift table
;
@1        MOVE.B    (A1)+,D1            ;Get next byte of CH1
          CMPI.W    #DownStart,D1       ;Less than 'A'?
          BLT       @2                  ;If less then no need to DownShift
          MOVE.B    0(A2,D1),D1         ;Fetch byte from Downshift table
;
@2        CMP.W     D1,D2               ;Test assumption
          BEQ       VeryW2              ;If result goto next character pair
;
          CMPI.W    #WeakStart,D2       ;Less than 'a'?
          BLT       VeryWF              ;If less then Very Weak fails
          MOVE.B    0(A3,D2),D2         ;Fetch byte from Very Weak table
          BEQ       VeryWF              ;If equal to 0 then Very Weak fails
;
          CMPI.W    #WeakStart,D1       ;Less than 'a'?
          BLT       VeryWF              ;If less then Very Weak fails
          MOVE.B    0(A3,D1),D1         ;Fetch byte from Very Weak table
          BEQ       VeryWF              ;If equal to 0 then Very Weak fails
;
          ANDI.B    #$E0,D1             ;Mask away all but character family
          ANDI.B    #$E0,D2             ;Mask away all but character family
          CMP.B     D2,D1               ;Are they of the same family?
          BNE       VeryWF              ;If not then fail
VeryW2    DBF       D0,VeryW1
          MOVEM.L   (SP)+,A2-A3         ;Restore regidsters
          BRA       TRUE                ;Exit with the result TRUE
VeryWF    MOVEM.L   (SP)+,A2-A3         ;Restore regidsters
          BRA       FALSE               ;Exit with the result TRUE

          .FUNC     CompStrIdentity
;=======================================================================
;
;                                       Compare string identity
;
;FUNCTION  CompStrIdentity(string1, string2: CompStrP; kind: KindOfCompare): BOOLEAN;
;
;-----------------------------------------------------------------------
          .REF      IdentEval
KIND      .EQU      8
STRING2   .EQU      KIND+2
STRING1   .EQU      STRING2+4
RETURN    .EQU      STRING1+4
;
;-----------------------------------------------------------------------
;
          HEAD
          MOVE.B    KIND(A6),D2         ;Kind of compare
          MOVE.L    STRING2(A6),A0      ;Pointer to string2
          MOVE.L    STRING1(A6),A1      ;Pointer to string1
          CLR.W     D0                  ;zap high byte
          MOVE.B    (A0)+,D0            ;Length to D0 and A0 points to first char of STRING2
          CLR.W     D1                  ;zap high byte
          MOVE.B    (A1)+,D1            ;Length to D1 and A1 points to first char of STRING1
          JSR       IdentEval           ;use same evaluation as CompChrIdentity
          MOVE.B    D2,RETURN(A6)       ;Put D2 as returned value of function
          TAIL      10,'COMPSTRI'

          .FUNC     CompareChar
;=======================================================================
;
;                                       Compare character identity
;
;FUNCTION  CompareChar(ch1, ch2: CHAR; kind: KindOfCompare): BOOLEAN;
;
;-----------------------------------------------------------------------
          .REF      IdentEval
KIND      .EQU      8
CH2       .EQU      KIND+2
CH1       .EQU      CH2+2
RETURN    .EQU      CH1+2
;
;-----------------------------------------------------------------------
;
          HEAD
          MOVE.B    KIND(A6),D2         ;Kind of compare
          LEA       CH2+1(A6),A0        ;Pointer to low byte of ch2
          LEA       CH1+1(A6),A1        ;Pointer to low byte of ch1
          MOVEQ     #1,D0               ;Set D0 and
          MOVEQ     #1,D1               ;D1 to 1 to set up for IdentEval
          JSR       IdentEval           ;use same evaluation as CompChrIdentity
          MOVE.B    D2,RETURN(A6)       ;Put D2 as returned value of function
          TAIL      6,'COMPAREC'

          .PROC     UpStrShift
;=======================================================================
;
;                                       Upshift String
;
;PROCEDURE UpStrShift(sP: CompStrP);
;
;-----------------------------------------------------------------------
          .REF      UpShft
;
;-----------------------------------------------------------------------
;
          MOVE.L    (SP)+,A1            ;Fetch return address
          MOVE.L    (SP)+,A0            ;Pointer to string
          CLR.W     D0                  ;zap high byte
          MOVE.B    (A0)+,D0            ;Length to D0 and A0 points to first char
          JMP       UpShft

          .PROC     UpChrShift
;=======================================================================
;
;                                       Upshift packed array of CHAR
;
;PROCEDURE UpChrShift(pc: CompChrP; length: INTEGER);
;
;-----------------------------------------------------------------------
          .REF      xUpTbl
          .DEF      UpShft
;
;-----------------------------------------------------------------------
;
          MOVE.L    (SP)+,A1            ;Fetch return address
          MOVE.W    (SP)+,D0            ;length
          MOVE.L    (SP)+,A0            ;Pointer to first char
;
UpShft    MOVE.L    A1,-(SP)            ;Restore return address and free A1
          SUBQ.W    #1,D0               ;Adjust counter
          BLT       UpQuit              ;Make shure counter >= 0 (length > 0)
          CLR.W     D1                  ;Clear hight byte of D1
          LEA       xUpTbl,A1           ;Point A1 to the upshift table
;
@1        MOVE.B    (A0)+,D1            ;Fetch next char
          CMPI.W    #UpStart,D1         ;Less than 'a'?
          BLT       @2                  ;If less then no need to UpShift
          MOVE.B    0(A1,D1),-1(A0)     ;Upshift and put back
;
@2        DBF       D0,@1
UpQuit    RTS

          .PROC     DownStrShift
;=======================================================================
;
;                                       Downshift String
;
;PROCEDURE DownStrShift(sP: CompStrP);
;
;-----------------------------------------------------------------------
          .REF      DownShft
;-----------------------------------------------------------------------
;
          MOVE.L    (SP)+,A1            ;Fetch return address
          MOVE.L    (SP)+,A0            ;Pointer to string
          CLR.W     D0                  ;zap high byte
          MOVE.B    (A0)+,D0            ;Length to D0 and A0 points to first char
          JMP       DownShft

          .PROC     DownChrShift
;=======================================================================
;
;                                       Downshift packed array of CHAR
;
;PROCEDURE DownChrShift(pc: CompChrP; length: INTEGER);
;
;-----------------------------------------------------------------------
          .DEF      xFirst,xSecond,Expand,ExpChar,xWeakTbl,xUpTbl,xDownTbl
          .DEF      DownShft
;-----------------------------------------------------------------------
;
          MOVE.L    (SP)+,A1            ;Fetch return address
          MOVE.W    (SP)+,D0            ;length
          MOVE.L    (SP)+,A0            ;Pointer to first char
;
DownShft  MOVE.L    A1,-(SP)            ;Restore return address and free A1
          SUBQ.W    #1,D0               ;Adjust counter
          BLT       DownQuit            ;Make shure counter >= 0 (length > 0)
          CLR.W     D1                  ;Clear hight byte of D1
          LEA       xDownTbl,A1         ;Point A1 to the Downshift table
;
@1        MOVE.B    (A0)+,D1            ;Fetch next char
          CMPI.W    #DownStart,D1       ;Less than 'A'?
          BLT       @2                  ;If less then no need to DownShift
          MOVE.B    0(A1,D1),-1(A0)     ;Downshift and put back
;
@2        DBF       D0,@1
DownQuit  RTS

          .PAGE
;=======================================================================
;
;                                       Tables
;
;-----------------------------------------------------------------------
;
          .INCLUDE  libin/INTRTBL.text
xFirst    .EQU      First-OneStart
xSecond   .EQU      Second-TwoStart
xWeakTbl  .EQU      WeakTbl-WeakStart
xUpTbl    .EQU      UpTbl-UpStart
xDownTbl  .EQU      DownTbl-DownStart
;=======================================================================
          .END

ÿ