{                Copyright 1984, Apple Computer Inc.                     }
PROGRAM Print;
{  This is the background printing process for the Canon Ink Jet.}


{ ---------------------------------------------------------------------- }

USES {$U LibOS/SysCall   }  SysCall,
     {$U LibPM/PMDecl    }  PMDecl,
     {$U LibHW/Hwint     }  Hwint,
     {$U LibSM/UnitStd   }  UnitStd,
     {$U LibSM/UnitHz    }  UnitHz,
     {$U LibQD/Storage   }  Storage,
     {$U LibQD/QuickDraw }  QuickDraw,
     {$U LibFM/FontMgr   }  FontMgr,
     {$U LibQD/GrafUtil  }  GrafUtil,

     {$U LibWM/Events    }  Events,
     {$U LibWM/Menus     }  Menus,
     {$U LibWM/Windows   }  Windows,
     {$U LibWM/Folders   }  Folders,
     {$U libSB/WmlStd    }  WmlStd,
     {$U libSB/WmlCrs    }  WmlCrs,
     {$U LibAM/AlertMgr  }  AlertMgr,

     {$U LibPr/PrStdInfo }  PrStdInfo,
     {$U LibPr/PrStdProcs}  PrStdProcs,
     {$U LibPr/PrFileProcs} PrFileProcs,
     {$U LibPr/Queues    }  Queues,
     {$U LibPr/PrEvents  }  PrEvents,
     {$U LibPr/PrDlgMgr  }  PrDlgMgr,
     {$U LibPr/PrMgr     }  PrMgr,

     {$U-}
     {$U LibPr/CNGlobals   }  CNGlobals,
     {$U LibPr/CNStdQDProcs}  CNStdQDProcs,
     {$U LibPr/CNProcs     }  CNProcs,
     {$U LibPr/CNDlg       }  CNDlg;

{ ---------------------------------------------------------------------- }
{$IFC PrDebug}
   {$R+}
{$ELSEC}
   {$R-}
{$ENDC}

{$IFC PrSymbols}
   {$D+}
{$ELSEC}
   {$D-}
{$ENDC}
{ ---------------------------------------------------------------------- }
{DIRECTORY}
PROCEDURE CNInitAlerts;                                                 FORWARD;
PROCEDURE CNPrintCleanup;                                               FORWARD;
PROCEDURE CNSysTerm (environPtr: P_env_blk; excepDataPtr: P_ex_data);   FORWARD;
PROCEDURE CNCallService (environPtr: P_env_blk;
                         excepDataPtr: P_ex_data);                      FORWARD;
FUNCTION  CNMoreHeap (hz: QDPtr; bytesNeeded: TC): TC;                  FORWARD;
PROCEDURE CNPrintFile (FName: Str255; VAR PrRec: TPrRec);               FORWARD; {!DC 5/2/84}
PROCEDURE CNInitMain;                                                   FORWARD;
PROCEDURE CNMainLoop;                                                   FORWARD;
{ ---------------------------------------------------------------------- }
{$S Init}
FUNCTION CNMoreHeap {hz: QDPtr; bytesNeeded: TC): TC};
VAR lNewMem,
    lNewDisk:  TL;
    Error:     TC;
    DSInfo:    DSInfoRec;
{ This function will be called if the heapZone needs more space.}
BEGIN
  Info_DataSeg (error, QDDSRec.RefNum, DSInfo);
  Size_DataSeg (error, QDDSRec.RefNum, BytesNeeded, lNewMem, 0, lNewDisk);
  CNMoreHeap := LnewMem - DSInfo.mem_size;
END;
{ ---------------------------------------------------------------------- }
{$S   }
PROCEDURE CNInitAlerts;
VAR cAlerts: TC;
BEGIN
  InitAlerts (CacheSize, CacheBytes, theHeap, nil, nil);
  OpenPhraseFile (cAlerts, sAlerts);
  IF AlertError <= 0 THEN BgdAlertFile := ReadAlerts (cAlerts, cAxVersion)
                     ELSE Exit (Print);
END;

{ ---------------------------------------------------------------------- }
{$S   }
PROCEDURE CNPrintCleanup;
BEGIN
END;
{ ---------------------------------------------------------------------- }
{$S Init}
PROCEDURE CNSysTerm {environPtr : P_env_blk; excepDataPtr : P_ex_data};
VAR error, port: TC;
BEGIN
  DialogHeight (0, FALSE);
  fBackground := FALSE;
  ACTIVATE_PROCESS (error, CallerProcID, FALSE);      {!DC 2/17/84}
  PrCloseChan (Listen);
  PrCloseChan (ListenCall);
  IF CNPrinter <> NIL THEN BEGIN                      {!DC 4/12/84}
    CNPrinter^^.SonID := PrNilProcess;
    { ----- Close and unmount the printer port. }
    PsClosePort (error, CNPrinter^^.PrRec.PrInfo.port);
  END;
  PrMgrDSUnBind (Error);  {--May not reference anything in the data seg after this}
  { ----- Close and delete the QuickDraw data segment. }
  Close_DataSeg (error, QDDSRec.RefNum);
END;
{ ---------------------------------------------------------------------- }
{$S   }
PROCEDURE CNCallService{(environPtr : P_env_blk; excepDataPtr : P_ex_data)};
VAR
   pevData:    ^PrEventBlk;
   Message:    ^PrMessage;
   cError:     TC;         {!DC 2/15/84}
   excepName:  T_Ex_Name;  {!DC 2/15/84}
BEGIN
  pevData := POINTER(excepDataPtr);
  Message := @pevData^.eventBytes[0];
  callerProcID := pevData^.event_header.send_pid;
  WITH pPrMgrDS^, DlgArgs DO CASE Message^ OF

    EPrintDialog:
    BEGIN     {!DC 2/14/84}
      ThePrRec.PrInfo.Port := CNPrinter^^.PrRec.PrInfo.Port;      {!DC 3/22/84}
      BIND_DATASEG(cError,DDSRefNum);                              {!BH 5/04/84}
      ReturnOpt := FInsDlg (ThePrRec, newStyle, menuSuppress);
      UNBIND_DATASEG(cError,DDSRefNum);                              {!BH 5/04/84}
      IF NewStyle THEN CNPrinter^^.PrRec := ThePrRec;            {!DC 3/14/84}
      fBackground := FALSE;
      ACTIVATE_PROCESS (cError, callerProcID, FALSE);  {!DC 2/16/84}
    END;

    ESettingsDialog:
    BEGIN  {!DC 2/14/84}
      ThePrRec.PrInfo.Port := CNPrinter^^.PrRec.PrInfo.Port;      {!DC 3/22/84}
      BIND_DATASEG(cError,DDSRefNum);                              {!BH 5/04/84}
      ReturnOpt := FFmtDlg (ThePrRec, newStyle, maxHigh, maxWide, minHigh, minWide, noSpecSize);
      UNBIND_DATASEG(cError,DDSRefNum);                              {!BH 5/04/84}
      IF NewStyle THEN CNPrinter^^.PrRec := ThePrRec;            {!DC 3/14/84}
      fBackground := FALSE;
      ACTIVATE_PROCESS (cError, callerProcID, FALSE);  {!DC 2/16/84}
    END;

    EPrintTerminate:
    BEGIN               {!DC 3/14/84}
      CNAbort:=TRUE;
    END;
  END;

  excepName := ListenCall.ChName;    {!DC 2/15/84}
  excepName[0] := CHR(8);            {!DC 2/15/84}
  Enable_Excep (cError, excepName);  {!DC 2/15/84}
END;

{ ---------------------------------------------------------------------- }
{$S   }
PROCEDURE CNPrintFile {FName:Str255; VAR PrRec:TPrRec};  {!DC 5/2/84}
VAR Error,  PageNum: TC;
    hPic:            PicHandle;
    rFrame:          Rect;
    PathNme:         PathName;
    pDevRec:         TpDevRec;                                                {!DC 5/2/84}
BEGIN
  pDevRec := @PrRec.PrDevInfo;                                                {!DC 5/2/84}
  IF NOT CNAbort THEN BEGIN
    IF NOT OpenFile THEN CNOpenFile (FName, PrRec);                           {!DC 5/2/84}
    WITH PrRec.PrInfo, rPaper DO IF pgSize.printDirect = portrait             {!DC 4/18/84}
    THEN yBottom := bottom
    ELSE yBottom := right;
    IF pDevRec^.CNInfo.feeder = feedCut THEN yBottom := yBottom - cBottomGap; {!DC 4/26/84, 5/2/84}
    yBottom := ((ORD4(yBottom) * CNResTruth) DIV CNRes);                      {!DC 4/26/84}
    PageNum := 1;
    REPEAT
      { ----- Set the file position and read the picture. }
      PfSetPos (error, hPfPages^^[PageNum].Start, PrFileRec.RefNum);
      CNReadPicture (error, hPfPages^^[PageNum].Length, PrFileRec.Refnum);
      IF NOT CNAbort THEN BEGIN
        { ----- Read the Picture Frame rectangle before looking at the rest of the picture. QuickDraw
                doesn't write the picture size or frame when using put and get procs. }
        CNGetPicProc (POINTER(ORD(@rFrame)), 8);

        { ----- Set up a picture header for using the correct frame rectangle}
        PsNonPreemptive;              {!DC 4/10/84}
        hPic := OpenPicture (rFrame);
        ClosePicture;
        PsPreemptive;                 {!DC 4/10/84}
        CNPrintPict (error, @PrRec, hPic, rFrame); {!DC 5/2/84}
        CNEndPage (error, (PageNum <> NumPages) AND (pDevRec^.CNInfo.feeder = feedCut));

        DisposeHandle (POINTER(ORD(hPic)));
        PageNum := PageNum + 1;
      END;
    UNTIL (PageNum > NumPages) OR CNAbort;
  END;

  { ----- Close the print file. }
  Close_Object (error, PrFileRec.RefNum);
  OpenFile := False;
END;
{ ---------------------------------------------------------------------- }
{                            Main  Procs                                 }
{ ---------------------------------------------------------------------- }
{$S Init}
PROCEDURE CNInitMain;
VAR Error:     TC;
    excepName: T_Ex_Name;
BEGIN
  cBottomGap := (CNResTruth * 3) DIV 2;   {1.5"}   {!DC 4/25/84}
  CNPrinter := NIL;
  CallerProcID := ActiveProcess;
  excepName := 'SYS_TERMINATE';
  DECLARE_EXCEP_HDL (error, excepName, @CNSysTerm);
  IF error <> 0 THEN Exit (Print);

  { ----- Initialize QuickDraw and open the Window Manager.}
  {--OpenWM must be called before any other creation of any other datasegment due to the concurrency}
  {--problem. }
  InitGraf (@thePort);
  OpenWM;

  { ----- Make the data segment for QuickDraw's TheHeap. }
  WITH QDDSRec DO BEGIN
    SegName  :=QDDSName;
    InitSize :=QDDSSize;
    LDSN     :=QDDSLDSN;
  END;
  WITH QDDSRec DO Make_DataSeg (error, SegName, InitSize, InitSize, refnum,
                                BegAddr, ldsn, ds_private);
  IF error <> 0 THEN Exit (Print);                          {!DC 4/11/84}

  { ----- Initialize TheHeap. }
  InitHeap (POINTER(ORD(QDDSRec.BegAddr)), POINTER(ORD(QDDSRec.BegAddr) + QDHeapSize), @CNMoreHeap);


  { ----- Open up the port and set some defaults.}
  OpenPort (POINTER(ORD4(@PrPort.gport)));
  PrPort.gport.grafprocs := @PrPort.gProcs;
  SetStdProcs (PrPort.gProcs);
  WITH PrPort.gProcs DO BEGIN
    TextProc   := @PrStdText;
    LineProc   := @PrStdLine;
    RectProc   := @PrStdRect;
    RRectProc  := @PrStdRRect;
    OvalProc   := @PrStdOval;
    PolyProc   := @PrStdPoly;
    RgnProc    := @PrStdRgn;
    BitsProc   := @PrStdBits;
    ArcProc    := @PrStdArc;
    GetPicProc := @CNGetPicProc;
  END;

  { ----- Init PrDlg stuff }
  PrBgdInit;
  CNInitAlerts;
  IF NOT CNDlgInit THEN EXIT (Print);                       {!DC 4/10/84}

  { ----- Initialize and Make/Open the eventchannels. }     {!DC 4/10/84}
  PrOpenChan (Listen, TRUE{Reciever});
  PrOpenCall (ListenCall, @CNCallService);

  PrMgrDSBind (Error);
  IF error <> 0 THEN Exit (Print);                          {!DC 4/11/84}

  PrDeBug := False;

  OpenFile := False;
  InitDeBug;

END;
{ ---------------------------------------------------------------------- }
{$S   }
PROCEDURE CNMainLoop;
VAR error:   TC;
    fileId:  TPrFileId;
    prRec :  TPrRec;
    pDevRec: ^TDevRec;
BEGIN
  CASE PrAccept(Listen) OF

    EPrintRequest:
    WITH Listen DO BEGIN
                     FileId.LongId := EVBlock.event_Text[1];
                     GetFileName(FileId);
                     CASE FileId.BlockNum OF
                       0:    BEGIN
                               CNDocBegin (FileId.sFileName, PrRec);          {!DC 5/2/84}
                               CNPrintFile (FileId.sFileName, PrRec);         {!DC 5/2/84}
                             END;
                       9999: CNDocEnd (Error, @PrRec);                        {!DC 5/2/84}
                       OTHERWISE CNPrintFile (FileId.sFileName, PrRec);       {!DC 5/2/84}
                     END;

                     IF CNAbort AND (FileId.BlockNum <> 9999) THEN BEGIN
                       CNCleanUp;
                       CNDocEnd (Error, @PrRec);                              {!DC 5/2/84}
                     END;
                     PrCall (Error, lParentId, EDone, FileId.LongId, EVBlock.event_Text[2] - 1, 0, 0);
                   END;

    EMetrics:      BEGIN
                     { ----- Get the Metrics. Set global PrRec and PrinterId.  }
                     CNDfMetrics (PrRec);
                     CNPrinter^^.PrRec := PrRec;
                     CNPrinter^^.PrRec.PrInfo.PrinterId := Listen.EVBlock.event_Text[2];
                     CNPrinter^^.PrRec.PrInfo.Port      := Listen.EVBlock.event_Text[3];
                   END;
    ETalkToMe:     BEGIN
                     { ----- Update the printer handle. This should be the first event after startup.  }
                     CNPrinter := POINTER(Listen.EVBlock.event_Text[1]);
                     CNPrinter^^.SonId := My_ID;
                     { ----- Make Lastjob equal to known state. }
                     pDevRec := POINTER(ORD(@CNPrinter^^.PrRec.PrDevInfo));
                     WITH LastJob DO BEGIN
                       Feeder := pDevRec^.CNInfo.feeder;
                       PgSize := CNPrinter^^.PrRec.PrInfo.PgSize;
                     END;
                   END;
  END;
END;

{ ---------------------------------------------------------------------- }
{$S   }
BEGIN
  CNInitMain;
  PrDeBug := FALSE;
  REPEAT
    PsYieldCpu;
    CNMainLoop;
  UNTIL FALSE;
END.

{ ---------------------------------------------------------------------- }
{
PROGRAM Print CHANGE LOG:
      5/02/84 DC: CNMainLoop: new param sequence for CNDocBegin, CNPrintFile, CNDocEnd calls;
                  removed DevRec variable.
      5/02/84 DC: CNPrintFile: new param sequence for CNPrintPict, CNOpenFile calls; removed DevRec
                  param.
      4/26/84 DC: removed all references to cTopGap
      4/26/84 DC: DNPrintFile -  further adjustments to yBottom
      4/25/84 DC: DNPrintFile - correct yBottom for lie about resolution
      4/25/84 DC: CNInitMain - initialize global variables.
      4/23/84 DC: CNPrintFile - IF NOT CNAbort potection after CNReadPicture call; last param removed
                  from CNPrintPict call.
      4/18/84 DC: CNPrintFile - initialize yBottom.
      4/12/84 DC: CNSysTerm - Added PsClosePort call.
      4/11/84 DC: CNInitMain - added some exits.
      4/10/84 DC: CNPrintFile - made QD calls nonpreemptive.
      4/10/84 DC: CNInitMain - moved PrOpenChan, PrOpenCall to follow CnDlgInit.  Also call
                  CNDlgInit as a function & exit if it comes back false.
      3/22/84 DC: CNCallService - Update the port in the doc's prRec from the shared prRec, which
                  gets updated every time connections are changed in Preferences.
     10/25/83 EZ: Moved pfcomment procedure and the stuffing of the pfcomment procedure to PrProcs.
                  Moved setting of theport^.fillpat to PrProcs. Deleted obsolete parameter to InitGraf.
                  These changes were made for the new QuickDraw.
      5/06/83 EZ: Changed +S 10000 to +S 20000.
      2/19/83 OD: Made shorter JoyfullNoise.
      1/29/83 OD: Add Sched_Fence calls.
                  Reformat the program: CNMainLoop etc; gen cleanup.
                  Suicide: add parameter.
}


ÿ