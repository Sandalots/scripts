(*
$EXEC
P?
-newdisk-intrinsic.lib
LibPr/ParDlg
{no listing}
LibPr/Temp
GLibPr/Temp
LibPr/ParDlg

$ENDEXEC
*)
{------------------------------------------------------------------------------}
{                Copyright 1983, 1984, Apple Computer Inc.                     }
{------------------------------------------------------------------------------}
{$S ParDlg} {!BH 2/24/84}
UNIT ParDlg;
     {code to support dialogs in background parent process}

INTERFACE
USES
    {$U LibOS/SysCall     } SysCall,
    {$U LibSM/UnitStd     } UnitStd,
    {$U LibSM/UnitHz      } UnitHz,
    {$U LibPM/PMDecl      } PMDecl,
    {$U LibQD/Storage     } Storage,
    {$U LibQD/QuickDraw   } QuickDraw,
    {$U LibFM/FontMgr     } FontMgr,
    {$U LibWM/Events      } Events,
    {$U LibWM/Menus       } Menus,
    {$U LibWM/Windows     } Windows,
    {$U LibWM/Folders     } Folders,
    {$U libSB/WmlStd      } WmlStd,
    {$U libSB/WmlCrs      } WmlCrs,
    {$U LibAM/AlertMgr    } AlertMgr,
    {$U LibHW/HwInt       } HwInt,

    {$U LibPr/PrStdInfo   } PrStdInfo,
    {$U LibPr/PrStdProcs  } PrStdProcs,
    {$U LibPr/Queues      } Queues,
    {$U LibPr/PrEvents    } PrEvents,
    {$U LibPr/PrDlgMgr    } PrDlgMgr,
    {$U LibPr/PrMgr       } PrMgr;

{ ---------------------------------------------------------------------- }

CONST
  cmaxlist  = 8;                                  {Maximum number of printers in menu list}

VAR
   maxCBtnList:     TC;                           {Maximum number of buttons in list menus}
   maxCBtnQ:        TC;
   ParDefTsp:       TSP;                          {The String for ', (default printer)'}
   DDSRefNum:       TC;                           {!BH make it global enough that we can bind & close it.}

FUNCTION  ParDlgInit:TF;                          {1-time unit initialization, TRUE if init took}
PROCEDURE ParDlgQuit;                             {1-time end to close the data seg}{!BH 2/24/84}

PROCEDURE NewList;                                {Zero the list of printer buttons}
PROCEDURE MakeListBtn (iBtn: TC; sp:TSp);         {Define a printer button}
PROCEDURE PushListBtn (iBtn: TC);                 {Push a printer button}
FUNCTION  FListDlg (VAR select: TC): DlgOption;   {Run the printer list dlg, selected btn returned in param}

PROCEDURE NewQ;                                   {Zero the list of queue status buttons}
PROCEDURE MakeQBtn (iBtn: TC; sp:TSp);            {Define a queue status button}
FUNCTION  FQDlg: DlgOption;                       {Run the queue status dlg}

{ ---------------------------------------------------------------------- }
                               IMPLEMENTATION
{ ---------------------------------------------------------------------- }

{$IFC PrDebug}
  {$R+}
{$ELSEC}
  {$R-}
{$ENDC}

{$IFC PrSymbols}
  {$D+}
{$ELSEC}
  {$D-}
{$ENDC}

CONST
  DSSegname = 'ParBtnData';

  {The menu and button indices.}
  muDoIt     = 0;                      {This mu serves for both dlgs}
    iDoItYes      = 0;  {"OK" btn}
    iDoItNo       = 1;  {"Cancel" btn}

  muPrinters = 1;                      {This mu serves for both dlgs}

TYPE
    TRgHCkFm  = ARRAY[0..5] OF TL;
    TPRgHCkFm = ^TRgHCkFm;

VAR
   hckfmList:       THckfm;   {The FListDlg ckfm}
   hckfmQ:          THckfm;   {The FQDlg ckfm}
   pRgHCkFm:        TPRgHCkFm;
   savePort:        GrafPtr;
   dlgPort:         GrafPort;

{ -------------------------DIRECTORY------------------------------------ }

{The Printer List dialog}
     {Initializing the dialog instance}
PROCEDURE ListSetUp;                                             FORWARD;
PROCEDURE ListCkSet;                                             FORWARD;
     {Bottleneck Procs for PrDlgMgr}
PROCEDURE ListCkCmd (iMu, iBtn: TC);                             FORWARD;
PROCEDURE ListEnd (fDismiss: TF);                                FORWARD;

{The Queue Status dialog}
     {Initializing the dialog instance}
PROCEDURE QSetUp;                                                FORWARD;
PROCEDURE QCkSet;                                                FORWARD;
     {Bottleneck Procs for PrDlgMgr}
PROCEDURE QCkCmd (iMu, iBtn: TC);                                FORWARD;
PROCEDURE QEnd (fDismiss: TF);                                   FORWARD;

{ ---------------------------------------------------------------------- }

PROCEDURE NewList;
BEGIN
WITH hckfmList^^.rgHMu[muPrinters]^^ DO BEGIN
  cBtn := 0;
  iBtnOn := 0;
END;
END;

{ ---------------------------------------------------------------------- }

PROCEDURE MakeListBtn {iBtn: TC; sp:TSp};
VAR h: TC;
BEGIN
  IF iBtn <= maxCBtnList THEN WITH hckfmList^^ DO BEGIN
    PsNonPreemptive;
    GetPort(savePort);
    SetPort(@dlgPort);
    WITH rgHMu[muPrinters]^^ DO BEGIN
      cBtn := CMAX (cBtn, iBtn);
      BtnBind (rgBtn[iBtn].lx);
      WITH rgBtn[iBtn], r DO BEGIN
        right := right - StringWidth(hsp^^) + StringWidth(sp);
        BtnSetSp (rgBtn[iBtn], sp, FALSE);
        WITH rgHMu[muDoIt]^^.rgBtn[iDoItYes].r DO BEGIN
          h := bottom - top;
          top := r.bottom + 5;
          bottom := top + h;
        END;
      END;
    END;
    rgHMu[muDoIt]^^.rBB.bottom := rgHMu[muDoIt]^^.rgBtn[iDoItYes].r.bottom;
    rBB.bottom := rgHMu[muDoIt]^^.rBB.bottom + dvCkfmMarg;
    SetPort(savePort);
    PsPreemptive;
  END;
END;

{ ---------------------------------------------------------------------- }

PROCEDURE PushListBtn {iBtn: TC};
VAR hMu: THMu;
BEGIN
  WITH hckfmList^^.rgHMu[muPrinters]^^ DO IF iBtn >= 0
                                          THEN IF iBtn <= cBtn
                                               THEN iBtnUsr := iBtn;
END;

{ ---------------------------------------------------------------------- }

PROCEDURE ListSetUp;
  {Sets up everything for the Printer List dialog, just before it runs}
BEGIN
  CkFmSetUp (hckfmList, TRUE {fInit}, setIdMuAll);     {PrDlgMgr's ckfm initialization}
  ListCkSet;                                           {our ckfm initialization}
  hckfmPrCk := hckfmList;                              {set generic handle for the ckfm}
  procCkCmd := @ListCkCmd;                             {stuff PrDlgMgr procs}
  DlgInstall (TRUE, @PrCkEvent, @PrCkTrkMse, @ListEnd);
  DlgOpen (hckfmList^^.rBB.bottom);                    {open the dlg window & init its grafport}
  CkFmShow (hckfmList, TRUE{fInit});                   {display the ckfm}
END;

{ ---------------------------------------------------------------------- }

PROCEDURE ListCkSet;
  {Turns on buttons and sets up strings when the dialog starts up.}
BEGIN
  hckfmList^^.rghmu[muDoIt]^^.iBtnOn := iBtnNil;     {initially no "on" btn in this menu}
END;

{ ---------------------------------------------------------------------- }

PROCEDURE ListCkCmd {iMu, iBtn: TC};
     {Bottleneck proc for responding to a particular button-push.}
BEGIN
  IF iMu = muDoIt THEN DlgEnd (FALSE); {FListDlg calls FCkFmHit to find whether IDoItYes was pushed.}
END;

{ ---------------------------------------------------------------------- }

FUNCTION FListDlg {VAR select: TC): DlgOption};
  {Runs the Printer List dialog}
BEGIN
  PsNonPreemptive;
  GetPort(savePort);
  SetPort(@dlgPort);

  ListSetup;                                 {get everything ready...}
  DlgMain;                                   {...and run the dialog}

  IF FCkfmHit (hckfmList, muDoIt, iDoItYes)  {if dlg was dismissed with "OK"...}
  THEN BEGIN
         FListDlg := eDlgOK;
         select := hckfmList^^.rgHMu[muPrinters]^^.iBtnOn;
       END
  ELSE FListDlg := eDlgCancel;

  SetPort(savePort);
  PsPreemptive;
END;

{ ---------------------------------------------------------------------- }

PROCEDURE ListEnd {fDismiss: TF};
  {Bottleneck proc for ending the Printer List dialog (becomes procEnd in the PrDlgMgr
   code).  Does what's needed between the time you hit a dismiss btn and the time the
   dlg is erased from the screen.}
BEGIN
  IF fDismiss THEN MuPush (hckfmList^^.rghmu[muDoIt], iBtnStd);    {if dismissed by a click outside
                                                                    the window, push "OK"}
END;

{ ---------------------------------------------------------------------- }

PROCEDURE NewQ;
BEGIN
  hckfmQ^^.rgHMu[muPrinters]^^.cBtn := 0;
END;

{ ---------------------------------------------------------------------- }

PROCEDURE MakeQBtn {iBtn: TC; sp:TSp};
VAR h: TC;
BEGIN
  IF iBtn <= maxCBtnQ THEN WITH hckfmQ^^ DO BEGIN
    PsNonPreemptive;
    GetPort(savePort);
    SetPort(@dlgPort);
    WITH rgHMu[muPrinters]^^ DO BEGIN
      cBtn := CMAX (cBtn, iBtn);
      BtnSetSp (rgBtn[iBtn], sp, FALSE);
      WITH rgHMu[muDoIt]^^.rgBtn[iDoItYes].r DO BEGIN
        h := bottom - top;
        top := rgHMu[muPrinters]^^.rgBtn[iBtn].r.bottom + 5;
        bottom := top + h;
      END;
    END;
    rgHMu[muDoIt]^^.rBB.bottom := rgHMu[muDoIt]^^.rgBtn[iDoItYes].r.bottom;
    rBB.bottom := rgHMu[muDoIt]^^.rBB.bottom + dvCkfmMarg;
    SetPort(savePort);
    PsPreemptive;
  END;
END;

{ ---------------------------------------------------------------------- }

PROCEDURE QSetUp;
  {Sets up everything for the Queue Status dialog, just before it runs}
BEGIN
  CkFmSetUp (hckfmQ, TRUE {fInit}, setIdMuAll);          {PrDlgMgr's ckfm initialization}
  QCkSet;                                                {our ckfm initialization}
  hckfmPrCk := hckfmQ;                                   {set generic handle for the ckfm}
  procCkCmd := @QCkCmd;                                  {stuff PrDlgMgr procs}
  DlgInstall (TRUE, @PrCkEvent, @PrCkTrkMse, @QEnd);
  DlgOpen (hckfmQ^^.rBB.bottom);                         {open the dlg window & init its grafport}
  CkFmShow (hckfmQ, TRUE{fInit});                        {display the ckfm}
END;

{ ---------------------------------------------------------------------- }

PROCEDURE QCkSet;
BEGIN
  hckfmQ^^.rghmu[muDoIt]^^.iBtnOn := iBtnNil;     {initially no "on" btn in this menu}
  hckfmQ^^.rghmu[muPrinters]^^.iBtnOn := iBtnNil; {no "on" btn in this menu}
END;

{ ---------------------------------------------------------------------- }

PROCEDURE QCkCmd {iMu, iBtn: TC};
     {Bottleneck proc for responding to a particular button-push.}
BEGIN
  IF iMu = muDoIt THEN DlgEnd (FALSE); {FQDlg calls FCkFmHit to find whether IDoItYes was pushed.}
END;

{ ---------------------------------------------------------------------- }

PROCEDURE QEnd {fDismiss: TF};
  {Bottleneck proc for ending the Queue Status dialog (becomes procEnd in the PrDlgMgr
   code).  Does what's needed between the time you dismiss the dlg and the time the
   dlg is erased from the screen.}
BEGIN
  IF fDismiss THEN MuPush (hckfmQ^^.rghmu[muDoIt], iBtnStd); {if dismissed by a click
                                                              outside the window, push "OK"}
END;

{ ---------------------------------------------------------------------- }

FUNCTION FQDlg { : DlgOption};
  {Runs the Queue Status dialog}
BEGIN
  PsNonPreemptive;
  GetPort(savePort);
  SetPort(@dlgPort);

  QSetup;                                         {get everything ready...}
  DlgMain;

  IF FCkfmHit (hckfmQ, muDoIt, iDoItYes)          {if dlg was dismissed with "OK"...}
  THEN FQDlg := eDlgOK
  ELSE FQDlg := eDlgCancel;

  SetPort(savePort);
  PsPreemptive;
END;

{ ---------------------------------------------------------------------- }
{$S     }
FUNCTION  ParDlgInit{:TF};
  {Should be called once by the parent process, when it begins to run.}
VAR DSPathname:     Pathname;
    cErr, i:        TC;
    DSAddr:         TL;
    hconnckfm:      THckfm;
BEGIN
  PsNonPreemptive;
  GetPort(savePort);
  OpenPort(@dlgPort);
  DSPathname := DSSegname;
  OPEN_DATASEG (cErr, DSPathname, DDSRefNum, DSAddr, ldsnDialog);
  SETACCESS_DATASEG (cErr, DDSRefNum, FALSE);                {read/write access}
  IF (cErr <= 0) THEN BEGIN
     pRgHCkFm := POINTER(DSAddr);
     hckfmList := POINTER(pRgHCkFm^[0]);
     hckfmQ := POINTER(pRgHCkFm^[1]);
     CkFmSetUp (hckfmList, TRUE {fInit}, []);
     CkFmSetUp (hckfmQ, TRUE {fInit}, []);
     maxCBtnList := cMaxList;
     maxCBtnQ := cMaxList;
     hconnckfm:=POINTER(pRgHckFm^[2]);
     WITH hconnckfm^^.rghmu[0]^^ DO BEGIN
       prsslot:=rgbtn[0].hsp^^;
       prsconnector:=rgbtn[1].hsp^^;
       prsdevice:=rgbtn[2].hsp^^;
       prsAserial:=rgbtn[3].hsp^^;
       prsBserial:=rgbtn[4].hsp^^;
       prsparallel:=rgbtn[5].hsp^^;
       prsnone:=rgbtn[6].hsp^^;
       ParDefTsp:=rgbtn[7].hsp^^;
     END;
     ParDlgInit:=TRUE;
  END
  ELSE BEGIN
     DDSRefNum:=PrNilRefnum;
     ParDlgInit:=FALSE;
  END;
  UNBIND_DATASEG(cErr,DDSRefNum);
  SetPort(savePort);
  PsPreemptive;
END;

{ ---------------------------------------------------------------------- }
{!BH 2/24/84}
{$S Finish}
PROCEDURE ParDlgQuit;
VAR
   cErr:            TC;
BEGIN
{Restore the cBtn fields to what they were before we messed them in MakeListBtn
 & MakeQBtn, then close the data seg.}
  IF (DDSRefNum <> PrNilRefnum) THEN BEGIN
    BIND_DATASEG(cErr,DDSRefNum);
    hckfmList^^.rgHMu[muPrinters]^^.cBtn := maxCBtnList;
    hckfmQ^^.rgHMu[muPrinters]^^.cBtn := maxCBtnQ;
    CLOSE_DATASEG (cErr, DDSRefNum);
  END;
END;

{ ---------------------------------------------------------------------- }

END.

ÿ