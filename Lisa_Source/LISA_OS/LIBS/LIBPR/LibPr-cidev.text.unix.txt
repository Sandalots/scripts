(*
$Exec
P?
-newdisk-intrinsic.lib
NewPr/CiDev
{no listing}
Temp
GTemp
NewPr/CiDev
$EndExec
*)
{               NOTE: The above compiles this program.                   }
{ ---------------------------------------------------------------------- }
{                Copyright 1983,1984 Apple Computer Inc.                 }
{ ---------------------------------------------------------------------- }
{  This is a bitmap interface to the dot matrix printer.
   Convention: The "Bit" direction is along the raster scan;
               The "Scan" direction is perpindicular to it.
               Thus horiz. resolution is in Bpi, vert. in Spi.
}
{ ---------------------------------------------------------------------- }

UNIT CiDev;
{ ---------------------------------------------------------------------- }
                               Interface
{ ---------------------------------------------------------------------- }
USES {$U LibOS/SysCall   }        SysCall,
     {$U LibPM/PMDecl    }        PMDecl,
     {$U LibSM/UnitStd   }        UnitStd,
     {$U LibSM/UnitHz    }        UnitHz,
     {$U LibQD/Storage   }        Storage,
     {$U LibQD/QuickDraw }        QuickDraw,
     {$U LibFM/FontMgr   }        FontMgr,
     {$U LibWM/Events    }        Events,
     {$U LibWM/Menus     }        Menus,
     {$U LibWM/Windows   }        Windows,
     {$U LibWM/Folders   }        Folders,
     {$U libSB/WmlStd    }        WmlStd,
     {$U libSB/WmlCrs    }        WmlCrs,
     {$U LibAM/AlertMgr  }        AlertMgr,
     {$U LibPr/PrStdInfo }        PrStdInfo,
     {$U LibPr/PrStdProcs}        PrStdProcs,
     {$U LibPr/Queues    }        Queues,
     {$U LibPr/PrEvents  }        PrEvents,
     {$U LibPr/PrDlgMgr  }        PrDlgMgr,
     {$U LibPr/PrMgr     }        PrMgr,
     {$U-}
     {$U LibPr/CiGlobals }        CiGlobals;

VAR
   charEuropean: SET OF 0..255;  {Chars that can be printed by switching to some European char set.}

{PROCS}

PROCEDURE CiDevClose;
PROCEDURE CiDevOpen (fFastGraf, fPEStop: Boolean);

(* ----- Device commands:*)
PROCEDURE CiSetBiDir  ( fBiDir : Boolean );   {Set bidirectional printing.}
PROCEDURE CiSetBpi    ( tybpi  : TTybpi  );   {Set the number of bits per inch}
PROCEDURE CiSetEmph   ( fEmph  : Boolean );   {Set emphasized printing.}
PROCEDURE CiSetLFFwd  ( fFwd   : Boolean );   {Set LF fwd or rev.}
PROCEDURE CiSetLineHt ( c144ths: Integer );   {Set LF size, in 144ths (max=99).}
PROCEDURE CiSetPEStop ( fPEStop: Boolean );   {Set Paper Empty to stop printing}
PROCEDURE CiSetUL     ( fUL    : Boolean );   {Set underlined printing.}
PROCEDURE CiSetWide   ( fWide  : Boolean );   {Set elongated printing.}

(* ----- These set internal state: there is no corresp. dev cmd.*)
PROCEDURE CiBindV;                            {Above are deferred; this forces move.}
PROCEDURE CiDeltaV    ( c144ths  : Integer ); {Rel. Move c144ths in vert. direction.}
PROCEDURE CiGotoV     ( c144ths  : Integer ); {Abs. Move c144ths in vert. direction.}
PROCEDURE CiNewPage;                          {Goes to the next page start.}
PROCEDURE CiSetFormLng( cPg144ths: Integer ); {Sets the page size in 144ths.}

(* ----- The Printing Procs:*)
PROCEDURE CiPrBand    ( p: TP; cBits, c144ths: Integer;
                        tybpi: TTybpi; fBitDbl, fBiDir: Boolean );
PROCEDURE CiPrBMHoriz ( pBM: QDPtr; cBits, cScans, cRowBytes: Integer;
                        tybpi: TTybpi; tyspi: TTyspi; fBitDbl, fBiDir: Boolean );
PROCEDURE CiPrBMVert  ( pBM: QDPtr; cBits, cScans, cRowBytes: Integer;
                        tybpi: TTybpi; tyspi: TTyspi; fBitDbl, fBiDir: Boolean );
PROCEDURE CiPrGraf    ( p: TP; cBits: Integer; fFastGraf: Boolean );
PROCEDURE CiPrTab     ( cPos: Integer );
PROCEDURE CiPrText    ( p: TP; cCount: Integer );

(* ----- Miscellaneous: *)
PROCEDURE CiDmpBuf;
FUNCTION  CiHRes    (tybpi: TTybpi{; fBitDbl: Boolean}): Integer;{= max bits in a scan.}
FUNCTION  CiMaxBits (tybpi: TTybpi; fBitDbl: Boolean): Integer;  {= max bits in a scan.}
PROCEDURE CiOut ( b: TB );

{ ---------------------------------------------------------------------- }
                               IMPLEMENTATION
{ ---------------------------------------------------------------------- }

{$S Ci}

{$IFC fSymOK} {$D+} {$ELSEC} {$D-} {$ENDC}
{$IFC fdbgok} {$R+} {$ELSEC} {$R-} {$ENDC}

CONST
   (* ----- Convenient non-characters. *)
   LF = 10; SO = 14; SI = 15; CAN = 24; ESC = 27;

   (* ----- Command characters; generally preceded by ESC:*)
   cmVPitch = 'T';

   cCmStdBM = 'G'; cCmFastBM= 'g';
   cmTab    = 'F'; cmRun    = 'V';
   cmFwdLF  = 'f'; cmRevLF  = 'r';
   cmUniDir = '>'; cmBiDir  = '<';
   cmPEStop = 'o'; cmPEGo   = 'O';    {Paper Empty Over-ride.}
   cmEmphOn = '!'; cmEmphOff= '"';    {Horiz. 1/160th smear.}
   cmWideOn =  SO; cmWideOff=  SI;    {Horiz. bit dbl.; DOES NOT USE "ESC"!!}
   cmULOn   = 'X'; cmULOff  = 'Y';

   cm72Res  = 'n'; cm80Res  = 'N'; cm96Res  = 'E';
   cm120Res = 'q'; cm136Res = 'Q'; cm144Res = 'p'; cm160Res = 'P';

   cBufMax       = 511{20};     {Command buffer max.}
   cLFMax        = 99;          {Max Line Feed, in 144ths}
   cBandMax      = c160Bits;    {Max no. of bytes in a scan=c160Bits.}
   cBurp144ths   = 24;          {Amount to "Burp" the printer for gear lash}
   cmCloseSwitch = 'D';         {Close DIP switches; takes two OR-mask bytes}
   cmOpenSwitch  = 'Z';         {Open DIP switches; takes two OR-mask bytes}
   cmReset       = 'c';         {reset command}

   NoCountry= -1;
   US       =  0;  {code for national char set on DMP} {DC 5/11/83}
   Italian  =  1;  {code for national char set on DMP} {DC 5/11/83}
   UK       =  3;  {code for national char set on DMP} {DC 5/11/83}
   German   =  4;  {code for national char set on DMP} {DC 5/11/83}
   Swedish  =  5;  {code for national char set on DMP} {DC 5/11/83}
   French   =  6;  {code for national char set on DMP} {DC 5/11/83}
   Spanish  =  7;  {code for national char set on DMP} {DC 5/11/83}

TYPE
   TCiDevRec = RECORD                        {The Ci state record:}
       cAct144ths : Integer;                 {The actual V location.}
       cCur144ths : Integer;                 {The current V location (deferred motion).}
       cPg144ths  : Integer;                 {The number of scans on a page.}
       cBuf       : Integer;                 {The cmd char buffer and count.}
       rgbBuf     : ARRAY[0..cBufMax] OF TB;
       fFastBM    : Boolean;                 {TRUE if the fast graf cmd available.}
   END;

VAR
   CiDevRec: TCiDevRec;

{ ---------------------------------------------------------------------- }

(* ----- DIRECTORY of external procedures. *)
Function  CiAlert   ( AlertFile:TAlertFile;AlertNumber:Integer;AlertKind:TAlertKind):Integer; EXTERNAL;
PROCEDURE PrHBand(pIn: QDPtr; cRowDelta: Integer; pOut: TP; cRowBytes: Integer);              EXTERNAL;
PROCEDURE PrVBand(pIn: QDPtr; cRowBytes, cScans: Integer; pOut1, pOut2: TP; fHiRes: Boolean); EXTERNAL;

(* ----- DIRECTORY of private forwardly declared procedures. *)
PROCEDURE CiBlockOut    ( p: TP; cBytes: Integer );  FORWARD;
PROCEDURE CiDigits      ( cData, cDigits: Integer ); FORWARD;
PROCEDURE CiEuropeanOut ( ch: CHAR );                FORWARD;
PROCEDURE CiSetCntry    ( cntry: INTEGER );          FORWARD;

{ ---------------------------------------------------------------------- }
PROCEDURE CiBindV {};
(* ----- This executes the deferred v location command. It should be the only place in the package *)
(* ----- emitting LF-s.*)
VAR c144Left: Integer;
    fReverse: Boolean;
BEGIN
   WITH CiDevRec DO c144Left  := cCur144ths-cAct144ths;
   IF c144Left <> 0 THEN BEGIN
      fReverse := (c144Left < 0);
      IF fReverse THEN c144Left := -c144Left + cBurp144ths;
      CiSetLFFwd  ( NOT fReverse );

      IF c144Left > cLFMax THEN BEGIN
         CiSetLineHt ( cLFMax );
         WHILE c144Left > cLFMax DO BEGIN
            CiOut (LF);
            c144Left := c144Left-cLFMax;
         END;
      END;
      CiSetLineHt ( c144Left );
      CiOut (LF);

      WITH CiDevRec DO BEGIN
         cAct144ths := cCur144ths MOD cPg144ths;
         cCur144ths := cAct144ths;
      END;

      IF fReverse THEN BEGIN { "Burp" the printer to fix the gear lash prob.}
         CiSetLFFwd ( TRUE );
         CiSetLineHt ( cBurp144ths );
         CiOut (LF);
      END;  { If fReverse }
   END; {With CiDevRec DO}
END;
{ ---------------------------------------------------------------------- }
PROCEDURE CiBlockOut { p: TP; cBytes: Integer };
(* -----  This is the only I/O for the Ci. *)

Const Null = 0;

Type TBuffer  = array[1..c160Bits] of T8{.PrStdInfo};
     TpBuffer = ^TBuffer;
     ThBuffer = ^TpBuffer;

Var AlertNum,I,
    AlertResult,
    Error        :Integer;
    hCancelBuffer: ThBuffer;

BEGIN
     Error:=0;
     IF (cBytes > 0) THEN PsOutPort( Error,p, cBytes );

     If  ( (Error>0) and Not(CiAbort) ) then Begin
       Repeat
         (* ----- Put up alert if alert has not already been put up. Cancel buffer *)
           AlertResult:=CiAlert(BgdAlertFile,CiDissconnect,CautionProc);
           hCancelBuffer:=Pointer(Ord(NewHandle(c160Bits)));
           For I:=1 to c160Bits do hCancelBuffer^^[I]:=Null;
           PsOutPort( Error,Pointer(Ord(hCancelBuffer^)), c160Bits );
           DisposeHandle(Pointer(Ord(hCancelBuffer)));
           If (AlertResult=0) then CiAbort:=True;
           If (AlertResult=1) then PsOutPort( Error,p, cBytes );
       Until ( (AlertResult=0{Cancel})or(Error=0{No error in PsOutPort}) );
     End;
END;
{ ---------------------------------------------------------------------- }
PROCEDURE CiDeltaV { c144ths: Integer };
BEGIN
   WITH CiDevRec DO cCur144ths := cCur144ths + c144ths;
END;

{ ---------------------------------------------------------------------- }
PROCEDURE CiDevClose;
VAR cError: Integer;
    chCmd : Char;
BEGIN
   CiBindV;

   CiSetLFFwd  ( TRUE );
   CiSetLineHt ( 144 DIV 6 );  {Leave printer in 6 lines per inch.}

   (* --- Reset printer to power on state. Note: This reset comand should be the last                *)
   (* --- command sent to the printer! When the printer recieves the reset command, it drops         *)
   (* --- DTR. There appears to be a bug in the RS 232 Driver code that sends 1 or 2 characters even *)
   (* --- though DTR is low. This loss of one or two characters will cause the printer to sometimes  *)
   (* --- print an escape code instead of invoking it because the Escape character was one of        *)
   (* --- the characters  lost. *)

   CiOut (ESC);
   chCmd:=cmReset;
   CiOut ( ORD (chCmd) );

   (* --- Force the data to be sent, *)
   CiDmpBuf;
END;
{ ---------------------------------------------------------------------- }
PROCEDURE CiDevOpen {fFastGraf, fPEStop: Boolean};
Var I : Integer;
BEGIN
   WITH CiDevRec DO BEGIN
       cBuf      := 0;
       cCur144ths:= cCiGap144ths;
       cAct144ths:= cCiGap144ths;
       fFastBM   := fFastGraf;
   END;

   (* ----- Send CAN instruction to terminate a graphics command that was prematurely terminated. *)
   For I:=0 to c160Bits do CiOut ( CAN );

   charEuropean := [$80, $81, $84..$86, $88, $8A, $8C..$8F, $93, $96, $98, $9A, $9D, $9F, $A1,
                    $A3, $A4, $A7, $AC, $C0, $C1];
   CiSetFormLng( 144*11 );      {Assume 11" paper.}
   CiSetBpi    ( tybpi96 );     { " 96 bpi.}
   CiSetLineHt ( 16 );          { " Graphics Printing.}
   CiSetLFFwd  ( TRUE );        { " Forward Printing.}
   CiSetBiDir  ( FALSE );       { " Graphics Printing.}
   CiSetWide   ( FALSE );       { " Not Wide.}
   CiSetEmph   ( FALSE );       { " Not Emph.}
   CiSetUL     ( FALSE );       { " Not Underlined.}
   CiSetPEStop ( fPEStop );
END;
{ ---------------------------------------------------------------------- }
PROCEDURE CiDigits { cData, cDigits };
LABEL 1,2,3,4;
BEGIN
   CASE cDigits OF
      1: GOTO 1;
      2: GOTO 2;
      3: GOTO 3;
      4: GOTO 4;
   END;
4: CiOut ( ORD ('0') + ( (cData MOD 10000) DIV 1000) );
3: CiOut ( ORD ('0') + ( (cData MOD 1000) DIV 100) );
2: CiOut ( ORD ('0') + ( (cData MOD 100) DIV 10) );
1: CiOut ( ORD ('0') + ( (cData MOD 10){DIV 1}) );
END;
{ ---------------------------------------------------------------------- }
PROCEDURE CiDmpBuf {};
BEGIN
  WITH CiDevRec DO BEGIN
     CiBlockOut ( @rgbBuf, cBuf );
     cBuf := 0;
  END;
END;
{ ---------------------------------------------------------------------- }
PROCEDURE CiEuropeanOut { ch: CHAR };
Var Country       :Integer;
    SpecialChar   :TB;
BEGIN
  WITH CiDevRec DO BEGIN
    Country:=NoCountry{.CiDev};
    CASE ord(ch) OF
      $80: BEGIN Country:=German;  SpecialChar:=$5B; END;  {A umlaut}
      $81: BEGIN Country:=Swedish; SpecialChar:=$5D; END;  {A with circle}
      $84: BEGIN Country:=Spanish; SpecialChar:=$5C; END;  {N tilde}
      $85: BEGIN Country:=German;  SpecialChar:=$5C; END;  {O umlaut}
      $86: BEGIN Country:=German;  SpecialChar:=$5D; END;  {U umlaut}
      $88: BEGIN Country:=Italian; SpecialChar:=$7B; END;  {a grave}
      $8A: BEGIN Country:=German;  SpecialChar:=$7B; END;  {a umlaut}
      $8C: BEGIN Country:=Swedish; SpecialChar:=$7D; END;  {a with circle}
      $8D: BEGIN Country:=Italian; SpecialChar:=$5C; END;  {c cedilla}
      $8E: BEGIN Country:=Italian; SpecialChar:=$5D; END;  {e acute}
      $8F: BEGIN Country:=Italian; SpecialChar:=$7D; END;  {e grave}
      $93: BEGIN Country:=Italian; SpecialChar:=$7E; END;  {i grave}
      $96: BEGIN Country:=Spanish; SpecialChar:=$7C; END;  {n tilde}
      $98: BEGIN Country:=Italian; SpecialChar:=$7C; END;  {o grave}
      $9A: BEGIN Country:=German;  SpecialChar:=$7C; END;  {o umlaut}
      $9D: BEGIN Country:=Italian; SpecialChar:=$60; END;  {u grave}
      $9F: BEGIN Country:=German;  SpecialChar:=$7D; END;  {u umlaut}
      $A1: BEGIN Country:=Italian; SpecialChar:=$5B; END;  {degree sign}
      $A3: BEGIN Country:=Italian; SpecialChar:=$23; END;  {pound sign}
      $A4: BEGIN Country:=Italian; SpecialChar:=$40; END;  {section mark}
      $A7: BEGIN Country:=German;  SpecialChar:=$7E; END;  {German double-s}
      $AC: BEGIN Country:=French;  SpecialChar:=$7E; END;  {umlaut alone}
      $C0: BEGIN Country:=Spanish; SpecialChar:=$5D; END;  {inverted ?}
      $C1: BEGIN Country:=Spanish; SpecialChar:=$5B; END;  {inverted !}
    END;
    If (Country<>NoCountry) then Begin
      CiSetCntry(Country);
      CiOut(SpecialChar);
    End;
  END;
  CiSetCntry(US);
END;
{ ---------------------------------------------------------------------- }
PROCEDURE CiGotoV { c144ths: Integer };
BEGIN
   CiDevRec.cCur144ths := c144ths;
END;
{ ---------------------------------------------------------------------- }
FUNCTION  CiHRes {tybpi: TTybpi): Integer};
VAR cBpi: Integer;
BEGIN
   CASE tybpi OF
      tybpi72:  cBpi := 72;
      tybpi80:  cBpi := 80;
      tybpi96:  cBpi := 96;
      tybpi120: cBpi := 120;
      tybpi136: cBpi := 136;
      tybpi144: cBpi := 144;
      tybpi160: cBpi := 160;
   END;
   CiHRes := cBpi;
END;
{ ---------------------------------------------------------------------- }
FUNCTION  CiMaxBits  { tybpi: TTybpi; fBitDbl: Boolean ): Integer};
VAR cMaxBits: Integer;
BEGIN
   cMaxBits := 8*CiHRes(tybpi);
   (* ----- Adj. for fBitDbl.*)
   IF fBitDbl THEN cMaxBits := cMaxBits DIV 2;
   CiMaxBits := cMaxBits;
END;
{ ---------------------------------------------------------------------- }
PROCEDURE CiNewPage {};
BEGIN
  WITH CiDevRec DO BEGIN
     CiGotoV (cPg144ths+cCiGap144ths);
     CiBindV;
     CiDmpBuf;
  END;

  (* ----- Burp the printer to fix the page start overprint problem. *)
  CiSetLFFwd(False);
  CiSetLineHt(8);
  CiOut(LF);
  CiSetLFFwd(True);
  CiOut(LF);
  CiDmpBuf;

END;
{ ---------------------------------------------------------------------- }
PROCEDURE CiOut { b: TB };
BEGIN
  WITH CiDevRec DO BEGIN
     rgbBuf [ cBuf ] := b;
     cBuf := cBuf + 1;
     IF cBuf > cBufMax THEN CiDmpBuf;
  END;
END;
{ ---------------------------------------------------------------------- }
PROCEDURE CiPrBand { p: TP; cBits, c144ths: Integer; tybpi: TTybpi; fBitDbl, fBiDir: Boolean };
(* ----- Print a band of 8 scanlines of cBits per scan. cBits should be a multiple of 8 *)
(* ----- for the fast graf command to work. *)
VAR p1: TP;
    cMarg, cError: Integer;
    fFast: Boolean;
BEGIN
    IF (cPrError = prErrAbort)  THEN EXIT(CiPrBand);

   cBits := CMIN ( cBits, CiMaxBits(tybpi, fBitDbl) );
   cMarg := 0;
   fFast := CiDevRec.fFastBM;

   (* ----- Trim the white bits off on the LEFT.*)
   p1 := POINTER ( ORD(p)+cBits-1 );
   WHILE (cBits > 0) AND (p1^ = 0) DO BEGIN
      cBits := cBits - 1;
      p1 := POINTER ( ORD(p1) - 1 );
   END;

   (* ----- Trim the white bits off on the RIGHT.*)
   IF (cBits > 0) THEN BEGIN
      p1 := POINTER ( ORD(p) );
      WHILE  (p1^ = 0) DO  p1 := POINTER ( ORD(p1) + 1 );
      cMarg := ORD(p1) - ORD(p);
   END;

   IF cBits > 0 THEN BEGIN
      (* ----- Reset entire state: insures resetting printer does not lose synch!*)
      CiSetBpi    ( tybpi   );
      CiSetWide   ( fBitDbl );
      CiSetBiDir  ( fBiDir );

      IF fFast THEN BEGIN
         cBits := ((cBits+7) DIV 8)*8; {Round up}
         cMarg := (cMarg DIV 8)*8;     {Round down}
      END;

      IF cMarg > 0 THEN CiPrTab  (cMarg);

      p     := POINTER ( ORD(p) + cMarg );
      cBits := cBits - cMarg;
      CiPrGraf (p, cBits, fFast);
   END
   ELSE BEGIN
      PsYieldCpu;
   END;

   CiDeltaV(c144ths);
END;
{ ---------------------------------------------------------------------- }
PROCEDURE CiPrBMHoriz { pBM: QDPtr; cBits, cScans, cRowBytes: Integer;
                        tybpi: TTybpi; tyspi: TTyspi; fBitDbl, fBiDir: Boolean };
VAR iCol, cCol: Integer;
    rgb1, rgb2: ARRAY [0..cBandMax] OF TB;
BEGIN

   cCol    := (cBits+7) DIV 8;
   cCol    := (cCol DIV 2); {Wordize cols. for Interlace, i.e. cCol assummed Even}
   cScans  := CMIN (cScans, cBandMax);

   FOR iCol := 1 TO cCol DO BEGIN
      IF tyspi = tyspi144 THEN BEGIN
         PrVBand  ( pBM, cRowBytes, cScans, @rgb1, @rgb2, TRUE );
         CiPrBand ( @rgb1, cScans,  1, tybpi, fBitDbl, fBiDir );
         CiPrBand ( @rgb2, cScans, 15, tybpi, fBitDbl, fBiDir );
      END
      ELSE BEGIN
         PrVBand  ( pBM, cRowBytes, cScans, @rgb1, @rgb2, FALSE );
         CiPrBand ( @rgb1, cScans, 16, tybpi, fBitDbl, fBiDir );
         CiPrBand ( @rgb2, cScans, 16, tybpi, fBitDbl, fBiDir );
      END;
      pBM := POINTER ( ORD (pBM) + 2 );
   END;
   CiDmpBuf;  {..to insure the last band prints immediately.}
END;
{ ---------------------------------------------------------------------- }
PROCEDURE CiPrBMVert  { pBM: QDPtr; cBits, cScans, cRowBytes: Integer;
                        tybpi: TTybpi; tyspi: TTyspi; fBitDbl, fBiDir: Boolean };
VAR iBand, cBands, dv: Integer;
    rgb: ARRAY [0..cBandMax] OF TB;
BEGIN
   cBits    := CMIN (cBits, cBandMax);
   cBands   := (cScans+7) DIV 8;

   FOR iBand := 1 TO cBands DO BEGIN
      IF tyspi = tyspi144 THEN BEGIN
         IF (iBand MOD 2) = 1 THEN dv := 1 ELSE dv := 15;
         PrHBand  ( pBM, 2*cRowBytes, @rgb, cRowBytes );
         CiPrBand ( @rgb, cBits,  dv, tybpi, fBitDbl, fBiDir );
         pBM := POINTER ( ORD (pBM) + cRowBytes*dv );
      END
      ELSE BEGIN
         PrHBand  ( pBM, cRowBytes, @rgb, cRowBytes );
         CiPrBand ( @rgb, cBits, 16, tybpi, fBitDbl, fBiDir );
         pBM := POINTER ( ORD (pBM) + (cRowBytes*8) );
      END;
   END;

   CiDmpBuf;  {..to insure the last band prints immediately.}
END;
{ ---------------------------------------------------------------------- }
PROCEDURE CiPrGraf  { p: TP; cBits: Integer; fFastGraf: Boolean };
(* ----- NOTE: if fFastGraf then iBits must be a multiple of 8 bytes. *)
VAR chCmd: CHAR;
    cNumber, cDigits: Integer;
BEGIN
   IF fFastGraf THEN BEGIN
      chCmd    := cCmFastBM;
      cNumber  := cBits DIV 8;
      cDigits  := 3;
   END
   ELSE BEGIN
      chCmd    := cCmStdBM;
      cNumber  := cBits;
      cDigits  := 4;
   END;

   CiBindV;
   CiOut (ESC);
   CiOut ( ORD (chCmd) );
   CiDigits (cNumber, cDigits);
   CiDmpBuf;
   CiBlockOut ( p, cBits );
END;
{ ---------------------------------------------------------------------- }
PROCEDURE CiPrTab  { cPos: Integer };
BEGIN
   CiBindV;
   CiOut (ESC);
   CiOut ( ORD (cmTab) );
   CiDigits (cPos, 4);
END;
{ ---------------------------------------------------------------------- }
PROCEDURE CiPrText  { p: TP; cCount: Integer };
VAR i: Integer;
    hackarooney: PACKED RECORD CASE INTEGER OF
                   0:  (foo, signedByte: TB);
                   1:  (bar, character:  CHAR);
                 END;
BEGIN
   IF cCount>0 THEN BEGIN
      CiBindV;
      FOR i := 1 TO cCount DO BEGIN
        WITH hackarooney DO BEGIN
          signedByte := p^;
          IF ord(character) IN charEuropean THEN CiEuropeanOut(character)
          ELSE CiOut (signedByte);
        END;
        p := POINTER( ORD(p) + 1 );
      END;
   END;
END;
{ ---------------------------------------------------------------------- }
PROCEDURE CiSetBiDir { fBiDir: Boolean };
BEGIN
   CiOut (ESC);
   IF fBiDir THEN CiOut ( ORD(cmBiDir)  )
   ELSE           CiOut ( ORD(cmUniDir) );
END;
{ ---------------------------------------------------------------------- }
PROCEDURE CiSetBpi { tybpi: TTybpi };
VAR cmRes: CHAR;
BEGIN
   CiOut (ESC);
   CASE tybpi OF
      tybpi72:  cmRes := cm72Res;
      tybpi80:  cmRes := cm80Res;
      tybpi96:  cmRes := cm96Res;
      tybpi120: cmRes := cm120Res;
      tybpi136: cmRes := cm136Res;
      tybpi144: cmRes := cm144Res;
      tybpi160: cmRes := cm160Res;
   END;
   CiOut ( ORD (cmRes) );
END;
{ ---------------------------------------------------------------------- }
PROCEDURE CiSetCntry {cntry: INTEGER};
BEGIN
  ciOut(ESC);
  ciOut(ord(cmOpenSwitch));
  ciOut(7);    {open switch 1 bits 0, 1, 2; this happens to be US setting}
  ciOut(0);    {do nothing to switch 2}

  IF cntry<>US THEN BEGIN
    ciOut(ESC);
    ciOut(ord(cmCloseSwitch));
    ciOut(cntry);       {set the country code in switch 1 bits 0, 1, 2}
    ciOut(0);           {do nothing to switch 2}
  END;
END;
{ ---------------------------------------------------------------------- }
PROCEDURE CiSetEmph { fEmph: Boolean };
BEGIN
   CiOut (ESC);
   IF fEmph THEN CiOut ( ORD(cmEmphOn)  )
   ELSE          CiOut ( ORD(cmEmphOff) );
END;
{ ---------------------------------------------------------------------- }
PROCEDURE CiSetFormLng { cPg144ths: Integer };
BEGIN
   CiDevRec.cPg144ths := cPg144ths;
END;
{ ---------------------------------------------------------------------- }
PROCEDURE CiSetLFFwd  { fFwd: Boolean };
BEGIN
   CiOut (ESC);
   IF fFwd
      THEN CiOut ( ORD(cmFwdLF) )
      ELSE CiOut ( ORD(cmRevLF) );
END;
{ ---------------------------------------------------------------------- }
PROCEDURE CiSetLineHt { c144ths: Integer };
(* -----  Sets the LF height, in half dots. NOTE: c144ths CAN'T be more than 99!! *)
BEGIN
   c144ths := CMIN ( cLFMax, c144ths );
   CiOut (ESC);
   CiOut ( ORD (cmVPitch) );
   CiDigits (c144ths, 2);
END;
{ ---------------------------------------------------------------------- }
PROCEDURE CiSetPEStop { fPEStop: Boolean };   {Set Paper Empty to stop printing}
BEGIN
   CiOut (ESC);
   IF fPEStop THEN CiOut ( ORD(cmPEStop) )
              ELSE CiOut ( ORD(cmPEGo)   );
END;
{ ---------------------------------------------------------------------- }
PROCEDURE CiSetUL { fUL: Boolean };
BEGIN
   CiOut (ESC);
   IF fUL THEN CiOut ( ORD (cmULOn)  )
          ELSE CiOut ( ORD (cmULOff) );
END;
{ ---------------------------------------------------------------------- }
PROCEDURE CiSetWide { fWide: Boolean };
BEGIN
  IF fWide THEN CiOut ( cmWideOn )
           ELSE CiOut ( cmWideOff );
END;
{ ---------------------------------------------------------------------- }
END.
(*
 ----------------------------------------------------------------------
         Changes:  added cCiGap144ths as interface constant.
                   added CiGotoV (c144ths) and changed CiDeltaV
                   added CiBindV;
                   cTot144ths --> cAct(ual)144ths & cCur144ths
                   inserted calls to CiBindV to printing & init procs.

 ----------------------------------------------------------------------
*)
{***************************SUMMARY OF CIDEV CHANGES FOR INTERNATIONAL VERSION*********************

 All these changes have to do with making draft printing handle non-USASCII characters.  We assume
 that all characters to be draft-printed by CiDev are ones that can in fact be printed on the
 C.Itoh; CiProcs guarantees us this.

   0. fPrEnhanceDraft flag controls these changes.  Set equal to fPrInternational (from PrStd).
   1. Set variable charEuropean added; Codes of chars that can only be printed by switching to some
      European char set.  Initialized in CiDevOpen.
   2. Constants cmCloseSwitch & cmOpenSwitch added (commands to C.Itoh).  Constants US, Italian, UK,
      German, Swedish, French, Spanish added; used in switching contries on C.Itoh.
   3. CiSetCntry added to set country in C.Itoh.
   4. CiEuropeanOut proc added.  Prints a European char by switching country, sending char code,
      then switches back to US.  Called from CiPrText if char code is in charEuropean.

***************************************************************************************************}

{
UNIT CiDev CHANGE LOG:
     10/25/83  EZ: Changed wordptr to QDPtr in CiPrBmVert, CiPrBMHoriz, PrVBand, PrHBand.
      7/08/83  DC: Changed names of zCiEuropeanOut and zCiSetCntry to CiEuropeanOut and CiSetCntry
      7/07/83  DC: Moved charEuropean into interface -- now referenced in CiProcs.
      6/24/83  DC: Replaced fPrEurope with fPrInternational
      5/18/83  DC: New compiler flag fPrEnhanceDraft so draft printing of special
                   chars can be made independent of European version.
      5/13/83  DC: Remove TCntry, declare INTEGER constants instead.
                   zCiSetCntry: change parameter to INTEGER.
      4/29/83  DC: The following mods are conditional, based on fPrEurope:
                     add cmCloseSwitch, cmOpenSwitch constants.
                     add TCntry enumerated type.
                     add charEuropean variable.
                     add zCiSetCntry proc.
                     add zCiEuropeanOut proc.
                     CiDevOpen: init charEuropean.
                     CiPrText: alias hack, and code to call zCiEuropeanOut.
      4/25/83  BH: cCiGap144ths changed from 72 to 80 (.5 to ~.6 inches).
      4/06/83  EZ: CiDevOpen: added CAN loop.
      1/25/82  OD: Protect hCiDev via NIL.
}

ÿ