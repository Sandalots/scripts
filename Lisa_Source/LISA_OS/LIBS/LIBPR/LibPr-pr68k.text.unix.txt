;---------------------------------------------------------------
;                Copyright 1983,1984, Apple Computer Inc.
;---------------------------------------------------------------

        .seg    'Pr'    ;The segment into which this stuff maps

        .PROC   PrVBand,6
; PROCEDURE PrVBand(pIn: TP; cRowBytes, cScans: TC; pOut1, pOut2: TP; fHiRes: TF);
;       Collect 2 vertical Lo-Res bands for the CI Printer.
;---------------------------------------------------------------
;       A6 offsets of parameters after link.
cbParam         .EQU    4+2+2+4+4+2
result          .EQU    cbParam+8       ;None!
pIn             .EQU    result-4        ;TP
cRowBytes       .EQU    pIn-2           ;TC
cScans          .EQU    cRowBytes-2     ;TC
pOut1           .EQU    cScans-4        ;TP
pOut2           .EQU    pOut1-4         ;TP
fHiRes          .EQU    pOut2-2         ;TF
;       A6 offsets of locals
cbVar           .EQU    0               ;no locals

entry   LINK    A6,#cbVar               ;
        MOVEM.L D0-D7/A1-A5, -(SP)      ;save regs
;
;  Setup input params:
;       D0: cRowBytes   D5: (cScans-1)  D6: fHiRes
;       A0: pInAdj      A1: pOut1       A2: pOut2
;
        MOVE.L  pIn(A6), A0             ;get pIn
        MOVE.L  pOut1(A6), A1           ;get pOut1
        MOVE.L  pOut2(A6), A2           ;get pOut2
        MOVE    cRowBytes(A6), D0       ;get cRowBytes
        EXT.L   D0                      ;make long for add to A Reg
        MOVE    cScans(A6), D5          ;get cScans
        SUBQ    #1, D5                  ;  cScans->cScans-1: For DBF & pInAdj calc!
        MOVE    fHiRes(A6), D6          ;get fHiRes

        MOVE    D5, D2                  ;Adj pIn to bottom of bitmap:
        MULU    D0, D2                  ;  pIn := pIn + cRowBytes*(cScans-1)
        ADD.L   D2, A0                  ;


;
; For each scanline, extract two bytes depending on fHiRes
;
;       D0: cRowBytes   D1: outByte1    D2: outByte2    D3: inWord
;       D4: bit count   D5: (cScans-1)- D6: fHiRes
;       A0: pInAdj      A1: (pOut1)+    A2: (pOut2)+
;

NxtScan MOVE    (A0), D3                ;get inWord
        BNE.S   NotZero                 ;optimize: 0 => store 0's!
        MOVE.B  D3, (A1)+               ;store 0 at & bump pOut1
        MOVE.B  D3, (A2)+               ;store 0 at & bump pOut2
        BRA.S   ScanEnd                 ;skip processing

NotZero TST     D6                      ;HiRes?
        BEQ.S   LoRes                   ;No, go to LoRes extract
;
;  HiRes bit extract:
;       inWord:[0 1 .. 14 15]   ->  outBytes: b2:[15 13 .. 1] & b1:[14 12 .. 0]
;
        MOVEQ   #7, D4                  ;Set up bit count
NxtBits LSR     #1, D3                  ;shift odd bit from inWord
        ADDX    D2, D2                  ;hack: shift in x bit
        LSR     #1, D3                  ;shift even bit from inWord
        ADDX    D1, D1                  ;hack: shift in x bit
        DBF     D4, NxtBits             ;Loop 8 bits
        BRA.S   StoreBs                 ;go to store bytes.
;
;  LoRes bit extract:
;       inWord: [0..7 8..15]   ->  outBytes: b2:[15..8]  &  b1:[7..0]
;
LoRes   MOVEQ   #7, D4                  ;Set up second byte's bit count
NxtBit2 LSR     #1, D3                  ;shift out a bit from inWord
        ADDX    D2, D2                  ;hack: shift in x bit
        DBF     D4, NxtBit2             ;Loop 8 bits

        MOVEQ   #7, D4                  ;Set up first byte's bit count
NxtBit1 LSR     #1, D3                  ;shift out a bit from inWord
        ADDX    D1, D1                  ;hack: shift in x bit
        DBF     D4, NxtBit1             ;Loop 8 bits

StoreBs MOVE.B  D1, (A1)+               ;store b1 & bump pOut1
        MOVE.B  D2, (A2)+               ;store b2 & bump pOut2

ScanEnd SUB.L   D0, A0                  ;adj pIn to prev. scanline
        DBF     D5, NxtScan             ;Loop thru scans


Done    MOVEM.L (SP)+, D0-D7/A1-A5      ;restore regs
        UNLK    A6                      ;
        MOVE.L  (SP)+, A0               ;pop rtn adr
        ADD.L   #cbParam, SP            ;strip params
        JMP     (A0)                    ;return
        .ASCII  'PrVBand '              ;Proc name for debugger

;---------------------------------------------------------------
        .PROC   PrHBand,4
; PROCEDURE PrHBand(pIn: TP; cRowDelta: TC; pOut: TP; cRowBytes: TC);
;       Collect a horiz band for the CI Printer.
;       Note that cRowBytes may not equal cRowDelta in hi res: there cRowDelta is set
;          to twice cRowBytes to skip alternate scans!
;---------------------------------------------------------------
;       A6 offsets of parameters after link.
cbParam         .EQU    4+2+4+2
result          .EQU    cbParam+8       ;None!
pIn             .EQU    result-4        ;TP
cRowDelta       .EQU    pIn-2           ;TC
pOut            .EQU    cRowDelta-4     ;TP
cRowBytes       .EQU    pOut-2          ;TC
;       A6 offsets of locals
cbVar           .EQU    0               ;no locals

entry   LINK    A6,#cbVar               ;
        MOVEM.L D0-D7/A1-A5, -(SP)      ;save regs
;
;  Setup input params:
;       D0: cRowDelta   D1: cRowBytes
;       A0: pIn         A1: pOut
;
        MOVE.L  pIn(A6), A0             ;get ptr to input data
        MOVE.L  pOut(A6), A1            ;get ptr to output data
        MOVE    cRowDelta(A6), D0       ;get cRowDelta
        EXT.L   D0                      ;make long for add to A0!
        MOVE    cRowBytes(A6), D1       ;get cRowBytes
;
;  For each of the cRowBytes collect 8 "vertical" bytes and store at pOut
;       D0: cRowDelta   D1: cRowBytes   D2: row count   D3: result      D4: bit addr
;       A0: pIn         A1: pOut        A2: pIn temp
;  D3 will has 2 bytes:
;       HI: Shifted return data
;       LO: Used for condx codes
;
        SUBQ    #1,D1                   ;fix cRowBytes for DBF
Nxt8x8  MOVEQ   #7, D4                  ;Set up bit addr
NxtBit  MOVE.L  A0, A2                  ;A2 starts at pIn
        MOVEQ   #7, D2                  ;Set up row count

NxtRow  BTST    D4, (A2)                ;Test the bit
        SNE     D3                      ;Store in low byte
        ROR     #1, D3                  ;Shift into result
        ADD.L   D0, A2                  ;Bump pIn to next scanline
        DBF     D2, NxtRow              ;Loop 8 rows

        LSR     #8, D3                  ;get result byte
        MOVE.B  D3, (A1)+               ;Store result
        DBF     D4, NxtBit              ;Loop 8 bits

        ADDQ.L  #1, A0                  ;bump pIn to next byte
        DBF     D1, Nxt8x8              ;Loop thru cRowBytes 8x8 chunks


Done    MOVEM.L (SP)+, D0-D7/A1-A5      ;restore regs
        UNLK    A6                      ;
        MOVE.L  (SP)+, A0               ;pop rtn adr
        ADD.L   #cbParam, SP            ;strip params
        JMP     (A0)                    ;return
        .ASCII  'PrHBand '              ;Proc name for debugger

;---------------------------------------------------------------
        .PROC   ClrBitmap,1
;       PROCEDURE ClrBitmap(pBitmap: TP ); EXTERNAL;
;       Clears out the bitmap pointed to by pBitmap
;---------------------------------------------------------------
        MOVE.L  (A7)+,A0                ; get return address
        MOVE.L  A2,D2                   ; save A2

        MOVE.L  (A7)+,A1                ; get bitmap address parameter
        MOVE.L  0(A1),A2                ; get base address
        MOVE    4(A1),D0                ; get the rowbytes
        MOVE    10(A1),D1               ; get bottom of bounds
        SUB     6(A1),D1                ; subtract top
        MULU    D0,D1                   ; calculate total # bytes
        BEQ.S   ADIOS                   ; skip if no bits

CB1     CLR     (A2)+                   ; clear the word
        SUBQ.L  #2,D1                   ; decrement (2 bytes per word)
        BGT.S   CB1

        MOVE.L  D2,A2                   ; restore A2
ADIOS   JMP     (A0)

;---------------------------------------------------------------
        .FUNC   fClrBits,2
;       FUNCTION  fClrBitmap(pBitmap: TP; rCheck: Rect ): TF; EXTERNAL;
;       Checks if the bitmap pointed to by pBitmap is clear
;---------------------------------------------------------------
        LINK    A6,#0
        MOVEM.L D0-D7/A1-A5, -(SP)      ;save regs

        MOVE.L  8(A6),A1                ; get rCheck parameter
        MOVE    4(A1),D1                ; get bottom of rCheck
        SUB     (A1),D1                 ; subtract top, D1 contains delta V
        BEQ.S   ADIOS                   ; if no scanlines, assume good
        SUBQ    #1,D1                   ; -1 for DBF

        MOVE    6(A1),D2                ; get right of rCheck
        SUB     2(A1),D2                ; subtract left, D2 contains delta H in bits
        BEQ.S   ADIOS                   ; if no bits, assume good
        LSR     #3,D2                   ; make it bytes
        SUBQ    #1,D2                   ; -1 for DBF

        MOVE.L  12(A6),A1               ; get bitmap address parameter
        MOVE.L  0(A1),A2                ; get base address into A2
        MOVE    4(A1),D0                ; get the rowbytes

LOOP1   MOVE    D2,D3                   ; D3(i):=checkbytes-1
LOOP2   TST.B   0(A2,D3)                ; test a byte in the bitmap
        BNE.S   SHUCKS                  ; failed because a non zero byte was found
        DBF     D3,LOOP2

        ADD     D0,A2                   ; add rowbytes
        DBF     D1,LOOP1                ; continue of more scanlines

ADIOS   MOVE.B  #01,16(A6)              ; Return TRUE
        BRA.S   Done                    ; Adios

SHUCKS  MOVE.B  #00,16(A6)              ; Return FALSE
Done    MOVEM.L (SP)+, D0-D7/A1-A5      ; restore regs
        UNLK    A6                      ;
        MOVE.L  (A7)+,A0                ; get return address
        ADD     #8,A7                   ; remove parameters
        JMP     (A0)                    ; Adios

;---------------------------------------------------------------
        .PROC   MoveBlk,3
;       PROCEDURE MoveBlk(a, b: TP; l: TL ); EXTERNAL;
;---------------------------------------------------------------
        MOVE.L  A2,D0                   ; save A2
        MOVE.L  (A7)+, A0               ; return address
        MOVE.L  (A7)+, D1               ; count
        MOVE.L  (A7)+, A1               ; b
        MOVE.L  (A7)+, A2               ; a

MLOOP   MOVE.B  (A2)+,(A1)+             ; move a to b
        SUBQ.L  #1,D1                   ; next byte
        BNE     MLOOP                   ; continue moving

        MOVE.L  D0,A2                   ; restore A2
        JMP     (A0)                    ; Adios
;---------------------------------------------------------------
        .FUNC   fEQBlk,3
;       FUNCTION  fEQBlk (a, b: TP; l: TL ): TF; FORWARD;
;---------------------------------------------------------------
        MOVE.L  A2,D0                   ; save A2
        MOVE.L  (A7)+, A0               ; return address
        MOVE.L  (A7)+, D1               ; count
        MOVE.L  (A7)+, A1               ; b
        MOVE.L  (A7)+, A2               ; a

FLOOP   CMPM.B  (A1)+,(A2)+             ; cmp a to b
        BNE     SHUCKS                  ; get out if not =
        SUBQ.L  #1,D1                   ; next byte
        BNE     FLOOP                   ; continue checking(works for unsigned)

        MOVE.B  #01,(A7)                ; Return TRUE
        BRA     FSKIP                   ; Adios

SHUCKS  MOVE.B  #00,(A7)                ; Return FALSE
FSKIP   MOVE.L  D0,A2                   ; restore A2
        JMP     (A0)                    ; Adios

;---------------------------------------------------------------
        .FUNC   PrAnd,2
;       FUNCTION PrAnd ( w1, w2: TW ): TW
;---------------------------------------------------------------
        MOVE.L  (A7)+, A0               ; return address
        MOVE    (A7)+, D1               ; w2
        MOVE    (A7)+, D0               ; w1
        AND     D0, D1                  ; and 'em
        MOVE    D1, (A7)                ; store return value

        JMP     (A0)

;---------------------------------------------------------------
        .FUNC   PrShRight,2
;       FUNCTION PrAnd ( w1, w2: TW ): TW
;---------------------------------------------------------------
        MOVE.L  (A7)+, A0               ; return address
        MOVE    (A7)+, D1               ; w2
        MOVE    (A7)+, D0               ; w1
        LSR     D1,D0                   ; shift 'em
        MOVE    D0, (A7)                ; store return value

        JMP     (A0)

;---------------------------------------------------------------
        .FUNC   PrShLeft,2
;       FUNCTION PrAnd ( w1, w2: TW ): TW
;---------------------------------------------------------------
        MOVE.L  (A7)+, A0               ; return address
        MOVE    (A7)+, D1               ; w2
        MOVE    (A7)+, D0               ; w1
        LSL     D1,D0                   ; shift 'em
        MOVE    D0, (A7)                ; store return value

        JMP     (A0)

;---------------------------------------------------------------
        .PROC   PrCallProc,1
;       FUNCTION PrCallProc ( ..... procPtr: TProc );
;
;  call proc
;---------------------------------------------------------------
;       PrBuf stuff
        .DEF    PbCallDumpProc
        .DEF    PbCallScanLine
        .DEF    PbCallChkProc
PbCallChkProc
PbCallDumpProc
PbCallScanLine

;       PrStd Communication Stuff
        .DEF    PsCallOpenPort
        .DEF    PsCallClosePort
        .DEF    PsCallOutPort
        .DEF    PsCallInPort
PsCallOpenPort
PsCallClosePort
PsCallOutPort
PsCallInPort

        MOVE.L  4(A7), A0               ; proc ptr
        MOVE.L  (A7)+, (A7)             ; move return addres
        CMP.L   #0,A0
        BEQ     ADIOS                   ;See if NIL
        JMP     (A0)                    ; Call proc
ADIOS   RTS

        .END

ÿ