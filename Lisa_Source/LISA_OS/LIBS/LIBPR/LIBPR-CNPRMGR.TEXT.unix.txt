(*
*)
{                          NOTE: The above compiles this program.                          }
{------------------------------------------------------------------------------------------}
{                           Copyright 1983, Apple Computer Inc.                            }
UNIT PrMgr;
INTRINSIC;

{------------------------------------------------------------------------------------------}
                                         INTERFACE
{------------------------------------------------------------------------------------------}

USES
  {$U LibSM/UnitStd    } UnitStd,
  {$U LIbOS/SysCall    } SysCall,
  {$U LibPM/PMDecl     } PMDecl,
  {$U LibSM/UnitHz     } UnitHz,
  {$U LibQD/Storage    } Storage,
  {$U LibQD/QuickDraw  } QuickDraw,
  {$U LibQD/GrafUtil   } GrafUtil,
  {$U LibFM/FontMgr    } FontMgr,
  {$U LibFM/FmgrUtil   } FmgrUtil,
  {$U LibWM/Events     } Events,
  {$U LibWM/Menus      } Menus,
  {$U LibWM/Windows    } Windows,
  {$U LibWM/Folders    } Folders,
  {$U LibAM/AlertMgr   } AlertMgr,
  {$U LibPM/PMM        } PMM,

  {$U LibPr/PrStdInfo  } PrStdInfo,
  {$U LibPr/PrStdProcs } PrStdProcs,
  {$U LibPr/Queues     } Queues,
  {$U LibPr/PrEvents   } PrEvents,
  {$U LibPr/PrDlgMgr   } PrDlgMgr;

{$SETC fCheckRoutine = (PrDebug OR FALSE)}
{$SETC fPrMgrDbg     = (PrDebug OR FALSE)}

CONST
                        {Alert parameters.  AX=Alert.}
   cAxVersion = 100;

   (* ----- Alert index constants *)
   cAxDiskFull  = 5;    {Not enough disk space to spool}
   cAxPageRange = 6;    {No pages within specified page range}
   cAxSpooling  = 7;    {The document is being spooled for printing}
   cAxChoose    = 8;    {No printer, choose another} OptCancel = 1; OptChange = 2;
   cAxNoPrinter = 9;    {No printer, don't print}
   cAxSmashed   =10;    {Files missing or smashed}


TYPE

  pPrintJob = ^PrintJob;
  hPrintJob = ^pPrintJob;
  PrintJob  = RECORD
    Link    : TL;
    JobID   : TL;
  END;

  TpPrPrinter = ^TPrPrinter;
  ThPrPrinter = ^TpPrPrinter;
  TPrPrinter  = RECORD
    Link    : ThPrPrinter;
    SonID   : TL;
    Valid   : TF;                 {Signifies a valid entry in the queue}
    DevName : THsp;   {.UnitStd}  {Handle to an E_Name}
    JobQueue: hQueue; {.Queues}   {Used exclusively by the Parent Print process.}
    CurJob  : hPrintJob;          {Handle of current print job or NIL if none in progress.}
    PrRec   : TPrRec; {.PrStdInfo}
  END;

  TDlgArgs    = PACKED RECORD   {Arg record passed from application to background and back.}
    fnopaging : TF;             {Whether to provide page ranges as part of dialog}
    suppress  : PrMenuSuppress; {What part of the dialogs should be suppressed.}
    ReturnOpt : DlgOption;      {The return option, e.g. OK or Cancel}
    fchange   : TF;             {Whether the user made changes or not}
  END;

  TPrDSHead   = RECORD {The "Header" in the printer shared DataSeg.}
    hzDS      : THz;   {.Storage}
    ParentID  : TL;
    QueueDS   : hQueue;{.Queues}
    DefaultPr : ThPrPrinter;
    ThePrRec  : TPrRec;{.PrStdInfo} {The shared PrRec that is passed between foreground and background.}
    DlgArgs   : TDlgArgs;           {Other procedure parameters that are passed between foreground and ...}
  END;
  TpPrDSHead  = ^TPrDSHead;

VAR

  cDSRefNum   : TC;
  hPrAlert    : TAlertfile;                  {The alert file. =NIL if alerts out.}

{------------------------------------------------------------------------------------------}

{----Init calls}

{  Init the PrMgr.  Called once per application.
     InitAlerts must be called prior to this procedure.  }
PROCEDURE PrMgrInit;
{  Init the PrMgr.  Called once per Background process.  }
PROCEDURE PrBgdInit;
{  Init the print world.  Called once per boot. }
PROCEDURE PrGlobalInit;
{  Wake up the Father print process and initialize him. }
PROCEDURE PrWakeDad;

{----The PrMgr Dialogs.  These take a PrPrf and return a fNewStl.
     The PrPrf is the document's printer setup, while the fNewStl is
     a flag indicating whether the user's formatting changed.
 A note on the VAR parameters:
     The PrPrf is a "Two-way" VAR  parameter: it both passes data and may be
     modified by a dialog, thus must be initialized.  This can be done in the
     stationary or by a PrPrfDefault call. }

{  The Format for printer... dialog. }
PROCEDURE PrPrfDlg  (VAR prprf: TPrRec; VAR fNewStl: TF; fPgSzSuppress: TF );

{  The Print... dialog; returns the DoIt button. }
FUNCTION  FPrInsDlg (VAR prprf: TPrRec; VAR fNewStl: TF; prmode: PrMenuSuppress): TF;

{  The background status dialog. }
PROCEDURE PrBgdDlg;

{----Control for binding/undbinding to the sharable data segment.}
PROCEDURE PrMgrDSBind  (VAR cErr: TC);
PROCEDURE PrMgrDSUnBind(VAR cErr: TC);

{----Operations on Print Records.}
{  Converts a virgin prprf print record to a valid default printer.}
PROCEDURE PrPrfDefault(VAR prprf: TPrRec);
{  Compares two print records and returns true if they are equivalent. }
FUNCTION  fPrPrfEq    (VAR prprf1,prprf2:TPrRec):TF;
{  Returns true if the print record is valid; false otherwise.}
FUNCTION  FPrPrfValid (VAR prprf: TPrRec): TF;

{----Miscellaneous Print Manager routines}
{  Builds a Long Font ID. }
PROCEDURE PrLFntID(VAR prprf: TPrRec; famIn: TC; setIn: Style; VAR lFntid: TLFntID);
{  Routines which return handles for the various printers.  Must be called
   after PrMgrDSBind and with ThePrRec set with the prprf in question.}
FUNCTION  PrIDMatch    :ThPrPrinter;
FUNCTION  PrFormatMatch:ThPrPrinter;
FUNCTION  PrPrintMatch :ThPrPrinter;

(*
FUNCTION  PrParentID  :TL;
FUNCTION  PrGetDefault:ThPrPrinter;
*)

{$IFC PrDebug }
PROCEDURE PrWrtRec (sLabel: TSp; PrRec: TPrRec);
{$ENDC}

{------------------------------------------------------------------------------------------}
                                     IMPLEMENTATION
{------------------------------------------------------------------------------------------}

{$S PrMgr }

{$IFC PrDebug}
   {$R+}
{$ELSEC}
   {$R-}
{$ENDC}

{$IFC PrSymbols}
   {$D+}
{$ELSEC}
   {$D-}
{$ENDC}

CONST

   timeWtMin = 500;     {The min wait alert time}

VAR
  pPrMgrDS    : TpPrDSHead;
  zHzDS       : THz;       {Our shared dataseg hz.}
                           {NOTE: The alerts live in the apps hz!}


  fPrDSOpen   : TF;                          {TRUE if the shared DS already opened.}
  cPrDSBinds  : TC;                          {count of calls to bind ldsn (stack). }

{------------------------------------------------------------------------------------------}
{                                PROCEDURE DIRECTORY                                       }
{------------------------------------------------------------------------------------------}

PROCEDURE PrMgrDSClose;                                         FORWARD;
PROCEDURE PrMgrDSOpen   (VAR cErr: TC);                         FORWARD;

PROCEDURE PrSwapAlerts  (VAR cErr: TC);                         FORWARD;
Function  CNPrRec68K: TpPrRec;                                  External;

{------------------------------------------------------------------------------------------}
{                                       Init Code                                          }
{------------------------------------------------------------------------------------------}
{$S PrMgrInit }

PROCEDURE PrBgdInit;
BEGIN
{$IFC fPrMgrDbg}
   WRITELN ('PrBgdInit');
{$ENDC}
   DlgMgrInit;

   zHzDS      := NIL;
   hPrAlert   := NIL;

   fPrDSOpen  := FALSE;
   cPrDSBinds := 0;
   cDSRefNum  := prNilRefNum;
END;

{------------------------------------------------------------------------------------------}
{$S PrMgrInit }

PROCEDURE PrMgrInit;
VAR
  cErr    : TC;
BEGIN
{$IFC fPrMgrDbg}
   WRITELN ('PrMgrInit');
{$ENDC}
   PrBgdInit;
   PrSwapAlerts (cErr);
END;

{------------------------------------------------------------------------------------------}
{$S PrInit }
PROCEDURE PrGlobalInit;
BEGIN
  cPrError:=0;
  fBackGround:=FALSE;  {--Suspend/Activate application Process flag.
                          TRUE implies that some background process
                          is doing a dialog somewhere.}
  lParentID:=PrNilProcess;
  fInitialized:=FALSE;
END;

{------------------------------------------------------------------------------------------}
{$S PrMgr }
PROCEDURE PrWakeDad;
VAR
  cErr      : TC;
BEGIN
   WHILE (fInitialized = FALSE) AND (lParentID <> PrNilProcess) DO BEGIN
     ACTIVATE_PROCESS(cErr,lParentID,FALSE);
     PsYieldCPU;
   END;
END;

{------------------------------------------------------------------------------------------}
{$S PrInit }
PROCEDURE PrSwapAlerts{VAR cErr: TC};
VAR cAlerts: TC;
{$IFC PrDebug }
    timeStrt: TL;
{$ENDC}
BEGIN
   cErr := 0;
   IF hPrAlert=NIL THEN BEGIN
      {$IFC PrDebug } timeStrt := Time; {$ENDC}

      OpenPhraseFile (cAlerts, spPrAlrt);
      IF alertError <= 0 THEN hPrAlert := ReadAlerts (cAlerts, cAxVersion);
      { ** I hope hPralert=nil if there is an error! ** }

{$IFC PrDebug }
      WRITELN ('PrSwapAlerts..Time to read=', ABS(Time-timeStrt):1,
               ', alertErr=', alertError:1);
{$ENDC}
      cErr := alertError;
   END;
END;

{------------------------------------------------------------------------------------------}
{                 Printer Background status Dialog (Monitor the Printer)                   }
{------------------------------------------------------------------------------------------}
{$S PrMgr }
PROCEDURE PrBgdDlg;
BEGIN
{$IFC fPrMgrDbg}
WRITELN('PrBgdDlg');
{$ENDC}
  PrWakeDad;
  IF (lParentID <> prNilProcess) THEN PrDeferToBgd(lParentID,EMonitorDialog);
END;

{------------------------------------------------------------------------------------------}
{                         Print Instance Dialog (Print...)                                 }
{------------------------------------------------------------------------------------------}
{$S PrMgr }
FUNCTION  FPrInsDlg{(VAR prprf: TPrRec; VAR fNewStl: TF; prmode: PrMenuSuppress):TF};
VAR
  cErr      : TC;
  lMatchID  : TL;
  { - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - }
  FUNCTION  PrGetID:TL;
  VAR
    hPrinter  : ThPrPrinter;
  BEGIN
    hPrinter:=PrPrintMatch;
    PrGetID:=PrNilProcess;
    IF (hPrinter <> NIL) THEN BEGIN
      IF hPrinter^^.SonID = PrNilProcess THEN BEGIN
        PrNotify(lParentID,EWake,ORD4(hPrinter),0);
        WHILE (hPrinter^^.SonID = PrNilProcess) DO PsYieldCPU;
      END;
      PrGetID:=hPrinter^^.SonID;
    END;
  END;
  { - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - }
BEGIN
{$IFC fPrMgrDbg}
  WRITELN('FPrInsDlg Start');
{$ENDC}

  FPrInsDlg := TRUE{Temporary True};
  fNewStl   := FALSE;

  PrMgrDSBind(cErr);
  IF cErr <= 0 THEN WITH pPrMgrDS^ DO BEGIN
    ThePrRec:=prprf;
    DlgArgs.suppress:=prmode;
    lMatchID:=PrGetID;
    IF (lMatchID <> prNilProcess) THEN BEGIN
      IF (prmode <> ePrDialogSuppress) THEN BEGIN
        fNewStl:=fNewStl OR DlgArgs.fchange;
        PrDeferToBgd(lMatchID,EPrintDialog);
        fNewStl:=fNewStl OR DlgArgs.fchange;
        FPrInsDlg:=(DlgArgs.ReturnOpt = eDlgOK);
        IF fNewStl THEN prprf:=ThePrRec;
      END
      ELSE BEGIN
        FPrInsDlg:=TRUE;   {--Everything is OK go ahead and print}
      END;
    END;
    PrMgrDSUnBind(cErr);
  END;
END;

{------------------------------------------------------------------------------------------}
{                   Printer Preference Dialog (Format for Printer...)                      }
{------------------------------------------------------------------------------------------}
{$S PrMgr }
PROCEDURE PrPrfDlg {(VAR prprf: TPrRec; VAR fNewStl: TF; fPgSzSuppress: TF )};
VAR
  lMatchID  : TL;
  cErr      : TC;
  { - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - }
  FUNCTION  PrGetID:TL;
  VAR
    hPrinter  : ThPrPrinter;
  BEGIN
    hPrinter:=PrFormatMatch;
    PrGetID:=PrNilProcess;
    IF hPrinter <> NIL THEN BEGIN
      IF hPrinter^^.SonID = PrNilProcess THEN BEGIN
        PrNotify(lParentID,EWake,ORD4(hPrinter),0);
        WHILE (hPrinter^^.SonID = PrNilProcess) DO PsYieldCPU;
      END;
      PrGetID:=hPrinter^^.SonID;
    END;
  END;
  { - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - }
BEGIN
{$IFC fPrMgrDbg}
  WRITELN ('PrPrfDlg Start');
{$ENDC}
  fNewStl:=FALSE;

  PrMgrDSBind (cErr);
  IF (cErr <= 0) THEN WITH pPrMgrDS^ DO BEGIN
    ThePrRec:=prprf;
    lMatchID:=PrGetID;

    WHILE TRUE DO BEGIN
      IF (lMatchID = prNilProcess) THEN BEGIN
        DlgArgs.suppress:=ePgRangeSuppress;  {--For the PrListDialog, ePgRangeSuppress means 'Cancel' suppress.}
        DlgArgs.fnopaging:=fPgSzSuppress;
        PrDeferToBgd(ParentID,ePrListDialog);
        lMatchID:=PrGetID;
        fNewStl:=TRUE;
      END;
      DlgArgs.suppress:=ePrNormal;
      DlgArgs.fnopaging:=fPgSzSuppress;
      PrDeferToBgd(lMatchID,ESettingsDialog);
      IF DlgArgs.ReturnOpt <> eDlgAnother THEN BEGIN
        IF DlgArgs.ReturnOpt = eDlgOK THEN BEGIN
          fNewStl:=(fNewStl OR DlgArgs.fchange);
          prprf:=ThePrRec;
        END;
  {$IFC fPrMgrDbg }
          PrWrtRec('PrPrfDlg end; prf:', prprf);
  {$ENDC}
          PrMgrDSUnBind(cErr);
          EXIT(PrPrfDlg);
      END;
      lMatchID:=prNilProcess;

    END{WHILE};
  END{WITH};
END;

{------------------------------------------------------------------------------------------}
{                                Btn and Data Seg Stuff                                    }
{   This is not in the init code: it is always within a Dlg or Dlg-like execution scope.   }
{------------------------------------------------------------------------------------------}
{$S PrMgr }
PROCEDURE PrMgrDSBind{VAR cErr: TC};
{$IFC PrDebug }
VAR  cbk     : TC;
{$ENDC}
BEGIN
{$IFC fPrMgrDbg }
  WRITELN('PrMgrDSBind');
{$ENDC}

   cErr := 0;

   cPrDSBinds := cPrDSBinds+1;
   IF cPrDSBinds = 1 THEN BEGIN
      IF fPrDSOpen THEN BEGIN
         BIND_DATASEG(cErr,cDSRefNum);
      END
      ELSE BEGIN
         PrMgrDSOpen (cErr);
      END;

{$IFC PrDebug }
      IF NOT FCheckHzOK (zHzDS, cbk) THEN WRITELN ('Hz Probs in PrMgrDSBind');
{$ENDC}

      IF cErr > 0 THEN BEGIN
{$IFC PrDebug }
         WRITELN('!PrMgrDSBind cErr=', cErr:1,
                  ' fPrDSOpen=', fPrDSOpen, ' cPrDSBinds=', cPrDSBinds:1 );
{$ENDC}
         PrMgrDSClose;
      END;
   END;
END;

{------------------------------------------------------------------------------------------}
{$S PrMgr }
PROCEDURE PrMgrDSClose;
VAR cErr: TC;
BEGIN
{$IFC fPrMgrDbg }
   WRITELN ('PrMgrDSClose');
{$ENDC}

   IF (cDSRefNum <> prNilRefNum) THEN CLOSE_DATASEG(cErr, cDSRefNum);

   pPrMgrDS   := NIL;
   fPrDSOpen  := FALSE;
   cPrDSBinds := 0;
   cDSRefNum  := prNilRefNum;

END;

{------------------------------------------------------------------------------------------}
{$S PrMgr }
PROCEDURE PrMgrDSOpen{VAR cErr: TC};
VAR
  lSegptr : LONGINT;
  sSegname: PathName;

{$IFC PrDebug }
  cbk     : TC;
  timeStrt, timeEnd: TL;
  DSInfo  :   DSInfoRec;
{$ENDC}

BEGIN
{$IFC fPrMgrDbg }
  WRITELN('PrMgrDSOpen');
{$ENDC}
{$IFC PrDebug }
   timeStrt:=Time;
{$ENDC}

   PrWakeDad;

   sSegname := sParentSeg;
   OPEN_DATASEG(cErr,sSegName,cDSRefNum,lSegPtr,ldsnshared);
   fPrDSOpen:= TRUE;

   IF (cErr <= 0) THEN BEGIN
     SETACCESS_DATASEG (cErr, cDSRefNum, FALSE{fReadOnly});
     pPrMgrDS   := POINTER(lSegPtr);
     WITH pPrMgrDS^ DO BEGIN
       zHzDS      := hzDS;
     END;
   END;

{$IFC PrDebug }
   timeEnd:=Time;
   IF NOT FCheckHzOK (zHzDS, cbk) THEN WRITELN ('Hz Probs in PrMgrDSOpen');
   {IF fPrMgrDbg THEN BEGIN}
      WRITELN ('PrMgrDSOpen: Time to open dataseg=', ABS(timeEnd-timeStrt):1);
      INFO_DATASEG(cErr, cDSRefNum, DSInfo);
      WRITELN ('PrMgr Hz: cb=', (CbOfHz(zHzDS)):1,
               ' cbk=', cbk:1,
               ' DataSeg: cb=', DSInfo.mem_size:1,
               ' pStrt=', ORD(pPrMgrDS):1,
               ' pEnd=', (ORD(pPrMgrDS)+DSInfo.mem_size):1 );
   {END;}
{$ENDC}
END;

{------------------------------------------------------------------------------------------}
{$S PrMgr }
PROCEDURE PrMgrDSUnBind{VAR cErr: TC};
BEGIN
{$IFC fPrMgrDbg}
  WRITELN ('PrMgrDSUnBind');
{$ENDC}

   cErr := 0;

   IF cPrDSBinds > 0 THEN BEGIN
      cPrDSBinds  := cPrDSBinds-1; {Pins at 0}
      IF cPrDSBinds = 0 THEN BEGIN
         UNBIND_DATASEG(cErr,cDSRefNum);

         IF (cErr > 0) THEN BEGIN
            {$IFC PrDebug }
            WRITELN ('!PrMgrDSUnBind cErr=', cErr:1,
                     ' fPrDSOpen=', fPrDSOpen, ' cPrDSBinds=', cPrDSBinds:1 );
            {$ENDC}
            PrMgrDSClose;
         END;
      END;
   END;
END;

{------------------------------------------------------------------------------------------}
{                                          Alerts                                          }
{------------------------------------------------------------------------------------------}

{------------------------------------------------------------------------------------------}
{                                          DeBug                                           }
{------------------------------------------------------------------------------------------}
{$IFC PrDebug }
{$ENDC}
{------------------------------------------------------------------------------------------}
{ Miscellaneous Stuff:                                                                     }
{                      Parameter memory Code                                               }
{                      PrPrf Info Code                                                     }
{------------------------------------------------------------------------------------------}
{$S PrMgrMsc }

{------------------------------------------------------------------------------------------}

PROCEDURE PrPrfDefault  {VAR prprf: TPrRec};
VAR
  cErr      : TC;
{Makes vanilla prprf.}
BEGIN
{$IFC fPrMgrDbg}
WRITELN ('PrPrfDefault');
{$ENDC}

  PrMgrDSBind(cErr);
  IF cErr <= 0 THEN WITH pPrMgrDS^ DO BEGIN
    prprf:=DefaultPr^^.PrRec;
    PrMgrDSUnBind(cErr);
  END
  ELSE BEGIN
    prprf:=CNPrRec68K^;
    prprf.PrVersion:= cPrPrfVersion;   {Make sure its the right one}
  END;
  WITH prprf DO BEGIN
    WITH PrJobInfo DO BEGIN
      DocPgFst :=   0;             {Page Range. PgFst=0 => all.}
      DocPgLst :=   0;
      SpooledPages:=0;             {Number of Pages in Spool File.}
      Copies   :=   1;             {No. copies.}
      Immediate:=FALSE;
    END;
  END;
END;

{------------------------------------------------------------------------------------------}
{$S PrMgrMsc }
FUNCTION  fPrPrfEq{( VAR prprf1,prprf2:TPrRec):TF};
TYPE
  aPrRec  = ARRAY [1..200] OF TL;
  paPrRec = ^aPrRec;

VAR
  pr1,pr2 : paPrRec;
  j       : TC;
BEGIN
{$IFC fPrMgrDbg}
WRITE('FPrPrfEq:');
{$ENDC}
fPrPrfEq:=fEqBlk(@prprf1,@prprf2,SIZEOF(TPrRec));
{
pr1:=@prprf1;
pr2:=@prprf2;
fPrPrfEq:=FALSE;
FOR j:=1 TO (SIZEOF(TPrRec) DIV 4) DO BEGIN
  IF pr1^[j] <> pr2^[j] THEN EXIT(fPrPrfEq);
END;
fPrPrfEq:=TRUE;
}
END;

{------------------------------------------------------------------------------------------}
{$S PrMgrMsc }
FUNCTION  FPrPrfValid{(VAR prprf: TPrRec): TF};
{Validity check via the obvious fields.}
VAR
  cErr    : TC;
  hMatch  : ThPrPrinter;
BEGIN
{$IFC fPrMgrDbg}
WRITELN ('FPrPrfValid');
{$ENDC}
  FPrPrfValid:=FALSE;
  IF (prprf.PrVersion = cPrPrfVersion) THEN BEGIN
    hMatch:=NIL;
    PrMgrDSBind(cErr);
    IF cErr <= 0 THEN WITH pPrMgrDS^ DO BEGIN
      ThePrRec:=prprf;
      hMatch:=PrIDMatch;
      PrMgrDSUnBind(cErr);
    END;{ELSE force it to be something valid}
    IF (hMatch <> NIL) THEN BEGIN
      FPrPrfValid := TRUE;
    END;
  END;
END;

{------------------------------------------------------------------------------------------}
{$S PrMgr}
PROCEDURE PrLFntID {(prprf: TPrRec; famIn: TC; setIn: Style; VAR lFntid: TLFntID)};
BEGIN
  WITH lfntid DO BEGIN
    dev := prprf.PrInfo.ADev.dev;
    fam:=famIn;
    seteface:=setIn;
  END;
END;

{------------------------------------------------------------------------------------------}
{$S PrMgr}
FUNCTION  PrIDMatch{:ThPrPrinter};
  { - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - }
  FUNCTION  uPrIDMatch(hQItem,pTPrRec:hQueue):BOOLEAN;
  VAR
    hQPrRec   : ThPrPrinter;
    pThePrRec : ^TPrRec;
  BEGIN
    hQPrRec:=POINTER(ORD4(hQItem));
    pThePrRec:=POINTER(ORD4(pTPrRec));
    uPrIDMatch:=TRUE;
    WITH hQPrRec^^.PrRec.PrInfo DO IF ((PrinterID = pThePrRec^.PrInfo.PrinterID)) THEN uPrIDMatch:=FALSE;
  END;
  { - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - }
BEGIN
{$IFC fPrMgrDbg}
WRITELN ('PrIDMatch');
{$ENDC}
  PrIDMatch:=NIL;
  WITH pPrMgrDS^ DO BEGIN
    PrIDMatch:=POINTER(EnumerateQueue(QueueDS,uPrIDMatch,@ThePrRec));
  END{WITH};
END;

{------------------------------------------------------------------------------------------}
{$S PrMgr}
FUNCTION  PrFormatMatch{:ThPrPrinter};
{ The PrRec in shared memory is checked for a match in the Parents current list of processes.}
VAR
  hPrinter  : ThPrPrinter;
  { - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - }
  FUNCTION  uPrFormatMatch(hQItem,pTPrRec:hQueue):BOOLEAN;
  VAR
    hQPrRec   : ThPrPrinter;
    pThePrRec : ^TPrRec;
  BEGIN
    hQPrRec:=POINTER(ORD4(hQItem));
    pThePrRec:=POINTER(ORD4(pTPrRec));
    uPrFormatMatch:=TRUE;
    WITH hQPrRec^^.PrRec.PrInfo DO IF ((PrinterID = pThePrRec^.PrInfo.PrinterID)
      AND (Port = pThePrRec^.PrInfo.Port)) THEN uPrFormatMatch:=FALSE;
  END;
  { - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - }

BEGIN
{$IFC fPrMgrDbg}
WRITELN ('PrFormatMatch');
{$ENDC}
  PrFormatMatch:=NIL;
  WITH pPrMgrDS^ DO BEGIN
    PrFormatMatch:=POINTER(EnumerateQueue(QueueDS,uPrFormatMatch,@ThePrRec));
  END{WITH};
END;

{------------------------------------------------------------------------------------------}
{$S PrMgr}
FUNCTION  PrPrintMatch{:ThPrPrinter};
{ The PrRec in shared memory is checked for a match in the Parent's current list of printers.}
VAR
  fPrMatch  : TF;
  hPrinter  : ThPrPrinter;
  { - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - }
  FUNCTION  uPrPrintMatch(hQItem,pTPrRec:hQueue):BOOLEAN;
  VAR
    hQPrRec   : ThPrPrinter;
    pThePrRec : ^TPrRec;
  BEGIN
    hQPrRec:=POINTER(ORD4(hQItem));
    pThePrRec:=POINTER(ORD4(pTPrRec));
    uPrPrintMatch:=TRUE;
    WITH hQPrRec^^.PrRec.PrInfo DO IF ((PrinterID = pThePrRec^.PrInfo.PrinterID)
      AND (ORD(Port) <> prNilPort) AND (Port = pThePrRec^.PrInfo.Port)) THEN uPrPrintMatch:=FALSE;
  END;
  { - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - }
  FUNCTION  PrAorD:BOOLEAN; {--Alert and/or raise the Printer Selection Dialog.  Return OK or Cancel.}
  BEGIN
    PrAorD:=FALSE;
    IF (AskAlert(hPrAlert,cAxChoose) = OptChange) THEN BEGIN {--Specified printer not connected, choose another}
      {--User said OK (change printers)}
      PrDeferToBgd(pPrMgrDS^.ParentID,EPrListDialog);
      PrAorD:=(pPrMgrDS^.DlgArgs.ReturnOpt = eDlgOK);
    END;
  END;
  { - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - }

BEGIN
{$IFC fPrMgrDbg}
WRITELN ('PrPrintMatch');
{$ENDC}
{--Set defaults for the return.}
  PrPrintMatch:=NIL;
  WITH pPrMgrDS^ DO BEGIN
    DlgArgs.ReturnOpt:=eDlgCancel;
    DlgArgs.fchange:=FALSE;
    fPrMatch:=FALSE;

    WHILE fPrMatch = FALSE DO BEGIN
      hPrinter:=POINTER(EnumerateQueue(QueueDS,uPrPrintMatch,@ThePrRec));
      IF (hPrinter <> NIL) THEN BEGIN
        PrPrintMatch:=hPrinter;
        DlgArgs.ReturnOpt:=eDlgOK;
        fPrMatch:=TRUE;           {EXIT(PrPrintMatch);}
  {$IFC fPrMgrDbg}
        PrWrtRec('PrPrintMatch; new:', ThePrRec);
  {$ENDC}
      END
      ELSE BEGIN
        IF DlgArgs.suppress = ePrDialogSuppress THEN BEGIN
          StopAlert(hPrAlert,cAxNoPrinter); {--Printer is not connected}
          DlgArgs.fchange:=FALSE;
          DlgArgs.ReturnOpt:=eDlgCancel;
          fPrMatch:=TRUE;         {EXIT(PrPrintMatch);}
        END
        ELSE BEGIN
          DlgArgs.fchange:=FALSE;
          IF (PrAorD <> TRUE) THEN BEGIN
            {DlgArgs.fchange:=FALSE;}
            DlgArgs.ReturnOpt:=eDlgCancel;
            fPrMatch:=TRUE;       {EXIT(PrPrintMatch);}
          END;
        END;
      END;
    END{WHILE};
  END;
END;

{------------------------------------------------------------------------------------------}
(*
{$S PrMgr}
FUNCTION  PrParentID{:TL};
VAR
  cErr    : TC;
BEGIN
{$IFC fPrMgrDbg}
PrWrtRec ('PrParentID');
{$ENDC}
  PrParentID:=prNilProcess;
  PrMgrDSBind(cErr);
  IF (cErr <= 0) THEN WITH pPrMgrDS^ DO BEGIN
    PrParentID:=ParentID;
    PrMgrDSUnBind(cErr);
  END;
END;
*)
{------------------------------------------------------------------------------------------}
(*
{$S PrMgr}
FUNCTION PrGetDefault{:ThPrPrinter};
{ The default printer is found in the current list of printers.}
VAR
  cErr      : TC;
BEGIN
  PrGetDefault:=NIL;
  PrMgrDSBind (cErr);
  IF (cErr <= 0) THEN WITH pPrMgrDS^ DO BEGIN
    PrGetDefault:=DefaultPr;
    PrMgrDSUnBind(cErr);
  END;
END;
*)
{------------------------------------------------------------------------------------------}
{                                     Commentary                                           }
{------------------------------------------------------------------------------------------}

END.

{***************************   SUMMARY OF PRMGR CHANGES FOR CONFIGURABILITY    *********************

   1. All dialogs are raised by background processes.

***************************************************************************************************}

{
UNIT PrMgr CHANGE LOG:
     11/29/83 BH: New unit allows background processes to put up their own dialogs.
}

ÿ