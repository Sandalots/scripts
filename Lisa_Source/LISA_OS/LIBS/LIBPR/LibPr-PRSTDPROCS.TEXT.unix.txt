(*
$Exec
PLibPr/PrStdProcs
{no listing}
LibPr/Temp
GLibPr/Temp
LibPr/PrStdProcs
$EndExec
*)
{NOTE: The above compiles this program.                                                    }
{------------------------------------------------------------------------------------------}
{                          Copyright 1983, 1984, Apple Computer Inc.                       }
{------------------------------------------------------------------------------------------}
(* This unit contains Shared code that is used in printing. Global Variables, Types and   *)
(* constants should reside in the PrStdInfo unit.                                         *)
{------------------------------------------------------------------------------------------}
UNIT PrStdProcs;
INTRINSIC;

INTERFACE

USES
     {$U LibOS/SysCall   }  SysCall,
     {$U LibPM/PMDecl    }  PMDecl,
     {$U LibHW/HWint     }  HWint,
     {$U LibSM/UnitStd   }  UnitStd,
     {$U LibSM/UnitHz    }  UnitHz,
     {$U LibQD/Storage   }  Storage,
     {$U LibQD/QuickDraw }  QuickDraw,
     {$U LibFM/FontMgr   }  FontMgr,
     {$U LibPr/PrStdInfo }  PrStdInfo;


CONST
     PortA   = $0A10;   {#10#1 position address as needed by OS for serial port A}
     PortB   = $0A20;   {#10#2 position for serial port B}

Type Str80 = String[80];

PROCEDURE PsCopy     (Src,Dst:TL;len:TC);
PROCEDURE PsOpenPort (Var Error:Integer;Port:Integer);
PROCEDURE PsClosePort(Var Error:Integer;Port:Integer);
PROCEDURE PsInPort   (Var Error:Integer;p: TP; c: TC);
PROCEDURE PsOutPort  (Var Error:Integer;p: TP; c: TC);
PROCEDURE PsPortName (Port:Integer;shandle:THsp);
PROCEDURE PsPortToDevName(Port:Integer;VAR DevName:e_name);
Procedure PsPreemptive;
Procedure PsNonPreemptive;

{Utility Procs}
PROCEDURE PsNumToStr(c,w:TC;pstr:TpSp;z:CHAR);
PROCEDURE PsYieldCpu;
FUNCTION  fClrBitmap( VAR bits: Bitmap; rCheck: Rect ): TF;

{DeBug Procs}
Procedure InitDeBug;

{Assembly procs}
FUNCTION  PrAnd( c1, c2: TC ): TC;
PROCEDURE ClrBitmap (pBitmap: TP );
FUNCTION  fClrBits( VAR bits: Bitmap; rCheck: Rect ): TF;
FUNCTION  fEQBlk (a, b: TP; l: TL ): TF;
PROCEDURE MoveBlk(a, b: TP; l: TL );
FUNCTION  PrShRight( c1, n2: TC ): TC;
FUNCTION  PrShLeft ( c1, n2: TC ): TC;

Procedure GetFileName(Var PrFileId:TPrFileId);
{ ---------------------------------------------------------------------- }

IMPLEMENTATION
{$S Pr }

{$IFC PrDebug}
   {$R+}
{$ELSEC}
   {$R-}
{$ENDC}

{$IFC PrSymbols}
   {$D+}
{$ELSEC}
   {$D-}
{$ENDC}

CONST
    prDSSize = 12000;

Var
     PrRefNum : Integer;   {Printer refnum}

{Procs covered in PR68K}
FUNCTION  PrAnd{ ( c1, c2: TC ): TC };                        EXTERNAL;
PROCEDURE ClrBitmap{ (pBitmap: TP ) };                        EXTERNAL;
FUNCTION  fClrBits{ ( VAR bits: Bitmap; rCheck: Rect ): TF }; EXTERNAL;
FUNCTION  fEQBlk{ (a, b: TP; l: TL ): TF };                   EXTERNAL;
PROCEDURE MoveBlk{ (a, b: TP; l: TL ) };                      EXTERNAL;
FUNCTION  PrShRight{ ( c1, n2: TC ): TC };                    EXTERNAL;
FUNCTION  PrShLeft{ ( c1, n2: TC ): TC };                     EXTERNAL;
{ ---------------------------------------------------------------------- }
Procedure InitDeBug;
Begin
  {$IFC fTracePR}    LogCall;     {$ENDC}
  If PrDebug then Begin
     PrBugpName:='-Printer';
     ReWrite(PrBugFile,PrBugpName);
  End;
End;
{ ---------------------------------------------------------------------- }
Procedure Word2Hex(Word:Integer;Var TheString:Str255;SpaceFill:Integer);
{Courtesy of Mr. Bayles Holt -- You had better leave this in this time Bayles!}
Var StrLen,CharPos,HexChar : Integer;
Begin
  {$IFC fTracePR}    LogCall;     {$ENDC}
  StrLen:=Ord(TheString[0]);
  For CharPos:=SpaceFill downto 1 do
  Begin
    HexChar:=wAnd(wShRt(Word,(CharPos-1)*4),$000F);
    If HexChar>9 then HexChar:=HexChar+7;
    TheString[StrLen+SpaceFill-CharPos+1]:=Chr(HexChar+Ord('0'));
  End;
  TheString[0]:=Chr(StrLen+SpaceFill);
End;
{ ---------------------------------------------------------------------- }
Procedure GetFileName{Var PrFileId:TPrFileId};
Var
    Error         : Integer;
    sId,sBlockNum : Str255;
    sPreFix       : String[2];
    sSufFix       : String[2];
    Loc           : Integer;
Begin
  {$IFC fTracePR}    LogCall;     {$ENDC}
  sPreFix   :='[P';
  sSufFix   :=']';
  sId      :='';
  sBlockNum:='';
  With PrFileId do Begin
    sFileName :='';
    Word2Hex(Id,sId,4);
    Word2Hex(BlockNum,sBlockNum,4);
    Loc:=1;
    Insert(sPreFix,  sFileName,Loc);
    Loc:=Loc+Length(sPreFix);
    Insert(sId,      sFileName,Loc);
    Loc:=Loc+Length(sId);
    Insert(sBlockNum,sFileName,Loc);
    Loc:=Loc+Length(sBlockNum);
    Insert(sSufFix,  sFileName,Loc);
  End;
End;
{ ---------------------------------------------------------------------- }
Procedure PsNonPreemptive;
Var Error:Integer;
Begin
   {$IFC fTracePR}    LogCall;     {$ENDC}
   Sched_Class ( error, True ); {Lock}
End;
{ ---------------------------------------------------------------------- }
Procedure PsPreemptive;
Var Error:Integer;
Begin
   {$IFC fTracePR}    LogCall;     {$ENDC}
   Sched_Class ( error, False ); {UnLock}
End;
{ ---------------------------------------------------------------------- }
PROCEDURE PsCopy{(Src,Dst:TL;len:TC)};
VAR
    PS,PD   : TP;
BEGIN
PS:=POINTER(Src);
PD:=POINTER(Dst);
MoveLeft(PS^,PD^,len);
END;
{ ---------------------------------------------------------------------- }
PROCEDURE PsClosePort{(Var Error:Integer;Port:Integer)};
VAR volumeName: E_Name;
  devControl  : dcType;
  path        : PathName;
BEGIN
   {$IFC fTracePR}    LogCall;     {$ENDC}
   CLOSE_OBJECT( Error, PrRefNum );
   (* ----- Get the port name.*)
   PsPortToDevName( port, volumeName);

   (* ----- Set automatic break to occur when device is closed next time (default) *)
   path:=CONCAT('-', volumeName, '-x');
   WITH devControl DO BEGIN
     dcversion:=2;
     dccode:=25;                     {--code to disable/enable break on closing}
     dcdata[0]:=1;                   {--(0 disable), (1 = enable) break on closing}
   END;
   DEVICE_CONTROL(Error,path,devControl);

   (* ----- UnMount the device.*)
   UNMOUNT( Error, volumeName );
END;
{ ---------------------------------------------------------------------- }
PROCEDURE PsInPort{ (Var Error:Integer;p: TP, c: TC ) };
VAR
    iJunk: TC;
    lRetCount: TL;
BEGIN
    {$IFC fTracePR}    LogCall;     {$ENDC}
    IF  c > 0 THEN  READ_DATA( Error, PrRefNum, ORD(p), c, lRetCount, sequential, 0);
END;
{ ---------------------------------------------------------------------- }
PROCEDURE PsOpenPort{(Var Error:Integer;Port:Integer)};
VAR
    path: PathName;
    access: MSet;
    iJunk: TC;
    devControl: dcType;
    volumeName, passName, devName: E_NAME;
BEGIN
  {$IFC fTracePR}    LogCall;     {$ENDC}
  WITH devControl DO BEGIN

     (*-----Get the port name.*)
      PsPortToDevName( port, devName);

     (*-----Mount the device.*)
      passName:='';
      MOUNT( Error, volumeName, passName, devName );

     (*-----Transform the device name into a file name.*)
      path:=CONCAT('-', devName, '-x'); {OD}

     (* Do the port dependent device controls*)
     (* Common (serial & Parallel) stuff:*)
     (* Note: Device controls now have names in syscall.  Please use!*)
      dcversion:=2;
      dccode:=17;             {auto LF disable}
      dcdata[0]:=0;
      DEVICE_CONTROL( Error, path, devControl );

     (*----- Port specific stuff: *)
      CASE port OF
          PortA, PortB: BEGIN
              dccode:=5;                                     {baud rate change}
              dcdata[0]:=9600;
              DEVICE_CONTROL( Error, path, devControl );

              dccode:=2;                                     {DTR change}
              DEVICE_CONTROL( Error, path, devControl );

              dccode:=1;                                     {8-bit no-parity change}
              dcdata[0]:=0;
              DEVICE_CONTROL( Error, path, devControl );

              dccode:=12;                                    {time out}
              dcdata[0]:=60;
              DEVICE_CONTROL( Error, path, devControl );

              dccode:=10;                                    {disconnect detect}
              dcdata[0]:=0;
              dcdata[1]:=-128;
              DEVICE_CONTROL( Error, path, devControl );
          END; {rs 232 ports}
          OTHERWISE BEGIN
              {none}
          END;
      END; {case}

      access:=[dwrite, dread, private];
      OPEN( Error, path, PrRefNum, access );

  END;
END;
{ ---------------------------------------------------------------------- }
PROCEDURE PsOutPort{ (Var Error:Integer;p: TP, c: TC ) };
VAR
    iJunk: TC;
    lRetCount: TL;
BEGIN
    {$IFC fTracePR}    LogCall;     {$ENDC}
    IF c > 0 THEN WRITE_DATA( Error, PrRefNum, ORD(p), c, lRetCount, sequential, 0);
END;
{ ---------------------------------------------------------------------- }
PROCEDURE PsPortName{(Port:Integer;shandle:THsp)};
VAR
    cfield      : T16;
    substring   : TSp;
BEGIN
    {$IFC fTracePR}    LogCall;     {$ENDC}
    cfield.i:=Port;     {--hackery}
    CASE cfield.n2 OF
        1,2,3:  BEGIN
            substring:=CONCAT(prsslot,' ');
            PsNumToStr(cfield.n2,0,@substring,' ');
            substring:=CONCAT(substring,' ',prsconnector,' ');
            PsNumToStr(cfield.n1,0,@substring,' ');
            IF (cfield.n0 <> 0) THEN BEGIN
              substring:=CONCAT(substring,' ',prsdevice,' ');
              PsNumToStr(cfield.n0,0,@substring,' ');
            END;
        END;

        11: BEGIN
            substring:=prsparallel;
        END;

        10: BEGIN IF (cfield.n1 = 1) THEN BEGIN
                substring:=prsAserial;
            END
            ELSE BEGIN
                substring:=prsBserial;
            END;
        END;

        OTHERWISE substring:=prsnone;
    END;
    PsCopy(ORD4(@substring),ORD4(@shandle^^),LENGTH(substring)+1);
END;
{ ---------------------------------------------------------------------- }
PROCEDURE PsPortToDevName{(Port:Integer;VAR DevName:e_name)};
VAR
    cfield     : T16;
    substring  : TSp;
BEGIN
    {$IFC fTracePR}    LogCall;     {$ENDC}
    substring:='#'; {--Beginning form of a valid vol name}
    cfield.i:=Port;
    PsNumToStr(cfield.n2,0,@substring,' ');
    IF (cfield.n1 <> 0) THEN BEGIN
        substring:=CONCAT(substring,'#');
        PsNumToStr(cfield.n1,0,@substring,' ');
        IF (cfield.n0 <> 0) THEN BEGIN
             substring:=CONCAT(substring,'#');
             PsNumToStr(cfield.n1,0,@substring,' ');
        END;
    END;
    DevName:=substring;
END;
{ ---------------------------------------------------------------------- }
PROCEDURE PsYieldCpu;
VAR
    cError: TC;
    ProcessInfo: ProcInfoRec;
BEGIN
    {$IFC fTracePR}    LogCall;     {$ENDC}
    INFO_PROCESS(cError,MY_ID,ProcessInfo);
    SETPRIORITY_PROCESS(cError,MY_ID,120);
    YIELD_CPU( cError, TRUE );
    SETPRIORITY_PROCESS(cError,MY_ID,ProcessInfo.Priority);
END;
{ ---------------------------------------------------------------------- }
PROCEDURE PsNumToStr{(c,w:TC;pstr:TpSp;z:CHAR)};
{Concatinates the number c, converted to a string, onto string pstr^}
{c is number to convert}
{w is number of digits required ('z' leading character filled)}
{pstr^ must be a valid string or the empty string}
VAR
    cDigit,clen     : TC;
    strOne          : STRING[1];
    strOut          : STRING[20];    {Better never have numbers greater than 20 characters}
BEGIN
    {$IFC fTracePR}    LogCall;     {$ENDC}
    strOut[0]:=CHR( 0 );
    strOne[0]:=CHR( 1 );
    REPEAT
        strOne[1]:=CHR((c MOD 10)+ORD('0'));
        strOut:=CONCAT( strOne, strOut );
        c:=c DIV 10;
    UNTIL c = 0;
    strOne[1]:=z;
    WHILE (w > (LENGTH(strOut))) DO strOut:=CONCAT(strOne,strOut);
    pstr^:=CONCAT(pstr^,strOut);
END;
{ ---------------------------------------------------------------------- }
FUNCTION  fClrBitmap{ ( VAR bits: Bitmap; rCheck: Rect ): TF };
VAR
    rTemp: Rect;
    fJunk: TF;
BEGIN
    {$IFC fTracePR}    LogCall;     {$ENDC}
    fJunk:=SectRect( bits.bounds, rCheck, rTemp );
    fClrBitmap:=fClrBits( bits, rTemp );
END;
{ ---------------------------------------------------------------------- }
END. {Unit PrStdProcs}
{Unit PrStdProcs Change Log:
     10/11/83 EZ: Made PrStdInfo and PrStdProcs units from PrStd.
 UNIT PrStd     CHANGE LOG:
 Note this is the change log for PRStd not PRStdInfo. PrStd was the original unit. It is now broken up
 into two units: PRStdInfo and PrStdProcs. This Change log is included for history purposes.
      9/28/83 EZ: Deleted declaration to Psstdbbg. The procedure did not exist.
      8/23/83 DC: PrStlDefault: change International paper size default back to 8-1/2x11".
      8/23/83 DC: International prPrf version # = 6 (don't want to invalidate prPrf's of docs
                        created on "Europe pre-release" version).
      6/24/83 DC: Replace fPrEurope with fPrInternational
      6/24/83 DC: Add global constants for all international paper sizes, change iPgLst to 12
      6/24/83 DC: International prPrf version # = 7
      6/24/83 DC: International default paper size = iPg210x12
      6/15/83 DC: cPrPrfVersion #s made consistent with Release 1.0.
      5/24/83 DC: PrPMDefault: for Europe, default paper is iPg8x14 instead of iPg8x11.
      5/18/83 DC: PrStd: New cPrPrfVersion version numbers.
      5/16/83 DC: PrStlDefault: if fPrEurope, use a factor of 60 instead of 6 (requires
                  ord4!), and set paper width to 210mm instead of 8.5".
      4/29/83 DC: Add fPrEurope compile flag, which is TRUE for European build only.
                  Add the following conditional changes based on fPrEurope:
                      prPgFract constant = 120 instead of 100
                      cPrPrfVersion = 6 instead of 5.  Forces all old docs to be updated
                        with default printer pref.
                      PrStlDefault uses a factor of 6 instead of 10, and sets up default
                        paper length = 12" instead of 11".
      4/15/83 EZ: Added comments to show defaults for fUnlockIO and FLockDraw.
      3/21/83 OD: Removed fOldPr, fXP12, & fPrOs compile flags.
      2/ 1/83 OD: fPsPageOK: FanFold => don't alert on copy 2,3,...
      1/31/83 OD: PrPMDefault:Default typer to cut sheet.
                  Add fUnLockIO & fLockDraw to control Sched_Fence calls.
      1/28/83 OD: Change cPrPrfVersion to 5 (for A5). Add Sched_Fence calls.
                  cPgMin 0 => 1. Add edev test to fPrPrfValid.
                  rSafe added.
}

ÿ