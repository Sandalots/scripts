
(*
$Exec
P?
-newdisk-intrinsic.lib
NewPr/CiBuf
{no listing}
Temp
GTemp
NewPr/CiBuf
$EndExec
*)
{               NOTE: The above compiles this program.                   }
{ ---------------------------------------------------------------------- }
{                Copyright 1983,1984 Apple Computer Inc.                 }
UNIT CiBuf;
{ ---------------------------------------------------------------------- }
                              Interface
{ ---------------------------------------------------------------------- }

USES {$U LibOS/SysCall         }  SysCall,
     {$U LibPM/PMDecl          }  PMDecl,
     {$U LibSM/UnitStd         }  UnitStd,
     {$U LibSM/UnitHz          }  UnitHz,
     {$U LibQD/Storage         }  Storage,
     {$U LibQD/QuickDraw       }  QuickDraw,
     {$U LibFM/FontMgr         }  FontMgr,
     {$U LibWM/Events          }  Events,
     {$U LibWM/Menus           }  Menus,
     {$U LibWM/Windows         }  Windows,
     {$U LibWM/Folders         }  Folders,
     {$U libSB/WmlStd          }  WmlStd,
     {$U libSB/WmlCrs          }  WmlCrs,
     {$U LibAM/AlertMgr        }  AlertMgr,
     {$U LibPr/PrStdInfo       }  PrStdInfo,
     {$U LibPr/PrStdProcs      }  PrStdProcs,
     {$U LibPr/Queues          }  Queues,
     {$U LibPr/PrEvents        }  PrEvents,
     {$U LibPr/PrDlgMgr        }  PrDlgMgr,
     {$U LibPr/PrMgr           }  PrMgr,
     {$U LibPr/PrBuf           }  PrBuf,
     {$U-}
     {$U LibPr/CiGlobals       }  CiGlobals,
     {$U LibPr/CiDev           }  CiDev;


(* ----- The CiChk...  and CiDump... procedures  are installed into the PrBuf unit. CiStdText is *)
(* ----- the QuickDraw bottle neck for captureing text if we are indraft mode.                   *)

FUNCTION  CiChkHRule( VAR cmd: TPbCommand; hPbData: THPbData ): TF;
FUNCTION  CiChkText ( VAR cmd: TPbCommand; hPbData: THPbData ): TF;
FUNCTION  CiChkVRule( VAR cmd: TPbCommand; hPbData: THPbData ): TF;

PROCEDURE CiDumpHRule( VAR cmd: TPbCommand; hPbData: THPbData );
PROCEDURE CiDumpText ( VAR cmd: TPbCommand; hPbData: THPbData );
PROCEDURE CiDumpVRule( VAR cmd: TPbCommand; hPbData: THPbData );

PROCEDURE CiStdText( cCount: TC; p: TP; ptNumer, ptDenom: Point );

(* These two subs are in the interface because there are called for defaulting in CiOpen. *)
(* They are also called be some of the procedures listed above. *)
PROCEDURE CiDftFont (famIn: TFam);
PROCEDURE CiDftFace (seteface: Style);

{ ---------------------------------------------------------------------- }
                           Implementation
{ ---------------------------------------------------------------------- }

{$S Ci}

{$IFC fSymOK} {$D+} {$ELSEC} {$D-} {$ENDC}
{$IFC fdbgok} {$R+} {$ELSEC} {$R-} {$ENDC}

{ ---------------------------------------------------------------------- }
FUNCTION  CiSubs(ch: CHAR): CHAR;                                       FORWARD;
{ ---------------------------------------------------------------------- }
FUNCTION  CiChkHRule{ VAR cmd: TPbCommand; hPbData: THPbData ): TF};
BEGIN
   CiChkHRule := FALSE; {Don't image rules for now.}
END;
{ ---------------------------------------------------------------------- }
FUNCTION  CiChkText { VAR cmd: TPbCommand; hPbData: THPbData ): TF};
{Clip & break at blanks.}
VAR ch: CHAR;
    fShowText: TF;
BEGIN
  WITH cmd, ThePort^.portRect DO BEGIN
    ch := hPbData^^[cFirstChar];
    fShowText := (v >= top) AND (v <= bottom) AND (cFont <> marker);
    IF fShowText THEN BEGIN
       fShowText := (ch <> ' ');
       IF (ch < ' ') OR (ch > CHR(126)) THEN BEGIN
         IF ord(ch) IN charSubs THEN hPbData^^[cFirstChar]:=CiSubs(ch)
         ELSE IF NOT (ord(ch) IN charEuropean) THEN hPbData^^[cFirstChar]:='?';  {DC 7/7/83}
         {else leave it alone, it will be handled.}
       END;
    END;
    CiChkText:= fShowText;
  END;
END;
{ ---------------------------------------------------------------------- }

FUNCTION  CiChkVRule{ VAR cmd: TPbCommand; hPbData: THPbData ): TF};
BEGIN
  CiChkVRule := FALSE; {Don't image rules for now.}
END;
{ ---------------------------------------------------------------------- }
PROCEDURE CiDftFace {seteface: Style};
BEGIN
   WITH CiDraft DO BEGIN
      fEmph := bold IN seteface;
      fUL   := underline IN seteface;
      CiSetEmph (fEmph); CiSetUL (fUL);
      face := seteface;
   END;
END;
{ ---------------------------------------------------------------------- }
PROCEDURE CiDftFont {famIn: TFam};
BEGIN
  WITH CiDraft DO BEGIN
     fWide := FALSE;
     CASE famIn OF
        p10tile, p10cent: tybpi := tybpi80;
        p12tile, p12cent: tybpi := tybpi96;
        p15tile:          tybpi := tybpi120;
        p20tile:          tybpi := tybpi136;
        cent12, tile12:   tybpi := tybpi144;
        tile18, cent18:   BEGIN tybpi :=tybpi160; fWide := TRUE; END;
        tile24, cent24:   BEGIN tybpi :=tybpi144; fWide := TRUE; END;
        OTHERWISE         tybpi := tybpi120; {Default: make small}
     END;
     CiSetBpi (tybpi); CiSetWide (fWide);
     fam := famIn;
  END;
END;
{ ---------------------------------------------------------------------- }
PROCEDURE CiDumpHRule{ VAR cmd: TPbCommand; hPbData: THPbData };
BEGIN
  (* ----- NOP-Shouldnt happen!. *)
END;
{ ---------------------------------------------------------------------- }
PROCEDURE CiDumpText { VAR cmd: TPbCommand; hPbData: THPbData };
VAR cLineHt, hFnt, hResBM, hResFnt: TC;
BEGIN
  WITH cmd DO BEGIN
     (* ----- Adjust (h,v) for rPrintable.*)
     h:=h - HoffSet{.PrBuf};
     v:=v - VOffSet{.PrBuf};

     (* ----- Go to the correct v location. Also reset state each text line.*)
     CiSetBiDir(TRUE);
     CiDftFont (cFont);
     CiDftFace (seteface);

     (* ----- Select matching CItoh font.*)
     IF (cFont<>CiDraft.fam    ) THEN CiDftFont (cFont);
     IF (seteface<>CiDraft.face) THEN CiDftFace (seteface);

     (* ----- Scale h to the font's metric & tab.*)
     hResBM  := CiHRes(tybpiBM);
     hResFnt := CiHRes(CiDraft.tybpi);
     hFnt    := h * ORD4(hResFnt) DIV hResBM;
     CiPrTab (hFnt);

     (* ----- Print the string.*)
     CiPrText ( POINTER( ORD(hPbData^)+cFirstChar ), cLength );
     If (cLength >0) then fPgDirty := TRUE;

  END;
END;
{ ---------------------------------------------------------------------- }
PROCEDURE CiDumpVRule{ VAR cmd: TPbCommand; hPbData: THPbData };
BEGIN
  (* ----- NOP-Shouldnt happen!. *)
END;
{ ---------------------------------------------------------------------- }
PROCEDURE CiStdText{ cCount: TC; p: TP; ptNumer, ptDenom: Point };
Var FInfo:FontInfo;
BEGIN
(*   GetFontInfo( FInfo );
  With ThePort^,FInfo do
     If  ( ((PnLoc.V+Ascent>=PortRect.Top) And (PnLoc.V-Descent<=PortRect.Bottom))
          And  Not(CiAbort)  ) then PbText( cCount, p );  *)
  If Not(CiAbort) then PbText( cCount, p );
END;
{ ---------------------------------------------------------------------- }
FUNCTION  CiSubs{(ch: CHAR): CHAR};
BEGIN
  CASE ord(ch) OF
    $82:             CiSubs := 'C';      {for C cedilla}
    $83:             CiSubs := 'E';      {for E acute}
    $87, $89, $8B,
    $BB:             CiSubs := 'a';      {for a acute, circumflex, tilde,
                                              superscript}                   {DC 8/10/83}
    $90, $91:        CiSubs := 'e';      {for e circumflex, umlaut}
    $92, $94, $95:   CiSubs := 'i';      {for i acute, circumflex, umlaut}
    $97, $99, $9B,
    $BC:             CiSubs := 'o';      {for o acute, circumflex, tilde,
                                              superscript}                   {DC 8/10/83}
    $9C, $9E:        CiSubs := 'u';      {for u acute, circumflex}
    $A5:             CiSubs := chr($A1); {degree symbol for bullet}
    $AB:             CiSubs := '''';     {for acute alone}
    $AE:             CiSubs := chr($80); {A umlaut for AE ligature}          {DC 8/10/83}
    $AF:             CiSubs := chr($85); {O umlaut for O slashed}            {DC 8/10/83}
    $B4:             CiSubs := 'Y';      {for Yen sign}
    $BE:             CiSubs := chr($8A); {a umlaut for ae ligature}          {DC 8/10/83}
    $BF:             CiSubs := chr($9A); {o umlaut for o slashed}            {DC 8/10/83}
    $C4:             CiSubs := 'f';      {for function sign}
    $C7:             CiSubs := '<';      {for "<<" character}                {DC 6/15/83}
    $C8:             CiSubs := '>';      {for ">>" character}                {DC 6/15/83}
    $CA:             CiSubs := ' ';      {for non-breaking space}            {DC 5/11/83}
    OTHERWISE        CiSubs := '!';      {This shouldn't happen!}
  END;
END;
{ ---------------------------------------------------------------------- }
END.
{***************************SUMMARY OF CIPROCS CHANGES FOR INTERNATIONAL VERSION*******************
 All these changes have to do with making draft printing handle non-USASCII characters.  CiDev
 assumes that all characters to be draft-printed by CiDev are ones that can in fact be printed on
 the C.Itoh; here we guarantee this.

   0. fPrEnhanceDraft flag controls these changes.  Set equal to fPrInternational (from PrStd).
   1. Set variable charSubs added; Codes of chars that cannot be printed on the C.Itoh, but can
      be reasonably substituted.  Initialized in CiOpen.
   2. CiSubs added to perform substitutions.  Note: 1-to-1 substitution, in the buffer.
   3. CiChkText calls CiSubs if char code is in charSubs.
   4. zCiInit added.
***************************************************************************************************}

{
UNIT CiProcs CHANGE LOG:
     10/25/83 EZ: TSeteface changed to Style in CiDftFace. CiOpen - bottleneck reference changed for
                  new quickdraw format.
      9/06/83 DC: zCiInit: new proc to call CiDevInit.
      8/10/83 DC: CiSubs: Change handling of O and o slashed, AE and ae ligatures (back the way
                  they were before, using umlauts!).
      7/08/83 DC: Changed name of zCiSubs to CiSubs.
      7/07/83 DC: Replaced charForeign with charEuropean (from CiDev).  No reusability problem.
      6/15/83 DC: CiOpen: add "<<" and ">>" characters ($C7, $C8) to charSubs initialization.
      6/15/83 DC: zCiSubs: add substitutions for "<<" and ">>" characters.
      5/18/83 DC: Made draft printing of special chars depend on flag fPrEnhanceDraft (from CiDev)
                  instead of fPrEurope.
      5/11/83 DC: CiOpen: Add the "non-breaking space" to charSubs initialization.
                  zCiSubs: Add substitution of space for "non-breaking space" ($CA).
                  zCiSubs: Change handling of O and o slashed, AE and ae ligatures.
      4/29/83 DC: The following mods are conditional, based on fPrEurope:
                    Add charSubs & charForeign variables.
                    Add zCiSubs function.
                    CiChkText: add conditional code to call zCiSubs.
                    CiOpen: add conditional code to initialize charSubs & charForeign.
      4/13/83 EZ: CiOpen: Changed fPEStop to be true for feedFanfold instead of feedCut. PR Bug #008.
      2/19/83 OD: CiMetrics: LoRes UL params = screen's.
      2/ 9/83 OD: marker removed from draft.
                  CiDftFont: tried for better fit.
      2/ 2/83 OD: fPgDirty added to detect blank pages.
      1/31/83 OD: vPaperBits-(vPaperBits-cGap) MOD 16 => (vPaperBits DIV 16)*16.
                  rSafe added to CiMetrics.
                  CiChkText: add marker font hack.
      1/30/83 OD: Add CiClose to CiCleanup.
      1/24/83 OD: remove cGap from page calcs in CiMetrics.
}

ÿ