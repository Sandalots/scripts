(*
*)
{               NOTE: The above compiles this program.                   }
{ ---------------------------------------------------------------------- }
{                Copyright 1983,1984 Apple Computer Inc.                 }
PROGRAM Print;

{ ---------------------------------------------------------------------- }

USES {$U LibOS/SysCall         }  SysCall,
     {$U LibPM/PMDecl          }  PMDecl,
     {$U LibHW/Hwint           }  Hwint,
     {$U LibSM/UnitStd         }  UnitStd,
     {$U LibSM/UnitHz          }  UnitHz,
     {$U LibQD/Storage         }  Storage,
     {$U LibQD/QuickDraw       }  QuickDraw,
     {$U LibFM/FontMgr         }  FontMgr,
     {$U LibQD/GrafUtil        }  GrafUtil,

     {$U LibWM/Events          }  Events,
     {$U LibWM/Menus           }  Menus,
     {$U LibWM/Windows         }  Windows,
     {$U LibWM/Folders         }  Folders,
     {$U libSB/WmlStd          }  WmlStd,
     {$U libSB/WmlCrs          }  WmlCrs,
     {$U LibAM/AlertMgr        }  AlertMgr,

     {$U LibPr/PrStdInfo       }  PrStdInfo,
     {$U LibPr/PrStdProcs      }  PrStdProcs,
     {$U LibPr/Queues          }  Queues,
     {$U LibPr/PrEvents        }  PrEvents,
     {$U LibPr/PrDlgMgr        }  PrDlgMgr,
     {$U LibPr/PrMgr           }  PrMgr,
     {$U LibPr/PrFileProcs     }  PrFileProcs,

     {$U-}
     {$U LibPr/CiGlobals       }  CiGlobals,
     {$U LibPr/CiStdQDProcs    }  CiStdQDProcs,
     {$U LibPr/CiProcs         }  CiProcs,
     {$U LibPr/CiDlg           }  CiDlg;         {!DC 2/14/84}

{ ---------------------------------------------------------------------- }

{$IFC fSymOK} {$D+} {$ELSEC} {$D-} {$ENDC}
{$IFC fdbgok} {$R+} {$ELSEC} {$R-} {$ENDC}

{ ---------------------------------------------------------------------- }
(*
    Usage of TheHeap that is created in this process.
              QDDataSeg ---- Opened in CiPrint
               +-----------------------------+
               !  TheHeap Owned by CiPrint   !
               !  +------------------------+ !
               !  !  hPfPages              ! !
               !  !  hPic                  ! !
               !  !  QuickDraw's Usage     ! !
               !  !  hPbData       {PrBuf} ! !
               !  !  hPbSorted     {PrBuf} ! !
               !  !  hPrBuf        {PrBuf} ! !
               !  !  hmpPbCommands {PrBuf} ! !
               !  !                        ! !
               !  +------------------------+ !
               +-----------------------------+
*)
{ ---------------------------------------------------------------------- }
{ ---------------------------------------------------------------------- }
{DIRECTORY}
PROCEDURE PrSysTerm  (environPtr : P_env_blk; excepDataPtr : P_ex_data);FORWARD;
PROCEDURE CallService(environPtr : P_env_blk; excepDataPtr : P_ex_data);FORWARD;
{ ---------------------------------------------------------------------- }
{$S CiDlg}
PROCEDURE CallService{(environPtr : P_env_blk; excepDataPtr : P_ex_data)};
VAR
   pevData     : ^PrEventBlk;
   Message     : ^PrMessage;
   cError      : TC;          {!DC 2/15/84}
   excepName   : T_Ex_Name;   {!DC 2/15/84}
   AlertResult : Integer;
BEGIN
   pevData:=POINTER(excepDataPtr);
   Message:=@pevData^.eventBytes[0];
   CallerId:=pevData^.event_header.send_pid;
   CASE Message^ OF
      EPrintDialog: WITH pPrMgrDS^, DlgArgs DO BEGIN     {!DC 2/14/84}
        (* ----- Make sure that the port field from the document reflects what the parent process *)
        (* ----- knows is connected. *)
        ThePrRec.PrInfo.Port:=CiPrinter^^.PrRec.PrInfo.Port;
        BIND_DATASEG(cError,DDSRefNum);                              {!BH 5/04/84}
        ReturnOpt := FInsDlg (ThePrRec, newStyle, menuSuppress);
        UNBIND_DATASEG(cError,DDSRefNum);                              {!BH 5/04/84}
        (* ----- Update the shared PrRec for the dialogs. *)
        IF NewStyle THEN CiPrinter^^.PrRec:=ThePrRec;
        (* ----- fBackRound=True means that the backround process is running the dialog. This is a *)
        (* ----- flag that is used by the applications.                                            *)
        fBackground:=FALSE;
        ACTIVATE_PROCESS (cError, CallerId, FALSE);  {!DC 2/16/84}
      End;

      ESettingsDialog: WITH pPrMgrDS^, DlgArgs DO BEGIN  {!DC 2/14/84}
        (* ----- Make sure that the port field from the document reflects what the parent process *)
        (* ----- knows is connected. *)
        ThePrRec.PrInfo.Port:=CiPrinter^^.PrRec.PrInfo.Port;
        BIND_DATASEG(cError,DDSRefNum);                              {!BH 5/04/84}
        ReturnOpt := FFmtDlg (ThePrRec, newStyle, maxHigh, maxWide, minHigh, minWide, noSpecSize);
        UNBIND_DATASEG(cError,DDSRefNum);                              {!BH 5/04/84}
        (* ----- Update the shared PrRec for the dialogs. *)
        IF NewStyle THEN CiPrinter^^.PrRec:=ThePrRec;
        (* ----- fBackRound=True means that the backround process is running the dialog. This is a *)
        (* ----- flag that is used by the applications.                                            *)
        fBackground:=FALSE;
        ACTIVATE_PROCESS (cError, CallerId, FALSE);  {!DC 2/16/84}
      End;

     (* -----  CiAbort is defaulted in CiDocBegin, set to true only by CallService, CiAlerts or CiOpenFile.           *)
      EPrintTerminate: Begin
        CiAbort:=True;
      End;
   END;
   excepName := ListenCall.ChName;  {!DC 2/15/84}
   excepName[0]:=CHR(8);            {!DC 2/15/84}
   Enable_Excep(cError, excepName); {!DC 2/15/84}
END;

{ ---------------------------------------------------------------------- }
{$S   }
PROCEDURE PrSysTerm {environPtr : P_env_blk; excepDataPtr : P_ex_data};
VAR
   Error : INTEGER;
BEGIN
   fBackground:=FALSE;
   ACTIVATE_PROCESS (error, CallerId, FALSE); {!DC 2/17/84}
   PsNonPreEmptive; DialogHeight(0,False); PsPreEmptive;
   PrCloseChan(Listen);
   PrCloseChan(ListenCall);
   IF (ciPrinter <> NIL) THEN BEGIN
      ciPrinter^^.SonID:=PrNilProcess;
      (* ----- Close and UnMount the printer port. *)
      PsClosePort(error,CiPrinter^^.PrRec.PrInfo.port);
   END;
   PrMgrDSUnBind(Error);  {--May not reference anything is the data seg after this}

   (* ---- Close the Quickdraw heap data segment. *)
   Close_DataSeg(error,QDDSRec.RefNum);

END;

{ ---------------------------------------------------------------------- }
{$S CiPrint}
{ ---------------------------------------------------------------------- }
{                            Main  Procs                                 }
{ ---------------------------------------------------------------------- }
{$S   }
PROCEDURE InitMain;
VAR Error    : Integer;
    excepName: T_Ex_Name;
    knil     : TProc;
    DSInfo   : DSInfoRec;
BEGIN
   ciPrinter:=NIL;
   CallerId:=ActiveProcess;
   excepName := 'SYS_TERMINATE';
   Declare_Excep_Hdl (error, excepName, @PrSysTerm);
   IF Error <> 0 THEN Exit(Print);


   (* ----- Initialize QuickDraw and open the Window Manager.*)
   (* ----- OpenWM must be called before any other creation of any other datasegment due to the *)
   (* ----- concurrency problem. *)
   InitGraf ( @thePort);
   PsNonPreEmptive;
     OpenWM;
   PsPreEmptive;

   (* ----- Make the data segment for QuickDraw's TheHeap. *)
   With QDDSRec{.CiPrint} do Begin
     SegName  :=QDDSName{.CiGlobals};
     InitSize :=QDDSSize{.CiGlobals};
     LDSN     :=QDDSLDSN{.CiGlobals};
     Make_DataSeg(error,SegName,InitSize,InitSize,refnum,
                                  BegAddr,ldsn,ds_private);
     If Error<>0 then Exit(Print);

     (* ----- Initialize TheHeap. *)
     knil:=POINTER(1);
     INFO_DATASEG(Error,RefNum,DSInfo);
     InitHeap(Pointer(Ord(BegAddr)),Pointer(Ord(BegAddr)+DSInfo.mem_size), KNil);
   End{WITH};


   (* ----- Open up the port and set some defaults.*)
   OpenPort ( Pointer(Ord4(@PrPort.gport)) );
   PrPort.gport.grafprocs  :=@PrPort.gProcs;

   (* ----- Set up the Printing bottle neck procedures.*)
   SetStdProcs(PrPort.gProcs);
   With PrPort.gProcs do Begin
      TextProc   :=@PrStdText;
      LineProc   :=@PrStdLine;
      RectProc   :=@PrStdRect;
      RRectProc  :=@PrStdRRect;
      OvalProc   :=@PrStdOval;
      PolyProc   :=@PrStdPoly;
      RgnProc    :=@PrStdRgn;
      BitsProc   :=@PrStdBits;
      ArcProc    :=@PrStdArc;
      GetPicProc :=@CiGetPicProc;
   End;
   PrPort.gport.FillPat:=White;


   (* ----- Init PrDlg stuff *)
   PrBgdInit;
   CiInitAlerts(Error);
   IF (Error <> 0) THEN Exit(Print);

   (* ----- If CiDLgInit has problems then we gotta bail out! EZ 4/10/84 *)
   If Not(CiDlgInit) then Exit(Print);     {!DC 2/14/84}

   PrMgrDSBind(Error);
   IF (Error <> 0) THEN Exit(Print);

   (* ----- Initialize and Make/Open the eventchannels. *) {!EZ 4/10/84 - moved from beginning of proc.}
   PrOpenChan(Listen,TRUE{Receiver});
   PrOpenCall(ListenCall,@CallService);

   PrDeBug :=False;

   OpenFile:=False;
   InitDeBug;

END;
{ ---------------------------------------------------------------------- }
{$S CiPrint}
PROCEDURE MainLoop;
VAR Error    : Integer;
    FileId   : TPrFileId;
    PrRec    : TPrRec;
    DevRec   : TDevRec;
    pDevRec  : ^TDevRec;
BEGIN
   REPEAT
      Case PrAccept(Listen) of
        EPrintRequest: With Listen Do Begin
           FileId.LongId:=EvBlock.event_Text[1]; {--The File name ID}
           GetFileName(FileId);
           Case FileId.BlockNum of
             0    : Begin
                      If fileid.Id<>LastJob.JobId then Begin
                        LastJob.JobId:=FileId.Id;
                        FirstPage:=True;
                      End;
                      CiDocBegin(FileId.sFileName,PrRec,DevRec);
                      CiPrintFile(FileId.sFileName,PrRec,DevRec);
                    End;

             LastBlock : CiDocEnd(Error,PrRec,DevRec);

             Otherwise  CiPrintFile(FileId.sFileName,PrRec,DevRec);
           End;{Case}
           (* ----- CiAbort is defaulted in CiDocBegin, set to true only by CallService, CiAlerts or  *)
           (* ----- CiOpenFile. If it is time to abort then abort. The test for  LastBlock is so that we do *)
           (* ----- not end up calling CiDocEnd twice. *)
           If ((CiAbort) and (FileId.BlockNum<>LastBlock)) then Begin
               CiCleanUp(PrRec,DevRec);
               CiDocEnd(Error,PrRec,DevRec);
            End;
            PrCall(Error,lParentID,EDone,FileID.LongId,EvBlock.event_Text[2]-1,0,0);{--Decrement number of copies}
        End;

        EMetrics: Begin  {--Put metrics in the shared dataseg for our device}
           CiDfMetrics  ( PrRec, TRUE, FALSE, FALSE); {!DC 2/15/84}
           CiPrinter^^.PrRec:=PrRec;
           CiPrinter^^.PrRec.PrInfo.PrinterId:=Listen.EVBlock.event_Text[2];
           CiPrinter^^.PrRec.PrInfo.Port     :=Listen.EVBlock.event_Text[3];
        End;

        ETalkToMe: Begin  {--Update the printer handle.  This should be the first event after start up}
           ciPrinter:=POINTER(Listen.EvBlock.event_text[1]);
           ciPrinter^^.SonID:=MY_ID;  {--Lets everyone know we're alive and well}

           (* ----- Make Lastjob equal to known state. *)
           pDevRec:=Pointer(Ord(@CiPrinter^^.PrRec.PrDevInfo));
           With LastJob do Begin
             Feeder:=pDevRec^.CiCfg.feeder;
             PgSize:=CiPrinter^^.PrRec.PrInfo.PgSize;
             JobId:=-1;
           End;

        End;

        Otherwise {--We ignore anything else };
      End; {Case}
   UNTIL FALSE;
END;

{ ---------------------------------------------------------------------- }
{$S   }
BEGIN
   InitMain;
   MainLoop;
END.

{ ---------------------------------------------------------------------- }
{
PROGRAM Print CHANGE LOG:
           5/04/84 : BH -- Change to allow dialog dataseg to bound and unbound
}
ÿ