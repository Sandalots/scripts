(*
$Exec
PLibPr/Pr.Sam.text
{no listing}
LibPr/PrTemp
GLibPr/PrTemp
LibPr/PrTemp
FRLibPr/Pr.Sam.obj
Y
Q

LPrTempTemp
*IOsPasLib
*Sys1Lib
*Sys2Lib


LibPr/Pr.Sam
$EndExec
*)
{     /OS VERSION/      NOTE: The above compiles & loads this program.   }
{ ---------------------------------------------------------------------- }

PROGRAM WmSam;

USES {$U LibOS/SysCall    }  SysCall,   {The basics.}
     {$U LibSM/UnitStd    }  UnitStd,
     {$U LibSM/UnitHz     }  UnitHz,
     {$U LibPM/Decl     }  PMDecl,
     {$U LibPM/PMM        }  PMM,
     {$U LibHW/HWInt      }  HWInt,

     {$U LibFM/FontMgr    }  FontMgr,   {Fonts & LisaGraf.}
     {$U LibQD/Storage    }  Storage,
     {$U LibQD/QuickDraw  }  QuickDraw,
     {$U LibFM/FMgrUtil   }  FMgrUtil,

     {$U LibWM/WM.Events  }  Events,
     {$U LibWM/WM.Menus   }  Menus,     {The Window Manager.}
     {$U LibWM/WM.Folders }  Folders,
     {$U LibWM/WmlStd     }  WmlStd,
     {$U LibWM/WmlCrs     }  WmlCrs,
     {$U LibAM/AlertMgr   }  AlertMgr,

     {$U LibPr/PrStd      }  PrStd,     {The PrintShop.}
     {$U LibPr/CiProcs    }  CiProcs,
     {$U LibPr/PrProcs    }  PrProcs,
     {$U LibPr/PrMgrUtil  }  PrMgrUtil,
     {$U LibPr/PrMgr      }  PrMgr;

CONST

   spMenus = 'Pr.SamText.text';

   muFst       = 2;
   muLst       = 6;

{ ---------------------------------------------------------------------- }

VAR

   rgMuInfo:  ARRAY[muFst..muLst] OF MenuInfo;
   pwnd1,  pwnd2:  WindowPtr;
   prprf1, prprf2: TPrprf;
   pprprf:  TPprprf;

   fMeDbg, fAbort, fFanFold, fBgdFmMu, fFakeDiskFull, fDrawRSafe, fDrawBands,
   fPrNone, fBgdProcess, fTest11Inch: TF; {fMeDbg->fPrDbg in Balls.}

   fMyMgrDbg, fMyDevDbg, fMyProcDbg: TF;  {dbg flags for print code}

   cMyRefNum: TC;

{ ---------------------------------------------------------------------- }
{                        Aborts/Errors                                   }
{ ---------------------------------------------------------------------- }

PROCEDURE SamGrafError(cErr: TC);
BEGIN
   WRITELN('Graf Err from Sam; cErr=', cErr:1);
END;

{ ---------------------------------------------------------------------- }

PROCEDURE SamCkAlert {};
BEGIN
IF fPrMgrDbg THEN WRITELN ('PrCkAlert: alertError=', alertError:1);
   IF alertError <> 0 THEN BEGIN
      WRITELN ('PrCkAlert: alertError=', alertError:1);
   (*   TellFiler (alertError, initFailed, internalError); *)
      IF alertError > 0 THEN HALT;
   END;
END;

{ ---------------------------------------------------------------------- }

PROCEDURE FlashPort ( pWnd: WindowPtr; c: TC );
VAR i: TC;
BEGIN
   IF fMeDbg THEN WRITELN ('FlashPort');
   SetPort (pWnd);
   FOR i := 1 TO c DO BEGIN
      InvertRect (pWnd^.portRect);
      PauseEvt (10);
      InvertRect (pWnd^.portRect);
      PauseEvt (10);
   END;
END;

{ ---------------------------------------------------------------------- }

PROCEDURE WrtPt ( sp: TSp; h,v: TC; fCR: TF );
BEGIN
   WRITE (  sp, '(', h:1, ',', v:1, ')' );
   IF fCR THEN WRITELN;
END;

{ ---------------------------------------------------------------------- }

PROCEDURE WrtRect ( sp: TSp; r: Rect; fCR: TF );
BEGIN
   WRITE (  sp,
            '[ ', r.left:1,
            ', ', r.top:1,
            ', ', r.right:1,
            ', ', r.bottom:1,
            ' ]' );
   IF fCR THEN WRITELN;
END;

{ ---------------------------------------------------------------------- }

PROCEDURE WrtGP (sp: TSp; gp: GrafPtr);
BEGIN
   WITH gp^ DO BEGIN
       WRITE    (sp, ' portBits:BA=', ORD(portBits.baseAddr):1,
                 ',rowBytes=', portBits.rowBytes:1);
       WrtRect(',bounds=', portBits.bounds, TRUE);
       WrtRect('  portRect=', portRect, TRUE);
       WrtPt  ('  Pen: loc=', pnLoc.h, pnLoc.v, FALSE );
       WrtPt  (',size=', pnSize.h, pnSize.v, FALSE );
       WRITELN  (',mode=', ORD(pnMode):1 );
   END;
END;

{ ---------------------------------------------------------------------- }

PROCEDURE WrtPrCfg(sLabel: TSp; prcfg: TPrcfg);
BEGIN
WITH prcfg, pgSize DO BEGIN
   WRITE (sLabel, 'port=', ORD(port):1, ',feeder=', ORD(feeder):1 );
   WRITE ('   pgSz=', cWidth:1, ',', cHeight:1, ',edev=', ORD(edev):1 );
   IF edev=edevTyper THEN WRITE (',ewhcl=', ORD(ewhcl):1);
   WRITELN;
END;
END;

{ ---------------------------------------------------------------------- }

PROCEDURE WrtPrDev(sLabel: TSp; adev: TAdev);
BEGIN
WITH adev DO BEGIN
   WRITE (sLabel, 'edev=', ORD(edev):1, ',fSub=', NOT fOffline );
   CASE edev OF
      edevTyper: WRITE (',ewhcl=', ORD(ewhcl):1, ',fSpoke=', fSpoke);
      edevDotMx: WRITE (',fHiRes=', fHiRes, ',fPortrait=', fPortrait);
   END;
   WRITELN;
END;
END;

{ ---------------------------------------------------------------------- }

PROCEDURE WrtPrStl(sLabel: TSp; prstl: TPrstl);
BEGIN
WITH prstl DO BEGIN
   WRITE (sLabel, 'pgSz=', pgSize.cWidth:1, ',', pgSize.cHeight:1 );
   WrtPrDev(',', adev);
END;
END;

{ ---------------------------------------------------------------------- }

PROCEDURE WrtPrIns(sLabel: TSp; prins: TPrins);
BEGIN
WITH prins DO BEGIN
   WRITELN (sLabel,
            'PgFst=',      cPgFst:1,
            ',PgLst=',     cPgLst:1,
            ',cCopies=',   cCopies:1,
            ',hTrunc=',    hTrunc:1,
            ',vTrunc=',    vTrunc:1,
            ',fDraft=',    fDraft  );
   WrtPrStl ('   stl: ', prstl);
   WrtPrCfg ('   cfg: ', prcfg);
END;
END;

{ ---------------------------------------------------------------------- }

PROCEDURE WrtPrPrf(sLabel: TSp; prprf: TPrprf);
BEGIN
WITH prprf DO BEGIN
   WRITELN (sLabel,
            'cVersion=',   cVersion:1,
            ',iArb=',      iArb:1,
            ',edev=',      ORD(edev):1 );
   WrtPrStl ('   stlDotMx: ', rgstl[edevDotMx]);
   WrtPrStl ('   stlTyper: ', rgstl[edevTyper]);
   WrtPrStl ('   stlLaser: ', rgstl[edevLaser]);
END;
END;

{ ---------------------------------------------------------------------- }

PROCEDURE WrtPrInfo(sLabel: TSp; prinfo: TPrinfo);
BEGIN
WITH prinfo DO BEGIN
   WRITELN (sLabel,
            ' device=',      device:1,
            ',tyScan=',      ORD(tyScan):1,
            ',cPatScale=',   cPatScale:1,
            ',cULThick=',    cULThick:1,
            ',cULOffset=',   cULOffset:1,
            ',cULShadow=',   cULShadow:1 );
   WrtRect ('   rRoller: ',    rRoller,      FALSE);
   WrtRect (',rPrintable: ',   rPrintable,   TRUE);
   WrtRect ('   rPaper: ',     rPaper,       FALSE);
   WrtRect ('   rSafe: ',      rSafe,        FALSE);
   WrtPt   ('; h,v res: ',  hRes, vRes,     TRUE);
END;
END;

{ ---------------------------------------------------------------------- }

PROCEDURE WrtPrPmWd (sLabel: TSp; devtype: TDeviceType; devInfo: TDeviceInfo; fCR: TF);
VAR aPrPmWd: TAPrPmWd;
BEGIN
   aPrPmWd.devinfo := devInfo;
   WITH aPrPmWd.prpmwd DO BEGIN
      WRITE (sLabel);
      CASE devtype OF
         Typer:     WRITE ('Ty,wh=', ORD(ewhcl):1);
         DMPrinter: WRITE ('DM');
      END;
      WRITE (',feed=', ORD(feeder):1, ',iPg=', ORD(iPgSz):1);
   END;
   WRITE ('[', ORD(devInfo):1, ']');
   IF fCr THEN WRITELN;
END;

{ ---------------------------------------------------------------------- }
{                            Msc Stuff                                   }
{ ---------------------------------------------------------------------- }
(*
FUNCTION CbMyHzMore (hz: Thz; cbNeeded: TC): TC;
VAR lNewMem, lNewDisk: TL;
    cErr: TC;
    DSInfo: DSInfoRec;
BEGIN
   WRITE ('EXPANDING theHeap, cbNeeded=', cbNeeded:4);
   INFO_DATASEG(cErr, cMyRefNum, DSInfo);
   SIZE_DATASEG(cErr, cMyRefNum, cbNeeded, lNewMem, 0, lNewDisk);
   IF cErr > 0 THEN BEGIN
      WRITELN('CbPrMgrMore: SIZE_DATASEG error=', cErr);
      HALT;
   END;
   WRITELN (', cbNewMem=', lNewMem:6, ', cbAdded=', (lNewMem-DSInfo.mem_size):4);
   CbMyHzMore := lNewMem - DSInfo.mem_size;
END;
*)
{ ---------------------------------------------------------------------- }

PROCEDURE MyHeapMake;
CONST cbMyHzFst   = 16000;
      ldsnMyHz    = 5;
VAR   pStart, pLimit: TP;
      cErr:       TC;
      lSegptr:    LONGINT;
      sSegname:   PathName;
      DSInfo:   DSInfoRec;
BEGIN

   sSegname := 'HeapData';
   MAKE_DATASEG(cErr, sSegName, cbMyHzFst, 0{Resident},
                cMyRefNum, lSegPtr, ldsnMyHz, DS_Private);

   IF cErr <> 0 THEN BEGIN
      WRITELN('MyHeapMake: MAKE_DATASEG error=', cErr);
      IF cErr > 0 THEN HALT;
   END;
   INFO_DATASEG(cErr, cMyRefNum, DSInfo);

   pStart   := POINTER(lSegPtr);
   pLimit   := POINTER(lSegPtr+DSInfo.mem_size);
   InitHeap(pStart, pLimit, NIL{@CbMyHzMore});
END;


{ ---------------------------------------------------------------------- }

PROCEDURE MyPMDefault (devtype: TDeviceType; VAR devInfo: TDeviceInfo; fFFold: TF);
VAR aPrPmWd: TAPrPmWd;
BEGIN
   PrPMDefault (devtype, aPrPmWd.devinfo);
   IF fFFold   THEN aPrPmWd.prpmwd.feeder := feedFanfold
               ELSE aPrPmWd.prpmwd.feeder := feedCut;
   devInfo := aPrPmWd.devinfo;
END;

{ ---------------------------------------------------------------------- }

PROCEDURE MyPMInit;
VAR devInfo: TDeviceInfo;
    fDbg: TF;
BEGIN
   WRITELN ('Sam: MyPMInit; setting ports:');

   IF fPrNone THEN BEGIN
      WRITELN ('     slot22, serialB both set to empty.');
      PMWriteConfig (slot22,  devNone, 0);
      PMWriteConfig (serialB, devNone, 0);
   END
   ELSE BEGIN
      MyPMDefault   (DMPrinter, devInfo, fFanFold);
      WrtPrPmWd ('     slot22=', DMPrinter, devinfo, TRUE);
      PMWriteConfig (slot22, DMPrinter, devInfo);

      MyPMDefault   (Typer, devInfo, fFanFold);
      WrtPrPmWd ('     serialB=', Typer, devinfo, TRUE);
      PMWriteConfig (serialB, Typer, devInfo);
   END;
(*
   IF fPMWrite THEN PmWrite;
   IF CPrAvail=0 THEN PrAlertNone;
*)
END;

{ ---------------------------------------------------------------------- }

PROCEDURE MyPMRead;
VAR i: TC;
    prPmCfg: TPrPmCfg;
BEGIN
   FOR i := ORD (Parallel) TO ORD (serialB) DO WITH prPmCfg DO BEGIN
      aPrPort.b := i;
      PMReadConfig (aPrPort.port, devtype, aPrPmWd.devinfo);
      IF devtype IN [DMPrinter, Typer , Laser] THEN BEGIN
         WrtPrPmWd ('Sam: MyPMRead:', devtype, aPrPmWd.devinfo, FALSE);
         WRITELN   (' port=', i:1);
      END;
   END;
END;

{ ---------------------------------------------------------------------- }

FUNCTION  PMakeFldr (spTitle: Str255;
                     left,top,right,bottom: TC;
                     fam: TFam; fVis: TF ): WindowPtr;
VAR r:     Rect;
    pWnd:  WindowPtr;
BEGIN
   SetRect (r,left,top,right,bottom);
   pWnd  := NewFolder (r,spTitle,fVis,NIL,0,My_ID, 80); {Does this set port???}
   SetPort (pWnd);
   TextFont (fam);
   PMakeFldr := pWnd;
END;

{ ---------------------------------------------------------------------- }

PROCEDURE FillWTitle (pwnd: WindowPtr);
VAR i, h, v, dv: TC;
    str: Str255;
BEGIN
   IF fMeDbg THEN WRITELN ('FillWTitle');
   {Port set by event loop.}
   EraseRect(pwnd^.portRect);
   GetFldrTitle (pwnd, str);
   str := CONCAT ( str,' ' );
   WHILE LENGTH (str) < 80 DO str := CONCAT ( str,str );
   str[0]:=CHR(80);
   h := 0; dv := 16; v := dv-2;
   FOR i := 1 TO 40 DO BEGIN
      MoveTo ( h, v );
      DrawString (str);
      v := v + dv;
   END;
END;

{ ---------------------------------------------------------------------- }

PROCEDURE NewStlNote (sp: TSp);
VAR prinfo: TPrInfo;
BEGIN
   FlashPort (activeFolder, 5);
   PrMetrics (pprprf^, prinfo);
   WrtPrInfo(sp, prinfo);
END;

{ ---------------------------------------------------------------------- }
{                            Printing                                    }
{ ---------------------------------------------------------------------- }

PROCEDURE FlipPrProcess;
VAR
    timeStrt: LongInt;
    cErr:     TC;
    spPath:   PathName;
    spEntry:  NameString;
BEGIN
   fBgdProcess  := NOT fBgdProcess;
   timeStrt := Time;
   IF fBgdProcess THEN BEGIN
      spPath   := spBackObj;     spEntry  := '';
      MAKE_PROCESS( cErr, prIDBack, spPath, spEntry, 0 );
      IF cErr > 0 THEN BEGIN
         WRITELN('Sam: MAKE_PROCESS, cErr=', cErr:1);
         prIDBack  := prNILProcess;
      END;
      WRITELN('Sam: MAKE_PROCESS time=', ABS (Time-timeStrt):1);
   END
   ELSE BEGIN
      KILL_PROCESS( cErr, prIDBack );
      prIDBack  := prNILProcess;
      IF cErr > 0 THEN WRITELN('Sam: KILL_PROCESS, cErr=', cErr:1);
      WRITELN('Sam: KILL_PROCESS time=', ABS (Time-timeStrt):1);
   END;
END;

{ ---------------------------------------------------------------------- }

PROCEDURE PrMyDoc (prprf: TPrprf; prins: TPrins);
   { Prints the doc. }
LABEL 1, 2, 3, 4, 5, 6;
CONST cChPrLn  = 60;
      cPrPages = 3;
VAR iPg, dvLn, hRes, vRes, cErr: TC;
    rBand, rBall: Rect;
    strDoc, strHdr: Str255;
    faceHdr: TSetEFace;
    famDoc, famHdr: TFam;
    prInfo: TPrInfo;
   {---------------------------------------------------------------------------}
   {               Local printing procs: Filled in by user                     }
   {---------------------------------------------------------------------------}
   PROCEDURE MyDocStart;  {Stuff I do at the start of printing}
   BEGIN
      IF fMeDbg THEN WRITELN ('MyDocStart');
   {Get any formatting information I may need:}
      PrMetrics( prprf, prInfo );
      IF fMyProcDbg THEN WrtPrInfo ('   prinfo=', prinfo);
      hRes := prInfo.hRes;
      vRes := prInfo.vRes;
   {Make a "Document".}
      GetFldrTitle (activeFolder, strDoc);
      (*
      IF fSpool THEN strDoc := CONCAT ( strDoc,' in Bkgnd, ' )
                ELSE strDoc := CONCAT ( strDoc,' in Frgnd, ' );
      *)
      strDoc := CONCAT ( strDoc,' printing, ' );
      WHILE LENGTH (strDoc) < cChPrLn DO strDoc := CONCAT ( strDoc,strDoc );
      strDoc[0] := CHR(cChPrLn);
      WHILE strDoc[LENGTH(strDoc)] <> ',' DO strDoc[0]:=CHR(LENGTH(strDoc)-1);
      strDoc[ LENGTH(strDoc) ] := '.';
      IF activeFolder=pWnd1 THEN famHdr:=Tile18 ELSE famHdr:=Cent18;
   END;

   PROCEDURE MyPageStart; {Stuff I do at the start of a page  }
   BEGIN
      IF fMeDbg THEN WRITELN ('MyPageStart');
   {Set up for the next page.}
      strHdr :=
      'Apple Confidential               Page ';
      CASE iPg OF
         1: BEGIN
            strHdr := CONCAT ( strHdr,'One' );
            IF activeFolder=pWnd1 THEN famDoc:=Tile12 ELSE famDoc:=Cent12;
            faceHdr := [];
         END;
         2: BEGIN
            strHdr := CONCAT ( strHdr,'Two' );
            IF activeFolder=pWnd1 THEN famDoc:=p12Tile ELSE famDoc:=p12Cent;
            faceHdr := [efaceBold];
         END;
         OTHERWISE BEGIN
            strHdr := CONCAT ( strHdr,'Buckle my shoe' );
            faceHdr := [efaceItalic];
            IF activeFolder=pWnd1 THEN famDoc:=p15Tile ELSE famDoc:=p15Tile;
         END;
      END;
   END;

   PROCEDURE PrintMyPage (iPage: Integer; rCurBand: Rect; prinfo: TPrInfo);
   {Code to print the current page     }
   VAR iLn, h, v, i: TC;
       r: Rect;
       finfo:  TFinfo;
   BEGIN
   WITH prinfo DO BEGIN
      IF fMeDbg THEN WRITELN ('PrintMyPage');

      IF fDrawRSafe THEN BEGIN
         PenSize (4,4); PenPat (dkGray);
         FrameRoundRect (rSafe, 30, 30);
      END;

      IF fDrawBands THEN BEGIN
         PenSize (4,4); PenPat (gray);
         FrameRoundRect (rCurBand, 30, 30);
         PenNormal;
         FrameRect (rPrintable);
      END;

      TextFont (famHdr); TextFace (faceHdr);
      IF fTest11Inch THEN BEGIN
         h := hRes DIV 4;  v := vRes DIV 4;
      END
      ELSE BEGIN
         h := hRes;  v := vRes+vRes DIV 2;
      END;
      MoveTo (h, v); DrawString (strHdr);

      TextFont (famDoc); TextFace ([]); GetFontInfo (finfo);
      WITH finfo DO dvLn := Ascent + Descent + Leading;

      h := 2*hRes;  v := 2*vRes;
      SetRect( r, h, v, h+3*hres, v+3*vRes ); ClipRect( r );
      FOR iLn := 0 TO 20 DO BEGIN
         CASE (iLn DIV 3) MOD 5 OF
            0:  TextFace ([]);
            1:  TextFace ([efaceItalic]);
            2:  TextFace ([efaceBold]);
            3:  TextFace ([efaceBold, efaceItalic]);
            4:  TextFace ([efaceUnderLined]);
         END;
         v := v + dvLn; MoveTo (h, v); DrawString (strDoc);
      END;

      SetRect( r, 0, 0, 10*hres, 20*vRes ); ClipRect( r );

      MoveTo( 2*hRes, 2*vRes );
      HRule ( 3*hRes-1 ); VRule( 3*vRes-1 ); HRule ( -3*hRes+1 ); VRule( -3*vRes+1 );

      h := 2*hRes;  v := 6*vRes;
      SetRect(rBall, h, v, h+hRes, v+vRes);
      FillOval (rBall, black);
   END;
   END;

   PROCEDURE MyDocEnd;    {Stuff I do at the end of printing  }
   BEGIN
      IF fMeDbg THEN WRITELN ('MyDocEnd');
   {Close up shop, if necessary (Deallocate, reset state, etc).}
   END;
{------------------------------------------------------------------------------}
BEGIN
   IF fMeDbg THEN WRITELN ('PrMyDoc');

1: PrDocStart ( cErr, prins, 7{ldsn: See Mark.D.'s Memo!});
   CASE PrCheckErr (cErr) OF
      prGoDocStart:  BEGIN PrSetSpool(prins, FALSE); GOTO 1; END;
      prGoStartPage: GOTO 2;
      prGoDumpBand:  GOTO 3;
      prGoEndPage:   GOTO 4;
      prGoDocEnd:    GOTO 5;
      prGoExit:      GOTO 6;
   END;

   MyDocStart;

   FOR iPg := 1 TO cPrPages DO BEGIN

2:    PrStartPage (cErr);
      CASE PrCheckErr (cErr) OF
         prGoDocStart:  BEGIN PrSetSpool(prins, FALSE); GOTO 1; END;
         prGoStartPage: GOTO 2;
         prGoDumpBand:  GOTO 3;
         prGoEndPage:   GOTO 4;
         prGoDocEnd:    GOTO 5;
         prGoExit:      GOTO 6;
      END;
      MyPageStart;

      WHILE PrNextBand (rBand) DO BEGIN
         PrintMyPage (iPg, rBand, prinfo);
3:       PrDumpBand (cErr);
         IF fFakeDiskFull AND (prIs=prIsSpooling)
            THEN cErr := prErrFore; {Pr test; not for apps}
         CASE PrCheckErr (cErr) OF
            prGoDocStart:  BEGIN PrSetSpool(prins, FALSE); GOTO 1; END;
            prGoStartPage: GOTO 2;
            prGoDumpBand:  GOTO 3;
            prGoEndPage:   GOTO 4;
            prGoDocEnd:    GOTO 5;
            prGoExit:      GOTO 6;
         END;
      END;

4:    PrEndPage (cErr); {Note: Must be called even while skipping pages!}
      CASE PrCheckErr (cErr) OF
         prGoDocStart:  BEGIN PrSetSpool(prins, FALSE); GOTO 1; END;
         prGoStartPage: GOTO 2;
         prGoDumpBand:  GOTO 3;
         prGoEndPage:   GOTO 4;
         prGoDocEnd:    GOTO 5;
         prGoExit:      GOTO 6;
      END;

   END; {For}

5: PrDocEnd (cErr);
   CASE PrCheckErr (cErr) OF
      prGoDocStart:  BEGIN PrSetSpool(prins, FALSE); GOTO 1; END;
      prGoStartPage: GOTO 2;
      prGoDumpBand:  GOTO 3;
      prGoEndPage:   GOTO 4;
      prGoDocEnd:    GOTO 5;
      prGoExit:      GOTO 6;
   END;

6: MyDocEnd; {Don't close up shop until after the above dispatch!}
   PrBgdDlg (cErr, FALSE);
   IF cErr<>0 THEN WRITELN ('Sam: Error in PrMgr; cErr=', cErr:1);
END;


{ ---------------------------------------------------------------------- }
{                          Menus/Events                                  }
{ ---------------------------------------------------------------------- }

PROCEDURE InstallMenus;
VAR i: INTEGER;
BEGIN
   IF fMeDbg THEN WRITELN ('InstallMenus');
   FOR i:=muFst TO muLst DO InsertMenu(rgMuInfo[i],0);
   DrawMenuBar;
END;

{ ---------------------------------------------------------------------- }

PROCEDURE SetMenuFlags;
BEGIN
   IF fMeDbg THEN WRITELN ('SetMenuFlags');

   CheckItem(rgMuInfo[4],1,fFanFold);
   CheckItem(rgMuInfo[4],2,fAutoStatus);
   CheckItem(rgMuInfo[4],3,fFakeDiskFull);
   CheckItem(rgMuInfo[4],4,fPrNone);
   CheckItem(rgMuInfo[4],5,fBgdProcess);
   CheckItem(rgMuInfo[4],6,fTest11Inch);
   CheckItem(rgMuInfo[4],7,fBgdFmMu);

   CheckItem(rgMuInfo[6],1,fMeDbg);
   CheckItem(rgMuInfo[6],2,eventDebug);
   CheckItem(rgMuInfo[6],3,fMyMgrDbg);
   CheckItem(rgMuInfo[6],4,fMyDevDbg);
   CheckItem(rgMuInfo[6],5,fMyProcDbg);
   CheckItem(rgMuInfo[6],6,fDrawBands);
   CheckItem(rgMuInfo[6],7,fDrawRSafe);
END;

{ ---------------------------------------------------------------------- }

PROCEDURE MenuCommand(mu, mi: TC);
VAR fNewStl: TF;
    prins:   TPrins;
    cErr:    TC;
BEGIN
   IF fMeDbg THEN WRITELN ('MenuCommand');
   cErr := 0;

   CASE mu OF
      2: CASE mi OF
         1: BEGIN END;
         2: BEGIN
            PrPrfDlg (cErr, pprprf^, fNewStl);
            IF fNewStl THEN NewStlNote('PrPrfDlg: New Stl: ');
            IF fMyMgrDbg THEN WrtPrPrf ('Sam: Format, PrPrf=', pprprf^);
         END;
         3: BEGIN
            IF FPrInsDlg (cErr, pprprf^, prins, fNewStl) THEN PrMyDoc (pprprf^, prins);
            IF fNewStl THEN NewStlNote('FPrInsDlg: New Stl: ');
            IF fMyMgrDbg THEN BEGIN
               WrtPrIns ('Sam: Print, PrIns=', prins);
               WrtPrPrf ('Sam: Print, PrPrf=', pprprf^);
            END;
         END;
         4: PrBgdDlg (cErr, fBgdFmMu);
         5: fAbort := TRUE;
      END;

      3: CASE mi OF
         1: BEGIN END;
         2: BEGIN END;
         3: BEGIN END;
         4: BEGIN END;
      END;

      4: CASE mi OF
         1: BEGIN fFanFold := NOT fFanFold; MyPMInit; END;
         2: fAutoStatus    := NOT fAutoStatus;
         3: fFakeDiskFull  := NOT fFakeDiskFull;
         4: fPrNone        := NOT fPrNone;
         5: FlipPrProcess;
         6: fTest11Inch    := NOT fTest11Inch;
         7: fBgdFmMu       := NOT fBgdFmMu;
      END;

      5: CASE mi OF
         1: MyPMInit;
         2: BEGIN MyPMInit; PmWrite; END;
         3: MyPMRead;
         4: BEGIN
            pprprf^.cVersion := pprprf^.cVersion+1;
            IF fMyMgrDbg THEN WrtPrPrf ('Prf Init:', pprprf^);
            IF NOT FPrPrfValid (pprprf^) THEN BEGIN
               FlashPort (activeFolder, 1);
               PrPrfFix  (pprprf^);
               IF FPrPrfValid (pprprf^) THEN FlashPort (activeFolder, 1);
            END;
            IF fMyMgrDbg THEN WrtPrPrf ('Prf Fixed:', pprprf^);
         END;
         5: IF CPrAvail=0 THEN PrAlertNone;
      END;

      6: CASE mi OF
         1: fMeDbg      := NOT fMeDbg;
         2: eventDebug  := NOT eventDebug;
         3: fMyMgrDbg   := NOT fMyMgrDbg;
         4: fMyDevDbg   := NOT fMyDevDbg;
         5: fMyProcDbg  := NOT fMyProcDbg;
         6: fDrawBands  := NOT fDrawBands;
         7: fDrawRSafe  := NOT fDrawRSafe;
      END;

   END;

   IF cErr<>0 THEN WRITELN ('Sam: Error in PrMgr; cErr=', cErr:1);
   HiLiteMenu (0);
   SetMenuFlags;

END;

{ ---------------------------------------------------------------------- }

PROCEDURE MenuEvent (event: EventRecord);
VAR mu, mi: TC;
BEGIN
   IF fMeDbg THEN WRITELN ('MenuEvent');

   CASE event.what OF
      keyDown: BEGIN
         IF event.appleKey THEN BEGIN
            MenuKey (event.ascii, mu, mi);
            HiLiteMenu (mu);
            PauseEvt (5);   { make sure the hilite shows }
            MenuCommand (mu, mi);
         END;
      END;

      buttonDown: BEGIN
         MenuSelect (event.where, mu, mi);
         MenuCommand(mu, mi);
      END;
   END;

END;

{ ---------------------------------------------------------------------- }

PROCEDURE FlrEvent (event: EventRecord);
BEGIN
   IF fMeDbg THEN WRITELN ('FlrEvent');

   CASE event.what OF
      folderDeactivate: BEGIN
         GiveControl (event);
      END;

      folderActivate: BEGIN
         TakeControl (event, FALSE, FALSE);
      END;

      folderUpdate: BEGIN
         BeginUpdate(event.who);
         EndUpdate  (event.who);
      END;
   END;
END;

{ ---------------------------------------------------------------------- }

FUNCTION SkipEvent (what: EventKind): TF;
{Used to skip over aborted events.}
BEGIN
   SkipEvent := FALSE;
END;

{ ---------------------------------------------------------------------- }

PROCEDURE MyTrkMse;
VAR icrs: TC;
    pt: Point;
BEGIN

   IF FMseInFldr ( activeFolder, pt )
      THEN icrs := icrsMenu
      ELSE icrs := icrsInactive;

   SetStdCursor(icrs);
END;

{ ---------------------------------------------------------------------- }

PROCEDURE MyEvent (event: EventRecord);
VAR pt: Point;
    fNewStl: TF;
    timeStrt: TL;
    cErr: TC;
BEGIN
   IF fMeDbg THEN WRITELN ('MyEvent');
   SetPort (event.who);

   CASE event.what OF
      buttonUp: BEGIN
         { use to measure double clicks }
      END;

      buttonDown: BEGIN
         IF event.who = activeFolder THEN BEGIN
            MoveTo (event.where.h, event.where.v);
            CaretMove (activeFolder, activeFolder^.pnLoc.h, activeFolder^.pnLoc.v);
         END;
      END;

      keyDown: BEGIN  {Should only be active folder.}
         InvertRect (activeFolder^.portRect);
         PauseEvt (10);
         InvertRect (activeFolder^.portRect);
         PauseEvt (10);
      END;

      folderDeactivate: BEGIN
         CaretKill;
         GiveControl (event);
      END;

      folderActivate: BEGIN
         TakeControl (event, FALSE, FALSE);
         InstallMenus;
         CaretMove (activeFolder, activeFolder^.pnLoc.h, activeFolder^.pnLoc.v);

         IF activeFolder=pwnd1 THEN pprprf := @prprf1 ELSE pprprf := @prprf2;

      timeStrt := Time;
         IF FPrArbRqd (pprprf^) THEN BEGIN
            PrArbDlg  (cErr, pprprf^, fNewStl);
            IF cErr<>0 THEN WRITELN ('Sam: Error in PrMgr; cErr=', cErr:1);
            IF fNewStl THEN NewStlNote('PrArbDlg: New Stl: ');
         END;
      WRITELN ('Arb Time =', ABS(Time-timeStrt):1);

      END;

      folderUpdate: BEGIN
         BeginUpdate(event.who);
         GetPen (pt); {Remember cur locn.}
         FillWTitle (event.who);
         MoveTo (pt.h, pt.v);
         EndUpdate(event.who);
      END;

   END;  { case event }
END;   { of ProcessTheEvent }

{ ---------------------------------------------------------------------- }

PROCEDURE DoEvents (event: EventRecord);
BEGIN
 {$IFC fDbgOk }
   IF fMeDbg THEN WmWrtEvt ('Sam: DoEvents ', event, TRUE);
 {$ENDC}

   IF NOT SkipEvent (event.what) THEN BEGIN
{Turn off the caret during events. Crude but safe!}
      CaretSynch;

{Pre-process the event.}
      IF (event.what = keyDown) AND event.appleKey THEN event.who := menuFolder;

{Descriminate the event on Folder}
      IF event.who = dialogFolder THEN BEGIN
         {MyDlgEvent (event);}
      END

      ELSE IF event.who = filerFolder THEN BEGIN
         FlrEvent (event);
      END

      ELSE IF event.who = scrapFolder THEN BEGIN
         MyEvent (event);
      END

      ELSE IF event.who = menuFolder THEN BEGIN
         MenuEvent (event);
      END

      ELSE IF (event.who=pwnd1) OR (event.who=pwnd2) THEN BEGIN
         MyEvent (event);
      END;

   END;
END;

{ ---------------------------------------------------------------------- }

PROCEDURE SamInit;
CONST fInitDbg = TRUE;
VAR pStart, pLimit: TP;
    menuFile: TEXT;
    i, cErr: TC;
    timeStrt: TL;
    ch: CHAR;
BEGIN
   fAbort         := FALSE;

   fMeDbg         := FALSE;
   fMyMgrDbg      := FALSE;
   fMyDevDbg      := FALSE;
   fMyProcDbg     := FALSE;
   fFakeDiskFull  := FALSE;
   fBgdFmMu       := TRUE;
   fFanFold       := TRUE;
   fPrNone        := FALSE;
   fBgdProcess    := FALSE;
   fTest11Inch    := TRUE;
   fDrawRSafe     := FALSE;
   fDrawBands     := FALSE;

IF fInitDbg THEN WRITE ('Sam: Initing My Heap');
   timeStrt := Timer;
   MyHeapMake;
IF fInitDbg THEN WRITELN (' Time =', (ABS(Timer-timeStrt) DIV 10):1);


IF fInitDbg THEN WRITE ('Sam: InitGraf');
   timeStrt := Timer;
   InitGraf (@thePort, @SamGrafError);
IF fInitDbg THEN WRITELN (' Time =', (ABS(Timer-timeStrt) DIV 10):1);


IF fInitDbg THEN WRITE ('Sam: InitWM');
   timeStrt := Timer;
   InitWM;
IF fInitDbg THEN WRITELN (' Time =', (ABS(Timer-timeStrt) DIV 10):1);


IF fInitDbg THEN WRITE ('Sam: InitWmlCrs');
   timeStrt := Timer;
   InitWmlCrs (cErr);
   IF cErr > 0 THEN WRITELN ('InitWmlCrs  error=', cErr:1);
IF fInitDbg THEN WRITELN (' Time =', (ABS(Timer-timeStrt) DIV 10):1);


IF fInitDbg THEN WRITE ('Sam: InitMenus:');
   timeStrt := Timer;
   InitMenus;
IF fInitDbg THEN WRITELN (' Time =', (ABS(Timer-timeStrt) DIV 10):1);


   cErr := 0;
IF fInitDbg THEN WRITE ('Sam: PmInit');
   timeStrt := Timer;
   PmInit (cErr); IF cErr > 0 THEN WRITELN ('PmInit error=', cErr:1);
IF fInitDbg THEN WRITELN (' Time =', (ABS(Timer-timeStrt) DIV 10):1);


IF fInitDbg THEN WRITE ('Sam: PrGlobalInit');
   timeStrt := Timer;
   PrGlobalInit;
IF fInitDbg THEN WRITELN (' Time =', (ABS(Timer-timeStrt) DIV 10):1);


IF fInitDbg THEN WRITE ('Sam: InitAlerts');
   timeStrt := Timer;
   InitAlerts (5, 2500, theHeap, NIL, @SamCkAlert);
IF fInitDbg THEN WRITELN (' alertError=', alertError:1,
                          ' Time =', (ABS(Timer-timeStrt) DIV 10):1);


IF fInitDbg THEN WRITE ('Sam: PrMgrInit');
   timeStrt := Timer;
   PrMgrInit (cErr);
IF fInitDbg THEN WRITELN (' cErr=', cErr:1, ' Time =', (ABS(Timer-timeStrt) DIV 10):1);


IF fInitDbg THEN WRITE ('Sam: Reading Sam''s Menus');
   timeStrt := Timer;
   RESET(menuFile, spMenus);
   IF IOResult<>0 THEN BEGIN
      WRITELN('Can''t find ', spMenus);
      HALT;
   END;
   FOR i := muFst TO muLst DO ReadMenu (menuFile,rgMuInfo[i]);
IF fInitDbg THEN WRITELN (' Time =', (ABS(Timer-timeStrt) DIV 10):1);


IF fInitDbg THEN WRITELN ('Sam: DrawMenuBar');
   DrawMenuBar;
   Close (menuFile);

IF fInitDbg THEN WRITELN ('Sam: PMakeFldr');
   pwnd1 := PMakeFldr ('One',100,75,300,250,Tile12,FALSE);
IF fInitDbg THEN WRITELN ('Sam: PMakeFldr');
   pwnd2 := PMakeFldr ('Two',560,40,680,100,Cent12,FALSE);

IF fInitDbg THEN WRITELN ('Sam: ShowFolder');
   ShowFolder (pwnd1);
IF fInitDbg THEN WRITELN ('Sam: ShowFolder');
   ShowFolder (pwnd2);
IF fInitDbg THEN WRITELN ('Sam: MakeFldrActive');
   MakeFldrActive (pwnd2, whyNot);

IF fInitDbg THEN WRITELN ('Sam: PrPrfDefault');
   PrPrfDefault (prprf1);
   PrPrfDefault (prprf2);
IF fInitDbg THEN WRITELN ('Sam: SetMenuFlags');
   SetMenuFlags;

IF fInitDbg THEN WRITELN ('Sam: Sizes:',
                           ' Prf=',     SizeOf(TPrPrf):1,
                           ',Stl=',     SizeOf(TPrStl):1,
                           ',Cfg=',     SizeOf(TPrCfg):1,
                           ',PrPmWd=',  SizeOf(TPrPmWd):1,
                          {',TAPmWd=',  SizeOf(TAPrPmWd):1,}
                           ',Btn=',     SizeOf(TBtn):1,
                           ',Mu=',      SizeOf(TMu):1,
                           ',Ckfm=',    SizeOf(TCkfm):1);

END;

{ ---------------------------------------------------------------------- }

PROCEDURE SamMain;
VAR event: EventRecord;
BEGIN
   SamInit;

   REPEAT  { Main Program Loop }
      IF ImActive THEN BEGIN
         IF EventAvail THEN BEGIN
            GetEvent (event);
            DoEvents (event);
         END
         ELSE BEGIN
            MyTrkMse;
            CaretBlink;
            LetOthersRun;
         END;
      END
      ELSE BEGIN
         GetEvent (event);
         DoEvents (event);
      END;
   UNTIL fAbort;
END;

BEGIN
   SamMain;
   HideCursor;
END.
{ ---------------------------------------------------------------------- }
ÿ