(*
$Exec
PLibPr/PrMgr
{no listing}
Temp
GTemp
LibPr/PrMgr
$EndExec
*)
{                          NOTE: The above compiles this program.                          }
{------------------------------------------------------------------------------------------}
{                        Copyright 1983, 1984, Apple Computer Inc.                         }
{------------------------------------------------------------------------------------------}
UNIT PrMgr;
INTRINSIC;

{------------------------------------------------------------------------------------------}
                                         INTERFACE
{------------------------------------------------------------------------------------------}

USES
  {$U LibSM/UnitStd    } UnitStd,
  {$U LIbOS/SysCall    } SysCall,
  {$U LibHW/HWint      } HWint,
  {$U LibPM/PMDecl     } PMDecl,
  {$U LibSM/UnitHz     } UnitHz,
  {$U LibQD/Storage    } Storage,
  {$U LibQD/QuickDraw  } QuickDraw,
  {$U LibQD/GrafUtil   } GrafUtil,
  {$U LibFM/FontMgr    } FontMgr,
  {$U LibFM/FmgrUtil   } FmgrUtil,
  {$U LibWM/Events     } Events,
  {$U LibWM/Menus      } Menus,
  {$U LibWM/Windows    } Windows,
  {$U LibWM/Folders    } Folders,
  {$U libSB/WmlCrs     } WmlCrs,
  {$U LibAM/AlertMgr   } AlertMgr,
  {$U LibPM/PMM        } PMM,

  {$U LibPr/PrStdInfo  } PrStdInfo,
  {$U LibPr/PrStdProcs } PrStdProcs,
  {$U LibPr/Queues     } Queues,
  {$U LibPr/PrEvents   } PrEvents,
  {$U LibPr/PrDlgMgr   } PrDlgMgr;

{$SETC fCheckRoutine = (PrDebug OR FALSE)}
{$SETC fPrMgrDbg     = (PrDebug OR FALSE)}

CONST
                        {Alert parameters.  AX=Alert.}
   cAxVersion = 100;

   CancelOpt    = 0;    {Cancel option returned from alerts}
   (* ----- Alert index constants *)
   cAxDiskFull  = 5;    {Not enough disk space to spool}
   cAxPageRange = 6;    {No pages within specified page range}
   cAxSpooling  = 7;    {The document is being spooled for printing}
   cAxChoose    = 8;    {No printer, choose another} OptCancel = 1; OptChange = 2;
   cAxNoPrinter = 9;    {No printer, don't print}
   cAxSmashed   =10;    {Files missing or smashed}
   cAxOneCopy   =11;    {Not enough disk space to make more than one copy}

   PrWildCard   = -2;

   DSCrashPassword = $B26D;


TYPE

  TDocID = RECORD
    CASE INTEGER OF
      0 : (JobID  : TL);
      1 : (FileID,BlockID : TC);
  END;

  TpSubJob = ^TSubJob;
  ThSubJob = ^TpSubJob;
  TSubJob  = RECORD
    Link    : TL;
    DocID   : TDocID;
  END;

  TpPrintJob = ^TPrintJob;
  ThPrintJob = ^TpPrintJob;
  TPrintJob  = RECORD
    Link      : TL;
    CopiesLeft: TC;
    FileID    : TC;
    DocQueue  : hQueue;
    DocName   : STRING[79];
  END;

  TpPrPrinter = ^TPrPrinter;
  ThPrPrinter = ^TpPrPrinter;
  TPrPrinter  = RECORD
    Link      : ThPrPrinter;
    SonID     : TL;
    Valid     : TF;                 {Signifies a valid entry in the queue}
    XMetrics  : TF;                 {If TRUE metrics have not been set in this handle}
    DevName   : E_Name; {.SysCall}  {Name of the printer => STRING[32]}
    JobQueue  : hQueue; {.Queues}   {Queue of print jobs}
    CurSubJob : ThSubJob;
    PrRec     : TPrRec; {.PrStdInfo}
  END;

  TpDlgRec    = ^TDlgRec;
  TDlgRec     = RECORD              {Arg record passed from application to background and back.}
    CASE INTEGER OF
      0: (nospecsize    : TF;             {Whether to provide user defined paper size as part of dialog}
          MenuSuppress  : PrMenuSuppress; {What part of the dialogs should be suppressed.}
          MaxHigh,                        {Limits on special paper sizes the app is willing to handle}
          MaxWide,
          MinHigh,
          MinWide       : TC;
          ReturnOpt     : DlgOption;      {The return option, e.g. OK or Cancel}
          NewStyle      : TF;             {Whether the user made changes or not}
          PrChosen      : ThPrPrinter;    {A handle to a specific PrPrinter}
          Extras        : TL);
      1: (Longs         : ARRAY [0..4] OF TL);
  END;

  TPrDSHead   = RECORD {The "Header" in the printer shared DataSeg.}
    hzDS      : THz;   {.Storage}
    hzCrash   : TC;                 {A password to insure a valid dataseg}
    ParentID  : TL;
    QueueDS   : hQueue;{.Queues}
    DefaultPr : ThPrPrinter;
    ThePrRec  : TPrRec;{.PrStdInfo} {The shared PrRec that is passed between foreground and background.}
    DlgArgs   : TDlgRec;            {Other procedure parameters that are passed between foreground and ...}
  END;
  TpPrDSHead  = ^TPrDSHead;

VAR

  cDSRefNum   : TC;
  hPrAlert    : TAlertfile;                  {The alert file. =NIL if alerts out.}
  pPrMgrDS    : TpPrDSHead;                  {Pointer to the shared data segment.}

{------------------------------------------------------------------------------------------}

{----Init calls}

{  Init the PrMgr.  Called once per application.
     InitAlerts must be called prior to this procedure.  }
PROCEDURE PrMgrInit;
{  Init the PrMgr.  Called once per Background process.  }
PROCEDURE PrBgdInit;
{  Init the print world.  Called once per boot. }
PROCEDURE PrGlobalInit;
{  Wait for the Father process to finish initialization. }
PROCEDURE PrWaitDad;

{----The PrMgr Dialogs.  These take a PrPrf and return a fNewStl.
     The PrPrf is the document's printer setup, while the fNewStl is
     a flag indicating whether the user's formatting changed.
 A note on the VAR parameters:
     The PrPrf is a "Two-way" VAR  parameter: it both passes data and may be
     modified by a dialog, thus must be initialized.  This can be done in the
     stationary or by a PrPrfDefault call. }

{  The Format for printer... dialog. }
PROCEDURE PrPrfDlg  (VAR prprf: TPrRec; VAR fNewStl: TF; fPgSzSuppress: TF );

{  The Print... dialog; returns the DoIt button. }
FUNCTION  FPrInsDlg (VAR prprf: TPrRec; VAR fNewStl: TF; prmode: PrMenuSuppress): TF;

{  The background status dialog. }
PROCEDURE PrBgdDlg;

{  Set default value for dialogs in cases where dialogs are called inside PrMgr,
     e.g. when PrInsDlg calls PrPrfDlg }
PROCEDURE PrDlgDefault(fPgSzSuppress:TF;Prmode:PrMenuSuppress;MaxHeight,MaxWidth,MinHeight,MinWidth:TC);

{----Control for binding/undbinding to the sharable data segment.}
PROCEDURE PrMgrDSBind  (VAR cErr: TC);
PROCEDURE PrMgrDSUnBind(VAR cErr: TC);

{----Alert putter upper that checks to remove spooling wait alert first}
FUNCTION  PrOtherAlert(AFile:TAlertFile;AlertN:TC;AlertT:TAlertKind;BGD:TF):TC;

{----Operations on Print Records.}
{  Converts a virgin prprf print record to a valid default printer.}
PROCEDURE PrPrfDefault(VAR prprf: TPrRec);
{  Compares two print records and returns true if they are equivalent. }
FUNCTION  fPrPrfEq    (VAR prprf1,prprf2:TPrRec):TF;
{  Returns true if the print record is valid; false otherwise.}
FUNCTION  FPrPrfValid (VAR prprf: TPrRec): TF;

{----Miscellaneous Print Manager routines}
{  Builds a Long Font ID. }
PROCEDURE PrLFntID    (VAR prprf: TPrRec; famIn: TC; setIn: Style; VAR lFntid: TLFntID);

(*
FUNCTION  PrParentID  :TL;
FUNCTION  PrGetDefault:ThPrPrinter;
*)

{$IFC PrDebug }
PROCEDURE PrWrtRec (sLabel: TSp; PrRec: TPrRec);
{$ENDC}

{------------------------------------------------------------------------------------------}
                                     IMPLEMENTATION
{------------------------------------------------------------------------------------------}

{$S PrMgr }

{$IFC PrDebug}
   {$R+}
{$ELSEC}
   {$R-}
{$ENDC}

{$IFC PrSymbols}
   {$D+}
{$ELSEC}
   {$D-}
{$ENDC}

CONST

   timeWtMin = 500;        {The min wait alert time}
   startupWt = 90000;      {90 seconds}

VAR
  zHzDS       : THz;       {Our shared dataseg hz.}
                           {NOTE: The alerts live in the apps hz!}


  fPrDSOpen   : TF;                          {TRUE if the shared DS already opened.}
  cPrDSBinds  : TC;                          {count of calls to bind ldsn (stack). }
  DlgSettings : TDlgRec;                     {Default dialog settings}

{------------------------------------------------------------------------------------------}
{                                PROCEDURE DIRECTORY                                       }
{------------------------------------------------------------------------------------------}

PROCEDURE PrComboInit;                                          FORWARD;
PROCEDURE PrMgrDSClose;                                         FORWARD;
PROCEDURE PrMgrDSOpen   (VAR cErr: TC);                         FORWARD;
PROCEDURE PrSwapAlerts  (VAR cErr: TC);                         FORWARD;

{  Routines which return handles for the various printers.  Must be called
   after PrMgrDSBind and with ThePrRec set with the prprf in question.}
FUNCTION  PrGetID       (handleProc:ThPrPrinter):TL;            FORWARD;
FUNCTION  PrIDMatch    :ThPrPrinter;                            FORWARD;
FUNCTION  PrFormatMatch:ThPrPrinter;                            FORWARD;
FUNCTION  PrPrintMatch :ThPrPrinter;                            FORWARD;

{------------------------------------------------------------------------------------------}
{                                       Init Code                                          }
{------------------------------------------------------------------------------------------}
{$S PrMgrInit }
PROCEDURE PrComboInit;
BEGIN
{$IFC fTracePR}    LogCall;     {$ENDC}
  DlgMgrInit;

  zHzDS      := NIL;
  hPrAlert   := NIL;

  fPrDSOpen  := FALSE;
  cPrDSBinds := 0;
  cDSRefNum  := prNilRefNum;

  PrDlgDefault(TRUE,ePrNormal,24*prPgFract,17*prPgFract,prPgFract,prPgFract);
END;

{------------------------------------------------------------------------------------------}
{$S PrMgrInit }
PROCEDURE PrBgdInit;
VAR
  cErr      : TC;
BEGIN
{$IFC fTracePR}    LogCall;     {$ENDC}
  PrComboInit;
  PsNonPreemptive;
  InitWmlCrs(cErr);
  PsPreemptive;
END;

{------------------------------------------------------------------------------------------}
{$S PrMgrInit }

PROCEDURE PrMgrInit;
VAR
  cErr    : TC;
BEGIN
{$IFC fTracePR}    LogCall;     {$ENDC}
{$IFC fPrMgrDbg}
   WRITELN ('PrMgrInit');
{$ENDC}
   PrComboInit;
   PrSwapAlerts (cErr);
END;

{------------------------------------------------------------------------------------------}
{$S PrInit }
PROCEDURE PrGlobalInit;
BEGIN
  {$IFC fTracePR}    LogCall;     {$ENDC}
  cPrError:=0;
  fBackGround:=FALSE;  {--Suspend/Activate application Process flag.
                          TRUE implies that some background process
                          is doing a dialog somewhere.}
  lParentID:=PrNilProcess;
  fInitialized:=FALSE;
  PrAlertUp:=FALSE;   {--The "Spooling" alert is not up}
END;

{------------------------------------------------------------------------------------------}
{$S Prwork }
PROCEDURE PrWaitDad;
VAR
  cErr      : TC;
  prInfo    : ProcInfoRec;
BEGIN
  {$IFC fTracePR}    LogCall;     {$ENDC}
  WHILE (fInitialized = FALSE) AND (lParentID <> PrNilProcess) DO BEGIN
    PsYieldCPU;
  END;
END;

{------------------------------------------------------------------------------------------}
{$S PrInit }
PROCEDURE PrSwapAlerts{VAR cErr: TC};
VAR cAlerts: TC;
{$IFC PrDebug }
    timeStrt: TL;
{$ENDC}
BEGIN
   {$IFC fTracePR}    LogCall;     {$ENDC}
   cErr := 0;
   IF hPrAlert=NIL THEN BEGIN
      {$IFC PrDebug } timeStrt := Time; {$ENDC}

      OpenPhraseFile (cAlerts, spPrAlrt);
      IF alertError <= 0 THEN hPrAlert := ReadAlerts (cAlerts, cAxVersion);
      { ** I hope hPralert=nil if there is an error! ** }

{$IFC PrDebug }
      WRITELN ('PrSwapAlerts..Time to read=', ABS(Time-timeStrt):1,
               ', alertErr=', alertError:1);
{$ENDC}
      cErr := alertError;
   END;
END;

{------------------------------------------------------------------------------------------}
{                 Printer Background status Dialog (Monitor the Printer)                   }
{------------------------------------------------------------------------------------------}
{$S PrMgr }
PROCEDURE PrBgdDlg;
BEGIN
{$IFC fTracePR}    LogCall;     {$ENDC}
{$IFC fPrMgrDbg}
WRITELN('PrBgdDlg');
{$ENDC}
  PrWaitDad;
  {--Parent takes care of aborting printing if requested by user}
  IF (lParentID <> prNilProcess) THEN PrDeferToBgd(lParentID,EMonitorDialog,0,0,0,0);
END;

{------------------------------------------------------------------------------------------}
{                         Print Instance Dialog (Print...)                                 }
{------------------------------------------------------------------------------------------}
{$S PrMgr }
FUNCTION  FPrInsDlg{(VAR prprf: TPrRec; VAR fNewStl: TF; prmode: PrMenuSuppress):TF};
VAR
  cErr      : TC;
  lMatchID  : TL;

BEGIN
{$IFC fTracePR}    LogCall;     {$ENDC}
{$IFC fPrMgrDbg}
  WRITELN('FPrInsDlg Start');
{$ENDC}

  FPrInsDlg := FALSE;
  fNewStl   := FALSE;

  PrMgrDSBind(cErr);
  IF cErr <= 0 THEN WITH pPrMgrDS^ DO BEGIN
    IF (fPrPrfValid(prprf) = FALSE) THEN PrPrfDefault(prprf);
    ThePrRec:=prprf;
    DlgArgs.MenuSuppress:=prmode;
    lMatchID:=PrGetID(PrPrintMatch);
    IF (lMatchID <> prNilProcess) THEN BEGIN
      fNewStl:=fNewStl OR DlgArgs.NewStyle;
      PrDeferToBgd(lMatchID,EPrintDialog,0,0,0,0);
      fNewStl:=fNewStl OR DlgArgs.NewStyle;
      FPrInsDlg:=(DlgArgs.ReturnOpt = eDlgOK);
      IF fNewStl THEN prprf:=ThePrRec;
    END;
    PrMgrDSUnBind(cErr);
  END;
END;

{------------------------------------------------------------------------------------------}
{                   Printer Preference Dialog (Format for Printer...)                      }
{------------------------------------------------------------------------------------------}
{$S PrMgr }
PROCEDURE PrPrfDlg {(VAR prprf: TPrRec; VAR fNewStl: TF; fPgSzSuppress: TF )};
VAR
  lMatchID  : TL;
  startID   : TL;
  cErr      : TC;

BEGIN
{$IFC fTracePR}    LogCall;     {$ENDC}
{$IFC fPrMgrDbg}
  WRITELN ('PrPrfDlg Start');
{$ENDC}
  fNewStl:=FALSE;

  PrMgrDSBind (cErr);
  IF (cErr <= 0) THEN WITH pPrMgrDS^ DO BEGIN
    IF (fPrPrfValid(prprf) = FALSE) THEN PrPrfDefault(prprf);
    DlgArgs:=DlgSettings;
    ThePrRec:=prprf;
    lMatchID:=PrGetID(PrFormatMatch);
    startID:=lMatchID;

    WHILE TRUE DO BEGIN
      IF (lMatchID = prNilProcess) THEN BEGIN {--Choose another printer dialog}
        DlgArgs.nospecsize:=fPgSzSuppress;
        PrDeferToBgd(lParentID,ePrListDialog,0,0,0,0);
        IF (DlgArgs.ReturnOpt = eDlgCancel) THEN BEGIN
          fNewStl:=FALSE;
          PrMgrDSUnBind(cErr);
          EXIT(PrPrfDlg);
        END;
        lMatchID:=PrGetID(PrFormatMatch);
        fNewStl:=(lMatchID <> startID);
      END;
      IF (lMatchID <> PrNilProcess) THEN BEGIN
        DlgArgs.MenuSuppress:=ePrNormal;
        DlgArgs.nospecsize:=fPgSzSuppress;
        PrDeferToBgd(lMatchID,ESettingsDialog,0,0,0,0); {--Format for this printer}
        IF (DlgArgs.ReturnOpt <> eDlgAnother) THEN BEGIN {--Don't choose another, we'll go with this choice}
          IF (DlgArgs.ReturnOpt = eDlgOK) THEN BEGIN
            fNewStl:=(fNewStl OR DlgArgs.NewStyle);
            prprf:=ThePrRec;
          END;
          PrMgrDSUnBind(cErr);
          EXIT(PrPrfDlg);
        END;
      END;
      lMatchID:=prNilProcess;

    END{WHILE};
  END{WITH};
END;

{------------------------------------------------------------------------------------------}
{$S PrInit }
PROCEDURE PrDlgDefault{(fPgSzSuppress:TF;Prmode:PrMenuSuppress;MaxHeight,MaxWidth,MinHeight,MinWidth:TC)};
BEGIN
  {$IFC fTracePR}    LogCall;     {$ENDC}
  WITH DlgSettings DO BEGIN
    nospecsize:=fPgSzSuppress;
    MenuSuppress:=Prmode;
    MaxHigh   := MaxHeight;
    MaxWide   := MaxWidth;
    MinHigh   := MinHeight;
    MinWide   := MinWidth;
    ReturnOpt := EDlgCancel;
    NewStyle  := FALSE;
    PrChosen  := NIL;
  END;
END;

{------------------------------------------------------------------------------------------}
{                                Btn and Data Seg Stuff                                    }
{   This is not in the init code: it is always within a Dlg or Dlg-like execution scope.   }
{------------------------------------------------------------------------------------------}
{$S Prwork }
PROCEDURE PrMgrDSBind{VAR cErr: TC};
{$IFC PrDebug }
VAR  cbk     : TC;
{$ENDC}
BEGIN
{$IFC fTracePR}    LogCall;     {$ENDC}
{$IFC fPrMgrDbg }
  WRITELN('PrMgrDSBind');
{$ENDC}

   cErr := 0;

   cPrDSBinds := cPrDSBinds+1;
   IF cPrDSBinds = 1 THEN BEGIN
      IF fPrDSOpen THEN BEGIN
         BIND_DATASEG(cErr,cDSRefNum);
      END
      ELSE BEGIN
         PrMgrDSOpen (cErr);
      END;

{$IFC PrDebug }
      IF NOT FCheckHzOK (zHzDS, cbk) THEN WRITELN ('Hz Probs in PrMgrDSBind');
{$ENDC}

      IF cErr > 0 THEN BEGIN
{$IFC PrDebug }
         WRITELN('!PrMgrDSBind cErr=', cErr:1,
                  ' fPrDSOpen=', fPrDSOpen, ' cPrDSBinds=', cPrDSBinds:1 );
{$ENDC}
         PrMgrDSClose;
      END;
   END;
END;

{------------------------------------------------------------------------------------------}
{$S PrMgr }
PROCEDURE PrMgrDSClose;
VAR cErr: TC;
BEGIN
{$IFC fTracePR}    LogCall;     {$ENDC}
{$IFC fPrMgrDbg }
   WRITELN ('PrMgrDSClose');
{$ENDC}

   IF (cDSRefNum <> prNilRefNum) THEN CLOSE_DATASEG(cErr, cDSRefNum);

   pPrMgrDS   := NIL;
   fPrDSOpen  := FALSE;
   cPrDSBinds := 0;
   cDSRefNum  := prNilRefNum;

END;

{------------------------------------------------------------------------------------------}
{$S PrMgr }
PROCEDURE PrMgrDSOpen{VAR cErr: TC};
VAR
  lSegptr : LONGINT;
  sSegname: PathName;

{$IFC PrDebug }
  cbk     : TC;
  timeStrt, timeEnd: TL;
  DSInfo  :   DSInfoRec;
{$ENDC}

BEGIN
{$IFC fTracePR}    LogCall;     {$ENDC}
{$IFC fPrMgrDbg }
  WRITELN('PrMgrDSOpen');
{$ENDC}
{$IFC PrDebug }
   timeStrt:=Time;
{$ENDC}

   sSegname := sParentSeg;
   OPEN_DATASEG(cErr,sSegName,cDSRefNum,lSegPtr,ldsnshared);
   fPrDSOpen:= TRUE;

   IF (cErr <= 0) THEN BEGIN
     SETACCESS_DATASEG (cErr, cDSRefNum, FALSE{fReadOnly});
     pPrMgrDS   := POINTER(lSegPtr);
     WITH pPrMgrDS^ DO BEGIN
       zHzDS      := hzDS;
     END;
   END;

{$IFC PrDebug }
   timeEnd:=Time;
   IF NOT FCheckHzOK (zHzDS, cbk) THEN WRITELN ('Hz Probs in PrMgrDSOpen');
   {IF fPrMgrDbg THEN BEGIN}
      WRITELN ('PrMgrDSOpen: Time to open dataseg=', ABS(timeEnd-timeStrt):1);
      INFO_DATASEG(cErr, cDSRefNum, DSInfo);
      WRITELN ('PrMgr Hz: cb=', (CbOfHz(zHzDS)):1,
               ' cbk=', cbk:1,
               ' DataSeg: cb=', DSInfo.mem_size:1,
               ' pStrt=', ORD(pPrMgrDS):1,
               ' pEnd=', (ORD(pPrMgrDS)+DSInfo.mem_size):1 );
   {END;}
{$ENDC}
END;

{------------------------------------------------------------------------------------------}
{$S Prwork }
PROCEDURE PrMgrDSUnBind{VAR cErr: TC};
BEGIN
{$IFC fTracePR}    LogCall;     {$ENDC}
{$IFC fPrMgrDbg}
  WRITELN ('PrMgrDSUnBind');
{$ENDC}

   cErr := 0;

   IF cPrDSBinds > 0 THEN BEGIN
      cPrDSBinds  := cPrDSBinds-1; {Pins at 0}
      IF cPrDSBinds = 0 THEN BEGIN
         UNBIND_DATASEG(cErr,cDSRefNum);

         IF (cErr > 0) THEN BEGIN
            {$IFC PrDebug }
            WRITELN ('!PrMgrDSUnBind cErr=', cErr:1,
                     ' fPrDSOpen=', fPrDSOpen, ' cPrDSBinds=', cPrDSBinds:1 );
            {$ENDC}
            PrMgrDSClose;
         END;
      END;
   END;
END;

{------------------------------------------------------------------------------------------}
{                                    Miscellaneous Stuff:                                  }
{------------------------------------------------------------------------------------------}

{------------------------------------------------------------------------------------------}
{$S PrMgr }
FUNCTION  PrOtherAlert{(AFile:TAlertFile;AlertN:TC;AlertT:TAlertKind;BGD:TF):TC};
VAR
  nerr    : TC;
  PInfoRec: ProcInfoRec;
Begin
  {$IFC fTracePR}    LogCall;     {$ENDC}
   INFO_PROCESS(nerr,MY_ID,PInfoRec);
   IF BGD THEN SETPRIORITY_PROCESS(nerr,PInfoRec.Global_ID,140);
   PsNonPreemptive;
   PrOtherAlert:=CancelOpt;     {--Default to Cancel option}
   If (PrAlertUp) then Begin
     (* ----- Take down 'Spooling Your document' alert *)
     HideFolder( Alertfolder );
     PrAlertUp:=False;
   End;
   IF BGD THEN BEGIN
     PrOtherAlert:=BackGroundAlert(AFile,AlertN,AlertT);
   END
   ELSE CASE AlertT OF
      AskProc:       PrOtherAlert:=AskAlert(AFile,AlertN);
      CautionProc:   PrOtherAlert:=ORD(CautionAlert(AFile,AlertN));
      NoteProc:      NoteAlert(AFile,AlertN);
      StopProc:      StopAlert(AFile,AlertN);
   END;
   PsPreemptive;
   IF BGD THEN SETPRIORITY_PROCESS(nerr,PInfoRec.Global_ID,PInfoRec.Priority);
End;
{------------------------------------------------------------------------------------------}
{$S Prwork }
PROCEDURE PrPrfDefault  {VAR prprf: TPrRec};
VAR
  cErr      : TC;
  fbind     : TF;
{Makes vanilla prprf.}
BEGIN
{$IFC fTracePR}    LogCall;     {$ENDC}
{$IFC fPrMgrDbg}
WRITELN ('PrPrfDefault');
{$ENDC}

  {--Wait for the dad to become fully initialized}
  PrWaitDad;

  PrMgrDSBind(cErr);
  fbind:=(cErr <= 0);
  IF fbind THEN fbind:=(pPrMgrDS^.DefaultPr <> NIL);
  IF fbind THEN BEGIN
    prprf:=pPrMgrDS^.DefaultPr^^.PrRec;
    PrMgrDSUnBind(cErr);
  END
  ELSE BEGIN
    prprf:=PrRec68K^;
    prprf.PrVersion:= cPrPrfVersion;   {Make sure its the right one}
  END;
  WITH prprf DO BEGIN
    WITH PrJobInfo DO BEGIN
      DocPgFst :=   0;             {Page Range. PgFst=0 => all.}
      DocPgLst :=   0;
      SpooledPages:=0;             {Number of Pages in Spool File.}
      Copies   :=   1;             {No. copies.}
      Immediate:=FALSE;
    END;
  END;
END;

{------------------------------------------------------------------------------------------}
{$S PrMgr }
FUNCTION  fPrPrfEq{( VAR prprf1,prprf2:TPrRec):TF};
TYPE
  aPrRec  = ARRAY [1..200] OF TL;
  paPrRec = ^aPrRec;

VAR
  pr1,pr2 : paPrRec;
  j       : TC;
BEGIN
{$IFC fTracePR}    LogCall;     {$ENDC}
{$IFC fPrMgrDbg}
WRITE('FPrPrfEq:');
{$ENDC}
fPrPrfEq:=fEqBlk(@prprf1,@prprf2,SIZEOF(TPrRec));
{
pr1:=@prprf1;
pr2:=@prprf2;
fPrPrfEq:=FALSE;
FOR j:=1 TO (SIZEOF(TPrRec) DIV 4) DO BEGIN
  IF pr1^[j] <> pr2^[j] THEN EXIT(fPrPrfEq);
END;
fPrPrfEq:=TRUE;
}
END;

{------------------------------------------------------------------------------------------}
{$S Prwork }
FUNCTION  FPrPrfValid{(VAR prprf: TPrRec): TF};
{Validity check via the obvious fields.}
VAR
  cErr    : TC;
  hMatch  : ThPrPrinter;
BEGIN
{$IFC fTracePR}    LogCall;     {$ENDC}
{$IFC fPrMgrDbg}
WRITELN ('FPrPrfValid');
{$ENDC}
  FPrPrfValid:=FALSE;
  IF (prprf.PrVersion = cPrPrfVersion) THEN BEGIN
    hMatch:=NIL;
    PrMgrDSBind(cErr);
    IF cErr <= 0 THEN WITH pPrMgrDS^ DO BEGIN
      ThePrRec:=prprf;
      hMatch:=PrIDMatch;
      PrMgrDSUnBind(cErr);
    END;
    FPrPrfValid := (hMatch <> NIL);
  END;
END;

{------------------------------------------------------------------------------------------}
{$S Prwork}
PROCEDURE PrLFntID {(prprf: TPrRec; famIn: TC; setIn: Style; VAR lFntid: TLFntID)};
BEGIN
  {$IFC fTracePR}    LogCall;     {$ENDC}
  WITH lfntid DO BEGIN
    dev := prprf.PrInfo.ADev.dev;
    fam:=famIn;
    seteface:=setIn;
  END;
END;

{------------------------------------------------------------------------------------------}
{$S PrMgr}
FUNCTION  PrGetID{(handleProc:ThPrPrinter):TL};
VAR
  hPrinter  : ThPrPrinter;
  elapsed   : TL;
BEGIN
  {$IFC fTracePR}    LogCall;     {$ENDC}
  hPrinter:=handleProc;
  pPrMgrDS^.DlgArgs.PrChosen:=hPrinter;
  PrGetID:=PrNilProcess;
  IF (hPrinter <> NIL) THEN BEGIN
    IF hPrinter^^.SonID = PrNilProcess THEN BEGIN
      PrNotify(lParentID,EWake,ORD4(hPrinter),0,0,0);
      elapsed:=Timer+StartUpWt;
      WHILE (hPrinter^^.SonID = PrNilProcess) AND (lParentID <> PrNilProcess)
        AND (Timer < Elapsed) DO PsYieldCPU;
    END;
    WITH hPrinter^^ DO BEGIN
      {--If the SonID equals the ParentID then the son process couldn't be aroused.
         Tell the client that printer isn't available, i.e. PrNilProcess.}
      IF (SonID = lParentID) THEN SonID:=PrNilProcess;
      PrGetID:=SonID;
    END;
  END;
END;
{------------------------------------------------------------------------------------------}
{$S Prwork}
FUNCTION  PrIDMatch{:ThPrPrinter};
  { - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - }
  FUNCTION  uPrIDMatch(hQItem,pTPrRec:hQueue):BOOLEAN;
  VAR
    hQPrRec   : ThPrPrinter;
    pThePrRec : ^TPrRec;
  BEGIN
   {$IFC fTracePR}    LogCall;     {$ENDC}
    hQPrRec:=POINTER(ORD4(hQItem));
    pThePrRec:=POINTER(ORD4(pTPrRec));
    uPrIDMatch:=(hQPrRec^^.PrRec.PrInfo.PrinterID <> pThePrRec^.PrInfo.PrinterID);
  END;
  { - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - }
BEGIN
{$IFC fTracePR}    LogCall;     {$ENDC}
{$IFC fPrMgrDbg}
WRITELN ('PrIDMatch');
{$ENDC}
  PrIDMatch:=NIL;
  WITH pPrMgrDS^ DO BEGIN
    PrIDMatch:=POINTER(EnumerateQueue(QueueDS,uPrIDMatch,@ThePrRec));
  END{WITH};
END;

{------------------------------------------------------------------------------------------}
{$S PrMgr}
FUNCTION  PrFormatMatch{:ThPrPrinter};
{ The PrRec in shared memory is checked for a match in the Parents current list of processes.}
VAR
  hPrinter  : ThPrPrinter;
  { - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - }
  FUNCTION  uPrFormatMatch(hQItem,pTPrRec:hQueue):BOOLEAN;
  VAR
    hQPrRec   : ThPrPrinter;
    pThePrRec : ^TPrRec;
  BEGIN
    {$IFC fTracePR}    LogCall;     {$ENDC}
    hQPrRec:=POINTER(ORD4(hQItem));
    pThePrRec:=POINTER(ORD4(pTPrRec));
    WITH hQPrRec^^.PrRec.PrInfo, pThePrRec^ DO uPrFormatMatch:= {Printer vs. Document}
      NOT ((PrinterID = PrInfo.PrinterID) AND ((PrNilPort = PrInfo.Port) OR (Port = PrInfo.Port)));
  END;
  { - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - }

BEGIN
{$IFC fTracePR}    LogCall;     {$ENDC}
{$IFC fPrMgrDbg}
WRITELN ('PrFormatMatch');
{$ENDC}
  PrFormatMatch:=NIL;
  WITH pPrMgrDS^ DO BEGIN
    PrFormatMatch:=POINTER(EnumerateQueue(QueueDS,uPrFormatMatch,@ThePrRec));
  END{WITH};
END;

{------------------------------------------------------------------------------------------}
{$S PrMgr}
FUNCTION  PrPrintMatch{:ThPrPrinter};
{ The PrRec in shared memory is checked for a match in the Parent's current list of printers.}
VAR
  fPrMatch  : TF;
  hPrinter  : ThPrPrinter;
  { - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - }
  FUNCTION  uPrSubPrintMatch(hQItem,pTPrRec:hQueue):BOOLEAN;
  VAR
    hQPrRec   : ThPrPrinter;
    pThePrRec : ^TPrRec;
  BEGIN
    {$IFC fTracePR}    LogCall;     {$ENDC}
    hQPrRec:=POINTER(ORD4(hQItem));
    pThePrRec:=POINTER(ORD4(pTPrRec));
    WITH hQPrRec^^.PrRec.PrInfo, pThePrRec^ DO uPrSubPrintMatch:= {Printer record vs. Document record}
      NOT ((PrinterID = PrInfo.PrinterID) AND (Port <> PrNilPort));
  END;
  { - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - }
  FUNCTION  uPrPrintMatch(hQItem,pTPrRec:hQueue):BOOLEAN;
  VAR
    hQPrRec   : ThPrPrinter;
    pThePrRec : ^TPrRec;
  BEGIN
    {$IFC fTracePR}    LogCall;     {$ENDC}
    hQPrRec:=POINTER(ORD4(hQItem));
    pThePrRec:=POINTER(ORD4(pTPrRec));
    WITH hQPrRec^^.PrRec.PrInfo, pThePrRec^ DO uPrPrintMatch:= {Printer record vs. Document record}
      NOT ((PrinterID = PrInfo.PrinterID) AND (Port <> PrNilPort) AND
      ((Port = PrInfo.Port) OR (PrNilPort = PrInfo.Port)));
  END;
  { - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - }
  FUNCTION  PrAorD:BOOLEAN; {--Alert and/or raise the Printer Selection Dialog.  Return OK or Cancel.}
  BEGIN
    {$IFC fTracePR}    LogCall;     {$ENDC}
    PrAorD:=FALSE;
    IF (AskAlert(hPrAlert,cAxChoose) = OptChange) THEN BEGIN {--Specified printer not connected, choose another}
      {--User said OK (change printers)}
      WITH pPrMgrDS^ DO BEGIN
        DlgArgs:=DlgSettings;
        DlgArgs.NewStyle:=TRUE;     {--We've changed printers, let the application know }
      END;
      PrDeferToBgd(pPrMgrDS^.ParentID,EPrListDialog,0,0,0,0);
      PrAorD:=(pPrMgrDS^.DlgArgs.ReturnOpt = eDlgOK);
    END;
  END;
  { - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - }

BEGIN
{$IFC fTracePR}    LogCall;     {$ENDC}
{$IFC fPrMgrDbg}
WRITELN ('PrPrintMatch');
{$ENDC}
{--Set defaults for the return.}
  PrPrintMatch:=NIL;
  WITH pPrMgrDS^ DO BEGIN
    DlgArgs.ReturnOpt:=eDlgCancel;
    DlgArgs.NewStyle:=FALSE;
    fPrMatch:=FALSE;

    WHILE fPrMatch = FALSE DO BEGIN
      hPrinter:=POINTER(EnumerateQueue(QueueDS,uPrPrintMatch,@ThePrRec));
      IF (hPrinter = NIL) THEN BEGIN
        hPrinter:=POINTER(EnumerateQueue(QueueDS,uPrSubPrintMatch,@ThePrRec));
      END;
      IF (hPrinter <> NIL) THEN BEGIN
        DlgArgs.NewStyle:=ThePrRec.PrInfo.Port <> hPrinter^^.PrRec.PrInfo.Port;
        ThePrRec.PrInfo.Port:=hPrinter^^.PrRec.PrInfo.Port;
        PrPrintMatch:=hPrinter;
        DlgArgs.ReturnOpt:=eDlgOK;
        fPrMatch:=TRUE;           {EXIT(PrPrintMatch);}
  {$IFC fPrMgrDbg}
        PrWrtRec('PrPrintMatch; new:', ThePrRec);
  {$ENDC}
      END
      ELSE BEGIN
        IF DlgArgs.MenuSuppress = ePrDialogSuppress THEN BEGIN
          StopAlert(hPrAlert,cAxNoPrinter); {--Printer is not connected}
          DlgArgs.NewStyle:=FALSE;
          DlgArgs.ReturnOpt:=eDlgCancel;
          fPrMatch:=TRUE;         {EXIT(PrPrintMatch);}
        END
        ELSE BEGIN
          DlgArgs.NewStyle:=FALSE;
          IF (PrAorD <> TRUE) THEN BEGIN
            {DlgArgs.NewStyle:=FALSE;}
            DlgArgs.ReturnOpt:=eDlgCancel;
            fPrMatch:=TRUE;       {EXIT(PrPrintMatch);}
          END;
        END;
      END;
    END{WHILE};
  END;
END;

{------------------------------------------------------------------------------------------}
(*
{$S PrMgr}
FUNCTION  PrParentID{:TL};
VAR
  cErr    : TC;
BEGIN
{$IFC fTracePR}    LogCall;     {$ENDC}
{$IFC fPrMgrDbg}
PrWrtRec ('PrParentID');
{$ENDC}
  PrParentID:=prNilProcess;
  PrMgrDSBind(cErr);
  IF (cErr <= 0) THEN WITH pPrMgrDS^ DO BEGIN
    PrParentID:=ParentID;
    PrMgrDSUnBind(cErr);
  END;
END;
*)
{------------------------------------------------------------------------------------------}
(*
{$S PrMgr}
FUNCTION PrGetDefault{:ThPrPrinter};
{ The default printer is found in the current list of printers.}
VAR
  cErr      : TC;
BEGIN
  {$IFC fTracePR}    LogCall;     {$ENDC}
  PrGetDefault:=NIL;
  PrMgrDSBind (cErr);
  IF (cErr <= 0) THEN WITH pPrMgrDS^ DO BEGIN
    PrGetDefault:=DefaultPr;
    PrMgrDSUnBind(cErr);
  END;
END;
*)
{------------------------------------------------------------------------------------------}
{                                     Commentary                                           }
{------------------------------------------------------------------------------------------}

END.

{***************************   SUMMARY OF PRMGR CHANGES FOR CONFIGURABILITY    *********************

   1. All dialogs are raised by background processes.

***************************************************************************************************}

{
UNIT PrMgr CHANGE LOG:
     11/29/83 BH: New unit allows background processes to put up their own dialogs.
}

ÿ