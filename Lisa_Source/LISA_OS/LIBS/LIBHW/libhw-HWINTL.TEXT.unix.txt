;
;   File HWINTL.TEXT
;
;-----------------------------------------------------------------------------
;
;   Hardware Interface Trap Routines
;
;   Written by Rick Meyers
;   Copyright 1983, Apple Computer Inc.
;
;
;   The routines below bridge the gap between the Pascal Hardware Interface
;   Unit (HWINT) and the assembly language hardware interface routines.
;   They provide implementations of the Pascal functions and procedures by
;   juggling parameters and trapping to the assembly language routines.
;
;-----------------------------------------------------------------------------
; Change Log
;
;   <13Oct83>  TCW  Export KeyToAscii Routine



                .PROC HWIntL

                .DEF DriverInit,DiskDriver,TwiggyDriver
                .DEF DiskSync,NMISync,COPSSync,Poll
                .DEF MouseLocation,MouseUpdates,MouseScaling,MouseThresh
                .DEF MouseOdometer
                .DEF CursorLocation,CursorTracking,CursorImage
                .DEF CursorHide,CursorShield,CursorDisplay
                .DEF CursorObscure,CursorInit,CursorReInit
                .DEF BusyImage,BusyDelay
                .DEF FrameCounter
                .DEF ScreenSize,ScreenAddr,AltScreenAddr
                .DEF ScreenKeybd,SetScreenKeybd
                .DEF Contrast,SetContrast,RampContrast
                .DEF DimContrast,SetDimContrast
                .DEF FadeDelay,SetFadeDelay
                .DEF PowerDown,PowerCycle
                .DEF Volume,SetVolume,Noise,Silence,Beep
                .DEF Keyboard,Legends,SetLegends
                .DEF KeyIsDown,KeyMap
                .DEF KeybdPeek,AltKeyPeek
                .DEF KeybdEvent,AltKeyEvent
                .DEF RepeatRate,SetRepeatRate
                .DEF KeyPushed,NMIKey,SetNMIKey
                .DEF ToggleKey,SetToggleKey
                .DEF KeyToAscii
                .DEF MicroTimer,Timer,AlarmAssign,AlarmReturn
                .DEF AlarmAbsolute,AlarmRelative,AlarmOff
                .DEF DateTime,SetDateTime,DateToTime
                .DEF TimeStamp,SetTimeStamp,TimeToDate


;-----------------------------------------------------------------------------
;
;   Equates

                .INCLUDE LibHW/HWIEQU.TEXT   ; magic routine numbers

ParameterArea   .EQU    $FA0000              ; parameter passing data area

;-----------------------------------------------------------------------------
;
;   Macros
;
;   Procedure Head  --  debug assist

               .MACRO   HEAD             ; procedure heading
                LINK    A6,#0            ; fancy NOP used by debugger
               .ENDM


;   Procedure Tail  --  debug assist, remove parameters and return
;
;       %1  --  number of bytes of parameters to delete
;       %2  --  procedure name as string, exactly 8 characters

                .MACRO  TAIL
                UNLK    A6              ; pattern recognized by debugger
                .IF     %1 = 0          ; if no parameters to pop then
                  RTS                   ;   return
                .ELSE                   ; else
                  .IF     %1 = 4        ;   if 4 bytes of parameters then
                    MOVE.L  (SP)+,(SP)  ;     slide down the return addr
                    RTS                 ;     and return
                  .ELSE                 ;   else
                    MOVE.L  (SP)+,A0    ;     load return address
                    ADD.W   #%1,SP      ;     remove parameters from stack
                    JMP     (A0)        ;     return
                  .ENDC                 ;   endif
                .ENDC                   ; endif
                .ASCII  %2              ; procedure name
                .ENDM


;   Trap Sequence
;
;       %1  --  number of the routine to which we're trapping
;

                .MACRO  TRAPTO
                MOVE.L  D7,-(SP)        ; save register
                MOVE.W  #%1,D7          ; routine number
                TRAP    #$5             ; trap
                MOVE.L  (SP)+,D7        ; restore register
                .ENDM


;-----------------------------------------------------------------------------
;
;   Dummy Procedure Heading
;

                HEAD                    ; dummy procedure heading
                .ASCII  'Copyright 1983, Apple Computer Inc. '

;-----------------------------------------------------------------------------
;
; Procedure DriverInit;
;

DriverInit      TRAPTO  _DriverInit     ; DriverInit
                RTS                     ; return


;-----------------------------------------------------------------------------
;
; Procedure DiskDriver (routine: LogicalAddress);
;

DiskDriver      MOVE.L  4(SP),A0        ; routine
                TRAPTO  _DiskDriver     ; DiskDriver
                MOVE.L  (SP)+,A0        ; return address
                ADD.W   #4,SP           ; remove parameter
                JMP     (A0)            ; return


;-----------------------------------------------------------------------------
;
; Procedure TwiggyDriver (routine: LogicalAddress);
;

TwiggyDriver    MOVE.L  4(SP),A0        ; routine
                TRAPTO  _TwiggyDriver   ; TwiggyDriver
                MOVE.L  (SP)+,A0        ; return address
                ADD.W   #4,SP           ; remove parameter
                JMP     (A0)            ; return


;-----------------------------------------------------------------------------
;
; Procedure DiskSync (busy: Boolean);
;

DiskSync        MOVE.L  (SP)+,A0        ; return address
                MOVE.B  (SP)+,D0        ; busy
                TRAPTO  _DiskSync       ; DiskSync
                JMP     (A0)            ; return


;-----------------------------------------------------------------------------
;
; Procedure NMISync
;

NMISync         TRAPTO  _NMISync        ; NMISync
                RTS                     ; return


;-----------------------------------------------------------------------------
;
; Procedure COPSSync
;

COPSSync        TRAPTO  _COPSSync       ; COPSSync
                RTS                     ; return


;-----------------------------------------------------------------------------
;
; Procedure Poll
;

Poll            TRAPTO  _Poll           ; Poll
                RTS                     ; return


;-----------------------------------------------------------------------------
;
; Procedure MouseLocation (var x: Pixels; var y: Pixels);
;

MouseLocation   TRAPTO  _MouseLocation  ; MouseLocation
                MOVEM.L (SP)+,A0/A1     ; return address, var y
                MOVE.W  D1,(A1)         ; Y-coordinate
                MOVE.L  (SP)+,A1        ; var x
                MOVE.W  D0,(A1)         ; X-coordinate
                JMP     (A0)            ; return


;-----------------------------------------------------------------------------
;
; Procedure MouseUpdates (delay: MilliSeconds);
;

MouseUpdates    MOVE.L  (SP)+,A0        ; return address
                MOVE.L  (SP)+,D0        ; delay
                TRAPTO  _MouseUpdates   ; MouseUpdates
                JMP     (A0)            ; return


;-----------------------------------------------------------------------------
;
; Procedure MouseScaling (scale: Boolean);
;

MouseScaling    MOVE.L  (SP)+,A0        ; return address
                MOVE.B  (SP)+,D0        ; scale
                TRAPTO  _MouseScaling   ; MouseScaling
                JMP     (A0)            ; return


;-----------------------------------------------------------------------------
;
; Procedure MouseThresh (threshold: Pixels);
;

MouseThresh     MOVE.L  (SP)+,A0        ; return address
                MOVE.W  (SP)+,D0        ; threshold
                TRAPTO  _MouseThresh    ; MouseThresh
                JMP     (A0)            ; return


;-----------------------------------------------------------------------------
;
; Function  MouseOdometer: ManyPixels;
;

MouseOdometer   TRAPTO  _MouseOdometer  ; MouseOdometer
                MOVE.L  D0,4(SP)        ; function result
                RTS                     ; return


;-----------------------------------------------------------------------------
;
; Procedure CursorLocation (x: Pixels; y: Pixels);
;

CursorLocation  MOVEM.L (SP)+,A0        ; return address
                MOVE.W  (SP)+,D1        ; Y-coordinate
                MOVE.W  (SP)+,D0        ; X-coordinate
                TRAPTO  _CursorLocation ; CursorLocation
                JMP     (A0)            ; return


;-----------------------------------------------------------------------------
;
; Procedure CursorTracking (track: Boolean);
;

CursorTracking  MOVE.L  (SP)+,A0        ; return address
                MOVE.B  (SP)+,D0        ; track
                TRAPTO  _CursorTracking ; CursorTracking
                JMP     (A0)            ; return


;-----------------------------------------------------------------------------
;
; Procedure CursorImage (hotX: Pixels; hotY: Pixels; height: CursorHeight;
;   data: CursorPtr; mask: CursorPtr);
;

CursorImage     MOVE.W  16(SP),D0       ; hotspot X-coordinate
                MOVE.W  14(SP),D1       ; hotspot Y-coordinate
                MOVE.W  12(SP),D2       ; height
                MOVE.L  8(SP),A0        ; data
                MOVE.L  4(SP),A1        ; mask

                MOVEM.L D2/A2-A3,-(SP)       ; save registers
                MOVE.L  #ParameterArea,A2    ; parameter area for 'data'
                MOVE.L  #ParameterArea+64,A3 ; parameter area for 'mask'
                SUB.W   #1,D2                ; initialize counter

@1              MOVE.W  (A0)+,(A2)+          ; transfer cursor data
                MOVE.W  (A1)+,(A3)+          ; transfer cursor mask
                DBF     D2,@1                ; loop height times

                MOVE.L  #ParameterArea,A0    ; parameter area for 'data'
                MOVE.L  #ParameterArea+64,A1 ; parameter area for 'mask'
                MOVEM.L (SP)+,D2/A2-A3       ; restore registers

                TRAPTO  _CursorImage    ; CursorImage
                MOVE.L  (SP)+,A0        ; return address
                ADD.W   #14,SP          ; remove parameters
                JMP     (A0)            ; return


;-----------------------------------------------------------------------------
;
; Procedure CursorHide;
;

CursorHide      TRAPTO  _CursorHide     ; CursorHide
                RTS                     ; return


;-----------------------------------------------------------------------------
;
; Procedure CursorShield (left: Pixels; top: Pixels; right: Pixels;
;     bottom: Pixels);
;

CursorShield    MOVE.L  D3,-(SP)        ; save registers
                MOVE.W  8(SP),D3        ; bottom
                MOVE.W  10(SP),D2       ; right
                MOVE.W  12(SP),D1       ; top
                MOVE.W  14(SP),D0       ; left
                TRAPTO  _CursorShield   ; CursorShield
                MOVE.L  (SP)+,D3        ; restore registers
                MOVE.L  (SP)+,A0        ; return address
                ADD.W   #8,SP           ; remove parameters
                JMP     (A0)            ; return


;-----------------------------------------------------------------------------
;
; Procedure CursorDisplay;
;

CursorDisplay   TRAPTO  _CursorDisplay  ; CursorDisplay
                RTS                     ; return


;-----------------------------------------------------------------------------
;
; Procedure CursorObscure;
;

CursorObscure   TRAPTO  _CursorObscure  ; CursorObscure
                RTS                     ; return


;-----------------------------------------------------------------------------
;
; Procedure CursorInit;
;

CursorInit      TRAPTO  _CursrInit      ; CursorInit
                RTS                     ; return


;-----------------------------------------------------------------------------
;
; Procedure CursorReInit;
;

CursorReInit    TRAPTO  _CursorReInit   ; CursorReInit
                RTS                     ; return


;-----------------------------------------------------------------------------
;
; Procedure BusyImage (hotX: Pixels; hotY: Pixels; height: CursorHeight;
;   data: CursorPtr; mask: CursorPtr);
;

BusyImage       MOVE.W  16(SP),D0       ; hotspot X-coordinate
                MOVE.W  14(SP),D1       ; hotspot Y-coordinate
                MOVE.W  12(SP),D2       ; height
                MOVE.L  8(SP),A0        ; data
                MOVE.L  4(SP),A1        ; mask

                MOVEM.L D2/A2-A3,-(SP)       ; save registers
                MOVE.L  #ParameterArea,A2    ; parameter area for 'data'
                MOVE.L  #ParameterArea+64,A3 ; parameter area for 'mask'
                SUB.W   #1,D2                ; initialize counter

@1              MOVE.W  (A0)+,(A2)+          ; transfer cursor data
                MOVE.W  (A1)+,(A3)+          ; transfer cursor mask
                DBF     D2,@1                ; loop height times

                MOVE.L  #ParameterArea,A0    ; parameter area for 'data'
                MOVE.L  #ParameterArea+64,A1 ; parameter area for 'mask'
                MOVEM.L (SP)+,D2/A2-A3       ; restore registers

                TRAPTO  _BusyImage      ; BusyImage
                MOVE.L  (SP)+,A0        ; return address
                ADD.W   #14,SP          ; remove parameters
                JMP     (A0)            ; return


;-----------------------------------------------------------------------------
;
; Procedure BusyDelay (delay: MilliSecnds);
;

BusyDelay       MOVE.L  (SP)+,A0        ; return address
                MOVE.L  (SP)+,D0        ; delay
                TRAPTO  _BusyDelay      ; BusyDelay
                JMP     (A0)            ; return


;-----------------------------------------------------------------------------
;
; Function  FrameCounter: Frames;
;

FrameCounter    TRAPTO  _FrameCounter   ; FrameCounter
                MOVE.L  D0,4(SP)        ; function result
                RTS                     ; return


;-----------------------------------------------------------------------------
;
; Procedure ScreenSize (var x: Pixels; var y: Pixels);
;

ScreenSize      TRAPTO  _ScreenSize     ; ScreenSize
                MOVEM.L (SP)+,A0/A1     ; return address, var y
                MOVE.W  D1,(A1)         ; vertical dimension
                MOVE.L  (SP)+,A1        ; var x
                MOVE.W  D0,(A1)         ; horizontal dimension
                JMP     (A0)            ; return


;-----------------------------------------------------------------------------
;
; Function  ScreenAddr: LogicalAddress;
;

ScreenAddr      TRAPTO  _ScreenAddr     ; ScreenAddr
                MOVE.L  A0,4(SP)        ; function result
                RTS                     ; return


;-----------------------------------------------------------------------------
;
; Function  AltScreenAddr: LogicalAddress;
;

AltScreenAddr   TRAPTO  _AltScreenAddr  ; AltScreenAddr
                MOVE.L  A0,4(SP)        ; function result
                RTS                     ; return


;-----------------------------------------------------------------------------
;
; Function  ScreenKeybd: VisibleScreen;
;

ScreenKeybd     TRAPTO  _ScreenKeybd    ; ScreenKeybd
                MOVE.B  D0,4(SP)        ; function result
                RTS                     ; return



;-----------------------------------------------------------------------------
;
; Procedure SetScreenKeybd (screen: VisibleScreen);
;

SetScreenKeybd  MOVE.L  (SP)+,A0        ; return address
                MOVE.B  (SP)+,D0        ; screen
                TRAPTO  _SetScreenKeybd ; SetScreenKeybd
                JMP     (A0)            ; return


;-----------------------------------------------------------------------------
;
; Function  Contrast: ScreenContrast;
;

Contrast        TRAPTO  _Contrast       ; Contrast
                MOVE.W  D0,4(SP)        ; function result
                RTS                     ; return


;-----------------------------------------------------------------------------
;
; Procedure SetContrast (contrast: ScreenContrast);
;

SetContrast     MOVE.L  (SP)+,A0        ; return address
                MOVE.W  (SP)+,D0        ; contrast
                TRAPTO  _SetContrast    ; SetContrast
                JMP     (A0)            ; return


;-----------------------------------------------------------------------------
;
; Procedure RampContrast (contrast: ScreenContrast);
;

RampContrast    MOVE.L  (SP)+,A0        ; return address
                MOVE.W  (SP)+,D0        ; contrast
                TRAPTO  _RampContrast   ; RampContrast
                JMP     (A0)            ; return


;-----------------------------------------------------------------------------
;
; Function  DimContrast: ScreenContrast;
;

DimContrast     TRAPTO  _DimContrast    ; DimContrast
                MOVE.W  D0,4(SP)        ; function result
                RTS                     ; return


;-----------------------------------------------------------------------------
;
; Procedure SetDinContrast (contrast: ScreenContrast);
;

SetDimContrast  MOVE.L  (SP)+,A0        ; return address
                MOVE.W  (SP)+,D0        ; contrast
                TRAPTO  _SetDimContrast ; SetDimContrast
                JMP     (A0)            ; return


;-----------------------------------------------------------------------------
;
; Function  FadeDelay: MilliSeconds;
;

FadeDelay       TRAPTO  _FadeDelay      ; FadeDelay
                MOVE.L  D0,4(SP)        ; function result
                RTS                     ; return


;-----------------------------------------------------------------------------
;
; Procedure SetFadeDelay (delay: MilliSecnds);
;

SetFadeDelay    MOVE.L  (SP)+,A0        ; return address
                MOVE.L  (SP)+,D0        ; delay
                TRAPTO  _SetFadeDelay   ; SetFadeDelay
                JMP     (A0)            ; return


;-----------------------------------------------------------------------------
;
; Procedure PowerDown;
;

PowerDown       TRAPTO  _PowerDown      ; PowerDown
                RTS                     ; return


;-----------------------------------------------------------------------------
;
; Procedure PowerCycle(delay: Seconds);
;

PowerCycle      MOVE.L  (SP)+,A0        ; return address
                MOVE.L  (SP)+,D0        ; delay
                TRAPTO  _PowerCycle     ; PowerCycle
                JMP     (A0)            ; return


;-----------------------------------------------------------------------------
;
; Function Volume: SpeakerVolume;
;

Volume          TRAPTO  _Volume         ; Volume
                MOVE.W  D0,4(SP)        ; function result
                RTS                     ; return


;-----------------------------------------------------------------------------
;
; Procedure SetVolume (volume: SpeakerVolume);
;

SetVolume       MOVE.L  (SP)+,A0        ; return address
                MOVE.W  (SP)+,D0        ; contrast
                TRAPTO  _SetVolume      ; SetVolume
                JMP     (A0)            ; return


;-----------------------------------------------------------------------------
;
; Procedure Noise (waveLength: MicroSeconds);
;

Noise           MOVE.L  (SP)+,A0        ; return address
                MOVE.L  (SP)+,D0        ; waveLength
                TRAPTO  _Noise          ; Noise
                JMP     (A0)            ; return


;-----------------------------------------------------------------------------
;
; Procedure Silence;
;

Silence         TRAPTO  _Silence        ; Silence
                RTS                     ; return


;-----------------------------------------------------------------------------
;
; Procedure Beep (waveLength: MicroSeconds; duration: MilliSeconds);
;

Beep            MOVE.L  (SP)+,A0        ; return address
                MOVE.L  (SP)+,D1        ; duration
                MOVE.L  (SP)+,D0        ; waelength
                TRAPTO  _Beep           ; Beep
                JMP     (A0)            ; return


;-----------------------------------------------------------------------------
;
; Function  Keyboard: KeybdId;
;

Keyboard        TRAPTO  _Keyboard       ; Keyboard
                MOVE.W  D0,4(SP)        ; function result
                RTS                     ; return


;-----------------------------------------------------------------------------
;
; Function  Legends: KeybdId;
;

Legends         TRAPTO  _Legends        ; Legends
                MOVE.W  D0,4(SP)        ; function result
                RTS                     ; return


;-----------------------------------------------------------------------------
;
; Procedure SetLegends (id: KeybdId);
;

SetLegends      MOVE.L  (SP)+,A0        ; return address
                MOVE.W  (SP)+,D0        ; id
                TRAPTO  _SetLegends     ; SetLegends
                JMP     (A0)            ; return


;-----------------------------------------------------------------------------
;
; Function KeyIsDown (key: KeyCap): Boolean;
;

KeyIsDown       MOVE.L  (SP)+,A0        ; return address
                MOVE.W  #0,D0           ; clear register
                MOVE.B  (SP)+,D0        ; key
                TRAPTO  _KeyIsDown      ; KeyIsDown
                MOVE.B  D1,(SP)         ; function result
                JMP     (A0)            ; return


;-----------------------------------------------------------------------------
;
; Procedure KeyMap (var keys: KeyCapSet);
;

KeyMap          MOVEM.L (SP)+,A0/A1     ; return address, var keys
                MOVE.L  D3,-(SP)        ; save registers
                TRAPTO  _KeyMap         ; KeyMap
                MOVE.L  D3,(A1)+        ; transfer bits 127..96
                MOVE.L  D2,(A1)+        ; transfer bits 95..64
                MOVE.L  D1,(A1)+        ; transfer bits 63..32
                MOVE.L  D0,(A1)+        ; transfer bits 31..0
                MOVE.L  (SP)+,D3        ; restore registers
                JMP     (A0)            ; return


;-----------------------------------------------------------------------------
;
; Function KeybdPeek (repeats: boolean; index: KeybdQIndex; var event: KeyEvent):
;    Boolean;
;
;
; Function AltKeyPeek (repeats: boolean; index: KeybdQIndex; var event: KeyEvent):
;    Boolean;
;

KeybdPeek       MOVEM.L (SP)+,A0/A1     ; return address, var event
                MOVE.W  (SP)+,D1        ; index
                MOVE.B  (SP)+,D0        ; repeats
                MOVEM.L D3-D6,-(SP)     ; save registers
                TRAPTO  _KeybdPeek      ; KeybdPeek
                BRA.S   MorePeek        ; continue

AltKeyPeek      MOVEM.L (SP)+,A0/A1     ; return address, var event
                MOVE.W  (SP)+,D1        ; index
                MOVE.B  (SP)+,D0        ; repeats
                MOVEM.L D3-D6,-(SP)     ; save registers
                TRAPTO  _AltKeyPeek     ; AltKeyPeek

MorePeek        MOVE.B  D1,(A1)+        ; key
                MOVE.B  D2,(A1)+        ; ascii
                MOVE.W  D3,(A1)+        ; state
                MOVE.W  D4,(A1)+        ; mouseX
                MOVE.W  D5,(A1)+        ; mouseY
                MOVE.L  D6,(A1)+        ; time
                MOVEM.L (SP)+,D3-D6     ; restore registers
                MOVE.B  D0,(SP)         ; function result
                JMP     (A0)            ; return


;-----------------------------------------------------------------------------
;
; Function KeybdEvent (repeats: boolean; wait: boolean; var event: KeyEvent):
;    Boolean;
;
;
; Function AltKeyEvent (repeats: boolean; wait: boolean; var event: KeyEvent):
;    Boolean;
;

KeybdEvent      MOVEM.L (SP)+,A0/A1     ; return address, var event
                MOVE.B  (SP)+,D1        ; wait
                MOVE.B  (SP)+,D0        ; repeats
                MOVEM.L D3-D6,-(SP)     ; save registers
                TRAPTO  _KeybdEvent     ; KeybdEvent
                BRA.S   MoreEvent       ; continue

AltKeyEvent     MOVEM.L (SP)+,A0/A1     ; return address, var event
                MOVE.B  (SP)+,D1        ; wait
                MOVE.B  (SP)+,D0        ; repeats
                MOVEM.L D3-D6,-(SP)     ; save registers
                TRAPTO  _AltKeyEvent    ; AltKeyEvent

MoreEvent       MOVE.B  D1,(A1)+        ; key
                MOVE.B  D2,(A1)+        ; ascii
                MOVE.W  D3,(A1)+        ; state
                MOVE.W  D4,(A1)+        ; mouseX
                MOVE.W  D5,(A1)+        ; mouseY
                MOVE.L  D6,(A1)+        ; time
                MOVEM.L (SP)+,D3-D6     ; restore registers
                MOVE.B  D0,(SP)         ; function result
                JMP     (A0)            ; return


;-----------------------------------------------------------------------------
;
; Procedure RepeatRate (var initial: MilliSeconds;
;    var subsequent: MilliSeconds);
;

RepeatRate      TRAPTO  _RepeatRate     ; RepeatRate
                MOVEM.L (SP)+,A0/A1     ; return address, var subsequent
                MOVE.L  D1,(A1)         ; subsequent
                MOVE.L  (SP)+,A1        ; var initial
                MOVE.L  D0,(A1)         ; initial
                JMP     (A0)            ; return


;-----------------------------------------------------------------------------
;
; Procedure SetRepeatRate (initial: MilliSeconds; subsequent: MilliSeconds);
;

SetRepeatRate   MOVE.L  (SP)+,A0        ; return address
                MOVE.L  (SP)+,D1        ; subsequent
                MOVE.L  (SP)+,D0        ; initial
                TRAPTO  _SetRepeatRate  ; SetRepeatRate
                JMP     (A0)            ; return


;-----------------------------------------------------------------------------
;
; Procedure KeyPushed (key: KeyCap);
;

KeyPushed       MOVE.L  (SP)+,A0        ; return address
                MOVE.W  #0,D0           ; clear register
                MOVE.B  (SP)+,D0        ; key
                TRAPTO  _KeyPushed      ; KeyPushed
                JMP     (A0)            ; return

;-----------------------------------------------------------------------------
;
; Function  NMIKey: KeyCap;
;

NMIKey          TRAPTO  _NMIKey         ; NMIKey
                MOVE.B  D0,4(SP)        ; function result
                RTS                     ; return


;-----------------------------------------------------------------------------
;
; Procedure SetNMIKey (key: KeyCap);
;

SetNMIKey       MOVE.L  (SP)+,A0        ; return address
                MOVE.W  #0,D0           ; clear register
                MOVE.B  (SP)+,D0        ; key
                TRAPTO  _SetNMIKey      ; SetNMIKey
                JMP     (A0)            ; return


;-----------------------------------------------------------------------------
;
; Function  ToggleKey: KeyCap;
;

ToggleKey       TRAPTO  _ToggleKey      ; ToggleKey
                MOVE.B  D0,4(SP)        ; function result
                RTS                     ; return


;-----------------------------------------------------------------------------
;
; Procedure SetToggleKey (key: KeyCap);
;

SetToggleKey    MOVE.L  (SP)+,A0        ; return address
                MOVE.W  #0,D0           ; clear register
                MOVE.B  (SP)+,D0        ; key
                TRAPTO  _SetToggleKey   ; SetToggleKey
                JMP     (A0)            ; return

;----------------------------------------------------------------------------
;
; Function KeyToAscii(key: Keycap, state: Integer): Ascii;
;

KeyToAscii      MOVE.L  (SP)+,A0        ; return address                      <13Oct83>
                MOVE.W  (SP)+,D1        ; state                               <13Oct83>
                MOVE.W  #0,D0           ; clear register                      <13Oct83>
                MOVE.B  (SP)+,D0        ; key                                 <13Oct83>
                TRAPTO  _KeyToAscii     ; KeyToAscii                          <13Oct83>
                MOVE.W  D0,(SP)         ; function result                     <13Oct83>
                JMP     (A0)            ; return                              <13Oct83>



;-----------------------------------------------------------------------------
;
; Function MicroTimer: MicroSeconds;
;

MicroTimer      TRAPTO  _MicroTimer     ; MicroTimer
                MOVE.L  D0,4(SP)        ; function result
                RTS                     ; return


;-----------------------------------------------------------------------------
;
; Function Timer: Milliseconds;
;

Timer           TRAPTO  _Timer          ; Timer
                MOVE.L  D0,4(SP)        ; function result
                RTS                     ; return


;-----------------------------------------------------------------------------
;
; Procedure AlarmAssign (var alarm: AlarmNumber; routine: LogicalAddress);
;

AlarmAssign     MOVE.L  4(SP),A0        ; routine
                TRAPTO  _AlrmAssign     ; AlarmAssign
                MOVE.L  (SP)+,A0        ; return address
                ADD.W   #4,SP           ; remove routine
                MOVE.L  (SP)+,A1        ; var alarm
                MOVE.W  D0,(A1)         ; alarm
                JMP     (A0)            ; return


;-----------------------------------------------------------------------------
;
; Procedure AlarmReturn (alarm: AlarmNumber);
;

AlarmReturn     MOVE.L  (SP)+,A0        ; return address
                MOVE.W  (SP)+,D0        ; alarm
                TRAPTO  _AlrmReturn     ; AlarmReturn
                JMP     (A0)            ; return


;-----------------------------------------------------------------------------
;
; Procedure AlarmAbsolute (alarm: AlarmNumber; time: Milliseconds);
;

AlarmAbsolute   MOVE.L  (SP)+,A0        ; return address
                MOVE.L  (SP)+,D1        ; time
                MOVE.W  (SP)+,D0        ; alarm
                TRAPTO  _AlrmAbsolute   ; AlarmAbsolute
                JMP     (A0)            ; return


;-----------------------------------------------------------------------------
;
; Procedure AlarmRelative (alarm: AlarmNumber; delay: Milliseconds);
;

AlarmRelative   MOVE.L  (SP)+,A0        ; return address
                MOVE.L  (SP)+,D1        ; delay
                MOVE.W  (SP)+,D0        ; alarm
                TRAPTO  _AlrmRelative   ; AlarmRelative
                JMP     (A0)            ; return


;-----------------------------------------------------------------------------
;
; Procedure AlarmOff (alarm: AlarmNumber);
;

AlarmOff        MOVE.L  (SP)+,A0        ; return address
                MOVE.W  (SP)+,D0        ; alarm
                TRAPTO  _AlrmOff        ; AlarmOff
                JMP     (A0)            ; return


;-----------------------------------------------------------------------------
;
; Procedure DateTime (var date: DateArray);
;

DateTime        MOVEM.L (SP)+,A0/A1     ; return address, var date
                MOVEM.L D3/D4,-(SP)     ; save registers
                TRAPTO  _DateTime       ; DateTime
                MOVE.W  D0,(A1)+        ; year
                MOVE.W  D1,(A1)+        ; day
                MOVE.W  D2,(A1)+        ; hour
                MOVE.W  D3,(A1)+        ; minute
                MOVE.W  D4,(A1)+        ; second
                MOVEM.L (SP)+,D3/D4     ; restore registers
                JMP     (A0)            ; return


;-----------------------------------------------------------------------------
;
; Procedure SetDateTime (date: DateArray);
;

SetDateTime     MOVEM.L (SP)+,A0/A1     ; return address, date
                MOVEM.L D3/D4,-(SP)     ; save registers
                MOVE.W  (A1)+,D0        ; year
                MOVE.W  (A1)+,D1        ; day
                MOVE.W  (A1)+,D2        ; hour
                MOVE.W  (A1)+,D3        ; minute
                MOVE.W  (A1)+,D4        ; second
                TRAPTO  _SetDateTime    ; SetDateTime
                MOVEM.L (SP)+,D3/D4     ; restore registers
                JMP     (A0)            ; return


;-----------------------------------------------------------------------------
;
; Procedure DateToTime (date: DateArray; var time: Seconds);
;

DateToTime      MOVE.L  8(SP),A1        ; date
                MOVEM.L D3/D4,-(SP)     ; save registers
                MOVE.W  (A1)+,D0        ; year
                MOVE.W  (A1)+,D1        ; day
                MOVE.W  (A1)+,D2        ; hour
                MOVE.W  (A1)+,D3        ; minute
                MOVE.W  (A1)+,D4        ; second
                TRAPTO  _DateToTime     ; DateToTime
                MOVEM.L (SP)+,D3/D4     ; restore registers
                MOVEM.L (SP)+,A0/A1     ; return address, var time
                MOVE.L  D0,(A1)         ; time
                ADD.W   #4,SP           ; remove parameters
                JMP     (A0)            ; return


;-----------------------------------------------------------------------------
;
; Function  TimeStamp: Seconds;
;

TimeStamp       TRAPTO  _TimeStamp      ; TimeStamp
                MOVE.L  D0,4(SP)        ; function result
                RTS                     ; return


;-----------------------------------------------------------------------------
;
; Procedure SetTimeStamp (time: Seconds);
;

SetTimeStamp    MOVE.L  (SP)+,A0        ; return address
                MOVE.L  (SP)+,D0        ; time
                TRAPTO  _SetTimeStamp   ; SetTimeStamp
                JMP     (A0)            ; return


;-----------------------------------------------------------------------------
;
; Procedure TimeToDate (time: Seconds; var date: DateArray);
;

TimeToDate      MOVEM.L (SP)+,A0/A1     ; return address, var date
                MOVE.L  (SP)+,D0        ; time
                MOVEM.L D3/D4,-(SP)     ; save registers
                TRAPTO  _TimeToDate     ; TimeToDate
                MOVE.W  D0,(A1)+        ; year
                MOVE.W  D1,(A1)+        ; day
                MOVE.W  D2,(A1)+        ; hour
                MOVE.W  D3,(A1)+        ; minute
                MOVE.W  D4,(A1)+        ; second
                MOVEM.L (SP)+,D3/D4     ; restore registers
                JMP     (A0)            ; return


;-----------------------------------------------------------------------------
;
;   Dummy Procedure Tail
;

                TAIL    0,'HWINTL  '
                .END
ÿ