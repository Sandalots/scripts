
;
;   File MACHINE.TEXT
;
;-----------------------------------------------------------------------------
;
;   Hardware Interface for the Screen, Power and the Speaker
;
;   Written by Rick Meyers
;   Copyright 1983, Apple Computer Inc.
;
;
;
;   The routines below provide an assembly language interface to the screen,
;   power-off and the speaker.  Input parameters are passed in registers,
;   output parameters are returned in registers.  Unless otherwise noted, all
;   registers are preserved.
;
;   Routines labeled with a * require supervisor mode.
;
;-----------------------------------------------------------------------------
;   Change Log
;
;   <29Jul83>  LAN  Deleted routines ReadMMU and WriteMMU from Machine.text
;   <17Aug83>  LAN  In "NOISE" multiply incoming wavelength by 1.25 in order
;                   to lower the frequency generated by the Pepsi I/O board.
;   <21Oct83>  LAN  Replaced ReadMMU and WriteMMU as requested by Jeff.
;                   (The moon is full today.)
;   <14Feb84>  DTO  When StepContrast and CheckActivity are called from alarm,
;                   lower interrupt priority from 7 to 1 during execution
;

;-----------------------------------------------------------------------------
;
;   The Display Screens
;
;   The display screen is a bit mappped display; that is, each pixel on the
;   screen is controlled by a bit in main memory.  The display has 720 pixels
;   horizontally and 364 lines vertically, and therefore requires 32,760 bytes
;   of main memory.  The screen size may be determined programmatically by
;   calling routine ScreenSize.
;
;   The hardware interface supports two separate displays.  The primary screen
;   is intended for normal users interaction, especially graphics.  The cursor
;   always appears on the primary screen.  The alternate screen is intended
;   for debugging output.  Initially the primary display is visible.  The user
;   can toggle between the two displays by typing <Option-Enter> (hold down
;   Option while typing Enter on the keypad).  Programs can toggle between the
;   two displays using routine ScreenKeybd and SetScreenKeybd.  Routines
;   ScreenAddr and AltScreenAddr return the addresses of the displays.  These
;   address are logical, rather than physical, addresses.
;
;   There are two keyboard input queues, one corresponding to each display.
;   When the primary display is visible, input goes into the primary input queue.
;   Similiarly, when the alternate display is visible, input goes into the
;   alternate input queue.
;
;   The screen is redisplayed about 60 times per second.  A frame counter
;   is incremented between screen updates, at the vertical retrace interrupt.
;   The frame counter is an unsigned 32-bit integer which is reset to 0 each
;   time the machine is booted.  An application can synchronize with the
;   vertical retaces by watching for changes in the value of this counter.
;   The frame counter should NOT be used as a timer, it's not accurate.
;
;   The display's contrast level is under program control.  Contrast values
;   range from 0 to 255 ($FF), with 0 as maximum contrast and 255 as minimum.
;   The low order two bits of the contrast value are ignored.  The initial
;   contrast value in 128 ($80).  A sudden change in the contrast level can
;   be jarring to the user.  A routine is provided which gradually changes
;   the contrast to the new setting over a period of approximately a second.
;   This routine returns immediately, then ramps the contrast using interrupt
;   driven processing.
;
;   The display automatically dims when no user activity has been noted over a
;   specified period of time.  (See the "Power Control" section below for details.)
;   The contrast value to which the screen is dimmed is under program control.
;   Routines are provided to both read and write the dim contrast setting.  The
;   initial dim contrast value is 176 ($B0).
;
;
;   Routine:    FrameCounter
;   Arguments:    D0 (output)  --  vertical retrace counter (long)
;   Function:   Return the current value of the vertical retrace counter.
;

FrameCounter    MOVE.L  ScrnFrames,D0       ; vertical retrace counter
                RTS                         ; return


;
;   Routine:    ScreenSize
;   Arguments:    D0 (output)  --  horizontal dimension (integer)
;                 D1 (output)  --  vertical dimension (integer)
;   Function:   Return the dimensions of the screen.
;

ScreenSize      MOVE.W  #720,D0             ; horizontal dimension
                MOVE.W  #364,D1             ; vertical dimension
                RTS                         ; return


;
;   Routine:    ScreenAddr
;   Arguments:    A0 (output)  --  primary screen address (address)
;   Function:   Return the logical address of the primary screen.
;

ScreenAddr      MOVE.L  ScrnAddr,A0         ; primary screen address
                RTS                         ; return


;
;   Routine:    AltScreenAddr
;   Arguments:    A0 (output)  --  alternate screen address (address)
;   Function:   Return the logical address of the alternate screen.
;

AltScreenAddr   MOVE.L  AltScrnAddr,A0      ; alternate screen address
                RTS                         ; return


;
;   Routine:    ScreenKeybd
;   Arguments:    D0 (output)  --  0=primary, 1=alternate (byte)
;   Function:   Return the currently visible/active screen/keyboard.
;

ScreenKeybd     MOVE.B  ScrnKeybd,D0        ; 0=primary, else=alternate
                RTS                         ; return


;
;   Routine:    SetScreenKeybd
;   Arguments:    D0 (input)   --  0=primary, 1=alternate (byte)
;   Function:   Set the currently visible/active screen/keyboard.
;

SetScreenKeybd  MOVE.L  D1,-(SP)            ; save registers
                MOVE.L  ScrnPhys,D1         ; primary screen physical address
                MOVE.B  D0,ScrnKeybd        ; 0=primary, else=alternate
                BEQ.S   @1                  ; branch if primary
                MOVE.L  AltScrnPhys,D1      ; alternate screen physical address

@1              ADD.L   MemoryBase,D1       ; minimum physical memory address
                LSR.L   #8,D1               ; divide by 2**15
                LSR.L   #7,D1               ; screen page
                MOVE.B  D1,VideoLatch       ; video page latch
                MOVE.L  (SP)+,D1            ; restore registers
                RTS                         ; return


;
;   Routine:    Contrast
;   Arguments:    D0 (output)  --  contrast level (integer)
;   Function:   Return the display contrast level.
;

Contrast        MOVE.W  ActiveContrast,D0   ; active contrast level
                RTS                         ; return


;
;   Routine:    SetContrast
;   Arguments:    D0 (input)   --  contrast level (integer)
;   Function:   Set the display contrast level.  The value specified should
;               be in the range 0 to 255.
;
;   Routine:    RampContrast
;   Arguments:    D0 (input)  --  contrast level (integer)
;   Function:   Gradually change the display contrast level.  The value
;               specified should be in the range 0 to 255.
;

SetContrast     MOVEM.L D0-D1,-(SP)         ; save registers
                AND.W   #$00FC,D0           ; multiple of four
                MOVE.W  D0,ActiveContrast   ; active contrast level             <28Feb83>
                MOVE.W  ScrnContrast,D1     ; current screen contrast
                SUB.W   D0,D1               ; contrast - goal
                BGE.S   ArmContrast         ; branch if step > 0
                NEG.W   D1                  ; make step positive
                BRA.S   ArmContrast         ; continue below

RampInternal    MOVEM.L D0-D1,-(SP)         ; save registers                    <28Feb83>
                AND.W   #$00FC,D0           ; multiple of four                  <28Feb83>
                MOVE.W  #4,D1               ; contrast step of 4                <28Feb83>
                BRA.S   ArmContrast         ; continue                          <28Feb83>

RampContrast    MOVEM.L D0-D1,-(SP)         ; save registers
                AND.W   #$00FC,D0           ; multiple of four
                MOVE.W  D0,ActiveContrast   ; active contrast level
                MOVE.W  #4,D1               ; contrast step of 4

ArmContrast     MOVE.W  D0,CntrastGoal      ; screen contrast goal
                MOVE.W  D1,CntrastStep      ; contrast step
                JSR     StepContrast        ; set contrast                      <28Feb83>
                MOVEM.L (SP)+,D0-D1         ; restore registers
                RTS                         ; return


;
;   Internal routine StContrast is called as an alarm routine at priority
;   level 6 to actually set the contrast.  StepContrast is called from ArmContrast.
;

StContrast      ANDI    #$F9FF,SR           ; interrupt prio 7 --> 1            <14Feb84>
StepContrast    MOVEM.L D0-D1/A0,-(SP)      ; save registers                    <28Feb83>
                TST.B   DiskSemaphore       ; 0=free, else=busy
                BNE     @4                  ; branch if parallel port busy

                MOVE.W  ScrnContrast,D0     ; current screen contrast
                MOVE.W  CntrastGoal,D1      ; screen contrast goal
                CMP.W   D0,D1               ; goal = contrast ?
                BEQ     @5                  ; branch if goal = contrast
                BLT.S   @1                  ; branch if goal < contrast

                ADD.W   CntrastStep,D0      ; contrast <- contrast + step
                CMP.W   D1,D0               ; contrast >= goal ?
                BLE.S   @2                  ; branch if contrast <= goal
                MOVE.W  D1,D0               ; contrast <- goal (overshot)
                BRA.S   @2

@1              SUB.W   CntrastStep,D0      ; contrast <- contrast - step
                CMP.W   D1,D0               ; contrast >= goal ?
                BGE.S   @2                  ; branch if contrast >= goal
                MOVE.W  D1,D0               ; contrast <- goal (overshot)

;   Set the screen contrast

@2              MOVE.W  D0,ScrnContrast     ; save contrast level
                TST.B   IOBoard             ; 0=Sept81, else=Feb82
                BNE.S   @3                  ; branch if not Sept81

;   Sept81 Boards

                NOT.B   D0                  ; invert for old hardware
                MOVE.L  #VIA1,A0            ; Hard Disk VIA memory address
                BSET    #2,DDRB1(A0)        ; program DEN/ as an output
                BSET    #2,PORTB1(A0)       ; DEN/ <- 1 to disable disk
                MOVE.B  #$FF,DDRA1(A0)      ; program port A for output
                MOVE.B  D0,PORTA1(A0)       ; set contrast value
                MOVE.L  D0,ContrstLatch     ; latch contrast value
                MOVE.B  #$00,DDRA1(A0)      ; program port A for input
                BCLR    #2,PORTB1(A0)       ; DEN/ <- 0 to enable disk
                BRA.S   @4                  ; continue

;   Feb82 Boards

@3              MOVE.L  #VIA1,A0            ; Hard Disk VIA memory address
                BSET    #2,DDRB1(A0)        ; program DEN/ as an output
                BSET    #2,PORTB1(A0)       ; DEN/ <- 1 to disable disk
                MOVE.B  #$FF,DDRA1(A0)      ; program port A for output
                MOVE.B  D0,PORTA1(A0)       ; put contrast value on bus

                BSET    #7,DDRB1(A0)        ; program WCNT as an output
                BCLR    #7,PORTB1(A0)       ; WCNT <- 0
                BSET    #7,PORTB1(A0)       ; WCNT <- 1
                BCLR    #7,DDRB1(A0)        ; program WCNT as an input

                MOVE.B  #$00,DDRA1(A0)      ; program port A for input
                BCLR    #2,PORTB1(A0)       ; DEN/ <- 0 to enable disk

;   Reprogram the alarm to go off again in 50 milliseconds.

@4              MOVE.W  CntrastAlarm,D0     ; contrast alarm number
                MOVE.L  #50,D1              ; change every 50 milliseconds
                JSR     AlarmRelative       ; set contrast alarm

@5              MOVEM.L (SP)+,D0-D1/A0      ; restore registers                 <28Feb83>
                RTS                         ; return



;
;   Routine:    DimContrast
;   Arguments:    D0 (output)  --  dim contrast level (integer)
;   Function:   Return the dim contrast level.
;

DimContrast     MOVE.W  CntrastDim,D0       ; dim screen contrast
                RTS                         ; return


;
;   Routine:    SetDimContrast
;   Arguments:    D0 (input)   --  dim contrast level (integer)
;   Function:   Set the dim contrast level.  The value specified should
;               be in the range 0 to 255.
;

SetDimContrast  MOVE.L  D0,-(SP)            ; save registers
                AND.W   #$00FC,D0           ; multiple of four
                MOVE.W  D0,CntrastDim       ; dim screen contrast
                MOVE.L  (SP)+,D0            ; restore registers
                RTS                         ; return



;-----------------------------------------------------------------------------
;
;   Power Control
;
;   Four distinct levels of operation are provided by a combination of
;   hardware and software.
;
;       1)  Fully Operational.
;       2)  Operational with display dimmed.
;       3)  Powered off.
;       4)  Unplugged.
;
;   Fully Operational  --  When the machine is fully operational, user
;   interaction with the system is monitored in an attempt to determine if
;   the machine is in use.  If no user activity is noted over a specified
;   period (usually several minutes) the screen contrast level is automatically
;   dimmed in order to preserve the screen phosphor.  The delay between the
;   last user activity and dimming of the screen is under software control.
;   The actual delay will range from the specified delay to twice the specified
;   delay.  The initial delay period is five minutes.
;
;   Operational with display dimmed  --  When the machine is operational but
;   the screen is dimmed, user interaction will cause the screen contrast to
;   return to its normal bright level.  Moving the mouse or pressing a key on
;   the keyboard (e.g. SHIFT) is enough to trigger the screen brightening.
;   Calling routines CursorLocation or SetFadeDelay also indicates user
;   activity.
;
;   Powered Off  --  Software can instruct the hardware to power off.  Routine
;   PowerDown provides this function.  In additon, the hardware can be
;   instructed to reboot after a specified period of time.  The delay before
;   rebooting may range from 0 to 1,048,575 seconds (about 12 days).  Routine
;   PowerCycle provides this function.  Note that PowerCycle is unable to turn
;   the machine back on if the clock/calendar hasn't been set since the last
;   loss of battery power.  In the powered off state, most of the machine is off,
;   but the clock/calendar circuitry continues to function.
;
;
;   Unplugged  --  If the machine is unplugged the clock/calendar circuitry
;   will continue to operate for a few hours on battery backup.
;
;
;   Routine:    FadeDelay
;   Arguments:    D0 (output)  --  milliseconds before dimming (long)
;   Function:   Return the approximate period of time, in milliseconds, between
;               the last user activity and the automatic dimming of the screen.
;

FadeDelay       MOVE.L  ActiveDelay,D0      ; milliseconds before dimming
                RTS                         ; return


;
;   Routine:    SetFadeDelay
;   Arguments:    D0 (input)   --  milliseconds before dimming (long)
;   Funciton:   Set the approximate period of time, in milliseconds, between
;               the last user activity and the automatic dimming of the screen.
;

SetFadeDelay    ACTIVE                      ; note user activity
                MOVE.L  D0,ActiveDelay      ; milliseconds before dimming
                JSR     CheckActivity       ; reset the alarm
                RTS                         ; return


;
;   NoteActivity
;
;   NoteActivity is called from the marco ACTIVE when ActiveState bit 0 changes
;   from 0=no activity to 1=activity.  If the screen is currently dim it is
;   brightened.

NoteActivity    BTST    #1,ActiveState      ; bit 1: 0=dim, 1=bright
                BNE.S   @1                  ; branch if already bright

                MOVEM.L D0,-(SP)            ; save registers
                BSET    #1,ActiveState      ; bit 1: 0=dim, 1=bright
                MOVE.W  ActiveContrast,D0   ; active contrast level
                JSR     RampInternal        ; brighten the screen               <28Feb83>
                MOVEM.L (SP)+,D0            ; restore registers
@1              RTS                         ; return

;
;   CheckActivity, ChkActivity
;
;   ChkActivity is called when the activity alarm goes off, generally every
;   few minutes.  CheckActivity is called from SetFadeDelay when a new delay is
;   specified.  If the screen is currently bright and no user activity has
;   been noted since this routine was last called, it dims the screen.  In any
;   event the activity state is set to indicate no activity and the alarm is
;   set to go off again after the specified interval.
;

ChkActivity     ANDI    #$F9FF,SR           ; interrupt prio 7 --> 1            <14Feb84>
CheckActivity   MOVEM.L D0-D1,-(SP)         ; save registers
                CMP.B   #$02,ActiveState    ; activity or already dim ?
                BNE.S   @1                  ; branch if activity or dim

                BCLR    #1,ActiveState      ; bit 1: 0=dim, 1=bright
                MOVE.W  ScrnContrast,D0     ; current screen contrast           <28Feb83>
                CMP.W   CntrastDim,D0       ; current contrast dim ?
                BGE.S   @1                  ; branch if contrast dim

                MOVE.W  CntrastDim,D0       ; dim contrast level
                JSR     RampInternal        ; dim the screen                    <28Feb83>

@1              BCLR    #0,ActiveState      ; bit 0: 0=no activity, 1=activity
                MOVE.W  ActiveAlarm,D0      ; active alarm number
                MOVE.L  ActiveDelay,D1      ; milliseconds before dimming (long)
                JSR     AlarmRelative       ; set alarm to call CheckActivity
                MOVEM.L (SP)+,D0-D1         ; restore registers
                RTS                         ; return



;
;   Routine:    PowerDown *
;   Arguments:    none
;   Function:   Power off the machine.
;

PowerDown       MOVEM.L D0-D1,-(SP)         ; save registers
                MOVE.W  #255,D0             ; minimum contrast value
                JSR     SetContrast         ; dim the screen
                JSR     Clock               ; D0-D1 <- hardware clock/calendar
                CMP.W   #$0FFF,D0           ; clock running ?
                BNE.S   @1                  ; branch if clock running
                MOVE.W  #$20,D0             ; power off, timer off, clock off
                BRA.S   @2                  ; continue
@1              MOVE.W  #$21,D0             ; power off, timer off, clock on

@2              JSR     COPSCMD             ; send command to COPS
                MOVE.L  #$00010000,D0       ; a big number

@3              SUB.L   #1,D0               ; coast for a while until
                BGE.S   @3                  ;  the power is really off

                MOVEM.L (SP)+,D0-D1         ; restore registers
                BRA.S   PowerDown           ; Still not off?  Try again!


;
;   Routine:    PowerCycle *
;   Arguments:    D0 (input)   --  seconds until reboot (long)
;   Function:   Power off the machine.  Reboot after the specified number of
;               seconds.  Note that the reboot will not occur if the clock/
;               calendar hasn't been set since the last loss of battery power.
;

PowerCycle      MOVEM.L D0-D1,-(SP)         ; save registers
                MOVE.W  #255,D0             ; minimum contrast value
                JSR     SetContrast         ; dim the screen
                JSR     Clock               ; D0-D1 <- hardware clock/calendar
                CMP.W   #$0FFF,D0           ; clock running ?
                BNE.S   @0                  ; branch if clock running

                MOVEM.L (SP)+,D0-D1         ; restore registers
                JSR     PowerDown           ; power off without reboot
                BRA.S   PowerCycle          ; Still not off? Try again!

@0              MOVEM.L (SP)+,D0-D1         ; restore registers
                MOVEM.L D0-D2,-(SP)         ; save registers
                MOVE.L  D0,D1               ; seconds until reboot
                SWAP    D1                  ; seconds (low, high)
                MOVE.W  #$2D,D0             ; disable timer, enable/set clock
                JSR     COPSCMD             ; send command to COPS
                MOVE.W  #5-1,D2             ; send five alarm nibbles to COPS

@1              MOVE.W  D1,D0               ; low nibble and garbage
                AND.W   #$000F,D0           ; next nibble of alarm setting
                OR.W    #$0010,D0           ; 'write to clock' command
                JSR     COPSCMD             ; send command to COPS
                ROL.L   #4,D1               ; shift next nibble to low bits
                DBF     D2,@1               ; loop 5 times

                MOVE.W  #$23,D0             ; power off, reboot later
                JSR     COPSCMD             ; send command to COPS
                MOVE.L  #$00010000,D0       ; a big number
@2              SUB.L   #1,D0               ; coast for a while until
                BGE.S   @2                  ;  the power is really off

                MOVEM.L (SP)+,D0-D2         ; restore registers
                BRA.S   PowerCycle          ; Still not off? Try again!



;-----------------------------------------------------------------------------
;
;   The Speaker
;
;   The routines below provide square wave output from the Lisa speaker.
;   The speaker volume can be set to values in the range 0 (soft) to 7 (loud).
;   The initial volume setting is 4.  The approximate wavelength of the
;   tone produced can also be set.  The minimum wavelength is about 8
;   mircoseconds, which corresponds to a frequecncy of 125,000 cycles per
;   seconds, well above the audible range.  The maximum wavelength is 8,191
;   microseconds, which corresponds to about 122 cycles per second.
;
;   The Noise and Silence routines are called in pairs to start and stop
;   square wave output.  In contrast, the Beep routine starts square wave
;   output which will automatically stop after a specified period of time.
;   The effects of Noise, Silence and Beep can be overridden by subsequent
;   calls.
;
;
;   Routine:    Volume
;   Arguments:    D0 (output)  --  The speaker volume (0 to 7) (word)
;   Function:   Return the speaker volume.
;

Volume          MOVE.B  VIA2+PORTB2,D0      ; VIA2 Port B
                LSR.W   #1,D0               ; shift volume to low bits
                AND.W   #$7,D0              ; volume (0 to 7)
                RTS                         ; return


;
;   Routine:    SetVolume
;   Arguments:    D0 (input)   --  speaker volume (0 to 7) (word)
;   Function:   Set the speaker volume.
;

SetVolume       MOVEM.L D0-D1,-(SP)         ; save registers
                AND.W   #$7,D0              ; volume (0 to 7)
                LSL.W   #1,D0               ; shift into position
                MOVE.B  VIA2+PORTB2,D1      ; read from Port B
                AND.B   #$F1,D1             ; make hole for volume
                OR.B    D0,D1               ; insert volume
                MOVE.B  D1,VIA2+PORTB2      ; write to Port B
                MOVEM.L (SP)+,D0-D1         ; restore registers
                RTS                         ; return


;
;   Routine:    Noise
;   Arguments:    D0 (input)   --  wavelength in mircoseconds (0 to 8191)
;   Function:   Produce a continuous square wave tone from the speaker.
;

Noise           MOVEM.L D0-D1/A0,-(SP)      ; save registers
                TST.B   PostPepsiIO         ; 0=pre-Pepsi, 1=post-Pepsi               <17Aug83>
                BEQ.S   @0                  ; branch if pre-Pepsi I/O board           <17Aug83>

                MOVE.W  D0,D1               ; 1 * wavelength                          <17Aug83>
                LSR.W   #2,D1               ; .25 * wavelength                        <17Aug83>
                ADD.W   D1,D0               ; wavelength * 1.25                       <17Aug83>

@0              AND.W   #$1FFF,D0           ; max wavelength of 8191
                MOVE.B  #$55,D1             ; shift register <- 01010101
                LSR.W   #3,D0               ; divide wavelength by eight
                CMP.W   #$FF,D0             ; result fits in one byte?
                BLE.S   @1                  ; branch if fits in one byte

                MOVE.B  #$33,D1             ; shift register <- 00110011
                LSR.W   #1,D0               ; divide wavelength by two more
                CMP.W   #$FF,D0             ; result fits in one byte?
                BLE.S   @1                  ; branch if fits in one byte

                MOVE.B  #$0F,D1             ; shift register <- 00001111
                LSR.W   #1,D0               ; divide wavelength by two more

@1              MOVE.L  #VIA2,A0            ; Keyboard VIA memory address
                MOVE.B  D0,T2CL2(A0)        ; T2 Counter (Low-Order)
                CMP.B   SpeakerShift,D1     ; shift register already set ?
                BEQ.S   @2                  ; branch if already set

                MOVE.B  D1,SpeakerShift     ; speaker shift register contents
                MOVE.B  ACR2(A0),D0         ; read Auxiliary Control Register
                AND.B   #$E3,D0             ; first disable shift register
                OR.B    #$10,D0             ; shift free-running at T2 rate
                MOVE.B  D0,ACR2(A0)         ; write Auxiliary Control Register
                MOVE.B  D1,SR2(A0)          ; write Shift Register

@2              MOVEM.L (SP)+,D0-D1/A0      ; restore registers
                RTS                         ; return


;
;   Routine:    Silence
;   Arguments:    none
;   Function:   Silence the speaker.
;

Silence         MOVE.B  #0,SpeakerShift     ; speaker shift register contents
                AND.B   #$E3,VIA2+ACR2      ; disable shift register
                RTS                         ; return


;
;   Routine:    Beep
;   Arguments:    D0 (input)   --  wavelength in mircoseconds (0 to 8191)
;                 D1 (input)   --  duration of tone in milliseconds (long)
;   Function:   Produce a square wave tone from the speaker for a specified
;               number of milliseconds.

Beep            MOVE.L  D0,-(SP)            ; save registers
                JSR     Noise               ; start the tone
                MOVE.W  SpeakerAlarm,D0     ; speaker alarm number
                JSR     AlarmRelative       ; set the alarm
                MOVE.L  (SP)+,D0            ; restore registers
                RTS                         ; return



;-----------------------------------------------------------------------------
;
;   The MMU (Memory Management Unit) Registers
;
;   Routines ReadMMU and WriteMMU access the MMU registers of the current domain.
;   These routines are a temporary part of the hardware interface package, used
;   in testing Smalltalk on the Lisa OS.
;
;
;   Routine:    ReadMMU *
;   Arguments:    D0 (input)   --  segment number (integer)
;                 D1 (output)  --  origin register contents (integer)
;                 D2 (output)  --  access/limit register contents (integer)
;   Function:   Return the origin and access/limit MMU registers for the
;               specified register in the current domain.
;

ReadMMU         MOVEM.L A0-A2/D0/D3-D7,-(SP); save registers
                DISABLE                     ; disable interrupts
                SWAP    D0                  ; segment in bits 23..16
                ADD.L   D0,D0               ; segment in bits 24..17
                AND.L   #$00FE0000,D0       ; remove any junk
                ADD.L   #$8000,D0           ; address of segment limit register
                MOVE.L  D0,A0               ; address of segment limit register
                LEA     ReadMagic,A1        ; address of ReadMagic
                MOVE.L  #MMURoutine,A2      ; address where MMU routines execute
                MOVEM.L (A2),D3-D7          ; save memory at MMURoutine
                MOVEM.L D3-D7,-(SP)         ; memory at MMURoutine
                MOVEM.L (A1),D3-D7          ; ReadMagic routine
                MOVEM.L D3-D7,(A2)          ; move ReadMagic to MMURoutine
                JSR     (A2)                ; call ReadMagic
                MOVEM.L (SP)+,D3-D7         ; memory at MMURoutine
                MOVEM.L D3-D7,(A2)          ; restore memory at MMURoutine
                ENABLE                      ; enable interrupts
                AND.W   #$0FFF,D1           ; origin register contents
                AND.W   #$0FFF,D2           ; access/limit register contents
                MOVEM.L (SP)+,A0-A2/D0/D3-D7; restore registers
                RTS                         ; return

ReadMagic       TST.B   SetUpSet            ; set the SETUP bit
                MOVE.W  8(A0),D1            ; read the origin register
                MOVE.W  (A0),D2             ; read the access/limit register
                TST.B   SetUpReset          ; clear the SETUP bit
                RTS                         ; return


;
;   Routine:    WriteMMU *
;   Arguments:    D0 (input)   --  segment number (integer)
;                 D1 (input)   --  origin register contents (integer)
;                 D2 (input)   --  access/limit register contents (integer)
;   Function:   Set the origin and access/limit MMU registers for the
;               specified register in the current domain.
;
;


WriteMMU        MOVEM.L A0-A2/D0/D3-D7,-(SP); save registers
                AND.W   #$0FFF,D1           ; origin register contents
                AND.W   #$0FFF,D2           ; access/limit register contents
                DISABLE                     ; disable interrupts
                SWAP    D0                  ; segment in bits 23..16
                ADD.L   D0,D0               ; segment in bits 24..17
                AND.L   #$00FE0000,D0       ; remove any junk
                ADD.L   #$8000,D0           ; address of segment limit register
                MOVE.L  D0,A0               ; address of segment limit register
                LEA     WriteMagic,A1       ; address of WriteMagic
                MOVE.L  #MMURoutine,A2      ; address where MMU routines execute
                MOVEM.L (A2),D3-D7          ; save memory at MMURoutine
                MOVEM.L D3-D7,-(SP)         ; memory at MMURoutine
                MOVEM.L (A1),D3-D7          ; WriteMagic routine
                MOVEM.L D3-D7,(A2)          ; move WriteMagic to MMURoutine
                JSR     (A2)                ; call WriteMagic
                MOVEM.L (SP)+,D3-D7         ; memory at MMURoutine
                MOVEM.L D3-D7,(A2)          ; restore memory at MMURoutine
                ENABLE                      ; enable interrupts
                MOVEM.L (SP)+,A0-A2/D0/D3-D7; restore registers
                RTS                         ; return

WriteMagic      TST.B   SetUpSet            ; set the SETUP bit
                MOVE.W  D1,8(A0)            ; write the origin register
                MOVE.W  D2,(A0)             ; write the access/limit register
                TST.B   SetUpReset          ; clear the SETUP bit
                RTS                         ; return







ÿ