
;
;   File CURSOR.TEXT
;
;-----------------------------------------------------------------------------
;
;   Hardware Interface for the Cursor
;
;   Written by Rick Meyers
;   Copyright 1983, Apple Computer Inc.
;
;
;
;   The routines below provide an assembly language interface to the cursor.
;   Input parameters are passed in registers, output parameters are returned
;   in registers.  Unless otherwise noted, all registers are preserved.
;
;   Routines labeled with a * require supervisor mode.
;




;-----------------------------------------------------------------------------
;
;   The Cursor
;
;   The cursor is a small image that is displayed on the screen.  It's shape
;   is specified by two bitmaps, called 'data' and 'mask'.  These bitmaps are
;   16 bits wide and from 0 to 32 bits high.  The rule used to combine the
;   bits already on the screen with the data and mask is
;
;       screen <- (screen and (not mask)) xor data.
;
;   The effect is that white areas of the screen are replaced with the cursor
;   data.  Black areas of the screen are replaced with (not mask) xor data.
;   If the data and mask bitmaps are identical, the effect is to 'or' the data
;   onto the screen.
;
;   The cursor has both a location and a hotspot.  The location is a position
;   on the screen, with X-coordinates of 0 to 719 and Y-coordinates of 0 to 363.
;   The hotspot is a position within the cursor bitmaps, with X- and Y-coordi-
;   nates ranging from 0 to 16.  The cursor is displayed on the screen with it's
;   hotspot at location.  If the cursor's location is near an edge of the screen,
;   the cursor image may be partially or completely off the screen.
;
;   Initially the cursor data and mask bitmaps contain all zeroes, which yields
;   a blank (invisible) cursor.  The initial hotspot is 0,0.
;
;
;   Function:   CursorImage
;   Arguments:    D0 (input)   --  hotspot X-coordinate (integer)
;                 D1 (input)   --  hotspot Y-coordinate (integer)
;                 D2 (input)   --  cursor height, 0-32 (integer)
;                 A0 (input)   --  address of data bitmap (address)
;                 A1 (input)   --  address of mask bitmap (address)
;   Function:   Set the cursor hotspot and data and mask bitmaps.
;

CursorImage     MOVEM.L D0-D3/A0-A3,-(SP)   ; save registers
                JSR     CursorHide          ; remove old cursor from screen
                MOVEQ   #16,D3              ; cache 16

                TST.W   D0                  ; hotspot X-coordinate >= 0 ?
                BGE.S   @1                  ; branch if >= 0
                MOVE.W  #0,D0               ; minimum hotspot X-coord of 0

@1              CMP.W   D3,D0               ; hotspot X-coordinate <= 16 ?
                BLE.S   @2                  ; branch if <= 16
                MOVE.W  D3,D0               ; maximum hotspot X-coord of 16

@2              TST.W   D1                  ; hotspot Y-coordinate >= 0 ?
                BGE.S   @3                  ; branch if >= 0
                MOVE.W  #0,D1               ; minimum hotspot Y-coord of 0

@3              CMP.W   D3,D1               ; hotspot Y-coordinate <= 16 ?
                BLE.S   @4                  ; branch if <= 16
                MOVE.W  D3,D1               ; maximum hotspot Y-coord of 16

@4              TST.W   D2                  ; cursor height >= 0 ?
                BGE.S   @5                  ; branch if >= 0
                MOVE.W  #0,D1               ; minimum cursor height of 0

@5              CMP.W   #32,D2              ; cursor height <= 32 ?
                BLE.S   @6                  ; branch if <= 32
                MOVE.W  #32,D2              ; maximum cursor height of 32

@6              MOVE.W  D0,CrsrHotX         ; cursor hotspot X-coordinate
                MOVE.W  D1,CrsrHotY         ; cursor hotspot Y-coordinate
                MOVE.W  D2,CrsrHeight       ; cursor hieght, 0-32
                LEA     CrsrData,A2         ; cursor data bitmap (address)
                LEA     CrsrMask,A3         ; cursor mask bitmap (address)
                BRA.S   @8                  ; check for cursor height of 0

@7              MOVE.W  (A0)+,(A2)+         ; transfer cursor data
                MOVE.W  (A1)+,(A3)+         ; transfer cursor mask
@8              DBF     D2,@7               ; iterate cursor height times

                MOVEM.L (SP)+,D0-D3/A0-A3   ; restore registers
                JSR     CursorDisplay       ; display new cursor on screen
                RTS                         ; return



;
;
;   Routine:    CursorLocation
;   Arguments:    D0 (input)   --  X-coordinate (integer)
;                 D1 (input)   --  Y-coordinate (integer)
;   Function:   Set the cursor location.  Note that if tracking is enabled
;               this will have no effect.
;

CursorLocation  ACTIVE                      ; report user activity
                TST.B   CrsrTracking        ; 0=disabled, else=enabled
                BNE.S   @8                  ; branch if tracking enabled

                MOVEM.L D0-D1,-(SP)         ; save registers
                TST.W   D0                  ; new X-coordinate >= 0
                BGE.S   @1                  ; branch if >= 0
                MOVE.W  #0,D0               ; minimum X of 0

@1              CMP.W   #MaxX,D0            ; new X-coordinate <= 719
                BLE.S   @2                  ; branch if <= 719
                MOVE.W  #MaxX,D0            ; maximum X of 719

@2              TST.W   D1                  ; new Y-coordinate >= 0
                BGE.S   @3                  ; branch if >= 0
                MOVE.W  #0,D1               ; minimum Y of 0

@3              CMP.W   #MaxY,D1            ; new Y-coordinate <= 363
                BLE.S   @4                  ; branch if <= 363
                MOVE.W  #MaxY,D1            ; maximum Y of 363

@4              CMP.W   CrsrX,D0            ; new X-coord ~= old X-coord
                BNE.S   @5                  ; branch if X changed
                CMP.W   CrsrY,D1            ; new Y-coord ~= old Y-coord
                BEQ.S   @7                  ; branch if cursor hasn't moved

@5              TST.B   CrsrObscured        ; 0=not obscured, else=obscured
                BNE.S   @6                  ; branch if obscured
                JSR     CursorHide          ; remove cursor from screen

@6              MOVE.W  D0,CrsrX            ; cursor X-coordinate
                MOVE.W  D1,CrsrY            ; cursor Y-coordinate
                JSR     CursorDisplay       ; redisplay cursor in new location
                MOVE.B  #0,CrsrObscured     ; 0=not obscured, else=obscured

@7              MOVEM.L (SP)+,D0-D1         ; restore registers
@8              RTS                         ; return




;-----------------------------------------------------------------------------
;
;   Cursor/Mouse Tracking
;
;   When 'tracking' is disabled the mouse location and cursor location are
;   independent.  Setting the cursor location will move the cursor, moving
;   the mouse will not.
;
;   When tracking is enabled the cursor location is changed to the mouse
;   location each time the mouse moves.  Setting the cursor location will
;   have no effect, the cursor sticks with the mouse.
;
;   When tracking is first enabled (i.e. on each transition from disabled to
;   enabled) the mouse location is modified to equal the cursor location.
;   Therefore, enabling tracking does not move the cursor; it does modify the
;   mouse location.
;
;   Initially tracking is enabled.
;
;
;   Routine:    CursorTracking
;   Arguments:    D0 (input)   --  0=disable; else=enable
;   Function:   Enable and disable cursor tracking of the mouse.  Note that
;               enabling tracking will modify the mouse location.
;

CursorTracking  TST.B   D0                  ; disable tracking ?
                BEQ.S   @1                  ; branch if being disabled

                TST.B   CrsrTracking        ; already enabled ?
                BNE.S   @1                  ; branch if already enabled

                MOVE.L  CrsrX,MousX         ; relocate mouse to cursor
@1              MOVE.B  D0,CrsrTracking     ; 0=disable, else=enable
                RTS                         ; return



;-----------------------------------------------------------------------------
;
;   Cursor Hide, Shield and Display
;
;   Care must be taken when updating the screen image which is 'under' the
;   cursor.  The simplest approach is to remove the cursor from the screen
;   (hide), do the screen modification, then redisplay the cursor (display).
;
;   This simple solution may yield excessive cursor blinking.  A subtler
;   approach is to remove the cursor only when it is in the same area of the
;   screen that is being updated.  A conditional hide, called 'shield', removes
;   the cursor from the screen if the data 'under' the cursor intersects a
;   given rectangular area.
;
;   Each hide or shield operation must be followed by a corresponding display
;   operation.  The operations are paired and can be nested.  The first of a
;   series of hides removes the cursor from the screen; it's corresponding
;   display redisplays the cursor.  Intervening operations have no apparent
;   effect.
;
;   It is also possible to remove the cursor from the screen and have it
;   reappear automatically the next time the cursor moves.  This operation
;   is called 'obscure'.  The cursor movement may be caused by either mouse
;   movement (if tracking is enabled) or by a call to CursorLocation (if
;   tracking is disabled).
;
;   Occasionally is is necessary to definately display the cursor, independent
;   of previous calls to CursorHide, CursorShield and CursorObscure.  This can
;   be done by calling CursorInit.  CursorInit also disables the busy cursor.
;
;
;
;   Routine:    CursorHide
;   Arguments:    none
;   Function:   Remove the cursor from the screen.  Note that every call to
;               CursorHide must be followed by exactly one call to CursorDisplay.
;

CursorHide      MOVE.B  #1,CrsrBusy         ; cursor change in progress
                TST.B   CrsrVisible         ; cursor visible ?
                BEQ.S   @3                  ; branch if not visible

                MOVEM.L D0-D1/A0-A1,-(SP)   ; save registers
                LEA     SavedData,A0        ; saved data address
                MOVE.L  SavedAddr,A1        ; saved data screen address
                MOVE.W  SavedRows,D0        ; rows of saved data
                MOVE.W  #720/8,D1           ; bytes per row on screen
                BRA.S   @2                  ; test of rows=0

@1              MOVE.L  (A0)+,(A1)          ; from saved to screen
                ADD.W   D1,A1               ; screen address of next row
@2              DBF     D0,@1               ; loop 'SavedRows' times

                MOVE.B  #0,CrsrVisible      ; cursor not visible
                MOVEM.L (SP)+,D0-D1/A0-A1   ; restore registers

@3              SUB.W   #1,CrsrHidden       ; <= 0 implies hidden
                MOVE.B  #0,CrsrBusy         ; cursor change complete
                RTS                         ; return



;
;   Routine:    CursorShield
;   Arguments:    D0 (input)   --  top left X-coordinate (integer)
;                 D1 (input)   --  top left Y-coordinate (integer)
;                 D2 (input)   --  bottom right X-coordinate (integer)
;                 D3 (input)   --  bottom right Y-coordinate (integer)
;   Function:   Remove the cursor from the screen if the data 'under' the
;               cursor intersects the specified rectangle.  Note that every
;               call to CursorShield must be followed by exactly one call
;               to CursorDisplay.
;

CursorShield    MOVE.B  #1,CrsrBusy         ; cursor change in progress
                SUB.W   #1,CrsrHidden       ; <= 0 implies hidden
                TST.B   CrsrVisible         ; cursor visible ?
                BEQ.S   @2                  ; branch if not visible

                MOVEM.L D0-D1,-(SP)         ; save registers
                CMP.W   SavedY,D3           ; bottom right Y < SavedY ?
                BLT.S   @1                  ; branch if cursor below shield

                SUB.W   SavedRows,D1        ; top left Y - rows
                CMP.W   SavedY,D1           ; top left Y - rows >= SavedY ?
                BGE.S   @1                  ; branch if cursor above shield

                CMP.W   SavedX,D2           ; bottom right X < SavedX ?
                BLT.S   @1                  ; branch if cursor right of shield

                SUB.W   #32,D0              ; top left X - columns
                CMP.W   SavedX,D0           ; top left X - columns >= SavedX ?
                BGE.S   @1                  ; branch if cursor left of shield

                ADD.W   #1,CrsrHidden       ; CursorHide will decrement it
                JSR     CursorHide          ; remove cursor from screen

@1              MOVEM.L (SP)+,D0-D1         ; restore registers
@2              MOVE.B  #0,CrsrBusy         ; cursor change complete
                RTS                         ; return



;
;   Routine:    CursorDisplay
;   Arguments:    none
;   Function:   Redisplay the cursor.  Note that every call to CursorDisplay must
;               be preceeded by exactly one call to CursorHide or CursorShield.
;
;   Register Assignments:
;
;       D0  --  saved data X-coordinate, cursor data
;       D1  --  saved data Y-coordinate, cursor mask
;       D2  --  left shift count
;       D3  --  32-bit mask
;       D4  --  rows of saved data
;       D5  --  bytes per row on screen
;
;       A0  --  saved data address
;       A1  --  saved data screen address
;       A2  --  cursor data address
;       A3  --  cursor mask address
;

CursorDisplay   MOVE.B  #1,CrsrBusy         ; cursor change in progress
                ADD.W   #1,CrsrHidden       ; <= 0 implies hidden
                BLE     @8                  ; branch if will remain hidden

                MOVE.W  #1,CrsrHidden       ; maximum hidden of 1
                TST.B   CrsrVisible         ; cursor visible ?
                BNE     @8                  ; branch if already visible

                MOVEM.L D0-D5/A0-A3,-(SP)   ; save registers

                LEA     SavedData,A0        ; saved data address
                MOVE.L  ScrnAddr,A1         ; screen memory address
                TST.B   CrsrSwitch          ; 0=normal, else=busy cursor
                BNE.S   @10                 ; branch if busy cursor

                LEA     CrsrData,A2         ; cursor data bitmap address
                LEA     CrsrMask,A3         ; cursor mask bitmap address
                MOVE.W  CrsrHotX,D0         ; cursor hotspot X-coordinate
                MOVE.W  CrsrHotY,D1         ; cursor hotspot Y-coordinate
                MOVE.W  CrsrHeight,D4       ; cursor height
                BRA.S   @11                 ; continue

@10             LEA     BusyData,A2         ; busy cursor data bitmap address
                LEA     BusyMask,A3         ; busy cursor mask bitmap address
                MOVE.W  BusyHotX,D0         ; busy cursor hotspot X-coordinate
                MOVE.W  BusyHotY,D1         ; busy cursor hotspot Y-coordinate
                MOVE.W  BusyHeight,D4       ; busy cursor height


;   Compute and bounds check the X-coordinate of the data under the cursor.

@11             SUB.W   CrsrX,D0            ;  cursor X-coordinate
                NEG.W   D0                  ;  - cursor hotspot X-coordinate
                MOVE.W  D0,D2               ; upper left X-coordinate
                AND.W   #$000F,D2           ; bit offset within word
                NEG.W   D2                  ;  negated and converted to
                ADD.W   #16,D2              ;  left shift count
                MOVE.L  #$0000FFFF,D3       ; 32-bit mask
                LSL.L   D2,D3               ;  shifted into position

                AND.W   #$FFF0,D0           ; upper left X-coord rounded down
                BGE.S   @0                  ; branch if >= 0

                MOVE.W  #0,D0               ; minimum upper left X-coord of 0
                LSL.L   #8,D3               ; adjust 32-bit mask
                LSL.L   #8,D3               ; adjust 32-bit mask
                ADD.W   #16,D2              ; adjust left shift count

@0              CMP.W   #720-32,D0          ; upper left X-coord <= 720-32
                BLE.S   @2                  ; branch if <= 720-32

                CMP.W   #720,D0             ; cursor off right edge ?
                BNE.S   @1                  ; branch if not off right edge
                MOVEQ   #0,D3               ; mask off all bits

@1              MOVE.W  #720-32,D0          ; maximum X-coord of 720-32
                LSR.L   #8,D3               ; adjust 32-bit mask
                LSR.L   #8,D3               ; adjust 32-bit mask
                ADD.W   #16,D2              ; adjust left shift count

;   Compute and bounds check the Y-coordinate of the data under the cursor.

@2              SUB.W   CrsrY,D1            ; cursor Y-coordinate
                NEG.W   D1                  ;  - cursor hotspot Y-coordinate
                BGE.S   @3                  ; branch if upper left Y >= 0

                ADD.W   D1,D4               ; decrease rows of saved data
                ADD.W   D1,D1               ; double for byte count
                SUB.W   D1,A2               ; increase cursor data address
                SUB.W   D1,A3               ; increase cursor mask address
                MOVE.W  #0,D1               ; minimum upper left Y of 0
                BRA.S   @4                  ; continue

@3              MOVE.W  #364,D5             ; maximum Y-coordinate
                SUB.W   D4,D5               ;  - cursor height
                CMP.W   D5,D1               ; cursor bottom <= 364-CrsrHeight ?
                BLE.S   @5                  ; branch if <= 364-CrsrHeight

                MOVE.W  #364,D4             ; last row on screen
                SUB.W   D1,D4               ; adjust rows of saved data

@4              TST.W   D4                  ; rows of saved data >= 0 ?
                BGE.S   @5                  ; branch if  >= 0
                MOVE.W  #0,D4               ; minimum rows of saved data

@5              MOVE.W  D0,SavedX           ; saved data X-coordinate
                MOVE.W  D1,SavedY           ; saved data Y-coordinate
                MOVE.W  D4,SavedRows        ; rows of saved data

;   Display the cursor on the screen.

                LSR.W   #3,D0               ; convert X-coord to bytes
                ADD.W   D0,A1               ;  and add to screen address
                MOVEQ   #720/8,D5           ; bytes per row on screen
                MULU    D5,D1               ;  * Y-coord
                ADD.L   D1,A1               ;  added to screen address
                MOVE.L  A1,SavedAddr        ; saved data screen address
                BRA.S   @7                  ; test for rows=0

 @6             MOVE.W  (A2)+,D0            ; cursor data
                ROL.L   D2,D0               ; shift to proper bit position
                AND.L   D3,D0               ; eliminate unwanted bits
                MOVE.W  (A3)+,D1            ; cursor mask
                ROL.L   D2,D1               ; shift to proper bit position
                AND.L   D3,D1               ; eliminate unwanted bits
                NOT.L   D1                  ; invert cursor mask

                MOVE.L  (A1),(A0)+          ; from screen to saved data
                AND.L   D1,(A1)             ; screen and (not mask)
                EOR.L   D0,(A1)             ;  xor cursor data
                ADD.W   D5,A1               ; screen address of next row
@7              DBF     D4,@6               ; loop 'SavedRows' times

                MOVE.B  #1,CrsrVisible      ; cursor visible
                MOVEM.L (SP)+,D0-D5/A0-A3   ; restore registers

@8              MOVE.B  #0,CrsrBusy         ; cursor change complete
                RTS                         ; return


;
;   Routine:    CursorObscure
;   Arguments:    none
;   Function:   Remove the cursor from the screen.  Redisplay the cursor
;               automatically the next time the cursor moves, either in
;               response to mouse movement or in response to a call to
;               CursorLocation.

CursorObscure   MOVE.B  #1,CrsrBusy         ; cursor change in progress
                BSET    #0,CrsrObscured     ; 0=not obscured, else=obscured
                BNE.S   @1                  ; branch if already obscured
                JSR     CursorHide          ; hide the cursor, unlock semaphore

@1              MOVE.B  #0,CrsrBusy         ; cursor change complete
                RTS                         ; return


;
;   Routine:    CursorInit
;   Arguments:    none
;   Function:   Definately redisplay the normal cursor, independent of previous
;               calls to CursorHide, CursorShield, CursorObscure and BusyDelay.
;
;
;   Routine:    CursorReInit
;   Arguments:    none
;   Function:   Definately redisplay the normal or busy cursor, independent of
;               previous calls to CursorHide, CursorShield and CursorObscure.
;               Retain the status of the busy cursor.
;

CursorInit      MOVE.L  D0,-(SP)            ; save registers
                MOVE.W  BusyAlarm,D0        ; busy cursor alarm number
                JSR     AlarmOff            ; disable the busy cursor alarm
                MOVE.B  #0,CrsrSwitch       ; 0=normal, else=busy cursor
                JSR     CursorHide          ; hide the cursor
                MOVE.L  (SP)+,D0            ; restore registers

CursorReInit    MOVE.B  #1,CrsrBusy         ; cursor change in progress
                MOVE.B  #0,CrsrObscured     ; 0=not obscured, else=obscured
                MOVE.W  #0,CrsrHidden       ; <= 0 implies hidden
                JSR     CursorDisplay       ; display the cursor
                RTS                         ; return


;-----------------------------------------------------------------------------
;
;   The Busy Cursor
;
;   Applications may desire to display a busy cursor (e.g. an hourglass) when
;   an operation in progress requires more than a few seconds to complete.
;   Routine BusyImage is used to specify the data bitamp, mask bitmap, height
;   and hotspot of the busy cursor.
;
;   A call to BusyDelay specifies that the normal cursor should currently be
;   displayed, and that display of the busy cursor should delayed for a
;   specified number of milliseconds. Subsequent calls to BusyDelay override
;   previous calls, postponing display of the busy cursor.  If no calls to
;   BusyDelay occur for the specified number of milliseconds, the busy cursor
;   will be displayed until the next call to BusyCursor.
;
;   Initially the busy cursor data and mask bitmaps contain all zeroes, which
;   yields a blank (invisible) cursor.  The initial hotspot is 0,0.  The initial
;   busy delay is  infinite, that is, the busy cursor will not be displayed until
;   BusyDelay is called.
;
;
;   Function:   BusyImage
;   Arguments:    D0 (input)   --  hotspot X-coordinate (integer)
;                 D1 (input)   --  hotspot Y-coordinate (integer)
;                 D2 (input)   --  cursor height, 0-32 (integer)
;                 A0 (input)   --  address of data bitmap (address)
;                 A1 (input)   --  address of mask bitmap (address)
;   Function:   Set the busy cursor hotspot and data and mask bitmaps.
;

BusyImage       MOVEM.L D0-D3/A0-A3,-(SP)   ; save registers
                MOVEQ   #16,D3              ; cache 16

                TST.W   D0                  ; hotspot X-coordinate >= 0 ?
                BGE.S   @1                  ; branch if >= 0
                MOVE.W  #0,D0               ; minimum hotspot X-coord of 0

@1              CMP.W   D3,D0               ; hotspot X-coordinate <= 16 ?
                BLE.S   @2                  ; branch if <= 16
                MOVE.W  D3,D0               ; maximum hotspot X-coord of 16

@2              TST.W   D1                  ; hotspot Y-coordinate >= 0 ?
                BGE.S   @3                  ; branch if >= 0
                MOVE.W  #0,D1               ; minimum hotspot Y-coord of 0

@3              CMP.W   D3,D1               ; hotspot Y-coordinate <= 16 ?
                BLE.S   @4                  ; branch if <= 16
                MOVE.W  D3,D1               ; maximum hotspot Y-coord of 16

@4              TST.W   D2                  ; cursor height >= 0 ?
                BGE.S   @5                  ; branch if >= 0
                MOVE.W  #0,D1               ; minimum cursor height of 0

@5              CMP.W   #32,D2              ; cursor height <= 32 ?
                BLE.S   @6                  ; branch if <= 32
                MOVE.W  #32,D2              ; maximum cursor height of 32

@6              MOVE.W  D0,BusyHotX         ; cursor hotspot X-coordinate
                MOVE.W  D1,BusyHotY         ; cursor hotspot Y-coordinate
                MOVE.W  D2,BusyHeight       ; cursor hieght, 0-32
                LEA     BusyData,A2         ; cursor data bitmap (address)
                LEA     BusyMask,A3         ; cursor mask bitmap (address)
                BRA.S   @8                  ; check for cursor height of 0

@7              MOVE.W  (A0)+,(A2)+         ; transfer cursor data
                MOVE.W  (A1)+,(A3)+         ; transfer cursor mask
@8              DBF     D2,@7               ; iterate cursor height times

                MOVEM.L (SP)+,D0-D3/A0-A3   ; restore registers
                RTS                         ; return


;
;   Routine:    BusyDelay
;   Arguments:    D0 (input)   --  milliseconds before busy cursor (long)
;   Funciton:   Set the approximate period of time, in milliseconds,
;               before the busy cursor will be displayed.


BusyDelay       MOVEM.L D0-D1,-(SP)         ; save registers
                TST.L   D0                  ; positive delay ?
                BGT.S   @1                  ; branch if positive delay

                TST.B   CrsrSwitch          ; 0=normal, else=busy cursor
                BNE.S   @3                  ; branch if busy cursor

                JSR     BusyCursor          ; display the busy cursor
                BRA.S   @3                  ; continue

@1              TST.B   CrsrSwitch          ; 0=normal, else=busy cursor
                BEQ.S   @2                  ; branch if normal cursor

                JSR     CursorHide          ; hide the busy cursor
                MOVE.B  #0,CrsrSwitch       ; 0=normal, else=busy cursor
                JSR     CursorDisplay       ; display the normal cursor

@2              MOVE.L  D0,D1               ; milliseconds
                MOVE.W  BusyAlarm,D0        ; busy alarm number
                JSR     AlarmRelative       ; set busy alarm

@3              MOVEM.L (SP)+,D0-D1         ; restore registers
                RTS                         ; return




;
;   BusyCursor & BzyCursor
;
;   Internal routine BusyCursor is called when the busy cursor alarm goes off,
;   and from BusyDelay.  It's function is to display the busy cursor.  BusyCursor
;   operates as an interrupt routine.  D0/D1 are not preserved.
;

BzyCursor       ANDI    #$F9FF,SR           ; interrupt prio 7 --> 1 from alarm     <05Jan84>
BusyCursor      TST.B   CrsrBusy            ; 0=not busy, else=busy
                BEQ.S   @1                  ; branch if not busy

                MOVE.W  BusyAlarm,D0        ; busy alarm number
                MOVE.L  #50,D1              ; try again in 50 milliseconds
                JSR     AlarmRelative       ; set busy alarm
                RTS                         ; return

@1              JSR     CursorHide          ; hide the normal cursor
                MOVE.B  #1,CrsrSwitch       ; 0=normal, else=busy cursor
                JSR     CursorDisplay       ; display the busy cursor
                RTS                         ; return





ÿ