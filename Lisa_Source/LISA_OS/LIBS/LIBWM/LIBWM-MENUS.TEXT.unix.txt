
UNIT Menus;

{ CopyRight 1983, Apple Computer, Inc. }
{ Written by Bill Atkinson             }

INTRINSIC SHARED;

INTERFACE

USES {$U libhw/HWInt       }  HWInt,
     {$U libsm/UnitStd     }  UnitStd,
     {$U libsm/UnitHz      }  UnitHz,
     {$U libqd/Storage     }  Storage,
     {$U libqd/QuickDraw   }  QuickDraw,
     {$U libfm/FontMgr     }  FontMgr,
     {$U libos/SysCall     }  SysCall,
     {$U libwm/Events      }  Events,
     {$U libwm/Windows     }  Windows,
     {$U libwm/Folders     }  Folders;

CONST maxItem     =  31;    { max number of items in a text menu }
      noMark      =  0;     { mark symbols for MarkItem }
      vertSpace   =  15;    { vertical spacing in text menus }
{$IFC wmOs }
      checkMark   =  ccCheck;
      appleMark   =  ccApple;
{$ELSEC}
      checkMark   =  142;
      appleMark   =  143;
{$ENDC}


TYPE

     MenuPtr  = ^MenuInfo;
     MenuInfo = RECORD
                  menuId:       INTEGER;
                  menuWidth:    INTEGER;
                  menuHeight:   INTEGER;
                  DrawProc:     ProcPtr;
                  ChooseProc:   ProcPtr;
                  enableFlags:  PACKED ARRAY[0..maxItem] OF BOOLEAN;
                  menuData:     Handle;
                END;
     rMenuInfo = array [0..0] of MenuInfo;
     rMenuPtr  = ^rMenuInfo;
     ItemPtr = ^Str255;


VAR menuDelay:   INTEGER;
    desktopMenu: ARRAY [0..1] OF MenuInfo;

PROCEDURE CalcMenuSize(VAR menu: MenuInfo);
PROCEDURE CheckItem  (VAR menu: MenuInfo; item: INTEGER; checked: BOOLEAN);
PROCEDURE ChooseTxtItem(VAR menu: MenuInfo;
                        menuRect: Rect;
                        hitPt: Point;
                        VAR whichItem: INTEGER);
PROCEDURE ClearMenuBar;
PROCEDURE DeleteMenu (menuId: INTEGER);
PROCEDURE DisableItem(VAR menu: MenuInfo; item: INTEGER);
PROCEDURE DisableMenu(VAR menu: MenuInfo);
PROCEDURE DrawMenuBar;
PROCEDURE DrawTxtMenu(VAR menu: MenuInfo; menuRect: Rect);
PROCEDURE EnableItem (VAR menu: MenuInfo; item: INTEGER);
PROCEDURE EnableMenu (VAR menu: MenuInfo);
PROCEDURE GetItem    (VAR menu: MenuInfo; item: INTEGER; itemString: ItemPtr);
PROCEDURE HiLiteMenu (menuId: INTEGER);
PROCEDURE InitMenus;
PROCEDURE InsertMenu (VAR menu: MenuInfo; beforeId: INTEGER);
PROCEDURE MarkItem   (VAR menu: MenuInfo; item: INTEGER; whichMark: INTEGER);
PROCEDURE MenuSelect (startPt: Point; VAR whichMenu,whichItem: INTEGER);
PROCEDURE MenuKey    (ch: CHAR; VAR whichMenu,whichItem: INTEGER);
PROCEDURE ReadMenu   (VAR menuFile: TEXT; VAR menu: MenuInfo);
PROCEDURE SetItem    (VAR menu: MenuInfo; item: INTEGER; itemString: ItemPtr);



IMPLEMENTATION

{$IFC wmDebug}
{$R+}
{$ELSEC}
{$R-}
{$ENDC}

{$IFC wmSymbols}
{$D+}
{$ELSEC}
{$D-}
{$ENDC}


CONST maxMenu   =    16;    { maximum number of menus in menu bar }
      maxBytes  =   800;    { max number of bytes in a menuData^^ }
      hDiam     =    27;    { horiz diameter of curvature for screen }
      vDiam     =    18;    { vert diameter of curvature for screen  }


VAR menuList:   ARRAY[0..maxMenu] OF MenuPtr;  { 0th elements points to desktopmenu[1] }
    menuLeft:   ARRAY[0..maxMenu] OF INTEGER;  { 0th elements set by filer to width of desktop title }
    lastMenu:   INTEGER;  { current number of menus in menuList }
    lastRight:  INTEGER;  { right edge of rightmost title }
    theMenu:    INTEGER;  { menuId of the highlighted menu }
    desktitle:  INTEGER;  { width of desktopMenu title }

FUNCTION  GetIndex(menuId: INTEGER): INTEGER;                        FORWARD;
PROCEDURE InvertTitle(whichMenu: INTEGER);                           FORWARD;


{$S WMothrwk }
{-------------------------------------------------------------------------}
PROCEDURE CalcMenuSize{(VAR menu: MenuInfo)};
{-------------------------------------------------------------------------}
{ scan menuData to set up menuWidth, menuHeight }
VAR srcPtr: ^Str255;
    width,height,maxWidth,len:  INTEGER;
    savePort:  GrafPtr;
    appleChar: CHAR;

BEGIN
{$IFC fTraceWM}    LogCall;     {$ENDC}
  GetPort(savePort);
  SetPort(menuFolder);   { measure with the right font }

  srcPtr := Pointer(ORD(menu.menuData^));         { point to title }
  IF @menu = @DesktopMenu[1] THEN
    BEGIN
  (*  TextFont(fIconFont);   *** If an icon is avail for the desktop menu title ***)
      deskTitle := StringWidth(srcPtr^)+20;
  (*  TextFont(sysText);   *** ditto ***)
    END;
  len := Length(srcPtr^);                    { get length of title }
  len := len-3;         { adjust since title has no special chs }

  maxWidth := 0;
  height := -vertSpace;
  REPEAT
    srcPtr := Pointer(ORD(srcPtr)+len+4);  { point to next item }
    len    := Length(srcPtr^);
    height := height+vertSpace;
    width  := StringWidth(srcPtr^);
    {$R-}
    appleChar := srcPtr^[len+1];
    {$IFC wmDebug}{$R+}{$ENDC}
    IF appleChar <> ' ' THEN width := width + 24;
    IF width > maxWidth THEN maxWidth := width;
  UNTIL len = 0;

  IF maxWidth > 340 THEN maxWidth := 340;
  menu.menuWidth := maxWidth + 24;
  menu.menuHeight := height;
  SetPort(savePort);
END;


{-------------------------------------------------------------------------}
PROCEDURE CallDrawProc(VAR menu: MenuInfo; menuRect: Rect; DrawProc: ProcPtr);
{-------------------------------------------------------------------------}
EXTERNAL;


{-------------------------------------------------------------------------}
PROCEDURE CallChooseProc(VAR menu: MenuInfo;
                         menuRect: Rect;
                         hitPt: Point;
                         VAR whichItem: INTEGER;
                         ChooseProc: ProcPtr);
{-------------------------------------------------------------------------}
EXTERNAL;


{$S WMwork }
{-------------------------------------------------------------------------}
PROCEDURE CheckItem(* VAR menu: MenuInfo; item: INTEGER; checked: BOOLEAN *);
{-------------------------------------------------------------------------}
BEGIN
{$IFC fTraceWM}    LogCall;     {$ENDC}
  IF checked THEN MarkItem(menu, item, checkMark)
  ELSE MarkItem(menu, item, noMark);
END;


{$S WMwork }
{-------------------------------------------------------------------------}
PROCEDURE ChooseTxtItem{(VAR menu: MenuInfo;
                        menuRect: Rect;
                        hitPt: Point;
                        VAR whichItem: INTEGER)};
{-------------------------------------------------------------------------}
{ called by MenuSelect to choose an item from a text menu }
VAR oldItem: INTEGER;

{$S WMwork }
  PROCEDURE InvertItem(item: INTEGER);
  VAR tempRect: Rect;
  BEGIN
{$IFC fTraceWM}    LogCall;     {$ENDC}
    IF item <> 0 THEN WITH tempRect DO
      BEGIN
        left := menuRect.left;
        right := menuRect.right;
        top := menuRect.top + vertSpace*(item-1);
        bottom := top + vertSpace;
        InvertRect(tempRect);
      END;
  END;

{$S WMwork }
BEGIN
{$IFC fTraceWM}    LogCall;     {$ENDC}
  oldItem := whichItem;
  whichItem := 0;
  IF PtInRect(hitPt,menuRect)
  THEN whichItem := (hitPt.v-menuRect.top+vertSpace-1) DIV vertSpace;
  IF (NOT menu.enableFlags[whichItem])
  OR (NOT menu.enableFlags[0])  { entire menu disabled }
  THEN whichItem := 0;

  IF whichItem <> oldItem THEN
    BEGIN
      InvertItem(oldItem);    { unhighlight old item }
      InvertItem(whichItem);  { highlight new item }
    END;
END;  { ChooseTxtItem }


{$S WMact }
{-------------------------------------------------------------------------}
PROCEDURE ClearMenuBar;
{-------------------------------------------------------------------------}
BEGIN
{$IFC fTraceWM}    LogCall;     {$ENDC}
  lastMenu := 0;
  lastRight := menuLeft[0]+deskTitle;
  theMenu := -1;
  DrawMenuBar;
END;


{$S WMwork }
{-------------------------------------------------------------------------}
PROCEDURE DeleteMenu(* menuId: INTEGER *);
{-------------------------------------------------------------------------}
VAR i,index,deleteWidth: INTEGER;
    savePort: GrafPtr;
BEGIN
{$IFC fTraceWM}    LogCall;     {$ENDC}
  GetPort(savePort);
  SetPort(menuFolder);
  index := GetIndex(menuId);
  IF index > 0 THEN
    BEGIN
      { delete this menu and adjust locations of following ones }
      IF index=lastMenu THEN deleteWidth := lastRight-menuLeft[index]
      ELSE deleteWidth := menuLeft[index+1]-menuLeft[index];
      lastRight := lastRight-deleteWidth;
      lastMenu := lastMenu-1;
      FOR i := index TO lastMenu DO
        BEGIN
          menuList[i] := menuList[i+1];
          menuLeft[i] := menuLeft[i+1]-deleteWidth;
        END;
    END;
  SetPort(savePort);
END;


{$S WMothrwk }
{-------------------------------------------------------------------------}
PROCEDURE DisableItem(* VAR menu: MenuInfo; item: INTEGER *);
{-------------------------------------------------------------------------}
BEGIN
{$IFC fTraceWM}    LogCall;     {$ENDC}
  menu.enableFlags[item] := FALSE;
END;


{$S WMcold }
{-------------------------------------------------------------------------}
PROCEDURE DisableMenu(* VAR menu: MenuInfo *);
{-------------------------------------------------------------------------}
BEGIN
{$IFC fTraceWM}    LogCall;     {$ENDC}
  menu.enableFlags[0] := FALSE;
END;


{$S WMwork }
{-------------------------------------------------------------------------}
PROCEDURE DrawMenuBar;
{-------------------------------------------------------------------------}
{ erase and re-draw menu bar and menu titles }
{ call while in menuFolder }
LABEL 99;
VAR whichMenu:  INTEGER;
    srcPtr:     ^Str255;
    index:      INTEGER;
    savePort:   GrafPtr;
    titleRect:  Rect;
BEGIN
{$IFC fTraceWM}    LogCall;     {$ENDC}
  IF NOT ImActive THEN EXIT(DrawMenuBar);
  GetPort(savePort);
  SetPort(menuFolder);
  SetRectRgn(thePort^.clipRgn,0,0,720,15);        { clip for half-round rect }
  EraseRoundRect(thePort^.portRect,hDiam,vDiam);  { white menu bar }
  ClipRect(thePort^.portRect);                    { restore normal clip }
  MoveTo(0,15); LineTo(720,15);                   { draw the black line }

  IF lastMenu = 0 THEN GOTO 99;

  FOR whichMenu := 0 TO lastMenu DO
    WITH menuList[whichMenu]^ DO
    BEGIN
      IF menuData <> NIL THEN
        BEGIN
          MoveTo(menuLeft[whichMenu]+10,12);
      (*  IF menuID = 0                    *** IF an icon is avail for the desktop menu title ***
          THEN TextFont(fIconFont);  *)
          srcPtr := Pointer(ORD(menuData^));
          DrawString(srcPtr^);  { draw title }
          TextFont(sysText);
          IF enableFlags[0]
          THEN
            BEGIN
              IF (menuId = theMenu) AND (menuId <> 0) THEN InvertTitle(whichMenu);
            END
          ELSE WITH titleRect DO  { dim erase the disabled menu title }
            BEGIN
              top := 1;
              bottom := 15;
              left := menuLeft[whichMenu];
              IF whichMenu = lastMenu
              THEN right := lastRight
              ELSE right := menuLeft[whichMenu+1];
              PenPat(gray);
              PenMode(patBic);
              PaintRect(titleRect);
              PenNormal;
            END;
        END;
    END;
99:
  SetPort(savePort);
END;


{$S WMwork }
{-------------------------------------------------------------------------}
PROCEDURE DrawTxtMenu{(VAR menu: MenuInfo; menuRect: Rect)};
{-------------------------------------------------------------------------}
{ called from MenuSelect to draw a text menu }

LABEL 1;
VAR srcPtr:             ^Str255;
    menuText:            Str255;
    len,vert,item:       INTEGER;
    appleChar,markChar,styleChar:  CHAR;
    markHoriz,itemHoriz,appleLeft,appleRight: INTEGER;
    tempRect: Rect;

BEGIN
{$IFC fTraceWM}    LogCall;     {$ENDC}
  IF menu.menuData = NIL THEN EXIT(DrawTxtMenu);
  markHoriz := menuRect.left-5;
  itemHoriz := menuRect.left+14;
  appleLeft := menuRect.right-30;
  appleRight := menuRect.right-11;
  vert := menuRect.top-4;
  item := 0;

  srcPtr := Pointer(ORD(menu.menuData^));    { point to title string      }
  len := Length(srcPtr^);                    { get length of title string }
  len := len-3;                   { adjust since title has no special chs }

  REPEAT
1:  item := item+1;
    srcPtr := Pointer(ORD(srcPtr)+len+4);    { point to next item     }
    len := Length(srcPtr^);                  { get its length         }
    IF len = 0 THEN EXIT(DrawTxtMenu);       { empty string marks end }
    vert := vert+vertSpace;

    IF srcPtr^ = '.' THEN GOTO 1;            { blank item }

    IF srcPtr^ = '-' THEN                    { separator line }
      BEGIN
        PenPat(gray);
        MoveTo(menuRect.left,vert-4);
        Line(500,0);
        PenNormal;
        GOTO 1;
      END;

    {$R-}
    appleChar := srcPtr^[len+1];
    markChar := srcPtr^[len+2];
    styleChar := srcPtr^[len+3];
    {$IFC wmDebug}{$R+}{$ENDC}

    IF markChar <> CHR(noMark) THEN    { draw mark character }
      BEGIN
        MoveTo(markHoriz,vert-12);
        TextFont(wmFont);
        DrawChar(markChar);
        TextFont(sysText);
      END;

    MoveLeft(srcPtr^,menuText,len+1);        { copy to a workspace    }
    IF menu.menuID = 0 THEN                  { draw the icons }
      BEGIN
        TextFont(wmFont);
        MoveTo(itemHoriz,vert-13);
        DrawChar(menuText[1]);               { the icon }
        IF menuText[2] <> CHR(noIcon) THEN
          BEGIN
            MoveTo(itemHoriz,vert-13);
            DrawChar(menuText[2]);           { the overlay }
          END;
        DELETE (menuText,1,2);
        TextFont(sysText);
      END;

    IF styleChar='B' THEN TextFace([bold]);
    IF styleChar='I' THEN TextFace([italic]);
    IF styleChar='_' THEN TextFace([underline]);
    IF menu.menuID = 0
    THEN MoveTo(itemHoriz+25,vert)
    ELSE MoveTo(itemHoriz,vert);
    DrawString(menuText);              { draw menu item }
    TextFace([]);

    IF appleChar <> ' ' THEN           { draw apple character }
      BEGIN
        MoveTo(appleLeft,vert-12);
        TextFont(wmFont);
        DrawChar((CHR(appleMark)));
        TextFont(sysText);
        MoveTo(appleRight,vert);
        DrawChar(appleChar);
      END;

    IF (NOT menu.enableFlags[item])
    OR (NOT menu.enableFlags[0])     { whole menu disabled }
    THEN
      BEGIN
        SetRect(tempRect,menuRect.left,vert-12,menuRect.right,vert+3);
        PenPat(gray);
        PenMode(patBic);
        PaintRect(tempRect);
        PenNormal;
      END;

  UNTIL FALSE;  { until empty item string }
END;


{$S WMothrwk }
{-------------------------------------------------------------------------}
PROCEDURE EnableItem(* VAR menu: MenuInfo; item: INTEGER *);
{-------------------------------------------------------------------------}
BEGIN
{$IFC fTraceWM}    LogCall;     {$ENDC}
  menu.enableFlags[item] := TRUE;
END;


{$S WMcold }
{-------------------------------------------------------------------------}
PROCEDURE EnableMenu(* VAR menu: MenuInfo *);
{-------------------------------------------------------------------------}
BEGIN
{$IFC fTraceWM}    LogCall;     {$ENDC}
  menu.enableFlags[0] := TRUE;
END;


{$S WMwork }
{-------------------------------------------------------------------------}
FUNCTION GetIndex{menuId: INTEGER): INTEGER};
{-------------------------------------------------------------------------}
{ look up menuId in menuList and return index into menuList }
{ return -1 if not found }
VAR i: INTEGER;
BEGIN
{$IFC fTraceWM}    LogCall;     {$ENDC}
  FOR i := 1 to lastMenu DO IF menuId=menuList[i]^.menuId THEN
    BEGIN
      GetIndex := i;
      EXIT(GetIndex);
    END;
  GetIndex := -1;
END;


{$S WMothrwk }
{-------------------------------------------------------------------------}
PROCEDURE GetItem(* VAR menu: MenuInfo; item: INTEGER; itemString: ItemPtr *);
{-------------------------------------------------------------------------}
{ inquire an item string from a text menu }
VAR srcPtr: ^Str255;
    i,len:  INTEGER;
BEGIN
{$IFC fTraceWM}    LogCall;     {$ENDC}
  itemString^ := '';
  IF item <= 0 THEN EXIT(GetItem);

  srcPtr := Pointer(ORD(menu.menuData^));         { point to title }
  len := Length(srcPtr^);                    { get length of title }
  len := len-3;         { adjust since title has no special chars }

  FOR i := 1 to item DO
    BEGIN
      srcPtr := Pointer(ORD(srcPtr)+len+4);  { point to next item }
      len := Length(srcPtr^);
      IF len = 0 THEN EXIT(GetItem);
    END;

  MoveLeft(srcPtr^,itemString^,len+1);
END;


{$S WMwork }
{-------------------------------------------------------------------------}
PROCEDURE HiLiteMenu(* menuId: INTEGER *);
{-------------------------------------------------------------------------}
{ make the given menu title be highlighted.  If menuId=0, then highlight none }
VAR oldIndex,newIndex: INTEGER;
    savePort: GrafPtr;
    saveHeap: THz;
BEGIN
{$IFC fTraceWM}    LogCall;     {$ENDC}
  IF NOT ImActive THEN EXIT(HiLiteMenu);
  IF menuId = theMenu THEN EXIT(HiLiteMenu);    { it's already highlighted }

  GetHeap(saveHeap);
  SetHeap(wmHeap);
  GetPort(savePort);
  SetPort(menuFolder);

  oldIndex := GetIndex(theMenu);
  IF oldIndex >= 0 THEN InvertTitle(oldIndex);  { un-highlight the old menu title }
  newIndex := GetIndex(menuId);
  IF newIndex > 0 THEN InvertTitle(newIndex);   { highlight the new one instead }
  theMenu := menuId;                            { remember who's highlighted    }

  SetHeap(saveHeap);
  SetPort(savePort);
END;


{$S WMinit }
{-------------------------------------------------------------------------}
PROCEDURE InitMenus;
{-------------------------------------------------------------------------}
{ one-time initialization for Unit Menus }
{ called only by filer }
VAR i: INTEGER;

BEGIN
{$IFC fTraceWM}    LogCall;     {$ENDC}

  WITH desktopMenu[1] DO
    BEGIN                 { the filer sets the menuData handle }
      menuID := 0;
      DrawProc := @DrawTxtMenu;
      ChooseProc := @ChooseTxtItem;
      FOR i := 0 TO maxItem DO enableFlags[i] := TRUE;
      menuData := NIL;
    END;
  menuList[0] := @desktopMenu[1];           { permanent entry }
  deskTitle := 0;
  menuLeft[0] := 10;
  menuDelay := 0;
  ClearMenuBar;

END;


{$S WMwork }
{-------------------------------------------------------------------------}
PROCEDURE InsertMenu(* VAR menu: MenuInfo; beforeId: INTEGER *);
{-------------------------------------------------------------------------}
{ Insert a menu into the menuBar and update menuLeft coordinates }
{ Insert menu before beforeId. If beforeId not found, append at right }
VAR srcPtr: ^Str255;
    titleWidth,index,newLeft,i: INTEGER;
    savePort: GrafPtr;
BEGIN
{$IFC fTraceWM}    LogCall;     {$ENDC}
  IF lastMenu=maxMenu THEN EXIT(InsertMenu);         { menu bar all full up ! }
  IF GetIndex(menu.menuId) >= 0 THEN EXIT(InsertMenu);  { already in menu bar }
  GetPort(savePort);
  SetPort(menuFolder);

  srcPtr := Pointer(ORD(menu.menuData^));       { point to title string }
  titleWidth := 20+StringWidth(srcPtr^);        { calculate title width }

  index := GetIndex(beforeId);
  IF index < 0 THEN
    BEGIN
      index := lastMenu+1;  { if not found, then append at right }
      newLeft := lastRight;
    END
  ELSE
    BEGIN  { move other menus over to make room for new one }
      newLeft := menuLeft[index];
      FOR i := lastMenu DOWNTO index DO
        BEGIN
          menuList[i+1] := menuList[i];
          menuLeft[i+1] := menuLeft[i] + titleWidth;
        END;
    END;

  lastRight := lastRight+titleWidth;
  menuList[index] := @menu;
  menuLeft[index] := newLeft;
  lastMenu := lastMenu+1;
  SetPort(savePort);
END;


{$S WMwork }
{-------------------------------------------------------------------------}
PROCEDURE InvertTitle{whichMenu: INTEGER};
{-------------------------------------------------------------------------}
{ invert the title of one of the menus }
VAR titleRect: Rect;
BEGIN
{$IFC fTraceWM}    LogCall;     {$ENDC}
  IF whichMenu <= lastMenu THEN WITH titleRect DO
    BEGIN
      left := menuLeft[whichMenu];
      top := 1;
      IF whichMenu = lastMenu
      THEN right := lastRight
      ELSE right := menuLeft[whichMenu+1];
      bottom := 15;
      InvertRect(titleRect);
    END;
END;


{$S WMwork }
{-------------------------------------------------------------------------}
PROCEDURE MarkItem(* VAR menu: MenuInfo; item: INTEGER; whichMark: INTEGER *);
{-------------------------------------------------------------------------}
{ set the mark character for a text menu item }
VAR srcPtr: ^Str255;
    i,len:  INTEGER;
BEGIN
{$IFC fTraceWM}    LogCall;     {$ENDC}
  IF item <= 0 THEN EXIT(MarkItem);
  srcPtr := Pointer(ORD(menu.menuData^));         { point to title }
  len := Length(srcPtr^);                    { get length of title }
  len := len-3;         { adjust since title has no special chs }

  FOR i := 1 to item DO
    BEGIN
      srcPtr := Pointer(ORD(srcPtr)+len+4);  { point to next item }
      len := Length(srcPtr^);
      IF len=0 THEN EXIT(MarkItem);
    END;

  {$R-}
  srcPtr^[len+2] := CHR(whichMark);    { install the mark character }
  {$IFC wmDebug}{$R+}{$ENDC}
END;


{$S WMwork }
{-------------------------------------------------------------------------}
PROCEDURE MenuKey(* ch: CHAR; VAR whichMenu,whichItem: INTEGER *);
{-------------------------------------------------------------------------}
{ Map a command-key into its associated menu and item. }
{ Returns menu 0, item 0 if item disabled or not found in current menu bar. }
VAR srcPtr: ^Str255;
    menu,item,len:  INTEGER;
    appleCh:  CHAR;

BEGIN
{$IFC fTraceWM}    LogCall;     {$ENDC}
  whichMenu := 0;
  whichItem := 0;
  IF ch=' ' THEN EXIT(MenuKey);
  ch := UpShift(ch);

  FOR menu := 1 TO lastMenu DO
    BEGIN
      srcPtr := Pointer(ORD(menuList[menu]^.menuData^));  { point to title }
      len := Length(srcPtr^);                        { get length of title }
      len := len-3;         { adjust since title has no special chs }

      item := 0;
      REPEAT
        srcPtr := Pointer(ORD(srcPtr)+len+4);  { point to next item }
        len := Length(srcPtr^);
        item := item+1;

        {$R-}
        appleCh := srcPtr^[len+1];  { get the apple key character }
        {$IFC wmDebug}{$R+}{$ENDC}
        IF ch = appleCh THEN
          BEGIN
            IF menuList[menu]^.enableFlags[item]
            AND menuList[menu]^.enableFlags[0]
            THEN
              BEGIN
                whichMenu := menuList[menu]^.menuId;
                whichItem := item;
              END;
            EXIT(MenuKey);
          END;

      UNTIL len=0;

    END;   { for menu }
END;



{$S WMwork }
{-------------------------------------------------------------------------}
PROCEDURE MenuSelect(* startPt: Point; VAR whichMenu,whichItem: INTEGER *);
{-------------------------------------------------------------------------}
{ takes care of all menu display and returns a menuId and item. }
{ Stays in control until the button is released.                }
{ If whichMenu=0 or whichItem=0, then no operation,             }
{ else perform task and then call HiLiteMenu(0) to clear title  }

VAR menuIndex:    INTEGER;
    mousePt:      Point;
    menuRect:     Rect;
    nextKey:      KeyEvent;
    oldMenuIndex: INTEGER;
    savedBits:    BitMap;
    savedHandle:  Handle;
    savePort:     GrafPtr;
    saveHeap:     THz;
    tempPt:       Point;
    theEvent:     EventRecord;
    i:            INTEGER;
    delay:        INTEGER;
    windPeek:     WindowPeek;


{-------------------------------------------------------------------------}
{$S WMwork }
    PROCEDURE ChooseMenu(VAR menuIndex: INTEGER; pt: Point);
    VAR i: INTEGER;
    BEGIN
{$IFC fTraceWM}    LogCall;     {$ENDC}
      IF pt.h < lastRight THEN FOR i := lastMenu DOWNTO 0 DO
        IF pt.h > menuLeft[i] THEN
           BEGIN
             menuIndex := i;  { we found it }
             EXIT(ChooseMenu);
           END;
      menuIndex := -1;    { no menu chosen }
    END; { ChooseMenu }


{-------------------------------------------------------------------------}
{$S WMwork }
    PROCEDURE SaveBits(saveRect: Rect);
    { allocates memory in wmHeap, sets up savedBits, and copies bits from screen }
    { balance with a call to RestoreBits.  (not nestable). }
    VAR width,height,bytesNeeded: INTEGER;
    BEGIN
{$IFC fTraceWM}    LogCall;     {$ENDC}
      width := saveRect.right-saveRect.left;
      height := saveRect.bottom - saveRect.top + 3;
      savedBits.rowbytes := ((width+15) DIV 16)*2;
      bytesNeeded := savedBits.rowBytes*height;
      savedHandle := NewHandle(bytesNeeded);
      savedBits.baseAddr := Pointer(ORD(savedHandle^));
      savedBits.bounds := saveRect;
      CopyBits(thePort^.portBits,savedBits,
               savedBits.bounds,savedBits.bounds,srcCopy,Nil);
    END;


{-------------------------------------------------------------------------}
{$S WMwork }
    PROCEDURE RestoreBits;
    { put back the screen bits that were saved by SaveBits }
    BEGIN
{$IFC fTraceWM}    LogCall;     {$ENDC}
      savedBits.baseAddr := Pointer(ORD(savedHandle^));
      CopyBits(savedBits,thePort^.portBits,
               savedBits.bounds,savedBits.bounds,srcCopy,Nil);
      DisposeHandle(savedHandle);
    END;


{-------------------------------------------------------------------------}
{$S WMwork }
    PROCEDURE DrawMenu(menuIndex: INTEGER);
    { save bits from screen and draw pull-down menu. }
    VAR saveRect,tempRect: Rect;
        titleLeft: INTEGER;
    BEGIN
{$IFC fTraceWM}    LogCall;     {$ENDC}
      WITH menuList[menuIndex]^ DO
        BEGIN
          titleLeft := menuLeft[menuIndex];
          { keep pull-down from going off right edge }
          IF titleLeft+menuWidth+9 > 720 THEN titleLeft := 720-menuWidth-9;

          WITH saveRect DO
            BEGIN
              top := 16;
              bottom := top+menuHeight + 7;
              left := titleLeft;
              right := titleLeft+menuWidth+9;
            END;

          SaveBits(saveRect);

          WITH menuRect DO
            BEGIN
              top := 16;
              bottom := top+menuHeight;
              left := titleLeft+2;
              right := left+menuWidth;
              EraseRect(menuRect);            { erase menuRect to white }
              PenSize(2,1);
              MoveTo(left-2,top);             { black outline }
              LineTo(left-2,bottom);
              LineTo(right,bottom);
              LineTo(right,top);
              MoveTo(left+3,bottom+1);        { black shadow outline }
              LineTo(left+3,bottom+3);
              LineTo(right+5,bottom+3);
              LineTo(right+5,top);
              PenPat(gray);                   { gray shadow fill }
              PenSize(3,2);
              MoveTo(right+2,top);
              LineTo(right+2,bottom+1);
              LineTo(left+5,bottom+1);
            END;

          PenNormal;
          ClipRect(menuRect);
          { call user's procedure to draw items }
          CallDrawProc(menulist[menuIndex]^,menuRect,DrawProc);
          ClipRect(thePort^.portRect);  { restore clipping to full screen }

        END; { with menuList[menuIndex] }
    END; { DrawMenu }


{-------------------------------------------------------------------------}
{$S WMwork }
BEGIN  { MenuSelect }
{$IFC fTraceWM}    LogCall;     {$ENDC}
  whichItem := 0;
  whichMenu := -1;
  IF NOT ImActive THEN EXIT(MenuSelect);

  HiLiteMenu(0);  { make sure all menu titles are white }
  IF NOT StillDown THEN EXIT(MenuSelect);
  IF KeybdPeek(FALSE,1,nextKey) THEN EXIT(MenuSelect);   { did user release the mouse btn? }

{$IFC wmMonkey}                                                                                 { MONKEY }
  IF (monkeyFlag) AND (monkeyWorld) THEN {disable the File/Print close menu items}              { MONKEY }
                                          BEGIN                                                 { MONKEY }
                                          DisableMenu(menuList[0]^);                            { MONKEY }
                                          DisableItem(menuList[1]^,1);                          { MONKEY }
                                          DisableItem(menuList[1]^,2);                          { MONKEY }
                                          DisableItem(menuList[1]^,4);                          { MONKEY }
                                          DisableItem(menuList[1]^,11);                         { MONKEY }
                                          END                                                   { MONKEY }
                                     ELSE {brute force enable the desk menu}                    { MONKEY }
                                          EnableMenu(menuList[0]^);                             { MONKEY }
{$ENDC}                                                                                         { MONKEY }


  GetHeap(saveHeap);
  SetHeap(wmHeap);
  GetPort(savePort);
  SetPort(menuFolder);

  ClipRect(thePort^.portRect);  { in case somebody clobbered it }

  menuIndex := 0;
  oldMenuIndex := -1;
  mousePt := startPt;

  REPEAT
    { check for change of which menu }
    IF mousePt.v < 16 THEN
      BEGIN
        ChooseMenu(menuIndex,mousePt);
        IF menuIndex <> oldMenuIndex THEN
          BEGIN
            IF oldMenuIndex >= 0 THEN
              BEGIN
                RestoreBits;
                InvertTitle(oldMenuIndex);  { unhighlight old menu title }
              END;
            oldMenuIndex := menuIndex;

            IF menuIndex < 0 THEN whichMenu := -1
            ELSE
              BEGIN
                whichMenu := menuList[menuIndex]^.menuId;
                InvertTitle(menuIndex);   { highlight new menu title }
                DrawMenu(menuIndex);
              END;
            whichItem := 0;
          END;
      END;

    IF menuIndex >= 0 THEN WITH menuList[menuIndex]^ DO
      BEGIN
        ClipRect(menuRect);
        { call user's procedure to highlight and choose an item }
        CallChooseProc(menuList[menuIndex]^,menuRect,mousePt,whichItem,ChooseProc);
        ClipRect(thePort^.portRect);  { restore clipping to full screen }
      END;

    GetMouse(mousePt);
  UNTIL NOT StillDown;

  IF menuIndex >= 0 THEN
    BEGIN
      FOR Delay := 1 TO menuDelay DO { nothing };
      RestoreBits;
    END;

  IF whichMenu = 0 THEN
    IF whichItem = 0 THEN
      InvertTitle(0)
    ELSE
      BEGIN                { tell filer to activate the object }
        theEvent.why := whyfind;
        theEvent.userdata := whichItem;
        theEvent.fromProcess := My_ID;
        windPeek := POINTER(ORD(filerFolder));
        MakeActive (windPeek, theEvent);
        whichItem := 0;
      END;


  theMenu := whichMenu;  { remember which menu title is highlighted }
  IF whichMenu < 0 THEN whichMenu := 0;

  SetHeap(saveHeap);
  SetPort(savePort);
END;



{$S WMcold }
{-------------------------------------------------------------------------}
PROCEDURE ReadMenu(* menuFile: TEXT; VAR menu: MenuInfo *);
{-------------------------------------------------------------------------}
{ read a menu from file, allocating into process-specific theHeap }

VAR str:                Str255;
    dstPtr:             Ptr;
    i,len,byteCount,item:    INTEGER;
    appleChar, styleChar:  CHAR;
    dstHandle:          Handle;

{$S WMopen1 }
       PROCEDURE Extract(flag: CHAR; VAR code: CHAR);
       BEGIN
{$IFC fTraceWM}    LogCall;     {$ENDC}
         code := ' ';
         IF len > 2 THEN
           IF str[len-1]=flag THEN
             BEGIN
               code := UpShift(str[len]);
               Delete(str,len-1,2);
               len := len-2;
             END;
       END;

{$S WMopen1 }
BEGIN
{$IFC fTraceWM}    LogCall;     {$ENDC}
  dstHandle := NewHandle(maxBytes);
  WITH menu DO
    BEGIN
      READLN(menuFile,menuId);
      DrawProc := @DrawTxtMenu;
      ChooseProc := @ChooseTxtItem;
      FOR i := 0 TO maxItem DO enableFlags[i] := TRUE;
      menuData := dstHandle;
    END;

  dstPtr := dstHandle^;
  READLN(menuFile,str);                   { read the title }
  len := Length(str);                       { get size of title }
  MoveLeft(str,dstPtr^,len+1);            { install the title }
  dstPtr := Pointer(ORD(dstPtr)+len+1);     { bump past title }
  item  :=  0;

  REPEAT
    READLN(menuFile,str);    { read an item }
    item := item + 1;
    IF (str = '.') OR (str = '-') THEN menu.enableFlags[item] := FALSE;

    len:=Length(str);

    Extract('\', styleChar);
    Extract('/', appleChar);

    {$R-}
      { install appleChar, markChar, and styleChar after string }
      str[len+1] := appleChar;
      str[len+2] := CHR(noMark);
      str[len+3] := styleChar;
    {$IFC wmDebug}{$R+}{$ENDC}

    MoveLeft(str,dstPtr^,len+4);         { install item,appleChar,markChar  }
    dstPtr := Pointer(ORD(dstPtr)+len+4);  { bump to next item       }
  UNTIL len=0;

  byteCount := ORD(dstPtr)-ORD(dstHandle^);
  IF byteCount > maxBytes THEN
    BEGIN
{$IFC wmDebug}
      WRITELN('ReadMenu says menu data bigger than',maxBytes,' bytes.');
{$ENDC}
      Halt;
    END;
  SetSize(dstHandle,byteCount);   { trim dst }
  CalcMenuSize(menu);  { measure width and height }
END;


{$S WMothrwk }
{-------------------------------------------------------------------------}
PROCEDURE SetItem(* VAR menu: MenuInfo; item: INTEGER; itemString: ItemPtr *);
{-------------------------------------------------------------------------}
{ change an item in a text menu.  Do so by copying the old items }
{ and the new item into a new dataHandle and re-calculating menuWidth }

VAR startPtr,srcPtr,dstPtr:     ^Str255;
    dstHandle:                  Handle;
    i,len,byteCount:            INTEGER;

BEGIN
{$IFC fTraceWM}    LogCall;     {$ENDC}
  IF (item <= 0) or (itemString^ = '') THEN EXIT(SetItem);

  { scan menuData till pointing at the item, abort if item not found. }
  startPtr := Pointer(ORD(menu.menuData^));           { point to title }
  srcPtr := startPtr;
  len := Length(srcPtr^);                        { get length of title }
  len := len-3;         { adjust since title has no special chs }
  FOR i := 1 to item DO
    BEGIN
      srcPtr := Pointer(ORD(srcPtr)+len+4);  { point to next item }
      len := Length(srcPtr^);
      IF len=0 THEN EXIT(SetItem);
    END;

  dstHandle := NewHandle(GetSize(menu.menuData)+Length(itemString^));
  dstPtr := Pointer(ORD(dstHandle^));
  byteCount := ORD(srcPtr)-ORD(startPtr);
  startPtr := Pointer(ORD(menu.menuData^));        { re-dereference startPtr }
  srcPtr := Pointer(ORD(startPtr) + byteCount);    { re-dereference srcPtr too }
  MoveLeft(startPtr^,dstPtr^,byteCount);     { copy title and all items before }
  dstPtr := Pointer(ORD(dstPtr)+byteCount);

  srcPtr := Pointer(ORD(srcPtr)+len+1);  { bump srcPtr to special chars }
  len := Length(itemString^);
  MoveLeft(itemString^,dstPtr^,len+1);  { copy the new item }
  dstPtr := Pointer(ORD(dstPtr)+len+1);
  MoveLeft(srcPtr^,dstPtr^,3);         { copy old special chars }
  dstPtr := Pointer(ORD(dstPtr)+3);
  srcPtr := Pointer(ORD(srcPtr)+3);      { bump src to next item }

  { scan for end of menuData }
  startPtr := srcPtr;
  REPEAT
    len := Length(srcPtr^);
    srcPtr := Pointer(ORD(srcPtr)+len+4);  { point to next item }
  UNTIL len=0;

  { copy all items after this item }
  byteCount := ORD(srcPtr)-ORD(startPtr);
  MoveLeft(startPtr^,dstPtr^,byteCount);
  dstPtr := Pointer(ORD(dstPtr)+byteCount);

  byteCount := ORD(dstPtr)-ORD(dstHandle^);
  SetSize(dstHandle,byteCount);   { trim dst }
  DisposeHandle(menu.menuData);   { discard the old data }
  menu.menuData := dstHandle;       { replace it with new }
  CalcMenuSize(menu);
END;


END. { unit Menus }


ÿ