
        .INCLUDE libqd/GRAFTYPES.TEXT
;---------------------------------------------------------
;
;
;     *****  *****  *   *  *****
;       *    *      *   *    *
;       *    *       * *     *
;       *    ***      *      *
;       *    *       * *     *
;       *    *      *   *    *
;       *    *****  *   *    *
;
;
;  Routines for measuring and drawing Text.
;


;-------------------------------------------
;
;  KERNED STRIKE FONT FORMAT OFFSETS:
;
FORMAT          .EQU    0               ;WORD
MINCHAR         .EQU    2               ;WORD
MAXCHAR         .EQU    4               ;WORD
MAXWD           .EQU    6               ;WORD
FBBOX           .EQU    8               ;WORD
FBBOY           .EQU    10              ;WORD
FBBDX           .EQU    12              ;WORD
FBBDY           .EQU    14              ;WORD
LENGTH          .EQU    16              ;WORD
ASCENT          .EQU    18              ;WORD
DESCENT         .EQU    20              ;WORD
XOFFSET         .EQU    22              ;WORD
RASTER          .EQU    24              ;WORD



        .PROC StdText,4
        .REF  CheckPic,PutPicByte,PutPicData,PutPicWord,PutPicLong
        .REF  DrText
;--------------------------------------------------------------------------
;
;  PROCEDURE StdText(count: INTEGER; textAddr: Ptr; numer,denom: Point);
;
;  A6 OFFSETS OF PARAMS AND LOCALS AFTER LINK:
;
PARAMSIZE       .EQU    14
COUNT           .EQU    PARAMSIZE+8-2           ;WORD
TEXTADDR        .EQU    COUNT-4                 ;LONG
NUMER           .EQU    TEXTADDR-4              ;POINT
DENOM           .EQU    NUMER-4                 ;POINT

TXLOC           .EQU    -4                      ;POINT
VARSIZE         .EQU    TXLOC                   ;TOTAL LOCALS

        LINK    A6,#VARSIZE                     ;ALLOCATE STACK FRAME
        MOVEM.L D5-D7/A3-A4,-(SP)               ;SAVE REGS
        MOVE    COUNT(A6),D6                    ;GET CHARACTER COUNT
        BLE     GOHOME                          ;QUIT IF COUNT <= 0
        JSR     CHECKPIC                        ;SET UP A4,A3 AND CHECK PICSAVE
        BLE     NOTPIC
        MOVE.L  PICSAVE(A3),A4                  ;GET PICSAVE HANDLE
        MOVE.L  (A4),A4                         ;DE-REFERENCE PICSAVE
;
;  CHECK TXFONT
;
        MOVE    TXFONT(A3),D7                   ;GET THEPORT^.TXFONT
        CMP     PICTXFONT(A4),D7                ;HAS IT CHANGED ?
        BEQ.S   FONTOK                          ;NO, CONTINUE
        MOVE.B  #3,-(SP)                        ;YES, PUSH TXFONT PARAM OPCODE
        JSR     PutPicByte                      ;PUT OPCODE
        MOVE    D7,-(SP)
        JSR     PutPicWord                      ;PUT TXFONT PARAM
        MOVE.L  PICSAVE(A3),A4                  ;GET PICSAVE HANDLE
        MOVE.L  (A4),A4                         ;DE-REFERENCE PICSAVE
        MOVE    D7,PICTXFONT(A4)                ;UPDATE CURRENT STATE
;
;  CHECK TXFACE
;
FONTOK  MOVE.B  TXFACE(A3),D7                   ;GET THEPORT^.TXFACE
        CMP.B   PICTXFACE(A4),D7                ;HAS IT CHANGED ?
        BEQ.S   FACEOK                          ;NO, CONTINUE
        MOVE.B  #4,-(SP)                        ;YES, PUSH TXFACE PARAM OPCODE
        JSR     PutPicByte                      ;PUT OPCODE
        MOVE.B  D7,-(SP)
        JSR     PutPicByte                      ;PUT TXFACE PARAM
        MOVE.L  PICSAVE(A3),A4                  ;GET PICSAVE HANDLE
        MOVE.L  (A4),A4                         ;DE-REFERENCE PICSAVE
        MOVE.B  D7,PICTXFACE(A4)                ;UPDATE CURRENT STATE
;
;  CHECK TXMODE
;
FACEOK  MOVE    TXMODE(A3),D7                   ;GET THEPORT^.TXMODE
        CMP     PICTXMODE(A4),D7                ;HAS IT CHANGED ?
        BEQ.S   MODEOK                          ;NO, CONTINUE
        MOVE.B  #5,-(SP)                        ;YES, PUSH TXMODE PARAM OPCODE
        JSR     PutPicByte                      ;PUT OPCODE
        MOVE    D7,-(SP)
        JSR     PutPicWord                      ;PUT TXMODE PARAM
        MOVE.L  PICSAVE(A3),A4                  ;GET PICSAVE HANDLE
        MOVE.L  (A4),A4                         ;DE-REFERENCE PICSAVE
        MOVE    D7,PICTXMODE(A4)                ;UPDATE CURRENT STATE
;
;  CHECK TXSIZE
;
MODEOK  MOVE    TXSIZE(A3),D7                   ;GET THEPORT^.TXSIZE
        CMP     PICTXSIZE(A4),D7                ;HAS IT CHANGED ?
        BEQ.S   SIZEOK                          ;NO, CONTINUE
        MOVE.B  #$0D,-(SP)                      ;YES, PUSH TXSIZE PARAM OPCODE
        JSR     PutPicByte                      ;PUT OPCODE
        MOVE    D7,-(SP)
        JSR     PutPicWord                      ;PUT TXSIZE PARAM
        MOVE.L  PICSAVE(A3),A4                  ;GET PICSAVE HANDLE
        MOVE.L  (A4),A4                         ;DE-REFERENCE PICSAVE
        MOVE    D7,PICTXSIZE(A4)                ;UPDATE CURRENT STATE
;
;  CHECK SPEXTRA
;
SIZEOK  MOVE.L  SPEXTRA(A3),D7                  ;GET THEPORT^.SPEXTRA
        CMP.L   PICSPEXTRA(A4),D7               ;HAS IT CHANGED ?
        BEQ.S   SPOK                            ;NO, CONTINUE
        MOVE.B  #6,-(SP)                        ;YES, PUSH SPEXTRA PARAM OPCODE
        JSR     PutPicByte                      ;PUT OPCODE
        MOVE.L  D7,-(SP)
        JSR     PutPicLong                      ;PUT SPEXTRA PARAM
        MOVE.L  PICSAVE(A3),A4                  ;GET PICSAVE HANDLE
        MOVE.L  (A4),A4                         ;DE-REFERENCE PICSAVE
        MOVE.L  D7,PICSPEXTRA(A4)               ;UPDATE CURRENT STATE
;
;  CHECK NUMER, DENOM
;
SPOK    MOVE.L  NUMER(A6),D7                    ;GET NUMER
        MOVE.L  DENOM(A6),D5                    ;GET DENOM
        CMP.L   PICTXNUMER(A4),D7               ;HAS IT CHANGED ?
        BNE.S   NOTSAME                         ;YES, RECORD CHANGE
        CMP.L   PICTXDENOM(A4),D5               ;HAS IT CHANGED ?
        BEQ.S   NUMEROK                         ;NO, CONTINUE
NOTSAME MOVE.B  #$10,-(SP)                      ;YES, PUSH TXRATIO OPCODE
        JSR     PutPicByte                      ;PUT OPCODE
        MOVE.L  D7,-(SP)
        JSR     PutPicLong                      ;PUT NUMER
        MOVE.L  D5,-(SP)
        JSR     PutPicLong                      ;PUT DENOM
        MOVE.L  PICSAVE(A3),A4                  ;GET PICSAVE HANDLE
        MOVE.L  (A4),A4                         ;DE-REFERENCE PICSAVE
        MOVE.L  D7,PICTXNUMER(A4)               ;UPDATE CURRENT STATE
        MOVE.L  D5,PICTXDENOM(A4)               ;UPDATE CURRENT STATE
NUMEROK


;-------------------------------------------------------------
;
;  USE DH AND DV TO CHOOSE ONE OF FOUR TEXT OPCODES.
;
        MOVE.L  PNLOC(A3),D5                    ;GET CURRENT PNLOC
        SUB.L   PICTXLOC(A4),D5                 ;CALC DV.DH
        MOVE.L  D5,D0                           ;COPY DV.DH
        AND.L   #$FF00FF00,D0                   ;ARE DH AND DV BOTH 0..255 ?
        BEQ.S   SHORT                           ;YES, USE SHORT FORM
        MOVE.B  #$28,-(SP)
        JSR     PutPicByte                      ;NO, PUT LONGTEXT OPCODE
        MOVE.L  PNLOC(A3),-(SP)
        JSR     PutPicLong                      ;PUT PNLOC 4 BYTES
        BRA.S   TEXT2                           ;AND CONTINUE

SHORT   MOVE.L  D5,D0                           ;YES, COPY DV.DH
        AND.L   #$00FF0000,D0                   ;IS DV = 0 ?
        BNE.S   DV                              ;NO, CONTINUE
        MOVE.B  #$29,-(SP)
        JSR     PutPicByte                      ;YES, PUT DHTEXT OPCODE
        BRA.S   SHARE2                          ;SHARE COMMON CODE

DV      TST.B   D5                              ;IS DH = 0 ?
        BNE.S   DHDV                            ;NO, CONTINUE
        MOVE.B  #$2A,-(SP)
        JSR     PutPicByte                      ;YES, PUT DVTEXT OPCODE
        BRA.S   SHARE1                          ;SHARE COMMON CODE

DHDV    MOVE.B  #$2B,-(SP)
        JSR     PutPicByte                      ;PUT DHDVTEXT OPCODE
        MOVE.B  D5,-(SP)
        JSR     PutPicByte                      ;PUT DH 0..255 TO PIC

SHARE1  SWAP    D5                              ;PUT DV IN LO WORD
SHARE2  MOVE.B  D5,-(SP)
        JSR     PutPicByte                      ;PUT DH OR DV 0..255 TO PIC

TEXT2   MOVE.B  D6,-(SP)
        JSR     PutPicByte                      ;PUT COUNT BYTE TO PIC
        MOVE.L  TEXTADDR(A6),-(SP)              ;PUSH ADDR OF TEXT
        MOVE    D6,-(SP)                        ;PUSH COUNT
        JSR     PutPicData                      ;PUT TEXT DATA
        MOVE.L  PICSAVE(A3),A4                  ;GET PICSAVE HANDLE
        MOVE.L  (A4),A4                         ;DE-REFERENCE PICSAVE
        MOVE.L  PNLOC(A3),PICTXLOC(A4)          ;UPDATE PICTXLOC STATE

;
;  DrText(count,textAddr,numer,denom);
;
NOTPIC  MOVE    D6,-(SP)                        ;PUSH COUNT
        MOVE.L  TEXTADDR(A6),-(SP)
        MOVE.L  NUMER(A6),-(SP)
        MOVE.L  DENOM(A6),-(SP)
        JSR     DrText                          ;DRAW THE TEXT

GOHOME  MOVEM.L (SP)+,D5-D7/A3-A4               ;RESTORE REGS
        UNLINK  PARAMSIZE,'STDTEXT '



        .PROC CallText,2
        .REF  STDTEXT
;---------------------------------------------------------------
;
;  PROCEDURE CallText(count: INTEGER; textAddr: Ptr);
;
        FAKEHEAD
        MOVE.L  (SP)+,A0                        ;POP RETURN ADDR
        MOVE.L  #$00010001,-(SP)                ;PUSH NUMER = (1,1)
        MOVE.L  (SP),-(SP)                      ;PUSH DENOM = (1,1)
        MOVE.L  A0,-(SP)                        ;RESTORE RETURN ADDR
        MOVE.L  GRAFGLOBALS(A5),A0              ;POINT TO QuickDraw GLOBALS
        MOVE.L  THEPORT(A0),A0                  ;GET CURRENT GRAFPORT
        MOVE.L  GRAFPROCS(A0),D0                ;IS GRAFPROCS NIL ?
        LEA     STDTEXT,A0
        BEQ.S   USESTD                          ;YES, USE STD PROC
        MOVE.L  D0,A0
        MOVE.L  TEXTPROC(A0),A0                 ;NO, GET PROC PTR
USESTD  JMP     (A0)                            ;GO TO IT
        FAKETAIL 'CALLTEXT'



        .PROC TextFont,1
        .REF  PortWord
;-------------------------------------------------------
;
;  PROCEDURE TextFont(font: INTEGER);
;
        FAKEHEAD
        MOVEQ   #TXFONT,D0                      ;PUT PORT OFFSET IN D0
        JMP     PORTWORD                        ;INSTALL PARAM INTO THEPORT
        FAKETAIL 'TEXTFONT'


        .PROC TextFace,1
;-------------------------------------------------------
;
;  PROCEDURE TextFace(face: Style);
;
        FAKEHEAD
        MOVE.L  GRAFGLOBALS(A5),A0              ;POINT TO QuickDraw GLOBALS
        MOVE.L  THEPORT(A0),A0                  ;POINT TO THEPORT
        MOVE.B  5(SP),TXFACE(A0)                ;INSTALL TXFACE
        MOVE.L  (SP)+,A0                        ;POP RETURN ADDR
        ADD     #2,SP                           ;STRIP PARAM
        JMP     (A0)                            ;AND RETURN
        FAKETAIL 'TEXTFACE'


        .PROC TextMode,1
        .REF  PortWord
;-------------------------------------------------------
;
;  PROCEDURE TextMode(mode: INTEGER);
;
        FAKEHEAD
        MOVEQ   #TXMODE,D0                      ;PUT PORT OFFSET IN D0
        JMP     PORTWORD                        ;INSTALL PARAM INTO THEPORT
        FAKETAIL 'TEXTMODE'


        .PROC TextSize,1
        .REF  PortWord
;-------------------------------------------------------
;
;  PROCEDURE TextSize(mode: INTEGER);
;
        FAKEHEAD
        MOVEQ   #TXSIZE,D0                      ;PUT PORT OFFSET IN D0
        JMP     PORTWORD                        ;INSTALL PARAM INTO THEPORT
        FAKETAIL 'TEXTSIZE'


        .PROC SpaceExtra,1
;-------------------------------------------------------
;
;  PROCEDURE SpaceExtra(extra: LongInt);
;
        FAKEHEAD
        MOVE.L  GRAFGLOBALS(A5),A0              ;POINT TO QuickDraw GLOBALS
        MOVE.L  THEPORT(A0),A0                  ;POINT TO THEPORT
        MOVE.L  4(SP),SPEXTRA(A0)               ;INSTALL FIXED POINT SPEXTRA
        MOVE.L  (SP)+,(SP)                      ;STRIP PARAM
        RTS                                     ;AND QUIT
        FAKETAIL 'SPACEEXT'


        .PROC DrawChar,1
        .REF  CallText
;----------------------------------------------------
;
;  PROCEDURE DrawChar(ch: CHAR);
;
        FAKEHEAD
        MOVE    #1,-(SP)                        ;PUSH COUNT=1
        PEA     7(SP)                           ;PUSH TEXTADDR
        JSR     CallText                        ;CALL TEXT ROUTINE
        MOVE.L  (SP)+,A0                        ;POP RETURN ADDR
        ADD     #2,SP                           ;STRIP CHAR PARAM
        JMP     (A0)                            ;AND RETURN
        FAKETAIL 'DRAWCHAR'


        .PROC DrawString,1
        .REF  CallText
;----------------------------------------------------
;
;  PROCEDURE DrawString(s: Str255);
;
        FAKEHEAD
        MOVE.L  4(SP),A0                        ;POINT TO STRING
        CLR     D0                              ;GET READY FOR BYTE
        MOVE.B  (A0)+,D0                        ;GET STRING LENGTH
        MOVE    D0,-(SP)                        ;PUSH COUNT
        MOVE.L  A0,-(SP)                        ;PUSH TEXTADDR
        JSR     CallText                        ;CALL TEXT ROUTINE
        MOVE.L  (SP)+,(SP)                      ;POP STRING PARAM
        RTS                                     ;AND RETURN
        FAKETAIL 'DRAWSTRI'


        .PROC DrawText,3
        .REF  CallText
;----------------------------------------------------
;
;  PROCEDURE DrawText(textBuf: WordPtr; start,count: INTEGER);
;
        FAKEHEAD
        MOVE.L  8(SP),A0                        ;POINT TO TEXTBUF
        ADD     6(SP),A0                        ;ADD STARTING OFFSET
        MOVE    4(SP),D0                        ;GET CHARACTER COUNT
        MOVE    D0,-(SP)                        ;PUSH COUNT
        MOVE.L  A0,-(SP)                        ;PUSH TEXTADDR
        JSR     CallText                        ;CALL TEXT ROUTINE
        MOVE.L  (SP)+,A0                        ;POP RETURN ADDR
        ADD     #8,SP                           ;STRIP PARAMS
        JMP     (A0)                            ;AND RETURN
        FAKETAIL 'DRAWTEXT'


        .FUNC   CharWidth,1
        .REF    TextWidth
;---------------------------------------------
;
;   FUNCTION CharWidth(ch: CHAR): INTEGER;
;
        FAKEHEAD
        CLR     -(SP)                           ;ROOM FOR FCN RESULT
        MOVE.L  SP,-(SP)                        ;PUSH TEXTBUF
        MOVE    #7,-(SP)                        ;PUSH OFFSET TO CHAR
        MOVE    #1,-(SP)                        ;PUSH COUNT = 1
        JSR     TEXTWIDTH
        MOVE    (SP)+,6(SP)                     ;MOVE UP RESULT
        MOVE.L  (SP)+,A0                        ;POP RETURN ADDR
        ADD     #2,SP                           ;STRIP CHAR PARAM
        JMP     (A0)                            ;AND RETURN
        FAKETAIL 'CHARWIDT'



        .FUNC   StringWidth,1
        .REF    TextWidth
;---------------------------------------------
;
;   FUNCTION StringWidth(s: Str255): INTEGER;
;
        FAKEHEAD
        MOVE.L  (SP)+,A1                        ;POP RETURN ADDR
        MOVE.L  (SP)+,A0                        ;POP ADDR OF STRING
        CLR     D0
        MOVE.B  (A0)+,D0                        ;GET UNSIGNED BYTE
        MOVE.L  A0,-(SP)                        ;PUSH TEXTADDR
        CLR     -(SP)                           ;FIRSTBYTE := 0
        MOVE    D0,-(SP)                        ;PUSH BYTECOUNT
        MOVE.L  A1,-(SP)                        ;PUT BACK RETURN ADDR
        JMP     TEXTWIDTH
        FAKETAIL 'STRINGWI'



        .FUNC   TextWidth,3
        .REF    StdTxMeas
;------------------------------------------
;
;  FUNCTION TEXTWIDTH(TEXTBUF: WordPtr; firstbyte,byteCount: INTEGER): INTEGER;
;
PARAMSIZE       .EQU    8
RESULT          .EQU    PARAMSIZE+8
TEXTBUF         .EQU    RESULT-4                ;LONG
FIRSTBYTE       .EQU    TEXTBUF-2               ;WORD
BYTECOUNT       .EQU    FIRSTBYTE-2             ;WORD

INFO            .EQU    -8                      ;4 WORDS
NUMER           .EQU    INFO-4                  ;POINT
DENOM           .EQU    NUMER-4                 ;POINT
VARSIZE         .EQU    DENOM                   ;TOTAL BYTES OF LOCALS


        LINK    A6,#VARSIZE                     ;ALLOCATE STACK FRAME
        CLR     RESULT(A6)                      ;INIT RESULT TO 0
        CLR     -(SP)                           ;MAKE ROOM FOR FCN CALL BELOW
        MOVE    BYTECOUNT(A6),-(SP)             ;PUSH BYTE COUNT
        BLE.S   GOHOME                          ;QUIT IF COUNT <= 0
                                                ;UNLK TAKES CARE OF SP
        MOVE.L  TEXTBUF(A6),A0                  ;GET ADDR OF BUFFER
        ADD     FIRSTBYTE(A6),A0                ;ADD STARTING INDEX
        .IF     FORMAC=1
        .ELSE
        MOVE.L  A0,D0
        OR.L    #$FF000000,D0                   ;KLUDGE TO SET NEEDBITS IN STDTXMEAS
        MOVE.L  D0,A0
        .ENDC
        MOVE.L  A0,-(SP)                        ;PUSH TEXTADDR
        MOVE.L  #$00010001,NUMER(A6)            ;NUMER := (1,1)
        MOVE.L  #$00010001,DENOM(A6)            ;DENOM := (1,1)
        PEA     NUMER(A6)                       ;PUSH VAR NUMER
        PEA     DENOM(A6)                       ;PUSH VAR DENOM
        PEA     INFO(A6)                        ;PUSH VAR INFO
        MOVE.L  GRAFGLOBALS(A5),A0              ;POINT TO QUICKDRAW GLOBALS
        MOVE.L  THEPORT(A0),A0                  ;GET CURRENT GRAFPORT
        MOVE.L  GRAFPROCS(A0),D0                ;IS GRAFPROCS NIL ?
        BNE.S   NOTSTD
        JSR     STDTXMEAS
        BRA.S   STD
NOTSTD  MOVE.L  D0,A0
        MOVE.L  TXMEASPROC(A0),A0               ;GET TXMEAS CAPTURE PROC
        JSR     (A0)                            ;CALL IT
STD     MOVE    (SP)+,D1                        ;POP UNSCALED WIDTH
        MULU    NUMER+H(A6),D1                  ;MULT TIMES NUMER
        DIVU    DENOM+H(A6),D1                  ;DIV BY DENOM
        MOVE    D1,RESULT(A6)                   ;RETURN SCALED WIDTH
GOHOME  UNLINK  PARAMSIZE,'TEXTWIDT'



        .FUNC   StdTxMeas,5
        .REF    QDSwapFont,FixMul,FixRatio
;------------------------------------------
;
;  FUNCTION  StdTxMeas(count: INTEGER; textAddr: Ptr;
;                      VAR numer,denom: Point;
;                      VAR info: FontInfo): INTEGER;
;
;  MEASURE SOME TEXT, RETURNING UNSCALED VALUES PLUS UPDATED SCALE FACTOR.
;  LEAVES POINTER TO SWAPFONT OUTPUT RECORD IN GLOBAL 'FONTPTR' FOR DRAWTEXT.
;
;  ALSO SETS UP FONTADJ TO ACCOUNT FOR FONT SUBSTITUTION
;
PARAMSIZE       .EQU    18
RESULT          .EQU    PARAMSIZE+8             ;FCN RESULT IS A WORD
COUNT           .EQU    RESULT-2                ;WORD
TEXTADDR        .EQU    COUNT-4                 ;LONG
NUMER           .EQU    TEXTADDR-4              ;LONG, VAR ADDR
DENOM           .EQU    NUMER-4                 ;LONG, VAR ADDR
INFO            .EQU    DENOM-4                 ;LONG, ADDR OF FONTINFO

INREC           .EQU    -16                     ;FMInput record
EXTRAWD         .EQU    INREC-4                 ;Fixed point
VARSIZE         .EQU    EXTRAWD

        LINK    A6,#VARSIZE                     ;ALLOCATE LOCALS
        MOVEM.L D3-D7/A2-A4,-(SP)               ;SAVE REGS
        CLR     RESULT(A6)                      ;INIT RESULT TO ZERO
        MOVE.L  GRAFGLOBALS(A5),A4              ;POINT TO QUICKDRAW GLOBALS
        MOVE.L  THEPORT(A4),A3                  ;GET CURRENT GRAFPORT
        MOVE.L  SPEXTRA(A3),FONTADJ(A4)         ;INIT FONTADJUST TO SPEXTRA
        LEA     INREC(A6),A1                    ;POINT TO FMINPUT RECORD
        MOVE    TXFONT(A3),(A1)+                ;GET TXFONT FROM THEPORT
        MOVE    TXSIZE(A3),(A1)+                ;GET TXSIZE FROM THEPORT
        MOVE.B  TXFACE(A3),(A1)+                ;GET TXFACE FROM THEPORT
        .IF     FORMAC=1
        TST     PNVIS(A3)                       ;IS PNVIS >= 0 ?
        SPL     (A1)+                           ;IF SO, NEEDBITS := TRUE
        .ELSE
        EOR.B   #$FF,TEXTADDR(A6)               ;FROM GETFONTINFO OR TEXTWIDTH?
        SNE     (A1)+                           ;IF NOT, NEEDBITS := TRUE
        .ENDC
        MOVE    DEVICE(A3),(A1)+                ;GET DEVICE FROM THEPORT
        MOVE.L  NUMER(A6),A0
        MOVE.L  (A0),(A1)+                      ;INSTALL INPUT NUMER
        MOVE.L  DENOM(A6),A0
        MOVE.L  (A0),(A1)+                      ;INSTALL INPUT DENOM
        CLR.L   -(SP)                           ;ROOM FOR FCN RESULT
        PEA     INREC(A6)                       ;PUSH INPUT RECORD
        JSR     QDSWAPFONT                      ;CALL SWAPFONT
        MOVE.L  (SP),FONTPTR(A4)                ;SAVE ANSWER FOR LATER
        MOVE.L  (SP)+,A4                        ;POINT TO OUTPUT RECORD

;
;  CHECK FOR FONTHANDLE NIL (CANT FIND FONT) IF SO, RETURN ALL ZEROS,
;  ELSE COPY VALUES FROM OUTREC AND UPDATE NUMER AND DENOM.
;
        MOVE.L  INFO(A6),A0                     ;POINT TO INFO RECORD
        CLR.L   (A0)                            ;INIT TO (0,0,0,0)
        CLR.L   4(A0)                           ;ALL 4 WORDS
        TST.L   2(A4)                           ;IS FONTHANDLE NIL ?
        BEQ     GOHOME                          ;YES, RETURN (0,0,0,0), WD=0
        MOVE.B  13(A4),1(A0)                    ;FILL IN UNSIGNED ASCENT
        MOVE.B  14(A4),3(A0)                    ;FILL IN UNSIGNED DESCENT
        MOVE.B  15(A4),5(A0)                    ;FILL IN UNSIGNED WIDMAX
        MOVE.B  16(A4),D0                       ;GET SIGNED LEADING
        EXT.W   D0                              ;SIGN EXTEND TO WORD
        MOVE.W  D0,6(A0)                        ;FILL IN LEADING

;
;  UPDATE NUMER AND DENOM
;
        MOVE.L  NUMER(A6),A0                    ;GET VAR ADDR
        MOVE.L  18(A4),(A0)                     ;UPDATE NUMER
        MOVE.L  DENOM(A6),A0                    ;GET VAR ADDR
        MOVE.L  22(A4),(A0)                     ;UPDATE DENOM

;
; IF SPEXTRA IS NON-ZERO, THEN ADJUST IT IN CASE OF FONT SUBSTITUTION
; SET FONTADJ TO  SPEXTRA*(INNUMER/INDENOM)*(OUTDENOM/OUTNUMER)
;
        MOVE.L  SPEXTRA(A3),D5                  ;GET FIXED POINT SPEXTRA
        BEQ.S   NOEXTRA                         ;SKIP IF SPEXTRA IS ZERO
        CLR.L   -(SP)                           ;ROOM FOR FIXMUL RESULT 2 BELOW
        CLR.L   -(SP)                           ;ROOM FOR FIXMUL RESULT 1 BELOW

        CLR.L   -(SP)                           ;ROOM FOR FIXRATIO RESULT
        MOVE.W  INREC+8+H(A6),-(SP)             ;PUSH INPUT NUMER.H
        MOVE.W  INREC+12+H(A6),-(SP)            ;PUSH INPUT DENOM.H
        JSR     FIXRATIO                        ;LEAVE RESULT ON STACK

        CLR.L   -(SP)                           ;ROOM FOR FIXRATIO RESULT
        MOVE.W  22+H(A4),-(SP)                  ;PUSH OUTPUT DENOM.H
        MOVE.W  18+H(A4),-(SP)                  ;PUSH OUTPUT NUMER.H
        JSR     FIXRATIO                        ;LEAVE RESULT ON STACK

        JSR     FIXMUL                          ;MUL TO CALC FACTOR
        MOVE.L  D5,-(SP)                        ;PUSH SPEXTRA
        JSR     FIXMUL                          ;CALC SPEXTRA * FACTOR
        MOVE.L  (SP)+,D5                        ;POP ADJUSTED SPEXTRA

        MOVE.L  GRAFGLOBALS(A5),A0              ;POINT TO QUICKDRAW GLOBALS
        MOVE.L  D5,FONTADJ(A0)                  ;SAVE ADJUSTED SPEXTRA IN FONTADJ

;
;  NOW ADD UP WIDTH OF CHARACTERS, IF ANY
;
NOEXTRA MOVE    COUNT(A6),D2                    ;GET COUNT OF CHARACTERS
        BLE.S   GOHOME                          ;QUIT IF <= 0

;---------------------------------------------------------------
;
;  DE-REFERENCE FONTHANDLE AND CHECK FOR FIXED WIDTH FONT
;
        MOVE.L  2(A4),A2                        ;GET FONT HANDLE
        MOVE.L  (A2),A2                         ;DE-REFERENCE IT
        MOVE.B  12(A4),D1                       ;GET SIGNED EXTRA
        EXT.W   D1                              ;SIGN EXTEND TO WORD
        TST.L   D5                              ;IS SPEXTRA ZERO ?
        BNE.S   NOTFIX                          ;NO, TREAT AS NOT FIXED WIDTH
        MOVE    FORMAT(A2),D0                   ;GET FORMAT WORD
        AND     #$2000,D0                       ;TEST FIXED WIDTH BIT
        BEQ.S   NOTFIX                          ;CONTINUE IF NOT FIXED
        ADD     MAXWD(A2),D1                    ;WIDTH:=MAXWD+EXTRA
        MULS    D2,D1                           ;MULT BY COUNT
        BRA     DONE                            ;UPDATE RESULT AND QUIT

;---------------------------------------------------
;
;  INIT TOTAL WIDTH TO COUNT TIMES EXTRA
;  POINT TO WIDTH TABLE IN FONT, GET MINCHAR & MAXCHAR
;
NOTFIX  CLR.L   EXTRAWD(A6)                     ;INIT SPEXTRA WIDTH TO 0.0
        MOVE.L  TEXTADDR(A6),A0                 ;POINT TO THE CHARACTERS
        MULS    D2,D1                           ;INIT WIDTH TO COUNT * EXTRA

        MOVE.L  A2,A1                           ;COPY FONTPTR
        ADD     LENGTH(A2),A1                   ;ADD 2*LENGTH BYTES
        ADD     LENGTH(A2),A1                   ;A1 POINTS TO WIDTH TABLE

        MOVE    MINCHAR(A2),D3                  ;D3 := MINCHAR
        MOVE    MAXCHAR(A2),D4                  ;D4 := MAXCHAR (SET UP FOR SPEED)
;
;  GET EACH CHARACTER, AND ADD UP THEIR WIDTHS.
;  CHECK FOR MISSING CHARACTERS AND SPACES.
;
        CLR     D0                              ;GET READY FOR BYTE
        BRA.S   MORE                            ;START LOOP AT BOTTOM
NEXTCH  MOVE.B  (A0)+,D0                        ;GET NEXT CHARACTER
        CMP.B   #32,D0                          ;IS IT A SPACE ?
        BNE.S   NOTSP                           ;NO, CONTINUE
        ADD.L   D5,EXTRAWD(A6)                  ;YES, ADD SPEXTRA TO EXTRAWD
NOTSP   CMP     D4,D0                           ;IS ASCII > MAXCHAR ?
        BLE.S   OKCHAR                          ;NO, CONTINUE
MISSING MOVE    D4,D0                           ;YES, USE MISSING WIDTH
        ADD     #1,D0                           ;ONE PAST MAXCHAR
OKCHAR  SUB     D3,D0                           ;CALC ASCII - MINCHAR
        BLT.S   MISSING                         ;MISSING IF ASCII < MINCHAR
        ADD     D0,D0                           ;CH:=(ASCII-MINCHAR)*2
        MOVE    16(A1,D0),D0                    ;GET OFFSET AND WIDTH BYTES
        CMP     #-1,D0                          ;-1 MEANS CHAR NOT IN FONT
        BEQ     MISSING                         ;USE MISSING SYMBOL
        AND     #$FF,D0                         ;LO BYTE = LOGICAL WIDTH
        ADD     D0,D1                           ;ADD TO RUNNING TOTAL WIDTH
MORE    DBRA    D2,NEXTCH                       ;DEC CHAR COUNT AND LOOP
        ADD.W   EXTRAWD(A6),D1                  ;ADD EXTRA WIDTH FROM SPEXTRA

DONE    MOVE    D1,RESULT(A6)                   ;UPDATE FUNCTION RESULT
GOHOME  MOVEM.L (SP)+,D3-D7/A2-A4               ;RESTORE REGS
        UNLINK  PARAMSIZE,'STDTXMEA'



        .FUNC QDSwapFont,1
        .REF  FMSwapFont
;--------------------------------------------------------------------
;
;  FUNCTION QDSwapFont(inRec: FMInput): FMOutPtr;
;
;  CALLS FUNCTION FMSwapFont(inRec: FMInput): FMOutPtr;
;
;  QDSwapFont is the only contact between QuickDraw and the Font Manager.
;  It swaps in the requested font and returns a pointer to an output record
;  telling how to use the font.  QDSwapFont is called from quickdraw
;  in response to DrawChar, DrawString, DrawText, CharWidth, StringWidth,
;  TextWidth, and GetFontInfo.
;
;  IF fontHandle returns as Nil (can't find the font), then:
;    1. The output record will be undefined except for errNum and fontHandle.
;    2. DrawString will neither draw the text nor bump the pen.
;    3. StringWidth will return 0.
;    4. GetFontInfo will return 0,0,0,0.
;
;
;     FMInput  = PACKED RECORD
;                  family:     INTEGER;     { i.e. Century               }
;                  size:       INTEGER;     { i.e. 12 point              }
;                  face:       Style;       { i.e. [bold,underlined]     }
;                  needBits:   BOOLEAN;     { do we need the bitmaps ?   }
;                  device:     INTEGER;     { i.e. 0 for screen          }
;                  numer:      Point;       { current drawing scale      }
;                  denom:      Point;       { current drawing scale      }
;                END;
;
;
;     FMOutPtr = ^FMOutPut;
;     FMOutput = PACKED RECORD
;                  errNum:     INTEGER;     { not used                   }
;                  fontHandle: Handle;      { handle to font             }
;                  bold:       Byte;        { how much to smear horiz    }
;                  italic:     Byte;        { how much to shear          }
;                  ulOffset:   Byte;        { pixels below baseline      }
;                  ulShadow:   Byte;        { how big is the halo        }
;                  ulThick:    Byte;        { how thick is the underline }
;                  shadow:     Byte;        { 0,1,2,or 3 only            }
;                  extra:      SignedByte;  { extra white dots each char }
;                  ascent:     Byte;        { ascent measure for font    }
;                  descent:    Byte;        { descent measure for font   }
;                  widMax:     Byte;        { width of widest char       }
;                  leading:    SignedByte;  { leading between lines      }
;                  unused:     Byte;
;                  numer:      Point;       { use this modified scale to }
;                  denom:      Point;       { draw or measure text with  }
;                END;
;
        .IF FORMAC=1
        JMP     FMSwapFont

        .ELSE
        .REF FMFontMetrics
;-----------------------------------------------
;
;  LISA has not yet updated to the new font interface,
;  so QDSwapFont bridges the gap using the old calls.
;
;  FORMAT OF AN OLD LISA LONGFONTID:
;
;  32 BITS TOTAL:   31..16  = FAMILY/SIZE/MISC
;                   15..8   = FACE
;                   7..0    = DEVICE
;
;
;  FORMAT OF AN OLD LISA CHARSTYLE:
;
;  FONTHANDLE   .EQU    0               ;HANDLE TO KERNED-STRIKE
;  WIDTHS       .EQU    4               ;HANDLE TO WIDTHS
;  BOLD         .EQU    8               ;BYTE
;  ITALIC       .EQU    9               ;BYTE
;  UNDERLINE    .EQU    10              ;BYTE
;  SHADOW       .EQU    11              ;BYTE
;  EXTRA        .EQU    12              ;INTEGER
;
;
PARAMSIZE       .EQU    4
RESULT          .EQU    PARAMSIZE+8             ;FUNCTION RESULT
INREC           .EQU    RESULT-4                ;LONG, ADDR OF INPUT RECORD

ERRNUM          .EQU    -2                      ;WORD
LONGFID         .EQU    ERRNUM-4                ;LONG
INFO            .EQU    LONGFID-8               ;FONTINFO RECORD, 4 WORDS
VARSIZE         .EQU    INFO

        LINK  A6,#VARSIZE                       ;ALLOCATE STACK FRAME
        MOVEM.L D3-D7/A2-A4,-(SP)               ;SAVE REGISTERS
        MOVE.L  GRAFGLOBALS(A5),A0              ;POINT TO QUICKDRAW GLOBALS
        LEA     FONTDATA(A0),A4                 ;POINT TO FMOUTREC
        MOVE.L  A4,RESULT(A6)                   ;RESULT ALWAYS POINTS TO FONTDATA
;
;  BUILD COMPOSITE LONGFONTID FOR LISA FONT MANAGER
;
        MOVE.L  INREC(A6),A0                    ;POINT TO INPUT RECORD
        MOVE.L  (A0)+,D1                        ;GET FAMILY IN HI WORD
                                                ;(IGNORE SIZE IN LO WORD)
        MOVE.W  (A0)+,D1                        ;GET 16 BIT FACE
        TST.B   D1                              ;TEST IF NEEDBITS IS SET
        BNE     DONEED                          ;BGE(NOT GETFONTINFO OR TEXTWIDTH)
        OR.W    #$8000,D1                       ;STYLE.ONLYMETRICSWIDTHS <- TRUE
DONEED  MOVE.W  (A0)+,D0                        ;GET 16 BIT DEVICE
        MOVE.B  D0,D1                           ;ONLY USE BOTTOM BYTE OF DEVICE
        MOVE.L  D1,LONGFID(A6)                  ;SAVE COMBINED LONG FONT ID
        MOVE.L  (A0)+,18(A4)                    ;COPY NUMER, NO CHANGE
        MOVE.L  (A0)+,22(A4)                    ;COPY DENOM, NO CHANGE
;
;  CHSTYLEPTR := FMSWAPFONT(^LONGFONTID);
;
        CLR.L   -(SP)                           ;MAKE ROOM FOR FUNCTION RESULT
        PEA     LONGFID(A6)                     ;PUSH ADDR OF VAR LONGFONTID
        MOVE.L  A4,-(SP)                        ;PUSH ADDR OF ERRNUM IN OUTREC
        JSR     FMSWAPFONT                      ;SWAP IN THE FONT
        MOVE.L  (SP)+,A3                        ;GET PTR TO CHARSTYLE

        TST     (A4)+                           ;SKIP OVER ERRNUM
;
;  IF POINTER TO CHARSTYLE IS NIL, RETURN FONTHANDLE NIL AND QUIT
;
        MOVE.L  A3,(A4)                         ;IS PTR TO CHSTYLE NIL ?
        MOVE.L  (A3),A2                         ;SAVE FONTHANDLE IN A2
        BEQ.S   GOHOME                          ;YES, RETURN FONTHANDLE NIL
        MOVE.L  (A3)+,(A4)+                     ;NO, COPY FONTHANDLE
        ADD     #4,A3                           ;IGNORE WIDTHS HANDLE
        MOVE    (A3)+,(A4)+                     ;COPY HOWBOLD AND HOWITALIC
        MOVE    #$0101,(A4)+                    ;ULOFFSET := 1, ULSHADOW := 1
        MOVE    (A3)+,(A4)+                     ;COPY ULTHICK AND HOWSHADOW
        MOVE    (A3)+,D0                        ;GET 16 BIT SIGNED EXTRA
        MOVE.B  D0,(A4)+                        ;PUT LO SIGNED BYTE OF EXTRA
        MOVE.L  (A2),A2                         ;DEREFERENCE FONTHANDLE
        MOVE.B  ASCENT+1(A2),(A4)+              ;COPY LO BYTE OF ASCENT
        MOVE.B  DESCENT+1(A2),(A4)+             ;COPY LO BYTE OF DESCENT
        MOVE.B  MAXWD+1(A2),(A4)+               ;COPY LO BYTE OF WIDMAX
        MOVE.B  XOFFSET+1(A2),(A4)+             ;COPY LO BYTE OF LEADING

GOHOME  MOVEM.L (SP)+,D3-D7/A2-A4               ;RESTORE REGISTERS
        UNLINK  PARAMSIZE,'QDSWAPFO'
        .ENDC                                   ;END OF LISA CONDITIONAL ASSY



        .PROC GetFontInfo,1
        .REF  StdTxMeas
;------------------------------------------
;
;  PROCEDURE GetFontInfo(VAR info: FontInfo);
;
;  Calls StdTxMeas thru capture proc, then adjusts and scales the result.
;
PARAMSIZE       .EQU    4
INFO            .EQU    PARAMSIZE+8-4           ;LONG, ADDR OF INFO

NUMER           .EQU    -4                      ;POINT
DENOM           .EQU    NUMER-4                 ;POINT
VARSIZE         .EQU    DENOM                   ;TOTAL LOCALS

        LINK    A6,#VARSIZE                     ;ALLOCATE LOCALS
        MOVE.L  #$00010001,NUMER(A6)            ;NUMER := (1,1)
        MOVE.L  #$00010001,DENOM(A6)            ;DENOM := (1,1)
        CLR.L   -(SP)                           ;ROOM FOR FCN, COUNT = 0
        .IF     FORMAC=1
        CLR.L   -(SP)                           ;TEXTADDR := NIL
        .ELSE
        MOVE.L  #$FF000000,-(SP)                ;KLUDGE TO SET NEEDBITS IN STDTXMEAS
        .ENDC
        PEA     NUMER(A6)                       ;PUSH VAR NUMER
        PEA     DENOM(A6)                       ;PUSH VAR DENOM
        MOVE.L  INFO(A6),-(SP)                  ;PUSH VAR INFO
        MOVE.L  GRAFGLOBALS(A5),A0              ;POINT TO QUICKDRAW GLOBALS
        MOVE.L  THEPORT(A0),A0                  ;GET CURRENT GRAFPORT
        MOVE.L  GRAFPROCS(A0),D0                ;IS GRAFPROCS NIL ?
        LEA     STDTXMEAS,A0
        BEQ.S   USESTD                          ;YES, USE STD PROC
        MOVE.L  D0,A0
        MOVE.L  TXMEASPROC(A0),A0               ;NO, GET TXMEAS CAPTURE PROC
USESTD  JSR     (A0)                            ;CALL IT
        TST     (SP)+                           ;DISCARD WIDTH FCN RSLT

;
;  ADJUST WIDMAX FOR EXTRA
;
        MOVE.L  GRAFGLOBALS(A5),A0              ;POINT TO QUICKDRAW GLOBALS
        MOVE.L  FONTPTR(A0),A0                  ;GET FMOUTPUT RECORD
        MOVE.L  INFO(A6),A1                     ;POINT TO RESULT INFO
        MOVE.B  12(A0),D0                       ;GET SIGNED EXTRA
        EXT.W   D0                              ;EXTEND TO WORD
        ADD     D0,4(A1)                        ;ADD TO WIDMAX
;
;  ADJUST ASCENT & DESCENT FOR SHADOW
;
        CLR     D0                              ;GET READY FOR BYTE
        MOVE.B  11(A0),D0                       ;GET SHADOW COUNT
        BEQ.S   NOTSHAD                         ;SKIP IF ZERO
        ADD     #1,0(A1)                        ;ADJUST ASCENT
        ADD     D0,2(A1)                        ;ADJUST DESCENT
NOTSHAD

;
; SCALE RESULT IF NUMER <> DENOM
;
        MOVE.L  NUMER(A6),D0
        CMP.L   DENOM(A6),D0                    ;IS NUMER SAME AS DENOM ?
        BEQ.S   NOSCALE                         ;YES, SKIP SCALING

        MOVE    (A1),D0                         ;GET ASCENT
        MULU    NUMER+V(A6),D0                  ;SCALE ASCENT
        DIVU    DENOM+V(A6),D0
        MOVE    D0,(A1)+                        ;UPDATE ASCENT

        MOVE    (A1),D0                         ;GET DESCENT
        MULU    NUMER+V(A6),D0                  ;SCALE DESCENT
        DIVU    DENOM+V(A6),D0
        MOVE    D0,(A1)+                        ;UPDATE DESCENT

        MOVE    (A1),D0                         ;GET MAXWID
        MULU    NUMER+H(A6),D0                  ;SCALE MAXWID
        DIVU    DENOM+H(A6),D0
        MOVE    D0,(A1)+                        ;UPDATE MAXWID

        MOVE    (A1),D0                         ;GET LEADING
        MULU    NUMER+V(A6),D0                  ;SCALE LEADING
        DIVU    DENOM+V(A6),D0
        MOVE    D0,(A1)+                        ;UPDATE LEADING

NOSCALE UNLINK  PARAMSIZE,'GETFONTI'


        .END

ÿ