
        .INCLUDE libqd/GRAFTYPES.TEXT
;-----------------------------------------------------------------
;
;  -->  DRAWLINE.TEXT
;
;  This is the Line Generator.
;
;

        .PROC   DrawLine
        .REF    RgnBlt,RSect,ShieldCursor,ShowCursor
        .REF    InitRgn,SeekRgn,PatExpand,ColorMap,DrawSlab,SlabMode
        .REF    LeftMask,RightMask,BitMask,XorSlab,FixRatio,FixMul
;----------------------------------------------------------------
;
;  PROCEDURE DRAWLINE(P1,P2: POINT);
;
;  DRAWS A LINE CLIPPED TO CURRENT PORT'S CLIPRGN AND VISRGN
;
;  P1 AND P2 ARE GIVEN IN LOCAL COORDINATES OF THEPORT.
;


;------------------------------------------------
;
;  A6 OFFSETS OF PARAMETERS AFTER LINK:
;
PARAMSIZE       .EQU    8                       ;SIZE OF PARAMETERS
P1              .EQU    PARAMSIZE+8-4           ;POINT
P2              .EQU    P1-4                    ;POINT


;------------------------------------------------
;
;  A6 OFFSETS OF LOCAL VARIABLES AFTER LINK:
;
STATEA          .EQU    -RGNREC                 ;RGEION STATE RECORD
STATEB          .EQU    STATEA-RGNREC           ;REGION STATE RECORD
EXPAT           .EQU    STATEB-64               ;16 LONGS
SAVESTK         .EQU    EXPAT-4                 ;LONG
LINERECT        .EQU    SAVESTK-8               ;RECT
MINRECT         .EQU    LINERECT-8              ;RECT
FAKERGN         .EQU    MINRECT-2               ;WORD, MUST BE JUST BEFORE MINRECT
FAKEPTR         .EQU    FAKERGN-4               ;LONG, FAKE MASTER PTR
RECTFLAG        .EQU    FAKEPTR-2               ;WORD
LEFTEDGE        .EQU    RECTFLAG-4              ;LONG, FIXED POINT
RIGHTEDGE       .EQU    LEFTEDGE-4              ;LONG, FIXED POINT
SLOPE           .EQU    RIGHTEDGE-4             ;LONG, FIXED POINT
RGNA            .EQU    SLOPE-4                 ;LONG
RGNB            .EQU    RGNA-4                  ;LONG
MASKBUF         .EQU    RGNB-4                  ;LONG
BUFLEFT         .EQU    MASKBUF-2               ;WORD
BUFSIZE         .EQU    BUFLEFT-2               ;WORD
DSTLEFT         .EQU    BUFSIZE-4               ;LONG
VERT            .EQU    DSTLEFT-2               ;WORD
MODECASE        .EQU    VERT-4                  ;LONG
SAVEA5          .EQU    MODECASE-4              ;LONG
PORT            .EQU    SAVEA5-4                ;LONG
MODE            .EQU    PORT-2                  ;WORD
PAT             .EQU    MODE-4                  ;LONG
VARSIZE         .EQU    PAT                     ;SIZE OF LOCAL VARIABLES


;----------------------------------------------------------
;
;  ENTER HERE.  DRAWLINE TAKES CARE OF CURSOR.
;
        CHECKSTACK VARSIZE
        LINK    A6,#VARSIZE                     ;ALLOCATE LOCAL VARS
        MOVEM.L D0-D7/A1-A5,-(SP)               ;SAVE REGS
        MOVE.L  SP,SAVESTK(A6)                  ;REMEMBER STACK FOR LATER
        MOVE.L  A5,SAVEA5(A6)                   ;REMEMBER GLOBAL PTR
        MOVE.L  GRAFGLOBALS(A5),A0              ;POINT TO LISAGRAF GLOBALS
        MOVE.L  THEPORT(A0),A3                  ;POINT TO CURRENT GRAFPORT
        TST     PNVIS(A3)                       ;IS PNVIS NEGATIVE ?
        BLT     GOHOME                          ;YES, QUIT
        MOVE.L  A3,PORT(A6)                     ;SAVE PORT FOR LATER


;-------------------------------------------------------
;
;  QUIT IF PEN MODE NOT IN 8..15
;
        MOVEQ   #-8,D0
        AND     PNMODE(A3),D0                   ;GET ALL BUT 3 BITS OF PEN MODE
        CMP     #8,D0                           ;IS PEN MODE 8..15 ?
        BNE     GOHOME                          ;NO, QUIT


;-------------------------------------------------------
;
;  Check for color filtering.  Alter mode and pattern accordingly.
;
        MOVE    PNMODE(A3),-(SP)                ;PUSH PEN MODE
        PEA     PNPAT(A3)                       ;PUSH ADDR OF PEN PATTERN
        JSR     COLORMAP                        ;CHECK AGAINST THECOLOR, THEFILTER
        MOVE.L  (SP)+,PAT(A6)                   ;GET ALTERED PATTERN
        MOVE    (SP)+,MODE(A6)                  ;AND ALTERED MODE


;---------------------------------------------------------
;
;  GET CLIPRGN AND VISRGN HANDLES AND DE-REFERENCE
;
        MOVE.L  A3,A5                           ;PUT GRAFPTR IN A5
        MOVE.L  CLIPRGN(A5),A2                  ;GET CLIPRGN HANDLE
        MOVE.L  (A2),A2                         ;GET CLIPRGN POINTER
        MOVE.L  VISRGN(A5),A3                   ;GET VISRGN HANDLE
        MOVE.L  (A3),A3                         ;GET VISRGN POINTER


;-------------------------------------------------------------
;
;  SET UP LINERECT, THE RECTANGLE BOUNDING THE LINE AND PEN
;
        MOVE    P1+V(A6),D1                     ;GET V1
        MOVE    P1+H(A6),D2                     ;GET H1
        MOVE    P2+V(A6),D3                     ;GET V2
        MOVE    P2+H(A6),D4                     ;GET H2
        CMP     D3,D1                           ;IS V1 > V2 ?
        BLE.S   VOK2                            ;NO, CONTINUE
        EXG     D1,D3                           ;YES, SWAP VERT
VOK2    CMP     D4,D2                           ;IS H1 > H2 ?
        BLE.S   HOK2                            ;NO, CONTINUE
        EXG     D2,D4                           ;NO SWAP HORIZ
HOK2    ADD     PNSIZE+H(A5),D4                 ;ADD PEN WIDTH TO RIGHT
        ADD     PNSIZE+V(A5),D3                 ;ADD PEN HEIGHT TO BOTTOM
        MOVE    D1,LINERECT+TOP(A6)             ;SETUP LINE TOP
        MOVE    D2,LINERECT+LEFT(A6)            ;SETUP LINE LEFT
        MOVE    D3,LINERECT+BOTTOM(A6)          ;SETUP LINE BOTTOM
        MOVE    D4,LINERECT+RIGHT(A6)           ;SETUP LINE RIGHT


;-----------------------------------------------------------------------
;
;  CALC MINRECT, THE INTERSECTION OF LINERECT, BITMAP BOUNDS,
;  CLIPRGN BBOX, AND VISRGN BBOX.  QUIT IF NO INTERSECTION.
;
        PEA     LINERECT(A6)                    ;PUSH LINE RECT
        PEA     PORTBITS+BOUNDS(A5)             ;PUSH BITMAP BOUNDS
        PEA     RGNBBOX(A2)                     ;PUSH CLIPRGN BBOX
        PEA     RGNBBOX(A3)                     ;PUSH VISRGN BBOX
        MOVE    #4,-(SP)                        ;PUSH NRECTS=4
        PEA     MINRECT(A6)                     ;PUSH DST ADDR
        JSR     RSECT                           ;CALC INTERSECTION
        BEQ     GOHOME                          ;QUIT IF NO INTERSECT


;----------------------------------------------------------------
;
;  HIDE CURSOR IF CURSOR INTERSECTS MINRECT.
;
        PEA     MINRECT(A6)                       ;PUSH SHIELDRECT PARAMETER
        MOVE.L  PORTBITS+BOUNDS+TOPLEFT(A5),-(SP) ;PUSH DELTA TO CONVERT TO GLOBAL
        MOVE.L  SAVEA5(A6),A5                     ;GET GLOBAL PTR
        JSR     SHIELDCURSOR                      ;HIDE CURSOR IF IT INTERSECTS
        MOVE.L  PORT(A6),A5                       ;GET BACK THEPORT


;---------------------------------------------------
;
;  GET THE TWO LINE ENDPOINTS INTO REGISTERS
;  AND CHECK FOR HORIZONTAL OR VERTICAL LINE
;
        MOVE    P1+V(A6),D1                     ;GET V1
        MOVE    P1+H(A6),D2                     ;GET H1
        MOVE    P2+V(A6),D3                     ;GET V2
        MOVE    P2+H(A6),D4                     ;GET H2
        CMP     D2,D4                           ;H1=H2 ?
        BEQ.S   HVLINE                          ;YES, DO VERTICAL LINE
        CMP     D1,D3                           ;NO, IS V2 > V1 ?
        BGT.S   SLANTED                         ;YES, DRAW SLANTED LINE
        BLT.S   VSWAP                           ;SWAP POINTS AND DRAW SLANTED
                                                ;ELSE V1=V2, DO HORIZONTAL

;-----------------------------------------------------------
;
;  LINE IS EITHER HORIZONTAL OR VERTICAL.
;  CHECK FOR ONE DOT LINE, BLACK OR WHITE PATTERN, AND
;  CLIPRGN AND VISRGN BOTH RECTANGULAR. IF SO, OPTIMIZE FAST LINES.
;
HVLINE  CMP.L   #$00010001,PNSIZE(A5)           ;IS PEN 1 BY 1 ?
        BNE.S   NOTFAST                         ;NO, CONTINUE
        MOVE.L  PAT(A6),A0                      ;POINT TO (ALTERED) PATTERN
        MOVE.L  (A0)+,D0                        ;GET 1ST HALF PEN PATTERN
        BEQ.S   IFWHITE                         ;BRANCH IF WHITE
        CMP.L   #-1,D0                          ;IS PATTERN BLACK ?
        BNE.S   NOTFAST                         ;NO, CONTINUE
IFWHITE CMP.L   (A0)+,D0                        ;IS 2ND HALF PAT SAME ?
        BNE.S   NOTFAST                         ;NO, CONTINUE
        CMP     #10,RGNSIZE(A2)                 ;IS CLIPRGN RECTANGULAR ?
        BNE.S   NOTFAST                         ;NO, CONTINUE
        CMP     #10,RGNSIZE(A3)                 ;IS IT RECTANGULAR ?
        BEQ     FASTLINE                        ;YES, DO IT FAST !


;--------------------------------------------------------------------
;
;  NOT FASTLINE.  PUSH PARAMS AND CALL RGNBLT FOR HORIZ OR VERT LINE.
;
NOTFAST PEA     PORTBITS(A5)                    ;PUSH SRCBITS = DSTBITS
        PEA     PORTBITS(A5)                    ;PUSH DSTBITS
        PEA     MINRECT(A6)                     ;PUSH SRCRECT = DSTRECT
        PEA     MINRECT(A6)                     ;PUSH DSTRECT
        MOVE    PNMODE(A5),-(SP)                ;PUSH MODE
        PEA     PNPAT(A5)                       ;PUSH PATTERN
        MOVE.L  CLIPRGN(A5),-(SP)               ;PUSH CLIPRGN HANDLE
        MOVE.L  VISRGN(A5),-(SP)                ;PUSH VISRGN HANDLE
        MOVE    #10,FAKERGN+RGNSIZE(A6)         ;MAKE A DUMMY RECT RGN
        LEA     FAKERGN(A6),A0                  ;GET ADDRESS OF FAKE REGION DATA
        MOVE.L  A0,FAKEPTR(A6)                  ;CREATE A FAKE MASTER PTR
        PEA     FAKEPTR(A6)                     ;PUSH FAKE HANDLE
        MOVE.L  SAVEA5(A6),A5                   ;GET GLOBAL PTR
        JSR     RGNBLT                          ;DRAW THE HORIZ OR VERT LINE
        BRA     DONE                            ;RESTORE CURSOR AND QUIT


;-------------------------------------------------------------
;
;  THE LINE IS SLANTED.   SORT VERTICALLY
;  AND DRAW TOP TO BOTTOM AS HORIZONTAL SLABS.
;
VSWAP   EXG     D1,D3                           ;SORT POINTS BY VERTICAL
        EXG     D2,D4                           ;SWAP HORIZ TO MATCH
SLANTED MOVE    D3,D0                           ;COPY LINE BOTTOM
        ADD     PNSIZE+V(A5),D0                 ;ADD PEN HEIGHT
        CMP     RGNBBOX+TOP(A2),D0              ;IS RESULT <= CLIP TOP ?
        BLE     DONE                            ;YES, RESTORE CURSOR AND QUIT
        CMP     RGNBBOX+BOTTOM(A2),D1           ;IS TOP >= CLIP BOTTOM ?
        BGE     DONE                            ;YES, RESTORE CURSOR AND QUIT


;-------------------------------------------------------------
;
;  SET UP INITIAL FIXED POINT LEFTEDGE AND RIGHTEDGE
;  AND CHECK FOR ZERO PENSIZE.
;
        MOVE.W  D2,LEFTEDGE(A6)                 ;LEFTEDGE.INT := TOP HORIZ
        MOVE.W  #$8000,LEFTEDGE+2(A6)           ;LEFTEDGE.FRACT := 1/2
        MOVE.L  LEFTEDGE(A6),RIGHTEDGE(A6)      ;RIGHTEDGE:=LEFTEDGE
        MOVE    PNSIZE+H(A5),D0                 ;GET PEN WIDTH
        BLE     DONE                            ;QUIT IF PENWIDTH <= 0
        ADD.W   D0,RIGHTEDGE(A6)                ;ADD TO RIGHTEDGE.INT
        MOVE    PNSIZE+V(A5),D6                 ;GET PEN HEIGHT
        BLE     DONE                            ;QUIT IF PEN HEIGHT <= 0


;----------------------------------------------------
;
;  CALC FIXED POINT SLOPE = FixRatio(dh,dv);
;
        CLR.L   -(SP)                           ;MAKE ROOM FOR FCN RESULT
        MOVE    D4,-(SP)                        ;PUSH BOTTOM HORIZ
        SUB     D2,(SP)                         ;CALC DH
        MOVE    D3,-(SP)                        ;PUSH BOTTOM VERT
        SUB     D1,(SP)                         ;CALC DV
        JSR     FIXRATIO                        ;CALC SLOPE
        MOVE.L  (SP)+,D7                        ;POP RESULT
        MOVE.L  D7,SLOPE(A6)                    ;SAVE FOR LATER

;
;  CALC adjust := pen height * slope
;
        CLR.L   -(SP)                           ;PUSH ROOM FOR FCN RESULT
        CLR.W   -(SP)                           ;PUSH LOWORD = 0
        MOVE.W  D6,-(SP)                        ;PUSH HIWORD = PEN HEIGHT
        MOVE.L  D7,-(SP)                        ;PUSH SLOPE
        JSR     FIXMUL                          ;CALC FixMul(pnSize.v,slope)
        MOVE.L  (SP)+,D6                        ;POP ANSWER INTO D6

;
;  ADJUST LEFTEDGE AND RIGHTEDGE DEPENDING ON SIGN AND MAGNITUDE OF SLOPE
;
        MOVE.L  D7,D0                           ;COPY SLOPE
        ASR.L   #1,D0                           ;CALC SLOPE/2
        ADD.L   D0,LEFTEDGE(A6)                 ;ADD SLOPE/2 TO LEFT EDGE
        ADD.L   D0,RIGHTEDGE(A6)                ;ADD SLOPE/2 TO RIGHT EDGE

        TST.L   D7                              ;IS SLOPE NEGATIVE ?
        BMI.S   NEGSLOPE                        ;YES, CONTINUE
        SUB.L   D6,LEFTEDGE(A6)                 ;SUBTRACT ADJUST FROM LEFTEDGE
        CMP.L   #$00010000,D7                   ;IS SLOPE < ONE ?
        BGE.S   LESSV1                          ;NO, BRANCH

MOREV1  ADD.L   D7,LEFTEDGE(A6)                 ;ADD SLOPE TO LEFTEDGE
        BRA.S   SLOPEOK                         ;CONTINUE

LESSV1  SUB.W   #1,RIGHTEDGE(A6)                ;RIGHTEDGE := RIGHTEDGE - 1
        BRA.S   SLOPEOK                         ;CONTINUE


;----------------------------------------------------
;
;  SLOPE IS NEGATIVE
;
NEGSLOPE SUB.L  D6,RIGHTEDGE(A6)                ;SUBTRACT ADJUST FROM RIGHTEDGE
        CMP.L   #$FFFF0000,D7                   ;IS SLOPE > -ONE ?

        BLT.S   LESSV2                          ;NO, CONTINUE

MOREV2  ADD.L   D7,RIGHTEDGE(A6)                ;ADD SLOPE TO RIGHTEDGE
        BRA.S   SLOPEOK

LESSV2  ADD.W  #1,LEFTEDGE(A6)                  ;LEFTEDGE := LEFTEDGE + 1



;------------------------------------------------------------
;
;  GET MINRECT INTO REGISTERS
;
SLOPEOK MOVE    MINRECT+TOP(A6),D1              ;GET MINRECT TOP
        MOVE    MINRECT+LEFT(A6),D2             ;GET MINRECT LEFT
        MOVE    MINRECT+BOTTOM(A6),D3           ;GET MINRECT BOTTOM
        MOVE    MINRECT+RIGHT(A6),D4            ;GET MINRECT RIGHT


;-----------------------------------------------------------------
;
;  IF LINE WAS CLIPPED VERTICALLY, ADJUST LEFTEDGE AND RIGHTEDGE
;
        CMP     LINERECT+TOP(A6),D1             ;DID TOP GET CLIPPED ?
        BEQ.S   TOPOK                           ;NO, CONTINUE
        MOVE    D1,D0
        SUB     LINERECT+TOP(A6),D0             ;CALC DISCARD AMMOUNT
        MOVE.L  D7,D5                           ;GET 32 BIT SLOPE
        SWAP    D5                              ;GET HI WORD OF SLOPE
        MULS    D0,D5                           ;MULT TIMES DISCARD
        SWAP    D5                              ;PUT RESULT IN HI WORD
        CLR.W   D5                              ;CLEAR LO WORD
        MOVE.L  D7,D6                           ;GET NEW COPY OF SLOPE
        MULU    D0,D6                           ;MULT LO WORD TIMES DISCARD
        ADD.L   D6,D5                           ;CALC 32BIT SLOPE*DISCARD
        ADD.L   D5,LEFTEDGE(A6)                 ;BUMP LEFTEDGE TO NEW TOP
        ADD.L   D5,RIGHTEDGE(A6)                ;BUMP RIGHTEDGE TO NEW TOP
TOPOK


;---------------------------------------------------------------------------
;
;  INITIALIZE REGION STATE RECORDS IN CASE RGNA OR RGNB IS RECTANGULAR
;
        MOVE    #-32767,STATEA+THISV(A6)
        MOVE    #-32767,STATEB+THISV(A6)
        MOVE    #32767,STATEA+NEXTV(A6)
        MOVE    #32767,STATEB+NEXTV(A6)


;--------------------------------------------------------
;
;  CALC BUFLEFT
;
        MOVE    MINRECT+LEFT(A6),D2
        SUB     PORTBITS+BOUNDS+LEFT(A5),D2     ;CONVERT TO GLOBAL COORDS
        AND     #$FFF0,D2                       ;TRUNC TO MULT OF 16
        ADD     PORTBITS+BOUNDS+LEFT(A5),D2     ;CONVERT BACK TO LOCAL
        MOVE    D2,BUFLEFT(A6)                  ;SAVE AS BUFLEFT

        MOVE    MINRECT+RIGHT(A6),D0            ;GET MINRECT RIGHT
        SUB     D2,D0                           ;CALC MAXH-BUFLEFT
        LSR     #5,D0                           ;DIV BY 32 FOR LONGS
        MOVE    D0,BUFSIZE(A6)                  ;BUFSIZE = # LONGS - 1


;---------------------------------------------
;
;  EXPAND STACK IF NECESSARY
;
        .IF     FORMAC=0
        LSL     #2,D0                           ;QUAD FOR BYTECOUNT
        NEG     D0                              ;MAKE OFFSET NEGATIVE
        TST.W   0(SP,D0)                        ;EXPAND STACK
        MOVE    BUFSIZE(A6),D0                  ;RESTORE BUFSIZE
        .ENDC


;--------------------------------------------------------------------
;
;  ALLOCATE AND CLEAR A SCANLINE BUFFER FOR THE COMPOSITE MASK
;
CLRMASK CLR.L   -(SP)                           ;ALLOC AND CLEAR ONE LONG
        DBRA    D0,CLRMASK                      ;LOOP TILL DONE
        MOVE.L  SP,MASKBUF(A6)                  ;REMEMBER WHERE MASKBUF IS


;------------------------------------------------------------------------
;
;  INIT STATE RECORDS AND ALLOCATE BUFFERS FOR EACH NON-RECTANGULAR RGN.
;
        CLR     RECTFLAG(A6)                    ;INIT BOTH ARE RECT
        CMP     #10,RGNSIZE(A2)                 ;IS CLIPRGN RECTANGULAR ?
        BEQ.S   ARECT                           ;YES, CONTINUE
        ADD     #2,RECTFLAG(A6)                 ;NO, SET ITS FLAG
        MOVE.L  A2,A0                           ;POINT TO CLIPRGN
        LEA     STATEA(A6),A1                   ;POINT TO STATE RECORD A
        MOVE    MINRECT+LEFT(A6),D0             ;SET UP MINH
        MOVE    MINRECT+RIGHT(A6),D1            ;SET UP MAXH
        MOVE    BUFLEFT(A6),D2                  ;SET UP BUFLEFT
        JSR     INITRGN
ARECT
        CMP     #10,RGNSIZE(A3)                 ;IS VISRGN RECTANGULAR ?
        BEQ.S   BRECT                           ;YES, CONTINUE
        ADD     #4,RECTFLAG(A6)                 ;NO, SET ITS FLAG
        MOVE.L  A3,A0                           ;POINT TO VISRGN
        LEA     STATEB(A6),A1                   ;POINT TO STATE RECORD B
        MOVE    MINRECT+LEFT(A6),D0             ;SET UP MINH
        MOVE    MINRECT+RIGHT(A6),D1            ;SET UP MAXH
        MOVE    BUFLEFT(A6),D2                  ;SET UP BUFLEFT
        JSR     INITRGN
BRECT


;--------------------------------------------------------------------
;
;  IF ALL REGIONS ARE RECTANGULAR, THEN DRAW MINRECT INTO MASK BUFFER
;
        TST     RECTFLAG(A6)                    ;ARE ALL RGNS RECT ?
        BNE.S   NOTRECT                         ;NO, CONTNUE
        MOVE.L  MASKBUF(A6),A0                  ;YES, POINT TO MASK BUFFER
        MOVE    MINRECT+LEFT(A6),D3             ;SET UP LEFT
        SUB     BUFLEFT(A6),D3                  ;MAKE IT BUFFER RELATIVE
        MOVE    MINRECT+RIGHT(A6),D4            ;SET UP RIGHT
        SUB     BUFLEFT(A6),D4                  ;MAKE IT BUFFER RELATIVE
        JSR     XorSlab                         ;AND XOR BETWEEN THEM
NOTRECT



;------------------------------------
;
;  CALC STARTING DSTLEFT
;
        MOVE    MINRECT+TOP(A6),D1
        SUB     PORTBITS+BOUNDS+TOP(A5),D1      ;CONVERT MINV TO GLOBAL COORDS
        MULU    PORTBITS+ROWBYTES(A5),D1        ;MULT BY DST ROWBYTES
        ADD.L   PORTBITS+BASEADDR(A5),D1        ;ADD START OF BITMAP
        SUB     PORTBITS+BOUNDS+LEFT(A5),D2     ;CONVERT BUFLEFT TO GLOBAL
        LSR     #3,D2                           ;CALC BUFLEFT DIV 8
        EXT.L   D2                              ;CLR HI WORD
        ADD.L   D2,D1                           ;ADD HORIZ BYTE OFFSET
        MOVE.L  D1,DSTLEFT(A6)                  ;SAVE AS DSTLEFT


;------------------------------------------------------
;
;  CHECK WHETHER PATTERN NEEDS TO BE INVERTED
;
        CLR.L   D7                              ;SAY NOT INVERTED
        MOVE    MODE(A6),D2                     ;GET (ALTERED) PEN MODE
        BCLR    #2,D2                           ;TEST AND CLR INVERT BIT
        BEQ.S   NOTINV                          ;SKIP IF NOT INVERTED
        NOT.L   D7                              ;INVERTED; D7 GETS ALL 1'S


;---------------------------------------
;
;  SET UP MODE CASE JUMP
;
NOTINV  JSR     SlabMode                        ;GET DrawSlab CASE JUMP
        MOVE.L  A0,MODECASE(A6)                 ;SAVE FOR LATER


;------------------------------------------------------------------
;
;  EXPAND 8 BYTE PATTERN TO 16 LONGS
;
        MOVE    PORTBITS+BOUNDS+LEFT(A5),D2     ;GET GLOBAL-LOCAL OFFSET
        MOVE.L  PAT(A6),A0                      ;POINT TO BYTE WIDE PATTERN
        LEA     EXPAT(A6),A1                    ;POINT TO EXPANDED PATTERN
        MOVE.L  SAVEA5(A6),A5                   ;GET GLOBAL PTR
        JSR     PATEXPAND                       ;EXPAND 8 BYTES TO 16 LONGS
        MOVE.L  PORT(A6),A5                     ;RESTORE GRAFPTR IN A5


;-------------------------------------------------
;
;  SET UP (VERT * 4) MOD 32 AS PATTERN SELECTOR
;
        MOVEQ   #$F,D7                          ;TREAT COORD MOD 16
        AND     MINRECT+TOP(A6),D7              ;GET TOP VERT LOCAL COORD
        LSL     #2,D7                           ;QUAD FOR 4 BYTE PATTERNS
        MOVE.L  EXPAT(A6,D7),D6                 ;GET FIRST PATTERN DATA


;----------------------------------------------------
;
;  MAKE LEFTEDGE,RIGHTEDGE,MINRECT RELATIVE TO BUFFER LEFT
;
        MOVE    BUFLEFT(A6),D1
        SUB.W   D1,LEFTEDGE(A6)                 ;ADJUST LEFTEDGE.INT
        SUB.W   D1,RIGHTEDGE(A6)                ;ADJUST RIGHTEDGE.INT
        SUB     D1,MINRECT+LEFT(A6)
        SUB     D1,MINRECT+RIGHT(A6)


        MOVE    MINRECT+TOP(A6),VERT(A6)        ;INIT CURRENT VERT:=MINV
;------------------------------------------------------------
;
;  MAKE MASK BUFFER CURRENT FOR THIS VERTICAL
;  THEN SET UP AND DRAW CURRENT SCANLINE.
;
NXTMASK JSR     SEEKMASK                        ;MAKE MASK BUFFER CURRENT
        MOVE.W  LEFTEDGE(A6),D1                 ;GET LEFTEDGE.INT
        MOVE.W  RIGHTEDGE(A6),D2                ;GET RIGHTEDGE.INT
        MOVE.L  MODECASE(A6),A0                 ;GET MODE CASE JUMP
        MOVE.L  DSTLEFT(A6),A1                  ;INIT DSTPTR TO LEFT
        MOVE.L  MASKBUF(A6),A2                  ;INIT MASKPTR TO LEFT
        LEA     MINRECT(A6),A3                  ;POINT TO MINRECT
        JSR     DRAWSLAB                        ;DRAW ONE SLAB IN PENMODE


;-------------------------------------------------------------------
;
;  BUMP DESTINATION VERTICALLY AND LOOP FOR ALL SCAN LINES
;
        ADD     #4,D7                           ;BUMP PATTERN SELECTOR
        AND     #$3F,D7                         ;MOD 64 FOR 16 LONG REPEAT
        MOVE.L  EXPAT(A6,D7),D6                 ;GET PATTERN FOR NEXT ROW

        MOVE    PORTBITS+ROWBYTES(A5),D0        ;GET DST ROWBYTES
        EXT.L   D0                              ;MAKE IT LONG
        ADD.L   D0,DSTLEFT(A6)                  ;BUMP DST TO NEXT ROW

        MOVE.L  SLOPE(A6),D0                    ;GET FIXED POINT SLOPE
        ADD.L   D0,LEFTEDGE(A6)                 ;ADD SLOPE TO LEFT EDGE
        ADD.L   D0,RIGHTEDGE(A6)                ;ADD SLOPE TO RIGHT EDGE

        ADD     #1,VERT(A6)                     ;BUMP CURRENT VERT
        MOVE    VERT(A6),D0                     ;GET CURRENT VERT
        CMP     MINRECT+BOTTOM(A6),D0           ;ARE WE AT BOTTOM YET ?
        BLT     NXTMASK                         ;NO, LOOP ALL SCAN LINES


DONE    MOVE.L  SAVEA5(A6),A5                   ;GET GLOBAL PTR
        JSR     SHOWCURSOR                      ;RESTORE CURSOR
GOHOME  MOVE.L  SAVESTK(A6),SP                  ;STRIP BUFFER
        MOVEM.L (SP)+,D0-D7/A1-A5               ;RESTORE REGISTERS
        UNLINK  PARAMSIZE,'DRAWLINE'



;-----------------------------------------------------------------------------
;
;  LOCAL ROUTINE TO UPDATE THE MASK BUFFER
;  BASED ON WHICH REGIONS ARE RECTANGULAR
;
SEEKMASK MOVE   RECTFLAG(A6),D0
         MOVE   RECTJMP(D0),D0
         JMP    RECTJMP(D0)
RECTJMP .WORD   IGNORE-RECTJMP                    ;DO NOTHING IF BOTH RECT
        .WORD   A-RECTJMP
        .WORD   B-RECTJMP
        .WORD   AB-RECTJMP
IGNORE  RTS


;--------------------------------------------------------------------
;
;  ONLY REGION A IS NON RECTANGULAR.  UPDATE IT AND USE IT AS THE MASK
;
A       MOVE    VERT(A6),D0
        LEA     STATEA(A6),A1
        JSR     SEEKRGN
        MOVE.L  SCANBUF(A1),MASKBUF(A6)
        RTS


;--------------------------------------------------------------------
;
;  ONLY REGION B IS NON RECTANGULAR.  UPDATE IT AND USE IT AS THE MASK
;
B       MOVE    VERT(A6),D0
        LEA     STATEB(A6),A1
        JSR     SEEKRGN
        MOVE.L  SCANBUF(A1),MASKBUF(A6)
        RTS


;-------------------------------------------------------------------
;
;  REGIONS A AND B ARE NON-RECTANGULAR. UPDATE EACH,
;  THEN FORM INTERSECTION IN THE MASK BUFFER.
;
AB      MOVE    VERT(A6),D0
        LEA     STATEA(A6),A1
        JSR     SEEKRGN
        LEA     STATEB(A6),A1
        JSR     SEEKRGN
        MOVE.L  STATEA+SCANBUF(A6),A0
        MOVE.L  STATEB+SCANBUF(A6),A1
        MOVE.L  MASKBUF(A6),A2
        MOVE    BUFSIZE(A6),D1
ABLOOP  MOVE.L  (A0)+,D0
        AND.L   (A1)+,D0
        MOVE.L  D0,(A2)+
        DBRA    D1,ABLOOP
        RTS


        .INCLUDE libqd/FASTLINE.TEXT


        .END



ÿ