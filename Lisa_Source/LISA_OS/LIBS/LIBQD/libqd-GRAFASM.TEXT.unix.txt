

        .INCLUDE  libqd/GRAFTYPES.TEXT
;------------------------------------------------------------
;
;  --> GRAFASM.TEXT
;
;  Miscellaneous unclassified routines.
;


        .PROC InitGraf,1
        .REF  ScreenAddr,ScreenSize
;--------------------------------------------------
;
;  PROCEDURE InitGraf(globalPtr: Ptr);
;
;
PARAMSIZE       .EQU    4
GLOBALPTR       .EQU    PARAMSIZE+8-4           ;LONG


        LINK    A6,#0                           ;NO LOCALS
        MOVE.L  A4,-(SP)                        ;SAVE REG
        MOVE.L  GLOBALPTR(A6),A4                ;GET POINTER TO QUICKDRAW GLOBALS
        MOVE.L  A4,GRAFGLOBALS(A5)              ;SAVE IN MAGIC LOCATION

        LEA     lastGrafGlob(A4),A0             ;SET UP START POINTER
        LEA     thePort+4(A4),A1                ;SET UP LIMIT POINTER
CLRLP   CLR.W   (A0)+                           ;CLEAR A WORD
        CMPA.L  A1,A0                           ;CHECK LIMIT POINTER
        BNE     CLRLP                           ;CLEAR ALL GLOBALS

                                                ;fontData = all zeros
                                                ;fontPtr = Nil
                                                ;fontAdj := 0.0
                                                ;patAlign := (0,0)
                                                ;polyMax := 0
                                                ;thePoly := Nil
                                                ;playIndex := 0
                                                ;playPic := Nil
                                                ;rgnMax := 0
                                                ;rgnIndex := 0
                                                ;rgnBuf := Nil
        LEA     wideData(A4),A4
        MOVE.L  A4,D0                           ;REMEMBER ADDR OF WIDEDATA
        MOVE    #10,(A4)+                       ;wideData.rgnSize := 10
        MOVE.L  #$80018001,(A4)+                ;wideData.rgnBBox :=
        MOVE.L  #$7FFF7FFF,(A4)+                ;(-32767,-32767,32767,32767)
        MOVE.L  A4,D1                           ;REMEMBER ADDR OF WIDEMASTER
        MOVE.L  D0,(A4)+                        ;wideMaster := @wideData
        MOVE.L  D1,(A4)+                        ;wideOpen := @wideMaster
        MOVEQ   #1,D0
        MOVE.L  D0,(A4)+                        ;randSeed := 1
        CLR.L   -(SP)                           ;ROOM FOR FCN RESULT
        JSR     ScreenAddr                      ;GET START OF SCREEN
        MOVE.L  (SP)+,(A4)+                     ;INSTALL screenBits.baseAddr

;--------------------------------------------
;
;  TEMPORARY... MANUALLY INSTALL WIDTH AND HEIGHT.
;  SHOULD USE SCREENSIZE. (SEE BELOW)
;
        .IF FORMAC
        PEA     8(A4)                           ;PUSH ADDR OF WIDTH
        PEA     6(A4)                           ;PUSH ADDR OF HEIGHT
        JSR     ScreenSize                      ;GET SCREEN SIZE
        .ELSE
        MOVE    #720,8(A4)                      ;TEMPORARY TILL NEW HWINTL
        MOVE    #364,6(A4)
        .ENDC

        MOVE    8(A4),D0                        ;GET WIDTH
        LSR     #3,D0                           ;CONVERT DOTS TO BYTES
        MOVE    D0,(A4)+                        ;FILL IN ROWBYTES
        CLR.L   (A4)+                           ;bounds.topLeft := 0,0
        ADD     #4,A4                           ;BUMP PAST BOTRIGHT
        MOVEQ   #26,D0                          ;INIT LOOP COUNT
        LEA     CURDATA,A0                      ;POINT TO CURSOR DATA
CRSRLP  MOVE.L  (A0)+,(A4)+                     ;COPY A LONG INTO GLOBALS
        DBRA    D0,CRSRLP                       ;LOOP FOR 27 LONGS
                                                ;thePort := NIL

        MOVE.L  (SP)+,A4                        ;RESTORE REG
        UNLINK  PARAMSIZE,'INITGRAF'


CURDATA .WORD   $0000,$4000,$6000,$7000         ;ARROW.DATA
        .WORD   $7800,$7C00,$7E00,$7F00
        .WORD   $7F80,$7C00,$6C00,$4600
        .WORD   $0600,$0300,$0300,$0000

        .WORD   $C000,$E000,$F000,$F800         ;ARROW.MASK
        .WORD   $FC00,$FE00,$FF00,$FF80
        .WORD   $FFC0,$FFE0,$FE00,$EF00
        .WORD   $CF00,$8780,$0780,$0380

        .WORD   $0001,$0001                     ;ARROW.HOTSPOT := (1,1)

        .LONG   $77DD77DD,$77DD77DD             ;dkGray
        .LONG   $88228822,$88228822             ;ltGray
        .LONG   $AA55AA55,$AA55AA55             ;gray
        .LONG   $FFFFFFFF,$FFFFFFFF             ;black
        .LONG   $00000000,$00000000             ;white



        .PROC OpenPort,1
        .REF  NewRgn
;-------------------------------------------------------------
;
;  PROCEDURE OpenPort(port: GrafPtr);
;  { allocate clipRgn and visRgn, then call InitPort.
;
        CLR.L   -(SP)                           ;MAKE ROOM FOR FUNCTION RESULT
        JSR     NEWRGN                          ;ALLOCATE A NEW REGION
        CLR.L   -(SP)                           ;MAKE ROOM FOR FUNCTION RESULT
        JSR     NEWRGN                          ;ALLOCATE A SECOND NEW REGION
        MOVE.L  12(SP),A0                       ;POINT TO PORT
        MOVE.L  (SP)+,CLIPRGN(A0)               ;INSTALL NEW REGION INTO CLIPRGN
        MOVE.L  (SP)+,VISRGN(A0)                ;AND OTHER INTO VISRGN
                                                ;FALL THRU TO InitPort


        .PROC InitPort,1
        .REF  RectRgn,CopyRgn
;-------------------------------------------------------------
;
;  PROCEDURE InitPort(port: GrafPtr);
;
;  { initialize all fields of an existing GrafPort }
;
        MOVE.L  GRAFGLOBALS(A5),A0              ;POINT TO QUICKDRAW GLOBALS
        MOVE.L  4(SP),A1                        ;GET PORT PARAM
        MOVE.L  A1,THEPORT(A0)                  ;SetPort(port)
        CLR.W   (A1)+                           ;DEVICE := 0
        LEA     SCREENBITS(A0),A0               ;POINT TO SCREENBITS
        MOVE.L  (A0)+,(A1)+                     ;portBits := screenBits
        MOVE.W  (A0)+,(A1)+                     ;COPY ROWBYTES
        MOVE.L  (A0),(A1)+                      ;COPY TOPLEFT
        MOVE.L  4(A0),(A1)+                     ;COPY BOTRIGHT
        MOVE.L  (A0),(A1)+                      ;portRect := screenBits.bounds
        MOVE.L  4(A0),(A1)+                     ;all 8 bytes
        MOVE.L  (A1)+,-(SP)                     ;visRgn := screenBits.bounds
        MOVE.L  A0,-(SP)
        JSR     RECTRGN
        MOVE.L  4(SP),A1                        ;GET PORT PARAM
        MOVE.L  GRAFGLOBALS(A5),A0              ;POINT TO QUICKDRAW GLOBALS
        MOVE.L  WIDEOPEN(A0),-(SP)              ;PUSH WIDE OPEN RGN
        MOVE.L  CLIPRGN(A1),-(SP)               ;PUSH CLIPRGN
        JSR     COPYRGN                         ;SET TO WIDE OPEN
        MOVE.L  4(SP),A1                        ;GET PORT PARAM
        LEA     BKPAT(A1),A1
        CLR.L   (A1)+                           ;bkPat := white
        CLR.L   (A1)+
        MOVEQ   #-1,D0
        MOVE.L  D0,(A1)+                        ;fillPat := Black
        MOVE.L  D0,(A1)+
        CLR.L   (A1)+                           ;pnLoc := (0,0)
        MOVE.L  #$00010001,(A1)+                ;pnSize := (1,1)
        MOVE    #8,(A1)+                        ;pnMode := patCopy
        MOVE.L  D0,(A1)+                        ;pnPat := black
        MOVE.L  D0,(A1)+
        CLR.W   (A1)+                           ;pnVis := 0
        CLR.L   (A1)+                           ;txFont, txFace := 0
        MOVE    #1,(A1)+                        ;txMode := srcOr
        CLR     (A1)+                           ;txSize := 0
        CLR.L   (A1)+                           ;spExtra := 0.0
        MOVE.L  #blackColor,(A1)+               ;fgColor := blackColor
        MOVE.L  #whiteColor,(A1)+               ;bkColor := whiteColor
        CLR.L   (A1)+                           ;colrBit,patStretch := 0
        CLR.L   (A1)+                           ;picSave := Nil
        CLR.L   (A1)+                           ;rgnSave := Nil
        CLR.L   (A1)+                           ;polySave := Nil
        CLR.L   (A1)+                           ;grafProcs := Nil
        MOVE.L  (SP)+,(SP)                      ;STRIP PARAM
        RTS                                     ;AND RETURN



        .PROC ClosePort,1
        .REF  DisposeRgn
;-------------------------------------------------------------
;
;  PROCEDURE ClosePort(port: GrafPtr);
;
;  { just disposes of clipRgn and visRgn }
;
        FAKEHEAD
        MOVE.L  4(SP),A0                        ;GET PORT
        MOVE.L  CLIPRGN(A0),-(SP)               ;PUSH CLIPRGN HANDLE
        JSR     DISPOSERGN                      ;DISPOSE OF IT
        MOVE.L  4(SP),A0                        ;GET PORT
        MOVE.L  VISRGN(A0),-(SP)                ;PUSH VISRGN HANDLE
        JSR     DISPOSERGN                      ;DISPOSE OF IT TOO
        MOVE.L  (SP)+,(SP)                      ;STRIP PARAM
        RTS                                     ;AND RETURN
        FAKETAIL 'CLOSEPOR'



        .PROC SetStdProcs,1
        .REF  StdText,StdLine,StdRect,StdRRect,StdOval,StdArc,StdPoly
        .REF  StdRgn,StdBits,StdComment,StdTxMeas,StdGetPic,StdPutPic
;-------------------------------------------------------------
;
;  PROCEDURE SetStdProcs(VAR procs: QDProcs);
;
        MOVE.L  4(SP),A1                        ;GET ADDRESS OF PROC RECORD
        LEA     StdText,A0
        MOVE.L  A0,(A1)+
        LEA     StdLine,A0
        MOVE.L  A0,(A1)+
        LEA     StdRect,A0
        MOVE.L  A0,(A1)+
        LEA     StdRRect,A0
        MOVE.L  A0,(A1)+
        LEA     StdOval,A0
        MOVE.L  A0,(A1)+
        LEA     StdArc,A0
        MOVE.L  A0,(A1)+
        LEA     StdPoly,A0
        MOVE.L  A0,(A1)+
        LEA     StdRgn,A0
        MOVE.L  A0,(A1)+
        LEA     StdBits,A0
        MOVE.L  A0,(A1)+
        LEA     StdComment,A0
        MOVE.L  A0,(A1)+
        LEA     StdTxMeas,A0
        MOVE.L  A0,(A1)+
        LEA     StdGetPic,A0
        MOVE.L  A0,(A1)+
        LEA     StdPutPic,A0
        MOVE.L  A0,(A1)+
        MOVE.L  (SP)+,(SP)                      ;STRIP PARAM
        RTS                                     ;AND RETURN



        .PROC LocalToGlobal,1
;-------------------------------------------------------------
;
;  PROCEDURE LocalToGlobal(VAR pt: Point);
;
;  restores all registers.
;
        FAKEHEAD
        MOVEM.L D0/A0/A1,-(SP)                  ;SAVE REGS
        MOVE.L  GRAFGLOBALS(A5),A0              ;POINT TO QUICKDRAW GLOBALS
        MOVE.L  THEPORT(A0),A0                  ;POINT TO CURRENT GRAFPORT
        MOVE.L  16(SP),A1                       ;POINT TO VAR PT
        MOVE    PORTBITS+BOUNDS+TOP(A0),D0      ;GET DV
        SUB     D0,(A1)+                        ;CONVERT VERT TO GLOBAL
        MOVE    PORTBITS+BOUNDS+LEFT(A0),D0     ;GET DH
        SUB     D0,(A1)+                        ;CONVERT HORIZ TO GLOBAL
        MOVEM.L (SP)+,D0/A0/A1                  ;RESTORE REGS
        MOVE.L  (SP)+,(SP)                      ;STRIP 4 BYTES
        RTS                                     ;AND RETURN
        FAKETAIL 'LOCALTOG'



        .PROC GlobalToLocal,1
;-------------------------------------------------------------
;
;  PROCEDURE GlobalToLocal(VAR pt: Point);
;
;  restores all registers.
;
        FAKEHEAD
        MOVEM.L D0/A0/A1,-(SP)                  ;SAVE REGS
        MOVE.L  GRAFGLOBALS(A5),A0              ;POINT TO QUICKDRAW GLOBALS
        MOVE.L  THEPORT(A0),A0                  ;POINT TO CURRENT GRAFPORT
        MOVE.L  16(SP),A1                       ;POINT TO VAR PT
        MOVE    PORTBITS+BOUNDS+TOP(A0),D0      ;GET DV
        ADD     D0,(A1)+                        ;CONVERT VERT TO LOCAL
        MOVE    PORTBITS+BOUNDS+LEFT(A0),D0     ;GET DH
        ADD     D0,(A1)+                        ;CONVERT HORIZ TO LOCAL
        MOVEM.L (SP)+,D0/A0/A1                  ;RESTORE REGS
        MOVE.L  (SP)+,(SP)                      ;STRIP 4 BYTES
        RTS                                     ;AND RETURN
        FAKETAIL 'GLOBALTO'



        .PROC GrafDevice,1
        .REF  PortWord
;----------------------------------------------------------
;
;  PROCEDURE GrafDevice(device: INTEGER);
;
        FAKEHEAD
        MOVEQ   #DEVICE,D0                      ;PUT PORT OFFSET IN D0
        JMP     PORTWORD                        ;INSTALL PARAM INTO THEPORT
        FAKETAIL 'GRAFDEVI'



        .PROC SetPort,1
;----------------------------------------------------------
;
;  PROCEDURE SetPort(gp: GrafPtr);
;  { switch the current port to a different GrafPort }
;
        FAKEHEAD
        MOVE.L  GRAFGLOBALS(A5),A0              ;POINT TO QUICKDRAW GLOBALS
        MOVE.L  4(SP),THEPORT(A0)               ;INSTALL INTO THEPORT
        MOVE.L  (SP)+,(SP)                      ;STRIP 4 BYTES
        RTS                                     ;AND RETURN
        FAKETAIL 'SETPORT '



        .PROC GetPort,1
;----------------------------------------------------------
;
;  PROCEDURE GetPort(VAR gp: GrafPtr);
;  { inquire the current GrafPort }
;
        FAKEHEAD
        MOVE.L  GRAFGLOBALS(A5),A0              ;POINT TO QUICKDRAW GLOBALS
        MOVE.L  4(SP),A1                        ;POINT TO VAR GP
        MOVE.L  THEPORT(A0),(A1)                ;COPY FROM THEPORT
        MOVE.L  (SP)+,(SP)                      ;STRIP 4 BYTES
        RTS                                     ;AND RETURN
        FAKETAIL 'GETPORT '



        .PROC SetPortBits,1
;----------------------------------------------------------
;
;  PROCEDURE SetPortBits(bm: BitMap);
;  { re-direct output to a different BitMap }
;
        FAKEHEAD
        MOVE.L  GRAFGLOBALS(A5),A0              ;POINT TO QUICKDRAW GLOBALS
        MOVE.L  THEPORT(A0),A0                  ;GET CURRENT GRAFPORT
        LEA     PORTBITS(A0),A0                 ;POINT TO PORTBITS
        MOVE.L  4(SP),A1                        ;POINT TO BITMAP
        MOVE.L  (A1)+,(A0)+                     ;COPY BASEADDR
        MOVE.W  (A1)+,(A0)+                     ;COPY ROWBYTES
        MOVE.L  (A1)+,(A0)+                     ;COPY BOUNDS.TOPLEFT
        MOVE.L  (A1)+,(A0)+                     ;COPY BOUNDS.BOTRIGHT
        MOVE.L  (SP)+,(SP)                      ;STRIP 4 BYTES
        RTS                                     ;AND RETURN
        FAKETAIL 'SETPORTB'



        .PROC PortSize,2
;----------------------------------------------------------
;
;  PROCEDURE PortSize(width,height: INTEGER);
;
        FAKEHEAD
        MOVE.L  GRAFGLOBALS(A5),A0              ;POINT TO QUICKDRAW GLOBALS
        MOVE.L  THEPORT(A0),A0                  ;GET CURRENT GRAFPORT
        MOVE    PORTRECT+LEFT(A0),D0            ;GET PORTRECT.LEFT
        ADD     6(SP),D0                        ;ADD WIDTH
        MOVE    D0,PORTRECT+RIGHT(A0)           ;UPDATE PORTRECT.RIGHT
        MOVE    PORTRECT+TOP(A0),D0             ;GET PORTRECT.TOP
        ADD     4(SP),D0                        ;ADD HEIGHT
        MOVE    D0,PORTRECT+BOTTOM(A0)          ;UPDATE PORTRECT.BOTTOM
        MOVE.L  (SP)+,(SP)                      ;STRIP 4 BYTES
        RTS                                     ;AND RETURN
        FAKETAIL 'PORTSIZE'



        .PROC MovePortTo,2
;----------------------------------------------------------
;
;  PROCEDURE MovePortTo(leftGlobal,topGlobal: INTEGER);
;  { move portRect to a different part of the bitmap }
;
        FAKEHEAD
        MOVE.L  GRAFGLOBALS(A5),A0              ;POINT TO QUICKDRAW GLOBALS
        MOVE.L  THEPORT(A0),A0                  ;GET CURRENT GRAFPORT
        MOVE    PORTRECT+LEFT(A0),D0            ;GET PORTRECT.LEFT
        SUB     PORTBITS+BOUNDS+LEFT(A0),D0     ;CONVERT TO GLOBAL
        SUB     6(SP),D0                        ;SUB LEFTGLOBAL FOR DH
        MOVE    PORTRECT.TOP(A0),D1             ;GET PORTRECT.TOP
        SUB     PORTBITS+BOUNDS+TOP(A0),D1      ;CONVERT TO GLOBAL
        SUB     4(SP),D1                        ;SUB TOPGLOBAL FOR DV
        ADD     D0,PORTBITS+BOUNDS+LEFT(A0)     ;OFFSET PORTBITS.BOUNDS DH,DV
        ADD     D0,PORTBITS+BOUNDS+RIGHT(A0)
        ADD     D1,PORTBITS+BOUNDS+TOP(A0)
        ADD     D1,PORTBITS+BOUNDS+BOTTOM(A0)
        MOVE.L  (SP)+,(SP)                      ;STRIP 4 BYTES
        RTS                                     ;AND RETURN
        FAKETAIL 'MOVEPORT'



        .PROC SetOrigin,2
        .REF  OffsetRgn
;----------------------------------------------------------
;
;  PROCEDURE SetOrigin(h,v: INTEGER);
;  { re-define the local coords by adjusting portBits.bounds, }
;  { portRect, and visRgn }
;
        FAKEHEAD
        MOVE.L  GRAFGLOBALS(A5),A0              ;POINT TO QUICKDRAW GLOBALS
        MOVE.L  THEPORT(A0),A0                  ;GET CURRENT GRAFPORT
        MOVE.L  4(SP),D0                        ;GET V AND H BOTH
        CMP.L   PORTRECT+TOPLEFT(A0),D0         ;SAME AS ALREADY IN THEPORT ?
        BEQ.S   DONE                            ;YES, QUIT

        MOVE    6(SP),D0                        ;GET H
        SUB     PORTRECT+LEFT(A0),D0            ;DH:=H-PORTRECT.LEFT
        MOVE    4(SP),D1                        ;GET V
        SUB     PORTRECT+TOP(A0),D1             ;DV:=V-PORTRECT.TOP

        ADD     D0,PORTBITS+BOUNDS+LEFT(A0)     ;OFFSET PORTBITS.BOUNDS
        ADD     D0,PORTBITS+BOUNDS+RIGHT(A0)
        ADD     D1,PORTBITS+BOUNDS+TOP(A0)
        ADD     D1,PORTBITS+BOUNDS+BOTTOM(A0)

        ADD     D0,PORTRECT+LEFT(A0)            ;OFFSET PORTRECT
        ADD     D0,PORTRECT+RIGHT(A0)
        ADD     D1,PORTRECT+TOP(A0)
        ADD     D1,PORTRECT+BOTTOM(A0)

        MOVE.L  VISRGN(A0),-(SP)                ;OFFSET VISRGN
        MOVE    D0,-(SP)
        MOVE    D1,-(SP)
        JSR     OFFSETRGN

DONE    MOVE.L  (SP)+,(SP)                      ;STRIP 4 BYTES
        RTS                                     ;AND RETURN
        FAKETAIL 'SETORIGI'




        .PROC SetClip,1
        .REF  CopyRgn
;----------------------------------------------------------
;
;  PROCEDURE SetClip(rgn: RgnHandle);
;
;  copy rgn into theport^.clipRgn
;
        FAKEHEAD
        MOVE.L  (SP)+,A0                        ;POP RETURN ADDR
        MOVE.L  GRAFGLOBALS(A5),A1              ;POINT TO QUICKDRAW GLOBALS
        MOVE.L  THEPORT(A1),A1                  ;GET CURRENT GRAFPORT
        MOVE.L  CLIPRGN(A1),-(SP)               ;PUSH THEPORT^.CLIPRGN
        MOVE.L  A0,-(SP)                        ;RESTORE RETURN ADDR
        JMP     COPYRGN                         ;AND GO TO COPYRGN
        FAKETAIL 'SETCLIP '



        .PROC GetClip,1
        .REF  CopyRgn
;----------------------------------------------------------
;
;  PROCEDURE GetClip(rgn: RgnHandle);
;
;  copy from theport^.clipRgn into rgn.
;
        FAKEHEAD
        MOVE.L  (SP)+,A0                        ;POP RETURN ADDR
        MOVE.L  (SP)+,D0                        ;POP RGN HANDLE
        MOVE.L  GRAFGLOBALS(A5),A1              ;POINT TO QUICKDRAW GLOBALS
        MOVE.L  THEPORT(A1),A1                  ;GET CURRENT GRAFPORT
        MOVE.L  CLIPRGN(A1),-(SP)               ;PUSH THEPORT^.CLIPRGN
        MOVE.L  D0,-(SP)                        ;PUSH RGN
        MOVE.L  A0,-(SP)                        ;RESTORE RETURN ADDR
        JMP     COPYRGN                         ;AND GO TO COPYRGN
        FAKETAIL 'GETCLIP '



        .PROC ClipRect,1
        .REF  RectRgn
;----------------------------------------------------------
;
;  PROCEDURE ClipRect(r: Rect);
;  { Make the current grafport's clipRgn match a given rectangle }
;
        FAKEHEAD
        MOVE.L  GRAFGLOBALS(A5),A0              ;POINT TO QUICKDRAW GLOBALS
        MOVE.L  THEPORT(A0),A0                  ;GET CURRENT GRAFPORT
        MOVE.L  4(SP),A1                        ;GET ADDR OF RECT
        MOVE.L  CLIPRGN(A0),-(SP)               ;PUSH CLIPRGN
        MOVE.L  A1,-(SP)                        ;PUCH ADDR OF RECT
        JSR     RECTRGN
        MOVE.L  (SP)+,(SP)                      ;STRIP 4 BYTES
        RTS                                     ;AND RETURN
        FAKETAIL 'CLIPRECT'



        .PROC BackPat,1
;----------------------------------------------------------
;
;  PROCEDURE BackPat(pat: Pattern);
;  { set the background pattern }
;
        FAKEHEAD
        MOVE.L  GRAFGLOBALS(A5),A0              ;POINT TO QUICKDRAW GLOBALS
        MOVE.L  THEPORT(A0),A0                  ;GET CURRENT GRAFPORT
        LEA     BKPAT(A0),A0                    ;POINT TO BKPAT
        MOVE.L  4(SP),A1                        ;GET ADDR OF PATTERN
        MOVE.L  (A1)+,(A0)+                     ;COPY ALL 8 BYTES
        MOVE.L  (A1)+,(A0)+                     ;FROM PAT INTO BKPAT
        MOVE.L  (SP)+,(SP)                      ;STRIP 4 BYTES
        RTS                                     ;AND RETURN
        FAKETAIL 'BACKPAT '



        .PROC AddPt,2
;-------------------------------------------------------------
;
;  PROCEDURE AddPt(src: Point; VAR dst: Point);
;  { add two points together, restores all regs }
;
        FAKEHEAD
        MOVEM.L D0/A0,-(SP)                     ;SAVE REGS
        MOVE.L  12(SP),A0                       ;POINT TO DST
        MOVE    16+V(SP),D0                     ;GET SRC.V
        ADD     D0,(A0)+                        ;ADD TO DST.V
        MOVE    16+H(SP),D0                     ;GET SRC.H
        ADD     D0,(A0)+                        ;ADD TO DST.H
        MOVEM.L (SP)+,D0/A0                     ;RESTORE REGS
        MOVE.L  (SP)+,(SP)
        MOVE.L  (SP)+,(SP)                      ;STRIP 8 BYTES
        RTS                                     ;AND RETURN
        FAKETAIL 'ADDPT   '



        .PROC SubPt,2
;-------------------------------------------------------------
;
;  PROCEDURE SubPt(src: Point; VAR dst: Point);
;  { subtract src Point from dst point, restores all regs }
;
        FAKEHEAD
        MOVEM.L D0/A0,-(SP)                     ;SAVE REGS
        MOVE.L  12(SP),A0                       ;POINT TO DST
        MOVE    16+V(SP),D0                     ;GET SRC.V
        SUB     D0,(A0)+                        ;SUB FROM DST.V
        MOVE    16+H(SP),D0                     ;GET SRC.H
        SUB     D0,(A0)+                        ;SUB FROM DST.H
        MOVEM.L (SP)+,D0/A0                     ;RESTORE REGS
        MOVE.L  (SP)+,(SP)
        MOVE.L  (SP)+,(SP)                      ;STRIP 8 BYTES
        RTS                                     ;AND RETURN
        FAKETAIL 'SUBPT   '



        .PROC SetPt,3
;-------------------------------------------------------------
;
;  PROCEDURE SetPt(VAR pt: Point; h,v: INTEGER);
;
        FAKEHEAD
        MOVE.L  (SP)+,A0                        ;POP RETURN ADDR
        MOVE.L  (SP)+,D0                        ;POP H,V
        MOVE.L  (SP)+,A1                        ;POP VAR ADDR OF PT
        MOVE.L  D0,(A1)                         ;STORE H,V INTO PT
        JMP     (A0)                            ;RETURN
        FAKETAIL 'SETPT   '


        .FUNC EqualPt,2
;----------------------------------------------------------
;
;  FUNCTION  EqualPt(pt1,pt2: Rect): BOOLEAN;
;
;  CLOBBERS D0,A0.
;
        FAKEHEAD
        MOVE.L  (SP)+,A0                        ;POP RETURN ADDR
        MOVE.L  (SP)+,D0                        ;POP PT2 VALUE
        CMP.L   (SP)+,D0                        ;IS IT SAME AS PT1 ?
        SEQ     (SP)                            ;IF YES, SET TO TRUE
        NEG.B   (SP)                            ;CONVERT -1 TO 1
        JMP     (A0)                            ;RETURN
        FAKETAIL 'EQUALPT '





        .END



ÿ