
        .INCLUDE libqd/GRAFTYPES.TEXT
;-----------------------------------------------------------
;
;
;     ****   ***  *****  *     *     *     ****    ***
;     *   *   *     *    **   **    * *    *   *  *   *
;     *   *   *     *    * * * *   *   *   *   *  *
;     ****    *     *    *  *  *   *   *   ****    ***
;     *   *   *     *    *     *   *****   *          *
;     *   *   *     *    *     *   *   *   *      *   *
;     ****   ***    *    *     *   *   *   *       ***
;
;
;
;  QuickDraw Routines to operate on BitMaps.
;

        .PROC StdBits,5
        .REF  CheckPic,PutPicByte,PutPicWord,PutPicRgn,PutPicData
        .REF  StretchBits,PackBits
;---------------------------------------------------------------
;
;  PROCEDURE StdBits(VAR srcBits: BitMap;
;                    VAR srcRect: Rect;
;                    VAR dstRect: Rect;
;                    mode:        INTEGER;
;                    maskRgn:     RgnHandle);
;
;  A6 OFFSETS OF PARAMS AFTER LINK:
;
PARAMSIZE       .EQU    18
SRCBITS         .EQU    PARAMSIZE+8-4           ;LONG, ADDR OF BITMAP
SRCRECT         .EQU    SRCBITS-4               ;LONG, ADDR OF RECT
DSTRECT         .EQU    SRCRECT-4               ;LONG, ADDR OF RECT
MODE            .EQU    DSTRECT-2               ;WORD
MASKRGN         .EQU    MODE-4                  ;LONG, RGNHANDLE

MYBITS          .EQU    -14                     ;BITMAP
PACKBUF         .EQU    MYBITS-256              ;SCANLINE PACKING BUFFER
SRCPTR          .EQU    PACKBUF-4               ;LONG
DSTPTR          .EQU    SRCPTR-4                ;LONG
VARSIZE         .EQU    DSTPTR                  ;TOTAL BYTES OF LOCALS


        LINK    A6,#VARSIZE                     ;ALLOCATE STACK FRAME
        MOVEM.L D3-D7/A2-A4,-(SP)               ;SAVE REGS
        JSR     CHECKPIC                        ;SET UP A4,A3 AND CHECK PICSAVE
        BLE     NOTPIC                          ;BRANCH IF NOT PICSAVE
;
;  TRIM SRCBITS
;
        MOVE.L  SRCBITS(A6),A0                  ;GET ADDR OF SRCBITS
        LEA     MYBITS(A6),A1                   ;POINT TO MY COPY
        MOVE.L  BASEADDR(A0),A3                 ;GET BASEADDR (CLOBBERS A3)
        MOVE    ROWBYTES(A0),D4                 ;GET OLD ROWBYTES
        MOVE.L  BOUNDS+TOPLEFT(A0),BOUNDS+TOPLEFT(A1)    ;COPY BOUNDS TOPLEFT
        MOVE.L  BOUNDS+BOTRIGHT(A0),BOUNDS+BOTRIGHT(A1)  ;COPY BOUNDS BOTRIGHT
        MOVE.L  SRCRECT(A6),A0                  ;POINT TO SRCRECT
        MOVE    TOP(A0),D0                      ;GET SRCRECT.TOP
        SUB     BOUNDS+TOP(A1),D0               ;SKIPTOP:=SRCRECT.TOP-BOUNDS.TOP
        BLE.S   TOPOK                           ;CONTINUE IF SKIPTOP NEG
        ADD     D0,BOUNDS+TOP(A1)               ;NEWTOP := SRCRECT TOP
        MULU    D4,D0                           ;CALC VERT OFFSET
        ADD.L   D0,A3                           ;ADJUST BASEADDR

TOPOK   MOVE    BOTTOM(A0),D0                   ;GET SRCRECT.BOTTOM
        CMP     BOUNDS+BOTTOM(A1),D0            ;IS SRCRECT BOT < BOUNDS BOT ?
        BGE.S   BOTOK                           ;NO, CONTINUE
        MOVE    D0,BOUNDS+BOTTOM(A1)            ;YES, TRIM BOUNDS BOTTOM

BOTOK   MOVE    LEFT(A0),D0                     ;GET SRCRECT.LEFT
        SUB     BOUNDS+LEFT(A1),D0              ;CALC SKIPLEFT
        BLE.S   LEFTOK                          ;CONTINUE IF SKIPLEFT NEG
        LSR     #3,D0                           ;DIV BY 8 FOR SKIP BYTES
        ADD     D0,A3                           ;OFFSET BASEADDR HORIZ
        LSL     #3,D0                           ;BYTES TIMES 8 FOR DOTS
        ADD     D0,BOUNDS+LEFT(A1)              ;ADD DOTS TO BOUNDS.LEFT

LEFTOK  MOVE    RIGHT(A0),D0                    ;GET SRCRECT.RIGHT
        SUB     BOUNDS+LEFT(A1),D0              ;CONVERT TO GLOBAL
        ADD     #7,D0                           ;ROUND UP
        LSR     #3,D0                           ;TO NEXT MULT OF 8
        LSL     #3,D0
        ADD     BOUNDS+LEFT(A1),D0              ;RETURN TO LOCAL
        CMP     BOUNDS+RIGHT(A1),D0             ;IS RESULT < BOUNDS.RIGHT ?
        BGE.S   RIGHTOK                         ;NO, CONTINUE
        MOVE    D0,BOUNDS+RIGHT(A1)             ;YES, TRIM RIGHT
RIGHTOK
;
;  CALC NEW ROWBYTES AFTER TRIMMING
;
        MOVE    BOUNDS+RIGHT(A1),D5             ;GET TRIMMED RIGHT
        SUB     BOUNDS+LEFT(A1),D5              ;CALC WIDTH
        ADD     #15,D5                          ;ROUND UP
        LSR     #4,D5                           ;DIV BY 16
        BLE     BITSOK                          ;IGNORE IF NEWROW <= 0
        ADD     D5,D5                           ;DOUBLE FOR NEW ROWBYTES
        MOVE    D5,ROWBYTES(A1)                 ;COPY ROWBYTES

        MOVE.B  #$90,-(SP)                      ;PUSH OPCODE = BITSRECT
        TST.L   MASKRGN(A6)                     ;IS MASKRGN NIL ?
        BEQ.S   NOTRGN                          ;YES, CONTINUE
        MOVE.B  #$90,(SP)                       ;REPLACE OPCODE = BITSRGN

NOTRGN  CMP     #8,D5                           ;IS NEWROW < 8 ?
        BLT.S   NOPACK                          ;YES, DONT BITPACK
        ADD.B   #8,(SP)                         ;SET BIT 3 FOR BITPACK
NOPACK  JSR     PutPicByte                      ;PUT OPCODE TO THEPIC

        PEA     MYBITS+ROWBYTES(A6)             ;PUSH ADDR OF ROWBYTYES,BOUNDS
        MOVE    #10,-(SP)                       ;PUSH BYTECOUNT = 10
        JSR     PutPicData                      ;PUT ROWBYTES,BOUNDS TO THEPIC

        MOVE.L  SRCRECT(A6),-(SP)
        MOVE    #8,-(SP)
        JSR     PutPicData                      ;PUT SRCRECT
        MOVE.L  DSTRECT(A6),-(SP)
        MOVE    #8,-(SP)
        JSR     PutPicData                      ;PUT DSTRECT
        MOVE    MODE(A6),-(SP)
        JSR     PutPicWord                      ;PUT MODE

        TST.L   MASKRGN(A6)                     ;IS MASKRGN NIL ?
        BEQ.S   NOMASK                          ;YES, SKIP IT
        MOVE.L  MASKRGN(A6),-(SP)               ;NO, PUSH MASKRGN
        JSR     PutPicRgn                       ;PUT MASKRGN TO THEPIC
NOMASK
;
;  NOW PUT THE BITMAP DATA:  IF NEWROW >= 8 THEN USE PACKBITS
;
        LEA     MYBITS(A6),A2                   ;POINT TO (TRIMMED) BITMAP
        MOVE    BOUNDS+BOTTOM(A2),D7
        SUB     BOUNDS+TOP(A2),D7               ;HEIGHT := BOUNDS BOT - TOP

        CMP     #8,D5                           ;IS NEWROW < 8 ?
        BLT.S   START2                          ;YES, DONT TRY TO PACK
        BRA.S   START1                          ;GO TO LOOP START
MORE1   MOVE.L  A3,SRCPTR(A6)                   ;SRCPTR := ^SCANLINE DATA
        LEA     PACKBUF(A6),A0
        MOVE.L  A0,DSTPTR(A6)                   ;DSTPTR := @PACKBUF
        PEA     SRCPTR(A6)                      ;PUSH VAR SRCPTR
        PEA     DSTPTR(A6)                      ;PUSH VAR DSTPTR
        MOVE    D5,-(SP)                        ;PUSH SRCBYTES = NEWROW
        JSR     PackBits                        ;PACK ROW INTO PACKBUF
        MOVE.L  DSTPTR(A6),D6                   ;GET UPDATED DSTPTR
        LEA     PACKBUF(A6),A0                  ;POINT TO PACKBUF
        SUB.L   A0,D6                           ;CALC PACKED BYTECOUNT
        MOVE.B  D6,-(SP)
        JSR     PutPicByte                      ;PUT PACKED BYTECOUNT TO THEPIC
        PEA     PACKBUF(A6)
        MOVE    D6,-(SP)
        JSR     PutPicData                      ;PUT PACKED DATA TO THEPIC
        ADD     D4,A3                           ;ADD OLDROW TO BITS PTR
START1  DBRA    D7,MORE1                        ;LOOP FOR HEIGHT ROWS
        BRA.S   BITSOK

;
;  ROWBYTES < 8, DONT USE PACKBITS
;
MORE2   MOVE.L  A3,-(SP)                        ;PUSH ADDR OF BITS
        MOVE    D5,-(SP)                        ;PUSH BYTECOUNT = NEWROW
        JSR     PutPicData                      ;PUT ONE ROW OF BITMAP DATA
        ADD     D4,A3                           ;ADD OLDROW TO BITS PTR
START2  DBRA    D7,MORE2                        ;LOOP FOR HEIGHT ROWS


BITSOK  MOVE.L  THEPORT(A4),A3                  ;RESTORE THEPORT PTR
NOTPIC  TST     PNVIS(A3)                       ;IS PNVIS >= 0 ?
        BLT.S   GOHOME                          ;NO, QUIT
        MOVE.L  SRCBITS(A6),-(SP)               ;PUSH SRCBITS
        PEA     PORTBITS(A3)                    ;PUSH DSTBITS = PORTBITS
        MOVE.L  SRCRECT(A6),-(SP)               ;PUSH SRCRECT
        MOVE.L  DSTRECT(A6),-(SP)               ;PUSH DSTRECT
        MOVE    MODE(A6),-(SP)                  ;PUSH MODE
        MOVE.L  CLIPRGN(A3),-(SP)               ;PUSH CLIPRGN
        MOVE.L  VISRGN(A3),-(SP)                ;PUSH VISRGN
        MOVE.L  MASKRGN(A6),-(SP)               ;PUSH MASKRGN
        BNE.S   MASKOK                          ;WAS IT NIL ?
        MOVE.L  WIDEOPEN(A4),(SP)               ;YES, REPLACE WITH WIDEOPEN
MASKOK  JSR     STRETCHBITS                     ;CALL STRETCHBITS

GOHOME  MOVEM.L (SP)+,D3-D7/A2-A4               ;RESTORE REGS
        UNLINK  PARAMSIZE,'STDBITS '



        .PROC CopyBits,6
        .REF  StdBits,ShieldCursor,ShowCursor,StretchBits
;---------------------------------------------------------------
;
;  PROCEDURE CopyBits(srcBits,dstBits: BitMap;
;                     srcRect,dstRect: Rect;
;                     mode:            INTEGER;
;                     maskRgn:         RgnHandle *);
;
;
;  A6 OFFSETS OF PARAMS AND LOCALS AFTER LINK:
;
PARAMSIZE       .EQU    22                      ;TOTAL BYTES OF PARAMS
SRCBITS         .EQU    PARAMSIZE+8-4           ;LONG, ADDR OF BITMAP
DSTBITS         .EQU    SRCBITS-4               ;LONG, ADDR OF BITMAP
SRCRECT         .EQU    DSTBITS-4               ;LONG, ADDR OF RECT
DSTRECT         .EQU    SRCRECT-4               ;LONG, ADDR OF RECT
MODE            .EQU    DSTRECT-2               ;WORD
MASKRGN         .EQU    MODE-4                  ;LONG, RGNHANDLE


        LINK    A6,#0                           ;NO LOCAL VARS
        MOVEM.L D6-D7/A2-A4,-(SP)               ;SAVE REGS
        MOVE.L  GRAFGLOBALS(A5),A4              ;POINT TO QUICKDRAW GLOBALS
        MOVE.L  THEPORT(A4),A3                  ;POINT TO CURRENT GRAFPORT
        MOVE.L  SRCBITS(A6),A2                  ;POINT TO SRCBITS
        MOVE.L  BASEADDR(A2),D7                 ;GET SRCBITS.BASEADDR
        MOVE.L  SCREENBITS+BASEADDR(A4),D6      ;GET SCREENBITS.BASEADDR
        CMP.L   D6,D7                           ;IS SRC FROM THE SCREEN ?
        BNE.S   SRCOK                           ;NO, CONTINUE
        MOVE.L  SRCRECT(A6),-(SP)               ;YES, PUSH SRCRECT
        MOVE.L  BOUNDS+TOPLEFT(A2),-(SP)        ;PUSH OFFSET POINT
        JSR     SHIELDCURSOR                    ;HIDE THE CURSOR IF IN SRCRECT

;-----------------------------------------------------
;
;  TEST IF DST IS TO THEPORT, (IF SO WE CLIP)
;
SRCOK   MOVE.L  DSTBITS(A6),A1                  ;POINT TO DSTBITS
        MOVE.L  PORTBITS+BASEADDR(A3),D0        ;GET PORTBITS.BASEADDR
        CMP.L   BASEADDR(A1),D0                 ;IS DST BASEADDR SAME ?
        BNE.S   NOTPORT                         ;NO, NOT TO THEPORT
        MOVE.L  PORTBOUNDS(A3),D0               ;GET PORT BOUNDS TOPLEFT
        CMP.L   BOUNDS(A1),D0                   ;IS BOUNDS TOPLEFT SAME ?
        BEQ.S   TOPORT                          ;YES, ITS PROBABLY TO THEPORT
;
;  DST IS DEFINITELY NOT TO THEPORT, SO WE CAN'T USE THE CAPTURE PROC.
;  StretchBits(srcBits,dstBits,srcRect,dstRect,mode,wideOpen,wideOpen,maskRgn);
;
NOTPORT MOVE.L  A2,-(SP)                        ;PUSH SRCBITS
        MOVE.L  A1,-(SP)                        ;PUSH DSTBITS
        MOVE.L  SRCRECT(A6),-(SP)               ;PUSH SRCRECT
        MOVE.L  DSTRECT(A6),-(SP)               ;PUSH DSTRECT
        MOVE    MODE(A6),-(SP)                  ;PUSH MODE
        MOVE.L  WIDEOPEN(A4),-(SP)              ;PUSH WIDEOPEN
        MOVE.L  (SP),-(SP)                      ;PUSH WIDEOPEN
        MOVE.L  MASKRGN(A6),-(SP)               ;PUSH MASKRGN
        BNE.S   MASKOK                          ;WAS IT NIL ?
        MOVE.L  WIDEOPEN(A4),(SP)               ;YES, REPLACE WITH WIDEOPEN
MASKOK  JSR     STRETCHBITS                     ;CALL STRETCHBITS
        BRA.S   CLEANUP                         ;AND CONTINUE

;
;  DST IS PROBABLY TO THEPORT, SO WE USE THE CAPTURE PROC.
;  CallBits(srcBits,srcRect,dstRect,mode,maskRgn)
;
TOPORT  MOVE.L  A2,-(SP)                        ;PUSH SRCBITS
        MOVE.L  SRCRECT(A6),-(SP)               ;PUSH SRCRECT
        MOVE.L  DSTRECT(A6),-(SP)               ;PUSH DSTRECT
        MOVE    MODE(A6),-(SP)                  ;PUSH MODE
        MOVE.L  MASKRGN(A6),-(SP)               ;PUSH MASKRGN (NIL OK)
        MOVE.L  GRAFPROCS(A3),D0                ;IS GRAFPROCS NIL ?
        LEA     STDBITS,A0
        BEQ.S   USESTD                          ;YES, USE STD PROC
        MOVE.L  D0,A0
        MOVE.L  BITSPROC(A0),A0                 ;NO, GET PROC PTR
USESTD  JSR     (A0)                            ;CALL IT

CLEANUP CMP.L   D6,D7                           ;WAS SRC FROM THE SCREEN ?
        BNE.S   DONE                            ;NO, CONTINUE
        JSR     SHOWCURSOR                      ;YES, REPLACE CURSOR
DONE    MOVEM.L (SP)+,D6-D7/A2-A4               ;RESTORE REGS
        UNLINK  PARAMSIZE,'COPYBITS'



        .PROC ScrollRect,4
        .REF  NewRgn,RectRgn,SectRgn,CopyRgn,OffsetRgn,SetEmptyRgn
        .REF  DiffRgn,ShieldCursor,RgnBlt,ShowCursor,DisposeRgn
;---------------------------------------------------------------------
;
;  PROCEDURE ScrollRect(dstRect: Rect; dh,dv: INTEGER; updateRgn: RgnHandle);
;
;  Scroll a rectangular block of bits, erase and return an update region
;
;  A6 OFFSETS OF PARAMS AND LOCALS AFTER LINK:
;
PARAMSIZE       .EQU    12                      ;TOTAL BYTES OF PARAMS
DSTRECT         .EQU    PARAMSIZE+8-4           ;LONG, ADDR OF RECT
DH              .EQU    DSTRECT-2               ;WORD
DV              .EQU    DH-2                    ;WORD
UPDATERGN       .EQU    DV-4                    ;LONG, RGNHANDLE

SRCRECT         .EQU    -8                      ;RECT
VARSIZE         .EQU    SRCRECT                 ;TOTAL LOCALS


        LINK    A6,#VARSIZE                     ;ALLOCATE STACK FRAME
        MOVEM.L D6-D7/A3-A4,-(SP)               ;SAVE REGS
        MOVE.L  GRAFGLOBALS(A5),A4              ;POINT TO QUICKDRAW GLOBALS
        MOVE.L  THEPORT(A4),A3                  ;POINT TO CURRENT GRAFPORT
        TST     PNVIS(A3)                       ;IS PNVIS < 0 ?
        BLT     ABORT                           ;YES, QUIT FAST
        TST.L   DV(A6)                          ;ARE DH AND DV BOTH 0 ?
        BEQ     ABORT                           ;YES, QUIT FAST

        CLR.L   -(SP)                           ;ROOM FOR FCN RESULT
        JSR     NEWRGN                          ;ALLOCATE SRCRGN
        MOVE.L  (SP)+,D7                        ;GET SRCRGN HANDLE IN D7
        CLR.L   -(SP)                           ;ROOM FOR FCN RESULT
        JSR     NEWRGN                          ;ALLOCATE DSTRGN
        MOVE.L  (SP)+,D6                        ;GET DSTRGN HANDLE IN D6
;
;  srcRgn := dstRect SECT visRgn SECT clipRgn
;
        MOVE.L  D7,-(SP)                        ;PUSH SRCRGN
        MOVE.L  DSTRECT(A6),-(SP)               ;PUSH DSTRECT
        JSR     RECTRGN                         ;RectRgn(srcRgn,dstRect);

        MOVE.L  D7,-(SP)                        ;PUSH SRCRGN
        MOVE.L  VISRGN(A3),-(SP)                ;PUSH VISRGN
        MOVE.L  D7,-(SP)                        ;PUSH SRCRGN
        JSR     SECTRGN                         ;SectRgn(srcRgn,visRgn,srcRgn);

        MOVE.L  D7,-(SP)                        ;PUSH SRCRGN
        MOVE.L  CLIPRGN(A3),-(SP)               ;PUSH CLIPRGN
        MOVE.L  D7,-(SP)                        ;PUSH SRCRGN
        JSR     SECTRGN                         ;SectRgn(srcRgn,clipRgn,srcRgn);
;
;  dstRgn := offset srcRgn
;
        MOVE.L  D7,-(SP)                        ;PUSH SRCRGN
        MOVE.L  D6,-(SP)                        ;PUSH DSTRGN
        JSR     COPYRGN                         ;CopyRgn(srcRgn,dstRgn);
        MOVE.L  D6,-(SP)                        ;PUSH DSTRGN
        MOVE.L  DV(A6),-(SP)                    ;PUSH DH,DV
        JSR     OFFSETRGN                       ;OffsetRgn(dstRgn,dh,dv);

        MOVE.L  DSTRECT(A6),A0                  ;POINT TO DSTRECT
        MOVE.L  (A0)+,SRCRECT(A6)               ;COPY DSTRECT INTO SRCRECT
        MOVE.L  (A0)+,SRCRECT+4(A6)
        MOVE.L  DV(A6),D0                       ;GET DH,DV
        SUB     D0,SRCRECT+LEFT(A6)             ;OFFSET SRCRECT (-DH,-DV)
        SUB     D0,SRCRECT+RIGHT(A6)
        SWAP    D0                              ;GET DV IN LO WORD
        SUB     D0,SRCRECT+TOP(A6)
        SUB     D0,SRCRECT+BOTTOM(A6)

;
;  Compute updateRgn := srcRgn - dstRgn
;
        MOVE.L  D7,-(SP)                        ;PUSH SRCRGN
        MOVE.L  D6,-(SP)                        ;PUSH DSTRGN
        MOVE.L  UPDATERGN(A6),-(SP)             ;PUSH UPDATERGN
        JSR     DIFFRGN                         ;DiffRgn(srcRgn,dstRgn,updateRgn);
;
;  ShieldCursor(dstRect,portBits.bounds.topLeft);  { protect source }
;
        MOVE.L  DSTRECT(A6),-(SP)                  ;PUSH DSTRECT
        MOVE.L  PORTBITS+BOUNDS+TOPLEFT(A3),-(SP)  ;PUSH OFFSET POINT
        JSR     SHIELDCURSOR                       ;REMOVE CRSR IF INTERSECTS
;
;  Copy all bits which are in both srcRgn and dstRgn.
;  RgnBlt(portBits,portBits,srcRect,dstRect,0,white,dstRgn,srcRgn,wideOpen);
;
        PEA     PORTBITS(A3)                    ;PUSH SRCBITS
        MOVE.L  (SP),-(SP)                      ;PUSH DSTBITS
        PEA     SRCRECT(A6)                     ;PUSH SRCRECT
        MOVE.L  DSTRECT(A6),-(SP)               ;PUSH DSTRECT
        CLR     -(SP)                           ;PUSH MODE = SRCCOPY
        CLR.L   -(SP)                           ;PAT NOT USED
        MOVE.L  D6,-(SP)                        ;PUSH DSTRGN
        MOVE.L  D7,-(SP)                        ;PUSH SRCRGN
        MOVE.L  WIDEOPEN(A4),-(SP)              ;PUSH WIDEOPEN
        JSR     RGNBLT                          ;COPY THE BITS
;
;  Erase the update region
;  RgnBlt(portBits,portBits,dstRect,dstRect,8,bkPat,updateRgn,wideOpen,wideOpen);
;
        PEA     PORTBITS(A3)                    ;PUSH SRCBITS
        MOVE.L  (SP),-(SP)                      ;PUSH DSTBITS
        MOVE.L  DSTRECT(A6),-(SP)               ;PUSH SRCRECT
        MOVE.L  (SP),-(SP)                      ;PUSH DSTRECT
        MOVE    #8,-(SP)                        ;PUSH MODE = PATCOPY
        PEA     BKPAT(A3)                       ;PUSH PAT = BKPAT
        MOVE.L  UPDATERGN(A6),-(SP)             ;PUSH UPDATERGN
        MOVE.L  WIDEOPEN(A4),-(SP)              ;PUSH WIDEOPEN
        MOVE.L  (SP),-(SP)                      ;PUSH WIDEOPEN
        JSR     RGNBLT                          ;ERASE THE UPDATE RGN

        JSR     SHOWCURSOR                      ;RESTORE THE CURSOR
        MOVE.L  D7,-(SP)                        ;PUSH SRCRGN
        JSR     DISPOSERGN                      ;DisPoseRgn(srcRgn);
        MOVE.L  D6,-(SP)                        ;PUSH DSTRGN
        JSR     DISPOSERGN                      ;DisPoseRgn(dstRgn);
        BRA.S   DONE

ABORT   MOVE.L  UPDATERGN(A6),-(SP)             ;PUSH UPDATERGN HANDLE
        JSR     SETEMPTYRGN                     ;SET IT TO EMPTY

DONE    MOVEM.L (SP)+,D6-D7/A3-A4               ;RESTORE REGS
        UNLINK  PARAMSIZE,'SCROLLREC'




          .PROC PackBits
;---------------------------------------------------------------------
;
;  PROCEDURE PackBits(VAR srcPtr,dstPtr: Ptr; srcBytes: INTEGER);
;
;  Packs one scanline of data, compressing equal bytes.
;  Returns updated srcPtr and dstPtr.
;
PARAMSIZE       .EQU    10                      ;TOTAL BYTES OF PARAMS
SRCPTR          .EQU    PARAMSIZE+8-4           ;LONG, VAR
DSTPTR          .EQU    SRCPTR-4                ;LONG,VAR
SRCBYTES        .EQU    DSTPTR-2                ;WORD

        LINK    A6,#0                           ;NO LOCALS
        MOVE.L  A2,-(SP)                        ;SAVE REGS
        MOVE.L  SRCPTR(A6),A0                   ;GET VAR ADDR
        MOVE.L  (A0),A0                         ;GET SRCPTR ITSELF
        MOVE.L  DSTPTR(A6),A1                   ;GET VAR ADDR
        MOVE.L  (A1),A1                         ;GET DSTPTR ITSELF
        MOVE    SRCBYTES(A6),D1                 ;GET SRCBYTES
        SUB     #1,D1                           ;INIT SRC DBRA COUNT
        BLT.S   DONE                            ;QUIT IF SRCBYTES <= 0
        MOVE.B  (A0)+,D0                        ;GET FIRST BYTE OF SRC
        BRA.S   START                           ;AND GO TO LOOP START

FILLOP  MOVE.B  -1(A0),D0                       ;PUT SRCDATA IN DO
        SUB     A0,D2                           ;COMPUTE FILLOP
        ADD     #1,D2
        MOVE.B  D2,(A2)                         ;STORE FILLOP

START   MOVE    A0,D2                           ;REMEMBER SRCSTART
        MOVE.L  A1,A2                           ;REMEMBER OPLOC
        CLR.B   (A1)+                           ;RESERVE ROOM FOR OPCODE
        CMP.B   (A0),D0                         ;IS NEXT SRC THE SAME ?
        BNE.S   DOCOPY                          ;NO, USE COPY LOOP
        CMP.B   1(A0),D0                        ;THREE IN A ROW ?
        BNE.S   DOCOPY                          ;NO, USE COPY LOOP
        BRA.S   DOFILL                          ;YES, USE FILL LOOP

NXTCOPY MOVE.B  (A0)+,D0                        ;GET BYTE OF SRC
        CMP.B   (A0),D0                         ;IS THE NEXT THE SAME ?
        BNE.S   DOCOPY                          ;NO, CONTINUE
        CMP.B   1(A0),D0                        ;THREE IN A ROW ?
        BEQ.S   COPYOP                          ;YES, END OF COPY RUN
DOCOPY  MOVE.B  D0,(A1)+                        ;COPY DATA TO DST
        DBRA    D1,NXTCOPY                      ;AND LOOP FOR MORE

ENDCOPY SUB     A0,D2                           ;COMPUTE COPYOP
        NEG     D2                              ;NEGATE ONLY
        MOVE.B  D2,(A2)                         ;INSTALL COPYOP
        BRA.S   DONE                            ;AND QUIT

COPYOP  TST     D1                              ;IS THIS THE LAST SRC BYTE ?
        BEQ     DOCOPY                          ;YES, FINISH OFF COPY RUN
        SUB     A0,D2                           ;COMPUTE COPYOP
        NOT     D2                              ;NEGATE AND SUBTRACT 1
        MOVE.B  D2,(A2)                         ;STORE COPYOP
        MOVE.L  A0,D2                           ;REMEMBER SRCSTART
        MOVE.L  A1,A2                           ;REMEMBER OPLOC
        CLR.B   (A1)+                           ;RESERVE ROOM FOR OPCODE

DOFILL  MOVE.B  D0,(A1)+                        ;COPY THE DATA BYTE
NXTFILL CMP.B   (A0)+,D0                        ;IS NEXT BYTE THE SAME ?
        DBNE    D1,NXTFILL                      ;LOOP TILL NOT SAME OR END SRC
        BEQ.S   ENDFILL                         ;BRANCH IF SRC EXHAUSTED
        SUB     #1,D1                           ;COMPENSATE FOR DBNE DIDNT SUB
        BGE     FILLOP                          ;BR IF SRC NOT EXHAUSTED

ENDFILL SUB     A0,D2                           ;COMPUTE FILLOP
        ADD     #1,D2
        MOVE.B  D2,(A2)                         ;INSTALL FILLOP

DONE    CLR.L   D0                              ;GET READY FOR WORD
        MOVE    SRCBYTES(A6),D0                 ;GET SRCBYTES
        MOVE.L  SRCPTR(A6),A0                   ;GET VAR ADDR OF SRCPTR
        ADD.L   D0,(A0)                         ;BUMP SRCPTR
        MOVE.L  DSTPTR(A6),A2                   ;GET VAR ADDR OF DSTPTR
        MOVE.L  A1,(A2)                         ;UPDATE DSTPTR
        MOVE.L  (SP)+,A2                        ;RESTORE REGS
        UNLK    A6                              ;RELEASE STACK FRAME
        MOVE.L  (SP)+,A0                        ;POP RETURN ADDR
        ADD     #PARAMSIZE,SP                   ;STRIP PARAMS
        JMP     (A0)                            ;AND RETURN


          .PROC UnpackBits
;--------------------------------------------------------
;
;  PROCEDURE UnpackBits(VAR srcPtr,dstPtr: Ptr; dstBytes: INTEGER);
;
;  Unpacks one scanline of data, as compressed by PackBits.
;  Returns updated srcPtr and dstPtr.
;
PARAMSIZE       .EQU    10                      ;TOTAL BYTES OF PARAMS
SRCPTR          .EQU    PARAMSIZE+8-4           ;LONG, VAR
DSTPTR          .EQU    SRCPTR-4                ;LONG, VAR
DSTBYTES        .EQU    DSTPTR-2                ;WORD

        LINK    A6,#0                           ;ALLOCATE STACK FRAME
        MOVE.L  SRCPTR(A6),A0                   ;GET ADDR OF SRCPTR
        MOVE.L  (A0),A0                         ;GET SRCPTR ITSELF
        MOVE.L  DSTPTR(A6),A1                   ;GET ADDR OF DSTPTR
        MOVE.L  (A1),A1                         ;GET DSTPTR ITSELF
        MOVE    DSTBYTES(A6),D2                 ;GET DSTBYTES
        EXT.L   D2                              ;MAKE IT LONG
        ADD.L   A1,D2                           ;LIMIT := DSTPTR + BYTECOUNT
        BRA.S   GETOP                           ;GO TO LOOP START

DOCOPY  EXT.W   D1                              ;CLEAR HI BYTE OF COUNT
NXTCOPY MOVE.B  (A0)+,(A1)+                     ;COPY A BYTE OF DATA
        DBRA    D1,NXTCOPY                      ;LOOP ALL COPY BYTES

GETOP   CMP.L   D2,A1                           ;IS DSTPTR >= LIMIT ?
        BHS.S   DONE                            ;YES, WE'RE DONE
        MOVE.B  (A0)+,D1                        ;NO, GET  OPCODE
        BPL     DOCOPY                          ;0..127 --> COPY 1..128 BYTES
        NEG.B   D1                              ;-1..-127 --> FILL 2..128 BYTES
        BVS.S   GETOP                           ;IGNORE $80 FOR BACKWARD COMPAT

DOFILL  EXT.W   D1                              ;CLEAR HI BYTE OF COUNT
        MOVE.B  (A0)+,D0                        ;GET FILL DATA BYTE
NXTFILL MOVE.B  D0,(A1)+                        ;COPY IT TO DST
        DBRA    D1,NXTFILL                      ;LOOP ALL FILL BYTES
        BRA     GETOP                           ;THEN GET NEXT OPCODE

DONE    MOVE.L  A0,D0                           ;STASH SRCPTR
        MOVE.L  SRCPTR(A6),A0                   ;GET VAR ADDR
        MOVE.L  D0,(A0)                         ;UPDATE VAR SRCPTR
        MOVE.L  DSTPTR(A6),A0                   ;GET VAR ADDR
        MOVE.L  A1,(A0)                         ;UPDATE VAR DSTPTR
        UNLK    A6                              ;RELEASE STACK FRAME
        MOVE.L  (SP)+,A0                        ;POP RETURN ADDR
        ADD     #PARAMSIZE,SP                   ;STRIP PARAMS
        JMP     (A0)                            ;AND RETURN




        .END



ÿ