;-----------------------------------------------------------
; 05JUL82: WRITTEN BY JEROME COONEN
; 27APR83: NEGATE, ABS, COPYSIGN ADDED. (JTC)
; 02MAY83: NEXTAFTER ADDED. (JTC)
; 04MAY83: SETXCP ADDED. (JTC)
; 09JUN83: A5,A6 PRESERVED.
; 09JUL83: ENTRY/EXIT, TESTXCP ADDED. (JTC)
;
; FOR CONVENIENCE, MOVE DST->A1, SRC->A2 TO HAVE POINTERS.
;
; JUMP TO MISCELLANEOUS ROUTINE BASED ON INDEX IN OPCODE IN
; D6.  DEPEND ON REGISTER MASK: ODDBALLS WITH STATE POINTER
; IN A0 AND ONE OPERAND ADDRESS IN A1.
; AT END, MUST JUMP TO FINISHUP SEQUENCES POPX, AS
; APPROPRIATE.
;-----------------------------------------------------------

ODDBALL:
        MOVEM.L LKADR1(A6),A1-A2        ; GET DST, SRC ADRS

        .IF PCOK
        MOVE.W  ODDTAB(PC,D7),D7
        JMP     ODDBALL(PC,D7)
        .ELSE
        MOVE.W  ODDTAB(D7),D7
        JMP     ODDBALL(D7)
        .ENDC

;-----------------------------------------------------------
; JUMP FROM INDEX-1, AFTER CHECK FOR LEGITIMACY.
;-----------------------------------------------------------
ODDTAB:
        .WORD   PUTW - ODDBALL   ; PUT STATE WORD
        .WORD   GETW - ODDBALL   ; GET STATE WORD
        .WORD   PUTV - ODDBALL   ; PUT TRAP VECTOR
        .WORD   GETV - ODDBALL   ; GET TRAP VECTOR
        .WORD   D2B  - ODDBALL   ; DECIMAL TO BINARY
        .WORD   B2D  - ODDBALL   ; BINARY TO DECIMAL
        .WORD   NEGZ - ODDBALL   ; NEGATE -- ANY FORMAT
        .WORD   ABSZ - ODDBALL   ; ABS -- ANY FORMAT
        .WORD   CPSZ - ODDBALL   ; COPY SIGN -- ANY FORMAT
        .WORD   NEXTZ - ODDBALL  ; NEXTAFTER: S, D, X
        .WORD   SETXCP - ODDBALL ; SET EXCEPTION, HALT IF...
        .WORD   ENTRYP - ODDBALL ; ENTRY PROTOCOL
        .WORD   EXITP  - ODDBALL ; EXIT PROTOCOL
        .WORD   TESTXCP- ODDBALL ; TEST AN EXCEPTION.

;-----------------------------------------------------------
; THE STATE ROUTINES ARE TRIVIAL, AND ALL "RETURN" TO POP1.
;-----------------------------------------------------------
PUTW:
        MOVE.W  (A1),(A0)
        BRA.S   GO1
ENTRYP:
        MOVE.W  (A0),(A1)
        CLR.W   (A0)
        BRA.S   GO1
GETW:
        MOVE.W  (A0),(A1)
        BRA.S   GO1
PUTV:
        MOVE.L  (A1),2(A0)
        BRA.S   GO1
GETV:
        MOVE.L  2(A0),(A1)
        BRA.S   GO1
NEGZ:
        BCHG    #7,(A1)
        BRA.S   GO1

ABSZ:
        BCLR    #7,(A1)
GO1:
        BRA     POP1

;-----------------------------------------------------------
; TEST AN EXCEPTION WHOSE INDEX IS (A1).  SET BYTE (A1) TO
; 1 (TRUE) IF THE EXCEPTION IS SET, SET IT TO 0 (FALSE) IF
; N0T SET.
;-----------------------------------------------------------
TESTXCP:
        MOVE.W  (A1),D0         ; FETCH INPUT INDEX
        BTST    D0,(A0)         ; EXCEPTION BITS IN HI BYTE
        SNE     D0
        NEG.B   D0
        MOVE.B  D0,(A1)         ; RESULT CODE
        BRA.S   GO1


;-----------------------------------------------------------
; NOTE THAT COPYSIGN COPIES THE SIGN OF THE "DST" ARGUMENT
; ONTO THAT OF THE "SRC" ARGUMENT.
;-----------------------------------------------------------
CPSZ:
        BTST    #7,(A1)
        BEQ.S   @1

        BSET    #7,(A2)
        BRA.S   @2
@1:
        BCLR    #7,(A2)
@2:
        BRA     POP2



;-----------------------------------------------------------
; NEXTAFTER FUNCTION: BEHAVES LIKE NONARITHMETIC OPS, BUT
; MAY SET ERROR FLAGS, SO EXITS THROUGH CHKERR RATHER THAN
; POP2.  CALLS REFP68K FOR COMPARE, MULTIPLY (NAN PRECEDENCE),
; CLASS, AND CONVERT.
; NOTE THAT NEXTAFTER CHANGES ITS *****SOURCE***** ARGUMENT
; IN THE DIRECTION OF THE DESTINATION ARGUMENT.
;-----------------------------------------------------------
NEXTZ:
;-----------------------------------------------------------
; ON ENTRY, D6.W IS OPCODE, A1 IS DST, A2 IS SRC, A0 STATE.
; USE OTHER REGISTERS FREELY, BUT MUST ALIGN OPWORD IN D6.HI
; WITH PROPER MASK FOR EXIT THROUGH CHKERR.  D6.L0 WILL HOLD
; ERROR FLAGS.  MASK OF OPERAND FORMAT BITS INTO D5 FOR USE
; AS MASK FOR CALLING CONVERSION AND INC/DEC ROUTINES.
; STACK FRAME = (A4) = DST-EXT; SRC-EXT; INT
;-----------------------------------------------------------
        SUBA.W  #22,SP  ; NEED 2 EXTENDEDS AND 1 INTEGER
        MOVEA.L SP,A4   ; FRAME POINTER THROUGHOUT WHOLE FCN

        MOVE.W  D6,D5   ; COPY OPCODE
        ANDI.W  #OPFOMASK,D5    ; ISOLATE FORMAT BITS
        ADDQ.W  #1,D6   ; SET #TWOADRS BIT CHEAPLY
        SWAP    D6      ; ALIGN IN HI WORD, LIKE ARITH OPS
        CLR.W   D6      ; ZERO FLAG AND SIGN BITS

;-----------------------------------------------------------
; CONVERT SRC TO EXTENDED
;-----------------------------------------------------------
        MOVE.L  A2,-(SP)        ; SRC OPERAND ADDRESS
        PEA     10(A4)          ; STACK FRAME ADDRESS
        MOVEQ   #OP2EXT,D0      ; CONVERT TO EXT OPCODE
        OR.W    D5,D0           ; ...WITH FORMAT
        MOVE.W  D0,-(SP)
        BSR     REFP68K

;-----------------------------------------------------------
; COMPARE SRC WITH ZERO.  IF IT'S EQUAL, ADJUSTMENTS WILL
; BE MADE IN DECREMENT ROUTINES BELOW.
;-----------------------------------------------------------
        CLR.L   (A4)
        CLR.L   4(A4)
        CLR.W   8(A4)

        PEA     (A4)
        PEA     10(A4)
        MOVE.W  #OPCMP,-(SP)
        BSR     REFP68K

        SNE     D4      ; D4.BYTE IS 1'S IF SRC IS ZERO

;-----------------------------------------------------------
; CONVERT DST TO EXTENDED
;-----------------------------------------------------------
        MOVE.L  A1,-(SP)
        PEA     (A4)
        MOVE.W  D0,-(SP)
        BSR     REFP68K

;-----------------------------------------------------------
; COMPARE THE TWO EXTENDED OPERANDS
;-----------------------------------------------------------
        PEA     (A4)    ; DST OPERAND
        PEA     10(A4)  ; SRC OPERAND
        MOVE.W  #OPCMP,-(SP)
        BSR     REFP68K

;-----------------------------------------------------------
; IF OVERFLOW IS SET, THE OPERANDS ARE UNORDERED, THAT IS,
; ONE OF THEM IS A NAN.  USE THE MULTIPLY OPERATION TO FORCE
; THE PRECEDENT NAN (IF THERE ARE TWO) TO THE SRC
;-----------------------------------------------------------
        BVC.S   NXORD

        PEA     (A4)    ; DST OPERAND
        PEA     10(A4)  ; SRC OPERAND
        MOVE.W  #OPMUL,-(SP)
        BSR     REFP68K

;-----------------------------------------------------------
; NOW CONVERT THE PRECEDENT NAN BACK TO INPUT FORMAT.
;-----------------------------------------------------------
        PEA     10(A4)          ; SRC OPERAND IS OUTPUT
        MOVE.L  A2,-(SP)        ; SRC ADDRESS
        MOVEQ   #OPEXT2,D0      ; CVT FROM EXT OPCODE
        OR.W    D5,D0           ; OVERLAY THE FORMAT
        MOVE.W  D0,-(SP)
        BSR     REFP68K
        BRA     NXFIN

;-----------------------------------------------------------
; GET HERE IF THE TWO OPERANDS ARE ORDERED.  IF THEY ARE
; EQUAL, THERE IS NOTHING TO DO; OTHERWISE MUST INC OR DEC
; THE SRC OP AS APPROPRIATE.  NOTE THE ONE *****FUNNY*****
; CASE: IF THE SRC IS ZERO, THEN ITS SIGN MAY BE MISLEADING.
; FOR INSTANCE, NEXT(-0, 3) SHOULD BE +0INC1.  BUT THE MINUS
; SIGN ON 0 CAUSES A DEC TO BE ISSUED INSTEAD.  THE FIX IS
; TO MAKE DEC SMART ENOUGH TO KNOW THAT IF 0 IS DEC-ED, THE
; SIGN SHOULD BE FLIPPED AND THE OPERAND SHOULD BE INC-ED
; INSTEAD.
;-----------------------------------------------------------
NXORD:
        BEQ.S   NXFIN
        BCC.S   NXGREAT

;-----------------------------------------------------------
; GET HERE WHEN SRC < DST.  INC IF SRC IS +, DEC IF -
;-----------------------------------------------------------
        BTST    #7,(A2)         ; SIGN BIT OF SRC OPERAND
        BEQ.S   NXINC
        BRA.S   NXDEC

;-----------------------------------------------------------
; GET HERE WHEN SRC > DST.  DEC IF SRC IS +, INC IF -
;-----------------------------------------------------------
NXGREAT:
        BTST    #7,(A2)
        BEQ.S   NXDEC

;-----------------------------------------------------------
; INCREMENT BY A UNIT IN THE LAST PLACE, ACCORDING TO THE
; FORMAT MASK IN D5.  THE FORMAT IS IN BITS $3800.  THE ONLY
; POSSIBLE CASES ARE:
;       $1000 -- SINGLE
;       $0800 -- DOUBLE
;       $0000 -- EXTENDED
;-----------------------------------------------------------
NXINC:
;-----------------------------------------------------------
; SINGLE CASE:
;-----------------------------------------------------------
        BTST    #SRCMD,D5       ; TEST $1000 BIT
        BEQ.S   @11

        ADDQ.L  #1,(A2)
        BRA.S   NXERR

;-----------------------------------------------------------
; DOUBLE CASE:
;-----------------------------------------------------------
@11:
        BTST    #SRCLO,D5       ; TEST $0800 BIT
        BEQ.S   @15

        ADDQ.L  #1,4(A2)
        BCC.S   @13
        ADDQ.L  #1,(A2)
@13:
        BRA.S   NXERR

;-----------------------------------------------------------
; EXTENDED CASE: BE SURE OUTPUT INFINITY HAS LEADING 0 BIT.
;-----------------------------------------------------------
@15:
        ADDQ.L  #1,6(A2)
        BCC.S   NXERR
        ADDQ.L  #1,2(A2)
        BCC.S   NXERR
        ROXR    2(A2)
        ADDQ.W  #1,(A2)
        CMPI.W  #$7FFF,(A2)
        BEQ.S   @16
        CMPI.W  #$FFFF,(A2)
        BNE.S   NXERR
@16:
        BCLR    #7,2(A2)        ; FALL THROUGH TO NXERR

;-----------------------------------------------------------
; TEST FOR EXCEPTIONS ACCORDING TO IEEE.  NEXT(HUGE, INF)
; YIELDS INF WITH OVERFLOW AND INEXACT SIGNALED.
; NEXT(TINY, 0) YIELDS SOME DENORMAL WITH UNDERFLOW
; AND INEXACT.  JUST SET THE APPROPRIATE BITS IN D6.LO AND
; EXIT AS THOUGH A TRUE ARITHMETIC OPERATION.  THE FIRST
; STEP IS TO FIND THE CLASS OF THE INC/DEC-ED SRC OPERAND.
;-----------------------------------------------------------
NXERR:
        MOVE.L  A2,-(SP)
        PEA     20(A4)          ; ADDRESS OF INTEGER
        MOVEQ   #OPCLASS,D0
        OR.W    D5,D0
        MOVE.W  D0,-(SP)
        BSR     REFP68K

;-----------------------------------------------------------
; KILL THE SIGN OF THE CLASS RESULT AND PLACE IN REGISTER
; THE CODES ARE:
;       1       SNAN -- CAN'T HAPPEN
;       2       QNAN -- CAN'T HAPPEN
;       3       INF -- OVERFLOW AND INEXACT
;       4       ZERO -- UNDERFLOW AND INEXACT
;       5       NORMAL -- OK
;       6       DENORMAL -- UNDERFLOW AND INEXACT
;-----------------------------------------------------------
        MOVE.W  20(A4),D1
        BPL.S   @1

        NEG.W   D1
@1:

;-----------------------------------------------------------
; CHECK FOR INFINITE RESULT (WHICH MUST HAVE COME FROM FIN).
;-----------------------------------------------------------
        CMPI.W  #CLINF,D1
        BNE.S   @3

        ORI.W   #ERRWXO,D6      ; SET INEXACT AND OVERFLOW
        BRA.S   NXFIN
@3:
        CMPI.W  #CLNORM,D1
        BEQ.S   NXFIN

        ORI.W   #ERRWXU,D6      ; SET INEXACT AND UNDERFLOW

;-----------------------------------------------------------
; EXIT THROUGH POINT IN FPCONTROL AFTER CLEANING STACK
;-----------------------------------------------------------
NXFIN:
        ADDA.W  #22,SP
        BRA     CHKERR

;-----------------------------------------------------------
; DECREMENT, WATCHING FOR ZERO VALUE.  BRANCH TREE IS LIKE
; THAT OF INC ABOVE.
;-----------------------------------------------------------
NXDEC:
        BTST    #SRCMD,D5       ; CHECK $1000 BIT FOR SINGLE
        BEQ.S   @21

        TST.B   D4              ; D4.B IS NON0 IF OPERAND IS
        BNE.S   @201

        BCHG    #7,(A2)
        ADDQ.L  #1,(A2)
        BRA.S   NXERR
@201:
        SUBQ.L  #1,(A2)
        BRA.S   NXERR

;-----------------------------------------------------------
; DOUBLE CASE
;-----------------------------------------------------------
@21:
        BTST    #SRCLO,D5       ; CHECK $0800 BIT FOR DOUBLE
        BEQ.S   @25

        TST.B   D4              ; D4.B IS NON0 IF OP IS
        BNE.S   @211

        BCHG    #7,(A2)
        ADDQ.W  #1,6(A2)
        BRA.S   NXERR
@211:
        SUBQ.L  #1,4(A2)
        BCC.S   @213

        SUBQ.L  #1,(A2)
@213:
        BRA.S   NXERR

;-----------------------------------------------------------
; EXTENDED CASE
;-----------------------------------------------------------
@25:
        TST.B   D4
        BNE.S   @251

        BCHG    #7,(A2)
        ADDQ.W  #1,8(A2)
        BRA.S   NXERR
@251:
        SUBQ.L  #1,6(A2)        ; DEC LOW LONG
        BCC.S   @259            ; NO C MEANS FINE
        SUBQ.L  #1,2(A2)
        BMI.S   @257            ; MAY HAVE BORROWED

        TST.W   (A2)            ; MIN EXP?
        BEQ.S   @259            ; YES --> DONE
        CMPI.W  #$8000,(A2)
        BEQ.S   @259

        ADDI.W  #$8000,2(A2)
        BRA.S   @258
@257:
        BCC.S   @259            ; NO CARRY --> DONE
@258:
        SUBQ.W  #1,(A2)
@259:
        BRA.S   NXERR


;-----------------------------------------------------------
; SET EXCEPTION AND HALT IF ENABLED.  SIMPLY SET THE
; SUITABLE BIT IN THE BYTE MASK $00001F00 IN D6 AND EXIT
; THROUGH FPCONTROL, AS THOUGH ARITHMETIC WERE PERFORMED.
;-----------------------------------------------------------
SETXCP:
        SWAP    D6      ; ALIGN IN HI WORD, LIKE ARITH OPS
        CLR.W   D6      ; ZERO FLAG AND SIGN BITS
        MOVE.W  (A1),D0         ; FETCH INPUT WORD INDEX
        ADDQ.W  #8,D0           ; ALIGN TO SECOND BYTE
        BSET    D0,D6
        BRA.S   EPEXIT          ; EXIT THROUGH FPCONTROL

;-----------------------------------------------------------
; RESTORE OLD ENVIRONMENT, AND CHECK CURRENT ERRS FOR HALT
;-----------------------------------------------------------
EXITP:
        SWAP    D6              ; ALIGN OPWORD, LIKE ARITH
        MOVE.W  #$1F00,D6       ; SET UP FLAG MASK #ERRO
                                ; #ERRU #ERRX #ERRI #ERRZ
        AND.W   (A0),D6         ; SAVE CURRENT ERRORS
        MOVE.W  (A1),(A0)       ; RESTORE OLD STATE
EPEXIT:
        BRA     CHKERR          ; EXIT VIA FPCONTROL

ÿ