; Copyright 1983, Apple Computer Inc.


;       File L:X80Misc, Version 25 May 1983.

; 24 May 83 - Fix bug in X80_BREAK that reversed directed roundings.
        
        .proc   i64%arith
        
        .def    i64neg,fp%i64
        
;       procedure i64neg ( var x, z : int64 ) ; external ;
;       z := -x

i64neg
        debugbegin
                        ; 0(sp) contains return address
                        ; 4(sp) contains address of z
                        ; 8(sp) contains address of x
                        ; 12(sp) = (old sp)
        movea.l 8(sp),a0        ; A0 gets address of x.
        movea.l 4(sp),a1        ; A1 gets address of z.
        move.l  (a0)+,(a1)+     ; z gets x.
        move.l  (a0)+,(a1)+
        neg.l   -(a1)           ; Negate lower (z).
        negx.l  -(a1)           ; Continue negate.
        movea.l (sp)+,a0        ; A0 gets return address.
        addq.l  #8,sp           ; sp = old sp.
        jmp     (a0)            ; Return.
        debugend 'I64NEG',2
        
; procedure fp%i64 ( var fp : fpregister ; var z : int64 ) ;
;               z := fp.
;       fp is expected to be rounded to an integral value, nonzero,
;       and with 1 <= exponent <= 62.

fp%i64
        debugbegin
        movea.l 8(sp),a0        ; A0 gets address of fp.
        move.l  fraction(a0),d0 ; D0 gets upper(significand).
        move.l  flsw(a0),d1     ; D1 gets lower(significand).
        move    #63,d2          ; D2 gets 63.
        sub     exponent(a0),d2 ; D2 gets right shift count = 63-exponent.
        cmp     #31,d2
        ble.s   rsle31
                        ; Right shift count >= 32 so shift 32.
        exg     d0,d1           ; D0 should get 0 because fp was integral.
        subi    #32,d2          ; Adjust right shift count.
rsle31                  ; Right shift count <= 31.
        cmpi    #15,d2          
        ble.s   rsle15
                        ; Right shift count >= 16 so shift 16.
        swap    d0              ; D0 gets (x w).
        clr.w   d1              ; D1 gets (y 0).
        swap    d1              ; D1 gets (0 y).
        eor     d0,d1           ; Swap w and y so
        eor     d1,d0           ; D0 gets (x y).
        eor     d0,d1           ; D1 gets (0 w).
        exg     d0,d1           ; D0 gets (0 w), D1 gets (x y).
        subi    #16,d2          ; Adjust right shift count.
rsle15                  ; Right shift count <= 15.
        move    #0,ccr          ; Clear X bit.
        bra.s   rstest
rightshift              ; Right shift D0 and D1 by one.
        roxr.l  #1,d0
        roxr.l  #1,d1
rstest                  ; Test if shift count = 0.
        dbf     d2,rightshift   
                        ; Now significand is right shifted.
        tst.b   sign(a0)
        beq.s   fpstore
                        ; Fp is negative so complement result.
        neg.l   d1
        negx.l  d0
fpstore                 ; Store result in z.
        movea.l 4(sp),a0        ; A0 gets address of z.
        move.l  d0,(a0)+        ; Store.
        move.l  d1,(a0)+
        move.l  (sp)+,a0        ; A0 gets return address.
        addq.l  #8,sp           ; Pass over parameter list.
        jmp     (a0)            ; Return.
        debugend 'fp%i64',2

        .proc   x80%misc

        .def    fp%normalize
        
;       procedure fp%normalize ( var fp : fpregister ) ; external ;
;       Normalizes fp.  Doesn't check for underflow. 

fp%normalize
        debugbegin
                        ; 0(sp) contains return address.
                        ; 4(sp) contains address of fp.
                        ; 8(sp) = (old sp)
        movea.l 4(sp),a0        ; A0 gets address of fp.
        cmp.b   #TagNonNormal,tag(a0)    ; Test tag of fp.
        bne.s   return          
                        ; Code below executed only for unnormalized fp.
checknorm
        move.l  flsw(a0),d1     ; D1 gets lower(fp).
        move.l  fraction(a0),d0 ; D0 gets upper(fp).
        bne.s   nonz32
                        ; fp is ( 0 0 ? ? ).
        tst.l   d1
        bne.s   nonz64
                        ; fp is unnormal zero.     
        move.b  #tagzero,tag(a0)        ; Tag fp as normal zero.
        bra.s   return
nonz64                          ; fp is ( 0 0 y z ).             
        exg     d0,d1           ; D0 is ( y z ), D1 is ( 0 0 ).
        sub.w   #32,exponent(a0) ; Adjust exponent.
nonz32                          ; fp is ( w x y z ) with w or x nonzero.
        swap    d0              ; D0 gets (x w).
        tst.w   d0             
        bne.s   nonz16
                        ; fp is ( 0 x y z ).
        swap    d1              ; D1 gets (z y).
        move.w  d1,d0           ; D0 gets ( x y ).
        clr.w   d1              ; D1 gets (z 0).
        sub.w   #16,exponent(a0) ; Adjust exponent.
        bra.s   testnorm
nonz16
        swap    d0              ; D0 gets (w x).
testnorm
        tst.l   d0
        bmi.s   normal
normloop                        ; Not normalized - shift once.
        subq.w  #1,exponent(a0) ; Adjust exponent.
        move    #0,ccr          ; Clear X bit.
        roxl.l  #1,d1           ; Shift left to normalize.
        roxl.l  #1,d0           ; Shift left.
        bpl.s   normloop
normal
        move.l  d0,fraction(a0) ; Restore fraction.
        move.l  d1,flsw(a0)
        move.b  #tagnormal,tag(a0) ; Tag FP as normalized.
return
        movea.l (sp)+,a0        ; A0 gets return address.
        addq.l  #4,sp           ; Bypass parameter.
        jmp     (a0)            ; Return.
        debugend 'fp%norma',0

; procedure fp%cnormalize ( var f : fp_register ) ;
; conditionally normalizes f if it is denormalized and we are in normalizing mode.
; otherwise f is not changed.

        .def    fp%cnormalize
fp%cnormalize
        debugbegin
        movea.l 4(sp),a0        ; A0 gets address of f.
        cmp     #extminxp,exponent(a0)
        bgt.s   return          ; Do nothing if exponent > minimum.
        move.l  fraction(a0),d0
        bmi.s   return          ; Do nothing if normalized.
        or.l    flsw(a0),d0
        beq.s   return          ; Do nothing if zero significand.
        getfpcb a1              ; Note this wipes out A0.
        btst    #mdnorm_m,fpmode(a1)
        beq.s   return          ; Do nothing in warning mode.
        movea.l 4(sp),a0        ; Restore a0.
        bra.s   checknorm
        debugend 'FP%CNORM',0
        
; function x80_integral ( var x : fp_extended ) : boolean
; returns true if x is an integral value or zero, false if not or inf or nan

        .def    x80_integral
x80_integral
        debugbegin
        movea.l (sp)+,a0        ; A0 gets return address.
        movea.l (sp)+,a1        ; A1 gets address of x.
        move    (a1),d0         ; D0 gets exponent and sign of x.
        lsl     #1,d0
        asr     #1,d0           ; Extend sign bit.
        cmpi    #$3fff,d0       
        bge.s   false           ; Not integral if inf or nan.
        cmpi    #63,d0
        bge.s   true            ; Exponent >= 63 implies integral.
        move.l  6(a1),d2        ; D2 gets least significant word (lsw) of x.
        cmpi    #31,d0
        blt.s   both            ; Exponent <= 30 implies check both words.
        bgt.s   checklsw        ; Exponent >= 32 implies check lsw only.
        tst.l   d2              ; Exponent = 32 implies integral = lsw=0.
        bra.s   decide
checklsw                ; 32 <= exponent <= 62.
        clr.l   d1
        neg     d0              ; D0 := -exponent.
        add     #63,d0          ; D0 := 63-exponent. 1 <= D0 <= 31.
        bset    d0,d1           ; D1 gets bit set corresponding to 2^0.
        subq.l  #1,d1           ; D1 gets mask for fraction bits.
        bra.s   andregs
both                    ; Exponent <= 30.
        tst.l   d2
        bne.s   false           ; Any bits on in lsw imply not integral.
        tst     d0
        bge.s   checkmsw        ; Exponent >= 0 implies check some msw bits.
        tst.l   2(a1)           ; Exponent <= -1 implies integral = msw=0.
        bra.s   decide
checkmsw                ; 0 <= Exponent <= 30.
        move.l  2(a1),d1        ; D1 gets msw.
        neg     d0
        add     #31,d0          ; D0 := 31-exponent.  1 <= D0 <= 31.
        bset    d0,d2           ; D2 gets 2^0 bit; rest of D2 is zero.
        subq.l  #1,d2           ; D2 gets mask for fraction bits.
andregs
        and.l   d1,d2           ; D2 gets fraction bits.
decide
        beq.s   true
false
        clr.b   (sp)            ; Not integral.
        jmp     (a0)            ; Return.
true
        move.b  #1,(sp)         ; Integral.
        jmp     (a0)            ; Return.
        debugend 'x80_inte',0
        
; procedure x80_break ( var x, intx, fracx : fp_extended ;
;                       var izero, fzero : boolean ) ;
; Caller guarantees x is finite.
; Returns normalized intx and fracx such that x = intx+fracx.
; Izero is true if intx is zero, in which case intx is undefined.
; Fzero is true if fracx is zero, in which case fracx is undefined.

        .def    x80_break

x80_break
        debugbegin
        link    a6,#0           ;  0(a6) contains old link.
                                ;  4(a6) contains return address.
                                ;  8(a6) contains address of fzero.
                                ; 12(a6) contains address of izero.
                                ; 16(a6) contains address of fracx.
                                ; 20(a6) contains address of intx.
                                ; 24(a6) contains address of x.
        movem.l d3-d7,-(sp)     ; Save registers.
        movea.l 24(a6),a0       ; A0 gets address of x.
        move    (a0),d0         ; D0 gets sign+exponent of x.
        move.l  2(a0),d4        ; D4 gets upper significand of x.
                                ; D4 will be upper(intx).
        move.l  d4,d6           ; D6 will be upper(fracx).
        move.l  6(a0),d5        ; D5 gets lower significand of x.
                                ; D5 will be lower(intx).
        move.l  d5,d7           ; D7 will be lower(fracx).
        lsl     #1,d0
        asr     #1,d0           ; D0 gets signed exponent of x.
        bmi     zint            ; exp <= -1 implies x is all fraction.
        cmpi    #63,d0
        bge.s   allint          ; exp >= 63 implies x is all integer.
        cmpi    #31,d0
        bgt.s   expgt31
        beq.s   expeq31
                        ; 0 <= exp <= 30 so lower part is all fraction.
        clr.l   d5              ; Lower(intx) is zero.
        neg     d0              ; D0 := -exponent.
        addi    #31,d0          ; D0 := 31-exponent. 1 <= D0 <= 31.
        clr.l   d1              ; Clear mask register.
        bset    d0,d1           ; Set bit corresponding to 2^0.
        subq.l  #1,d1           ; Set fraction bit mask.
        and.l   d1,d6           ; Remove integer bits from upper fracx.
        not.l   d1              ; Set integer bit mask.
        and.l   d1,d4           ; Remove fraction bits from upper intx.
        bra.s   donorm
        
expeq31                 ; exp = 31 so lower is all fraction, upper all integer.
        clr.l   d5              ; Clear lower intx.
        clr.l   d6              ; Clear upper fracx.
        bra.s   donorm
        
expgt31                 ; 32 <= exp <= 62 so upper part is all integer.
        clr.l   d6              ; Clear upper fracx.
        neg     d0
        addi    #63,d0          ; D0 := 63-exponent. 1 <= D0 <= 31.
        clr.l   d1              ; Clear mask register.
        bset    d0,d1           ; Set bit corresponding to 2^0.
        subq.l  #1,d1           ; Set fraction bit mask.
        and.l   d1,d7           ; Remove integer bits from lower fracx.
        not.l   d1              ; Set integer bit mask.
        and.l   d1,d5           ; Remove fraction bits from lower intx.
        bra.s   donorm

allint                  ; x is entirely integral.
        clr.l   d6              ; Clear fraction.
        clr.l   d7              ; Clear fraction.
        
donorm                  ; Normalize intx and fracx, checking for zeros.
        move    (a0),d0         ; D0 gets sign+exponent of x.
        move    d0,d1           ; D1 gets sign+exponent of x.
        andi    #$8000,d1       ; D1 gets sign of x.
        lsl     #1,d0
        asr     #1,d0           ; D0 gets signed exponent of x.
        tst.l   d5
        bne.s   @1
        tst.l   d4
        beq.s   zint
@1
        tst.l   d4
        bne.s   testinorm
        exg     d4,d5           ; Shift left by 32.
        subi    #32,d0          ; Modify exponent.
        tst.l   d4
        bra.s   testinorm
normint                 ; Normalization loop.
        subq    #1,d0
        lsl.l   #1,d5
        roxl.l  #1,d4
testinorm
        bge.s   normint
        movea.l 20(a6),a1       ; A1 gets address of intx.
        bclr    #15,d0          ; !BUG FIX - clear sign bit.
        or      d1,d0           ; D0 gets sign and exponent.
        move    d0,(a1)+        ; Store sign and exponent.
        move.l  d4,(a1)+
        move.l  d5,(a1)         ; Store significand.
        clr.b   d5              ; izero := false.
        bra.s   finint
zint                    ; Intx is zero.
        move.b  #1,d5           ; izero := true.
finint
        movea.l 12(a6),a1       ; A1 gets address of izero.
        move.b  d5,(a1)         ; Store Izero.
        
        move    (a0),d0         ; D0 gets sign+exponent of x.
        move    d0,d1           ; D1 gets sign+exponent of x.
        andi    #$8000,d1       ; D1 gets sign of x.
        lsl     #1,d0
        asr     #1,d0           ; D0 gets signed exponent of x.
        tst.l   d7
        bne.s   @1
        tst.l   d6
        beq.s   zfrac
@1
        tst.l   d6
        bne.s   testfnorm
        cmpi    #$c020,d0
        ble.s   normfrac        ; Don't normalize if underflow would result.   
        exg     d6,d7           ; Shift left by 32.
        subi    #32,d0          ; Modify exponent.
        tst.l   d6
        bra.s   testfnorm
normfrac                 ; Normalization loop.
        cmpi    #$c000,d0
        ble.s   donefnorm       ; Don't normalize if underflow would result.
        subq    #1,d0
        lsl.l   #1,d7
        roxl.l  #1,d6
testfnorm
        bge.s   normfrac
donefnorm
        movea.l 16(a6),a1       ; A1 gets address of fracx.
        bclr    #15,d0          ; !BUG FIX - clear sign bit.
        or      d1,d0           ; D0 gets sign and exponent.
        move    d0,(a1)+        ; Store sign and exponent.
        move.l  d6,(a1)+
        move.l  d7,(a1)         ; Store significand.
        clr.b   d7              ; fzero := false.
        bra.s   finfrac
zfrac                    ; Fracx is zero.
        move.b  #1,d7           ; fzero := true.
finfrac
        movea.l  8(a6),a1       ; A1 gets address of fzero.
        move.b  d7,(a1)         ; Store fzero.
        
        movem.l (sp)+,d3-d7     ; Restore registers.
        unlk    a6
        movea.l (sp)+,a0        ; A0 gets return address.
        adda.l  #20,sp          ; Skip parameters.
        jmp     (a0)            ; Return.
        debugend 'X80_BREA',0

ÿ