; Copyright 1983, Apple Computer Inc.
        
; File L:SubX48, Version 22 February 1983.

        .proc   sub%x48

        .def    %xcomp,%xmul,%xdiv,%xint,%xdec
        .def    %xpot

;       base conversion procedures

;       Routine %xcomp provides single extended compare for base conversion.
;       A is in D1 and D2.
;       B is in D4 and D5.   
;       D3 gets -1 if A<B, 0 if A=B, +1 if A>B.

%xcomp  
        debugbegin
        tst.l   d2
        beq.s   @4
        NORM    D2,D1
@5      tst.l   d5
        beq.s   @6
        NORM    D5,D4
@7      CMP     D1,D4
        BEQ.S   @1
@3      BlT.S   @2
        MOVEQ   #-1,D3
        RTS
@2      MOVEQ   #1,D3
        RTS
@1      CMP.L   D2,D5
        BNE.S   @3
        CLR     D3
        RTS
@4      move    #-256,d1
        bra.s   @5
@6      move    #-256,d4
        bra.s   @7
        debugend '%xcomp',2

;       Routine %xmul provides single extended multiply for base conversion.
;       D1, D2 contain A and get R := A*B.
;       D4,D5 contain B.
;       Uses:   D0-D7


%xmul   
        debugbegin
        ADD     D4,D1           ; D1 gets exponent of R.
        TST     D2
        BEQ.S   AISZERO              ; Branch if low order word of A is zero.
        TST     D5
        BEQ.S   BISZERO              ; Branch if low order word of B is zero.
        MOVE.L  D2,D6           ; D6 gets A.
        MOVE.L  D5,D7           ; D7 gets B.
        SWAP    D6              ; D6 gets swap(A).
        SWAP    D7              ; D7 gets swap(B).
        MOVE.L  D7,D4           ; D4 gets swap(B).
                        ; In what follows we compute
                        ; (AU+eAL)*(BU+eBL)
                        ; = AU*BU + (AL*BU+AU*BL)e + AL*BL ee
                        ; by
                        ; AU*BU + (AU*BU+AL*BL-(AU-AL)*(BU-BL))e + AL*BL ee
                        ; to save one multiplication.
        MULU    D6,D4           ; D4 gets AU*BU.
        MULU    D2,D5           ; D5 gets AL*BL.
        MOVE    #-1,D3          ; Use D3 as add/subtract flag: -1 means 
                                ; subtract ABS(AU-AL)*ABS(BU-BL).
                                ; ABS is necessary to avoid overflow.
        SUB     D2,D6           ; D6 gets AU-AL.
        BCC.S   @1
        NEG     D6              ; In case of borrow, reverse result.
        NEG     D3              ; Reverse flag.
@1      MOVE.L  D7,D2           ; D2 gets swap(B).
        SWAP    D2              ; D2 gets B.
        SUB     D2,D7           ; D7 gets BU-BL.
        BCC.S   @2              
        NEG     D7
        NEG     D3
@2      MULU    D6,D7           ; D7 gets product of ABS differences.
        MOVE.L  D4,D6           ; D6 gets AU*BU.
        CLR     D0              ; Use D0 as overflow accumulator.
        ADD.L   D5,D6           ; D6 gets AU*BU+AL*BL.
        ADDX    D0,D0
        TST     D3
        BMI.S   @3              ; Branch if subtract indicated.
        CLR     D3              ; Need a clear register for ADDX.
        ADD.L   D7,D6           ; D6 gets middle term.
        ADDX    D3,D0
        BRA.S   @4
@3      CLR     D3              ; Need a clear register for SUBX.
        SUB.L   D7,D6           ; D6 gets middle term.
        SUBX    D3,D0
@4      CLR.L   D7
        MOVE    D6,D7           ; D7 has low(middle term).
        SWAP    D7              ; Align for add.
        CLR     D6
        SWAP    D6              ; D6 has high(middle term).
        MOVE    D0,D2           ; D2 gets overflow bits.
        SWAP    D2              ; Position overflow bits in upper word.
        CLR     D2              ; Clear low(D2).
        ADD.L   D7,D5           ; D5 gets low order word of product.
        ADDX.L  D6,D2
        ADD.L   D4,D2           ; Add in high order word.
        BRA.S   MULNORM


AISZERO      SWAP    D2              ; D2 gets swap(A).
        TST     D5
        BEQ.S   QUICKIE         ; Test for AL=BL=0.
        MOVE.L  D5,D6           ; D6 gets B.
        SWAP    D6              ; D6 gets swap(B).
        MULU    D2,D5           ; D5 gets UA*LB.
        MULU    D6,D2           ; D2 gets UA*UB.
        BRA.S   JOIN0
BISZERO      SWAP    D5              ; D5 gets swap(B).
        MOVE.L  D2,D6           ; D6 gets A.
        SWAP    D2              ; D2 gets swap(A).
        MULU    D5,D2           ; D2 gets UA*UB.
        MULU    D6,D5           ; D5 gets UB*LA.
JOIN0   MOVE.L  D5,D7           ; D7 gets (U*L).
        SWAP    D5              ; D5 gets swap(U*L).
        CLR     D5              ; D5 gets [ low(U*L), 0 ].
        CLR     D7              ; D7 gets [ high(U*L), 0 ].
        SWAP    D7              ; D7 gets [ 0 up(U*L) ].
        ADD.L   D7,D2           ; D2 gets sum.
        BRA.S   MULNORM
QUICKIE SWAP    D5
        MULU    D5,D2
        CLR.L   D5
MULNORM BTST    #31,D2          ; Check most sig bit of R.
        BEQ.S   @5              ; Branch if normalize required.
        ADDQ    #1,D1           ; Increment exponent.
        BRA.S   @6
@5      LSL.L   #1,D5           ; Start shift to Normalize.
        ROXL.L  #1,D2           ; Finish shift.
@6
        tst.l   d5
        beq.s   return          ; Branch if result is exact.
        setex   xinex           ; Set inexact.
        CLR.L   D3              ; We need a clear register for ADDX.
        ADDI.L  #$80000000,D5   ; Do round.
        ADDX.L  D3,D2           ; Propagate carry.
        BCC.S   @1              ; Branch if no rounding carry overflow.
        BSET    #31,D2          ; Restore carried out bit.
        ADDQ    #1,D1           ; Increment exponent.
        BRA.S   RETURN
@1      TST.L   D5              ; Check ambiguous case.
        BNE.S   RETURN
        BCLR    #0,D2           ; Force round to even.
RETURN  RTS
        debugend '%XMUL',3

;       Routine %xdiv provides single extended precision divide for base
;       conversion.
;       D1, D2 contain A and get R := A/B.
;       D4,D5 contain B.
;       Uses:   D0-D7

        .ref    %xdiv32

%xdiv   
        debugbegin
         CLR.L   D0              ; D0 will be a zero register for use in double
                                ; precision adds and subtracts when needed.
        SUB     D4,D1           ; D1 gets exp(A)-exp(B).
        move.l  d5,d6           ; D6 gets B.
        clr     d6              ; D6 gets [Bu,0].
        jsr     %xdiv32         ; D4 gets [Q0,Q3].
        tst.l   d2
        bne.s   %xd5            ; Branch if R3 <> 0.
                        ; R3 = 0 so Q = W*W*Q0 + W*Q3.
                        ; and R = 0.
        swap    d4              ; D4 gets [Q3,Q0].
        tst     d4              
        beq.s   @1              ; Branch if Q0=0.
        ror.l   #1,d4           ; Q0=1 so right align Q3.
        bra.s   @3
@1      subq    #1,d1           ; Q0=0 so adjust exponent.
@3      move.l  d4,d2           ; Set result.
        rts
        
%xd5    move.l  d4,-(sp)        ; Save [Q0,Q1] on stack.
        swap    d6              ; D6 gets [Bu,0].
        jsr     %xdiv32         ; D4 gets [Q4,Q7].
        move.l  d2,d7           ; D7 gets R7, the final remainder.
        move.l  (sp)+,d2        ; D2 gets [Q0,Q3].
        swap    d2              ; D2 gets [Q3,Q0].
        move    d2,d3           ; D3 gets [?,Q0].
        clr     d2              ; D2 gets [Q3,0].
        add.l   d4,d2           ; D2 gets [Q3,0]+[Q4,Q7].
        addx    d0,d3           ; Propagate carry.
        tst     d3
        beq.s   @1              ; Branch if Q0=0 at end.
                        ; Q0=1, so shift it in.
        lsr.l   #1,d2           ; Shift result.
        bset    #31,d2          ; Set most significant bit.
        bcs.s   %xdround        ; Branch if round bit shifted out above.
        tst.l   d7
        bne.s   inexact         ; Branch if inexact.
        bra.s   %xdret          ; Otherwise return.
                        ; Q0=0, so figure out the round bit.
@1      subq    #1,d1           ; Adjust exponent.
        tst.l   d7
        beq.s   %xdret          ; Branch if exact.
        clr     d6              ; D6 gets [?,0].
        add.l   d7,d7           ; D7 gets 2R.
        addx    d0,d6           ; Propagate carry.
        sub.l   d5,d7           ; D7 gets 2R-B.
        subx    d0,d6           ; Propagate borrow.
        bmi.s   inexact         ; Branch if 2R < B.
%xdround 
        addq.l #1,d2            ; Do round up.
inexact                 ; Set inexact.
        setex   xinex           ; Turn on inexact bit.
%xdret  
        rts                     ; Return.
        debugend '%XDIV',3

;       Routine %xint converts single extended to an integral value for
;       base conversion, setting inexact in FPEXCEP if appropriate.
;       No trapping can occur.
;       D1 contains the exponent.
;       D2 contains the significand.

        .macro  setinexact      ; Routine to set inexact in FPEXCEP.
        bset    #xinex,fpexcep(a3)
        .endm
        
%xint    
        debugbegin
        TST     D1
        BMI.S   MINEX           ; Branch if negative exponent, x < 1.0.
        MOVE    D1,D0           ; D0 gets exponent.
        subI    #31,D0          ; D0 gets 31-exponent which is the bit number
                                ; corresponding to 2^0. 
        neg     d0              ; D0 <= 31.
        ble     retxint         ; Branch if number >= 2^31, so already integral.
        movem.l a3/d3-d5,-(sp)  ; Save registers.
        CLR.L   D3
        BSET    D0,D3           ; D3 gets 2^0 bit.
        subq.l  #1,d3           ; D3 gets fraction mask.
        move.l  d3,d4
        and.l   d2,d4           ; D4 gets fraction bits of x.
        beq.s   fixregs         ; Branch if number is integral.
                        ; X is not exactly integral.
        initfpcb                ; Initialize A3 to point to FPCB.
        setinexact              ; Set inexact flag.
        btst    #moderr,fpmode(a3)
        bne.s   down            ; Branch if rounding mode rz or rm.
        btst    #moderl,fpmode(a3)
        bne.s   up              ; Branch if rounding mode rp.
                        ; Rounding mode is round to nearest.
        move    d0,d5
        subq    #1,d5           ; D5 gets bit number for 2^-1.
        bclr    d5,d4           ; D4 gets fraction bits except 2^-1.
        beq.s   down            ; Branch if fraction < 0.5.
        tst.l   d4
        bne.s   up              ; Branch if fraction > 0.5.
                        ; Ambiguous case - set 2^0 bit even.
        btst    d0,d2
        beq.s   down            ; Branch if result is already even.
up
        clr.l   d4
        bset    d0,d4           ; D4 gets 2^0 bit.
        add.l   d4,d2           ; Add in rounding bit.
        bcc.s   down            ; Branch if no carry out.
        addq    #1,d1           ; Rounding carry out, so increment exponent.
        move.l  #$80000000,d2   ; And set proper significand.
        bra.s   fixregs
down
        not.l   d3              ; D3 gets integral mask.
        and.l   d3,d2           ; D2 gets fraction bits cleared.
fixregs
        movem.l (sp)+,a3/d3-d5  ; Restore registers.
        bra.s   retxint
MINEX   
        tst.l   d2
        beq.s   retxint         ; Branch to end if significand is zero.
        movea.l a3,a1           ; A1 saves current A3.
        initfpcb                ; A3 gets pointer to FPCB.
        setinexact
        btst    #moderr,fpmode(a3)
        bne.s   zero            ; Branch if rz or rm.
        btst    #moderl,fpmode(a3)
        bne.s   one             ; Branch if rp.
        CMPI    #-1,D1
        BLT.S   ZERO            ; exp<-1 means round to 0.
        CMPI.L  #$80000000,D2   ; Compare x with 0.5.
        Bls.S   ZERO            ; x<=0.5 means round to 0.
one
        MOVE.L  #$80000000,D2   ; Round x to 1.0.
        CLR     D1              ; Exponent gets 0.
        BRA.S   undoa3 
ZERO    CLR.L   D2              ; Return zero significand.
        move    #-256,d1        ; Return very small exponent.
undoa3  
        movea.l a1,a3           ; Restore old value of A3.
RETXINT  
        RTS
        debugend '%XINT',3

;       routine %xdec converts ascii decimal digits to an extended
;       Input:  A1 contains the string of input digits
;       Output: lsw(D1) contains exponent of extended result
;               D2 contains significand of extended result
;               lsw(D3) contains implied exponent, a power of ten

;       Then STRING = Significand * 2**exponent * 10**implied exponent
;       Uses    A1, D0-D7

%xdec
        debugbegin
        clr     d0
        move.b  (a1)+,d0        ; D0 gets length of string.
        clr.l   d2              ; Clear significand.
        clr.l   d4              ; Clear digit buffer.
        move    d0,d7           ; d7 gets count for first loop.
        cmpi    #9,d7           
        ble.s   @1              ; Branch if count was <= 9.
        move    #9,d7           ; Maximum count of 9 for first loop.
@1      sub     d7,d0           ; d0 gets residual count for other loops.
        subq    #1,d0           ; Decrement counter for DBF.
        subq    #1,d7           ; Decrement counter for DBF.
;               The first loop for up to nine digits will all fit in one
;               32 bit register, so it is fast.
loop1   lsl.l   #1,d2           ; d2 gets 2 * old D.
        move.l  d2,d1           ; d1 gets 2 * old D.
        lsl.l   #2,d1           ; d1 gets 8 * old D.
        add.l   d1,d2           ; d2 gets 10 * old D.
        move.b  (a1)+,d4        ; d4 gets next digit.
        subi.b  #asciizero,d4          ; d4 gets digit value (subtract '0').
        add.l   d4,d2           ; Add in digit.
        dbf     d7,loop1
        
        clr     d3              ; Clear implicit exponent counter.
        clr     d5              ; Clear left extension register.
        clr.l   d6              ; Clear sticky bit.
        tst     d0
        blt.s   done            ; Branch if no more digits remain.
        
        ;       Second loop for more than nine digits continues until
        ;       the significand overflows 32 significant bits.
        
loop2   lsl.l   #1,d2
        roxl    #1,d5           ; (d5,d2) get 2 * old D.
        move.l  d2,d1
        move    d5,d7           ; (d7,d1) get 2 * old D.
        lsl.l   #1,d1
        roxl    #1,d7
        lsl.l   #1,d1
        roxl    #1,d7           ; (d7,d1) get 8 * old D.
        add.l   d1,d2
        addx    d7,d5           ; (d5,d2) get 10 * old D.
        clr     d7              ; Clear d7 for ADDX.
        move.b  (a1)+,d4
        subi.b  #asciizero,d4
        add.l   d4,d2           ; Add in next digit.
        addx    d7,d5           ; (d5,d2) get next digit.
        subq    #1,d0           ; Decrement count.
        bmi.s   done
        tst     d5
        beq.s   loop2           ; Branch if still within 32 bits.
        
        move    d0,d3           ; d3 gets number of digits remaining =
                                ; implicit power of ten.
        addq    #1,d3           ; Correct for DBF bias.
        
        ;       The significand has overflowed 32 bits.
        ;       Now we just scan the rest of the digits for non-zeros
        ;       and set the sticky bit accordingly.

loop3
        cmpi.b  #asciizero,(a1)+
        beq.s   @1
        st      d6              ; Turn on sticky bit if not '0'.
@1      dbf     d0,loop3

        ; Finished with string now.
        ; The significand is in D2, overflow in D5, sticky bits in D6.
        ; The implicit exponent is in D3.
        
done
        move    #31,d1          ; d1 gets binary exponent of result.
        tst     d5
        beq.s   normal          ; If there is no overflow then the result
                                ; is exact but needs normalizing.
                                ; If there is overflow then the result
                                ; needs right-normalizing and rounding.
        andi    #$FF,d6         ; Clear upper byte of D6.
        swap    d6              ; Position sticky bits conveniently.

        ; Triple shift loop to get the most significant bit of result
        ; into D2, adjusting exponent.
        
loop4
        lsr     #1,d5           ; Shift out from extension
        roxr.l  #1,d2           ; through significand
        roxr.l  #1,d6           ; into sticky register.
        addq    #1,d1           ; Adjust exponent.
        tst     d5              ; Is the overflow clear?
        bne.s   loop4           ; Branch if more to go.
        
        ; Overflowed case needs round.
        
        tst.l   d6
        beq.s   @2              ; Branch if result is exact.
        setex   xinex           ; Turn on inexact bit.
        clr.l   d7              ; Handy register for ADDX.
        addi.l  #$80000000,d6   ; Add round bit.
        addx.l  d7,d2           ; Ripple.
        bcc.s   @1
        bset    #31,d2          ; Rounding overflow - restore bit.
        addq    #1,d1           ; Increment exponent.
        rts
@1      tst.l   d6              
        bne.s   @2              ; Branch if not ambiguous case.
        bclr    #0,d2           ; Force even result.
@2      rts

        ; Simple case needs left-normalizing.
        
normal  tst.l   d2              ; Check for zero significand.
        beq.s   @1
        norm    D2,D1           ; Normalize.
        rts
@1      move    #$8000,d1      ; Set exponent for normal zero.
        rts
        debugend '%xdec',3
        
;       Routine %xpot computes a power of ten.
;       Input:  D0: an integer P in 0..127
;       Output: D1: exponent of 10**P.
;               D2: significand of 10**P.
;       Uses:   D0-D7.


%xpot
        debugbegin
        cmpi    #13,d0
        ble.s   p10compute      ; Branch if n <= 13 so 10^n is exact.
        setex   xinex           ; Turn on inexact exception.
p10compute
        move    d0,d3           ; d3 gets P.
        andi    #$F,d3          ; D3 gets Pl, the low order bits.
        bne     low
        andi    #$70,d0         ; D3 gets Pu, the upper order bits.
        bgt.s   @3              ; Branch if P <> 0.
        clr     d1              ; Since P=0, set result to 1.0.
        move.l  tenssig,d2      ; 
        rts
@3      lsr     #2,d0           ; D0 gets 4*Pu.
        move.l  tenbsig-4(d0),d2 ; Move significand.
        lsr     #1,d0           ; D0 gets 2*Pu.
        move    tenbe-2(d0),d1  ; Move exponent.
potrts  rts

;       Tables of powers of ten.  Stored as exponents and significands.
;       TENS contains 10**0, 10**1, .. 10**15.
;       TENB contains 10**16, 10**32, .. 10**96,10**112.

TENSE   .BYTE   0,3,6,9,$D,$10,$13,$17,$1A,$1D,$21,$24,$27,$2B,$2E,$31
TENSSIG .WORD   $8000,0,$A000,0,$C800,0,$FA00,0,$9C40,0,$C350,0
        .WORD   $F424,0,$9896,$8000,$BEBC,$2000,$EE6B,$2800,$9502,$F900
        .WORD   $BA43,$B740,$E8D4,$A510,$9184,$E72A,$B5E6,$20F4
        .WORD   $E35F,$A932
TENBE   .WORD   $35,$6A,159,$D4,265,$13E,372
TENBSIG .WORD   $8E1B,$C9BF,$9DC5,$ADA8,$AF29,$8D05,$C278,$1F4A
        .WORD   $D7E7,$7A90,$EFB3,$AB17,$850f,$adc1

low     move.b  tense(d3),d1    ; Move exponent.
        ext.w   d1
        lsl     #2,d3           ; D3 gets 4*Pl.
        move.l  tenssig(d3),d2  ; Move significand.
        andi    #$70,d0         ; D0 gets Pu.
        beq.s   potrts          ; Return if Pu=0.
        lsr     #2,d0           ; D0 gets 4*Pu.
        move.l  tenbsig-4(d0),d5 ; D5 gets significand of large power.
        lsr     #1,d0           ; D0 gets 2*Pu.
        move    tenbe-2(d0),d4  ; D4 gets exponent of large power.
        jmp     %xmul           ; Go multiply them together.

        debugend '%XPOT',3

ÿ