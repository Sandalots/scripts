(* Copyright 1983, Apple Computer Inc. *)

(* File L:Scan, Version 4 February 1983. *)

{$ifc fsymok }
procedure displaysn ( var sn : scannednumber ) ;
begin (* displaysn *)
with sn do
begin
write(' Scanned number: ') ;
if error then write(' ERROR ') else case kind of
nan :   begin
        if sign then write('-') ;
        write('NaN') ;
        if length(significand) > 0 then
                begin
                write('''',significand) ;
                if moresignificand then write('...') ;
                write('''') ;
                end ;
        end ;
inf :   begin
        if sign then write(' - Infinity') else write(' + Infinity ') ;
        end ;
normal : begin
        if sign then write('-') ;
        write(significand) ;
        if moresignificand then write('...') ;
        if implicit <> 0 then write(' e ',implicit:2) ;
        if length(exponent) > 0 then 
                begin
                write(' E ') ;
                if esign then write('-') ;
                write( exponent) ;
                if moreexponent then write('...') ;
                end ;
        end ;
end (* case kind *) ;
end (* with sn *) ;
writeln ;
end   (* displaysn *) ;
{$endc }

procedure errorsn ( var sn : scannednumber ) ;

        (* Creates sn for NaN ascbin for invalid ascii input. *)

begin (* errorsn *)
with sn do
        begin (* error *)
        error := false ;
        kind := nan ; (* Set up appropriate NaN instead. *)
        significand := inerrstring ;
        moresignificand := false ;
        end   (* error *) ;
end   (* errorsn *) ;

procedure tokenscan ( function nextch : char ; var sn : scannednumber ) ;

        (* Using nextch to get the next character, creates a scanned token in sn. *)
        (* Leading spaces are ignored. *)
        (* If nextch = rubout, exit immediately with error true. *)
        
var
current, leading : char ;
ls, le : integer ;
nzbp, nzap : integer ;

function upcase ( c : char ) : char ;
begin
if ('a' <= c) and (c <= 'z') then upcase := chr(ord(c)-32) else upcase := c ;
end ;

function digital : boolean ;
        (* Returns true if current character in '0'..'9' *)
begin (* digital *)
digital := ('0' <= current) and (current <= '9') ;
end   (* digital *) ;

function nanok : boolean ;
        (* Returns true if current character is OK for nan significand string. *)
begin (* nanok *)
nanok := (current<>'''') and (' ' <= current) and (current <= '~')
end   (* nanok *) ;

procedure bump ;
begin (* bump *)
current := nextch ;
if ord(current) = rubout then 
        begin (* bail out *)
        sn.error := true ;
        exit( tokenscan ) ;
        end   (* bail out *) ;
end   (* bump *) ;

procedure apps ; (* Appends current to significand and bumps. *)
begin (* apps *)
with sn do if ls < maxfpstring then 
        begin
        ls := ls + 1 ;
        significand[ls] := current ;
        end
        else
        moresignificand := true ;
bump ;
end   (* apps *) ;

procedure appz ( n : integer ) ; (* Appends n zeros to significand. *)

var
i : integer ;

begin (* appz *)
with sn do for i := 1 to n do if ls < maxfpstring then 
        begin
        ls := ls + 1 ;
        significand[ls] := '0' ;
        end;
end   (* appz *) ;

procedure appe ; (* Appends current to exponent and bumps. *)
begin (* appe *)
with sn do if le < maxfpstring then
        begin
        le := le + 1 ;
        exponent[le] := current ;
        end
        else
        moreexponent := true ;
bump ;
end   (* appe *) ;

begin (* tokenscan *)
with sn do 
begin (* with sn *)
error := false ;
sign := false ;
kind := normal ;
significand := '' ;
moresignificand := false ;
implicit := 0 ;
esign := false ;
exponent := '' ;
moreexponent := false ;

bump ; (* Initialize current character. *)
while (current = ' ') do bump ; (* Remove leading blanks.  *)

if (current = '+') or (current = '-') then
        begin (* leading sign *)
        sign := current = '-' ;
        leading := current ;
        bump ;
        if current = leading then
                begin (* infinity *)
                kind := inf ;
                while current = leading do bump ;
                exit( tokenscan ) ;
                end   (* infinity *) ;
        end   (* leading sign *) ;

if upcase(current) = 'N' then
        begin (* nan *)
        kind := nan ;
        bump ;
        if upcase(current) <> 'A' then exit ( tokenscan ) ;
        bump ;
        if upcase(current) <> 'N' then exit ( tokenscan ) ;
        bump ;
        if current <> '''' then exit ( tokenscan ) ;
        bump ;
        ls := 0 ;
        while nanok and (ls < maxfpstring) do apps ;
        significand[0] := chr(ls) ;
        while nanok do
                begin
                moresignificand := true ;
                bump ;
                end ;
        if current = '''' then bump ;
        exit( tokenscan ) ;
        end   (* nan *) ;

ls := 0 ;
nzbp := 0 ; nzap := 0 ; (* Number of zeros found before and after point. *)
error := not digital ; (* Assume error until we find a digit. *)
while current = '0' do bump ; (* Skip leading zeros. *)
while digital do 
        begin (* digits before point *)
        if current = '0' then 
                begin (* zero before point *)
                nzbp := nzbp + 1 ;
                bump ;
                end   (* zero before point *)
                else
                begin (* nonzero before point *)
                appz(nzbp) ; (* Append significant zeros. *)
                nzbp := 0 ;
                apps ; (* Append nonzero. *)
                end   (* nonzero before point *) ;
        end   (* digits before point *) ;
if current = '.' then 
        begin (* point found *)
        bump ;
        if error then error := not digital ; (* Assume error until we find a digit. *)
        while digital do 
                begin (* digits after point *)
                if current = '0' then 
                        begin (* zero after point *)
                        nzap := nzap + 1 ;
                        bump ;
                        end   (* zero after point *)
                        else
                        begin (* nonzero after point *)
                        implicit := implicit - nzap - 1 ; (* Adjust exponent. *)
                        if ls > 0 then appz( nzbp + nzap ) ; (* Append zeros. *)
                        nzbp := 0 ; nzap := 0 ;
                        apps ; (* Append nonzero. *)
                        end   (* nonzero after point *) ;
                end   (* digits after point *) ;
        end   (* point found *);
if nzbp > 0 then implicit := nzbp ; (* Handles case dddd0000.0000 *)
significand[0] := chr(ls) ;

if (not error) and ((upcase(current) = 'E') or (upcase(current) = 'D')) then
        begin (* exponent *)
        bump ; (* handle E/D for exponent *)
        le := 0 ;
        if current = '+' then bump else if current='-' then 
                begin (* negative exponent *)
                esign := true ;
                bump ;
                end   (* negative exponent *) ;
        if digital then
                begin (* exponent found *)
                while current = '0' do bump ; (* Skip leading zeros.  *)
                while digital do appe ; (* exponent digit *) ;
                exponent[0] := chr(le) ;
                end   (* exponent found *)
                else
                error := true ;
        end   (* exponent *) ;

end   (* with sn *) ;
end   (* tokenscan *) ;

{$ifc not fp_compiler }
procedure filescan 
        ( var infile : text ; var sn : scannednumber ; var sout : fp_string ) ;

        (* 
            Searches Infile for a valid number token.
            Leading blanks and carriage returns are ignored.
            If a valid number token is found, 
           it is placed in sn with error false, and sout will contain
           the scanned characters, up to maxfpstring.
            If none is found, sn.error is true, sout is undefined, and
           Ioresult is set to FPIORESULT if it would otherwise be zero.
                                                                                *)

var
retry, abort : boolean ;
is, ls, lsonentry : integer ;
s : fp_string ;

function nextfile : char ;

var
allblank : boolean ;
c : char ;
i : integer ;

procedure backupscreen ; (* Undo a line of interactive input. *)
var i : integer ;
begin
for i := 1 to ls do write( infile, chr(backspace), ' ', chr(backspace) ) ;
end ;

begin (* nextfile *)
lsonentry := ls ; (* Save length of s prior to processing. *)
if retry then 
        begin (* retry string *)
        nextfile := s[is] ;
        is := is + 1 ;
        if is > ls then retry := false ; (* Terminate retry. *)
        end   (* retry string *)
        else
        begin (* read file *)
        read( infile, c ) ;
        case ord(c) of
        
        backspace : begin (* backspace *)
                if ls > 0 then
                        begin (* do backspace *)
                        backupscreen ;
                        ls := ls - 1 ;
                        s[0] := chr(ls) ;
                        write( infile, s ) ;
                        end   (* do backspace *) ;
                nextfile := chr(rubout) ;
                abort := true ;
                end (* backspace *) ;
                        
        cancel, rubout : begin (* rubout *)
                backupscreen ; 
                ls := 0 ;
                nextfile := chr(rubout) ;
                abort := true ;
                end (* rubout *) ;
                
        otherwise begin (* normal *)
                if eof(infile) then
                        begin (* eof *)
                        nextfile := chr(carriagereturn) ;
                        end   (* eof *)
                        else
                        begin (* not eof *)
                        if eoln(infile) then
                                begin (* eoln *)
                                allblank := true ;
                                for i := 1 to ls do 
                        allblank := allblank and (s[i] = ' ') ;
                                if allblank then
                                        begin (* only blank so far *)
                                        ls := 0 ;
                                        nextfile := ' ' ;
                                        end   (* only blank so far *)
                                        else
                                        begin (* started token *)
                                        nextfile := chr(carriagereturn) ;
                                        end   (* started token *) ;
                                end   (* eoln *)
                                else
                                begin (* char *)
                                if ls < maxfpstring then
                                        begin (* append *)
                                        ls := ls + 1 ;
                                        s[ls] := c ;
                                        end   (* append *) ;
                                nextfile := c ;
                                end   (* char *) ;
                        end   (* not eof *) ;
                end (* normal *) ;
        
        end (* case ord(c) *) ;
        end   (* read file *) ;
end   (* nextfile *) ;

begin (* filescan *)
ls := 0 ;
retry := false ;
repeat
abort := false ;
tokenscan( nextfile, sn ) ;
retry := ls > 0 ;
is := 1 ;
until not abort ;
s[0] := chr(lsonentry) ;
sout := s ;
%_backupinput( infile ) ;
if sn.error then if ioresult=0 then %_setiorslt( fpioresult ) ;
end   (* filescan *) ;
{$endc }

procedure stringscan ( var s : fp_string ; first, last : integer ; var next : integer ;
                       var sn : scannednumber ) ;
                       
        (* 
            Searches S for a valid number token.
            The search starts at S[first] and continues no further than S[last].
            Next will be set to the first character that was not accepted.
            Leading blanks are ignored.
            If a valid number token is found, it is placed in sn with error false.
            If none is found, sn.error is true.
                                                                                *)

var
is : integer ;

function nextstring : char ;

begin (* nextstring *)
if is <= last then nextstring := s[is] else nextstring := '?' ;
is := is + 1 ;
end   (* nextstring *) ;

begin (* stringscan *)
if first < 1 then first := 1 ;
if last > length(s) then last := length(s) ;
is := first ;
tokenscan( nextstring, sn ) ;
if is <= last + 2 then next := is - 1 else next := last + 1 ;
end   (* stringscan *) ;

procedure nanscan ( var sn : scannednumber ; var x : fp_int64 ) ;

        (* Creates an extended NaN fraction in x, using the information in sn.
           Excess or improper characters result in creating an ascnan nan. *)
           
var (* nanscan *)
i, is, ls, lpre, lpost : integer ;
pre, post : array[1..maxfpstring] of nibble ;
k, ix : integer ;
allzero, error : boolean ;

procedure bump ; begin is := is + 1 ; end ;

function current : char ;
begin (* current *)
if is <= ls then current := sn.significand[is] else current := chr(13) ;
end   (* current *) ;

function hexit : integer ; (* Returns hex value of current or else -1. *)
var
c : char ;
begin (* hexit *) 
c := current ;
if c < 'A' then
        begin (* c < A *)
        if ('0' <= c) and (c <= '9') then hexit := ord(c) - 48 else hexit := -1 ;
        end   (* c < A *)
        else
        begin (* c >= A *)
        if c <= 'F' then hexit := ord(c) - 55
        else if ('a' <= c) and (c <= 'f') then hexit := ord(c) - 87 
        else hexit := -1 ;
        end   (* c >= A *) ;
end   (* hexit *) ;

begin (* nanscan *)
ls := length(sn.significand) ;
if (ls=0) or sn.moresignificand then error := true else
        begin (* normal significand *)
        is := 1 ;
        lpre := 0 ; lpost := 0 ;
        while current = '0' do bump ; (* Skip leading zeros. *)
        while hexit >= 0 do
                begin (* pre colon hex digit *)
                lpre := lpre + 1 ;
                pre[lpre] := hexit ;
                bump ;
                end   (* pre colon hex digit *) ;
        if current = ':' then
                begin (* after colon *)
                bump ;
                while hexit >= 0 do
                        begin (* post colon hex digit *)
                        lpost := lpost + 1 ;
                        post[lpost] := hexit ;
                        bump ;
                        end   (* post colon hex digit *) ;
                if lpost > 12 then while (lpost > 12) and (post[lpost] = 0) do
                                        lpost := lpost - 1 ; (* Cut trailing zeros. *)
                end   (* after colon *) ;
        error := (is <= ls) or (lpre > 4) or (lpost > 12) ;
        if (lpre=4) and (pre[1] >= 8) then error := true ; (* Can't set I bit. *)
        end   (* normal significand *) ;
for ix := 0 to 7 do x[ix] := 0 ;
if error then x[1] := nanascnan else
        begin (* not error *)
        allzero := true ;
        i := lpre ; ix := 1 ;
        while i >= 1     do
                begin (* pair of post colon hexits *)
                k :=       pre[i] ;
                if i <> 1     then k := 16 * pre[i-1] + k ;
                i := i - 2 ;
                if k >= 128 then k := k - 256 ;
                if k <> 0 then allzero := false ;
                x[ix] := k ;
                ix := ix - 1 ;
                end   (* pair of post colon hexits *) ;
        i := 1 ; ix := 2 ;
        while i <= lpost do
                begin (* pair of post colon hexits *)
                k := 16 * post[i] ;
                if i <> lpost then k := k + post[i+1] ;
                i := i + 2 ;
                if k >= 128 then k := k - 256 ;
                if k <> 0 then allzero := false ;
                x[ix] := k ;
                ix := ix + 1 ;
                end   (* pair of post colon hexits *) ;
        if allzero then x[1] := nanzero ;
        end   (* not error *) ;
end   (* nanscan *) ;

ÿ