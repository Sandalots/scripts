;*****
;*****
;******************************************************************************
;*****
;*****
;*****          WHAT WILL BE THE TYPE OF THE CALCULATED VALUE OF 'Y' NOW
;*****          IN REG D0-D2.  SINCE 'Y' IS THE RESULT OF EITHER A MULTIPLY
;*****          OR A DIVIDE INVOLVING THE OPERANDS
;*****
;*****             'X' = TYPE NORM OR NON-ZERO-UNNORM
;*****                 = ALWAYS POSITIVE (DUE TO ABS) WITH A CLEAR TAG FIELD
;*****
;*****
;*****             'SCALE' = TYPE NORM
;*****                     = ALWAYS POSITIVE (DUE TO mc%pwrten) WITH A CLEAR
;*****                       TAG FIELD (WE CLEARED IT JUST BEFORE 'Y' CALC'D)
;*****
;*****             'Y' (FROM A MULTIPLY) = TYPE NORM OR NON-ZERO-UNNORM
;*****
;*****             'Y' (FROM A DIVIDE )  = TYPE NORM OR NON-ZERO-UNNORM
;*****                                     (THIS IS THE RESULT OF THE SINGLE
;*****                                      NORM-SHIFT AT THE END OF mc%div_opt.
;*****                                      EVEN THE MOST SEVERELY UNNORM
;*****                                      COULD NEVER GET AN UNNORM-ZERO
;*****                                      QUOTIENT).
;*****
;*****          THE TAG FIELD OF 'Y' WILL EITHER BE CLEAR OR HAVE THE NORM
;*****          BIT SET (DUE TO THE SIGN-EXP CALCULATION IN 'mc%mul_opt' OR
;*****          'mc%div_opt').  THIS WILL SAVE HAVING TO 'TYPE' THE INT('Y')
;*****          LATER; SEE COMMENTS BELOW.
;*****
;*****          CAN THE 'Y' CALCULATION EVER OVERFLOW OR UNDERFLOW.
;*****
;*****             SINCE THE PURPOSE OF THE SCALING OPERATION IS TO MAKE
;*****             THE (INPUT VALUE) INTO A FP NUMBER WHICH, WHEN CONVERTED,
;*****             CAN BE EXPRESSED IN A 'LEN' DIGIT BCD STRING, THE 'Y'
;*****             CALCULATION WILL NEVER OVERFLOW OR UNDERFLOW.
;*****
;*****             THE LARGEST POSSIBLE (INPUT VALUE) WILL ALWAYS PRODUCE
;*****             A POSITIVE 'ISCALE' VALUE; THEN 'Y' WILL BE CALCULATED
;*****             AS A DIVIDE OF (VERY BIG NUMBER) BY (VERY BIG NUMBER).
;*****             NO OVERFLOW REGARDLESS OF THE VALUE OF 'K' USED.
;*****
;*****             THE SMALLEST POSSIBLE (INPUT VALUE), WHETHER IT BE A
;*****             NORM, DENORM, OR UNNORM, WILL ALWAYS PRODUCE A NEGATIVE
;*****             'ISCALE' VALUE; THEN 'Y' WILL BE CALCULATED AS A
;*****             MULTIPLY OF (VERY SMALL NUMBER) BY (VERY LARGE NUMBER)
;*****             WHEN 'K' POSITIVE IS USED, OR AS A MULTIPLY OF
;*****             (VERY SMALL NUMBER) BY (NUMBER AROUND 10) WHEN 'K'
;*****             NEGATIVE USED.
;*****             NO UNDERFLOW REGARDLESS OF THE VALUE OF 'K' USED.
;*****
;*****             INSPECTION OF THE POSSIBLE RANGES OF 'ISCALE' IN AN
;*****             AN EARLIER COMMENT CAN BE USED TO VERIFY THESE
;*****             CONCLUSIONS.
;*****
;*****
;******************************************************************************
;
;
;      NOW HAVE THE UNROUNDED RESULT FOR 'Y' IN REGS. D0-D2 WITH G-R-S
;      BITS IN REG. D3.  LOGICALLY 'OR' THE G-R-S BITS INTO 'Y.
;
;
INTPART_Y  EQU   *
;
        TST.L    D3
        beq      @23
           ORI.B    #1,D2                       ; 'OR' G-R-S BITS INTO 'Y' LSB
@23

;
;
;      RESTORE ORIGINAL ROUNDING MODE (ITS BEHIND SAVED INPUT VALUE ON STACK),
;      HOWEVER MAINTAIN EXTENDED PRECISION.
;
;      RESTORE ORIGINAL INPUT VALUE'S SIGN TO 'Y'.
;
;
        MOVE.W   2+SIZEAC(A7),CNTRL(A3)      ; RESTORE ORIGINAL CNTRL REG
        ANDI.B   #MDRND_B,MODE(A3)           ; MASK OFF ORIGINAL ROUNDING MODE
        ORI.B    #EXT_PREC,MODE(A3)          ; FORCE EXTENDED PRECISION
;                                              NOTE: SINCE EXTENDED = 0, THIS
;                                              ISN'T NECESSARY, BUT THE
;                                              EQUATE COULD CHANGE!!!
;
        TST.B    2+SIGN(A7)
        bpl      @24                          ; IF (ORIGINAL INPUT VALUE
;                                                                   NEGATIVE)
           BSET     #31,D0                      ; MAKE 'Y' NEGATIVE
;                                              ELSE ('Y' IS ALREADY POSITIVE)
@24                                          ; END
;
;
;**    *****************************************************************
;**
;**
;**       WHERE ARE THE VARIABLES.
;**
;**
;**       REGISTER D0 - D2       'KLUG' ROUNDED 'Y'_CALCULATED WITH SIGN
;**                               RESTORED (TYPE NORM OR NON-ZERO-UNNORM)
;**                               - TAG FIELD IS CLEAR OR NORM TAG SET.
;**
;**       REGISTER D4.W          'ILOG'  (WORD)
;**
;**       REGISTER D6.W          'LEN'  (WORD)
;**
;**       REGISTER D7.W          'K'  (WORD)
;**
;** A7 -> SAVED ON STACK         'RECALC_CTR'  (WORD)
;**
;**       SAVED ON STACK         (INPUT VALUE)  (3 LONG WORDS)
;**
;**       SAVED ON STACK         ORIGINAL CONTROL REGISTER  (WORD)
;**
;**
;**    *****************************************************************
;
;      TAKE THE INTEGER PART OF THE 'KLUG' ROUNDED 'Y' WITH THE ORIGINAL
;      ROUNDING MODE, ORIGINAL SIGN, AND IN EXTENDED PRECISION.
;
;      THE mc%int_a ENTRY POINT ACCEPTS EITHER NORM OR NON-ZERO-UNNORM
;      TYPE INPUT; INPUT DOESN'T HAVE TO HAVE PROPER TAG. NOTE THAT 'mc%int_a'
;      ALWAYS TAGS TRUE ZERO AS SUCH, AND EITHER LEAVES THE TAG FIELD ALONE
;      OR SETS THE NORM TAG FOR ANY OTHER TYPE INPUT.
;
;      NOTE THAT  mc%int_a' CAN SET THE INX BIT IN TEMPSTAT WHEN ROUNDING.
;      THIS IS CORRECT; IT TELLS THE USER THAT THE BINARY FP INPUT
;      OPERAND CANNOT BE EXACTLY REPRESENTED AS A BCD FP RESULT GIVEN
;      THE SPECIFIED VALUE OF 'K'.
;
;      SINCE 'mc%int_a' TRASHES REGS. D4-D6, NEED TO SAVE 'ILOG' AND 'LEN'
;      ON THE STACK.  HOWEVER, THE NEXT OPERATION FOLLOWING THE RETURN
;      FROM 'mc%int_a' AND THE TAGGING OF ITS RESULT WILL BE A CALL TO
;      THE 'mc%pwrten' SUBROUTINE TO CALCULATE 10^'LEN'.  WANT TO USE
;      REGS. D4-D6 TO SAVE INT('Y') AND REG. D7 FOR 'LEN' WHEN THE
;      'mc%pwrten' SUBROUTINE IS CALLED.  THIS MEANS THAT 'LEN', 'K', AND
;      'ILOG' NEED TO BE SAVED ON THE STACK NOW AND A COPY OF 'LEN'
;      SAVED IN REG. D7 IN PREPARATION FOR THE SUBSEQUENT CALL TO THE
;      'mc%pwrten' SUBROUTINE.
;
;      SAVE 'LEN', 'K', AND 'ILOG' ON STACK.
;      KEEP A COPY OF 'LEN' IN REG. D7.
;
        MOVEM.W  D7/D6/D4,-(A7)              ; SAVE 'K', 'LEN', & 'ILOG' ON STK
        MOVE.W   D6,D7                       ; PUT ('LEN') IN D7
        jSR      mc%int_a                      ; TAKE INTEGER PART OF 'Y'
;
;
;      NOTE THAT TRUE ZERO TAG CAN NOT HAVE BEEN ACCIDENTLY SET BY SIGN-EXP
;      CALCULATION IN THE MULTIPLY OR DIVIDE SINCE THE TAG FIELDS OF THE
;      TWO OPERANDS WERE FIRST CLEARED.
;
;      THE RESULT IN REGS. D0-D2 NEEDS TO BE 'TYPED'.  THE TYPE, TRUE ZERO,
;      IS ALREADY TAGGED AS SUCH.  TAG NORM OR NON-ZERO-UNNORM AS SUCH.
;
;
        BTST     #ZERO_R,D0
        bne      @26                         ; IF (NOT TAGGED TRUE ZERO)
           ANDI.L   #$8000FFFF,D0               ; CLEAR TAG FIELD
           TST.L    D1
           bmi      @25                         ; IF (UNNORM)
              BSET     #NNORM_R,D0                 ; TAG IT UNNORM
           bra      @26                         ; ELSE (NORM)
@25
              BSET     #NORM_R,D0                  ; TAG IT NORM
@26                                          ; END
;
;
;      NOW HAVE THE INT('Y') IN REGS D0-D2 WITH A VALID TAG AND THE ORIGINAL
;      SIGN.
;
;      NEED TO CALCULATE 10^'LEN'.
;
;      SAVE TAGGED INT('Y') IN D4-D6.
;
;      THE 'mc%pwrten' SUBROUTINE WANTS THE (POWER TO BE FOUND) IN REG D7;
;      A COPY OF ('LEN') WAS PUT IN D7 ABOVE.  'mc%pwrten' SAVES REGS. D4-D7
;      AND ALL ADDRESS REGS.
;
;
        MOVE.L   D0,D4                       ; SAVE INT('Y') IN D4-D6
        MOVE.L   D1,D5
        MOVE.L   D2,D6
        jSR      mc%pwrten                      ; CALCULATE 10^('LEN')
;                                              RETURNS POSITIVE, NORM, RESULT
;                                              IN REGS D0-D2 WITH VALID TAG
;
;*****************************************************************************
;*****
;*****
;*****       REVIEW WHAT THE ALGORITHM HAS ACTUALLY DONE SO FAR.  WE STARTED
;*****       BY SEEKING AN INTEGER, 'TRUE_ILOG' = FLOOR[TRUE_LOG10('X')].  THEN
;*****
;*****          10^('TRUE_ILOG'+1) < ('X') <= 10^('TRUE_ILOG')
;*****
;*****       IS A VALID EQUATION AS LONG AS WE ARE USE THE FUNCTION TRUE_LOG10.
;*****
;*****       HOWEVER, AS EXPLAINED PREVIOUSLY, WE WERE ONLY ABLE TO GET AN
;*****       APPROX_LOG10('X') WHICH COULD HAVE PRODUCED
;*****
;*****          ('TRUE_ILOG' - 1) <= 'ILOG' <= ('TRUE_ILOG' + 1)
;*****
;*****       THIS ERROR IN 'ILOG' CAUSES A SIMILAR ERROR IN 'SCALE' WHICH
;*****       RESULTS IN 'Y' NOT FITTING INTO A 'LEN' DIGIT DECIMAL STRING.
;*****       ANOTHER WAY OF SAYING THIS IS THAT THE INTEGER PART OF 'Y'
;*****       COULD BE OUTSIDE OF THE RANGE
;*****
;*****          10^('LEN'-1)  <  INT('Y')  <=  10^('LEN')
;*****
;*****       IF INT('Y') HAS BEEN EFFECTED BY THIS ERROR IN CALCULATING 'ILOG'
;*****       THEN ADJUST 'ILOG' AND RECALCULATE THE INT('Y').
;*****
;*****       IF ['Y' > 10^'LEN'] THEN
;*****
;*****          'ILOG' := 'ILOG' + 1
;*****          GO BACK TO RECALCULATE INT('Y)
;*****
;*****       ENDIF
;*****
;*****       IF ['Y' = 10^'LEN'] THEN
;*****
;*****          'ILOG' := 'ILOG' + 1
;*****          INT('Y') := INT('Y') / 10
;*****
;*****       ENDIF
;*****
;*****       IF ['Y' < 10^('LEN' - 1)] AND (INPUT VALUE WAS NORM) THEN
;*****
;*****          'ILOG' := 'ILOG' - 1
;*****          GO BACK TO RECALCULATE INT('Y)
;*****
;*****       ENDIF  (THIS TEST IS ARGUABLE BUT IS INCLUDED FOR SAFETY).
;*****
;*****
;******************************************************************************
;**
;**
;**       WHERE ARE THE VARIABLES.
;**
;**
;**       REGISTER D0 - D2       10^('LEN') = (TYPE NORM)
;**
;**       REGISTER D4 - D6       INT('Y') = TAGGED[TYPE NORM, NON-ZERO-UNNORM,
;**                                                               OR TRUE-ZERO]
;**       REGISTER FP_N          TRASHED BY mc%pwrten
;**
;** A7 -> SAVED ON STACK         'ILOG'  (WORD)
;**
;**       SAVED ON STACK         'LEN'  (WORD)
;**
;**       SAVED ON STACK         'K'  (WORD)
;**
;**       SAVED ON STACK         'RECALC_CTR'  (WORD)
;**
;**       SAVED ON STACK         (INPUT VALUE)  (3 LONG WORDS)
;**
;**       SAVED ON STACK         ORIGINAL CONTROL REGISTER  (WORD)
;**
;**
;**    *****************************************************************
;
;
;      CHECK TO SEE IF ROUNDING ERRORS IN CALCULATING 'ILOG' HAVE PUT
;      THE INTEGER PART 'Y' OUTSIDE OF THE RANGE
;
;          10^('LEN'-1)  <  INT('Y')  <  10^('LEN')
;
;      OR IF  INT('Y')  =  10^('LEN')  EXACTLY.  ADJUST 'ILOG' AND RECALCULATE
;      INT('Y') IF NEED BE.
;
;
;      COMPARES SHOULD BE MAGNITUDE ONLY SINCE 'LEN' ALWAYS POSITIVE.
;      SIGN OF ORIGINAL (INPUT VALUE) WAS RESTORED TO 'Y' BEFORE CALLING
;      INTEGER PART; TAKE ABS[INT('Y')] BEFORE DOING ANY COMPARES.
;
;      MOVE ABS[INT('Y')] INTO FP_REG FOR 'CMP' SUBROUTINE.  'CMP' SUBROUTINE
;      TRASHES ALL DATA REGISTERS; SETS CONDITION CODES IN IOPSTATUS REG
;      ACCORDING TO [FPN - <EA>] WHERE <EA> IS OPERAND IN REGS. D0-D2.
;
;
        BCLR     #31,D4                      ; TAKE ABS[INT('Y')]
        MOVE.W   STATUS(A3),-(A7)            ; SAVE CURRENT STATUS REG
        CLR.B    IOPSTATUS(A3)               ; CLEAR IOPSTATUS REG - SINCE
;                                              CMP ONLY DIDDLES WITH CMPCC
;                                              BITS, THIS ALLOWS CMPI INSTR.
        MOVEM.L  D4-D6,(A2)                  ; PUT ABS[INT('Y')] INTO FPN
;
;
;      ENTRY POINT mc%cmp_A CAN BE USED WHERE ABS[INT('Y')] IS (TYPE NORM
;      OR TRUE ZERO).  MUST USE ENTRY POINT mc%cmp_K WHERE ABS[INT('Y')]
;      IS (TYPE UNNORM) EVEN IF WE KNOW IT IS NON-ZERO-UNNORM.  NOTE THAT
;      INT('Y') WAS TYPED AFTER IT WAS CALCULATED.
;
;
;**    *****************************************************************
;**
;**
;**       WHERE ARE THE VARIABLES.
;**
;**
;**       REGISTER D0 - D2       10^('LEN') = (TYPE NORM)
;**
;**       REGISTER FP_N          ABS[INT('Y')] = TAGGED[TYPE NORM, NON-ZERO-
;**                                                    UNNORM, OR TRUE-ZERO]
;**
;** A7 -> SAVED ON STACK         ORIGINAL STATUS REGISTER  (WORD)
;**
;**       SAVED ON STACK         'ILOG'  (WORD)
;**
;**       SAVED ON STACK         'LEN'  (WORD)
;**
;**       SAVED ON STACK         'K'  (WORD)
;**
;**       SAVED ON STACK         'RECALC_CTR'  (WORD)
;**
;**       SAVED ON STACK         (INPUT VALUE)  (3 LONG WORDS)
;**
;**       SAVED ON STACK         ORIGINAL CONTROL REGISTER  (WORD)
;**
;**
;**    *****************************************************************
;
;
;      TEST FOR INT('Y') > 10^('LEN').
;
;      NOTE THAT ONLY ONE RECALCULATION IS ALLOWED; NOTE THAT CMP IS
;      DONE REGARDLESS OF VALUE OF 'RECALC_CTR' SINCE NEXT TEST NEEDS IT.
;
;
        BTST     #$1f,fract(A2)            ; TEST TYPE OF ABS[INT('Y')]
        beq.s    @27                         ; IF NOT(UNNORM OR DENORM)
           jSR      mc%cmp_A                       ; FCMP.E  10^('LEN'),INT('Y')
        bra.s    @28                         ; ELSE (UNNORM OR DENORM)
@27
           jSR      mc%cmp_K                       ; FCMP.E  10^('LEN'),INT('Y')
@28                                          ; END
        TST.W    8(A7)
        bne      @29                         ; IF('RECALC_CTR = 0) THEN
           CMPI.B   #GREATER,IOPSTATUS(A3)
           bne      @29                        ; IF [INT('Y') > 10^('LEN')]
              MOVE.W   (A7)+,STATUS(A3)            ; RESTORE SAVED STATUS REG
              MOVEM.W  (A7)+,D4/D6/D7              ; GET 'ILOG', 'LEN', & 'K'
              ADDQ.W   #1,(A7)                     ; INCREMENT 'RECALC_CTR'
              ADDQ.W   #1,D4                       ; INCREMENT 'ILOG'
              BRA      LEN_CALC                    ; RECALC. EVERYTHING ELSE
@29                                          ; END
;
;
;      TEST FOR INT('Y') = 10^('LEN').
;
;      NOTE:  NO NEED TO TEST 'RECALC_CTR' SINCE NO RECALCULATION IS DONE
;             EVEN IF INT('Y') = 10^('LEN').
;
;
        CMPI.B   #EQUAL,IOPSTATUS(A3)
        bne      @30                      ; IF [INT('Y') = 10^('LEN')]
;
;         NOTE THAT DIVIDING THE ABS[INT('Y')] BY TEN IS THE SAME AS
;         CALCULATING 10^('LEN' - 1).
;
           MOVE.W   4(A7),D7                    ; GET COPY OF 'LEN' FROM STACK
           SUBQ.W   #1,D7                       ; CALCULATE 10^('LEN' - 1)
           jSR      mc%pwrten                      ; ABS[INT('Y')] = 10^('LEN'-1)
;                                                 RETURNS RESULT IN REG. D0-D2
           MOVEM.L  D0-D2,(A2)                  ; PUT NEW ABS[INT('Y')] IN FPN
           ADDQ.W   #1,2(A7)                    ; INCREMENT 'ILOG' ON STACK
           BRA.S    BDCALC                      ; GO OUTPUT THE RESULT
@30                                         ; END
;
;
;      CALCULATE 10^('LEN' - 1).
;
;
        MOVE.W   4(A7),D7                    ; GET COPY OF 'LEN' FROM STACK
        SUBQ.W   #1,D7                       ; CALCULATE 10^('LEN' - 1)
        jSR      mc%pwrten                      ; RETURNS RESULT IN REGS. D0-D2
;
;
;      TEST FOR INT('Y') < 10^('LEN' - 1).
;
;      NOTE THAT ONLY ONE RECALCULATION IS ALLOWED; IF 'RECALC_CTR' IS
;      NOT EQUAL TO ZERO, THEN NO NEED TO EVEN TEST INT('Y').
;
;      THIS TEST ONLY APPLIES WHERE (ORIGINAL INPUT VALUE) WAS (TYPE NORM).
;      THIS IS THE SAME AS THE (ORIGINAL INPUT VALUE BEING OF
;      [TYPE NOT(UNNORM OR DENORM OR TRUE ZERO)]. HOWEVER, SINCE
;      TO GET HERE THE (ORIGINAL INPUT VALUE) CANNOT EVER BE AN INFINITY
;      OR NAN, THE NORM CHECK CAN BE PERFORMED ON THE MANTISSA_MSB OF
;      (ORIGINAL INPUT VALUE) WITHOUT LOOKING AT THE TAGS.
;
;      ???????????? WHY IS THIS NOT THE SAME AS ABS[INT('Y')] BEING OF
;      ???????????? [TYPE NOT(UNNORM OR DENORM OR TRUE ZERO)].
;
;      !!!!!!!!!!!! NOTE - DOES THE (3.3) & (3.4) CHANGE HERE MEAN THAT SOME
;                          TAG WORK PERFORMED EARLIER WAS EXTRANEOUS.
;
        TST.W    8(A7)
        bne      @31                         ; IF('RECALC_CTR = 0) THEN
           TST.B    14(A7)                      ; TEST (ORIG. IN. VAL. MTISSA.)
          bpl       @31                            ; IF (NORM)
              jSR      mc%cmp_A                       ; FCMP.E 10^('LEN'-1),
              CMPI.B   #LESS,IOPSTATUS(A3)                         ; INT('Y')
              bne      @31                         ; IF [INT('Y')<10^('LEN'-1)]
                 MOVE.W   (A7)+,STATUS(A3)            ; RSTORE SAVED STATUS REG
                 MOVEM.W  (A7)+,D4/D6/D7              ; GET 'ILOG', 'LEN',& 'K'
                 ADDQ.W   #1,(A7)                     ; INCREMENT 'RECALC_CTR'
                 SUBQ.W   #1,D4                       ; DECREMENT 'ILOG'
                 BRA      LEN_CALC                    ; RECALC EVERYTHING ELSE
@31                                            ; END
;                                                 ELSE (UNNORM, DENORM, OR
;                                                               TRUE ZERO)
;                                                    THIS TEST DOESN'T APPLY
;
;
;      GET HERE ONLY IF  {10^('LEN' - 1)  <  ABS[INT('Y')]  <  10^('LEN)}
;      OR ABS[INT('Y')] WAS (TYPE UNNORM OR DENORM).  THE LATTER CASE WILL
;      HAVE AN (OUTPUT MANTISSA STRING) WHICH IS "DENORMALIZED" TO REPRESENT
;      THE LACK OF 64 SIGNIFICANT BINARY BITS IN THE ORIGINAL INTERNAL FP
;      FORMAT.
;
;******************************************************************************
;*****
;*****
;*****       WHAT REMAINS TO BE DONE NOW THAT ABS[INT('Y')] HAS PASSED THE
;*****       ABOVE TEST?
;*****
;*****           ABS[INT('Y')]  =  MAGNITUDE (OUTPUT MANTISSA STRING)
;*****                             EXPRESSED AS AN EXTENDED PRECISION
;*****                             INTERNAL FORMAT FLOATING POINT NUMBER
;*****                             ROUNDED (PER THE USER SUPPLIED ROUNDING
;*****                             MODE) TO AN INTEGER VALUE.
;*****
;*****
;*****           'ILOG'         =  SIGN AND MAGNITUDE (OUTPUT EXPONENT STRING)
;*****                             EXPRESSED AS A TWO'S COMPLEMENT WORD
;*****                             LENGTH INTEGER
;*****
;*****       NEED TO CONVERT ABS[INT('Y')] INTO A 'LEN' DIGIT BCD STRING
;*****       WITH THE SIGN OF THE ORIGINAL (INPUT VALUE).
;*****
;*****       NEED TO CONVERT 'ILOG' INTO THE FOUR DIGIT BCD EXPONENT
;*****       AND ITS ACCOMPANYING SIGN.
;*****
;*****
;******************************************************************************
;
;
BDCALC  EQU   *
;
;      RESTORE ORIGINAL STATUS REGISTER FROM STACK.
;
;
        MOVE.W   (A7)+,STATUS(A3)            ; RESTORE SAVED STATUS REG
;
;
;**    *****************************************************************
;**
;**
;**       WHERE ARE THE VARIABLES.
;**
;**
;**       REGISTER FP_N          ABS[INT('Y')] = TAGGED[TYPE NORM, NON-ZERO-
;**                                                    UNNORM, OR TRUE-ZERO]
;**
;** A7 -> SAVED ON STACK         'ILOG'  (WORD)
;**
;**       SAVED ON STACK         'LEN'  (WORD)
;**
;**       SAVED ON STACK         'K'  (WORD)
;**
;**       SAVED ON STACK         'RECALC_CTR'  (WORD)
;**
;**       SAVED ON STACK         (INPUT VALUE)  (3 LONG WORDS)
;**
;**       SAVED ON STACK         ORIGINAL CONTROL REGISTER  (WORD)
;**
;**
;**    *****************************************************************
;
;
;      CONVERT ABS[INT('Y')] INTO A 'LEN' DIGIT BCD STRING.
;
;      NOTE THAT ABS[INT('Y')] IS IN FPN AND ALL DATA REGISTERS ARE DON'T
;      CARES.  'mc%binstr' WANTS EXPONENT OF ABS[INT('Y')] IN D0.W.  TRUE
;      ZERO MUST BE SPECIAL CASE SINCE ITS EXP = $C000.
;
;
        BTST     #ZERO_M,TAG(A2)
        beq      @32                         ; IF {ABS[INT('Y')] IS TRUE ZERO}
           moveq    #0,D0                       ; 'LOOPCNT' = 0
        bra      @33                         ; ELSE {ABS[INT('Y')] NOT
;                                                                  TRUE ZERO}
@32
           MOVE.W   EXP(A2),D0                  ; MAKE 'LOOPCNT' FOR mc%binstr THE
;                                                 EXPONENT OF ABS[INT('Y')]
@33                                          ; END
;
        jSR      mc%binstr                      ; RETURNS RESULT BCD STRING
;                                              PACKED INTO REGS D3-D7
;
;      WILL ACCUMULATE COMPLETE BCD STRING ON STACK.  MOVE THE (OUTPUT
;      MANTISSA STRING) NOW IN REGS. D3-D7 ONTO STACK.
;
;      CAN'T USE A7 TO MOVE STRING ONTO STACK SINCE IT WILL PUSH BYTES
;      AS WORDS - USE REGISTER A0 TO PUSH STRING.
;
;      SINCE THERE IS A SUBROUTINE CALL BEFORE THE STRING IS ALL PUSHED,
;      IF (A7) IS LEFT POINTING AT THE OLD STACK, THE SUBROUTINE RETURN
;      ADDRESS WILL OVER-WRITE PART OF THE STRING!!! (ALSO, THE 'KDM'
;      TRACE MODE USES (A7) STACK FOR TEMP STORAGE).
;
;      MOVE (A7) UP TO WHERE TOP OF STACK WILL BE WHEN STRING ALL PUSHED.
;
;
        MOVE.L   A7,A0                       ; SAVE POINTER TO CURRENT STACK
;                                              AS (A7); USE (A0) TO PUSH.
;
        SUB.L    #14,A7                      ; MOVE (A7) UP TO WHERE TOP OF
;                                              STRING WILL BE
;
        MOVE.B   D7,-(A0)                    ; TRANSFER (OUTPUT MTISSA STRING)
        MOVE.B   D6,-(A0)                    ; TO STACK
        MOVE.B   D5,-(A0)
        MOVE.B   D4,-(A0)
        MOVE.B   D3,-(A0)
        SWAP     D7                             ; ACCESS UPPER 8 BCD DIGITS
        SWAP     D6
        SWAP     D5
        SWAP     D4
        MOVE.B   D7,-(A0)
        MOVE.B   D6,-(A0)
        MOVE.B   D5,-(A0)
        MOVE.B   D4,-(A0)
;
;
;      NOTE: mc%binstr RETURNED 18 PACKED BCD DIGITS, ONLY 18 OF WHICH CAN
;            EVER BE VALID ('LEN' <= 18).  THE EXTRa DIGIT WILL ALWAYS
;            BE A '0' WHICH IS WHAT WE WANT.
;
;      HAVE NOW PUT ONLY 18 BCD DIGITS, PACKED INTO BYTES, ONTO STACK.
;      NEED TO PUT ONE MORE '0' BCD DIGIT AND A NYBBLE OF DON'T CARE ON STACK.
;
;      NOTE: NIBBLE OF DON'T CARE IS GUARANTEED A '0' BELOW - (HELPS TESTING).
;
        moveq    #0,D0
        MOVE.B   D0,-(A0)
;
;
;      CONVERT ABS('ILOG') INTO A 4 DIGIT BCD STRING.
;      SINCE 'ILOG' IS A WORD LENGTH INTEGER AND 'mc%binstr' IS OPTIMIZED FOR
;      CONVERTING FP NUMBERS WHICH HAVE BEEN ROUNDED TO AN INTEGER, LEFT
;      JUSTIFY THE WORD LENGTH INTEGER, 'ILOG', AND PLACE IT INTO THE
;      MANTISSA FIELD OF FPN-REG.  NOW THE MANTISSA FIELD OF FPN-REG LOOKS
;      LIKE A NORMALIZED MANTISSA.  WHAT WOULD BE THE EXPONENT OF THIS
;      FP VERSION OF 'ILOG' IS CALCULATED AS [15 - (NUMBER OF SHIFTS NEEDED
;                                                   TO RIGHT JUSTIFY 'ILOG')]
;      THIS NEED NOT BE PLACED IN FPN-REG SINCE 'mc%binstr' EXPECTS IT TO BE
;      IN REG D0.W
;
;
        MOVE.W   14(A7),D1                   ; GET COPY OF 'ILOG' OFF OLD STACK
        bpl      @34                         ; IF ('ILOG' NEGATIVE) THEN
           NEG.W    D1                          ; TAKE ABS VALUE
@34                                          ; END
;
;      NEED TO LEFT JUSTIFY THE ABS('ILOG') TO MAKE IT LOOK LIKE A NORMALIZED
;      FP MANTISSA; NEED TO CALCULATE THE EXPONENT OF THIS FP VERSION OF
;      ABS('ILOG').  NOTE THAT ABS('ILOG') <= 4933.
;      HANDLE ABS('ILOG') = 0 FIRST.
;
;
        bne     @35                          ; IF [ABS('ILOG') = 0] THEN
           MOVE.W   D1,D0                       ; EXP[(FP_ABS('ILOG')] = 0
        bra     l_just                       ; ELSE [ABS('ILOG') <> 0]
@35
           moveq    #14,D0                      ; LEFT JUSTIFY ABS('ILOG') AND
;                                                    CALC. EXP[FP_ABS('ILOG')]
JUST_ILOG  ASL.W    #1,D1                          ; SIMULTANEOUSLY.
           DBVS     D0,JUST_ILOG        ; +-->    IF (CCR V-BIT CLEAR) THEN
;                                         |          D0 := D0 - 1
;                                         |          IF  (PLUS) THEN
;         THE KEY HERE IS THAT D0 WILL    |             GO TO JUST_ILOG
;         NEVER GO TO (-1) SINCE A MAX    |          END
;         SHIFT OF 16 WOULD CAUSE THIS ---+       ELSE (CCR V-BIT SET)
;         CONDITIONAL TO FAIL, PREVENT-              THE V-BIT SETS WHEN
;         ING D0 FROM BEING DECR TO (-1).            THE MSB OF ABS('ILOG')
;                                                    GETS A '1' SHIFTED IN
;         LOOP TERMINATES WITH                    END
;            [0 <= REG.D0 <= 14]
;
;
l_just                                       ; END
;
        MOVE.W   D1,FRACT(A2)                ; PUT LEFT JUSTIFIED ABS('ILOG')
;                                              INTO FPN-REG MANTISSA FIELD.
;                                              'mc%binstr' WON'T LOOK AT TRASH
;                                              IN THE REST OF FRACT
        jSR      mc%binstr                      ; CONVERT TO BCD STRING -
;                                              RETURNS THE RESULT IN D6 & D7.
;
;      PUT 4 BCD DIGITS OF EXPONENT ONTO STACK.
;
;
        MOVE.B   D7,-(A0)
        MOVE.B   D6,-(A0)
;
;
;      CALCULATE 'P', SIGN OF MANTISSA AND SIGN OF EXPONENT AND STORE ON STACK.
;      NOTE THAT 'P' = 'LEN' - 1 ALWAYS.
;
        MOVE.W   14+2(A7),D0                 ; GET COPY OF 'LEN' OFF OLD STACK.
        SUBQ.W   #1,D0                       ; 'P' = 'LEN' -1
        MOVE.B   D0,-(A0)                    ; PUT BYTE OF 'P' ONTO STACK
;                                              NOTE THAT 0 <= 'P' <= 16
        TST.W    14(A7)                      ; TEST 'ILOG' ON OLD STACK
        SMI      D0                          ; IF ('ILOG' NEGATIVE)
;                                                 D0.B = $FF
;                                              ELSE ('ILOG' POSITIVE)
;                                                 D0.B = $00
;                                              END
        TST.B    14+8(A7)                    ; TEST (INPUT VALUE) ON OLD STACK.
        SMI      D1                          ; IF [(INPUT VALUE) NEGATIVE]
;                                                 D1.B = $FF
;                                              ELSE [(INPUT VALUE) POSITIVE]
;                                                 D1.B = $00
;                                              END
;
        ANDI.B   #$F0,D0                     ; MASK SIGN OF EXPONENT
        ANDI.B   #$0F,D1                     ; MASK SIGN OF MANTISSA
        OR.B     D0,D1                       ; COMBINE THE SIGNS
        MOVE.B   D1,-(A0)                    ; PUT BYTE OF (SIGN EXP - SIGN
;                                               MANTISSA) ONTO STACK
;
         ANDI.B   #<STIOP_B+STINEX_B>,TEMPSTAT(A3)
;                                              CLEAR ALL FLAGS, OTHER THAN
;                                               iop & INX FLAGS.
;
;      NOW READY TO USE THE 'STRSLT' MACRO TO MOVE THE STRING ACCUMULATED
;      ON THE STACK OUT INTO THE ORIGINAL CALLER'S MEMORY.
;
;      THE 'STRSLT' MACRO WANTS (A7) TO BE POINTING AT THE TOP
;      OF THE BCD STRING.  THE BCD STRING IS 14 BYTES LONG.
;
;      NOTE: 'STRSLT' REMOVES THE BCD STRING FROM THE STACK.
;            THEREFORE,  MUST CLEAN 'ILOG', 'LEN', 'K',
;            'RECALC_CTR' OFF STACK.  RESTORE (INPUT VALUE)
;            TO FPN REG. AND ORIGINAL CONTROL WORD TO CONTROL
;            REG.
;
;
        STRSLT   14,BDEC_END                 ; STORE THE 14 BYTE BCD STRING
;                                              OUT TO ORIGINAL CALLER'S MEMORY
;
        ADDA.L   #8,A7                       ; CLEAN UP STACK
        MOVE.L   (A7)+,(A2)                  ; RESTORE (INPUT VALUE) TO FPN
        MOVE.L   (A7)+,4(A2)
        MOVE.L   (A7)+,8(A2)
        MOVE.W   (A7)+,CNTRL(A3)             ; RESTORE ORIGINAL CONTROL WORD
;
BDEC_END  RTS
;
;
;******************************************************************************
;
;
CALLSTR   EQU    *
;
;
;      'SPECIAL VALUE' BCD STRING NOW ON STACK; MOVE IT OUT TO CALLER'S MEMORY.
;      THE 'STRSLT' MACRO WANTS (A7) TO BE POINTING AT THE TOP
;      OF THE BCD STRING.  THE BCD STRING IS 14 BYTES LONG.
;
;      NOTE: 'STRSLT' REMOVES THE BCD STRING FROM THE STACK.
;
        STRSLT   14,SPBDEC_END               ; STORE THE 14 BYTE BCD STRING
;                                              OUT TO ORIGINAL CALLER'S MEMORY
;
SPBDEC_END  RTS                              ; END OF 'BINDEC' IF INPUT IS
;                                              A SPECIAL VALUE!
        debugend 'MC%BINDE',0
;
;******************************************************************************
;******************************************************************************

ÿ