{ Copyright 1983, Apple Computer Inc. }

(* File L:FPLib2, Version 9 February 1983. *)

(* Contains all the extra FPLIB procedures left out elsewhere.
   These procedures support initialization and Basic. *)
   
procedure excepname ;
begin
case e of
Invop   : name := 'Invop' ;
Overfl   : name := 'Overfl' ;
Underfl   : name := 'Underfl' ;
Div0   : name := 'Div0' ;
Inxact   : name := 'Inxact' ;
Cvtovfl   : name := 'Cvtovfl' ;
Fp_Xcpn6   : name := 'Fp_Xcpn6' ;
Fp_Xcpn7   : name := 'Fp_Xcpn7' ;
end (* case *) ;
end ;

function getclos (*: closure*) ;
begin 
if odd(abs(fpcb_.block.status.mode) div 4) 
        then getclos := affine 
        else getclos := proj ;
end   ;

procedure setclos (* x : closure *) ;
var k : integer ;
begin 
k := getbite2(fpcb_.block.status.mode) ;
if x = proj then bclr(k,2) else bset(k,2,true) ;
fpcb_.block.status.mode := setbite2(k) ;
end   ;

procedure setdnorm (* x : denorm *) ;
var k : integer ;
begin 
k := getbite2(fpcb_.block.status.mode) ;
if x = warning  then bclr(k,3) else bset(k,3,true) ;
fpcb_.block.status.mode := setbite2(k) ;
end   ;

function getdnorm (*: denorm*) ;
var k : integer ;
begin 
k := getbite2(fpcb_.block.status.mode) ;
if  btst(k, 3) then getdnorm := normalizing else getdnorm := warning ;
end   ;

procedure setprec (* x : extprec *) ;
var k : integer ;
begin
k := getbite2(fpcb_.block.status.mode) ;
case x of 
xprec : begin
        bclr(k,4) ;
        bclr(k,5) ;
        end   ;
sprec : begin
        bclr(k,5) ;
        bset(k,4, true) ;
        end   ;
dprec : begin
        bclr(k,4) ;
        bset(k,5, true) ;
        end   ;
end   ;
fpcb_.block.status.mode := setbite2(k) ;
end   ;

function getprec (*: extprec*) ;
var k : integer ;
begin 
k := getbite2(fpcb_.block.status.mode) ;
case (  2 * ord( btst(k,5) ) + ord ( btst(k,4) ) )   of
0 : getprec := xprec ;
1 : getprec := sprec ;
2 : getprec := dprec ;
end   ;
end   ;

procedure sethalt (* e : xcpn ; b : boolean *) ;
var
es : excepset ;
begin 
gettrapset( es ) ;
if b then settrapset( es + [e] ) else settrapset ( es - [e] ) ;
end   ;

function gethalt (* e : xcpn *) (*: boolean*) ;
var
es : excepset ;
begin 
gettrapset( es ) ;
gethalt := e in es ;
end   ;

procedure settrapset (* es : excepset *) ;
var
equiv : record case boolean of
        false : (e : excepset );
        true  : ( b : bite ) ;
        end ;
begin (* settrapset *)
equiv.e := es ;
fpcb_.block.status.trap := equiv.b ;
end   (* settrapset *) ;

procedure settrap ;
begin (* settrap *)
fpcb_.ptrapvector[e] := f ;
end   (* settrap *) ;

function gettrap ;
begin (* gettrap *)
gettrap := fpcb_.ptrapvector[e] ;
end   (* gettrap *) ;

procedure i64abs ;
        (* z := abs(x) *) 
        (* Note that NAN goes to NAN automatically *)
begin (* i64abs *)
if x[0] < 0 then i64neg( x, z) else z := x ;
end   (* i64abs *) ;

procedure i64mfp ;

var
nan : fp_extended ;
absx : fp_int64 ;
t : fp_register ;

begin (* i64mfp *)
i64abs( x, absx ) ; (* Get absolute value *)
if absx[0] < 0 then
        begin (* nan *)
        createnan( false, 0, naninteger, 0, nan ) ;
        xmovefp( nan ) ;
        end   (* nan *)
        else
        with t do
                begin (* number *)
                if x[0] < 0 then  sign := -128 else  sign := 0  ; (* Sign.  *)
                tag := tagnonnormal ;
                exponent := 63 ; (* Suitable exponent if fp is normalized.  *)
                fraction := absx ;
                fp%normalize( t ) ;
                movefp(t) ;
                end   (* number *) ;
end   (* i64mfp *) ;

procedure i64mfp1 ;

var
nan : fp_extended ;
absx : fp_int64 ;
t : fp_register ;

begin (* i64mfp1 *)
i64abs( x, absx ) ; (* Get absolute value *)
if absx[0] < 0 then
        begin (* nan *)
        createnan( false, 0, naninteger, 0, nan ) ;
        xmovefp( nan ) ;
        end   (* nan *)
        else
        with t do
                begin (* number *)
                if x[0] < 0 then  sign := -128 else  sign := 0  ; (* Sign.  *)
                tag := tagnonnormal ;
                exponent := 63 ; (* Suitable exponent if fp is normalized.  *)
                fraction := absx ;
                fp%normalize( t ) ;
                movefp1(t) ;
                end   (* number *) ;
end   (* i64mfp1 *) ;

procedure fpmovei64 (*  : fp_int64 *)  ;

var
fp, fp2 : fp_register ;

procedure i64zero ;
var i : integer ;
begin (* i64zero *)
for i := 0 to 7 do z[i] := 0 ;
end   (* i64zero *) ;

procedure i64nan ;
var i : integer ;
begin (* i64nan *)
for i := 1 to 7 do z[i] := 0 ;
z[0] := -128 ;
end   (* i64nan *) ;

procedure i64inf ;
begin (* i64inf *)
i64nan ;
setxcpn( cvtovfl, true ) ;
end   (* i64inf *) ;

begin (* fpmovei64 *)
fpmove ( fp ) ;
with fp do case tag of

tagzero : i64zero ;
taginf : i64inf ;
tagnan : i64nan ;

tagnonnormal, tagnormal :
        begin (* NonNormal/normal *)
        fpint ;
        fpmove ( fp2 ) ; (* fp2 gets rounded FP0. *)
        movefp ( fp ) ; (* FP0 gets original value. *)
        fp%normalize( fp2 ) ; (* fp2 gets rounded normalized argument. *)
        with fp2 do
                begin
                if tag = tagzero then i64zero
                        else
                if exponent > 62 then i64inf
                        else
                        fp%i64( fp2, z ) ;
                end ;
        end   (* NonNormal/normal *) ;
        
end (* case *) ;
end   (* fpmovei64 *) ;

procedure logb ;
        (* z gets the exponent of x.  *)
var
r : fp_extended ;
fpcb : fp_blocktype ;
trapcoming : boolean ;

begin
blockprelude(fpcb) ; (* Save environment, clear exceptions and traps.  *)
case fpkindx(x) of
zero : begin
        infinity(r) ;
        fpnegx(r,r) ; (* logb(0) = -INF.  *)
        setxcpn(div0, true ) ;
        end   ;
NonNormal, normal : begin
        wmovex(ilogb(x), r ) ;
        end   ;
inf : fpabsx(x,r)  ; (* Logb(INF) = +INF.  *)
nan : checknan(x,r)  ;
end   (* case *) ;
blockpostlude( fpcb, trapcoming) ;
if trapcoming then postmonadic( 'logb',x,r) ;
z := r ;
end   ;

procedure nextafter ;
        (* z gets next number x in direction y.  *)
var 
er : integer ;
maxexp, minexp, leastexp, lsbite, lsval : integer ; (* Precision parameters. *)
r, u : fp_extended ;
cc : fp_cc ;
fpcb : fp_blocktype ;
trapcoming : boolean ;

procedure precparms ; (* Sets up precision parameters according to getprec. *)
begin (* precparms *)
case getprec of
xprec : begin
        lsbite := 9 ; lsval := 1 ;
        maxexp := $3ffe ; minexp := $c001 ; leastexp := minexp ;
        end ;
dprec : begin
        lsbite := 8 ; lsval := 8 ;
        maxexp := $3ff  ; minexp := $fc02 ; leastexp := minexp - 52 ;
        end ;
sprec : begin
        lsbite := 4 ; lsval := 1 ;
        maxexp := $7f ; minexp := $ff82 ; leastexp := minexp - 23 ;
        end ;
end (* case getprec *) ;
end   (* precparms *) ;

procedure ulp( var z : fp_extended ) ; (* Creates absolute least ulp of x. *)

var i : integer ;

begin (* ulp *)
fpabsx(x,z) ;
for i := 2 to 9 do z[i] := 0 ;
if leastexp < minexp then
        begin (* s and d *)
        z[2] := -128 ; (* One ulp of least exponent. *)
        setexponent( z, leastexp, z) ; (* Postive sign, least exponent. *)
        end   (* s and d *)
        else
        begin (* x *)
        z[lsbite] := lsval ;
        setexponent( z, leastexp-1, z) ; (* Postive sign, least exponent. *)
        end   (* x *) ;
end   (* ulp *) ;

procedure maxprec ( var z : fp_extended ) ; (* Creates maximum significand. *)
var i : integer ;
begin (* maxprec *)
setexponent( x, maxexp , z) ;
for i := 2 to (lsbite-1) do z[i] := -1 ;
z[lsbite] := -lsval ;
for i := (lsbite+1) to 9 do z[i] := 0 ;
end   (* maxprec *) ;

begin (* nextafter *)
blockprelude(fpcb) ; (* Save old floating point environment and standardize.  *)

cc := fpcomx(x,y) ;
case cc of

equal : r := x ; (* If x=y, then r=x.  *)
unord : begin (* unord *)
        if (fpkindx(x) = nan) or (fpkindx(y) = nan) 
                then choosenan( x, y, r)
                else r := x ; (* Allow for projective infinity. *) ;
        end (* unord *) ;

lesser, greater : 
        begin (* x<>y *)
        precparms ;
        if fpkindx(x)=inf then 
                begin (* x is inf *)
                maxprec(r) ;
                end (* x is inf *)
        else 
                begin (* x is zero, norm, or unnorm.  *)
                ulp(u)  ; (* u gets smallest possible increment.  *)
                if cc = lesser then 
                        begin
                        setround(rpos) ;
                        fpaddx(x,u,r) 
                        end
                        else 
                        begin
                        setround(rneg) ;
                        fpsubx( x, u, r ) ;
                        end ;
                er := getexponent(r) ;
                if er > maxexp then 
                        begin 
                        setxcpn( overfl, true ) ;
                        setxcpn( inxact, true ) ;
                        infinity(r) ;
                        copysign( x, r, r) ;
                        end 
                        else if er < minexp then
                        begin
                        if (er > -$4000) or (r[2] >= 0) then 
                                (* Exclude strange extendeds with minimum exponent
                                   and normalized significands.  *)
                                begin (* set underflow/inexact *)
                                setxcpn( underfl, true ) ;
                                setxcpn( inxact, true ) ;
                                end   (* set underflow/inexact *) ;
                        if fpkindx(r) = zero then copysign( x, r, r) ;
                        end 
                        else 
                        begin
                        clrexcepset ;
                        end ;
                end (* x is zero, norm or unnorm.  *)
        end (* x<>y *) ;
end (* case *) ;

blockpostlude(fpcb, trapcoming) ;
if trapcoming then postdyadic( 'nextafter',x,y,r) ;
z := r ;
end   (* nextafter *) ;

procedure pmoved ;
var
i, next : integer ;
fp : fp_register ;
error : boolean ;
xx : fp_extended ;

begin (* pmoved *)
fpmove ( fp ) ;
pmovefp( s, 1, length(s), next, error ) ;
for i := next to length(s) do error := error or (s[i] <> ' ') ;
        (* It's all right to have trailing blanks, but otherwise an
        incomplete scan is an error. *)
if error then
        begin
        errornan ( nanascbin, xx ) ;
        xmoved ( xx, x ) ;
        end 
        else
        fpmoved(x) ;
movefp( fp ) ;
end   (* pmoved *) ;

procedure pmovex ;
var
i, next : integer ;
fp : fp_register ;
error : boolean ;

begin (* pmovex *)
asciimovex( false, input, s, 1, length(s), next, x, error ) ;
for i := next to length(s) do error := error or (s[i] <> ' ') ;
        (* It's all right to have trailing blanks, but otherwise an
        incomplete scan is an error. *)
if error then errornan ( nanascbin, x ) ;
end   (* pmovex *) ;

procedure x80_eform ;
begin
x_eform( x, maxek, sigma, s, e) ;
end ;

procedure x80_maxform (* var x : fp_extended ;
                      var sigma : integer ; var s : fp_string ; var e : integer *) ;

        (* Converts finite x to sign * s * 10^e.
        S has maximum significance.
        Sigma is 0 if x has negative sign, 1 otherwise.
        x is normal zero or NonNormal if and only if
        one or more leading digits of S is 0.
                                                        *)

var (* x80_maxform *)
i : integer ;
intx, fracx : fp_extended ;
izero, fzero : boolean ;
fsigma, li, lf, ei, nz : integer ;
fracs : fp_string ;

begin (* x80_maxform *)
x80_break( x, intx, fracx, izero, fzero ) ; (* Get parts of x. *)
if fzero or izero then
        begin (* only one part *)
        x_eform( x, maxek, sigma, s, e ) ;
        end   (* only one part *)
        else
        begin (* two parts *)
        setxcpn(inxact, false) ;
        x_iform( intx, sigma, s, ei ) ; (* Get integer part of x. *)
        if getxcpn( inxact) then
                begin (* too large to convert correctly *)
                e := ei ;
                end   (* too large to convert correctly *)
                else
                begin (* intx converted correctly *)
                x_eform( fracx, maxek, fsigma, fracs, e ) ;
                        (* Get fraction part of x. *)
                li := length(s) ;
                lf := length(fracs) ;
                nz := ei - e - lf ; (* Number of zeros to place between s and fracs. *)
                for i := (li+1) to (li+nz) do s[i] := '0' ; (* Append zero. *)
                li := li + nz ;
                for i := 1 to lf do s[li+i] := fracs[i] ;
                        (* Concatenate int and frac. *)
                s[0] := chr(li+lf) ; (* Adjust final length. *)
                end   (* intx converted correctly *) ;
        end   (* two parts *) ;
end   (* x80_maxform *) ;

procedure dmovep ;
const
width = 24 ;
var
xx : fp_extended ;
error : boolean ;

begin (* dmovep *)
dmovex( x, xx) ;
x80_free_ascii( xx, width, maxlisa, fp_lisa, s, error) ;
while length(s) < width do s := concat(' ',s) ;
end   (* dmovep *) ;

procedure xmovep ;
const
width = 27 ;
var
error : boolean ;

begin (* xmovep *)
x80_free_ascii( x, width, maxlisa, fp_lisa, s, error) ;
while length(s) < width do s := concat(' ',s) ;
end   (* xmovep *) ;

function nextrandom  ;

const A = 16807; B15 = 32768; B16 = 65536;
var xhi,xalo,leftlo,fhi,k: longint;

begin {nextrandom}
if (lastrandom <= 0) or (lastrandom >= randmodulus) then 
        lastrandom := randmodulus div 2 ;
        { Without fail, we must have 1 <= lastrandom <= randmodulus-1. }
xhi := lastrandom div B16;
xalo := (lastrandom - xhi*B16)*A;
leftlo := xalo div B16;
fhi := xhi*A + leftlo;
k := fhi div B15;
lastrandom := (((xalo - leftlo*B16) - randmodulus) + (fhi - k*B15)*B16) + k;
if lastrandom < 0 then lastrandom := lastrandom + randmodulus;
        { Without fail, we will have 1 <= nextrandom <= randmodulus-1. }
nextrandom := lastrandom ;
end; {nextrandom}

ÿ