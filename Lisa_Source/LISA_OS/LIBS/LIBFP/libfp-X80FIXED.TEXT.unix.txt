{ Copyright 1983, Apple Computer Inc. }


(* File L:X80Fixed, Version 6 February 1983. *)

procedure postoutput ( name : fp_string ; var x : fp_extended ; var s : fp_string ) ;
        (* Routine to set up trapping for a Pascal Real Arithmetic fp_extended to ascii
        routine. *)

var
r : fp_traprecord ;

begin (* postoutput *)
with r do
        begin (* with *)
        gettexcepset( es ) ;
        optype1 := tfp_extended   ;
        op1 :=  @ x  ;
        op2 := nil ;
        resulttype := tfp_string ;
        result :=  @ s   ;
        procname := @ name ;
        fp_postoperation ( r) ;
        end   (* with *) ;
end   (* postoutput *) ;

procedure x_integral ( var x, intx : fp_extended ; var xzero : boolean ) ;

        (* intx := integral(normal(x)).
        xzero is true if intx is zero. *)

begin (* x_integral *)
xmovefp(x) ;  (* FP0 := x. *)
fp%normalize(fpcb_.block.f[0]) ; (* FP0 := normalize(x). *)
fpint ;       (* FP0 := int(x). *)
xzero := fpkind = zero ;
fpmovex( intx ) ; (* intx := int(x). *)
end   (* x_integral *) ;

procedure x_fform ( var x : fp_extended ; after : integer ; allafter : boolean ;
        var s : fp_string ) ;

        (* Computes s := x for s of form 'ddd.ddddd'.
              If allafter is true then there will be exactly after digits after
              the point; otherwise there may be less than after.
              There may be digits before the point; if so the leading digit is
      not zero.  The caller should insure that 1 <= after <= maxfpstring-1
      and that length(s) will not exceed maxfpstring.
                                                        *)
var (* x_fform *)
i, is, e, p : integer ;
adjust, excess, ncall, ifirst, ilast : integer ;
b : fp_bcdstring ;

procedure app ( c : char ) ; (* Appends one character to s *)
begin
is := is + 1 ;
s[is] := c ;
end ;

function bnib( i : integer ) : nibble ;
        (* Returns logical nibble b[i], reversing odd-even pairs *)
begin (* bnib *)
if odd(i) then bnib := b[i-1] else bnib := b[i+1] ;
end   (* bnib *) ;

function dec ( i : integer ) : char ;
        (* Returns ascii digit corresponding to logical nibble b[i] *)
begin (* dec *)
if odd(i) then dec := chr(48 + b[i-1]) else dec := chr(48 + b[i+1] ) ;
end   (* dec *) ;

begin (* x_fform *)
(*error := (after <= 0) or (after >= maxfpstring) ;
if not error then
        begin*)
        if after <= maxfk then ncall := -after else ncall := -maxfk ;
        xmovefp(x) ;
        x80%paddr( ncall, @b ) ;
        p := 16 * b[3] + b[2] ;
        e := ((b[5] * 10 + b[4]) * 10 + b[7]) * 10 + b[6] ;
        if b[1] = $f then e := -e ;
        adjust := after + e - p ;
        if adjust < 0 then adjust := 0 ; (* Number of zeros to add at end. *)
        excess := fpbuffer + adjust - after ; (* Excess digits before point. *)
        (*error := excess < (20 - maxfpstring) ;
        if not error then
                begin*)
                is := 0 ;
                if excess <= 0 then
                    begin (* prepend excess zeros to position point *)
                    app('.') ; (* prepend point *)
                    for i := 1 to (-excess) do app('0') ; (* prepend zero *)
                    ifirst := 9 ;
                    end   (* prepend excess zeros to position point *)
                    else
                    begin (* get digits before point *)
                    ilast := excess + 8 ;
                    if ilast > 27 then ilast := 27 ;
                        (* ilast is last digit before point *)
                    ifirst := 9 ;
                    while (bnib(ifirst) = 0) and (ifirst <= ilast) do
                            ifirst := ifirst + 1 ;
                            (* ifirst is first nonzero digit before point. *)
                    for i := ifirst to ilast do app(dec(i)) ;
                        (* Append digits before point. *)
                    app('.') ;
                    ifirst := excess + 9 ;
                    end   (* get digits before point *) ;
                ilast := 27 ;
                for i := ifirst to ilast do (* convert bcd string *)
                        app(dec(i)) ;
                if not allafter then adjust := 0 ; (* Don't add zeros at end. *)
                (*error := (is + adjust) > maxfpstring ;
                if not error then*)
                        for i := 1 to adjust do (* append zero *)
                                app('0') ;
                s[0] := chr(is) ;
                (*end ;
        end ;*)
end   (* x_fform *) ;

procedure x_eform (* var x : fp_extended ; n : integer ;
                      var sigma : integer ; var s : fp_string ; var e : integer *) ;

        (* Converts x to sign * s * 10^e.  1 <= length(s) <= n.
        If length(s) < n then the difference represents implied trailing
        zeros that would be present if length(s) had been forced to be
        exactly n.
        Sigma is 0 if x has negative sign, 1 otherwise.
        x is normal zero or NonNormal if and only if
        one or more leading digits of S is 0.
                                                        *)

var (* x_eform *)
i : integer ;
ncall, ifirst, ilast : integer ;
b : fp_bcdstring ;

function bnib( i : integer ) : nibble ;
        (* Returns logical nibble b[i], reversing odd-even pairs *)
begin (* bnib *)
if odd(i) then bnib := b[i-1] else bnib := b[i+1] ;
end   (* bnib *) ;

function dec ( i : integer ) : char ;
        (* Returns ascii digit corresponding to logical nibble b[i] *)
begin (* dec *)
if odd(i) then dec := chr(48 + b[i-1]) else dec := chr(48 + b[i+1] ) ;
end   (* dec *) ;

begin (* x_eform *)
        if n <= maxek then ncall := n else ncall := maxek ;
        xmovefp(x) ;
        x80%paddr( ncall, @b ) ;
        if b[0] = $f then sigma := 1  else sigma := 0  ;
        e := ((b[5] * 10 + b[4]) * 10 + b[7]) * 10 + b[6] ;
        if b[1] = $f then e := -e ;
        ifirst := 28 - ncall ;
        ilast := 27 ;
        while bnib( ilast ) = 0 do ilast := ilast - 1;
        if ilast < ifirst then ilast := ifirst ;
        for i := ifirst to ilast do s[i-ifirst+1] := dec(i) ;
        s[0] := chr(ilast-ifirst+1) ;
        e := e - ilast + ifirst ;
end   (* x_eform *) ;

procedure x_iform (*
        var x : fp_extended ;
        var sigma : integer ; var s : fp_string ; var e : integer *) ;

        (* s := ascii representation of finite x.
        sign * s * 10^e = x; x must be normalized and integral.
        Sigma is 0 for positive x, 1 for negative x.
        e will be >= 0.
        If x is zero then s will be '0'.
        *)


begin (* x_iform *)
if getexponent(x) >= 32
        then
        begin (* use x80 arithmetic *)
        x_eform( x, maxek, sigma, s, e ) ;
        if e < 0 then s[0] := chr(length(s)+e) ; (* Remove trailing fractional zeros. *)
        end   (* use x80 arithmetic *)
        else
        begin (* use x48 arithmetic *)
        x80%todec ( x, s ) ;
        e := 0 ;
        if fpminusx(x) then sigma := 1 else sigma := 0 ;
        end   (* use x48 arithmetic *) ;
end   (* x_iform *) ;

procedure x80_i_ascii (*
        var x : fp_extended ;
        var s : fp_string ; var error : boolean*) ;

        (* s := ascii representation of finite x rounded to an integral value.
        If x rounds to zero then s will be '0' or '-0'.
        The length of s will be minimal but >= 1+sigma.
        Error is true, and s is undefined, if the length of s would exceed
        maxfpstring. *)


var
intx : fp_extended ;
i, ls, e : integer ;
sigma : integer ;
xzero : boolean ;
fpcb : fp_blocktype ;
trapcoming : boolean ;

begin (* x80_i_ascii *)
blockprelude(fpcb) ;
x_integral( x, intx, xzero ) ;
if xzero then
        fp_zero_ascii( fpminus, 0, 0, fp_i, s, error )
        else
        begin (* nonzero *)
        x_iform ( intx, sigma, s, e ) ;
        ls := length(s) ; (* ls gets returned length of s. *)
        error := (sigma + ls + e) > maxfpstring ;
        if not error then
                begin (* length OK *)
                for i := (sigma + ls + 1) to (sigma + ls + e) do s[i] := '0' ;
                        (* Pad extra zeros. *)
                if sigma = 1 then
                        begin (* negative *)
                        for i := ls downto 1 do s[i+1] := s[i] ; (* Shift. *)
                        s[1] := '-' ; (* Insert sign. *)
                        end   (* negative *) ;
                s[0] := chr(sigma + ls + e) ; (* Set final length. *)
                end   (* length OK *) ;
        end   (* nonzero *) ;
blockpostlude( fpcb, trapcoming ) ;
if trapcoming then postoutput( 'x80_i_ascii',x,s) ;
end   (* x80_i_ascii *) ;

procedure x80_f_ascii (*
        var x : fp_extended ; beforepoint : boolean ; after : integer ;
        var s : fp_string ; var error : boolean*) ;

        (* s := ascii representation of finite x displayed in fixed point
        notation with after>=0 digits after the point.
        If beforepoint is true then s will always have at least one digit before
        the point.  If beforepoint is false and abs(x) < 1 then s may have no
        digits before the point.
        The length of s will be minimal but >= after+1+sigma.
        Error is true, and s is undefined, if the length of s would exceed
        maxfpstring. *)

var
is, li, lf, e, i, sigma, oflo : integer ;
intx, fracx : fp_extended ;
ints, fracs : fp_string ;
izero, fzero : boolean ;
fpcb : fp_blocktype ;
trapcoming : boolean ;

procedure app ( c : char ) ; (* Appends one character to s *)
begin
is := is + 1 ;
s[is] := c ;
end ;

begin (* x80_f_ascii *)
blockprelude(fpcb) ;
if after >= 1 then
        x80_break ( x, intx, fracx, izero, fzero )  (* Break x up into pieces. *)
        else
        begin (* after=0 *)
        x_integral ( x, intx, izero ) ;
        fzero := true ;
        end   (* after=0 *) ;
if izero then
        begin (* integral part is zero *)
        if fpminusx(x) then sigma := 1 else sigma := 0 ; (* do sign *)
        if beforepoint then
                begin (* leading zero *)
                li := 1 ; ints[1] := '0' ;
                end   (* leading zero *)
                else
                li := 0 ;
        e := 0 ;
        end   (* integral part is zero *)
        else
        begin (* integral part nonzero *)
        x_iform( intx, sigma, ints, e ) ; (* Get string for int part. *)
        li := length(ints) ; (* li gets length of ints. *)
        end   (* integral part nonzero *) ;
error := (after < 0) or ((sigma + li + e + 1 + after) > maxfpstring ) ;
if not error then
        begin (* no error *)
        for i := 1 to e do ints[li+i] := '0' ; (* pad extra zeros *)
        li := li+e ; (* adjust length *)
        oflo := 0 ; (* Default case. *)
        if (after = 0) or getxcpn(inxact) or fzero then
                begin (* no fraction string *)
                lf := 0 ;
                end   (* no fraction string *)
                else
                 begin (* get fraction *)
                 x_fform( fracx, after, true, fracs ) ;
                 lf := length(fracs) ;
                 if fracs[1] <> '.' then
                        begin (* .999 rounded to 1.00 *)
                        i := li ;
                        while (i >= 1) and (ints[i] = '9') do
                                begin (* decimal carry *)
                                ints[i] := '0' ;
                                i := i - 1 ;
                                end   (* decimal carry *) ;
                        if i < 1 then
                                begin (* overflow *)
                                izero := false ;
                                oflo := 1 ;
                                end   (* overflow *)
                                else
                                begin (* no overflow *)
                                ints[i] := chr(ord(ints[i])+1) ; (* Increment. *)
                                        (* Should be SUCC but for 10.2 compiler bug *)
                                end   (* no overflow *) ;
                        end   (* .999 rounded to 1.00 *) ;
                 end   (* get fraction *) ;
        error := error or ((sigma + oflo + li + after + 1) > maxfpstring) ;
        if not error then
                begin
                is := 0 ;
                if sigma = 1 then app('-') ;
                if oflo = 1 then app('1') ; (* Prepend overflow digit. *)
                for i := 1 to li do app(ints[i]) ;
                if lf = (after+1) then
                        begin (* normal case *)
                        for i := 1 to lf do app(fracs[i]) ;
                        end   (* normal case *)
                        else
                        begin (* overflow - fraction only zeros *)
                        app('.') ;
                        for i := 1 to after do app('0') ;
                        end   (* overflow - fraction only zeros *) ;
                s[0] := chr(is) ;
                end ;
        end   (* no error *) ;
blockpostlude( fpcb, trapcoming ) ;
if trapcoming then postoutput( 'x80_f_ascii',x,s) ;
end   (* x80_f_ascii *) ;

procedure x80_e_ascii (*
        var x : fp_extended ; before, after, ew : integer ;
        var s : fp_string ; var error : boolean*) ;

        (* s := ascii representation of finite x displayed in scientific
        notation with before digits before the point, after digits
        after the point, and ew exponent digits.
        The length of s will be minimal: before+after+ew+3+sigma.
        Error is true, and s is undefined, if before<0, after<0, ew<1, ew
        is too small for the exponent of x, or the length of s would exceed
        maxfpstring. *)

var (* x80_e_ascii *)
e, n, nc, i, ls, sigma : integer ;
fpcb : fp_blocktype ;
trapcoming : boolean ;

begin (* x80_e_ascii *)
blockprelude(fpcb) ;
if fpminusx(x) then sigma := 1 else sigma := 0 ; (* Length of sign field. *)
n := before + after ; (* Number of significant digits required. *)
nc := n + ew + 3+sigma ; (* Number of characters required. *)
error := (after < 0 ) or (before < 0) or (ew < 1) or (n < 1) or
        (nc > maxfpstring) ;
if not error then
        begin
        x_eform( x, n, sigma, s, e ) ;
        ls := length(s) ; (* Number of significant digits returned. *)
        e := e + after - n + ls ; (* Adjust e for trailing zeros. *)
        for i := n downto (ls+1) do s[i] := '0' ; (* Add trailing zero. *)
        for i := n downto (before+1) do s[i+1+sigma] := s[i] ;
                (* Make room for sign and point. *)
        s[before+1+sigma] := '.' ;
        if sigma > 0 then
                begin (* make room for sign *)
                for i := before downto 1 do s[i+1] := s[i] ;
                s[1] := '-' ;
                end   (* make room for sign *) ;
        s[nc-ew-1] := 'E' ;
        if e < 0 then
                begin
                s[nc-ew] := '-' ;
                e := -e ;
                end
                else
                s[nc-ew] := '+' ;
        for i := nc downto (nc-ew+1) do
                begin (* exponent digit *)
                s[i] := chr( 48 + e mod 10 ) ;
                e := e div 10 ;
                end   (* exponent digit *) ;
        s[0] := chr(nc) ; (* Set length(s). *)
        error := e <> 0 ; (* Still an error if exponent too big for ew. *)
        end ;
blockpostlude( fpcb, trapcoming ) ;
if trapcoming then postoutput( 'x80_e_ascii',x,s) ;
end   (* x80_e_ascii *) ;

procedure x80_nan_ascii(*
        var x : fp_extended ; width : integer ;
        var s : fp_string ; var error : boolean*) ;
        (* S := ascii representation of NaN x.  Only the sign and fraction
        of x are considered.
        If width=3, the sign will not be displayed if negative.
        Length(S) is minimal but at least 3.
        Error is true if width<3.
                                                        *)

var
pre, post : fp_string ;
xx : fp_extended ;

begin (* x80_nan_ascii *)
xx := x ;
if xx[2] < 0 then xx[2] := xx[2] + 128 ; (* Clear I bit. *)
fp%hex( @ xx, 10, @ s) ;
pre := copy( s, 5, 4 ) ;
post := copy( s, 9, 12) ;
fp_nan_ascii( fpminusx(x), width, pre, post, s) ;
error := length(s) > width ;
end   (* x80_nan_ascii *) ;

ÿ