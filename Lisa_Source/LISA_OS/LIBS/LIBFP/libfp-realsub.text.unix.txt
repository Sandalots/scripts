; Copyright 1983, Apple Computer Inc.

;       File Libfp/RealSub, Version 23 February 1984.  

; MACROs for various precision functions.
        
        .macro  minus           ; function minus%1( x : %1 ) : boolean 
        .proc   signof%1
x       .equ    8               ; Address of x relative to a6.
b       .equ    12              ; Address of boolean relative to a6.
        link    a6,#0
        .if     '%1'='S'
        btst    #7,x(a6)        ; Single precision argument is on stack.
        .else 
        movea.l x(a6),a0        ; A0 gets address of argument.
        btst    #7,(a0)         ; Test sign bit.
        .endc
        sne     b(a6)           ; Set result.
        andi.b  #1,b(a6)        ; Clear higher bits of result.
        move.l  4(a6),8(a6)     ; Move return address.
        unlk    a6
        addq.l  #4,sp           ; Skip argument.
        debugrts 'SIGNOF%1 '
        .endm
        
;       EQU and MACRO for double precision operations

        .macro  dblearith               ; Does +-*/ according to %1.
        .proc   %d_%1           ; procedure d%1d ( x, y : double ; var z : double ) 
x       .equ    16                      ; X is at  16(a6).
y       .equ    12                      ; Y is at  12(a6).
t       .equ    -10                     ; T is at -10(a6) = temporary extended.
z       .equ    8                       ; Z is at   8(a6) := X op Y.
        link    a6,#t                   ; Allocate space for T.
        move.l  x(a6),-(sp)             ; var x : double
        pea     t(a6)                   ; var t : extended
        fd2x                            ; t := x
        move.l  y(a6),-(sp)             ; var y : double
        pea     t(a6)                   ; var t : extended
        move.w  #ffdbl+fo%1+fcdbl,-(sp) ; Double precision operation.
        jsrfp
        pea     t(a6)                   ; var t : extended
        move.l  z(a6),-(sp)             ; var z : double 
        fx2d                            ; z := t
        move.l  4(a6),x(a6)             ; Move return address into place.
        unlk    a6
        adda.l  #x-4,sp                 ; Skip arguments.
        debugrts '%D_%1  '
        .endm
        
        .macro  dblefunction            ; procedure %1d ( var z : double ) 
                                        ; %2 is length of '%1'.
        .proc   %d_%1                   ; procedure %1d ( var z : double ) 
z       .equ    8                       ; z is at   8(a6).
t       .equ    -10                     ; T is at -10(a6) = temporary extended.
        link    a6,#t                   ; Allocate space for T.
        move.l  z(a6),-(sp)             ; var z : double
        pea     t(a6)                   ; var t : extended
        fd2x                            ; d2x( z, t )
        pea     t(a6)                   ; var t : extended
        move.w  #fo%1+fcdbl,-(sp)    ; Coerce double precision.
        jsrfp
        pea     t(a6)                   ; var t : extended
        move.l  z(a6),-(sp)             ; var z : double 
        fx2d                            ; procedure x2d( t, z ) 
        move.l  4(a6),8(a6)             ; Move return address.
        unlk    a6
        addq.l  #4,sp                   ; Skip parameters.
        debugrts '%D_%1'
        .block  5-%2,$20                ; Fill.
        .endm

rmode   .equ    $6000           ; Rounding mode mask bits.
notrmode .equ   $9fff           ; NOT rounding mode mask bits.

        .macro  break
        trap    #9
        .endm
        
        .macro  debugbegin
        link    a6,#0
        move.l  (sp)+,a6
        .endm
        
        .macro  debugname       ; DEBUG label for procedures that do UNLK-RTS.
        .if     FSymOK
        .ascii  %1              ; Name of procedure.
        .endc
        .ENDM

        .macro  debugend        ; DEBUGEND for procedures that don't UNLK-RTS.
        .if     FSymOK
        unlk    a6
        rts
        .ascii  %1              ; Name of procedure.
        .endc
        .ENDM

        .macro  debugrts        ; Short DEBUGEND for procedures that need to
        rts                     ; execute RTS.
        .if     FSymOK
        .ascii  %1              ; Name of procedure - 8 char string.
        .endc
        .endm

        .macro  debugurts        ; Short DEBUGEND for procedures that need to
        unlk    a6              ; execute UNLK A6 - RTS.
        rts
        .if     FSymOK
        .ascii  %1              ; Name of procedure - 8 char string.
        .endc
        .endm

        .macro  realarith               ; Does +-*/ according to %1.
        .proc   %f_%1
x       .equ    12                      ; X is at  12(a6).
y       .equ    8                       ; Y is at   8(a6).
t       .equ    -10                     ; T is at -10(a6) = temporary extended.
r       .equ    12                      ; R is at  12(a6) := X op Y.
        link    a6,#t                   ; Allocate space for T.
        pea     x(a6)                   ; var x : real
        pea     t(a6)                   ; var t : extended
        fs2x                            ; t := x
        pea     y(a6)                   ; var y : real
        pea     t(a6)                   ; var t : extended
        .if     '%1'='DIV'
        move.w  #ffsgl+fodiv+fcsgl,-(sp) ; Single precision divide.
        jsrfp
        .else
        f%1s                            ; t := t op y
        .endc
        pea     t(a6)                   ; var t : extended
        pea     r(a6)                   ; var r : real 
        fx2s                            ; r := t
        move.l  4(a6),r-4(a6)           ; Move return address into place.
        unlk    a6
        addq.l  #r-8,sp                 ; Skip argument.
        debugrts '%F_%1  '
        .endm
        
        .macro  realrelational          ; Does relational according to %1.
        .proc   %f_%1
x       .equ    12                      ; X is at  12(a6).
y       .equ    8                       ; Y is at   8(a6).
tx      .equ    -10                     ;TX is at -10(a6) = temporary extended.
r       .equ    14                      ; R is at  14(a6) := X rel Y.
        link    a6,#tx                  ; Allocate space for TX.
        pea     x(a6)                   ; var x : real
        pea     tx(a6)                  ; var tx : extended
        fs2x                            ; s2x( x, tx )
        pea     y(a6)
        pea     tx(a6)
        fcmps                           ; compare (y, tx)
        fb%1    @1
        sf      r(a6)
        bra.s   @2
@1
        st      r(a6)
@2
        andi.b  #1,r(a6)                ; Set boolean result 0 or 1.
        move.l  4(a6),r-4(a6)           ; Move return address into place.
        unlk    a6
        addq.l  #r-8,sp                 ; Skip parameters.
        debugrts '%F_%1   '
        .endm

        .macro  realfunction            ; Implements real function %1(x:real).
                                        ; %2 is length of '%1'.
        .proc   %_%1                    ; real function %_%1 ( x : real ) ;
x       .equ    8                       ; x is at   8(a6).
t       .equ    -10                     ; T is at -10(a6) = temporary extended.
r       .equ    12                      ; R is at  12(a6) := func(X).
        link    a6,#t                   ; Allocate space for T.
        pea     x(a6)
        pea     t(a6)                   ; var t : extended
        fs2x                            ; s2x( x, t )
        pea     t(a6)                   ; var t : extended
        .if     '%1'='SQRT'
        move.w  #fosqrt+fcsgl,-(sp)  ; Coerce single precision square root.
        jsrfp
        .else
        .ref    %1x
        jsr     %1x                     ; func  ( t )
        .endc
        pea     t(a6)                   ; var t : extended
        pea     r(a6)                   ; var r : real 
        fx2s                            ; procedure x2s( t, r ) 
        move.l  4(a6),8(a6)             ; Move return address.
        unlk    a6
        addq.l  #r-8,sp                 ; Skip parameters.
        debugrts '%_%1'
        .block  8-%2,$20                ; Fill.
        .endm

        realarith       ADD
        realarith       SUB
        realarith       MUL
        realarith       DIV
;
; %f_ADD/SUB/MUL/DIV - Floating point operation
;
; Parameters: ST.L - Left operand x
;             ST.L - Right operand y
;
; Returns:    ST.L - Result x op y
;
        
        realrelational  EQ
        realrelational  NE
        realrelational  GT
        realrelational  GE
        realrelational  LT
        realrelational  LE
;
; %f_rel - Floating point relationals:
;
; Parameters: ST.L - Left operand x
;             ST.L - Right operand y
;
; Returns:    ST.B - Boolean result x rel y
;
        
        realfunction SQRT,4
        realfunction EXP,3
        realfunction LN,2
        realfunction SIN,3
        realfunction COS,3
        realfunction ATAN,4
;
; %_func - Floating point functions:
;
;       satisfy Pascal conventions for real function %_func( x : real ) 
;

        .proc   %f_abs
;
; %f_ABS - Floating point absolute value
;
; Parameters: ST.L - Number to abs
;
; Returns:    ST.L - Result
;
        link    a6,#0
        bclr    #31,8(a6)       ; Clear sign bit
        debugurts '%F_ABS  '

        .proc   %f_neg
;
; %f_NEG - Floating point negation
;
; Parameters: ST.L - Number to negate
;
; Returns:    ST.L - Result
;
        link    a6,#0
        bchg    #31,8(a6)       ; Clear sign bit
        debugurts '%F_NEG  '
        
        .proc   %I_FLT
;
; %I_FLT - Float integer
;
; Parameters: ST.L - Integer to float
;
; Returns:    ST.L - Result
;
x       .equ    8                       ; x is at   8(a6).
t       .equ    -10                     ; T is at -10(a6) = temporary extended.
r       .equ    8                       ; R is at  8(a6) := float(X).
        link    a6,#t                   ; Allocate space for T.
        pea     x(a6)
        pea     t(a6)                   ; var t : extended
        fl2x                            ; l2x( x, t )
        pea     t(a6)                   ; var t : extended
        pea     r(a6)                   ; var r : real 
        fx2s                            ; procedure x2s( t, r ) 
        debugurts '%I_FLT  '
        
        .proc   %_round
;
; %_ROUND - Round real
;
; Parameters: ST.L - Real
;
; Returns:    ST.L - Result
;
x       .equ    8                       ; x is at   8(a6).
r       .equ    8                       ; R is at  8(a6) := round(X).
OldRegs   .equ    -8
oldm    .equ    OldRegs-2               ; Old status.
t       .equ    oldm-10                 ; Temporary extended.
        link    a6,#t                   ; Allocate space for locals.
        pea     x(a6)
        pea     t(a6)                   ; var t : extended
        fs2x                            ; s2x( x, t )
        movem.l a0/d0,OldRegs(a6)       ; OldRegs := current A0 and D0.
                        ; Take steps to insure biased round in ambiguous case.
        move    t(a6),d0                ; D0 gets sign+exponent.
        bclr    #15,d0                  ; Clear sign.
        cmpi    #$3fff+23,d0
        bge.s   @1                      ; Branch if exponent >= 23; x is integral.
        bset    #0,t+9(a6)              ; Jam LSB on.
@1
        clr.l   -(sp)                   ; Create slot for sane_environ function result.
        .ref    sane_environ
        jsr     sane_environ
        movea.l (sp)+,a0                ; A0 := address of sane_environ.
        move.w  (a0),oldm(a6)           ; Oldm := current status.
        andi.w  #$9fff,(a0)             ; Force round to nearest.
        pea     t(a6)                   ; var t : extended
        pea     r(a6)                   ; var r : longint 
        fx2l                            ; procedure x2l( t, r ) 
        move.w  (a0),d0                 ; D0 gets current status.
        andi.w  #notrmode,d0            ; Remove rounding mode.
        andi.w  #rmode,oldm(a6)         ; Remove all old status except rounding mode.
        or.w    oldm(a6),d0             ; OR in old rounding mode.
        move.w  d0,(a0)                 ; Store revised status.
        movem.l OldRegs(a6),a0/d0       ; A0, D0 := OldRegs.
        debugurts '%_ROUND '
        

        .proc   %_trunc
;
; %_TRUNC - Truncate real
;
; Parameters: ST.L - Real
;
; Returns:    ST.L - Result
;
x       .equ    8                       ; x is at   8(a6).
r       .equ    8                       ; R is at  8(a6) := trunc(X).
OldRegs   .equ    -8
oldm    .equ    OldRegs-2               ; Old status.
t       .equ    oldm-10                 ; Temporary extended.
        link    a6,#t                   ; Allocate space for locals.
        pea     x(a6)
        pea     t(a6)                   ; var t : extended
        fs2x                            ; s2x( x, t )
        movem.l a0/d0,OldRegs(a6)       ; OldRegs := current A0 and D0.
        clr.l   -(sp)                   ; Create slot for sane_environ function result.
        .ref    sane_environ
        jsr     sane_environ
        movea.l (sp)+,a0                ; A0 := address of sane_environ.
        move.w  (a0),oldm(a6)           ; Oldm := current status.
        ori.w   #rmode,(a0)             ; Force round to zero.
        pea     t(a6)                   ; var t : extended
        pea     r(a6)                   ; var r : longint 
        fx2l                            ; procedure x2l( t, r ) 
        move.w  (a0),d0                 ; D0 gets current status.
        andi.w  #notrmode,d0            ; Remove rounding bits.
        andi.w  #rmode,oldm(a6)         ; Remove all bits except old rounding.
        or.w    oldm(a6),d0             ; OR in rest of old status word.
        move.w  d0,(a0)                 ; Store revised status.
        movem.l OldRegs(a6),a0/d0       ; A0, D0 := OldRegs.
        debugurts '%_TRUNC '

        .proc   %_pwr10
;
; %_PWR10 - Power of Ten: returns 10 ^ n.
;
; Parameters: ST.W - Integer
;
; Returns:    ST.L - Result
;
n       .equ    10                      ; 10(a6) is address of n.
r       .equ    8                       ;  8(a6) is address of result.
s       .equ    -6                      ; -6(a6) is beginning of Decimal temporary:
                                        ;  s(a6) is Sgn
                                        ;s+2(a6)         is Exp.
                                        ;s+4(a6) is Sig, digit string.
        debugbegin
        clr.w   -(sp)                   ; Create space for return address.
        move.l  2(sp),(sp)              ; Move return address to create a slot
                                        ; for result.
        link    a6,#s          
        clr.w   s(a6)                   ; Sgn := 0.
        move.w  n(a6),s+2(a6)           ; Exp := n.
        move.b  #1,s+4(a6)              ; Length(Sig) := 1.
        move.b  #49,s+5(a6)             ; Sig[1] := '1'.
        pea     s(a6)                   ; (var) s : decimal
        pea     r(a6)                   ; var r : single
        fdec2s
        debugurts '%_PWR10 '
        
        .proc   %r_r
; %r_r invokes f32%r_r after adjusting stack.
; %r_r is called with input parameter on stack, returns with function value
; at same place on stack.
; f32%r_r follows Pascal convention, requires slots for input parameter
; and function result to be separate.
; Bug in Pascal compiler requires us to save all scratch registers.
        .ref    f32%r_r
        link    a6,#0                   ; 4(a6) contains return address.
                                        ; 8(a6) contains address of fib and
                                        ;       will contain real value.
        movem.l a0-a2/d0-d3,-(sp)       ; Save all scratch registers.
        clr.l   -(sp)                   ; Create slot for function result.
        move.l  8(a6),-(sp)             ; Set up fib address.
        jsr     f32%r_r
        move.l  (sp)+,8(a6)             ; Store function result.
        movem.l (sp)+,a0-a2/d0-d3       ; Restore scratch registers.
        debugurts '%R_R    '

ÿ