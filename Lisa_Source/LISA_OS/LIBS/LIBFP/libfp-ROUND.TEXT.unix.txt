        .title   'ROUND (3.2) SUBROUTINE  - 8/3/81 - 17:24:38 - 3.3'
        PAGE
        .proc    mc%rnd     ; 0,0
        .def     mc%round,mc%rnd_opt
;
;*******************************************************************************
;
;       revision      date        description
;       --------      ----        -----------
;         1.1      29.oct.80       original
;
;         1.2       5.nov.80       optimized speed by eliminating use of
;                                  reg d6 in grs_dbl and by jmp to add_to_l
;                                  instead of jsr
;
;         1.3      13.nov.80       further optimized speed by first
;                                  serving RN-extended
;
;         1.4       3.dec.80       provided alternate entry point for
;                                  callers wishing to furnish rounding prec.
;                                  and/or mode byte values
;
;         1.5       6.jan.81       chg FPRB pointer from reg a6 to reg a3;
;                                  chg all references to 'trapstatus' reg
;                                  be references to 'tempstatus' reg.stat
;
;         1.6      13.jan.81       ***  MICROSYSTEMS RELEASE  ***
;                                  modified for EXORMACS download
;
;         1.7      13.feb.81       permanently converted to resident asm form
;
;         1.8      23.apr.81       added exception bit table
;
;         3.1       7.jul.81       changed to EXORmacs stuctured asm syntax
;
;         3.2      29.jul.81       move RND_ZERO and ADD_DBL so that bra.s's
;                                  are not out of range when path counters
;                                  are added
;
;                  27 mar 83       check for untrapped underflow when
;                                  rounding extended to single or double
;
;*******************************************************************************
;
;
;
;                         mc%round SUBROUTINE
;
;       This subroutine contains two entry points:
;
;       =====
;       mc%round - this entry point should be used to mc%round to the precision
;       =====   and rounding mode specified by the MODE byte of the FPRB.
;
;
;       enter with:    d0-d2    contains extended precision, internal
;       ==========              format floating point number to be
;                               rounded (does not need to be properly tagged).
;
;                      d3       contains guard-mc%round-stiky bits
;
;                      a3       points to fp register block (FPRB)
;
;
;
;       exit with:
;       =========
;
;                      d0-d2    contains result rounded per the Rounding
;                               Mode and Rounding Precision specified in
;                               the MODE byte of the FPRB with all g-r-s
;                               bits cleared.
;                               (result is not typed - tag field is trash)
;
;                         d3    cleared
;
;                      d4-d7    unchanged
;
;                      a1-a7    unchanged
;
;                         a0    destroyed  - will contain rnd_prec from mode
;                                            byte encoded as: EXT = $00000000
;                                                             SGL = $00000002
;                                                             DBL = $00000004
;
;
;       Flags Set:              The INEX bit of the STATUS byte
;       =========               will have been set if the rounded
;                               result was inexact (i.e. if any
;                               g-r-s bit was set).
;
;
;       Subroutines Called:     None
;       ==================
;
 PAGE
;
;       =========
;       mc%rnd_opt - optional entry point to mc%round subroutine - used to
;       =========   mc%round to the rounding precision specified by reg d5
;                   and the rounding mode specified by reg a0.
;
;
;       enter with:    d0-d2    contains extended precision, internal
;       ==========              format floating point number to be
;                               rounded (does not need to be properly tagged).
;
;                      d3       contains guard-mc%round-stiky bits
;
;                      d5       rounding precision in LS_word
;
;                                  EXT - $00000000
;                                  SGL - $00000002
;                                  DBL - $00000004
;
;                      a0       rounding mode in LS_word
;
;                                   RN - %xxxx...xx00
;                                   RZ - %xxxx...xx01
;                                   RP - %xxxx...xx10
;                                   RM - %xxxx...xx11
;
;                               Note that the 'don't-cares' allow loading
;                               the MODE byte from the FPRB into this reg
;                               if the current rounding mode is desired.
;
;                      a3       points to fp register block (FPRB)
;
;
;
;       exit with:
;       =========
;
;                      d0-d2    contains result rounded per the Rounding
;                               Mode in reg. a0 and the Rounding Precision
;                               in reg. d5 with all g-r-s bits cleared.
;                               (result is not typed - tag field is trash)
;
;                         d3    cleared
;
;                      d4-d7    unchanged
;
;                         a0    contains copy of reg d5
;
;                      a1-a7    unchanged
;
;
;
;       Flags Set:              The INEX bit of the STATUS byte will have
;       =========               been set if the rounded result was inexact
;                               (i.e. if any g-r-s bit was set).
;
;
;
;       Subroutines Called:     None
;       ==================
;
;
;***********************  F L A G S   ******************************
;
;
;  This procedure is called by: Valid, Ldfp, Pwrten, Decbin (mc%round entry pt.)
;                               Stfloat (mc%rnd_opt entry pt.)
;
;
;  Exception    Set by     Reason
;  =========    ======     ======
;
;  rsvx         ---        Not affected by this routine
;
;  iovf         ---        Not affected by this routine
;
;  inex         mc%round      Result cannot be exactly represented
;               mc%rnd_opt  using the current rounding precision.
;
;  dz           ---        Not affected by this routine
;
;  unfl         ---        Not affected by this routine
;
;  ovfl         ---        Not affected by this routine
;
;  iop          ---        Not affected by this routine
;
;
;      *************************************************************************
 PAGE
;
;
;      *************************************************************************
;
;
;      LOCAL EQUATES
;
MK_GR_DB   EQU    $07FF       ; AND-MASK TO OBTAIN G-R BITS IN DBL PREC
CL_GR_DB   EQU    $F800       ; AND-MASK TO CLEAR G-R BITS IN DBL PREC
;
;
;      *************************************************************************
;
;                     ***************************
;                     *  mc%rnd_opt STARTS HERE  *
;                     ***************************
;
;
mc%rnd_opt  EQU     *
        debugbegin
;
;
;      Save reg. d5 on stack and branch around MODE byte acquisition.
;      Note that this also branches around the priority service for
;      RN-EXT; RN-EXT will now be serviced with the same priority as
;      RN-DBL and RN-SGL.
;
        MOVE.L   D5,-(A7)
        BRA      GRS_CALC
        debugend 'MC%RND_O',0
;
;
;                     ***********************
;                     *  mc%round STARTS HERE  *
;                     ***********************
;
;
mc%round   EQU      *
        debugbegin
;
;
;      Save reg. d5 on stack
;
        MOVE.L   D5,-(A7)
;
;      Copy the mode byte from the FPRB and mask off rounding mode & precision
;
        moveq    #<MDPREC_B+MDRND_B>,D5   ; (SIGN EXTD = 0) CLEARS MSBYTE OF D5.
        AND.B    MODE(A3),D5
        bne      rnd_n_ext                ; IF  (mc%round NEAREST-EXTENDED PREC)
           TST.L    D3
;
;         If g = r = s = 0 then result is exact and mc%round is done, else set
;         the inexact result flag in temp-status register and continue.
;
           beq         clr_g_r_s                ; IF  (G,R,S  <>  0)  THEN
              BSET     #STINEX_M,TEMPSTAT(A3)   ; SET INX RESULT FLAG
              ASL.L    #1,D3                    ; SHIFT G-BIT INTO C-BIT
;
;            mc%round to Nearest  -  If (g = 1) then [add '1' to 'l' and if
;            (r = s = 0), then clear 'l'].
;            Note that this will mc%round to even in case of a tie.
;
             bcc     clr_g_r_s                     ; IF  (G-BIT = 1)  THEN
EXTD_ADD         ADDQ.L   #1,D2                    ; ADD '1' TO L-BIT
                 bcc        tst_r_s                   ; IF  (CARRY OUT)  THEN
                    ADDQ.L  #1,D1                     ; PROPOGATE CARRY
                    bcc     tst_r_s                   ; IF (MTISSA OVFLOW) THEN
                       ROXR.L  #1,D1                     ; D1 & D2 ARE ZERO
;                                                          so restore v-bit
                       ADDQ.W  #1,D0                     ; & INCR. EXPONENT
tst_r_s                                                ; ENDIF
;                                                      ENDIF
                 TST.L    D3
                 bne      clr_g_r_s                   ; IF  (R-S BITS = 0)  THEN
                    ANDI.B   #$FE,D2                  ; CLEAR THE L-BIT
;                                                    ENDIF
;                     (G = 1)                      ENDIF
clr_g_r_s         ; (G,R,S  <>  0)               ENDIF
           moveq    #0,D3                    ; CLEAR G-R-S BITS IN REG D3
           MOVE.L   (A7)+,D5                 ; RESTORE SAVED REG D5
           RTS

st_min2  .word $ff82,$fc02              ; Minimum exponents for normal
                                        ; single and double.
st_lower .word $ff61,$fbc1              ; Exponents below which we don't do anything.

checkunfl                       ; Routine to check for untrapped underflows and
                                ; denormalize when rounding extended to single
                                ; or double.
        cmp     st_min2-2(d5),d0
        bge     checkret                ; Branch if exponent >= minimum.
        cmp     st_lower-2(d5),d0
        ble     checkret                ; Branch if exponent so small double rounding
                                        ; doesn't matter.
        btst    #enunfl_m,enable(a3)
        bne     checkret                ; Branch if underflows are trapped.
        move    d0,-(sp)                ; Save exponent.
  
  MOVE ST_MIN2-2(D5),D4     ; GET THE MINIMUM EXPONENT FOR PRECISION
  SUB  D0,D4              ; D4 NOW IS THE NUMBER OF SHIFTS TO DENORMALIZE
  MOVE ST_MIN2-2(D5),D0     ; SET THE DENORMALIZED EXPONENT TO THE MIN EXP
;
;   FAST SHIFT THE NUMBER RIGHT D4 BITS. PUT ALL STICKY BITS INTO D3
;
    cmp      #32,d4
    blt.s    shf_16           ; IF SHIFT CT > 32
      OR.L  D2,D3             ; OR D2 INTO STICKY
      MOVE.L D1,D2            ; D1 INTO D2
      CLR.L  D1
      SUB    #32,D4           ; REDUCE SHIFT COUNT BY 32
shf_16
    cmp      #16,d4
    blt.s    shf_8            ; IF SHIFT CT > 16
      OR D2,D3                ; LOW WD OF D2 INT STICKY
      CLR D2                  ; LOW WD = 0
      MOVE D1,D2
      SWAP D2                 ; D2 NOW SHIFTED 16 BITS
      CLR D1
      SWAP D1                 ; D1 NOW SHIFTED 16 BITS
      SUB  #16,D4             ; REDUCE SHIFT CT BY 16
shf_8
    cmp      #8,d4
    blt.s    end_shf_8        ; SHIFT CT STILL > 8
      CLR.L  D6               ; SAVE LOWER BYTE OF D1 IN D6 UPPER BYTE
      MOVE.B D1,D6
      SWAP D6
      LSL.L #8,D6
      LSR.L #8,D1             ; D1 IS SHIFTED 8 BITS RIGHT
      OR.B  D2,D3             ; LOW BYTE OF D2 INTO STICKY
      LSR.L #8,D2             ; D2 NOW SHIFTED 8 BITS RIGHT BUT MISSING HIGH BYT
      OR.L  D6,D2             ; OR IN HIGH BYTE
      SUB   #8,D4             ; REDUCE SHIFT CT BY 8
end_shf_8
      BRA.s ST_01             ; GO TO END OF LOOP FIRST
ST_LOOP EQU *
      LSR.L  #1,D1            ; SHIFT RIGHT 1
      ROXR.L #1,D2
      ROXR.L #1,D3
      bcc.s   go_again        ; IF STICKY BIT SHIFTED OUT
        BSET  #31,D3          ; SHIFT IT BACK IN
go_again
ST_01 DBRA D4,ST_LOOP         ; GO AGAIN IF MORE TO DO
        bsr.s mc%rnd_opt               ; GO ROUND THE RESULT

denoret                                 ; Return address from previous jsr.
        btst    #stinex_m,tempstat(a3)
        beq.s   @1                      ; Branch if exact so far.
        bset    #stunfl_m,tempstat(a3)  ; Set underflow flag.
@1
        move    (sp)+,d4                ; d4 gets old exponent.
        sub     d4,d0                   ; D0 gets left shift count.
        ble.s   setexponent             ; Branch if shift count <= 0.
        bra.s   lshiftdbf
leftshift
        asl.l   #1,d2
        roxl.l  #1,d1                   ; Finish left shift.
lshiftdbf
        dbf     d0,leftshift
        bcc.s   setexponent             ; Branch if no carry out occurred.
        roxr.l  #1,d1                   ; Restore carried out bit.
        roxr.l  #1,d2
        addq    #1,d4                   ; Increment exponent.
setexponent
        move    d4,d0                   ; Restore original exponent.
checkret
        rts

rnd_n_ext                     ; (mc%round NEAREST-EXTENDED PRECISION)
;
;      Get here only for RN, sgl or dbl prec, or for RZ, RM, or RP.
;
;      The mode byte with rounding mode and precision masked off is
;      currently in reg d5; make a copy of reg d5 in reg a0.  Shift
;      reg d5.b left by three to form an even byte index based upon
;      rounding precision.
;
        MOVE.W   D5,A0                    ; COPY MASKED OFF ROUNDING MODE & PREC
        LSR.W    #3,D5                    ; SHIFT OUT ROUNDING MODE TO FORM
;                                           even byte rnd prec index in d5.w
;
GRS_CALC  EQU  *
;
;      If the rounding precision is extended, the g-r-s long word already
;      exists in reg d3.  If the rounding precision is single or double,
;      build a g-r-s long word in reg d3 and clear any g-r bits in regs.
;      d1 and d2.  Use reg d5 as an index thru jump table to service
;      the three rounding precisions.
;
        JMP      GRS_TAB(D5.W)               ; *: CONVERT TO (PC,D5.W)
;                                           rnd prec is the index
;

;      *************************************************************************
;
;      Jump Table for building a g-r-s long word - indexed by rounding prec.
;                                                  in reg d5.w
GRS_TAB EQU      *
        BRA.S    GRS_EXT
        BRA.S    GRS_SGL
;                grs_dbl falls thru
;
;
;      *************************************************************************
;
GRS_DBL EQU      *
;
;      Rounding Precision Double  -  Combine the r-s bits which are current-
;      ly in bits 0-9 of reg d2 and all bits of reg d3 into reg d3.  Transfer
;      the g-bit which is currently bit 10 of reg d2 to the MSB of reg d3.
;      Clear the g-r bits in reg d2.
;
        bsr.s   checkunfl
        OR.B     D2,D3                    ; 'OR' EIGHT OF THE TEN D2.W R-BITS
;                                                                   into d3.b
        TST.L    D3                       ; 'OR' D2.B AND D3.L R-S BITS INTO D2.
        SNE      D2
        MOVE.W   D2,D3                    ; COPY OF D2.W WHICH NOW CONTAINS G-R-
        ANDI.W   #MK_GR_DB,D3             ; MASK OFF THE G-R-S BITS
        ASL.W    #5,D3                    ; SHIFT THE G-BIT INTO THE MSB OF D3.W
        SWAP     D3                       ; MOVE THE G-BIT TO THE MSB OF D3.L
;
        ANDI.W   #CL_GR_DB,D2             ; CLEAR G-R BITS IN REG D2
        BRA.S    TST_INX
;
GRS_SGL   EQU    *
;
;      Rounding Precision Single  -  Combine the r-s bits which are current-
;      ly in bits 0-6 of reg d1 and all bits of regs. d2 & d3 into reg. d3
;      Transfer the g-bit which is currently bit 7 of reg d1 to the MSB of
;      reg d3.  Clear all g-r bits in regs d1 & d2.
;
        bsr.s    checkunfl
        ASL.B    #1,D1                    ; TRANSFER THE G-BIT TO THE X-BIT
        OR.B     D1,D3                    ; 'OR' D1.B'S SEVEN R-BITS INTO D3.B
        OR.L     D2,D3                    ; SETS CCR Z-BIT = 'OR' OF ALL R-S BIT
        SNE      D3                       ; MAKE D3.B THE 'OR' OF ALL R-S BITS
        ROXR.L   #1,D3                    ; PUT G-BIT INTO D3.L MSB
;                                           Sne made bit shiftout a don't care
        CLR.B    D1                       ; CLEAR ALL G-R BITS IN REGS. D1 & D2
        moveq    #0,D2
;
GRS_EXT  EQU     *
;
TST_INX  EQU     *
;
;      If g = r = s = 0 then result is exact and mc%round is done, else set
;      the inexact result flag in status register and continue.  Note that
;      reg a0.w contains a copy of the mode byte with rnd prec and rnd mode
;      masked off.
;
        TST.L    D3
        beq         prog_end                 ; IF (G,R,S  <>  0) THEN
           BSET     #STINEX_M,TEMPSTAT(A3)   ; SET INEXACT RESULT FLAG
           EXG      D5,A0                    ; GET COPY OF MODE BYTE, SAVE D5
           ANDI.W   #MDRND_B,D5              ; MASK ROUNDING MODE IN REG D5.W
           BEQ.S    RND_NEAR                 ; HANDLE RN QUICKLY
;
;         Get here if rounding mode is RZ, RP, or RM.  Use rounding mode as
;         an index into a jump table for these three modes.
;
           ASL.W    #1,D5                    ; MAKE INDEX FOR EVEN BYTES
           JMP      MODE_TAB-2(D5.W)            ; *: CONVERT TO (PC,D5.W)
;                                              (-2) since RN mode already handle
;
;         **********************************************************************
;
;         Jump Table for RZ, RP, and RM indexed by rnding mode in d5.w
;
MODE_TAB   EQU      *
           BRA.S    RND_ZERO
           BRA.S    RND_PLUS
;                   rnd_mnus falls thru
;
;
;         **********************************************************************
;
RND_MNUS   EQU      *
;
;         mc%round to Minus Infinity  -  If sign of FP number equals '1'
;         (negative), then add '1' to 'l'.
;
           TST.L    D0
           BPL      RND_DONE                 ; IF  (NEGATIVE)  THEN
              SMI      D3                       ; SET R-S BITS SO 'L' CAN'T BE
;                                                 cleared by add_to_l.
              JMP      ADD_TO_L(A0.W)              ; *: CONVERT TO (PC,A0.W)
;                                                 index for jump is rnd prec
;
RND_ZERO   EQU      *
    BRA      RND_DONE
;
;
RND_PLUS   EQU      *
;
;         mc%round to Plus Infinity  -  If sign of FP number equals '0'
;         (positive), then add '1' to 'l'.
;
           TST.L    D0
           BMI      RND_DONE                 ; IF  (POSITIVE)  THEN
              SPL      D3                       ; SET R-S BITS SO 'L' CAN'T BE
;                                                 cleared by add_to_l
              JMP      ADD_TO_L(A0.W)              ; *: CONVERT TO (PC,A0.W)
;                                                 index for jump is rnd prec
;
;
RND_NEAR   EQU      *
;
;         mc%round to Nearest  -  If (g = 1) then [add '1' to 'l' and if
;         (r = s = 0), then clear 'l'].
;         Note that this will mc%round to even in case of a tie.
;
           ASL.L    #1,D3                    ; SHIFT G-BIT INTO C-BIT
           bcc      rnd_done                       ; IF (G = 1) THEN
              JMP      ADD_TO_L(A0.W)              ; *: CONVERT TO (PC,A0.W)
;                                                 add one to l-bit; index for
;                                                 jump is rnd prec
;
;            *******************************************************************
;
;            LOCAL EQUATES
;
AD_1_SGL      EQU      $00000100   ; CONSTANT TO ADD 1 TO L-BIT IN SGL PREC
AD_1_DBL      EQU      $00000800   ; CONSTANT TO ADD 1 TO L-BIT IN DBL PREC
;
;
;            *******************************************************************
;
;            JUMP TABLE FOR ADDING ONE TO THE L-BIT indexed by rnd prec
;
ADD_TO_L      EQU      *
;
              BRA.S    ADD_EXT
              BRA.S    ADD_SGL
       BRA      ADD_DBL
;
;            *******************************************************************
;
ADD_SGL       EQU      *

              ADD.L    #AD_1_SGL,D1
              bcc      @1                          ; IF (MANTISSA OVERFLOWS) THEN
                 ROXR.L   #1,D1                    ; SHIFT V-BIT BACK IN
                 ADDQ.W   #1,D0                    ; AND INCR EXPONENT
@1                                                 ; ENDIF
              TST.L    D3
              bne      @2                          ; IF (R-S BITS = 0) THEN
                 ANDI.W   #$FE00,D1                ; CLEAR THE L-BIT
@2                                                 ; ENDIF
              BRA.S    RND_DONE
;
ADD_EXT       EQU      *
;
              BRA      EXTD_ADD
;
;
ADD_DBL       EQU     *
;
              ADD.L    #AD_1_DBL,D2
              bcc         @3                       ; IF (CARRY OUT) THEN
                 ADDQ.L   #1,D1                    ; PROPOGATE CARRY
                 bcc      @3                       ; IF (MANTISSA OVERFLOWS) THE
                    ROXR.L   #1,D1                    ; D1 & D2 ARE '0' SO,
;                                                        shift v-bit back in
                    ADDQ.W   #1,D0                    ; AND INCR EXPONENT
@3                                                 ; ENDIF
;                                                  ENDIF
              TST.L    D3
              bne      rnd_done                    ; IF (R-S BITS = 0) THEN
                 ANDI.W   #$F000,D2                ; CLEAR THE L-BIT
;                                                  ENDIF

;                  (G = 1)                       ENDIF
;
RND_DONE   EQU      *
;
           moveq    #0,D3                    ; CLEAR G-R-S BITS IN REG D3
prog_end         ; (G,R,S <> 0)               ENDIF
        MOVE.L   (A7)+,D5                 ; RESTORE REG D5 FROM STACK
;
        RTS                               ; END OF mc%round SUBROUTINE
        debugend 'MC%ROUND',0
    
ÿ