        .title 'BINDEC (3.7) SUBROUTINE  - %H% - %T% - %I%'
        PAGE
;
        .proc   mc%BINDEC   ; 0,0
        .ref    mc%binstr,mc%int_a,mc%pwrten,mc%mul_opt,mc%div_opt,mc%cmp_A,mc%cmp_K
        .ref    mc%iop,mc_calcea
        .ref    mc%cdnorm
;
;******************************************************************************
;
;       REVISION      DATE        DESCRIPTION
;       --------      ----        -----------
;         1.1       15.JAN.81      ORIGINAL  -
;
;         1.2       30.JAN.81      MOVED THE TEST FOR RECALC_CTR IN THE
;                                  FIRST COMPARE SO THAT THE CMP TO 10^'LEN'
;                                  IS ALWAYS PERFORMED; ONLY THE TEST FOR
;                                  <GT> IS BYPASSED AFTER ONE RECALC.
;
;         1.3       11.FEB.81      ***  MICROSYSTEMS RELEASE  ***
;                                  CHANGED MOVEM.W TO MOVEM.L IN TWO SS
;                                  AROUND 'Y' CALC.
;
;         1.4       13.FEB.81      PERMENENTLY CONVERTED TO RESIDENT ASM FORM
;
;         1.5       25.FEB.81      CHANGED VALUE OF LOG10 (2) CONSTANT IN
;                                  APPROX_LOG10 CALCULATION
;
;         1.6        1.APR.81      CHANGED 'STRESULT' SUBROUTINE CALL TO BE
;                                  'STRSLT' MACRO.  CHANGED 'FMULT' AND
;                                  'FDIV' SUBROUTINE CALLS TO 'mc%mul_opt'
;                                  AND 'mc%div_opt' SUBROUTINE CALLS TO CORRECT
;                                  THE SLIGHT ERRORS CREATED WHEN 'FMULT' AND
;                                  'FDIV' WERE MADE TO ROUND THEIR RESULTS.
;
;         1.7        2.APR.81      REMOVED THE 'FORCE ILOG TO ZERO WHEN K
;                                  AND ILOG ARE NEGATIVE' SECTION OF
;                                  'LEN_CALC'.
;
;         1.8        3.APR.81      RESTORED FPN REGISTER AFTER ALL CALC DONE
;
;         1.9        3.APR.81      ADDED EXCEPTION BIT CHART.
;                                  CLEARED INX FLAG IN TEMPSTAT AFTER ILOG
;                                  CALCULATION
;
;         2.0       15.MAY.81      ADDED ABORT ADDRESS TO 'STRSLT' MACRO.
;                                  MODIFIED EXCEPTION BIT CHART - 'mc_calcea'
;                                  SETS mc%iop = 13, NOT 'mc%iop'.  REMOVED
;                                  XREF TO 'mc%iop'.
;
;         3.1        7.JUL.81      CHANGED TO EXORMACS STRUCTURED ASM SYNTAX
;
;         3.2       28.JUL.81      MOVED ZERO_OUT AND NAN_OUT SO THAT BRA.S'S
;                                  ARE NOT BE OUT OF RANGE WHEN PATH COUNTERS
;                                  ARE ADDED
;
;         3.3       18.AUG.81      IN ABS[INT('Y')] < 10^('LEN'-1) TEST
;                                  CHANGED QUALIFYING TEST TO BE ON
;                                  THE MANTISSA_MSB OF ABS[INT('Y')]
;                                  INSTEAD OF ON THE UNNORM TAG OF SAME.
;
;         3.4      14.SEPT.81      LAST REV DIDN'T FIX IT!  TEST SHOULD
;                                  BE QUALIFIED ON MANTISSA_MSB OF
;                                  THE (ORIGINAL INPUT VALUE) INSTEAD OF
;                                  ABS[INT('Y')].
;                                  ???? WHY DIDN'T LAST REV WORK ?????
;
;                                  CHANGED THE  ANDI.B' OF TEMPSTAT
;                                  NEAR END TO ALLOW INX THRU.
;
;                                  ADDED USYS SUGGESTION FOR  K' NEGATIVE.
;                                  REPLACED ISCALE := ILOG + 1 - LEN
;                                  WITH IF ('K' NEGATIVE) AND (ILOG < 'K')
;                                       THEN ILOG := 'K'
;                                       ENDIF
;                                       ISCALE := ILOG + 1 - LEN
;                                  ALSO, EXPANDED 'K' NEGATIVE RANGE TO
;                                  -32 <= 'K' <= +18.
;                                  ???? NOTE - COMMENTS MAY NEED EDITING
;                                       TO CONFORM WITH THESE ADDITIONS.
;
;         3.5      16.SEPT.81      FIXED TYPO IN 3.4
;
;         3.6      16.SEPT.81      CHANGES TO STFP MAKE A LOWER BOUND CHECK
;                                  ON 'K' UNNECESSARY.  NOTE - STFP ALLOWS'S
;                                  'K' DYNAMIC DOWN TO -128
;
;         3.7      22.SEPT.81      CLEAR INX BIT AFTER CALL TO FINT
;
;       Hough      13 Jan 83       Changed code and comments to allow 18 digits on
;                                  output.
;       Hough      18 Jan 83       Removed padding NOPs inserted by Laura Okin.
;
;       Hough      11 Feb 83       Changed test of whether to branch to cmp_a or
;                                  cmp_k to be based on most significant bit rather
;                                  than tag field which is still trashed from
;                                  mul_opt or div_opt.
;
;
;******************************************************************************
;
;
;
;                         BINDEC SUBROUTINE
;
;
;       BINDEC WILL BE CALLED BY THE STFP ROUTINE WHENEVER THE FP-INSTRUCTION
;       IS OF THE FORM:
;
;               FMOVE.P   FPN,<EA>
;
;
;
;       ENTER WITH:           D0-D2    CONTAINS EXTENDED PRECISION, INTERNAL
;       ==========                     FORMAT FLOATING POINT NUMBER COPIED
;                                      FROM FPN BY CALLER.
;                                      (MUST BE PROPERLY TYPED CONSISTENT WITH
;                                      THE INDEX IN REG D4)
;
;                             D3       CLEARED BY CALLER
;
;                             D4       TYPE INDEX OF FP-NUMBER IN D0-D2
;                                      (MUST BE SIGN-EXTENDED TO WORD BOUNDARY)
;
;                                       0 = NORMALIZED
;                                       2 = ZERO
;                                       4 = INFINITY
;                                       6 = NAN
;                                       8 = NOT NORMALIZED
;
;                                      NOTE: AN UNNORMALIZED ZERO WILL BE
;                                            TYPED AS NOT-NORMALIZED.
;
;                             D7       'K', WHERE POSITIVE 'K' REPRESENTS THE
;                                      DESIRED NUMBER OF SIGNIFICANT DECIMAL
;                                      DIGITS AND NEGATIVE 'K' REPRESENTS
;                                      THE DESIRED NUMBER OF DECIMAL DIGITS
;                                      TO THE RIGHT OF THE DECIMAL POINT.
;
;                                      IF 'K' IS NOT IN THE RANGE
;                                      1 <= (+'K') <= 18
;                                             OR
;                                      -32 <= (-'K') <= -1
;                                      AN mc%iop = 10 WILL BE SIGNALED, 'K' WILL
;                                      BE ASSIGNED THE NEAREST VALID VALUE,
;                                      AND THE RESULT CALCULATED USING THE
;                                      ASSIGNED VALUE FOR 'K'.
;
;                             A1       POINTS TO UNPACKED INSTRUCTION BUFFER
;
;                             A3       POINTS TO FP REGISTER BLOCK (FPRB)
;
;                             A2       POINTS TO FPN IN FPRB
;
;                             A4       POINTS TO USER SUPPLIED FETCH/STORE
;                                      ROUTINE
;
;                             A5       POINTS TO USER REGISTERS
;
;
;       EXIT WITH:
;       =========
;
;                             D0-D7    DESTROYED
;
;                                A0    DESTROYED (BY ROUND AMONG OTHERS)
;
;                             A1-A7    UNCHANGED
;
;
;
;       SUBROUTINES CALLED:    mc%mul_opt, mc%div_opt, FINT, mc%pwrten, FCMP,
;       ==================     mc%binstr, mc_calcea
;
;
;
;***********************  F L A G S   ******************************
;
;  THIS PROCEDURE IS CALLED BY: STFP
;
;
;  EXCEPTION    SET BY     REASON
;  =========    ======     ======
;
;  RSVX         ---        NOT AFFECTED BY THIS ROUTINE
;
;  IOVF         ---        NOT AFFECTED BY THIS ROUTINE
;
;  INEX         FINT       BINARY FP INPUT OPERAND CAN NOT BE EXACTLY
;                          REPRESENTED AS A BCD FP RESULT USING THE
;                          SPECIFIED VALUE OF 'K'.
;
;  DZ           ---        NOT AFFECTED BY THIS ROUTINE
;
;  UNFL         ---        NOT AFFECTED BY THIS ROUTINE
;
;  OVFL         ---        NOT AFFECTED BY THIS ROUTINE
;
;  mc%iop= 13      mc_calcea     ILLEGAL INSTRUCTION
;  mc%iop= 10      BINDEC     'K' OUT OF RANGE
;
;
;
;
 PAGE
;******************************************************************************
;
;
;       LOCAL EQUATES
;
;
maxek   equ      18                          ; Maximum number of output decimal
;                                              digits.
;
IOPTEN  EQU      $28                         ; mc%iop CODE 10 ADJ FOR IOPSTATUS
;                                              REG. - CROSS ASM BOMBS (10<<2)
;
EXT_RM  EQU      $03                         ; MODE BYTE VALUE TO FORCE
;                                              EXTENDED PRECISION, ROUND TO
;                                              MINUS
;                                              CROSS ASM BOMBS ON
;                                                    (EXTENDED<<4)+RM
;
EXT_PREC EQU     $00                         ; 'OR' INTO MASKED MODE BYTE
;                                              TO FORCE EXTENDED PRECISION
;                                              CROSS ASM BOMBS ON (EXTENDED<<4)
;
ZERO_M  EQU      1                           ; BIT NUMBER EQUATE FOR FPN TAG
;                                              NO BIT NUMBER EQUATES IN GLOBAL
;                                              EQUATE FILE FOR TAGS!!
;
NNORM_M EQU      4                           ; BIT NUMBER EQUATE FOR FPN TAG
;                                              NO BIT NUMBER EQUATES IN GLOBAL
;                                              EQUATE FILE FOR TAGS!!
;
;
;******************************************************************************
;
;
;
;
;                     ************************
;                     *  BINDEC STARTS HERE  *
;                     ************************
;
;
        debugbegin
;
;
;      SIGN EXTEND THE 6-BIT TWO'S COMPLEMENT VALUE OF 'K' PASSED IN
;      THE LSBITS OF D7.B.  IF 'K' OUT OF RANGE, SET iop=10 AND ASSIGN
;      'K' THE NEAREST VALID VALUE.
;
;
        EXT.W    D7
;
        cmp.w       #maxek,d7                    ; IF (POSITIVE 'K' OUT OF RANGE)
        ble         @1
           MOVE.W   #maxek,D7                      ; SET 'K' TO 18
           BSET     #STIOP_M,TEMPSTAT(A3)       ; SET iop BIT IN TEMPSTATUS
           MOVE.B   #IOPTEN,IOPSTATUS(A3)       ; SET iop=10
@1                                            ; END
;
        TST.W    D7
        bne      @2                           ; IF ('K' = 0)
           MOVE.W   #1,D7                       ; SET 'K' TO 1
           BSET     #STIOP_M,TEMPSTAT(A3)       ; SET iop BIT IN TEMPSTATUS
           MOVE.B   #IOPTEN,IOPSTATUS(A3)       ; SET iop=10
@2                                            ; END
;
;
;******************************************************************************
;*****
;*****       IF THE BINARY NUMBER TO BE CONVERTED IS TRUE ZERO, TRUE INFINITY,
;*****       OR A NAN OUTPUT THE APPROPRIATE STRING AND QUIT.
;*****
;*****               TRUE ZERO  -  MANTISSA = STRING OF +0 OR -0; EXP = +0000
;*****                             IF ('K' POSITIVE) THEN 'P' = ('K'-1),ELSE
;*****                             ('K' NEGATIVE) 'P' = -('K').
;*****
;*****              + INFINITY  -  NON-DECIMAL STRING WITH SE = $A AND ALL
;*****                             OTHER BYTES ZERO (INCLUDING 'P')
;*****
;*****              - INFINITY  -  NON-DECIMAL STRING WITH SE = $B AND ALL
;*****                             OTHER BYTES ZERO (INCLUDING 'P')
;*****
;*****                     NAN  -  NON-DECIMAL STRING WITH SE = $C, SM = $0
;*****                             MANTISSA = THE BINARY ADDRESS WHERE THE NAN
;*****                             WAS CREATED (RIGHT JUSTIFIED IN STRING) AND
;*****                             ALL OTHER BYTES ZERO.
;*****                             EXP = 0000,  'P' = 0
;*****
;*****       ELSE, THE BINARY NUMBER TO BE CONVERTED IS NORMALIZED OR NOT
;*****       NORMALIZED IN TYPE SO CONTINUE.
;*****
;*****
;******************************************************************************
;
;
;      JUMP BASED UPON THE TYPE INDEX IN REG D4
;
        JMP      TYPETABL(D4.W)                 ; *: CONVERT TO (PC,D4.W)
;
;      ********************
;
ZERO_OUT   EQU   *
;
        MOVE.L   #0,-(A7)                    ; PUT (EXP = MTISSA = 0) ON STACK
        MOVE.L   #0,-(A7)
        MOVE.L   #0,-(A7)
        TST.W    D7
        bmi.s    @3                          ; IF ('K' POSITIVE) THEN
           SUBQ.W   #1,D7                       ; 'P' = ('K' - 1)
        bra.s    @4                          ; ELSE ('K' NEGATIVE)
@3
           NEG.W    D7                          ; 'P' = -('K')
@4                                             ; END
        ANDI.W   #$00FF,D7                   ; SET (SE = SM = $0)
;
;      NOW D7.W CONTAINS SE AND SM IN THE MSBYTE AND 'P' IN THE LSBYTE.
;
        TST.L    D0
        bpl.s     @5                         ; IF (-TRUE ZERO) THEN
           ORI.W    #$0F00,D7                   ; SE = $0,  SM = $F
@5                                             ; END
        MOVE.W   D7,-(A7)                    ; PUT (SE, SM, AND 'P') ON STACK
        BRA      CALLSTR                     ; GO MOVE IT OUT
;
;
NAN_OUT    EQU   *
;
        MOVE.L   D2,-(A7)                    ; PUT (EXP = $0000,
        MOVE.L   #0,-(A7)                    ; MANTISSA = ZERO FILLED NAN
        MOVE.L   #0,-(A7)                    ; ADDRESS, RIGHT JUSTIFIED) ON STK
        MOVE.W   #$C000,-(A7)                ; PUT (SE = $C,  SM = 'P' = $0)
;                                              ON STACK
        BRA      CALLSTR                     ; GO MOVE IT OUT
;
;      ********************
;
TYPETABL   EQU   *
;
        BRA.S    NORM_OUT
        BRA.S    ZERO_OUT
        BRA.S    INFIN_OUT
        BRA.S    NAN_OUT
        BRA.S    NNORM_OUT
;
;      ********************
;
;
INFIN_OUT  EQU   *
;
        MOVE.L   #0,-(A7)                    ; PUT (EXP = MTISSA = 0) ON STACK
        MOVE.L   #0,-(A7)
        MOVE.L   #0,-(A7)
        TST.L    D0
        bmi.s    @6                          ; IF (+INFINITY)
           MOVE.W   #$A000,-(A7)                ; PUT (SE=$A, SM='P'=$0) ON STK
        bra.s    @7                          ; ELSE (-INFINITY)
@6
           MOVE.W   #$B000,-(A7)                ; PUT (SE=$B, SM='P'=$0) ON STK
@7                                            ; END
        BRA      CALLSTR                     ; GO MOVE IT OUT
;
;
NNORM_OUT  EQU   *
;
;      INPUT VALUES TYPED AS NOT-NORMALIZED COULD BE DENORMALIZED NUMBERS,
;      NON-ZERO UNNORMALIZED NUMBERS, OR UNNORMALIZED ZERO.  IF UNNORMALIZED
;      ZERO, JOIN NORMAL ZERO ROUTINE.
;
;
        TST.L    D1
        bne.s    @8                         ; IF (UNNORM ZERO) THEN
           TST.L    D2
           bne.s    @8
              BRA      ZERO_OUT                 ; GO TO NORMAL ZERO HANDLER
@8
;                                              ELSE (NON-ZERO-UNNORM OR DENORM)
        jsr     mc%cdnorm                  ; Conditionally normalize if denorm.
;                                                CONTINUE AS WITH NORM
;                                                END
;
;
NORM_OUT   EQU   *
;
;      SAVE THE CONTROL WORD.  FORCE EXTENDED PRECISION & RM ROUNDING MODE.
;      NOTE:  IT IS ASSUMED HERE THAT NORM-BIT & CLOSURE-BIT ARE DON'T
;      CARES FOR THE BALANCE OF THE BINDEC ALGORITHM.
;
;
        MOVE.W   CNTRL(A3),-(A7)             ; NO BYTE DATA ALLOWED ON STACK
        MOVE.B   #EXT_RM,MODE(A3)
;
;
;      SAVE COPY OF FP NUMBER ON STACK. TAKE THE ABSOLUTE VALUE OF CURRENT
;      COPY.
;
;
        MOVEM.L  D0-D2,-(A7)
        ANDI.L   #$00FFFFFF,D0               ; 'X' = ABS(FPN), MASK DON'T CARES
;                                                  FOR NEXT COMPARE.
;
;      IF ['X' IS DENORMED]  THEN 'ILOG' = (-4933) ELSE ['X' IS NOT DENORMED]
;      'ILOG' = FFIX [LOG10 ('X')] IN RM MODE (NOTE: RM MODE IS
;                                                           'FLOOR' FUNCTION).
;
;
        cmp.l   #<nnorm_l+extminxp>,d0             ; IF ('X' IS DENORM)
        bne     @9
           MOVE.W   #-4933,D1                         ; 'ILOG' = (-4933)
        bra     mv_ilog                            ; ELSE ('X' IS NORM OR
;                                                             UNNORM-NON-ZERO)
@9
;
;         SINCE LOG10 ('X') = LOG2 ('X') * LOG10 (2), FIND AN APPROXIMATION FOR
;         THE LOG2 ('X') WHICH IS LESS THAN OR EQUAL {TRUE_VALUE[LOG2 ('X')]}
;         FOR ALL NORMALIZED ('X').
;
;         IF ('X' IS NORM) THEN SUCH AN APPROX_LOG2 ('X') WILL ALWAYS BE THE
;         ALGEBRAIC SUM OF (EEEE.0) AND (0.FFF...FFF) EXPRESSED AS AN INTERNAL
;         FORMAT, EXTENDED FP NUMBER.
;
;
           LSL.L    #1,D2                       ; GET PURE FRACTION IN D1-D2
           ROXL.L   #1,D1
;
;
;         IF [EXP('X') IS NEGATIVE] THEN WE WISH TO PERFORM THE FP SUMMATION,
;         {TWO'S COMP[(EEEE.0) + (+0.FFF...FFF)]}, TO GET THE MAGNITUDE OF
;         APPROX_LOG2 ('X') EXPRESSED AS A POSITIVE FP NUMBER, THEN SET THE
;         SIGN BIT TO BE NEGATIVE.  REALIZING THAT (EEEE.0), THE INTERNAL
;         FORMAT EXP('X'), IS ACTUALLY A NEGATIVE TWO'S COMPLEMENT NUMBER,
;         IT CAN BE SHOWN THAT THE TWO'S COMPLEMENT OF THE HEX STRING,
;         (EEEE.FFF...FFF), WILL ALWAYS BE A POSITIVE TWO'S COMPLEMENT
;         STRING WHICH CAN THEN BE SHIFTED OVER TO FORM THE DESIRED INTERNAL
;         FORMAT FP NUMBER.
;
;
           TST.W    D0
           bpl      @10                         ; IF (EXPONENT NEGATIVE)
              BSET     #31,D0                      ; SET SIGN OF RESULT TO NEG.
              NEG.L    D2                          ; TWO'S COMP THE FRACTION
              NEGX.L   D1
              NEGX.W   D0                          ; TWO'S COMP THE EXP. TOO
@10                                             ; END
;
;
;******************************************************************************
;*****
;*****
;*****       THE ORIGINAL METHOD FOR SHIFTING THE SUM EXP.FFFF OVER UNTIL
;*****       IT IS NORMALIZED WAS
;*****           MOVEQ.L   #-1,D6
;*****           REPEAT
;*****              ADDQ.W   #1,D6
;*****              LSR.W    #1,D0
;*****              ROXR.L   #1,D1
;*****              ROXR.L   #1,D2
;*****           UNTIL D0<EQ>#0
;*****           MOVE.W   D6,D0
;*****       WITH THE REPEAT LOOP IMPLEMENTED WITH A TST-BNE INSTEAD OF A
;*****       CMP-BNE.
;*****
;*****       THE METHOD USED BELOW COMPARES IN SPEED AS FOLLOWS:
;*****           $EEEE = %0000 0000 0000 0000      SLOWER BY 12 CYCLES
;*****           $EEEE = %0000 0000 0000 0001      SLOWER BY 12 CYCLES
;*****           $EEEE = %0000 0000 0000 001X      SLOWER BY  8 CYCLES
;*****           $EEEE = %0000 0000 0000 01XX      SLOWER BY  4 CYCLES
;*****           $EEEE = %0000 0000 0000 1XXX      EQUAL
;*****           $EEEE = %0000 0000 0001 XXXX      FASTER BY  4 CYCLES
;*****              |             |                        |
;*****           $EEEE = %01XX XXXX XXXX XXXX      FASTER BY 44 CYCLES
;*****
;*****       THUS THE METHOD USED BELOW IS INSIGNIFICANTLY SLOWER ONLY
;*****       IN 8 OF THE 65,535 POSSIBLE CASES.
;*****
;*****
;******************************************************************************
;
;
;         SHIFT THE SUM EXP.FFFF OVER UNTIL IT IS NORMALIZED. THE (NUMBER
;         OF SHIFTS - 1) IS THE EXPONENT OF THE RESULT.
;
;
           moveq     #15,D6                     ; INIT COUNTER FOR LOOP
LOG2NORM      LSR.W    #1,D0                       ; SHIFT THE STRING,
              ROXR.L   #1,D1                       ; $EEEE.FF...FF ONE BIT
              ROXR.L   #1,D2                       ; TO THE RIGHT.
              TST.W    D0                   ; +-->   IF ($EEEE <> 0)
              DBEQ     D6,LOG2NORM          ; |         D6 := D6 -1
;                                             |         IF <PL> THEN
;            THE KEY HERE IS THAT D6 WILL     |            GO TO LOG2NORM
;            NEVER GO TO (-1) SINCE A MAX     |         END
;            SHIFT OF 16 WOULD CAUSE THIS ----+      ELSE ($EEEE = 0)
;            CONDITIONAL TO FAIL, PREVENT-              STRING IS NORM'ED
;            ING D6 FROM BEING DECR TO (-1).         END
;
           MOVE.W   #15,D0                      ; CALCULATE THE NEW EXPONENT
           SUB.W    D6,D0                       ; NEW_EXP = 14 - REG_D6
;
;
;         NOW HAVE APPROX_LOG2 ('X') IN REG D0-D2 IN EXTENDED INTERNAL FORMAT.
;
;
;******************************************************************************
;*****
;*****       NOTE:
;*****
;*****       IF ('X' WAS NORM ORIGINALLY) THEN
;*****
;*****       CASE:
;*****
;*****       1).  ('X' >= 2)
;*****
;*****              [$800...000 E$0001 <= 'X' <= $FFF...FFF E$3FFE)
;*****
;*****              HERE [EXP('X') >= (+1) ] AND [FRACT ('X') = DON'T CARE], SO
;*****              APPROX_LOG2 ('X')  =  NORMALIZED, POSITIVE, NON-ZERO,
;*****                                  WITH NEW_EXP >= 0
;*****              THEREFORE
;*****              +1.00...00E+0 <= APPROX_LOG2 ('X') <= +1.11...11E+13
;*****              OR
;*****              +1 <= APPROX_LOG2 ('X') <= (2^14 - 2^-50)
;*****
;*****              APPROX_LOG10 ('X')  = APPROX_LOG2 ('X')  *  LOG10 (2)
;*****              APPROX_LOG10 ('X') >= LOG10 (2)
;*****                               >= 0.3
;*****
;*****              ILOG = FFIX [APPROX_LOG10 ('X')]  IN RM ROUNDING MODE
;*****              ILOG >= 0
;*****
;*****              NOTE THAT FOR THE LARGEST POSSIBLE VALUE OF ('X') ONLY
;*****              14 SHIFTS WILL BE PERFORMED IN THE 'LOG2NORM' LOOP.
;*****
;*****       2).  (1 < 'X' < 2)
;*****
;*****              [EXP('X') = 0 ] AND [FRACT ('X') = NON-ZERO]
;*****
;*****              APPROX_LOG2 ('X') = UNNORMALIZED, POSITIVE, NON-ZERO
;*****                                WITH NEW_EXP = 0.
;*****
;*****              +0.00...01E+0 <= APPROX_LOG2 ('X') <= +0.11...11E+0
;*****
;*****              APPROX_LOG10 ('X') = APPROX_LOG2 ('X')  *  LOG10 (2)
;*****
;*****              ALTHOUGH THIS MULTIPLY INVOLVES ONE UNNORMALIZED
;*****              OPERAND WHICH WILL PRODUCE AN UNNORMALIZED PRODUCT
;*****              (A PRODUCT OF UNNORMALIZED ZERO IS NOT POSSIBLE
;*****              WHEN AT LEAST ONE OPERAND IS NORMALIZED - THE
;*****              CONSTANT LOG10 (2) IS ALWAYS NORMALIZED HERE),
;*****              THE VALUE FOR
;*****                       ILOG = FFIX [APPROX_LOG10 ('X')]
;*****              WILL STILL BE CORRECT DUE TO RM ROUNDING MODE.
;*****                       ILOG = 0
;*****              THIS AGREES WITH THE VALUE FOR TRUE_ILOG WHERE
;*****              (1 < 'X' < 2)  AND  [0 < TRUE_LOG10 ('X') < 0.3].
;*****
;*****       3).  ('X' = 1)
;*****
;*****              [EXP('X') = 0 ] AND [FRACT ('X') = ZERO]
;*****
;*****              APPROX_LOG2 ('X')  =  UNNORMALIZED, POSITIVE ZERO
;*****                                               WITH NEW_EXP = 0
;*****              APPROX_LOG2 ('X')  =  +0.00...00E+0
;*****
;*****              APPROX_LOG10 ('X')  = APPROX_LOG2 ('X')  *  LOG10 (2)
;*****
;*****              SINCE THE MULTIPLY ROUTINE DOES NOT ACCEPT UNNORM-ZERO
;*****              AS AN INPUT OPERAND, THE FOLLOWING TEST MUST BE PERFORMED.
;*****                 IF [APPROX_LOG2 ('X') = UNNORM ZERO] THEN
;*****                    ILOG = 0
;*****                 ENDIF
;*****              THIS AGREES WITH THE VALUE FOR TRUE_ILOG WHERE
;*****              ('X' = 1) AND [TRUE_LOG10 ('X') = 0].
;*****
;*****       4).  (0.5 < 'X' < 1)
;*****
;*****              [EXP('X') = -1 ] AND [FRACT ('X') = NON-ZERO]
;*****
;*****              APPROX_LOG2 ('X') = UNNORMALIZED, NEGATIVE, NON-ZERO
;*****                                WITH NEW_EXP = 0
;*****
;*****              -0.00...01E+0 <= APPROX_LOG2 ('X') <= -0.11...11E+0
;*****
;*****              APPROX_LOG10 ('X') = APPROX_LOG2 ('X')  *  LOG10 (2)
;*****
;*****              ALTHOUGH THIS MULTIPLY INVOLVES ONE UNNORMALIZED
;*****              OPERAND WHICH WILL PRODUCE AN UNNORMALIZED PRODUCT
;*****              (A PRODUCT OF UNNORMALIZED ZERO IS NOT POSSIBLE
;*****              WHEN AT LEAST ONE OPERAND IS NORMALIZED - THE
;*****              CONSTANT LOG10 (2) IS ALWAYS NORMALIZED HERE),
;*****              THE VALUE FOR
;*****                       ILOG = FFIX [APPROX_LOG10 ('X')]
;*****              WILL STILL BE CORRECT DUE TO RM ROUNDING MODE.
;*****                       ILOG = -1
;*****              THIS AGREES WITH THE VALUE FOR TRUE_ILOG WHERE
;*****              (0.5 < 'X' < 1)  AND  [-0.3 < TRUE_LOG10 ('X') < 0].
;*****
;*****       5).  ('X' = 0.5)
;*****
;*****              [EXP('X') = -1 ] AND [FRACT ('X') = ZERO]
;*****
;*****              APPROX_LOG2 ('X') = NORMALIZED NEGATIVE '1'
;*****                                WITH NEW_EXP = 0
;*****              APPROX_LOG2 ('X') = -1.00...00E+0
;*****
;*****              APPROX_LOG10 ('X') = APPROX_LOG2 ('X')  *  LOG10 (2)
;*****              APPROX_LOG10 ('X') = -[LOG10 (2)]
;*****                               = -0.3
;*****
;*****              ILOG = FFIX [APPROX_LOG10 ('X')]  IN RM ROUNDING MODE
;*****              ILOG = -1
;*****
;*****       6).  (0.5 < 'X' < 0)
;*****
;*****              [EXP('X') <= (-2) ] AND [FRACT ('X') = DON'T CARE]
;*****
;*****              APPROX_LOG2 ('X') = NORMALIZED, NEGATIVE, NON-ZERO,
;*****                                WITH NEW_EXP >= 0
;*****                                [NEW_EXP = 0 ONLY FOR EXP('X') = (-2)
;*****                                AND FRACT('X') = NON-ZERO]
;*****
;*****              -1.00...00E+0 > APPROX_LOG2 ('X') >= -1.00...00E+14
;*****
;*****              APPROX_LOG10 ('X') = APPROX_LOG2 ('X')  *  LOG10 (2)
;*****              APPROX_LOG10 ('X') < -[LOG10 (2)]
;*****                               < -0.3
;*****
;*****              ILOG = FFIX [APPROX_LOG10 ('X')]  IN RM ROUNDING MODE
;*****              ILOG < -1
;*****
;*****              NOTE THAT FOR THE SMALLEST POSSIBLE VALUE OF ('X'),
;*****              [WHERE EXP('X') = $C000 AND FRACT('X') = ZERO],
;*****              ONLY 15 SHIFTS WILL BE PERFORMED IN THE 'LOG2NORM'
;*****              LOOP YIELDING AN APPROX_LOG2('X') = -1.00...00E+14.
;*****
;*****       ENDCASE
;*****       ENDIF
;*****
;*****
;******************************************************************************
;
;
;         IF [APPROX_LOG2('X') IS AN UNNORMALIZED ZERO] THEN
;            'ILOG' = 0,
;         ELSE 'ILOG' = FFIX [APPROX_LOG2('X')  *  LOG 10 (2)].
;
;
           TST.L    D1
           bne      log10calc                  ; IF [APPROX_LOG2('X') UNNORM]
              TST.L    D2
              beq      @11                         ; IF (NOT UNNORM ZERO) THEN
                 BRA.S    LOG10CALC                   ; CALC LOG10 ('X') SAME
@11                                                   ; AS NORM
;                                                    ELSE (UNNORM ZERO)
;                                                       NO NEED TO SET D1.W TO
;                                                       ZERO - IT ALREADY IS!!
;                                                    END
           bra  mv_ilog                            ; ELSE [APPROX_LOG2('X') NORM
;                                                          OR UNNORM NON-ZERO]
LOG10CALC     EQU   *
;
;
;            MULTIPLY APPROX_LOG2 ('X') NOW IN REG D0-D2 BY LOG10 (2) CONSTANT.
;            THIS PRODUCT WILL ALWAYS BE OF TYPE NORM OR NON-ZERO-UNNORM
;            [THE WORST CASE IS A PRODUCT OF AN UNNORM APPROX_LOG2 ('X') AND
;            A NORM LOG10 (2) CONSTANT; THIS PRODUCT CAN NEVER BE AN UNNORM
;            ZERO].  THE 'mc%mul_opt' ROUTINE DOESN'T CARE ABOUT TAGS; HOWEVER,
;            CLEAR APPROX_LOG2('X') TAG FIELD TO SAVE HAVING TO TAG THE
;            RESULT LATER - MORE ABOUT THIS IN FOLLOWING COMMENTS.
;
;
              ANDI.L   #$8000FFFF,D0               ; CLEAR APPROX_LOG2('X')
;                                                    TAG FIELD
;
              MOVE.L   #$0000FFFE,(A2)             ; LOAD LOG10(2) CONSTANT
              MOVE.L   #$9A209A84,FRACT(A2)                      ; INTO FPN
              MOVE.L   #$FBCFF799,FRACT+4(A2)
              MOVEM.L  D7/A1,-(A7)                 ; FMULT KILLS A1 & D7, SAVEM
;                                                    CALLER DELIVERED CLR D3
              jSR      mc%mul_opt                   ; RETURNS UNROUNDED
;                                                    APPROX_LOG10('X') IN D0-D3
;
;            ORIGINALLY HAD 'BSR  ROUND' HERE - SEE COMMENTS BELOW.
;
;            HAVE THE UNROUNDED APPROX_LOG10('X') IN REGS. D0-D2 AND THE
;            G-R-S BITS FROM THE MULTIPLY IN REG D3.
;
;
;**    *****************************************************************
;**
;**
;**       WHERE ARE THE VARIABLES.
;**
;**
;**       REGISTER D0-D2         UNROUNDED APPROX_LOG10('X')
;**
;**       REGISTER D3            G-R-S BITS
;**
;**       REGISTER D7.W          'K'  (WORD)
;**
;**
;** A7 -> SAVED ON STACK         (INPUT VALUE)  (3 LONG WORDS)
;**
;**       SAVED ON STACK         ORIGINAL CONTROL REGISTER  (WORD)
;**
;**
;******************************************************************************
;*****
;*****
;*****       THE QUESTION HERE IS, CAN THIS MULTIPLY OVERFLOW OR UNDERFLOW?.
;*****       THE TWO OPERANDS ARE
;*****
;*****        LOG10(2) CONSTANT = NORMALIZED POSITIVE NUMBER
;*****                          = +1.001...101E-2
;*****
;*****        APPROX_LOG2('X')  =
;*****
;*****           CASE 1: NORMALIZED POSITIVE NUMBER
;*****              +1.00...00E+0 <= APPROX_LOG2 ('X') <= +1.11...11E+13
;*****
;*****           CASE 2: UNNORMALIZED POSITIVE NUMBER
;*****              +0.00...01E+0 <= APPROX_LOG2 ('X') <= +0.11...11E+0
;*****
;*****           CASE 4: UNNORMALIZED NEGATIVE NUMBER
;*****              -0.00...01E+0 <= APPROX_LOG2 ('X') <= -0.11...11E+0
;*****
;*****           CASE 5: NORMALIZED NEGATIVE NUMBER
;*****              APPROX_LOG2 ('X') = -1.00...00E+0
;*****
;*****           CASE 6: NORMALIZED NEGATIVE NUMBER
;*****              -1.00...00E+0 > APPROX_LOG2 ('X') >= -1.00...00E+14
;*****
;*****       THEN THE RANGE OF APPROX_LOG10('X') FOR THESE CASE WOULD BE
;*****
;*****           CASE 1: NORMALIZED POSITIVE NUMBER
;*****              +1.001...101E-2 <= APPROX_LOG10 ('X') <= +1.001...101E+12
;*****
;*****           CASE 2: NON-ZERO-UNNORM OR NORMALIZED POSITIVE NUMBER
;*****              +0.00...01E-2 <= APPROX_LOG10 ('X') <= +1.001...011E-2
;*****
;*****           CASE 4: NON-ZERO-UNNORM OR NORMALIZED NEGATIVE NUMBER
;*****              -0.00...010E-2 <= APPROX_LOG10 ('X') <= -1.001...100E-2
;*****
;*****           CASE 5: NORMALIZED NEGATIVE NUMBER
;*****              APPROX_LOG10 ('X') = -1.001...101E-2
;*****
;*****           CASE 6: NORMALIZED NEGATIVE NUMBER
;*****              -1.001...101E-2 > APPROX_LOG10 ('X') >= -1.001...101E+12
;*****
;*****       OBVIOUSLY, NO OVERFLOW OR UNDERFLOW CAN OCCUR.
;*****
;*****
;*****       AS NOTED ABOVE, THE MULTIPLY WHICH PRODUCES APPROX_LOG10('X')
;*****       WAS ORIGINALLY ROUNDED IN 'RM' MODE.
ÿ