(* File Libfp/Financial, Version 13 March 1983. *)

procedure do_npv ( first, last, net : integer ; rate : extended ;
                        var npv : extended ;
                        procedure payment( i : integer ; var pmt : extended ) ) ;

        (* Do_NPV computes net present value OBSERVING THE CURRENT ROUNDING MODE
           mostly so that it can be used by internal rate of return routines. *)

var
present, past, future : extended ;
j, k, n, stop : longint ;
anf, kpmt, jpmt, int, r1, fpk0 : extended ;
ccneq : boolean ;
sgn : integer ;
fpkm1 : extended ;

procedure pastcompound ( n : longint ) ;
        (* Computes past := past * (1+rate)^n. *)
var 
cif, xn : extended ;
begin (* pastcompound *)
if n = 1 then
        begin (* n = 1 *)
        cif := past ;
        mulx( rate, cif ) ; (* cif := rate * past *)
        addx( cif, past ) ; (* past := past + rate * past *)
        end   (* n = 1 *)
        else
        begin (* n > 1 *)
        l2x( n, xn ) ;
        compound( rate, xn, cif ) ;
        mulx( cif, past ) ;
        end   (* n > 1 *) ;
end   (* pastcompound *);

procedure futurecompound ( n : longint ) ;
        (* Computes future := future / (1+rate)^n. *)
var 
cif, xn : extended ;
begin (* futurecompound *)
if n = 1 then
        begin (* n = 1 *)
        cif := future ;
        mulx( r1, cif ) ; (* cif := r1 * future *)
        subx( cif, future ) ; (* future := future -   r1 * future *)
        end   (* n = 1 *)
        else
        begin (* n > 1 *)
        l2x( n, xn ) ;
        compound( rate, xn, cif ) ;
        divx( cif, future ) ;
        end   (* n > 1 *) ;
end   (* futurecompound *);

procedure doannuity  ( n : longint ) ;
        (* Computes anf := kpmt * annuity ( rate, n ) *)
var 
xn : extended ;
begin
l2x( n, xn ) ;
annuity ( rate, xn, anf ) ;
mulx( kpmt, anf ) ;
end ;

procedure cmpmt ;
        (* Compares kpmt and jpmt, bitwise. *)
var i : integer ;
begin
payment( j, jpmt ) ;
i := 0 ;
while (kpmt[i] = jpmt[i]) and (i < 4) do i := i + 1 ;
if i < 4 then ccneq := true  else ccneq := kpmt[4] <> jpmt[4] ;
end ;

begin (* fp_npv *)
i2x( 0, fpk0 ) ;
past := fpk0 ;
future := fpk0 ;

i2x(-1, fpkm1 ) ;
if relx( rate, fpkm1) <> eq then 
begin (* r <> -1 *)

if first < net then
        begin (* first < net *)
        if last < net then stop := last else stop := net - 1 ;
        k := first ;
        payment(k, kpmt) ;
        repeat
        j := k ;
        if k < stop then
                begin (* k < stop *)
                repeat
                        j := j + 1 ;
                        cmpmt ;
                until ccneq or (j = stop) ;
                if ccneq then j := j - 1 ;
                end   (* k < stop *) ;
                (* Payments k..j are equal. *)
        if k = j then
                begin (* one payment *)
                addx( kpmt, past ) ;
                end   (* one payment *)
                else
                begin (* several equal payments *)
                pastcompound(j-k) ; (* Move past value forward. *)
                if classx(kpmt,sgn) <> zero then
                        begin (* several non-zero *)
                        doannuity( k-j-1) ;
                        subx( anf , past ) ;      (* Add in to past balance. *)
                        end   (* several non-zero *) ;
                end   (* several equal payments *) ;
        pastcompound(1) ;
        k := j + 1 ;
        kpmt := jpmt ;
        until k > stop ;
        if net > k then pastcompound( net-k ) ;
        end   (* first < net *) ;

if last > net then
        begin (* last > net *)
        i2x( 1, int) ;
        addx( rate,int ) ; (* int:= 1+r. *)
        r1 := rate ;
        divx(int, r1  ) ;  (* r1 := r/(1+r). *)
        if first > net then stop := first else stop := net+1 ;
        k := last  ;
        payment(k, kpmt) ;
        repeat
        j := k ;
        if k > stop then
                begin (* k > stop *)
                repeat
                        j := j - 1 ;
                        cmpmt ;
                until ccneq or (j =stop) ;
                if ccneq then j := j + 1 ;
                end   (* k > stop *) ;
                (* Payments j..k are equal. *)
        if k = j then
                begin (* one payment *)
                addx( kpmt, future ) ;
                futurecompound(1) ;
                end   (* one payment *)
                else
                begin (* several equal payments *)
                futurecompound( k-j+1 ) ;
                if classx(kpmt,sgn) <> zero then
                        begin (* several non-zero *)
                        doannuity( k-j+1 ) ; (* Payment multiplier. *)
                        addx( anf , future ) ;      (* Add in to future balance. *)
                        end   (* several non-zero *) ;
                end   (* several equal payments *) ;
        k := j - 1 ;
        kpmt := jpmt ;
        until k < stop;
        if net < k then futurecompound( k-net ) ;
        end   (* last > net *) ;

end (* r <> -1 *) ;

if (first <= net) and (net <= last) 
        then payment( net, present) 
        else present := fpk0 ;

addx( past, present ) ;
addx( future, present ) ;
npv := present ;
end   (* do_npv *) ;

procedure fin_npv ;

var
rd : rounddir ;
env : environ ;

begin (* fin_npv *)
rd := getrnd ;
procentry(env) ;
setrnd(rd) ; (* Observe caller's rounding mode. *)
do_npv( first, last, net, rate, npv, payment ) ;
procexit(env) ;
end   (* fin_npv *) ;

procedure fin_2return (         { Compute rate of return implied by two payments. }
        first,                  { Period at which initial amount is paid. }
        last    : integer ;     { Period at which final amount is paid. }
        initial,                { Initial payment. }
        final : extended ;      { Final payment. }
        var rate : extended     { Rate of return from 
                                   (1+rate)^(last-first) = -final/initial.  }
        ) ;

var
n : longint ;
delta, xn : extended ;
env : environ ;

begin (* fin_2return *)
procentry( env ) ;
n := last ;
n := n - first ;
l2x( n, xn ) ;
delta := final ;
addx( initial, delta ) ; (* delta := initial + final *)
divx( initial, delta ) ; 
negx( delta ) ; (* delta := -1 - final/initial *)
ln1x( delta ) ; (* delta := loge( -final/initial ) *)
divx( xn, delta ) ; (* delta := loge/n *)
exp1x( delta ) ; (* delta := expe(loge/n) - 1 *)
procexit( env ) ;
rate := delta ;
end   (* fin_2return *) ;

procedure fin_irrreturn ( { Compute internal rate of return of series of payments. }
                      
        first,                  { Period at which first payment occurs. }
        last    : integer ;     { Period at which final payment occurs. }
        var ncs : integer ;     { Error code = number of changes of sign among
                                  coefficients; on normal return ncs = 1.
                                  ncs = -2 if an inf or nan coefficient was
                                  supplied; -4 if insufficient heap space was
                                  available; if ncs < 1 then irr will be a NaN.
                                  if odd ncs >=1 then a numerical solution will
                                  be returned which may not be unique;
                                  if even ncs >=2 then a numerical solution will
                                  be returned if one is found; none may be found
                                  even if it exists; if none is found irr will
                                  be a NaN. }
        var irr : extended ;    { Internal rate of return found; may be NaN. }
        procedure     payment ( { User procedure to compute periodic payment. }
                i : integer ;   { fin_irrreturn guarantees  first <= i <= last. }
                var pmt : extended )  { Net payment due at period i, 
                                  positive to investor, negative to investment. }
        ) ;

label 1, 4, 5, 6 ;

var
i : integer ;
pmt : extended ;
sgn, cursign : integer ;
env : environ ;
x, p, p1 : extended ;
firstc, lastc : integer ; (* first and last acceptable coefficients *)
left, right, center : extended ;
mid, newton : extended ;
zerominus : boolean ;
rp, rp1, lp, lp1 : extended ;
nfe  : integer ;
slope, den : extended ;
np : integer ; 
found : boolean ;
leftbound : extended ;
method : integer ;
firstsign : integer ;
firstsum, lastsum : extended ;
factor, wfirstsum, wlastsum : extended ;
guess, guessp, guessp1 : extended ;
donewton : boolean ;

procedure eval ;
        (* Computes p = f(x) and p1 := f'(x).
           Computes error bounds and exits if zero is found. *)

var
fl, fu, df : extended ; (* upper and lower bounds for p and p1 *)

procedure peval ( i : integer; var pmt : extended ) ;
var
rnd : rounddir ;
begin
rnd := getrnd ;
payment( i, pmt ) ;
setrnd( rnd ) ;
end ;

procedure p1eval ( i : integer; var pmt : extended ) ;
var
factor : extended ;
begin
payment( i, pmt ) ;
l2x( ord4(lastc) - i, factor ) ;
mulx( factor, pmt ) ;
end ;

begin (* eval *)
if donewton then fin_npv( firstc, lastc-1, lastc-1, x, p1, p1eval ) ;
clearxcps ; (* Previous don't matter. *)
fin_npv( firstc, lastc, lastc, x, p, peval ) ;
 
nfe := nfe + 1 ;
{
write(' nfe ',nfe) ;
write(' exp diff ',
        getexponent(x)-(getexponent(p)-getexponent(p1)),' x ') ;
showx(x) ;
 }
if classx( p, sgn ) = zero then
        begin
        {
        writeln(' FOUND: function vanished ') ;
        }
        goto 1 ;
        end ;
        
if donewton then
        begin
        fl := p ;
        fu := p ;
        df := p1 ;
        scalbx( getexponent(x)-64, df ) ;
        absx(df) ;
        setrnd( downward ) ;
        subx( df, fl ) ;
        setrnd(   upward ) ;
        addx( df, fu ) ;
        setrnd( tonearest ) ;
        
        if (signofx(fl) <> signofx(fu)) then 
                begin
                {
                writeln(' FOUND: negligible Newton step ') ;
                }
                goto 1 ;
                end ;
        end ;

end   (* eval *) ;

procedure getcenter ;
begin
center := left ;
addx( right, center ) ;
scalbx( -1, center ) ;
if (relx( left, center ) = eq) or (relx( center, right) = eq) then 
        begin
        {
        writeln(' FOUND: negligible x interval ') ;
        }
        if getexponent( lp ) < getexponent( rp ) then x := left else 
        if getexponent( rp ) < getexponent( lp ) then x := right else
        x := center ;
        goto 1 ;
        end ;
end ;

procedure checkmid ;
        (* given a function evaluation point mid, checks whether it can
           replace left or right *)

const
deltaexp = 8 ; (* Required decrement of exponent. *)

var 
dobisect : boolean ;
del, del2 : extended ;

begin (* checkmid *)
x := mid ;
eval ;
if signofx(p) = signofx(rp) then
         begin (* new interval [left,mid] *)
                 dobisect := not (relx( mid, center ) in [lt,eq])
                        and ((getexponent(rp)-getexponent(p)) < deltaexp) ;
                 right := mid ;
                 rp := p ; rp1 := p1 ;
                 getcenter ;
         end   (* new interval [left,mid] *)
         else
         begin (* new interval [mid,right] *)
                 dobisect := not (relx( center, mid ) in [lt,eq])
                        and ((getexponent(lp)-getexponent(p)) < deltaexp) ;
                 left  := mid ;
                 lp := p ; lp1 := p1 ;
                 getcenter ;
         end   (* new interval [mid,right] *) ;

if dobisect then goto 6 ;
end   (* checkmid *) ;

begin (* fin_irrreturn *)
procentry(env) ;
found := false ;
ncs := -6;
np := 0 ; (* Count number of interesting payments. *)
i2x( 0, lastsum ) ;
wlastsum := lastsum ;
for i := first to last do
        begin (* gather coefficients *)
        payment( i, pmt ) ;
        case classx( pmt, sgn ) of
        denormal, normal : if ncs <> -2 then
                begin (* number *)
                lastc := i ;
                np := np + 1 ;
                i2x(i, factor) ;
                if ncs = -6 then 
                        begin (* first coefficient *)
                        firstc := i ;
                        cursign := sgn ;
                        ncs := 0 ;
                        firstsign := sgn ;
                        firstsum := pmt ;
                        wfirstsum := pmt ;
                        mulx( factor, wfirstsum) ;
                        end   (* first coefficient *)
                        else
                        begin (* check variation of sign *)
                        if sgn = firstsign 
                                then addx( pmt, firstsum)  
                                else addx( pmt, lastsum) ;
                        mulx( factor, pmt ) ;
                        if sgn = firstsign 
                                then addx( pmt, wfirstsum ) 
                                else addx( pmt, wlastsum ) ;
                        if cursign <> sgn then 
                                begin (* sign varied *)
                                ncs := ncs + 1 ;
                                cursign := sgn ;
                                end   (* sign varied *) ;
                        end   (* check variation of sign *) ;
                end   (* number *) ;
        infinite, snan, qnan : ncs := -2 ;
        end (* case classx *) ;
        end   (* gather coefficients *) ;
if ncs >= 1 then
        begin (* normal *)
        i2x(-1, leftbound) ;
                (* Generate initial guess by looking at two weighted positive and
                        negative payments. *)
        divx( firstsum, wfirstsum ) ; 
        divx( lastsum, wlastsum ) ;
        subx( wlastsum, wfirstsum ) ;
        x := firstsum ;
        addx( lastsum, x ) ;
        divx( lastsum, x ) ;
        negx(x) ;
        ln1x( x ) ;
        divx( wfirstsum, x ) ;
        if (relx( leftbound, x) <> lt) or 
                (classx( x, sgn ) in [infinite, qnan, snan]) then
                begin
                (* Generate initial guess by looking at two unweighted positive
                        and negative payments. *)
                fin_2return( firstc, lastc, firstsum, lastsum, x ) ;
                end ;
        if ( ncs = 1) and (np = 2 ) then
                begin (* two payments *)
                {
                writeln(' FOUND: two payments ') ;
                }
                goto 1 ;
                end   (* two payments *) ;
        nfe := 0 ;
                (* Start with Newton. *)
        donewton := true ;
        eval ;
        guess := x ;
        guessp := p ; guessp1 := p1 ;
        repeat
                left := x ;
                lp := p ;
                lp1 := p1 ;
                newton := lp ;
                divx( lp1, newton) ;
                x := left  ;
                subx(newton,x) ;
                if relx( leftbound, x ) <> lt then x := leftbound ;
                                (* new point out of range *)
                eval ;
                if signofx(p) <> signofx(lp) then 
                        begin (* bracketed *)
                        if relx( left, x) = lt then
                                begin (* x is on right *)
                                right := x ;
                                rp := p ;
                                end   (* x is on right *)
                                else
                                begin (* left is on right *)
                                right := left ;
                                rp := lp ;
                                left := x ;
                                lp := p ;
                                end   (* left is on right *) ;
                        goto 5 ;
                        end   (* bracketed *) ;
        until getexponent(p) >= getexponent(lp) ;

           donewton := false ;
           if relx( leftbound, x) <> eq  then
                begin 
                guess := x ;
                guessp := p ;
                x := leftbound ;
                eval ;
                end ;
           left := x ; lp := p ; 
           zerominus := signofx(guessp) ;
           while (signofx(p) = zerominus) do
                begin
                if not odd(ncs) then 
                     (* avoid endless search for sign change; quit when x=4 *)
                        if getexponent(x) >= 2 then goto 4 ;
                left := x ; lp := p ;
                adds(  1.0, x ) ;
                eval ;
                end ;
        
        if relx( x, guess) = lt then
                begin (* x < guess *)
                left := x ; lp := p ;
                right := guess ; rp := guessp ;
                end   (* x < guess *)
                else
                begin (* x > guess *)
                right:= x ; rp := p ;
                if relx( left, guess ) = lt then
                        begin (* guess becomes left endpoint *)
                        left  := guess ; lp := guessp ;
                        end   (* guess becomes left endpoint *) ;
                end   (* x > guess *) ;
5:
           donewton := false ;
                getcenter ;
                method := 0 ;
                repeat 
                       
                      repeat
                              (* Try secant. *)
                      slope := left ;
                      subx( right, slope ) ;
                      den := lp ;
                      subx( rp, den ) ;
                      divx( den, slope ) ;
                      if getexponent(rp) < getexponent(lp) then
                        begin (* compute from right *)
                        {
                        write(' RSECANT ') ;
                        }
                        mid := right ;
                        mulx( rp, slope ) ;
                        subx( slope, mid ) ;
                        end   (* compute from right *)
                        else
                        begin (* compute from left *)
                        {
                        write(' LSECANT ') ;
                        }
                        mid := left  ;
                        mulx( lp, slope ) ;
                        subx( slope, mid ) ;
                      if relx( mid, left) = eq then nextx( mid, right) ;
                        end   (* compute from left *) ;
                      if relx( mid,right) = eq then nextx( mid, left ) ;
                      if relx( mid, left) = eq then nextx( mid, right) ;
                      if (relx( left, mid )= lt) and
                         (relx( mid, right) = lt) 
                         then checkmid 
                         else 
                         begin
                         goto 6 ;
                         end ;
                      until false ;
                       
6:
                              (* Try bisection. *)
                      {
                      write('  BISECT ') ;
                      }
                      method := method + 1 ;
                      mid := center ;
                      checkmid ;
            until false ;
1 : 
        found := true ;
        irr := x ;
        end   (* normal *) ;
4:
if not found then 
        begin
        errornan( nanirr, irr ) ;
        end ;
procexit(env) ;
end   (* fin_irrreturn *) ;

procedure fin_return  (* { Analyze series of payments for external or internal
                            rate of return. 
                            Discounting by external rates may be specified
                            for positive or negative payments or both or neither.
                            Standard internal rate of return is obtained by
                            specifying e. g. negperiod, posperiod := first-1.
                            A conservative external rate of return is obtained by
                            considering negative payments as from the investor to
                            the investment, positive payments as from the investment
                            to the investor, and specifying 
                            negperiod := first ; posperiod := last ;
                            negrate := guaranteed safe rate of return ;
                            posrate := expected average portfolio reinvestment 
                                rate of return. }
                      
        first,                  { Period at which initial payment is available. }
        last    : integer ;     { Period at which final payment is to be computed. }
        negperiod, posperiod :  { Periods to which negative or positive payments }
        integer ;               { are to be discounted; if < first or > last then
                                  corresponding payments are not discounted. }
        negrate, posrate :      { Discount rates for negative and positive payments }
        extended ;              { respectively; ignored if corresponding period
                                  does not satisfy first <= ...period <= last. }
        var ncs : integer ;     { Error code = number of changes of sign among
                                  adjusted payments; on normal return ncs = 1.
                                  ncs = -2 if an inf or nan payment was
                                  supplied; -4 if insufficient heap space was
                                  available.  }
        var ret : extended ;    { Rate of return: if there is exactly
                                  one change of sign among the adjusted 
                                  payments ret will
                                  contain the single real root > -1; if an
                                  even number of changes of sign, ret will be nan;
                                  if odd number > 1, will return some
                                  real root > -1. }
        procedure     payment ( { User procedure to compute periodic payment. }
                i : integer ;   { fin_return guarantees  first <= i <= last. }
                var pmt : extended )  { Net payment due at period i. }
        *) ;

type
coefrec = record
        index : integer ;
        a : extended ; (* Coefficient a[index]. *)
        end ;
        
var
env : environ ;
marker : ^ integer ;
crsize : integer ;
coefs : longint ;
pc : ^ coefrec ;
keep : boolean ;
i, firstc, lastc, sgn  : integer ;
pmt, fpk0 : extended ;
posnpv, negnpv : extended ;
curindex : integer ;

procedure pospayment ( i : integer ; var pmt : extended ) ;
        (* Returns positive payments only. *)
var p : extended ;
begin (* pospayment *)
payment(i, p ) ;
if signofx( p ) then pmt := fpk0 else pmt := p ;
end   (* pospayment *) ;

procedure negpayment ( i : integer ; var pmt : extended ) ;
        (* Returns negative payments only. *)
var p : extended ;
begin (* negpayment *)
payment(i, p ) ;
if signofx( p ) then pmt := p else pmt := fpk0 ;
end   (* negpayment *) ;

procedure payproc ( i : integer ; var pmt : extended ) ;
begin (* payproc *)
while (i > pc^.index) and (pc^.index < lastc)  do pc := pointer( ord4(pc) + crsize ) ;
while (i < pc^.index) and (pc^.index > firstc) do pc := pointer( ord4(pc) - crsize ) ;
if i = pc^.index then pmt := pc^.a else pmt := fpk0 ;
end   (* payproc *) ;

begin (* fin_return *)
procentry(env) ;
mark( marker ) ;
crsize := sizeof( coefrec ) ;

coefs := fp_new( (last - ord4(first) + 1) * crsize ) ;
if coefs = ord(nil) then
        begin
        ncs := -4 ; (* insufficient heap error *)
        end
        else
        begin (* heap ok *)
        i2x( 0, fpk0 ) ;
        pc := pointer(coefs) ;
        ncs := -6;
        for i := first to last do
                begin (* gather coefficients *)
                payment( i, pmt ) ;
                case classx( pmt, sgn ) of
                zero : keep := (ncs <> -2) and ((i = posperiod) or (i = negperiod)) ;
                denormal, normal : keep := ncs <> -2 ;
                infinite, snan, qnan : begin keep := false ; ncs := -2 ; end ;
                end (* case classx *) ;
                if keep then 
                        begin (* number *)
                        if ncs = -6 then 
                                begin (* first coefficient *)
                                firstc := i ;
                                ncs := 0 ;
                                end   (* first coefficient *) ;
                        with pc^ do
                                begin 
                                index := i ;
                                a := pmt ;
                                end ;
                        pc := pointer(ord4(pc) + crsize) ;
                        lastc := i ;
                        end   (* number *) ;
                end   (* gather coefficients *) ;
        if ncs >= 0 then
                begin (* only numbers *)
                release( pc ) ; (* Release unused heap space. *)
                if (first <= posperiod) and (posperiod <= last) then
                        begin (* discount positive payments *)
                        fin_npv( firstc, lastc, posperiod, posrate, posnpv, pospayment ) ;
                        pc := pointer(coefs) ;
                        repeat
                        with pc^ do 
                                begin (* with pc^ *)
                                curindex := index ;
                                if not signofx( a ) then a := fpk0 ;
                                end   (* with pc^ *) ;
                        pc := pointer( ord4(pc) + crsize ) ;
                        until curindex = lastc ;
                        end   (* discount positive payments *) ;
                if (first <= negperiod) and (negperiod <= last) then
                        begin (* discount negative payments *)
                        negx(fpk0) ;
                        fin_npv( firstc, lastc, negperiod, negrate, 
                                negnpv, negpayment ) ;
                        pc := pointer(coefs) ;
                        repeat
                        with pc^ do 
                                begin (* with pc^ *)
                                curindex := index ;
                                if signofx( a ) then a := fpk0 ;
                                end   (* with pc^ *) ;
                        pc := pointer( ord4(pc) + crsize ) ;
                        until curindex = lastc ;
                        negx(fpk0) ;
                        end   (* discount negative payments *) ;
                pc := pointer(coefs) ;
                repeat
                with pc^ do
                        begin (* with pc^ *)
                        curindex := index ;
                        if curindex = posperiod then addx( posnpv, a) ;
                        if curindex = negperiod then addx( negnpv, a) ;
                        end   (* with pc^ *) ;
                pc := pointer( ord4(pc) + crsize ) ;
                until curindex = lastc ;
                pc := pointer(coefs) ;
                fin_irrreturn( firstc, lastc, ncs, ret, payproc ) ;
                end   (* only numbers *) ;
        end   (* heap ok *) ;
 
if ncs < 0 then errornan( nanirr, ret ) ;
 
release( marker ) ;
procexit( env ) ;
end   (* fin_return *) ;

ÿ