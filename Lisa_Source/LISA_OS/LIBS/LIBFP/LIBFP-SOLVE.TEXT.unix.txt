
 (* File LibFP/Solve, Version 20 January 1984. *)

{$setc solvetest := false}

procedure math_solve (*           { Computes zero of function. }
        est1, est2 : extended ;   { A priori estimates of zero. }
        var result : extended ;   { f(result) may = 0 or nan or its sign may
                                    differ from one of its neighbors or it 
                                    may be a fixed point for the iteration 
                                    or it may merely be
                                    the x with minimal abs(f(x)) among those x
                                    sampled by math_solve.   The user must determine
                                    the significance of this result. }
        procedure       f       ( { User routine to evaluate f(x). }
                    x : extended ;{ Argument. }
                var fx : extended ){ f(x). }
        *) ;
        
label 1, 2, 3, 4 ;

type

farray = record
        x, f : array [1..4] of extended ;
        end ;
var

env : environ ;
p : farray ;
nfe : integer ;
mid, center, left, right, lp, rp : extended ;
didbisect : boolean ;
slope, den, t : extended ;
sgn : integer ;
ip, kp : integer ;
imp, guessok : boolean ;
g, fg : extended ;

function arelx( x, y : extended ) : relop ;
        (* Returns abs(x) rel abs(y). *)
begin (* arelx *)
absx(x) ; absx(y) ;
arelx := relx( x, y ) ;
end   (* arelx *) ;

procedure eval ( x : extended ; var p : extended ) ;
        
        (* Evaluates p := f(x) and goes to 1 if function value is zero or nan. *)

var
sgn : integer ;

begin (* eval *)
clearxcps ; (* Previous don't matter. *)
f( x, p ) ;

nfe := nfe + 1 ;

{$ifc solvetest }
write(' nfe ',nfe) ;
write(' exp ',getexponent(p)) ;
write(' x ') ;
showx(x) ;
{$endc }
 
if classx( p, sgn ) in [ zero, qnan, snan ] then
        begin
        {$ifc solvetest }
        writeln(' FOUND: function zero or nan ') ;
        {$endc }
        result := x ;
        goto 1 ;
        end ;
        
end   (* eval *) ;

function improve2 ( var p : farray ) : boolean ;
        (* 
                Given x (p1) and y (p2), with x <> y and fx <= fy,
           returns a new x, y, and z (p3), such that fx <= fy <= fz and
           one of the three is the secant iterate of the others.
                If a sign change is encountered, go to 2.
                Otherwise iterate until fz is not an exponent improvement
           over fx, then rearrange parameters so that fx <= fy <= fz.
                Improve2 will be TRUE if the final fx is an exponent improvement
           over its original value.
                All comparisons of function value are absolute values.
                                                                *)
const
kmax = 3 ;

var
step, fyfx, z, fz, t : extended ;
slopeneg, timetoquit : boolean ;
i, k : integer ;
xexp : integer ;
zgek : boolean ;
sgn : integer ;

begin (* improve2 *)
with p do 
        begin (* with p *)
        xexp := getexponent( f[1] ) ; (* Exponent of x on entry. *)
        repeat
        step := x[2] ;
        subx( x[1], step ) ; (* y-x *)
        fyfx := f[2] ;
        subx( f[1], fyfx ) ; (* fy-fx *)
        divx( fyfx, step ) ; (* (y-x)/(fy-fx) *)
        if getexponent(step) < 7 then mulx( f[1], step ) else
                begin (* modify secant step *)
                {$ifc solvetest }
                writeln(' SECANT reduce step ') ;
                {$endc }
                slopeneg := signofx( step ) ;
                step := f[1] ;
                scalbx(   7, step ) ;
                if slopeneg then negx(step) ;
                end   (* modify secant step *) ;
        z := x[1] ;
        subx( step, z ) ; (* Subtract secant step. *)
        case classx( z, sgn ) of
        infinite :
                begin (* substitute largest magnitude *)
                {$ifc solvetest }
                writeln(' SECANT substitute maxnorm for infinite z ') ;
                {$endc }
                i2x( 0, t ) ;
                nextx( z, t ) ;
                end   (* substitute largest magnitude *) ;
        qnan, snan :
                begin (* substitute *)
                {$ifc solvetest }
                writeln(' SECANT substitute mean for nan z ') ;
                {$endc }
                z := x[1] ;
                scalbx( -1, z ) ;
                t := x[2] ;
                scalbx( -1, t ) ;
                addx( t, z ) ; (* Substitute mean of x and y. *)
                end   (* substitute *) ;
        end (* case class(z) *) ;
        if relx( x[1], z ) = eq then
                begin (* negligible secant step *)
                {$ifc solvetest }
                writeln(' SECANT substitute mean for negligible step ') ;
                {$endc }
                z := x[1] ;
                scalbx( -1, z ) ;
                t := x[2] ;
                scalbx( -1, t ) ;
                addx( t, z ) ; (* Substitute mean of x and y. *)
                end   (* negligible secant step *) ;
        {$ifc solvetest }
        write(' SECANT  ') ;
        {$endc }
        eval ( z, fz ) ;
        if signofx( f[1] ) <> signofx ( fz ) then
                begin (* sign change *)
                x[2] := z ;
                f[2] := fz ;
                goto 2 ;
                end   (* sign change *) ;
        timetoquit := getexponent( fz ) >= getexponent( f[1] ) ;
        k := kmax ;
        repeat
        k := k - 1 ;
        zgek := arelx( fz, f[k] ) <> lt ;
        until zgek or (k = 1) ;
        if zgek then k := k + 1 ;
        for i := kmax downto k + 1 do
                begin (* move one record *)
                x[i] := x[i-1] ;
                f[i] := f[i-1] ;
                end   (* move one record *) ;
        x[k] := z ;
        f[k] := fz ;
        until timetoquit ;
        improve2 := getexponent( f[1] ) < xexp ;
        end   (* with p *) ;
end   (* improve2 *) ;

function improve3 ( var p : farray ) : boolean ;
        (* 
                Given x (p1), y (p2), and w (p3), all unequal and with
           fx <= fy <= fw,
           returns a new x, y, w, and z (p4), such that fx <= fy <= fw <= fz and
           one of the points is the critical point of the parabola through the
           others.
                If a sign change is encountered, go to 2.
                Otherwise iterate until fz is not an exponent improvement
           over fw, then rearrange parameters so that fx <= fy <= fw <= fz.
                Improve3 will be TRUE if the final fx is an exponent improvement
           over its original value.
                All comparisons of function value are absolute values.
                                                                *)
const
kmax = 4 ;

var
z, fz, t : extended ;
x1, x21, x31, y1, y21, y31 : extended ;
denominator, step : extended ;
timetoquit : boolean ;
k, i : integer ;
xexp : integer ;
zgek : boolean ;
sgn : integer ;

begin (* improve3 *)
with p do 
        begin (* with p *)
        xexp := getexponent( f[1] ) ; (* Exponent of x on entry. *)
        repeat
        x1 := x[1] ;
        x21 := x[2] ;
        x31 := x[3] ;
        subx( x1, x21 ) ; (* x2-x1 *)
        subx( x1, x31 ) ; (* x3-x1 *) 
        y1 := f[1] ;
        y21 := f[2] ; 
        y31 := f[3] ;
        subx( y1, y21 ) ; (* y2-y1 *)
        subx( y1, y31 ) ; (* y3-y1 *)
        mulx( x21, y31 ) ; (* y3-y1 * x2-x1 *)
        mulx( x31, y21 ) ; (* y2-y1 * x3-x1 *)
        denominator := y21 ;
        subx( y31, denominator ) ; (* (y2-y1)*(x3-x1)-(y3-y1)*(x2-x1) *)
        mulx( x21, y31 ) ; (* y3-y1 * (x2-x1)^2 *)
        mulx( x31, y21 ) ; (* y2-y1 * (x3-x1)^2 *)
        step := y21 ;
        subx( y31, step ) ; (* y2-y1 * (x3-x1)^2 - y3-y1 * (x2-x1)^2 *)
        divx( denominator, step ) ;
        scalbx( -1, step ) ; (* x1 + step is critical point of parabola *)
        if (getexponent(step) - getexponent(x31)) >= 7 then
                begin (* modify parabolic step *)
                {$ifc solvetest }
                writeln(' PARABOLA reduce step ') ;
                {$endc }
                scalbx( 6 + getexponent( x31 ) - getexponent( step ), step ) ;
                end   (* modify parabolic step *) ;
        z := x1 ;
        addx( step, z ) ;
                
        case classx( z, sgn ) of
        infinite :
                begin (* substitute largest magnitude *)
                {$ifc solvetest }
                writeln(' PARABOLA substitute maxnorm for infinite z ') ;
                {$endc }
                i2x( 0, t ) ;
                nextx( z, t ) ;
                end   (* substitute largest magnitude *) ;
        qnan, snan :
                begin (* substitute *)
                {$ifc solvetest }
                writeln(' PARABOLA substitute mean for nan z ') ;
                {$endc }
                z := x[1] ;
                scalbx( -1, z ) ;
                t := x[2] ;
                scalbx( -1, t ) ;
                addx( t, z ) ; (* Substitute mean of x and y. *)
                end   (* substitute *) ;
        end (* case class(z) *) ;
        for i := 1 to 3 do if relx( x[i], z ) = eq then timetoquit := true ;
        if not timetoquit then
                begin (* reasonable parabola step *)
                {$ifc solvetest }
                write(' PARABOLA') ;
                {$endc }
                eval ( z, fz ) ;
                if signofx( f[1] ) <> signofx ( fz ) then
                        begin (* sign change *)
                        x[2] := z ;
                        f[2] := fz ;
                        goto 2 ;
                        end   (* sign change *) ;
                timetoquit := getexponent( fz ) >= getexponent( f[1] ) ;
                k := kmax ;
                repeat
                k := k - 1 ;
                zgek := arelx( fz, f[k] ) <> lt ;
                until zgek or (k = 1) ;
                if zgek then k := k + 1 ;
                for i := kmax downto k + 1 do
                        begin (* move one record *)
                        x[i] := x[i-1] ;
                        f[i] := f[i-1] ;
                        end   (* move one record *) ;
                x[k] := z ;
                f[k] := fz ;
                end   (* reasonable parabola step *) ;
        until timetoquit ;
        improve3 := getexponent( f[1] ) < xexp ;
        end   (* with p *) ;
end   (* improve3 *) ;

procedure getcenter ;

        (* Computes center of interval and checks for degeneracy. *)
        
var
nextl, nextr : extended ;

begin
nextl := left ;
nextx( nextl, right ) ;
if relx( nextl, right ) <> lt then
        begin (* left and right equal or nearly *)
                {$ifc solvetest }
        writeln(' FOUND: negligible x interval ') ;
                {$endc }
        if arelx( lp, rp ) = lt then result := left else result := right ;
        goto 1 ;
        end   (* left and right equal or nearly *) ;
nextr := right ;
nextx( nextr, left ) ;
if signofx(nextl) = signofx(nextr) then
        begin (* same signs *)
        scalbx( -1, nextl ) ;
        scalbx( -1, nextr ) ;
        center := nextl ;
        addx( nextr, center ) ;
        end   (* same signs *)
        else
        begin (* signs differ *)
        center := nextl ;
        addx( nextr, center ) ;
        scalbx( -1, center ) ;
        end   (* signs differ *) ;
end ;

procedure checkmid ;
        (* given a function evaluation point mid, checks whether it can
           replace left or right *)

const
deltaexp = 8 ; (* Required decrement of exponent. *)

var 
p : extended ;

begin (* checkmid *)
eval( mid, p ) ;
if signofx(p) = signofx(rp) then
         begin (* new interval [left,mid] *)
                 didbisect := not (relx( mid, center ) in [lt,eq])
                        and ((getexponent(rp)-getexponent(p)) < deltaexp) ;
                 right := mid ;
                 rp := p ;
                 getcenter ;
         end   (* new interval [left,mid] *)
         else
         begin (* new interval [mid,right] *)
                 didbisect := not (relx( center, mid ) in [lt,eq])
                        and ((getexponent(lp)-getexponent(p)) < deltaexp) ;
                 left  := mid ;
                 lp := p ;
                 getcenter ;
         end   (* new interval [mid,right] *) ;

end   (* checkmid *) ;

procedure makeguess ;
        (* Fills in one or two additional guesses as required. *)
begin (* makeguess *)
if classx( est1, sgn ) = zero then
        begin (* one guess zero *)
        i2x( 1, est2  ) ; (* Second guess one. *)
        end   (* one guess zero *)
        else
        begin (* one guess non zero *)
        if signofx( est1 ) then
                begin (* one guess minus *)
                i2x(0, est2 ) ; (* Second guess +0. *)
                end   (* one guess minus *)
                else
                begin (* one guess non minus *)
                i2x(0, est2) ;
                negx(est2) ; (* Second guess -0. *)
                end   (* one guess non minus *) ;
        end   (* one guess non zero *) ;
end   (* makeguess *) ;

begin (* math_solve *)
procentry(env) ;

case relx( est1, est2 ) of

eq:     makeguess ;

unord : if (classx(est1, sgn) in [qnan,snan]) and 
                (classx(est2, sgn) in [qnan, snan]) then
                begin (* two nans *)
                i2x(0,est1) ; i2x(1,est2 ) ;
                end   (* two nans *)
                else
                begin (* one nan *)
                if classx(est1, sgn) in [qnan, snan] then est1 := est2 ;
                makeguess ;
                end   (* one nan *) ;
                 
end (* case *) ;

nfe := 0 ;

with p do 
        begin (* with p *)  
        x[1] := est1 ; x[2] := est2 ;
        {$ifc solvetest }
        write(' EST1    ') ; 
        {$endc }
        eval( x[1], f[1] ) ; 
        {$ifc solvetest }
        write(' EST2    ') ; 
        {$endc }
        eval( x[2], f[2] ) ;
        if signofx( f[1] ) = signofx( f[2] ) then
                begin (* same sign *)
                if arelx( f[1], f[2] ) = gt then
                        begin (* reverse order *)
                        t := x[1] ;
                        x[1] := x[2] ;
                        x[2] := t ;
                        t := f[1] ;
                        f[1] := f[2] ;
                        f[2] := t ;
                        end   (* reverse order *) ;
                imp := improve2( p ) ; 
3 :
                imp := improve3( p ) ;
4 :
                for ip := 2 to 7 do
                        begin (* try secant pairs *)
                        {$ifc solvetest }
                        writeln(' Try secant ',ip) ;
                        {$endc }
                        if ip <= 4 then
                                begin (* take points from last parabola *)
                                g := x[ip] ;
                                fg := f[ip] ;
                                guessok := (relx( x[1], g ) <> eq) 
                                        and (classx( g, sgn ) = normal)
                                        and (classx( fg, sgn ) = normal) ;
                                end   (* take points from last parabola *)
                                else
                                begin (* take gold, silver, bronze bullets *)
                                i2x( 6-ip, g ) ;
                                guessok := (relx( x[1], g ) <> eq) ;
                                end   (* take gold, silver, bronze bullets *) ;
                        if guessok then
                                begin (* try wild guess *)
                                for kp := 11 to 14 do 
                                        begin (* try combinations *)
                                        x[2] := g ;
                                        case kp of
                                        12 :     begin
                                                addx( x[1], x[2] ) ;
                                                scalbx( -1, x[2] ) ;
                                                end ;
                                        13 :     begin
                                                negx( x[2] ) ;
                                                addx( x[1], x[2] ) ;
                                                addx( x[1], x[2] ) ;
                                                end ;
                                        14 :     begin
                                                scalbx( 1, x[2] ) ;
                                                subx( x[1], x[2] ) ;
                                                end ;
                                        end (* case kp *) ;
                                        {$ifc solvetest }
                                        write('GUESSING ') ;
                                        {$endc }
                                        eval( x[2], f[2] ) ;
                                        if arelx( f[1], f[2] ) = gt then
                                                begin (* lucky guess! *)
                                                t := x[1] ; x[1] := x[2] ; x[2] := t ;
                                                t := f[1] ; f[1] := f[2] ; f[2] := t ;
                                                end   (* lucky guess! *) ;
                                        if improve2( p ) then goto 3 ;
                                        if improve3( p ) then goto 4 ;
                                        end   (* try combinations *) ;
                                end   (* try wild guess *) ;
                        end   (* try secant pairs *) ;
                {$ifc solvetest }
                writeln(' FOUND: gave up at apparent minimum ') ;
                {$endc }
                result := x[1] ;
                goto 1 ;
                end   (* same sign *)
                else
                begin (* signs differ *)
2 :
                if relx( x[1], x[2] ) = lt then
                        begin (* normal *)
                        left := x[1] ; lp := f[1] ;
                        right := x[2] ; rp := f[2] ;
                        end   (* normal *)
                        else
                        begin (* reverse *)
                        left := x[2] ; lp := f[2] ;
                        right := x[1] ; rp := f[1] ;
                        end   (* reverse *) ;
                
                getcenter ;
                
                repeat 
                        repeat
                                (* Try secant. *)
                        slope := left ;
                        subx( right, slope ) ;
                        den := lp ;
                        subx( rp, den ) ;
                        divx( den, slope ) ;
                        if getexponent(rp) < getexponent(lp) then
                                begin (* compute from right *)
                {$ifc solvetest }
                                write(' RSECANT ') ;
                        {$endc }
                                mid := right ;
                                mulx( rp, slope ) ;
                                subx( slope, mid ) ;
                                end   (* compute from right *)
                                else
                                begin (* compute from left *)
                {$ifc solvetest }
                                write(' LSECANT ') ;
                        {$endc }
                                mid := left  ;
                                mulx( lp, slope ) ;
                                subx( slope, mid ) ;
                                if relx( mid, left) = eq then nextx( mid, right) ;
                                end   (* compute from left *) ;
                        if relx( mid,right) = eq then nextx( mid, left ) ;
                        if relx( mid, left) = eq then nextx( mid, right) ;
                        if (relx( left, mid )= lt) and
                                (relx( mid, right) = lt) 
                                then checkmid 
                                else 
                                didbisect := true ;
                        until didbisect ;
                                
                                (* Try bisection. *)
                        {$ifc solvetest }
                        write('  BISECT ') ;
                        {$endc }
                        mid := center ;
                        checkmid ;
                until false ;
                end   (* signs differ *) ;
        end   (* with p *) ;
1 : 
procexit(env) ;
end   (* math_solve *) ;

ÿ