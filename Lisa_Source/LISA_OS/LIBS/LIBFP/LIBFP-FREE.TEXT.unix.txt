(* File Libfp/Free, Version 23 May 1984.

Free format binary to Ascii conversion routine. *)

procedure fp_free_ascii ;

var

callround : rounddir ;
e : environ ;
sign : integer ;
class : numclass ;
size : fp_type ;
df : decform ;
found : boolean ;
dr : decimal ;
ms, i, ind, w, ne, ls, lsig, nz, lestring : integer ;
te : array[1..6] of char ;
roundpos, rndexp, fmtexp, initexp : integer ;
siglim, gex, lse : integer ;
godown, noborrow : boolean ;

procedure rounds ;
        (* S := Sig, then s is rounded in decimal at position roundpos.
                If roundpos <= 0, then roundpos := 1.
                If roundpos >= length(s) then s is unchanged.
                If 1 <= roundpos < length(s), then s is rounded to a length
                of roundpos decimal digits, according to the current rounding
                mode in callround.  If s rounds up to 1000..., rndexp is incremented.
        *)
var
i, ls : integer ;
roundup, carry : boolean ;
c : char ;

begin (* rounds *)
s := dr.sig ;
ls := length(s) ;
if roundpos < ls then 
        begin (* do something *)
        if roundpos <= 0 then roundpos := 1 ;
        case callround of
        towardzero : roundup := false ;
        upward :        if sign = 1 then roundup := false else
                        begin (* look for inexact *)
                        i := ls ;
                        while s[i] = '0' do i := i - 1 ;
                        roundup := i > roundpos ;
                        end   (* look for inexact *) ;
        downward :      if sign = 0 then roundup := false else
                        begin (* look for inexact *)
                        i := ls ;
                        while s[i] = '0' do i := i - 1 ;
                        roundup := i > roundpos ;
                        end   (* look for inexact *) ;
        tonearest : 
                begin (* nearest *)
                c := s[roundpos+1] ;
                roundup := c >= '5' ;
                if c = '5' then
                        begin (* round at 5 case *)
                        i := ls ;
                        while s[i] = '0' do i := i - 1 ;
                        if i >= (roundpos+2) 
                                then roundup := true
                                else roundup := odd(ord(s[roundpos])) ;
                        end   (* round at 5 case *) ;
                end   (* nearest *) ;
        end (* case callround *) ;
        if roundup then
                begin (* round magnitude up *)
                setxcp( inexact, true ) ;
                i := roundpos ;
                repeat
                        carry := s[i] = '9' ;
                        if carry then s[i] := '0' else
                                s[i] := succ(s[i]) ;
                        i := i - 1 ;
                until (not carry) or ( i < 1 ) ;
                if carry then 
                        begin (* overflow *)
                        rndexp := rndexp + 1 ;
                        s[1] := '1' ;
                        end   (* overflow *) ;
                end   (* round magnitude up *)
                else
                begin (* test inexact *)
                i := ls ;
                while s[i] = '0' do i := i - 1 ;
                if i > roundpos then setxcp( inexact, true ) ;
                end   (* test inexact *)  ;
        s[0] := chr(roundpos) ;
        end   (* do something *);
end   (* rounds *) ;

procedure formatexponent ;
        (* from exponent fmtexp,
                formats entire exponent field backwards in te with length in ne *)
var
aexp, i : integer ;

begin (* formatexponent *)
if fmtexp = 0 then ne := 0 else
        begin (* add E field *)
                (* Build up exponent field backwards! *)
        aexp := abs(fmtexp) ;
        ne := 1 ;
        while aexp > 0 do
                begin (* add exponent digit *)
                te[ne] := chr(48 + aexp mod 10) ;
                aexp := aexp div 10 ;
                ne := ne + 1 ;
                end   (* add exponent digit *) ;
        if fmtexp < 0 then 
                begin
                te[ne] := '-' ;
                ne := ne + 1 ;
                end 
                else
                if form.plus_eform then 
                begin
                te[ne] := '+' ;
                ne := ne + 1 ;
                end ;
        te[ne] := 'E' ;
        end   (* add E field *) ;
end   (* formatexponent *) ;

begin (* fp_free_ascii *) 
callround := getrnd ;
procentry(e) ;
fp_size( x, sign, class, size ) ;
case class of
zero : if (width <= 1) or (sign = 0) then s := '0' else s := '-0' ;
infinite  : if sign=0 then s := 'Inf' else s := '-Inf' ;

qnan, snan : if width <= 3 then s := 'NaN' else
        begin (* nan *)
        x2str( df, x, s) ;
        while s[1] = ' ' do delete( s, 1, 1 ) ; (* Eliminate leading blank. *)
        if length(s) > width then
                begin (* minimal length *)
                if sign = 0 then s := 'NaN' else s := '-NaN' ;
                end   (* minimal length *) ;
        end   (* nan *) ;
        
denormal, normal : with dr do 
        begin (* normal *)
        if size <= tfp_comp then
                begin (* integral *)
                df.style := fixeddecimal ;
                df.digits :=  0 ;
                x2dec( df, x, dr) ;
                s := sig ;
                ls := length(s) ;
                if form.int_eform then
                        begin (* integral E form *)
                        while s[ls] = '0' do ls := ls - 1 ;
                        nz := length(s) - ls ;
                        if nz >= 3 then
                                begin (* is E form useful *)
                                if form.plus_eform then 
                                        lestring := 3 else lestring := 2 ;
                                if nz >= 10 then lestring := lestring + 1 ;
                                if lestring < nz then
                                        begin (* do E form *)
                                        ls := ls + 1 ;
                                        s[ls] := 'E' ;
                                        if form.plus_eform then 
                                                begin
                                                ls := ls + 1 ;
                                                s[ls] := '+' ;
                                                end ;
                                        if nz >= 10 then
                                                begin
                                                ls := ls + 1 ;
                                                s[ls] := '1' ;
                                                end ;
                                        ls := ls + 1 ;
                                        s[ls] := chr( 48 + nz mod 10 ) ;
                                        s[0] := chr(ls) ;
                                        end   (* do E form *) ;
                                end   (* is E form useful *) ;
                        end   (* integral E form *) ;
                found := (length(s)+sign) <= width ;
                end   (* integral *)
                else
                begin (* not integral *)
                with df do
                        begin 
                        style := floatdecimal ;
                        digits := 19 ;
                        gex := getexponent(x) ;
                        if (gex < 0) or (gex > 62) then 
                                begin (* reduce digits *)
                                siglim := form.maxsig ; 
                                        (* Don't need more than maxsig digits. *)
                                if callround = tonearest 
                                        then siglim := siglim + 1 ; 
                                        (* Specify one extra digit for rounding to
                                           nearest. *)
                                if digits > siglim then digits := siglim ; 
                                        (* when maxsig is small, 
                                        avoid unnecessary digits *)
                                end   (* reduce digits *) ;
                        if digits > width then digits := width ;
                        if digits < 1 then digits := 1 ; (* don't reduce too far *)
                        end ;
                case callround of
                tonearest, towardzero : godown := true ;
                upward : godown := signofx( x ) ;
                downward : godown := not signofx( x ) ;
                end (* case callround *) ;
                if godown then setrnd( towardzero ) else setrnd( callround ) ;
                x2dec( df, x, dr) ;
                if testxcp(inexact) then 
                        begin (* signal inexact *)
                        if not godown then
                                begin (* signal down *)
                                i := length(sig) ;
                                noborrow := false;
                                repeat
                                if sig[i] = '0' then
                                        begin
                                        sig[i] := '9' ;
                                        end
                                        else
                                        begin
                                        sig[i] := pred(sig[i]) ;
                                        noborrow := true ;
                                        end ;
                                i := i - 1 ;
                                until noborrow ;
                                end   (* signal down *) ;
                        lsig := length(sig) + 1 ;
                        sig[0] := chr(lsig) ;
                        sig[lsig] := '!' ;
                        exp := exp - 1 ;
                        end   (* signal inexact *) ;
                found := false ;
                end   (* not integral *) ;
        if not found then
                begin (* not i format *)
                lsig := length(sig) ;
                w := width ; if w > decstrlen then w := decstrlen ;
                w := w - sign - 1 ; 
                        (* w is max number of digits possible *)
                lse := lsig + exp ;
                if ((w+exp) >= -1) or not form.sig_fform then
                if ( lse < w ) or (( lse > 0 ) and ( lse = w )) then
                        begin (* try f *)
                        with form do if exp >= 0 then roundpos := maxsig else
                                begin (* all or part fraction *)
                                ms := maxsig ;
                                if ms > w then ms := w ;
                                if sig_fform 
                                        then roundpos := ms
                                        else roundpos := ms + lse ;
                                end   (* all or part fraction *) ;
                        if roundpos > 19 then roundpos := 19 ;
                        if roundpos >= lsig then 
                                if sig[lsig] = '!' then 
                                roundpos := lsig - 1 ;
                        if roundpos >= 1 then
                                begin (* round to f *)
                                rndexp := exp + lsig ; (* convert to fraction *)
                                rounds ;
                                ls := length(s) ;
                                rndexp := rndexp - ls ; (* convert to integer *)
                                if rndexp > 0 then
                                        begin (* point to right *)
                                        for i := ls+1 to ls+rndexp do s[i] := '0' ;
                                        s[0] := chr(ls+rndexp) ;
                                        insert('.',s,ls+rndexp+1) ;
                                        end   (* point to right *) 
                                        else
                                        if rndexp < (-ls) then
                                        begin (* point to left *)
                                        for i := ls downto 1 do
                                                s[i-ls-rndexp] := s[i] ;
                                        for i := (-ls-rndexp) downto 1 do
                                                s[i] := '0' ;
                                        s[0] := chr(-rndexp) ;
                                        s := concat('.',s) ;
                                        end   (* point to left *)
                                        else
                                        begin (* point in middle *)
                                        insert('.',s,ls+rndexp+1) ;
                                        end   (* point in middle *) ;
                                ls := length(s) ;
                                while s[ls] = '0' do ls := ls - 1 ;
                                if ls = 1 then found := false (* nothing left *) else
                                        begin (* some digits left *)
                                        if not form.trail_point then
                                        if s[ls] = '.' then ls := ls - 1 ;
                                        s[0] := chr(ls) ;
                                        found := length(s) <= (w+1) ;
                                        end   (* some digits left *) ;
                                end ; (* round to f *)
                        end   (* try f *) ;
                end   (* not i format *) ;
        if not found then
                begin (* try e format *)
                roundpos := w ; 
                with form do if roundpos > maxsig then roundpos := maxsig ;
                if roundpos > 19 then roundpos := 19 ;
                if roundpos >= lsig then if sig[lsig] = '!' then 
                        roundpos := lsig - 1 ;
                
                initexp := exp + lsig -1 ;
                rndexp := initexp ;
                
                repeat  
                        (* Roundpos can only decrease; 
                           thus infinite loops are avoided. *)
                fmtexp := rndexp ;
                formatexponent ;
                if roundpos > (w-ne) then roundpos := w - ne ;
                rndexp := initexp ;
                rounds ;
                until fmtexp = rndexp ;
                
                insert( '.', s, 2) ;
                ls := length(s) ;
                while s[ls] = '0' do ls := ls - 1 ; (* Scan trailing zeros. *)
                if not form.trail_point then if s[ls] = '.' then ls := ls - 1 ;
                ind := ls + ne + 1 ;
                for i := ne downto 1 do
                        begin (* add E character *)
                        s[ind-i] := te[i] ;
                        end   (* add E character *) ;
                s[0] := chr(ls+ne) ; (* Adjust length *)
                end   (* try e format *) ;
        if sign = 1 then insert( '-', s, 1) ;
        end   (* normal *) ;

end (* case *) ;
procexit (e) ;
end   (* fp_free_ascii *) ;

ÿ