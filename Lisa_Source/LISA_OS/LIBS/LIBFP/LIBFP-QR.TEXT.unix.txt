(* File LibFp/QR, Version 23 May 1984. *)

{$X- }

procedure fetchqr ( i, j : integer ) ;

var
px : ^ extended ;

begin (* fetchqr *)
px := pointer( qr00+ i * qrifactor + j * qrjfactor ) ;
qrij := px^ ;
end   (* fetchqr *) ;

procedure storeqr ( i, j : integer ; var qrij : extended ) ;

var
px : ^ extended ;

begin (* storeqr *)
px := pointer( qr00+ i * qrifactor + j * qrjfactor ) ;
px^ := qrij ;
end   (* storeqr *) ;

{$X+ }

procedure initqrglobals ( qr : p_qr_record ) ;
        
        (* Sets up qr global variables according to qr, so that qrij is at
                qr00 + i * qrifactor + j * qrjfactor . 
                
                Note that row  0 = current sum of squares of reduced column
                          row -1 = pivot index: column i of the factored
                                        matrix corresponds to column pivot(i)
                                        of the original matrix.
                                        *)

var
sizex, i0 : integer ;
pqr : ptr_qr_record ;

begin (* initqrglobals *)
pqr := pointer ( qr ) ;
sizex := sizeof( extended ) ;
if pqr^.pivoted then i0 := pivotindex else i0 := 1 ;
qr00 := ord(qr) + sizeof(qr_record) - sizex * (1 + i0 * ord4(pqr^.p)) ;
qrifactor := sizex * ord4(pqr^.p) ;
qrjfactor := sizex ;
end   (* initqrglobals *) ;

procedure qr_factor ;

var
sizex : integer ;
i, j, k, minnp : integer ;
diag, sub, newdiag, as, ad, d2, s2, sto : extended ;
c, s : extended ;
env : environ ;
sgn : integer ;
nrows : longint ;
maxcol : integer ;
fpk0, maxnorm, sumsquare : extended ;
inexactflag : boolean ;
upper, newupper, lower, newlower : extended ;
ipivot, normexp : integer ;
pqr : ptr_qr_record ;

begin (* factor *)
procentry(env) ;
sizex := sizeof(extended) ;
nrows := ord4(n) ;
if pivot then nrows := nrows + 2 ;
if (p<1) or (n<1) 
        then qr := ord4(nil) 
        else qr := fp_new( sizex * nrows * p + sizeof( qr_record ) ) ;
if qr <> ord(nil) then
        begin (* ok *)
        pqr := pointer( qr ) ;
        pqr^.n := n ; pqr^.p := p ; pqr^.pivoted := pivot ; 
        pqr^.pdetneg := false ; (* Sign of det(Q). *)
        i2x( 1, pqr^.diagprod   ) ; (* Diagonal product of R. *)
        initqrglobals( qr ) ;
        i2x( 0, fpk0 ) ;
        if n < p then minnp := n else minnp := p ;
        inexactflag := false ;   (* Cumulative inexact flag for zero pivots. *)
        for j := 1 to minnp do
                begin (* for j *)
                if j = 1 then
                        begin (* first column *)
                        if pivot then
                                begin (* initialize for pivoting *)
                                maxnorm := fpk0 ;
                                maxcol := 1 ;
                                for k := 1 to p do 
                                        begin (* copy column k from A to QR *)
                                        sumsquare := fpk0 ;
                                        for i := 1 to n do
                                                begin (* copy row i *)
                                                afetch( i, k, qrij ) ;
                                                storeqr ( i, k, qrij ) ;
                                                mulx(qrij, qrij) ;
                                                addx(qrij, sumsquare) ;
                                                end   (* copy row i *) ;
                                        storeqr( reducednorm, k, sumsquare ) ;
                                        if relx( maxnorm, sumsquare ) = lt then
                                                begin (* note max norm *)
                                                maxnorm := sumsquare ;
                                                maxcol := k ;
                                                end   (* note max norm *) ;
                                        qrij[0] := k ;
                                        qrij[1] := getexponent(sumsquare) ;
                                        storeqr( pivotindex, k, qrij ) ;
                                        end   (* copy column k from A to QR *) ;
                                end   (* initialize for pivoting *)
                                else
                                begin (* initialize without pivoting *)
                                for k := 1 to p do 
                                        begin (* copy column k from A to QR *)
                                        for i := 1 to n do
                                                begin (* copy row i *)
                                                afetch( i, k, qrij ) ;
                                                storeqr ( i, k, qrij ) ;
                                                end   (* copy row i *) ;
                                        end   (* copy column k from A to QR *) ;
                                end   (* initialize without pivoting *) ;
                        end   (* first column *)
                        else
                        begin (* subsequent column *)
                        if pivot then
                                begin (* update reduced norms *)
                                maxnorm := fpk0 ;
                                maxcol := j ;
                                for k := j to p do 
                                        begin (* update column k *)
                                        fetchqr(pivotindex,k) ;
                                        normexp := qrij[1] ; (* Recompute exp. *)
                                        ipivot := qrij[0] ; (* Save pivot info. *)
                                        fetchqr( reducednorm, k ) ;
                                        sumsquare := qrij ;
                                        normexp := getexponent(sumsquare) ;
                                        fetchqr( j-1, k) ;
                                        mulx( qrij, qrij ) ;
                                        subx( qrij, sumsquare) ;
                                        if (normexp - getexponent(sumsquare)) > 32
                                                then
                                                begin (* recompute *)
                                                sumsquare := fpk0 ;
                                                for i := j to n do
                                                        begin
                                                        fetchqr( i, k ) ;
                                                        mulx( qrij, qrij ) ;
                                                        addx( qrij, sumsquare ) ;
                                                        end ;
                                                end   (* recompute *) ;
                                        storeqr( reducednorm, k, sumsquare ) ;
                                        if relx( maxnorm, sumsquare ) = lt then
                                                begin (* note largest norm *)
                                                maxnorm := sumsquare ;
                                                maxcol := k ;
                                                end   (* note largest norm *) ;
                                        sumsquare[0] := ipivot ;
                                        sumsquare[1] := getexponent(sumsquare) ;
                                        end   (* update column k *) ;
                                end   (* update reduced norms *) ;
                        end   (* subsequent column *) ;
                if pivot then
                        begin (* pivot? *)
                        if maxcol <> j then
                                begin (* perform pivot *)
                                for i := pivotindex to n do
                                        begin (* swap *)
                                        fetchqr( i, maxcol ) ;
                                        maxnorm := qrij ;
                                        fetchqr( i, j ) ;
                                        storeqr( i, maxcol, qrij ) ;
                                        storeqr( i, j, maxnorm ) ;
                                        end   (* swap *) ;
                                with pqr^ do pdetneg := not pdetneg ;
                                end   (* perform pivot *) ;
                        end   (* pivot? *) ;
                setxcp ( inexact, inexactflag ) ; (* Restore detection. *)
                fetchqr( j, j ) ;
                diag := qrij ;
                for i := j+1 to n do
                        begin (* zero qr[i,j] *)
                        fetchqr( i, j ) ;
                        sub := qrij ;
                        if classx(sub ,sgn) <> zero then
                                begin (* qrij nonzero *)
                                ad := diag ; absx(ad) ;
                                as := sub   ; absx(as) ;
                                d2 := diag ; mulx( d2, d2 ) ;
                                s2 := sub   ; mulx( s2, s2 ) ;
                                addx ( s2, d2 ) ;
                                sqrtx( d2 ) ; (* d2 = sqrt( diag**2 + sub **2 ) *)
                                newdiag := d2 ;
                                cpysgnx( newdiag, diag ) ;
                                if relx( as, ad ) = gt then 
                                        begin (* small c *)
                                        s := as ;
                                        divx( d2, s ) ; (* s := abs(sub )/sqrt *)
                                        c := diag ;
                                        divx( d2, c ) ;
                                        if not signofx(sub ) then negx(c) ;
                                        if signofx( c  ) then 
                                                begin (* negative cos *)
                                                negx(newdiag ) ;
                                                end   (* negative cos *) ;
                                        sto := c ;
                                        sto[0] := sto[0] + $4000 ;
                                                (* Turn on MSB of exponent. *)
                                        end   (* small c *)
                                        else
                                        begin (* small s *)
                                        c := ad ;
                                        divx( d2, c ) ; (* c := abs(d)/sqrt *)
                                        s := sub  ;
                                        divx( d2, s ) ;
                                        if not signofx(diag) then negx(s) ;
                                        sto := s ; (* Store s. *)
                                        end   (* small s *) ;
                                storeqr( i, j, sto ) ; (* Store s or c. *)
                                for k := j+1 to p do
                                        begin (* rotate rows i and j *)
                                        fetchqr( j, k ) ; (* upper := qr[j,k] *)
                                        upper := qrij ;
                                        fetchqr( i, k  ) ; (* qrij := qr[i,k] *)
                                        lower := qrij ;
                                        newupper := upper ; newlower := lower;
                                        mulx( c, newupper ) ;
                                        mulx( s, newlower  ) ;
                                        subx( newlower, newupper ) ;
                                        storeqr( j, k, newupper ) ;
                                        newupper := upper ; newlower := lower;
                                        mulx( s, newupper ) ;
                                        mulx( c, newlower  ) ;
                                        addx( newupper, newlower ) ;
                                        storeqr( i, k, newlower  ) ;
                                        end   (* rotate rows i and j *) ;
                                diag := newdiag ; (* New diagonal element. *)
                                end   (* qrij nonzero *) ;
                        end   (* zero qr[i,j] *) ;
                if (classx( diag, sgn ) = zero)
                         and testxcp(inexact) then
                        begin (* substitute eps for zero pivot *)
                        if pivot then 
                                begin
                                fetchqr( pivotindex, j) ;
                                k := qrij[0] ; (* Original column number. *)
                                end
                                else
                                k := j ;
                        sumsquare := fpk0 ;
                        for i := 1 to n do
                                begin (* compute original column norm *)
                                afetch( i, k, qrij ) ;
                                mulx( qrij, qrij ) ;
                                addx( qrij, sumsquare ) ;
                                end   (* compute original column norm *);
                        sqrtx(sumsquare) ;
                        scalbx( -64, sumsquare ) ;
                        cpysgnx( sumsquare, diag ) ;
                        storeqr( j, j, sumsquare ) ;
                        end   (* substitute eps for zero pivot *)
                        else
                        begin (* store pivot *)
                        storeqr( j, j, diag ) ; (* New diagonal element. *)
                        with pqr^ do mulx( diag, diagprod       ) ;
                        end   (* store pivot *) ;
                inexactflag := testxcp ( inexact ) ;
                end   (* for i *) ;
        end   (* ok *) ;
procexit(env) ;
end   (* factor *) ;

procedure qr_determinant ;

var
pqr : ptr_qr_record ;

begin (* qr_determinant *)
pqr := pointer( qr ) ;
with pqr^ do if n = p then
        begin (* square *)
        det := diagprod ;
        if pdetneg then negx(det) ;
        end   (* square *)
        else
        begin (* non-square *)
        errornan( nandet, det ) ;
        end   (* non-square *) ;
end   (* qr_determinant *) ;

procedure qr_trandeterminant ;

var
pqr : ptr_qr_record ;

begin (* qr_trandeterminant *)
pqr := pointer( qr ) ;
with pqr^ do if n >= p then
        begin (* square or overdetermined *)
        det := diagprod ;
        mulx( diagprod, det) ;
        end   (* square or overdetermined *) 
        else
        begin (* underdetermined *)
        i2x( 0, det ) ;
        end   (* underdetermined *) ;
end   (* qr_trandeterminant *) ;

procedure qr_condition ;

label 1 ;

var
i, j, k, n, p, minnp : integer ;
env : environ ;
sizex : integer ;
xk : extended ;
fpk1, fpkm1 : extended ;
x : longint ;
fpk0, sum : extended ;
marker : ^ integer ;
sgn : integer ;
maxrowsum, maxcolsum, rowsum, colsum : extended ;
pqr : ptr_qr_record ;


{$X- }

procedure fetchx( i : integer ) ; (* Sets xk := x[i]. *)
var
px: ^ extended ;
begin
px := pointer( x + sizex * ord4(i) ) ;
xk := px^ ;
end ;

procedure storex( i : integer ) ; (* Sets xk =: x[i]. *)
var
px: ^ extended ;
begin
px := pointer( x + sizex * ord4(i) ) ;
px^ := xk ;
end ;

{$X+ }

begin (* qr_condition *)
mark(marker) ;
procentry( env ) ;
pqr := pointer ( qr ) ;
n := pqr^.n ;
p := pqr^.p ;
if n < p then minnp := n else minnp := p ;
 
sizex := sizeof(extended) ;
x := fp_new( sizex * ord4(p)) ;
if x = ord(nil) then errornan( nancond, cond) else
        begin (* found storage *)
        initqrglobals(qr) ;
        i2x( 1, fpk1 ) ;
        i2x(-1, fpkm1 ) ;
        i2x( 0, fpk0 ) ;
        
        maxrowsum := fpk0 ; (* For max row sum of R. *)
        maxcolsum := fpk0 ; (* Fow max col sum of R-1. *)
        for j := 1 to minnp do
                begin (* solve Rx = column j of identity *)
                colsum := fpk0 ;
                for i := j downto 1 do
                        begin (* find xi from Ri*x = 0 *)
                        rowsum := fpk0 ;
                        if i = j then sum := fpkm1 else
                                begin (* accumulate sum *)
                                sum := fpk0 ;
                                for k := i+1 to j do
                                        begin (* accumulate Ri*x *)
                                        fetchqr( i, k ) ;
                                        fetchx( k ) ;
                                        mulx( qrij, xk ) ;
                                        addx( xk, sum ) ;
                                        if j=minnp then
                                                begin
                                                absx(qrij) ;
                                                addx( qrij, rowsum ) ;
                                                end ;
                                        end   (* accumulate Ri*x *) ;
                                end   (* accumulate sum *) ;
                        fetchqr( i, i ) ;
                        if classx( qrij, sgn ) in [zero,infinite,qnan,snan] then 
                                begin
                                cond := fpk0 ;
                                goto 1 ;
                                end ;
                        divx( qrij, sum ) ;
                        negx( sum ) ;
                        xk := sum ;
                        storex( i ) ;
                        absx(xk) ;
                        addx( xk, colsum ) ;
                        if j = minnp then
                                begin
                                absx(qrij) ;
                                addx(qrij, rowsum) ;
                                if relx( rowsum, maxrowsum ) = gt then
                                        maxrowsum := rowsum ;
                                end ;
                        end   (* find xi from Ri*x = 0 *) ;
                if relx( colsum, maxcolsum ) = gt then 
                        maxcolsum := colsum ;
                end   (* solve Rx = column j of identity *) ;
        
        cond:= fpk1 ;
        divx( maxcolsum, cond ) ;
        divx( maxrowsum, cond ) ;
1:
        end   (* found storage *) ;
 
procexit( env ) ;
release(marker) ;
end   (* qr_condition *) ;

procedure qr_solve ;

var
q, x : longint ;
n, p, sizex : integer ;
i, j, k, minip, minnp, maxnp : integer ;
lower, upper, newlower, newupper, c2, s2, xij : extended ;
fpk0, fpk1, c, s : extended ;
xj : extended ;
marker : ^ integer ;
env : environ ;
ncols : longint ;
firstcol, lastcol, kcol, istore : integer ;
sgn : integer ;
pqr : ptr_qr_record ;

{$X- }

procedure fetchx ( i, j : integer ; var xij : extended ) ;

var
pqr : ^ extended ;

begin (* fetchx *)
pqr := pointer ( x + sizex * (i + maxnp * ord4(j))) ;
xij := pqr^ ;
end   (* fetchx *) ;

procedure storex ( i, j : integer ; var xij : extended ) ;

var
pqr : ^ extended ;

begin (* storex *)
pqr := pointer ( x + sizex * (i + maxnp * ord4(j) ) ) ;
pqr^ := xij ;
end   (* storex *) ;

{$X+ }

begin (* solve *)
procentry(env) ;
mark(marker) ;
sizex := sizeof(extended) ;
pqr := pointer(qr) ;
n := pqr^.n ; p := pqr^.p ;
if n < p then
        begin
        maxnp := p ;
        minnp := n ;
        end 
        else
        begin
        maxnp := n ;
        minnp := p ;
        end ;
ncols := (2 * ord4(memavail)) div (sizex  * ord4(maxnp) ) ;
        (* Maximum number of columns that can be processed
           at once with given heap space. *)
error := ncols < 1 ;
if not error then
        begin (* ok *)
        initqrglobals(qr) ;
        if ncols > m then ncols := m ;
        x := fp_new( sizex * ncols * maxnp ) ;
        x := x - sizex * (1 + ord4(maxnp) );
        i2x( 1, fpk1 ) ;
        firstcol := 1 ; 
        repeat
        lastcol := firstcol + ncols - 1 ;
        if lastcol > m then lastcol := m ;
        for k := firstcol to lastcol do
                begin (* copy columns *)
                kcol := k - firstcol + 1 ;
                for i := 1 to n do
                        begin (* copy column k *)
                        bfetch( i, k, xij ) ;
                        storex ( i, kcol, xij ) ;
                        end   (* copy column k *) ;
                end   (* copy columns *);
        for i := 1 to n do
                begin (* scan qr for i *)
                minip := i-1 ;
                if minip > p then minip := p ;
                for j := 1 to minip do
                        begin (* rotate according to qr[i,j] *)
                        fetchqr( i, j) ;
                        s := qrij ;
                        if getexponent(s) > 0 then
                                begin (* was a cosine *)
                                if classx(s,sgn) <> qnan then s[0] := s[0] - $4000 ;
                                c := s ;
                                c2 := c ;
                                mulx( c, c2 ) ; (* c2 := c^2 *)
                                s := fpk1 ;
                                subx( c2, s ) ; (* s := 1 - c^2 *)
                                sqrtx(s) ;      (* s := sqrt(1-c^2) *)
                                end   (* was a cosine *)
                                else
                                begin (* was a sine *)
                                s2 := s ;
                                mulx( s, s2 ) ; (* s2 := s^2 *)
                                c := fpk1 ;
                                subx( s2, c ) ; (* c := 1 - s^2 *)
                                sqrtx(c) ;      (* c := sqrt(1-s^2) *)
                                end   (* was a sine *) ;
                        if classx(s,sgn) <> zero then for k := firstcol to lastcol do
                                begin (* rotate rows i and j *)
                                kcol := k - firstcol + 1 ;
                                fetchx( j, kcol, upper ) ; (* upper := qr[j,k] *)
                                fetchx( i, kcol, lower  ) ; (* lower := qr[i,k] *)
                                newupper := upper ; newlower := lower ;
                                mulx( c, newupper ) ;
                                mulx( s, newlower  ) ;
                                subx( newlower, newupper ) ;
                                storex( j, kcol, newupper ) ;
                                newupper := upper ; newlower := lower ;
                                mulx( s, newupper ) ;
                                mulx( c, newlower  ) ;
                                addx( newupper, newlower ) ;
                                storex( i, kcol, newlower  ) ;
                                end   (* rotate rows i and j *) ;
                        end   (* rotate according to qr[i,j] *) ;
                end   (* scan qr for i *) ;
        
        for k := firstcol to lastcol do
                begin (* back solve column k *)
                kcol := k - firstcol + 1 ;
                if n < p then
                        begin (* n < p *)
                        i2x(0 , fpk0) ;
                        for i := p downto n+1 do
                                begin (* store zero in extra x rows *)
                                if pqr^.pivoted then
                                        begin
                                        fetchqr( pivotindex, i ) ;
                                        istore := qrij[0] ; 
                                                (* Correct column to store this result. *)
                                        end
                                        else
                                        istore := i ;
                                xstore( istore, k, fpk0) ; (* x[i,k] := 0 *)
                                end   (* store zero in extra x rows *) ;
                        end   (* n < p *);
                for i := minnp downto 1 do
                        begin (* back solve and store result  *)
                        fetchx( i, kcol, xij ) ;
                        for j := i+1 to minnp do
                                begin (* subtract R*B *)
                                fetchx( j, kcol, xj ) ;
                                fetchqr( i, j ) ;
                                if (classx( xj, sgn ) <> zero )
                                   and (classx( qrij, sgn ) <> zero ) then
                                        begin (* accumulate product *)
                                        mulx( qrij, xj ) ;
                                        subx( xj, xij ) ;
                                        end   (* accumulate product *) ;
                                end   (* subtract R*B *) ;
                        if classx( xij,sgn ) <> zero then
                                begin (* divide by pivot *)
                                fetchqr( i, i ) ;
                                if classx( qrij, sgn ) = zero 
                                        then i2x( 0, xij ) (* don't divide by zero *)
                                        else divx( qrij, xij ) ;
                                end   (* divide by pivot *) ;
                        storex( i, kcol, xij ) ;
                        if pqr^.pivoted then
                                begin
                                fetchqr( pivotindex, i ) ;
                                istore := qrij[0] ; 
                                        (* Correct column to store this result. *)
                                end
                                else
                                istore := i ;
                        xstore( istore, k, xij ) ;
                        end   (* back solve and store result  *) ;
                end   (* back solve column k *) ;
                
        firstcol := lastcol + 1 ;
        until firstcol > m ;
        end   (* ok *) ;
release(marker) ;
procexit(env) ;
end   (* solve *) ;

procedure qr_residual;

var
i, j, k : integer ;
env : environ ;
resid, aij, xjk, ax : extended ;

begin (* residual *)
procentry(env) ;
for k := 1        to m       do
        begin (* column k of b-ax *)
        for i := 1 to n do
                begin (* compute b-a*x *)
                bfetch( i, k,   resid  ) ;
                for j := 1 to p do
                        begin (* subtract Aij*Xjk *)
                        afetch( i, j, aij ) ;
                        xfetch( j, k, xjk ) ;
                        mulx( aij, xjk ) ;
                        subx( xjk, resid ) ;
                        end   (* subtract Aij*Xjk *) ;
                rstore( i, k,   resid  ) ;
                end   (* compute b-a*x *) ;
        end   (* column k of b-ax *);
procexit(env) ;
end   (* residual *) ;

procedure qr_improve ;

var
r : longint ;
n, p, sizex : integer ;
i, j, k, maxnp : integer ;
marker : ^ integer ;
env : environ ;
ncols : longint ;
firstcol, lastcol, kcol : integer ;
b, ax : extended ;
xik, dx : extended ;
pqr : ptr_qr_record ;

{$X- }

procedure fetchb ( i, j : integer ; var xij : extended ) ;

begin (* fetchb *)
bfetch( i, firstcol+j-1, xij ) ;
end   (* fetchb *) ;

procedure fetchx ( i, j : integer ; var xij : extended ) ;

begin (* fetchx *)
xfetch( i, firstcol+j-1, xij ) ;
end   (* fetchx *) ;

procedure fetchr ( i, j : integer ; var xij : extended ) ;

var
pqr : ^ extended ;

begin (* fetchr *)
pqr := pointer ( r + sizex * (i + maxnp * ord4(j))) ;
xij := pqr^ ;
end   (* fetchr *) ;

procedure storer ( i, j : integer ; xij : extended ) ;

var
pqr : ^ extended ;

begin (* storer *)
pqr := pointer ( r + sizex * (i + maxnp * ord4(j) ) ) ;
pqr^ := xij ;
end   (* storer *) ;

{$X+ }

begin (* improve *)
procentry(env) ;
mark(marker) ;
sizex := sizeof(extended) ;
pqr := pointer(qr) ;
n := pqr^.n ; p := pqr^.p ;
if n < p then
        begin
        maxnp := p ;
        end 
        else
        begin
        maxnp := n ;
        end ;
ncols := ord4(memavail) div (sizex  * ord4(maxnp) ) ;
        (* Maximum number of columns that can be processed
           at once using only half of available heap space. *)
error := ncols < 1 ;
if not error then
        begin (* ok *)
        if ncols > m then ncols := m ;
        r := fp_new( sizex * ncols * maxnp ) ;
        r := r - sizex * (1 + ord4(maxnp) );
        firstcol := 1 ; 
        repeat
        lastcol := firstcol + ncols - 1 ;
        if lastcol > m then lastcol := m ;
        qr_residual ( n, p, lastcol - firstcol + 1, afetch, fetchb, fetchx, storer ) ;
        qr_solve( lastcol - firstcol + 1, qr, error, fetchr, storer ) ;
        for k := firstcol to lastcol do
                begin (* column k of improved x *)
                kcol := k - firstcol + 1 ;
                for i := 1 to p do
                        begin (* update xik *)
                        xfetch( i, k, xik ) ;
                        fetchr( i, kcol, dx ) ;
                        addx( dx, xik) ;
                        xstore( i, k, xik);
                        end   (* update xik *) ;
                end   (* column k of improved x *);
        firstcol := lastcol + 1 ;
        until firstcol > m ;
        end   (* ok *) ;
release(marker) ;
procexit(env) ;
end   (* improve *) ;

procedure qr_transolve ;

var
q, x : longint ;
n, p, sizex : integer ;
i, j, k, minip, minnp, maxnp : integer ;
xij : extended ;
fpk0, fpk1 : extended ;
xj : extended ;
marker : ^ integer ;
env : environ ;
ncols : longint ;
istore : integer ;
sgn : integer ;
pqr : ptr_qr_record ;

{$X- }

procedure fetchx ( i : integer ; var xij : extended ) ;

var
pqr : ^ extended ;

begin (* fetchx *)
pqr := pointer ( x + sizex * ord4(i)) ;
xij := pqr^ ;
end   (* fetchx *) ;

procedure storex ( i : integer ; var xij : extended ) ;

var
pqr : ^ extended ;

begin (* storex *)
pqr := pointer ( x + sizex * ord4(i) ) ;
pqr^ := xij ;
end   (* storex *) ;

{$X+ }

begin (* transolve *)
procentry(env) ;
mark(marker) ;
sizex := sizeof(extended) ;
pqr := pointer(qr) ;
n := pqr^.n ; p := pqr^.p ;
if n < p then
        begin
        maxnp := p ;
        minnp := n ;
        end 
        else
        begin
        maxnp := n ;
        minnp := p ;
        end ;
ncols := (2 * ord4(memavail)) div (sizex  * ord4(minnp) ) ;
        (* Maximum number of columns that can be processed
           at once with given heap space. *)
error := ncols < 1 ;
if not error then
        begin (* ok *)
        initqrglobals(qr) ;
        x := fp_new( sizex * minnp ) ;
        x := x - sizex;
        i2x( 1, fpk1 ) ;
        for k := 1 to m do
                begin (* back solve column k *)
                if pqr^.pivoted then
                        for i := 1 to p do
                                begin (* copy column k *)
                                j := i ;
                                fetchqr( pivotindex, j) ;
                                if i <> qrij[0] then
                                        begin (* find original position *)
                                        j := 0 ;
                                        repeat
                                        j := j + 1 ;
                                        fetchqr( pivotindex, j) ;
                                        until i = qrij[0] ;
                                        end   (* find original position *) ;
                                if j <= minnp then 
                                        begin
                                        bfetch( i, k, xij ) ;
                                        storex ( j, xij ) ;
                                        end ;
                                end   (* copy column k *)  
                        else
                        for i := 1 to minnp do
                                begin (* copy column k *)
                                bfetch( i, k, xij ) ;
                                storex ( i, xij ) ;
                                end   (* copy column k *) ;
                for i := 1 to minnp     do
                        begin (* back solve R*z=x  *)
                        fetchx( i, xij ) ;
                        for j := 1 to i-1     do
                                begin (* subtract R*B *)
                                fetchx( j, xj ) ;
                                fetchqr( j, i ) ;
                                mulx( qrij, xj ) ;
                                subx( xj, xij ) ;
                                end   (* subtract R*B *) ;
                        if classx( xij,sgn ) <> zero then
                                begin (* divide by pivot *)
                                fetchqr( i, i ) ;
                                divx( qrij, xij ) ;
                                end   (* divide by pivot *) ;
                        storex( i, xij ) ;
                        end   (* back solve R*z=x  *) ;
                if n < p then
                        begin (* n < p *)
                        i2x(0 , fpk0) ;
                        for i := p downto n+1 do
                                begin (* store zero in extra x rows *)
                                if pqr^.pivoted then
                                        begin
                                        fetchqr( pivotindex, i ) ;
                                        istore := qrij[0] ; 
                                        (* Correct column to store this result. *)
                                        end
                                        else
                                        istore := i ;
                                xstore( istore, k, fpk0) ; (* x[i,k] := 0 *)
                                end   (* store zero in extra x rows *) ;
                        end   (* n < p *);
                for i := minnp downto 1 do
                        begin (* back solve and store result  *)
                        fetchx( i, xij ) ;
                        for j := i+1 to minnp do
                                begin (* subtract R*B *)
                                fetchx( j, xj ) ;
                                fetchqr( i, j ) ;
                                mulx( qrij, xj ) ;
                                subx( xj, xij ) ;
                                end   (* subtract R*B *) ;
                        if classx( xij,sgn ) <> zero then
                                begin (* divide by pivot *)
                                fetchqr( i, i ) ;
                                divx( qrij, xij ) ;
                                end   (* divide by pivot *) ;
                        storex( i, xij ) ;
                        if pqr^.pivoted then
                                begin
                                fetchqr( pivotindex, i ) ;
                                istore := qrij[0] ; 
                                        (* Correct row to store this result. *)
                                end
                                else
                                istore := i ;
                        xstore( istore, k, xij ) ;
                        end   (* back solve and store result  *) ;
                end   (* back solve column k *) ;
        end   (* ok *) ;
release(marker) ;
procexit(env) ;
end   (* transolve *) ;


ÿ