;*****
;*****       CONSIDER THE EFFECT OF ROUNDING IN 'RM' MODE HERE.
;*****
;*****       SINCE THE NEXT THING TO BE DONE TO APPROX_LOG10('X') WILL BE
;*****       TO 'FFIX' IT TO A WORD LENGTH INTEGER, THE IDEAL WOULD BE TO
;*****       HAVE A 'FMULT_FINT_ROUND' ROUTINE WHICH KEPT UP WITH THE G-R-S
;*****       BITS FROM THE MULTIPLY UNTIL THE ROUND FOLLOWING THE INTEGER
;*****       PART.
;*****
;*****       DOES ROUNDING AFTER THE 'FMULT' AND AGAIN AFTER THE 'FINT'
;*****       EVER PRODUCE AN ERROR IN THE WORD LENGTH INTEGER?
;*****
;*****          SINCE THE MAXIMUM EXPONENT IN APPROX_LOG10('X') IS 12,
;*****          THE G-R-S BITS PRODUCED IN THE MULTIPLY WOULD EFFECT
;*****          ONLY THE STIKY BIT IN THE INTEGER PART'S ROUNDING IF WERE
;*****          THE MULTIPLY'S G-R-S BITS CARRIED OVER.
;*****
;*****          IF THE VALUE FOR APPROX_LOG10('X') IS POSITIVE, 'RM' IS
;*****          SIMPLY TRUNCATION; THUS THE ROUNDING AFTER THE MULTIPLY
;*****          AND AGAIN IN THE INTEGER PART PRODUCES THE SAME ANSWER
;*****          AS THE 'IDEAL' ROUTINE'S SINGLE ROUND.
;*****
;*****          IF THE VALUE FOR APPROX_LOG10('X') IS NEGATIVE, 'RM' ADDS
;*****          '1' TO THE LS_MANTISSA BIT IFF ANY G-R-S BIT IS SET.
;*****          IT CAN BE SHOWN (ALTHOUGH IT IS NOT OBVIOUS) THAT ONCE
;*****          AGAIN THE ROUNDING AFTER THE MULTIPLY AND AGAIN IN THE
;*****          INTEGER PART PRODUCES THE SAME ANSWER AS THE 'IDEAL'
;*****          ROUTINE'S SINGLE ROUND.
;*****
;*****       NOW THAT WE KNOW THAT THE TWO ROUNDING OPERATIONS INSTEAD OF
;*****       THE 'IDEAL' SINGLE ROUNDING DOES NOT INTRODUCE ANY ERROR, DO
;*****       WE EVEN NEED TO BOTHER WITH ROUNDING AFTER THE MULTIPLY?
;*****
;*****          WHERE APPROX_LOG10('X') IS POSITIVE, NO, THE ROUNDING AFTER
;*****          THE MULTIPLY IS EXTRANEOUS SINCE 'RM' IS SIMPLY TRUNCATION.
;*****
;*****          WHERE APPROX_LOG10('X') IS NEGATIVE, THE G-R-S BITS FROM
;*****          THE MULTIPLY ARE SIGNIFICANT.  HOWEVER,THE EXPENSE OF A
;*****          COMPLETE ROUNDING OPERATION IS UNNECESSAY.  THE FOLLOWING
;*****          PROCEDURE PRODUCES THE SAME RESULT AT A MUCH CHEAPER PRICE
;*****          THAN A 'BSR  ROUND'.
;*****
;*****              CALCULATE AN UNROUNDED APPROX_LOG10('X') USING THE
;*****              'mc%mul_opt' SUBROUTINE (THIS ROUTINE DOES NOT LOOK AT
;*****              THE ROUNDING MODE - IT RETURNS AN UNROUNDED RESULT
;*****              IN REGS. D0-D2 WITH THE G-R-S BITS IN REG D3).
;*****
;*****              TST.L   D3             TEST ALL G-R-S BITS FROM THE MULT
;*****
;*****              IF  <NE>  THEN
;*****
;*****                 ORI.B    #1,D2          'OR' THE G-R-S BITS FROM THE
;*****                                         MULT INTO APPROX_LOG10('X')
;*****              END                        LS_BIT.
;*****
;*****              PROCEED WITH THE INTEGER PART PROCESS
;*****
;*****          IF 'mc%mul_opt' HAD INCLUDED AN IMPLICIT ROUNDING OPERATION, THE
;*****          SAME EFFECT COULD HAVE BEEN PRODUCED BY CALLING 'FMULT' UNDER
;*****          'RZ' MODE (THE QUICKEST ROUNDING MODE) AND 'OR'ING THE
;*****          INEXACT FLAG INTO APPROX_LOG10('X') LS_BIT BEFORE TAKING THE
;*****          INTEGER PART.  THIS WOULD BE SLOWER, HOWEVER.
;*****
;*****          THIS SAME TECHNIQUE IS USED BELOW AND HAS ADDITIONAL COMMENTS
;*****          JUST ABOVE THE LABEL 'INTPART_Y'.
;*****
;*****
;*****       FINALLY, WHAT IS IN THE TAG FIELD OF THE APPROX_LOG10('X')
;*****       PRODUCT?  'mc%mul_opt' CALCS THE SIGN & EXPONENT OF THE PRODUCT
;*****       SIMULTANEOUSLY BY ADDING THE TWO OPERANDS' SIGN-TAG-EXP LONG
;*****       WORDS TOGETHER.  NOTE THAT THIS ALSO ADDS THE TAG FIELDS
;*****       TOGETHER WITH ANY CARRY OUT OF THE EXPONENT FIELD.
;*****
;*****       SINCE BOTH OPERANDS HAD CLEAR TAG FIELDS WHEN 'mc%mul_opt' WAS CALLE
;*****       THE WORST THAT COULD HAVE HAPPENED TO THE TAG FIELD OF THE
;*****       PRODUCT IS THAT THE 'NORM' TAG WAS SET BY THE CARRY OUT
;*****       OF THE EXPONENT FIELD.  THIS WILL SAVE HAVING TO 'TYPE' THE
;*****       INT[APPROX_LOG10('X')] LATER;  SEE THE COMMENTS FOLLOWING THE
;*****       'BSR mc%int_a' BELOW.
;*****
;*****       THE DON'T CARE BITS ARE STILL ALL ZERO.
;*****
;*****
;******************************************************************************
;
;
;            NOW HAVE THE UNROUNDED APPROX_LOG10('X') IN REGS. D0-D2 AND
;            THE G-R-S BITS FROM THE MULTIPLY IN REG D3.  LOGICALLY 'OR' THE
;            G-R-S BITS INTO APPROX_LOG10('X') LS_BIT.
;
;
              TST.L    D3
              beq      @12
                 ORI.B    #1,D2
@12
;
;
;            TAKE THE INTEGER PART OF THE 'KLUG' ROUNDED PRODUCT AND FFIX
;            IT INTO A WORD LENGTH INTEGER IN REG D1.W.
;
;
;******************************************************************************
;*****
;*****
;*****       ORIGINALLY CALLED 'FIX_D' HERE.  THE FOLLOWING COMMENTS APPLY
;*****       TO THIS ORIGINAL CALL BUT ARE LEFT IN TO CLARIFY THE
;*****       OPTIMIZATION WHICH IS IMPLEMENTED.
;*****
;*****       SINCE THE 'KLUG' ROUNDED PRODUCT CAN ONLY BE OF TYPE NORM OR
;*****       NON-ZERO-UNNORM, IT NEED NOT BE TAGGED BEFORE CALLING THE
;*****       FIX SUBROUTINE.  THE 'FIX_D' ENTRY POINT WILL ACCEPT INPUT
;*****       WHICH IS EITHER NORM OR NON-ZERO-UNNORM IN TYPE (THIS IS NOT
;*****       MENTIONED IN THE FIX SUBROUTINE COMMENTS BUT INSPECTION SHOWS
;*****       THAT THE ONLY DIFFERENCE IN THE 'FIX_D' ENTRY POINT AND THE
;*****       'FIX_E' ENTRY POINT IS THE VERSION OF 'FINT' USED.  SINCE
;*****       'mc%int_a' ALLOWS INPUT OF TYPE NORM OR NON-ZERO-UNNORM AND
;*****       ALWAYS RETURNS A TAGGED TRUE ZERO INTEGER PART IF SUCH IS FOUND,
;*****       THE 'FFIX_D' ENTRY POINT WILL ACCEPT THE SAME INPUT TYPES).
;*****       THIS SAVES THE EXPENSE OF TAGGING THE 'KLUG' ROUNDED PRODUCT AND
;*****       SELECTING THE APPROPRIATE 'FFIX' ENTRY POINT.
;*****
;*****       FFIX_D EXPECTS THE FP-INPUT TO BE IN REGS D0-D2 AND
;*****       A PRECISION INDEX (BYTE, WORD, OR LONG) IN REG D7.
;*****
;*****       SINCE THE INTEGER TO BE RETURNED WILL ALWAYS BE IN THE RANGE,
;*****       -5000 < ILOG < 5000  (APPROX), A WORD LENGTH INTEGER IS MORE
;*****       THAN SUFFICIENT.
;*****
;*****
;*****       %%% SPEEDUP  - SINCE WE KNOW THAT THE FIXED RESULT WILL FIT
;*****           DONE       IN A WORD LENGTH INTEGER, THE TEST FOR NO-FIT
;*****           HERE       IN 'FIX_D' IS EXTRANEOUS. DUPLICATE THE
;*****                      'FIX_D' CODE HERE WITHOUT THIS TEST.  THIS
;*****                      SAVES ONE BSR, ONE RTS, THE TEST BRANCH,
;*****                      AND THE NEED TO LOAD REG D7 WITH THE PRECISION
;*****                      INDEX. THIS COMES TO 50 CYCLES SAVINGS AT A
;*****                      COST OF ADDING 20 BYTES OF CODE.
;*****
;*****
;******************************************************************************
;
;
;            TAKE THE INTEGER PART OF THE 'KLUG' ROUNDED PRODUCT AND FFIX
;            IT INTO A WORD LENGTH INTEGER IN REG D1.W. NOTE THAT mc%int_a
;            TRASHES REGS. D4-D6 BUT NO SAVE IS NECESSARY.  ALSO, NOTE THAT
;            mc%int_a ROUNDS WHICH COULD SET THE INX BIT IN TEMPSTAT.
;
;
              jSR      mc%int_a                      ; TAKE INT[APPROX_LOG10('X')
;                                                    ROUNDS UNDER CURRENT
;                                                    MODE WHICH IS 'RM'
;                                                    RETURNS RESULT IN D0-D2
;
              BCLR     #ENINEX_M,TEMPSTAT(A3)      ; CLEAR INX BIT WHICH COULD
;                                                    HAVE BEEN SET BY mc%int_a.
;
;            NOTE THAT 'mc%int_a' ALWAYS TAGS TRUE ZERO AS SUCH AND EITHER
;            LEAVES THE TAG FIELD ALONE OR SETS THE NORM TAG FOR ANY OTHER
;            NORM OR NON-ZERO-UNNORM INPUT.  SINCE APPROX_LOG10('X') HAD
;            EITHER A CLEAR TAG FIELD OR A NORM TAG SET WHEN 'mc%int_a' WAS
;            CALLED, THE FOLLOWING TEST FOR A ZERO TAG CANNOT BE ACCIDENTLY
;            FOULED UP.
;
              BTST     #ZERO_R,D0
              beq      @13                        ; IF {INT[APPROX_LOG10('X')]
;                                                                 = 0 } THEN
                 CLR.W    D1                          ; 'ILOG' = 0
;
              bra      @14                        ; ELSE {INT[APPROX_
;                                                          LOG10('X')] <> 0}
@13
                 moveq    #31,D6                      ; CALCULATE SHIFT COUNT
                 SUB.W    D0,D6
                 LSR.L    D6,D1                       ; RIGHT JUSTIFY RESULT
                 TST.L    D0
                 bpl      @14                      ; IF [APPROX_LOG10('X')
;                                                             WAS NEGATIVE]
                    NEG.W    D1                          ; MAKE 'ILOG' NEGATIVE
;
@14                                                ; END
;
              MOVEM.L  (A7)+,A1/D7                 ; RESTORE REGS A1 AND D7
;
;                                (APPROX_LOG2('X') IS NORM OR UNNORM-NON-ZERO)
;
;                                ('X' IS NORM OR UNNORM-NON-ZERO)
;
mv_ilog
        MOVE.W   D1,D4                       ; MOVE 'ILOG' TO REGISTER D4.W
;
;
;**    *****************************************************************
;**
;**
;**       WHERE ARE THE VARIABLES.
;**
;**
;**       REGISTER D4.W          'ILOG'  (WORD)
;**
;**       REGISTER D7.W          'K'  (WORD)
;**
;**
;** A7 -> SAVED ON STACK         (INPUT VALUE)  (3 LONG WORDS)
;**
;**       SAVED ON STACK         ORIGINAL CONTROL REGISTER  (WORD)
;**
;**
;******************************************************************************
;*****
;*****
;*****       NOW HAVE A TWO'S COMPLEMENT INTEGER IN REG D4.W, ILOG, WHICH IS
;*****       THE INTEGER PART OF LOG10('X') ROUNDED IN RM.
;*****
;*****       NOTE THAT (-32) <= 'K' <= 18 BUT THAT 'K' <> 0.
;*****
;*****       CALCULATE AN INTEGER, 'LEN', REPRESENTING THE DESIRED NUMBER OF
;*****       SIGNIFICANT DECIMAL MANTISSA DIGITS IN THE OUTPUT STRING.
;*****
;*****       OBVIOUSLY FOR 'K' POSITIVE 'LEN' = 'K'.
;*****
;*****       FOR 'K' NEGATIVE 'LEN' = MAX{1,MIN{18,'ILOG'+1-'K'}}.
;*****
;*****       TO EXPLAIN THIS MESS, CONSIDER FIRST ONLY THOSE INPUT VALUES WITH
;*****       MAGNITUDE >= 1.  THE TERM, ('ILOG'+1), THEN REPRESENTS THE NUMBER
;*****       OF SIGNIFICANT DECIMAL MANTISSA DIGITS TO THE LEFT OF THE DECIMAL
;*****       POINT IN A FIXED POINT REPRESENTATION [REMEMBER THAT 'ILOG' IS
;*****       THE FLOOR OF THE LOG10 ('X')]; SINCE 'K' NEGATIVE REPRESENTS THE
;*****       NUMBER OF SIGNIFICANT DECIMAL MANTISSA DIGITS TO THE RIGHT OF
;*****       THE DECIMAL POINT IN A FIXED POINT REPRESENTATION, THE TOTAL
;*****       NUMBER OF SIGNIFICANT DECIMAL MANTISSA DIGITS IN THE DESIRED
;*****       FIXED POINT REPRESENTATION IS [('ILOG'+1) - 'K'].
;*****
;*****       THE 'MIN' FUNCTION IS NECESSARY ONLY BECAUSE THE MAXIMUM NUMBER
;*****       OF SIGNIFICANT DECIMAL MANTISSA DIGITS IS RESTRICTED TO 18; FOR
;*****       LARGE INPUT VALUES 'ILOG' WILL BE VERY LARGE AND THE BCD STRING
;*****       WHICH IS RETURNED WILL AT MOST HAVE 18 SIGNIFICANT DIGITS IN
;*****       THE MANTISSA.
;*****          IT IS LEFT TO THE USER TO CHECK THE RETURNED BCD STRING
;*****          TO SEE IF ITS FIXED POINT REPRESENTATION WILL FIT IN THE
;*****          USER'S FIELD WIDTH; THE USER CAN MAKE THIS CHECK USING THE
;*****          RETURNED BCD EXPONENT, THE RETURNED VALUE OF 'P', AND
;*****          THE INPUT VALUE OF 'K' NEGATIVE.  ONCE THE USER DETERMINES
;*****          THAT THE FIXED POINT REPRESENTATION OF THE RETURNED BCD
;*****          STRING WILL NOT FIT IN THE DESIRED FIELD WIDTH, HE/SHE CAN
;*****          (1) VIOLATE THE FIELD WIDTH; APPEND SUFFICIENT ZEROS TO
;*****          THE RETURNED BCD MANTISSA TO PRODUCE A FIXED POINT REPRESEN-
;*****          TATION - NOTE THAT THIS IMPARTS FALSE SIGNIFICANCE.
;*****          (2) PRODUCE A USER DEFINED 'FIELD WIDTH OVERFLOW RESULT'
;*****          (3) DEFAULT TO A FLOATING POINT REPRESENTATION ('E' FORMAT)
;*****          WHICH WILL FIT IN THE DESIRED FIELD WIDTH.
;*****
;*****       RETURNING TO THE EXPLANATION OF THE 'LEN' EQUATION CONSIDER
;*****       THOSE INPUT VALUES WITH MAGNITUDE < 1; THE DESIRED FIXED POINT
;*****       REPRESENTATION WOULD NOW BE OF THE FORM 0.X....
;*****       SINCE 'K' NEGATIVE REPRESENTS ONLY THE NUMBER OF SIGNIFICANT
;*****       DECIMAL MANTISSA DIGITS TO THE RIGHT OF THE DECIMAL POINT, THE
;*****       'MAX' FUNCTION IS USED TO PROVIDE FOR THE SINGLE '0' DIGIT TO THE
;*****       LEFT OF THE DECIMAL POINT.
;*****          SHOULD THE FIXED POINT REPRESENTATION OF THE RETURNED BCD
;*****          STRING NOT FIT IN THE DESIRED FIELD WIDTH, THE USER CAN
;*****          CHOOSE ONE OF THE OPTIONS DESCRIBED ABOVE.
;*****
;*****
;******************************************************************************
;
;
;      INITIALIZE A WORD LENGTH COUNTER, 'RECALC_CTR', TO ZERO ON THE STACK.
;      THIS COUNTER IS USED TO LIMIT THE NUMBER OF RE-CALCULATIONS TO ONE.
;
;
        CLR.W    -(A7)                       ; SET 'RECALC_CTR' = 0
;
;
;      REG D4.W CONTAINS THE TWO'S COMPLEMENT VALUE FOR 'ILOG'.
;      REG D7.W CONTAINS THE TWO'S COMPLEMENT VALUE FOR 'K'.
;      CALCULATE THE VALUE OF THE INTEGER, 'LEN'.
;
;
LEN_CALC   EQU   *
;
        TST.W    D7
        bpl      @17                        ; IF ('K' NEGATIVE) THEN
           moveq    #1,D6
           ADD.W    D4,D6                       ; 'LEN' = ('ILOG'+1) - 'K'
           SUB.W    D7,D6
           bgt      @15                         ; IF ('LEN' <= 0) THEN
              moveq    #1,D6                       ; SET 'LEN' := 1
           bra      @16                         ; ELSE
@15
              cmp.w       #maxek,d6             ; IF ('LEN' > 18) THEN
              ble         @16
                 moveq    #maxek,D6                      ; SET 'LEN' := 18
@16                                          ; END
        bra      @18                         ; ELSE ('K' POSITIVE)
@17
        MOVE.W   D7,D6                       ; 'LEN' = 'K'
@18                                          ; END
;
;
;**    *****************************************************************
;**
;**
;**       WHERE ARE THE VARIABLES.
;**
;**
;**       REGISTER D4.W          'ILOG'  (WORD)
;**
;**       REGISTER D6.W          'LEN'  (WORD)
;**
;**       REGISTER D7.W          'K'  (WORD)
;**
;**
;** A7 -> SAVED ON STACK         'RECALC_CTR'  (WORD)
;**
;**       SAVED ON STACK         (INPUT VALUE)  (3 LONG WORDS)
;**
;**       SAVED ON STACK         ORIGINAL CONTROL REGISTER  (WORD)
;**
;**
;******************************************************************************
;*****
;*****
;*****       CALCULATE AN INTEGER, 'ISCALE', WHICH REPRESENTS THE POWER OF TEN
;*****       NEEDED TO CALCULATE THE SCALE FACTOR, 'SCALE'.  THE FOLLOWING
;*****       EXAMPLES ILLUSTRATE THE CONCEPT OF SCALE FACTOR.
;*****
;*****       EXAMPLE (1):  IF THE INPUT VALUE WERE DECIMAL 1567.0 AND 'K' = 3,
;*****       THE DESIRED BCD OUTPUT STRING IS +1.57E+3 (IN RN MODE) WHERE THE
;*****       MANTISSA DECIMAL POINT IS IMPLICIT TO THIS ALGORITHM.
;*****
;*****           INPUT VALUE = DECIMAL 1567.0
;*****                   'K' = 3
;*****                'ILOG' = 3
;*****                 'LEN' = 'K' = 3
;*****
;*****       THE EXPONENT VALUE IN THE DESIRED OUTPUT STRING WILL ALWAYS
;*****       BE 'ILOG'.  A SCALE FACTOR, 'SCALE', IS SOUGHT WHICH WILL
;*****       REDUCE THE FOUR SIGNIFICANT DIGIT ABS(INPUT VALUE), 1567.0,
;*****       TO THE DESIRED THREE SIGNIFICANT DECIMAL DIGIT OUTPUT MANTISSA,
;*****       157 (ROUNDED IN RN MODE).
;*****       BY INSPECTION WE NEED
;*****
;*****            'SCALE' = 10^'ISCALE' = 10^1
;*****           'ISCALE' = 1
;*****
;*****       TO ACHEIVE
;*****
;*****           (OUTPUT MANTISSA STRING) = ABS(INPUT VALUE) / ('SCALE')
;*****                                157 = 1567.0  /  10^1
;*****                                                        (ROUND RN)
;*****       THE EQUATION
;*****
;*****           'ISCALE' = ('ILOG'+1) - 'LEN
;*****                    = (3 + 1) - 3
;*****                    = 1
;*****
;*****       PROVIDES THE NECESSARY VALUE FOR 'ISCALE'.  FOR THIS EXAMPLE,
;*****       THE TERM ('ILOG'+1) IS THE NUMBER OF DECIMAL DIGITS TO THE
;*****       LEFT OF THE DECIMAL POINT IN THE ABS(INPUT VALUE); 'LEN' IS
;*****       THE DESIRED NUMBER OF SIGNIFICANT DECIMAL DIGITS IN THE OUTPUT
;*****       MANTISSA.  SUBTRACTING THE TWO TERMS GIVES THE NUMBER OF PLACES
;*****       THE DECIMAL POINT MUST BE MOVED TO THE LEFT IN THE
;*****       ABS(INPUT VALUE) TO FIT IT INTO A 'LEN' DIGIT STRING.
;*****
;*****       EXAMPLE (2):
;*****
;*****       WHAT ABOUT AN INPUT VALUE OF MAGNITUDE LESS THAN '1' WHERE 'ILOG'
;*****       WOULD BE NEGATIVE.
;*****
;*****           INPUT VALUE = DECIMAL 0.01567
;*****                   'K' = 3
;*****                'ILOG' = -2
;*****                 'LEN' = 'K' = 3
;*****
;*****       THE DESIRED OUTPUT STRING WOULD NOW BE +1.57E-1.
;*****
;*****           'ISCALE' = ('ILOG'+1) - 'LEN
;*****                    = (-2 + 1) - 3
;*****                    = -4
;*****            'SCALE' = 10^-4
;*****           (OUTPUT MANTISSA STRING) = ABS(INPUT VALUE) / ('SCALE')
;*****                                157 = 0.01567  /  10^-4
;*****                                157 = 0.01567  *  10^+4
;*****                                                        (ROUND RN)
;*****
;*****       WHEN THE INPUT VALUE IS OF MAGNITUDE LESS THAN '1', THE HEURISTIC
;*****       EXPLANATION FOR THE 'ISCALE' EQUATION IS NOT AS CLEAR.  NOW
;*****       CONSIDER THE TERM,('ILOG'+1), TO BE THE NUMBER OF '0' DIGITS
;*****       TO THE RIGHT OF THE ABS(INPUT VALUE)'S DECIMAL POINT; 'LEN'
;*****       IS STILL THE DESIRED NUMBER OF SIGNIFICANT DECIMAL DIGITS IN
;*****       THE OUTPUT MANTISSA STRING.  THE VALUE OF 'ISCALE' IS, THEREFORE,
;*****       NOW THE NUMBER OF PLACES THE DECIMAL POINT MUST BE MOVED TO
;*****       THE RIGHT IN THE ABS(INPUT VALUE) TO FIT IT INTO A 'LEN' DIGIT
;*****       STRING.
;*****
;*****       EXAMPLE (3):
;*****
;*****       REPEAT THE FIRST EXAMPLE WITH 'K' = -3.  NOW THE DESIRED OUTPUT
;*****       STRING IS +1.567000E+3 WHICH CAN QUICKLY BE MANIPULATED INTO
;*****       THE FIXED POINT REPRESENTATION, +1567.000; REMEMBER THAT 'K' = -3
;*****       MEANT THAT WE SOUGHT THREE SIGNIFICANT DIGITS TO THE RIGHT OF THE
;*****       DECIMAL PLACE IN THE FIXED POINT REPRESENTATION.
;*****
;*****           INPUT VALUE = DECIMAL 1567.0
;*****                   'K' = -3
;*****                'ILOG' = 3
;*****                 'LEN' = ('ILOG'+1) - 'K'
;*****                       = (3 + 1) - (-3)
;*****                       = 7
;*****
;*****       NOTICE THAT 'LEN' IS NOW LARGER THAN IN THE 'K' POSITIVE EXAMPLE.
;*****
;*****           'ISCALE' = ('ILOG'+1) - 'LEN
;*****                    = (3 + 1) - 7
;*****                    = -3
;*****            'SCALE' = 10^-3
;*****           (OUTPUT MANTISSA STRING) = ABS(INPUT VALUE) / ('SCALE')
;*****                            1567000 = 1567.0  /  10^-3
;*****                            1567000 = 1567.0  *  10^+3
;*****                                                        (ROUND RN)
;*****
;*****       THE EXPLANATION OF THE 'ISCALE' EQUATION IS NOW THE SAME AS WHEN
;*****       'K' WAS A POSITIVE '3'.  NOW, HOWEVER, SINCE 'LEN' WAS EFFECTED
;*****       BY THE NEGATIVE 'K', THE VALUE FOR 'ISCALE' IS NEGATIVE - MEANING
;*****       THAT IT REPRESENTS THE NUMBER OF PLACES THE DECIMAL POINT MUST
;*****       BE MOVED TO THE RIGHT IN THE ABS(INPUT VALUE) TO FIT IT INTO
;*****       A 'LEN' DIGIT STRING WITH THE IMPLICIT DECIMAL POINT AFTER THE
;*****       FIRST DIGIT.
;*****
;*****       EXAMPLE (4):
;*****
;*****       FINALLY CONSIDER THE EXAMPLE OF AN INPUT VALUE OF MAGNITUDE LESS
;*****       THAN '1' WHEN 'K' IS NEGATIVE.  USING THE SAME INPUT VALUE
;*****       AS BEFORE:
;*****
;*****           INPUT VALUE = DECIMAL 0.01567
;*****                   'K' = -3
;*****                'ILOG' = -2
;*****                 'LEN' = ('ILOG'+1) - 'K'
;*****                       = (-2 + 1) - (-3)
;*****                       = 2
;*****
;*****       THE DESIRED OUTPUT STRING WOULD NOW BE +1.6E-2 (IN RN MODE).
;*****
;*****           'ISCALE' = ('ILOG'+1) - 'LEN
;*****                    = (-2 + 1) - 2
;*****                    = -3
;*****            'SCALE' = 10^-3
;*****           (OUTPUT MANTISSA STRING) = ABS(INPUT VALUE) / ('SCALE')
;*****                                 16 = 0.01567  /  10^-3
;*****                                 16 = 0.01567  *  10^+3
;*****                                                        (ROUND RN)
;*****
;*****       THE HEURISTIC EXPLANATION FOR THE 'ISCALE' EQUATION HERE IS
;*****       THE SAME AS IN EXAMPLE (2) ABOVE.
;*****
;*****
;******************************************************************************
;
;
;      ??????  NEED MORE COMMENTS ON THE NEXT STEP - ALSO CHECK ALL
;      ??????  OTHER COMMENTS TO SEE IF ANY EDITS NEEDED.
;
;      IF ('K' IS NEGATIVE AND ILOG < 'K') THEN ILOG NEEDS TO BE
;      ADJUSTED TO FORCE THE PROPER OUTPUT STRING.
;
;
        TST.W    D7
        bpl      @19                         ; IF ('K' IS NEGATIVE) THEN
           cmp.w       d4,d7                    ; IF (ILOG < 'K') THEN
           ble         @19
              MOVE.W   D7,D4                       ; ILOG := 'K'
@19                                          ; END
;
;
;      CALCULATE AN INTEGER, 'ISCALE', WHICH REPRESENTS THE POWER OF TEN
;      NEEDED TO CALCULATE THE SCALE FACTOR, 'SCALE'.
;
;
        moveq    #1,D5
        ADD.W    D4,D5                       ; 'ISCALE' = ('ILOG'+1) - 'LEN'
        SUB.W    D6,D5
;
;
;**    *****************************************************************
;**
;**
;**    WHAT ARE THE VARIABLES, THEIR RANGES, AND WHERE ARE THEY?
;**
;**
;**    REGISTER D4.W       (-4933) <= 'ILOG' <= 4931
;**
;**    REGISTER D5.W       (-4933) <= 'ISCALE' <= 4931
;**
;**    REGISTER D6.W       1 <= 'LEN' <= 18
;**
;**    REGISTER D7.W       (-32) <= 'K' <= (+18)  BUT 'K' <> 0
;**
;**
;** A7 -> SAVED ON STACK         'RECALC_CTR'  (WORD)
;**
;**       SAVED ON STACK         (INPUT VALUE)  (3 LONG WORDS)
;**
;**       SAVED ON STACK         ORIGINAL CONTROL REGISTER  (WORD)
;**
;**
;**    *****************************************************************
;
;
;      USING THE 'mc%pwrten' SUBROUTINE CALCULATE A SCALE FACTOR, 'SCALE', AS A
;      FP-NUMBER WHERE
;
;          'SCALE' = 10^[ABS('ISCALE')]
;
;      'mc%pwrten' PRESERVES REGS D4-D7 AND ALL ADDRESS REGS; IT EXPECTS D7
;      TO CONTAIN THE BINARY INTEGER TO FIND THE POWER OF TEN OF WHERE
;      (0 =< D7 <8192).  THE FP VALUE, 'SCALE', IS RETURNED IN REGS D0-D2.
;      POWERS OF TEN UP TO 10^27 ARE EXACT; HIGHER POWERS ARE RETURNED
;      ROUNDED TO NEAREST REGARDLESS OF CURRENT ROUNDING MODE (CURRENT
;      ROUNDING MODE IS PRESERVED HOWEVER).  THE RETURNED VALUE FOR 'SCALE'
;      WILL ALWAYS BE OF TYPE NORM AND WILL ALWAYS BE POSITIVE IN SIGN.
;
;
        MOVE.W   D7,A0                       ; SAVE 'K' IN REG A0
        MOVE.W   D5,D7                       ; PUT 'ISCALE' IN REG D7
        SMI      D5                          ; D5.B = 'LAMBDA' = 'ISCALE'S SIGN
        bpl      @20                         ; IF ('ISCALE' NEGATIVE) THEN
           NEG.W    D7                          ; NEGATE 'ISCALE' TO GET 'ABS'
@20                                          ; END
        jSR      mc%pwrten                      ; 'SCALE' = 10^[ABS('ISCALE')]
;
;
;      NOW THAT 'SCALE' IS CALCULATED, DON'T NEED 'ISCALE' ANYMORE -
;      RESTORE 'K' TO REG D7.W
;
        MOVE.W   A0,D7                       ; RESTORE 'K' TO D7.W
;
;
;******************************************************************************
;*****
;*****
;*****       THE PURPOSE OF USING THE ABS('ISCALE') IN FINDING THE 'SCALE'
;*****       WAS TO REDUCE ROUNDING ERRORS.  AS SHOWN IN THE ABOVE EXAMPLES,
;*****       WE WISH TO SCALE THE FP ABS(INPUT VALUE) TO GET A FP REPRESEN-
;*****       TATION OF THE 'LEN' DECIMAL DIGIT MANTISSA OUTPUT STRING.  SINCE
;*****       WHEN 'ISCALE' IS NEGATIVE
;*****
;*****                                  ABS(INPUT VALUE)   ABS (INPUT VALUE)
;*****       (OUTPUT MANTISSA STRING) = ---------------- = -----------------
;*****                                    SCALE FACTOR        10^'ISCALE'
;*****
;*****                                      ABS(INPUT VALUE)
;*****                                = -------------------------
;*****                                  (A NEGATIVE POWER OF TEN)
;*****
;*****                                         ABS(INPUT VALUE)
;*****                                = ------------------------------
;*****                                   1 / (A POSITIVE POWER OF TEN)
;*****
;*****       ACTUALLY INVOLVES TWO DIVISIONS WITH ROUNDING, THE MORE ACCURATE
;*****       CALCULATION FOR THE OUTPUT STRING IS
;*****
;*****       IF ('ISCALE' NEGATIVE) THEN
;*****
;*****          (OUTPUT MANTISSA STRING) = ABS(INPUT VALUE) * ('SCALE')
;*****
;*****                                   = ABS(INPUT VALUE)*[10^ABS('ISCALE')]
;*****       ELSE ('ISCALE' POSITIVE)
;*****
;*****          (OUTPUT MANTISSA STRING) = ABS(INPUT VALUE) / ('SCALE')
;*****
;*****                                   = ABS(INPUT VALUE)/[10^ABS('ISCALE')]
;*****       ENDIF
;*****
;*****       THIS METHOD WILL ALWAYS INVOLVE ONLY ONE ROUNDING ERROR IN THE
;*****       CALCULATION OF THE (OUTPUT MANTISSA STRING) FP REPRESENTATION.
;*****
;*****
;*****************************************************************************
;
;
;      MAKE COPY OF (INPUT VALUE) FROM STACK IN FP_REG.
;      TAKE ABS(FP_REG) TO REMAKE THE VARIABLE, 'X'.
;      CLEAR FP_REG'S TAG FIELD TO SAVE THE EXPENSE OF HAVING TO 'TYPE'
;      THE CALCULATED 'Y' LATER.
;
        MOVE.L   2+SIGN(A7),SIGN(A2)         ; COPY (INPUT VALUE) INTO FP_REG
        MOVE.L   2+FRACT(A7),FRACT(A2)
        MOVE.L   2+FRACT+4(A7),FRACT+4(A2)
        CLR.W    SIGN(A2)                    ; 'X' = ABS(FP_REG)
;                                                  = ABS(INPUT VALUE)
;                                              NOTE TAG FIELD CLEARED.
;
;**    *****************************************************************
;**
;**
;**       WHERE ARE THE VARIABLES.
;**
;**
;**       REGISTER D0 - D2       'SCALE' = 10^[ABS('ISCALE')]
;**                                      = (TYPE NORM, ALWAYS)
;**
;**       REGISTER D4.W          'ILOG'
;**
;**       REGISTER D5.B          'LAMBDA' = SIGN OF 'ISCALE'
;**
;**       REGISTER D6.W          'LEN'
;**
;**       REGISTER D7.W          'K'  (WORD)
;**
;**       REGISTER FP_N          'X' = ABS(INPUT VALUE)
;**                                    (TYPE NORM OR NON-ZERO-UNNORM FP NUMBER)
;**
;** A7 -> SAVED ON STACK         'RECALC_CTR'  (WORD)
;**
;**       SAVED ON STACK         (INPUT VALUE)  (3 LONG WORDS)
;**
;**       SAVED ON STACK         ORIGINAL CONTROL REGISTER  (WORD)
;**
;**
;**    *****************************************************************
;
;
;      CALCULATE THE FP REPRESENTATION OF THE OUTPUT MANTISSA STRING, 'Y',
;      USING 'LAMBDA' TO DETERMINE IF A MULTIPLY OR DIVIDE BY 'SCALE'
;      IS REQUIRED.  IF ('LAMBDA' POSITIVE) THEN 'Y' = 'X' / 'SCALE'
;      ELSE ('LAMBDA' NEGATIVE) 'Y' = 'X' * 'SCALE'.
;
;      SAVE REGS. D4, D6, & D7 SINCE EITHER 'mc%mul_opt' OR 'mc%div_opt'
;      WILL TRASH THEM.
;      DON'T NEED TO SAVE REG D5, 'LAMBDA'.
;      SAVE REG A1 SINCE 'mc%mul_opt' WILL TRASH IT.
;      BOTH 'mc%mul_opt' AND 'mc%div_opt' EXPECT REG D3 TO BE CLEAR.
;      CLEAR 'SCALE'S TAG FIELD TO SAVE THE EXPENSE OF HAVING TO 'TYPE'
;      THE CALCULATED 'Y' LATER.
;
;
        MOVEM.L  A1/D7/D6/D4,-(A7)           ; SAVE REGS ON STACK
        moveq    #0,D3                       ; CLEAR D3 FOR 'mc%mul_opt' OR
        SWAP     D0                                            ; 'mc%div_opt'
        CLR.W    D0                          ; CLEAR 'SCALE'S TAG FIELD
        SWAP     D0                          ; NOTE: 'SCALE' ALWAYS POSITIVE
        TST.B    D5
        bmi      @21                         ; IF ('LAMBDA' POSITIVE)
           jSR      mc%div_opt                    ; 'Y' = 'X' / 'SCALE'
        bra      @22                         ; ELSE ('LAMBDA' NEGATIVE)
@21
           jSR      mc%mul_opt                   ; 'Y' = 'X' * 'SCALE'
@22                                          ; END
        ;andi.l  #$8000ffff,d0                ; Clear tag field for subsequent INT.
        MOVEM.L  (A7)+,D4/D6/D7/A1
;
;
;      NOW HAVE THE UNROUNDED RESULT FOR 'Y' IN REGS. D0-D2 WITH THE G-R-S
;      BITS IN REG. D3.
;
;
;******************************************************************************
;*****
;*****
;*****       THE PURPOSE OF THE SHENANIGANS WITH THE G-R-S BITS IS TO REDUCE
;*****       ROUNDING ERRORS.  IF 'ROUND' WERE CALLED HERE TO GET A ROUNDED
;*****       FP NUMBER FOR 'Y', ONE ROUNDING ERROR WOULD BE INCURRED;
;*****       HOWEVER, WE ACTUALLY DESIRE THE INTEGER PART OF 'Y' WHICH WOULD
;*****       INCUR ANOTHER ROUNDING ERROR.
;*****
;*****       LOGICALLY 'OR'ING THE G-R-S BITS INTO 'Y'S LS_BIT INSURES THAT
;*****       THE INTEGER PART OF 'Y' IS CALCULATED WITH ONLY ONE ROUNDING
;*****       ERROR, AS EXPLAINED BELOW.
;*****
;*****           THE RESTRICTION OF ('LEN' <= 18) MEANS THAT THE ULTIMATE
;*****           VALUE OF 'Y' IS BETWEEN '0' AND '999,999,999,999,999,999';
;*****           THEREFORE 'Y' REQUIRES AT MOST 60 BINARY BITS TO BE EXACTLY
;*****           EXPRESSED AS A BINARY INTEGER.
;*****
;*****           IF THE G-R-S BITS FROM THE MULTIPLY OR DIVIDE USED TO
;*****           CALCULATE 'Y' WERE TO BE CARRIED OVER TO THE INTEGER PART
;*****           CALCULATION, THEY COULD AT MOST EFFECT THE VALUE OF THE
;*****           STIKY BIT PRIOR TO THE INTEGER PART'S ROUND.  SINCE THE
;*****           LSB OF THE CALCULATED 'Y' WOULD ALSO EFFECT ONLY THE INTEGER
;*****           PART'S STIKY BIT, 'OR'ING THE G-R-S BITS INTO THE LS_BIT
;*****           OF THE CALCULATED 'Y' IS SEEN TO BE A FORM OF STIKY BIT
;*****           PRE-CALCULATION FOR THE INTEGER PART'S ROUND OPERATION.
;*****
;*****           THE SAME EFFECT WOULD BE ACHIEVED IF THERE EXISTED
;*****           'FMULT_FINT_ROUND' AND 'FDIV_FINT_ROUND' SUBROUTINES WHICH
;*****           KEPT UP WITH THE G-R-S BITS UNTIL THE FINAL INTEGER PART
;*****           ROUNDING.
ÿ