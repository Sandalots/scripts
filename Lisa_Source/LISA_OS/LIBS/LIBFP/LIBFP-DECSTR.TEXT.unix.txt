        
        (* File LibFP/DecStr, Version 8 February 1984. *)
        
procedure Dec2Str{(var f: DecForm; var d: Decimal; var s: DecStr)};

var
    i      : integer;
    ds, es : Decstr;
        maxsig, is, ordsi, nansig : integer ;
maxls, ls : integer ;

    {*
    ** Convert an integer to a signed decimal string, for printing
    ** exponents.
    *}
    procedure Int2Str(ie: integer; var is: DecStr);
    var
        i: integer;
        s1 : string[1];

    begin
        i := ie;    {make integer local}
        if i < 0 then
            i := -i;
        s1 := '1';

        s1[1] := chr(ord('0') + (i MOD 10));
        is := s1;
        i := i DIV 10;
        while i <> 0 do
        begin
            s1[1] := chr(ord('0') + (i MOD 10));
            insert(s1, is, 1);
            i := i DIV 10
        end;

        if ie < 0 then is := concat('-', is)
    end;



begin  { Dec2Str }
    {*
    ** Cases 0, INFINITY, and NAN are treated separately.
    ** In any case, a lead sign or space is printed.
    *}
    if d.sgn <> 0 then
        s := '-'
    else
        s := ' ';

    {*
    ** If it is a nonzero, finite number, place a decimal
    ** point after the lead digit and print the exponent field.
    ** Exponent must be adjusted to align point after lead digit.
    ** The special cases are handled trivially.
    *}

    case d.sig[1] of

    '0':
        s := concat(s, '0.0');

    'I':
        if d.sgn <> 0 then
            s := '-INF'
        else
            s := ' INF';

    'N':
        {*
        ** Insert the hexits from d.sig and insert a colon
        ** after the leading four.
        *}
        with d do begin
        maxsig := length(sig) ;
        if maxsig > 5 then maxsig := 5 ;
        nansig := 0 ;
        for i := 2 to maxsig do
                begin (* accumulate NaN significand *)
                ordsi := ord(sig[i]) ;
                nansig := 16 * nansig + ordsi ;
                if ordsi <= 57 
                        then nansig := nansig - 48
                        else nansig := nansig - 55 ;
                end   (* accumulate NaN significand *) ;
        s := concat( s, 'NaN(000)') ;
        nansig := nansig mod 256 ;
        if nansig = 0 then nansig := nanzero ;
        is := 8 ;
        repeat
        s[is] := chr( 48 + nansig mod 10 ) ;
        nansig := nansig div 10 ;
        is := is - 1 ;
        until nansig = 0 ;
        while s[6] = '0' do delete( s, 6, 1 ) ;
        end;

    '?':
        s := concat(s, '???.???');

    otherwise
        if f.style = FloatDecimal then
                begin
                ds := d.sig ;
                ls := length(ds) ;
                i := d.exp + ls - 1;
                maxls := f.digits ;
                if maxls > (decstrlen-8) then maxls := decstrlen - 8 ;
                while ls < maxls do
                        begin (* append zeros *)
                        ls := ls + 1 ;
                        ds[ls] := '0' ;
                        end   (* append zeros *) ;
                ds[0] := chr(ls) ;
                insert('.', ds, 2);
                Int2Str(i, es);
                s := concat(s, ds, 'E', es)
                end
        else
        begin
            {*
            ** If it is a finite number, place a decimal
            ** point after the lead digit and print the exponent field.
            ** Exponent must be adjusted to align point after lead digit.
            ** Three cases are handled, respectively:
            **      XXXXXXXXX000..000.  (may be no zeros left of '.')
            **      XXXXX.XXXXX
            **      0.0000...000XXXXXXX (may be no zeros right of '.')
            *}
            if ((length(d.sig)+d.exp) > (decstrlen-3)) or
               ((-d.exp) > (decstrlen-3)) then
                begin (* lulu error *)
                s := concat(s, '???.???');
                end   (* lulu error *)
                else
                begin (* ok *)
            if d.exp >= 0 then
            begin
                s := concat(s, d.sig);
                for i := 1 to d.exp do
                    s := concat(s, '0');
                s := concat(s, '.') ;
                ls := length(s) ;
                maxls := ls + f.digits ;
            end
            else if (-d.exp) < length(d.sig) then
            begin
                s := concat(s, d.sig) ;
                insert('.', s, 1+(length(d.sig) + d.exp + 1));
                ls := length(s) ;
                maxls := ls + d.exp + f.digits ;
            end
            else
            begin
                s := concat(s, '0.');
                for i := 1 to -(length(d.sig) + d.exp) do
                    s := concat(s, '0');
                s := concat(s, d.sig) ;
                ls := length(s) ;
                maxls := ls + d.exp + f.digits ;
            end ;
                if maxls > decstrlen then maxls := decstrlen ;
                while ls < maxls do
                        begin
                        ls := ls + 1 ;
                        s[ls] := '0' ;
                        end ;
                s[0] := chr(ls) ;
                end   (* ok *) ;
        end
    end
end;

procedure strtodec(s: DecStr; var d: Decimal);

var
index : integer ;
validprefix : boolean ;

begin
index := 1 ;
str2dec( s, index, d, validprefix ) ;
if index <= 1 then d.sig := nanascbinstring ; (* BAD input! *)
end ;
        
procedure X2Str { (f : DecForm; x : Extended; var y : DecStr) } ;

var
d : decimal ;
tf : decform ;
rnd : rounddir ;
i, rpos : integer ;
env : environ ;
carry : boolean ;

function roundup ( p : integer ) : boolean ;
        (* Tells whether to round UP (true) or DOWN (false) based on digits
           p..length(sig) of d and based on current rounding mode rnd. *)

var
i, ip : integer ;
sticky, stickp : boolean ;

begin (* roundup *)
with d do begin
sticky := false ;
if p >= 0 then ip := p+1 else ip := 2 ;
for i := ip to length(sig) do sticky := sticky or (sig[i] <> '0') ;
if p >= 1 then stickp := sig[p] <> '0' else stickp := false ;
setxcp( inexact, sticky or stickp) ; (* Set inexact. *)
case rnd of
towardzero : roundup := false ;
upward : roundup := (sgn = 0) and (sticky or stickp) ;
downward : roundup := (sgn = 1) and (sticky or stickp) ;
tonearest : if p < 1 then roundup := false else
        roundup := (sig[p] >= '6') or ((sig[p] = '5') and sticky) ;
end (* case *) ;
end (* with d *) ;
end   (* roundup *) ;

begin
rnd := getrnd ;
procentry(env) ;
setrnd( rnd ) ;
x2dec( f, x, d ) ;
with d do if sig[1] = '?' then
        begin (* do it over to remove question marks *)
        setxcp( inexact, false ) ;
        setrnd( towardzero ) ;
        tf.style := floatdecimal ;
        tf.digits := sigdiglen - 1 ;
        x2dec( tf, x, d ) ;
        if testxcp( inexact ) then 
                begin
                sig := concat( sig, '1') ;
                exp := exp - 1 ;
                end ;
        setrnd( rnd ) ;
        if f.style = fixeddecimal then if exp < - f.digits then
                begin (* decimal rounding required *)
                rpos := length(sig) + exp + f.digits ;
                if roundup(rpos) then
                        begin (* increment decimal *)
                        if rpos <= 1 then
                                begin (* 1 *)
                                sig := '1' ;
                                exp := -f.digits ;
                                end   (* 1 *)
                                else
                                begin (* round *)
                                for i := rpos to length(sig) do
                                        sig[i] := '0' ;
                                carry := true ;
                                for i := rpos-1 downto 1 do if carry then
                                        begin
                                        sig[i] := succ(sig[i]) ;
                                        carry := sig[i] > '9' ;
                                        if carry then
                                                sig[i] := '0' ;
                                        end ;
                                if carry then
                                        begin (* overflow *)
                                        sig[1] := '1' ;
                                        exp := exp + 1 ;
                                        end   (* overflow *) ;
                                end   (* round *) ;
                        end   (* increment decimal *)
                        else
                        begin (* decrement decimal *)
                        if rpos <= 1 then
                                begin (* 0 *)
                                sig := '0' ;
                                exp := -f.digits ;
                                end   (* 0 *)
                                else
                                for i := rpos to length(sig) do
                                        sig[i] := '0' ;
                        end   (* decrement decimal *) ;
                end   (* decimal rounding required *) ;
        end   (* do it over to remove question marks *) ;
dec2str( f, d, y ) ;
procexit(env) ;
end;

        procedure S2Str { (f : DecForm; x : Single;   var y : DecStr) } ;
        var
                xx : extended ;

        begin
                s2x( x, xx ) ;
                x2str( f, xx, y ) ;
        end;

        procedure D2Str { (f : DecForm; x : Double;   var y : DecStr) } ;
        var
                xx : extended ;
        begin
                d2x( x, xx )  ;
                x2str( f, xx, y ) ;
        end;

        procedure C2Str { (f : DecForm; x : Comp;     var y : DecStr) } ;
        var
                xx : extended ;
        begin
                c2x( x, xx )  ;
                x2str( f, xx, y ) ;
        end;

            { y := x { (according to the format f) }

        procedure Str2S { (x : DecStr; var y : Single) } ;
        var
                d : Decimal;
        begin
                strtodec(x, d);
                Dec2S(d, y)
        end;

        procedure Str2D { (x : DecStr; var y : Double) } ;
        var
                d : Decimal;
        begin
                strtodec(x, d);
                Dec2D(d, y)
        end;

        procedure Str2C { (x : DecStr; var y : Comp) } ;
        var
                d : Decimal;
        begin
                strtodec(x, d);
                Dec2C(d, y)
        end;

        procedure Str2X { (x : DecStr; var y : Extended) } ;
        var
                d : Decimal;
        begin
                strtodec(x, d);
                Dec2X(d, y)
        end;

            { y := x }


ÿ