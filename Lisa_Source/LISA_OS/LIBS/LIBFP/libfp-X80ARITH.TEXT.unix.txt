; Copyright 1983, Apple Computer Inc.

        
; File L:X80Arith, Version 28 March 1983.

        .proc   x80%arith      
        
        .macro  pushmode        ; Saves current FP mode on stack, 
                                ; then ORs in #$%1 to the current FP mode.
                                ; %1 should be :
                                ; 10 to round extended results to single,
                                ; 20 to round extended results to double.
        move.b  fpmode(a2),-(sp); Save current FP mode on stack.
        andi.b  #$cf,fpmode(a2) ; Clear current extended rounding precision.
        ori.b   #$%1,fpmode(a2) ; OR in %1.
        .endm
        
        .macro  popmode         ; Restore current FP mode from stack.
        move.b  (sp)+,fpmode(a2)
        .endm
        
        .macro  block1          ; Evens up addressing by inserting a blank.
        .if     debugging
        .block  1,$20
        .endc
        .endm
        
        .macro  retn            ; Creates return that skips %1 bytes of argument.
ret%1            
        debugbegin
        bsr     loadfp0         ; Restore FP0.
        unlk    a6
        movea.l (sp)+,a0        ; A0 gets return address.
        .if     '%1' = 'C'
        adda.l  #$%1,sp
        .else
        addq.l  #$%1,sp         ; Skip parameters.
        .endc
        jmp     (a0)
        debugend 'x80ret%1',1
        .endm
        
        .def    fpneg           ; FP0 :=    -FP0 
fpneg
        debugbegin
        getfpcb a0
        bchg    #7,fpreg0+sign(a0) ; Reverse sign bit of FP0.
        rts
        debugend 'fpneg',3
        
        .def    fpabs           ; FP0 := abs(FP0)
fpabs
        debugbegin
        getfpcb a0
        bclr    #7,fpreg0+sign(a0) ; Clear sign bit of FP0.
        rts
        debugend 'fpabs',3
        
        .macro  absnegxd        ; Creates fp%1%2( var x,z : %2 )
        .def    fp%1%2
fp%1%2  debugbegin
        movea.l (sp)+,a0        ; A0 gets return address.
        movea.l (sp)+,a2        ; A2 gets address of z.
        movea.l (sp)+,a1        ; A1 gets address of x.
        move.l  (a1)+,d0        ; D0 gets most significant word.
        .if     '%1'='abs'
        bclr    #31,d0          ; Clear leading bit.
        .else
        bchg    #31,d0          ; Reverse leading bit.
        .endc
        move.l  d0,(a2)+        ; Store most significant word.
        move.l  (a1)+,(a2)+     ; Store second word.
        .if     '%2'='x'
        move.w  (a1)+,(a2)+      ; Store end of extended.
        .endc
        jmp     (a0)            ; Return
        debugend 'fp%1%2',2
        .endm
        
        absnegxd ABS,X
        absnegxd NEG,X
        absnegxd ABS,D
        absnegxd NEG,D
        
        ; savefp0 puts 
        ; address of fpcb_ in a2
        ; FP0 in -12(a6)
        
savefp0
        debugbegin
        getfpcb a2                      ; A2 gets address of FPCB_.
        move.l  fpreg0(a2),-12(a6)
        move.l  fpreg0+4(a2),-8(a6)
        move.l  fpreg0+8(a2),-4(a6)
        rts
        debugend 'savefp0',1
        
        ; loadfp0 restores FP0 from -12(a6).
        ; A2 is assumed to point to fpcb_.
        
loadfp0
        debugbegin
        move.l  -12(a6),fpreg0(a2)
        move.l  -8(a6),fpreg0+4(a2)
        move.l  -4(a6),fpreg0+8(a2)
        rts
        debugend 'loadfp0',1
        
        .macro  tmovefp         ; procedure tmovefp ( var x : t )
                                ; does FP0 := t.  %1 is t.
        .def    %1movefp
%1movefp debugbegin
        movea.l (sp)+,a2        ; A0 gets return address.
        movea.l (sp)+,a1        ; A1 gets address of t.
        dofline flineop+fea1,movefp+%1type+fp0op
        jmp     (a2)            ; Return.
        debugend '%1movefp',1
        .endm
        
        .macro  tmovefp1        ; procedure tmovefp1( var x : t )
                                ; does FP1 := t.  %1 is t.
        .def    %1movefp1
%1movefp1 debugbegin
        movea.l (sp)+,a2        ; A0 gets return address.
        movea.l (sp)+,a1        ; A1 gets address of t.
        dofline flineop+fea1,movefp+%1type+fp1op
        jmp     (a2)            ; Return.
        debugend '%1movefp1',0
        .endm
        
        tmovefp d
        tmovefp x
        tmovefp1 d
        tmovefp1 x
        tmovefp c
        
        ; procedure wmovefp (  x : t )
        ; does FP0 := x.
        .def     wmovefp
wmovefp debugbegin
        movea.l (sp)+,a2        ; A0 gets return address.
        move    (sp)+,d0        ; D0 gets x.           
        dofline flineop+fed0,movefp+wtype+fp0op
        jmp     (a2)            ; Return.
        debugend 'wmovefp',1
        
        ; procedure wmovefp1 (  x : t )
        ; does FP1 := x.
        .def     wmovefp1
wmovefp1 debugbegin
        movea.l (sp)+,a2        ; A0 gets return address.
        move    (sp)+,d0        ; D0 gets x.           
        dofline flineop+fed0,movefp+wtype+fp1op
        jmp     (a2)            ; Return.
        debugend 'wmovefp1',0
        
        .macro  lmovefpn        ; proc %1movefp ( x : %1 ) does fp0op := x
                                ; proc %1movefp1( x : %1 ) does fp1op := x
                                ; for 32 bit types %1
        .def     %1movefp
%1movefp debugbegin
        movea.l (sp)+,a2        ; A2 gets return address.
        move.l  (sp)+,d0        ; D0 gets x.           
        dofline flineop+fed0,movefp+%1type+fp0op
        jmp     (a2)            ; Return.
        debugend '%1movefp',1
        
        .def     %1movefp1
%1movefp1 debugbegin
        movea.l (sp)+,a2        ; A2 gets return address.
        move.l  (sp)+,d0        ; D0 gets x.           
        dofline flineop+fed0,movefp+%1type+fp1op
        jmp     (a2)            ; Return.
        debugend '%1movefp1',0
        .endm
        
        lmovefpn l
        lmovefpn s
        
        ; function  fpmovew : integer 
        ; returns FP0   
        .def     fpmovew
fpmovew debugbegin
        dofline flineop+fed0,fpmove+wtype+fp0op
        move    d0,4(sp)        ; Set up function result.
        rts            ; Return.
        debugend 'fpmovew',1
        
        .macro  fpmove32        ; func fpmove%1 does fpmove%1 := fp0op for 32 bit types %1
        .def     fpmove%1
fpmove%1 debugbegin
        dofline flineop+fed0,fpmove+%1type+fp0op
        move.l  d0,4(sp)        ; Set up function result.
        rts            ; Return.
        debugend 'fpmove%1',1
        .endm
        
        fpmove32 l
        fpmove32 s
        
        .macro  fpmovez         ; proc fpmove%1 ( var z : %1 ) does z := fp0op
        .def    fpmove%1
fpmove%1 debugbegin
        movea.l (sp)+,a2        ; A2 gets return address.
        movea.l (sp)+,a1        ; A1 gets address of z.
        dofline flineop+fea1,fpmove+%1type+fp0op
        jmp     (a2)
        debugend 'fpmove%1',1
        .endm
        
        fpmovez d
        fpmovez x
        
        retn    8
        
        .macro  tmovet  ; procedure %1move%2 ( var x : %1 ; var z : %2 ) 
        .def    %1move%2
%1move%2 debugbegin
        link    a6,#-12
        bsr     savefp0         ; Current FP0 saved at -12(a6).
        movea.l 12(a6),a1       ; A1 gets address of x.
        dofline flineop+fea1,movefp+%1type+fp0op ; FP0 gets x.
        movea.l 8(a6),a1        ; A1 gets address of z.
        dofline flineop+fea1,fpmove+%2type+fp0op ; z gets FP0.
        bra.s   	ret8
        debugend '%1move%2',2
        .endm
        
        tmovet  d,x
        tmovet  x,d
        
        retn    6
        
        .macro  wmovet  ; procedure wmove%1 ( x : w  ; var z : %1 ) 
        .def    wmove%1
wmove%1 debugbegin
        link    a6,#-12
        bsr     savefp0         ; Current FP0 saved at -12(a6).
        move    12(a6),d0       ; D0 gets x.
        dofline flineop+fed0,movefp+wtype+fp0op ; FP0 gets x.
        movea.l 8(a6),a1        ; A1 gets address of z.
        dofline flineop+fea1,fpmove+%1type+fp0op ; z gets FP0.
        bra.s   ret6
        debugend 'wmove%1',2
        .endm
        
        wmovet  d
        wmovet  x
        
        .macro  lmovet  ; procedure lmove%1 ( x : l  ; var z : %1 ) 
        .def    lmove%1
lmove%1 debugbegin
        link    a6,#-12
        bsr     savefp0         ; Current FP0 saved at -12(a6).
        move.l  12(a6),d0       ; D0 gets x.
        dofline flineop+fed0,movefp+ltype+fp0op ; FP0 gets x.
        movea.l 8(a6),a1        ; A1 gets address of z.
        dofline flineop+fea1,fpmove+%1type+fp0op ; z gets FP0.
        bra.s   	ret8
        debugend 'lmove%1',2
        .endm
        
        lmovet  d
        lmovet  x
        
        .macro  smovet  ; procedure smove%1 ( x : s  ; var z : %1 ) 
        .def    smove%1
smove%1 debugbegin
        link    a6,#-12
        bsr     savefp0         ; Current FP0 saved at -12(a6).
        move.l  12(a6),d0       ; D0 gets x.
        dofline flineop+fed0,movefp+stype+fp0op ; FP0 gets x.
        movea.l 8(a6),a1        ; A1 gets address of z.
        dofline flineop+fea1,fpmove+%1type+fp0op ; z gets FP0.
        bra.s   	ret8
        debugend 'smove%1',2
        .endm
        
        smovet  d
        smovet  x
        
        retn    4
        
        .macro  tmovew  ; function  %1movew ( var x : %1 ) : w          
        .def    %1movew
%1movew debugbegin
        link    a6,#-12
        bsr     savefp0         ; Current FP0 saved at -12(a6).
        movea.l  8(a6),a1       ; A1 gets address of x.
        dofline flineop+fea1,movefp+%1type+fp0op ; FP0 gets x.
        dofline flineop+fed0,fpmove+wtype+fp0op ; D0 gets FP0.
        move    d0,12(a6)       ; Store function result.
        bra.s	   ret4
        debugend '%1movew',2
        .endm
        
        tmovew  d
        tmovew  x
        
        .macro  tmovel  ; function  %1movel ( var x : %1 ) : l          
        .def    %1movel
%1movel debugbegin
        link    a6,#-12
        bsr     savefp0         ; Current FP0 saved at -12(a6).
        movea.l  8(a6),a1       ; A1 gets address of x.
        dofline flineop+fea1,movefp+%1type+fp0op ; FP0 gets x.
        dofline flineop+fed0,fpmove+ltype+fp0op ; D0 gets FP0.
        move.l  d0,12(a6)       ; Store function result.
        bra.s	   ret4
        debugend '%1movel',2
        .endm
        
        tmovel  d
        tmovel  x
        
        .macro  tmoves  ; function  %1moves ( var x : %1 ) : s          
        .def    %1moves
%1moves debugbegin
        link    a6,#-12
        bsr     savefp0         ; Current FP0 saved at -12(a6).
        movea.l  8(a6),a1       ; A1 gets address of x.
        dofline flineop+fea1,movefp+%1type+fp0op ; FP0 gets x.
        dofline flineop+fed0,fpmove+stype+fp0op ; D0 gets FP0.
        move.l  d0,12(a6)       ; Store function result.
        bra.s	   ret4
        debugend '%1moves',2
        .endm
        
        tmoves  d
        tmoves  x
        
        .macro  fpmonadic       ; procedure fp%1 ; FP0 gets %1(FP0).
        .def    fp%1
fp%1    debugbegin
        dofline flineop+fp0op,monadic+f%1op+fp0op
        rts
        debugend 'fp%1',2
        .endm
        
        fpmonadic int
        block1  1,$20           ; To even up address counter.
        fpmonadic sqrt
        
        .macro  bimonadic       ; procedure fp%1 ( var x, z : %2 ) 
        .def    fp%1%2
fp%1%2  debugbegin
        link    a6,#-12         ; Allow room to store FP0.
        bsr     savefp0         ; -12(a6) gets FP0.
        movea.l 12(a6),a1       ; A1 gets address of x.
        dofline flineop+fea1,movefp+%2type+fp0op ; FP0 gets x.
        .if     '%2'='D'
        pushmode 20
        .endc
        dofline flineop+fp0op,monadic+f%1op+%2type+fp0op ; FP0 gets op x   
        .if     '%2'='D'
        popmode
        .endc
        movea.l 8(a6),a1        ; A1 gets address of z.
        dofline flineop+fea1,fpmove+%2type+fp0op ; z gets FP0.
        bra.s   	ret8
        debugend 'fp%1%2',1
        .endm
        
        bimonadic int,x
        block1  1,$20           ; To even up addressing.
        bimonadic int,D
        block1  1,$20           ; To even up addressing.
        bimonadic sqrt,x
        bimonadic sqrt,D
        
        .macro  smonadic       ; function  fp%1s ( x) : real  
        .def    fp%1s
fp%1s   debugbegin
        link    a6,#-12         ; Allow room to store FP0.
        bsr     savefp0         ; -12(a6) gets FP0.
        move.l   8(a6),d0       ; D0 := x.             
        dofline flineop+fed0,movefp+stype+fp0op ; FP0 gets x.
        pushmode 10
        dofline flineop+fp0op,monadic+f%1op+stype+fp0op ; FP0 gets op x   
        popmode
        dofline flineop+fed0,fpmove+stype+fp0op ; D0 :=  FP0.
        move.l  d0,12(a6)       ; Store result.
        bra.s	   ret4
        debugend 'fp%1s',1
        .endm
        
        smonadic int
        block1  1,$20           ; To even up addressing.
        smonadic sqrt
        
        .macro  getcc           ; Uses A2 to get condition code, puts in %1.
        move.b  fpcond(a2),%1   
        andi.b  #ccmask,%1      ; Masks off other bits.
        .endm
        
        .macro  fpdyadic        ; Sets up proc FP%1 to do FP0 := FP0 %1 FP1.
        .def    fp%1 
fp%1    debugbegin
        dofline flineop+fp1op,dyadic+f%1op+fp0op
        .if     '%1'='COM'
        getfpcb a2              ; A2 gets pointer to FPCB_.
        getcc   4(sp)           ; Put function result in 4(sp)
        .endc
        rts
        debugend 'fp%1',3
        .endm
        
        fpdyadic add
        fpdyadic sub
        fpdyadic mul
        fpdyadic div
        fpdyadic rem
        fpdyadic COM
        
        retn    C
        
        .macro  tridyadic       ; Sets up proc FP%1%2 ( var x, y, z : %2 )
                                ; to do z := x %1 y.
        .def    fp%1%2
fp%1%2  debugbegin
        link    a6,#-12         ; Allow room to store FP0.
        bsr     savefp0         ; -12(a6) gets FP0.
        movea.l 16(a6),a1       ; A1 gets address of x.
        dofline flineop+fea1,movefp+%2type+fp0op ; FP0 gets x.
        movea.l 12(a6),a1       ; A1 gets address of y.
        dofline flineop+fea1,dyadic+f%1op+%2type+fp0op ; FP0 gets x op y.
        movea.l 8(a6),a1        ; A1 gets address of z.
        dofline flineop+fea1,fpmove+%2type+fp0op ; z gets FP0.
        bra.s   retc
        debugend 'fp%1%2',2
        .endm
        
        .macro  dtridyadic       ; Sets up proc FP%1d ( var x, y, z : d )
                                ; to do z := x %1 y.
        .def    fp%1d
fp%1d  debugbegin
        .if     amdcode
        pushd9512 12(sp)
        pushd9512 8(sp)
        cmd9512   d%1
        busy9512
        popd9512 4(sp)
        movea.l (sp)+,a0        ; A0 gets return address.
        adda.l  #12,sp          ; Skip parameters.
        jmp     (a0)
        .else
        link    a6,#-12         ; Allow room to store FP0.
        bsr     savefp0         ; -12(a6) gets FP0.
        movea.l 16(a6),a1       ; A1 gets address of x.
        dofline flineop+fea1,movefp+%2type+fp0op ; FP0 gets x.
        movea.l 12(a6),a1       ; A1 gets address of y.
        .if     '%1'<>'REM'
        pushmode 20
        .endc
        dofline flineop+fea1,dyadic+f%1op+%2type+fp0op ; FP0 gets x op y.
        .if     '%1'<>'REM'
        popmode
        .endc
        movea.l 8(a6),a1        ; A1 gets address of z.
        dofline flineop+fea1,fpmove+%2type+fp0op ; z gets FP0.
        bra.s   retc
        .endc
        debugend 'FP%1D',2
        .endm
        
        dtridyadic add,d
        tridyadic add,x
        dtridyadic sub,d
        tridyadic sub,x
        dtridyadic mul,d
        tridyadic mul,x
        dtridyadic div,d
        tridyadic div,x
        tridyadic REM,d
        tridyadic rem,x
        
        .macro  fpcomt       ; proc fpcom%1( var x, y: %2 ) : conditioncode
        .def    fpcom%1
fpcom%1 debugbegin
        link    a6,#-12         ; Allow room to store FP0.
        bsr     savefp0         ; -12(a6) gets FP0.
        movea.l 12(a6),a1       ; A1 gets address of x.
        dofline flineop+fea1,movefp+%1type+fp0op ; FP0 gets x.
        movea.l  8(a6),a1       ; A1 gets address of y.
        dofline flineop+fea1,dyadic+fcomop+%1type+fp0op ; FP0 gets x op y.
        getcc   16(a6)          ; Store condition code.
        bra.s   	ret8
        debugend 'fpcom%1',2
        .endm
        
        fpcomt  d
        fpcomt  x
        
        .macro  sdyadic         ; Function fp%1s ( x, y : real ) : real ;
        .def    fp%1s
fp%1s   debugbegin
        link    a6,#-12         ; Allow room to store FP0.
        bsr     savefp0         ; -12(a6) gets FP0.
        move.l  12(a6),d0       ; D0 := x.             
        dofline flineop+fed0,movefp+stype+fp0op ; FP0 gets x.
        move.l   8(a6),d0       ; D0 := y.             
        .if     '%1'<>'REM'
        .if     '%1'<>'COM'
        pushmode 10
        .endc
        .endc
        dofline flineop+fed0,dyadic+f%1op+stype+fp0op ; FP0 gets x op y.
        .if     '%1'<>'REM'
        .if     '%1'<>'COM'
        popmode
        .endc
        .endc
        .if     '%1'='COM'
        getcc   16(a6)
        .else
        dofline flineop+fed0,fpmove+stype+fp0op ; D0 gets result.
        move.l  d0,16(a6)       ; Set result in function slot.
        .endc
        bra.s   	ret8
        debugend 'fp%1s',2
        .endm
        
        sdyadic add
        sdyadic sub
        sdyadic mul
        sdyadic div
        sdyadic REM
        sdyadic COM
        
        .def    x80%paddr
        
; procedure x80%paddr ( k : integer ; a : ^ something )
; executes FMOVE from FP0 to p-string at a.
; k is specified dynamically in D0.

x80%paddr
        debugbegin
        link    a6,#-6          ;   0(sp) =
                                ;   0(a6) contains old link.
                                ;   4(a6) contains x80%paddr return address.
                                ;   8(a6) contains A.
                                ;  12(a6) contains k. 
                                ;  14(a6) = (old sp)
        move    12(a6),d0       ; D0 gets k.
        movea.l 8(a6),a1        ; A1 gets A, the address of the operand.
        dofline $f051,$1038       ; Do the F-line operation.
        unlk    a6
        movea.l (sp)+,a0        ; A0 gets return address.
        addq.l  #6,sp           ; Skip x80%paddr arguments.
        jmp     (a0)            ; Return.
        debugend 'x80%padd',0

ÿ