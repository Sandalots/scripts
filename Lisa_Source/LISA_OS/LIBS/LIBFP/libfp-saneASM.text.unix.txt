`;-----------------------------------------------------------
; 28AUG82: WRITTEN BY JEROME COONEN
; 13OCT82: REMOVE USES OF D0 ACROSS FP CALLS (JTC).
; 12DEC82: FIX BUGS IN CLASS FCNS (JTC).
; 28DEC82: SPLIT NAN CLASS TO SNAN, QNAN; SCALB, LOGB (JTC).
; 29DEC82: I2X, L2X PASS BY VALUE HERE. (JTC)
; 27APR83: NEG AS CORE FUNCTION ADDED. (JTC)
;  3 Aug 83 : (DGH) Procname %sane_asm; remove .INCLUDEs and .END.
;-----------------------------------------------------------
        ;.INCLUDE        TLASM:TOOLMACS.TEXT
        ;.INCLUDE        SANEMACS.TEXT

;-----------------------------------------------------------
;-----------------------------------------------------------
        ;.PROC   ADDSXXX,4
        .def    addsxxx
addsxxx
        MOVEA.L (SP)+,A0
        FADDS
        JMP     (A0)

        .DEF    ADDD
ADDD:
        MOVEA.L (SP)+,A0
        FADDD
        JMP     (A0)

        .DEF    ADDXXXX
ADDXXXX:
        MOVEA.L (SP)+,A0
        FADDX
        JMP     (A0)

        .DEF    ADDC
ADDC:
        MOVEA.L (SP)+,A0
        FADDC
        JMP     (A0)

;-----------------------------------------------------------
;-----------------------------------------------------------
        .DEF    SUBSXXX
SUBSXXX:
        MOVEA.L (SP)+,A0
        FSUBS
        JMP     (A0)

        .DEF    SUBD
SUBD:
        MOVEA.L (SP)+,A0
        FSUBD
        JMP     (A0)

        .DEF    SUBXXXX
SUBXXXX:
        MOVEA.L (SP)+,A0
        FSUBX
        JMP     (A0)

        .DEF    SUBC
SUBC:
        MOVEA.L (SP)+,A0
        FSUBC
        JMP     (A0)

;-----------------------------------------------------------
; NOTE THAT MULS MUST BE FUDGED BECAUSE IT IS A 68K INSTRUC.
; THE SAME HOLDS FOR DIVS BELOW.
;-----------------------------------------------------------
        .DEF    MULSXXX
MULSXXX:
        MOVEA.L (SP)+,A0
        FMULS
        JMP     (A0)

        .DEF    MULD
MULD:
        MOVEA.L (SP)+,A0
        FMULD
        JMP     (A0)

        .DEF    MULX
MULX:
        MOVEA.L (SP)+,A0
        FMULX
        JMP     (A0)

        .DEF    MULC
MULC:
        MOVEA.L (SP)+,A0
        FMULC
        JMP     (A0)

;-----------------------------------------------------------
;-----------------------------------------------------------
        .DEF    DIVSXXX
DIVSXXX:
        MOVEA.L (SP)+,A0
        FDIVS
        JMP     (A0)

        .DEF    DIVD
DIVD:
        MOVEA.L (SP)+,A0
        FDIVD
        JMP     (A0)

        .DEF    DIVX
DIVX:
        MOVEA.L (SP)+,A0
        FDIVX
        JMP     (A0)

        .DEF    DIVC
DIVC:
        MOVEA.L (SP)+,A0
        FDIVC
        JMP     (A0)

;-----------------------------------------------------------
;-----------------------------------------------------------
        .DEF    CMPX
CMPX:
        MOVEA.L (SP)+,A0        ; RET ADDRESS
        MOVE.B  4(SP),D1        ; RELOP TO BE TESTED
        ADDQ.B  #1,D1           ; HANDY TO INCREMENT

        MOVE.L  (SP),D0         ; SWAP ADDRESSES
        ADDQ.L  #2,SP           ; KILL SLOT FOR RELOP
        MOVE.L  4(SP),(SP)
        MOVE.L  D0,4(SP)

        CMPI.B  #4,D1           ; EQUAL
        BEQ.S   @1
        CMPI.B  #8,D1           ; UNORDERED
        BEQ.S   @1

        FCPXX
        BRA.S   @10
@1:
        FCMPX
@10:
        FBNE    @12
        BTST    #2,D1           ; #2 IS EQUAL BIT
        BRA.S   @50
@12:
        FBO     @14
        BTST    #3,D1           ; #3 IS UNORDERED BIT
        BRA.S   @50
@14:
        FBGT    @16
        BTST    #1,D1           ; #1 IS LESS BIT
        BRA.S   @50
@16:
        BTST    #0,D1           ; #0 IS GREATER BIT
@50:
        SNE     D1
        NEG.B   D1
        MOVE.B  D1,(SP)
        JMP     (A0)


;-----------------------------------------------------------
;-----------------------------------------------------------
        .DEF    RELX
RELX:
        MOVEA.L (SP)+,A0
        MOVE.L  (SP),D0         ; SWAP ADDRESSES
        MOVE.L  4(SP),(SP)
        MOVE.L  D0,4(SP)

        MOVEQ   #7,D1           ; ASSUME UNORDERED
        FCMPX
        FBU     @9              ; UNORDERED
        FBNE    @1
        MOVEQ   #3,D1           ; EQUAL
        BRA.S   @9
@1:
        FBGT    @3
        MOVEQ   #1,D1           ; LESS
        BRA.S   @9
@3:
        MOVEQ   #0,D1           ; GREATER
@9:
        MOVE.B  D1,(SP)
        JMP     (A0)


;-----------------------------------------------------------
; THE INTEGER AND LONGINT PARAMETERS ARE PASSED BY ADDRESS.
;-----------------------------------------------------------
        .DEF    I2XXXX
I2XXXX:
        MOVEA.L (SP)+,A0
        FI2X
        JMP     (A0)

        .DEF    L2XXXX
L2XXXX:
        MOVEA.L (SP)+,A0
        FL2X
        JMP     (A0)

        .DEF    S2XXXX
S2XXXX:
        MOVEA.L (SP)+,A0
        FS2X
        JMP     (A0)

        .DEF    D2X
D2X:
        MOVEA.L (SP)+,A0
        FD2X
        JMP     (A0)

        .DEF    C2X
C2X:
        MOVEA.L (SP)+,A0
        FC2X
        JMP     (A0)

        .DEF    X2X
X2X:
        MOVEA.L (SP)+,A0
        FX2X
        JMP     (A0)

;-----------------------------------------------------------
;-----------------------------------------------------------
        .DEF    X2I
X2I
        MOVEA.L (SP)+,A0
        FX2I
        JMP     (A0)

        .DEF    X2L
X2L:
        MOVEA.L (SP)+,A0
        FX2L
        JMP     (A0)

        .DEF    X2S
X2S:
        MOVEA.L (SP)+,A0
        FX2S
        JMP     (A0)

        .DEF    X2D
X2D:
        MOVEA.L (SP)+,A0
        FX2D
        JMP     (A0)

        .DEF    X2C
X2C:
        MOVEA.L (SP)+,A0
        FX2C
        JMP     (A0)

;-----------------------------------------------------------
;-----------------------------------------------------------
        .DEF    DEC2S
DEC2S:
        MOVEA.L (SP)+,A0
        FDEC2S
        JMP     (A0)

        .DEF    DEC2D
DEC2D:
        MOVEA.L (SP)+,A0
        FDEC2D
        JMP     (A0)

        .DEF    DEC2X
DEC2X:
        MOVEA.L (SP)+,A0
        FDEC2X
        JMP     (A0)

        .DEF    DEC2C
DEC2C:
        MOVEA.L (SP)+,A0
        FDEC2C
        JMP     (A0)

;-----------------------------------------------------------
;-----------------------------------------------------------
        .DEF    S2DECXXX
S2DECXXXX:
        MOVEA.L (SP)+,A0
        FS2DEC
        JMP     (A0)

        .DEF    D2DECXXX
D2DECXXX:
        MOVEA.L (SP)+,A0
        FD2DEC
        JMP     (A0)

        .DEF    X2DECXXX
X2DECXXX:
        MOVEA.L (SP)+,A0
        FX2DEC
        JMP     (A0)

        .DEF    C2DECXXX
C2DECXXX:
        MOVEA.L (SP)+,A0
        FC2DEC
        JMP     (A0)

;-----------------------------------------------------------
;-----------------------------------------------------------
        .DEF    REMX
REMX:
        MOVEA.L (SP)+,A0        ; RETURN ADDRESS
        MOVEA.L (SP)+,A1        ; ADDRESS OF QUOTIENT
        FREMX                   ; RETURNS WITH QUO IN D0
        MOVE.W  D0,(A1)         ; DELIVER INTEGER RESULT
        JMP     (A0)


;-----------------------------------------------------------
;-----------------------------------------------------------
        .DEF    SQRTX
SQRTX:
        MOVEA.L (SP)+,A0
        FSQRTX
        JMP     (A0)

        .DEF    RINTX
RINTX:
        MOVEA.L (SP)+,A0
        FRINTX
        JMP     (A0)

        .DEF    SCALBXXXX
SCALBXXXX:
        MOVEA.L (SP)+,A0
        FSCALBX
        JMP     (A0)

        .DEF    LOGBX
LOGBX:
        MOVEA.L (SP)+,A0
        FLOGBX
        JMP     (A0)


;-----------------------------------------------------------
;-----------------------------------------------------------
        ;.DEF    NEGXXXX
NEGXXXX:
        MOVEA.L (SP)+,A0        ; RETURN ADDRESS
        FNEGX
        JMP     (A0)

        ;.DEF    ABSX
ABSX:
        MOVEA.L (SP)+,A0        ; RETURN ADDRESS
        FABSX
        JMP     (A0)


;-----------------------------------------------------------
; NOTE THAT FOR THESE FUNCTIONS THE SOURCE OPERAND IS
; FIRST.
;-----------------------------------------------------------
        ;.DEF    CPYSGNX
CPYSGNX:
        MOVEA.L (SP)+,A0        ; SOURCE ADDRESS
        FCPYSGNX
        JMP     (A0)



;-----------------------------------------------------------
; THE CLASS FUNCTIONS USE A COMMON STARTUP ROUTINE TO FETCH
; THE LEADING 32 BITS AND STORE THE SIGN IN ITS VAR PARAM.
; A SET OF FINISHUP SEQUENCES SET THE RETURN VALUE TO THE
; APPROPRIATE INTEGER VALUE.
;-----------------------------------------------------------
        .DEF    CLASSX
        .DEF    CLASSD
        .DEF    CLASSSXXX
        .DEF    CLASSC

;-----------------------------------------------------------
; PASCAL: FUNCTION CLASS?(VAR X: ???????; VAR SGN: INTEGER):
;                                            NUMBERCLASS
; STACK = RET < SGN < X < RESULT
;-----------------------------------------------------------
CLASSX:
        MOVEA.L (SP)+,A0        ; RET ADRS
        MOVEA.L (SP),A1         ; SGN ADRS
        FCLASSX
        BRA.S   CLASSCOM

CLASSSXXX:
        MOVEA.L (SP)+,A0        ; RET ADRS
        MOVEA.L (SP),A1         ; SGN ADRS
        FCLASSS
        BRA.S   CLASSCOM

CLASSD:
        MOVEA.L (SP)+,A0        ; RET ADRS
        MOVEA.L (SP),A1         ; SGN ADRS
        FCLASSD
        BRA.S   CLASSCOM

CLASSC:
        MOVEA.L (SP)+,A0        ; RET ADRS
        MOVEA.L (SP),A1         ; SGN ADRS
        FCLASSC
CLASSCOM:
        MOVE.W  (A1),D0         ; RET CODE
        CLR.W   (A1)            ; 0 FOR +, 1 FOR -
        TST.W   D0
        BPL.S   @1

        ADDQ.W  #1,(A1)
        NEG.W   D0
@1:
        SUBQ.W  #1,D0
        MOVE.B  D0,(SP)
        JMP     (A0)



;-----------------------------------------------------------
;-----------------------------------------------------------
        .DEF    NEXTSXXX
NEXTSXXX:
        MOVEA.L (SP)+,A0        ; RET ADRS
        FNEXTS
        JMP     (A0)

        .DEF    NEXTD
NEXTD:
        MOVEA.L (SP)+,A0
        FNEXTD
        JMP     (A0)

        .DEF    NEXTX
NEXTX:
        MOVEA.L (SP)+,A0
        FNEXTX
        JMP     (A0)


;-----------------------------------------------------------
;-----------------------------------------------------------
; THE ENVIRONMENT PARAMETER IS AN INTEGER PASSED BY ADDRESS
; FOR CONVENIENCE IN LATER ROUTINES, SET/GETENV LEAVE A1
; UNCHANGED BUT TOUCH D0.
;-----------------------------------------------------------
;-----------------------------------------------------------
        .DEF    SETENV
        .DEF    GETENV
        .DEF    SETRND
        .DEF    GETRND
        .DEF    SETXCP
        .DEF    TESTXCP
        .DEF    SETHLT
        .DEF    TESTHLT
        .DEF    PROCEXTXXX
        .DEF    PROCENTRY

SETENV:
        link    a6,#0           ;  0(a6) contains old link.
                                ;  4(a6) contains return address.
                                ;  8(a6) contains ENV.
        pea     8(a6)
        FSETENV
        move.l  4(a6),a0        ; A0 gets return address.
        unlk    a6
        add.l   #6,sp           ; Increment past return address and ENV.
        JMP     (A0)

        debugname '%SANE_AS'
        debugbegin

PROCEXTXXX:
        MOVEA.L (SP)+,A0        ; RETURN ADDRESS
        FPROCExit 
        JMP     (A0)

GETENV:
        MOVEA.L (SP)+,A0        ; RETURN ADDRESS
        FGETENV
        JMP     (A0)
PROCENTRY:
        MOVEA.L (SP)+,A0        ; RETURN ADDRESS
        FPROCEntry
        JMP     (A0)


;-----------------------------------------------------------
;-----------------------------------------------------------
SETRND:
        MOVEA.L (SP)+,A1        ; RET ADRS

        MOVE.W  (SP),D1         ; NEW RND MODE
        ROL.W   #5,D1           ; ALIGN ROUND BITS
        ANDI.W  #$6000,D1       ; MASK JUST THOSE

        PEA     (SP)            ; OVERWRITE INPUT OPERAND
        BSR.S   GETENV
        ANDI.W  #$9FFF,(SP)     ; KILL OLD MODE
        OR.W    D1,(SP)         ; NEW MODE
        PEA     (SP)
        fsetenv

        ADDQ.L  #2,SP           ; KILL INPUT SLOT
        JMP     (A1)

;-----------------------------------------------------------
;-----------------------------------------------------------
GETRND:
        MOVEA.L (SP)+,A1
        PEA     (SP)
        BSR.S   GETENV

        MOVE.W  #$6000,D0       ; ISOLATE ROUND BITS
        AND.W   (SP)+,D0
        ROR.W   #5,D0           ; IN HI BYTE
        MOVE.W  D0,-(SP)
        JMP     (A1)

;-----------------------------------------------------------
; PASCAL: PROCEDURE TESTXCP(X: EXCEPTION): BOOLEAN;
; SO STACK = RET < X < RESULT.   HI BYTE (0) IS SET OF FLAGS
; AND LOW BYTE (1) IS HALTS.
;-----------------------------------------------------------
TESTXCP:
        CLR.W   D1
        BRA.S   GENTEST
TESTHLT:
        MOVEQ   #1,D1
GENTEST:
        MOVEA.L (SP)+,A1        ; RET ADRS, SAFE FROM GETENV

        PEA     2(SP)
        BSR.S   GETENV
        MOVE.B  (SP)+,D0        ; BIT INDEX IN HI BYTE
        EXT.W   D0              ; MAKE IT WORD INDEX
        BTST    D0,0(SP,D1)
        SNE     D0              ; D0 = 0000 OR 00FF
        NEG.B   D0              ; D0 = 0000 OR FFFF

        MOVE.B  D0,(SP)         ; SAVE BOOLEAN RESULT
        JMP     (A1)

;-----------------------------------------------------------
; PASCAL: PROCEDURE SETXCP(X: EXCEPTION; ONOFF: BOOLEAN);
; STACK = RET < ONOFF < X
; THIS PROCEDURE WAS ORIGINALLY SET UP TO JUST SET OR CLEAR
; BITS IN THE CONTROL WORD.  NOWADAYS, SETTING AN EXCEPTION
; FLAG ENTAILS A POSSIBLE HALT, SO A SPECIAL DUMMY FLOATING
; POINT ROUTINE IS USED.
;-----------------------------------------------------------
SETXCP:
        TST.B   4(SP)           ; CHECK BOOLEAN
        BEQ.S   CLRXCP

        MOVEA.L (SP)+,A0        ; SAVE RETURN ADDRESS
        ADDQ.L  #2,SP           ; KILL BOOLEAN
        MOVE.B  (SP),D0         ; GET ERROR INDEX
        EXT.W   D0              ; EXTEND TO WORD
        MOVE.W  D0,(SP)         ; REPLACE AS WORD
        PEA     (SP)            ; SAVE ADDRESS
        FSETXCP
        ADDQ.L  #2,SP           ; KILL INDEX WORD
        JMP     (A0)

CLRXCP:
        CLR.W   D1
        BRA.S   GENSETX
SETHLT:
        MOVEQ   #1,D1
GENSETX:
        MOVEA.L (SP),A1         ; RET ADDRESS, LEAVE SLOT

        PEA     (SP)
        BSR.S   GETENV
        MOVE.B  6(SP),D0        ; BIT INDEX IN HI BYTE
        EXT.W   D0              ; MAKE IT WORD INDEX

        TST.B   4(SP)           ; TEST INPUT BOOLEAN
        BNE.S   @1

        BCLR    D0,0(SP,D1)
        BRA.S   @3
@1:
        BSET    D0,0(SP,D1)
@3:
        PEA     (SP)
        fsetenv

        ADDQ.L  #8,SP           ; KILL RET AND INPUTS
        JMP     (A1)

        ;.END


ÿ