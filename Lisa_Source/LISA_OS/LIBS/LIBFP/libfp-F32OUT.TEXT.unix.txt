{ Copyright 1983, Apple Computer Inc. }

(* File L:F32Out, Version 23 February 1983. *)

{$ifc fp_compiler }
procedure f32_w_e ;
begin end ;
procedure f32_w_f ;
begin end ;
{$elsec }

procedure r_integral ( x : real ; var intx : real ; var xzero : boolean ) ;

        (* intx := integral(x).
        xzero is true if intx is zero. *)

var (* r_integral *)
xx, xintx : x48 ;
callrm : rmode ;

begin (* r_integral *)
if f32_integral(x) then intx := x else
        begin (* not integral *)
        x%sto( x, xx) ;
        if f32_minus(x) then (* Set up rounding modes for negative numbers. *)
                begin (* negative *)
                callrm := getround ; (* Save calling rounding mode. *)
                if callrm in [rneg, rpos] then
                        begin (* reverse directed *)
                        if callrm = rneg then setround(rpos) else setround(rneg) ;
                        x%int( xx, xintx) ;
                        intx := -x%tos( xintx ) ;
                        setround( callrm ) ; (* Restore calling rounding mode. *)
                        end   (* reverse directed *)
                        else
                        begin (* rn or rz *)
                        x%int( xx, xintx) ;
                        intx := -x%tos( xintx ) ;
                        end   (* rn or rz *) ;
                end   (* negative *)
                else
                begin (* positive *)
                x%int( xx, xintx) ;
                intx := x%tos( xintx ) ;
                end   (* positive *) ;
        end   (* not integral *) ;
xzero := intx = 0.0 ;
end   (* r_integral *) ;

procedure r_break ( x : real ; var intx, fracx : real ; var izero, fzero : boolean ) ;

        (* x = intx + fracx, the normalized integral and fractional parts of finite x.
        izero is true if intx is zero; fzero is true if fracx is zero. *)

begin (* r_break *)
fracx := f32_fraction(x) ;
fzero := fracx = 0.0 ;
intx := x - fracx ;
izero := intx = 0.0 ;
end   (* r_break *) ;

procedure r_fform ( x : real ; after : integer ; allafter : boolean ;
        var s : fp_string ) ;

        (* Revised 13 Feb 83 to correct bug for after >> maxf32k. *)
        
        (* Computes s := x for s of form 'ddd.ddddd'.
              If allafter is true then there will be exactly after digits after
              the point; otherwise there may be less than after.
              There may be digits before the point; if so the leading digit is
      not zero.  The caller should insure that 1 <= after <= maxfpstring-1
      and that length(s) will not exceed maxfpstring.
                                                        *)
const
maxx48pot = 127 ; (* Maximum power of ten that x%pot can compute. *)

var (* r_fform *)
i, is  : integer ;
adjust, excess, ncall, ifirst, ilast : integer ;
pot, ti, ts  : x48 ;
t : fp_string ;
delncall, delexp : integer ; (* Indicated change in ncall. *)
callrm : rmode ;

procedure app ( c : char ) ; (* Appends one character to s *)
begin
is := is + 1 ;
s[is] := c ;
end ;

begin (* r_fform *)
if after <= maxx48pot then ncall := after else ncall := maxx48pot ;
delncall := 0 ;
repeat
ncall := ncall - delncall ; (* Adjust power of ten. *)
clrexcepset ;              (* Initialize. *)
x%pot( ncall, pot) ;
x%sto( x, ti) ;
x%mul( ti, pot, ts) ;
if f32_minus(x) then
        begin (* negative *)
        callrm := getround ; (* Save calling rounding mode. *)
        if callrm in [rneg, rpos] then
                begin (* reverse directed *)
                if callrm = rneg then setround(rpos) else setround(rneg) ;
                x%int( ts, ti) ;
                setround( callrm ) ; (* Restore calling rounding mode. *)
                end   (* reverse directed *)
                else
                begin (* rn or rz *)
                x%int( ts, ti) ;
                end   (* rn or rz *) ;
        end   (* negative *)
        else
        begin (* positive *)
        x%int( ts, ti) ;
        end   (* positive *) ;
delexp := ti.exponent - 31 ; (* Compute discrepancy in power of two. *)
delncall := delexp div 4 ;   (* Approximate correction in power of ten. *)
if delncall < 1 then delncall := 1 ; (* But insure correction by at least one. *)
until delexp <= 0 ;          (* Loop until ti < 2^32. *)
x48%todec( ti, t) ;
adjust := after - ncall ; (* Number of zeros to add at end. *)
excess := length(t) - ncall ; (* Excess digits before point. *)
is := 0 ;
if excess <= 0 then
    begin (* prepend excess zeros to position point *)
    app('.') ; (* prepend point *)
    for i := 1 to (-excess) do app('0') ; (* prepend zero *)
    ifirst := 1 ;
    end   (* prepend excess zeros to position point *)
    else
    begin (* get digits before point *)
    for i := 1 to excess do app(t[i]) ;
        (* Append digits before point. *)
    app('.') ;
    ifirst := excess + 1 ;
    end   (* get digits before point *) ;
for i := ifirst to length(t) do app(t[i]) ; (* convert bcd string *)
if not allafter then adjust := 0 ; (* Don't add zeros at end. *)
        for i := 1 to adjust do (* append zero *)
                app('0') ;
s[0] := chr(is) ;
end   (* r_fform *) ;

procedure r_eform (
        x : real ; n : integer ;
        var sigma : integer ; var s : fp_string ; var e : integer ) ;

        (* Converts x to sign * s * 10^e.  1 <= length(s) <= n.
        If length(s) < n then the difference represents implied trailing
        zeros that would be present if length(s) had been forced to be
        exactly n.
        Sigma is 0 if x has negative sign, 1 otherwise.
        x is normal zero or NonNormal if and only if
        one or more leading digits of S is 0.
                                                        *)
var
xi, ti, ts, tp : x48 ;
i, ncall, e2, e1 : integer ;
norm : boolean ;
dp : integer ;
callrm : rmode ;

begin (* r_eform *)
        (* First ESTIMATE e.
                We want a e such that
                10^(n- 1) <= int(abs(x)*10^e) < 10^n

                For a first guess, use
                n - log10( 2**e * 1+f )
                which we approximate by
                n - ((77+(1/16))/256) * (e + f )

                *)

x%sto(x,xi) ;
norm := xi.significand < 0  ; (* If x is not normalized then we don't force
        n significant digits in E format.  *)
e1 := xi.exponent ;
e1 := 77 * e1 + (e1 div 16) ;
if norm then
        begin
        e2 := (xi.significand + $80000000 (* make positive *)) div $800000  ;
                (* 0 <= e2 <= 255 *)
        e1 := e1 + (77*e2) div 256 ;
        (* If normalized add in a contribution for the fraction.
        If not normalized, assume significand of 1.00000...  *)
        end ;
e := e1 div 256 ;
if e1 > 256*e then e := e + 1 ;
        (* but we try to get et as close as possible anyway to
        the ceiling of e1/256.  *)

if n <= maxf32k then ncall := n else ncall := maxf32k ;
e := ncall - e ;

        (* Now remedy flaws in approximation by altering e as necessary.  *)

dp := 0 ; (* Assume no correction required.  *)
repeat
e := e + dp ; (* Correct e.  *)
dp := 0 ; (* Assume no correction required.  *)
ts := xi ;
clrexcepset              ; (* Initialize. *)
x%pot(abs(e),tp) ;
if e > 0 then x%mul(ts,tp,ts)
else if e < 0 then x%div(ts,tp,ts) ;
if f32_minus(x) then
        begin (* negative *)
        callrm := getround ; (* Save calling rounding mode. *)
        if callrm in [rneg, rpos] then
                begin (* reverse directed *)
                if callrm = rneg then setround(rpos) else setround(rneg) ;
                x%int( ts, ti) ;
                setround( callrm ) ; (* Restore calling rounding mode. *)
                end   (* reverse directed *)
                else
                begin (* rn or rz *)
                x%int( ts, ti) ;
                end   (* rn or rz *) ;
        end   (* negative *)
        else
        begin (* positive *)
        x%int( ts, ti) ;
        end   (* positive *) ;

x%pot(ncall,tp) ;
case x%comp(tp,ti) of
 - 1: dp := - 1 ; (* If too big, correct and repeat.  *)
 0: begin
        if norm or (ncall=1) then
                begin (* We want only ncall digits.  *)
                e := e - 1 ;
                x%pot(ncall- 1,ti) ;
                end
                else
                begin (* If not normalized we want ncall- 1 digits.  *)
                e := e - 2 ;
                x%pot(ncall-2,ti) ;
                end ;
        end ;
 +1: begin
         x%pot(ncall- 1,tp) ;
         if norm then
                begin (* If normalized, insure enough sig digits. *)
                if x%comp(ti,tp) = - 1
                 then dp :=  + 1 ; (* If not enough digits, correct *)
                end  (* Need exactly ncall digits.  *)
                else
                begin (* If unnormalized, want no more than ncall- 1 digits.  *)
                if x%comp(ti,tp) <> - 1
                then dp := - 1 ; (* Try again for less than ncall.  *)
                end ;
        end ;
end ;
until (dp = 0)  ;
x48%todec(ti,s) ;
if length(s) < ncall then
        begin (* add unnormalizing digits *)
        dp := ncall - length(s) ;
        for i := length(s) downto 1 do s[i+dp] := s[i] ;
        for i := 1 to dp do s[i] := '0' ;
        s[0] := chr(ncall) ;
        end   (* add unnormalizing digits *) ;
e := - e ;
if f32_minus(x) then sigma := 1 else sigma := 0 ;
end   (* r_eform *) ;

procedure r_iform (
        x : real  ;
        var sigma : integer ; var s : fp_string ; var e : integer ) ;

        (* s := ascii representation of finite x.
        sign * s * 10^e = x; x must be normalized and integral.
        Sigma is 0 for positive x, 1 for negative x.
        e will be >= 0.
        If x is zero then s will be '0'.
        *)

var
sx : x48 ;

begin (* r_iform *)
x%sto( x, sx ) ;
if sx.exponent >= 32
        then
        begin (* get e form *)
        r_eform( x, maxf32k, sigma, s, e ) ;
        if e < 0 then s[0] := chr(length(s)+e) ; (* Remove trailing fractional zeros. *)
        end   (* get e form *)
        else
        begin (* get i form *)
        x48%todec ( sx, s ) ;
        e := 0 ;
        if f32_minus(x) then sigma := 1 else sigma := 0 ;
        end   (* get i form *) ;
end   (* r_iform *) ;

procedure f32_f_ascii (*
        x : real ; beforepoint : boolean ; after : integer ;
        var s : fp_string ; var error : boolean*) ;

        (* s := ascii representation of finite x displayed in fixed point
        notation with after>=0 digits after the point.
        If beforepoint is true then s will always have at least one digit before
        the point.  If beforepoint is false and abs(x) < 1 then s may have no
        digits before the point.
        The length of s will be minimal but >= after+1+sigma.
        Error is true, and s is undefined, if the length of s would exceed
        maxfpstring. *)

var
is, li, lf, e, i, sigma, oflo : integer ;
intx, fracx : real ;
ints, fracs : fp_string ;
izero, fzero : boolean ;
fpcb : fp_status ;
trapcoming : boolean ;

procedure app ( c : char ) ; (* Appends one character to s *)
begin
is := is + 1 ;
s[is] := c ;
end ;

begin (* f32_f_ascii *)
statusprelude(fpcb) ;
if after >= 1 then
        r_break ( x, intx, fracx, izero, fzero )  (* Break x up into pieces. *)
        else
        begin (* after=0 *)
        r_integral ( x, intx, izero ) ;
        fzero := true ;
        end   (* after=0 *) ;
if izero then
        begin (* integral part is zero *)
        if f32_minus(x) then sigma := 1 else sigma := 0 ; (* do sign *)
        if beforepoint then
                begin (* leading zero *)
                li := 1 ; ints[1] := '0' ;
                end   (* leading zero *)
                else
                li := 0 ;
        e := 0 ;
        end   (* integral part is zero *)
        else
        begin (* integral part nonzero *)
        r_iform( intx, sigma, ints, e ) ; (* Get string for int part. *)
        li := length(ints) ; (* li gets length of ints. *)
        end   (* integral part nonzero *) ;
error := (after < 0) or ((sigma + li + e + 1 + after) > maxfpstring ) ;
if not error then
        begin (* no error *)
        for i := 1 to e do ints[li+i] := '0' ; (* pad extra zeros *)
        li := li+e ; (* adjust length *)
        oflo := 0 ; (* Default case. *)
        if (after = 0) or getxcpn(inxact) or fzero then
                begin (* no fraction string *)
                lf := 0 ;
                end   (* no fraction string *)
                else
                 begin (* get fraction *)
                 r_fform( fracx, after, true, fracs ) ;
                 lf := length(fracs) ;
                 if fracs[1] <> '.' then
                        begin (* .999 rounded to 1.00 *)
                        i := li ;
                        while (i >= 1) and (ints[i] = '9') do
                                begin (* decimal carry *)
                                ints[i] := '0' ;
                                i := i - 1 ;
                                end   (* decimal carry *) ;
                        if i < 1 then
                                begin (* overflow *)
                                izero := false ;
                                oflo := 1 ;
                                end   (* overflow *)
                                else
                                begin (* no overflow *)
                                ints[i] := chr(ord(ints[i])+1) ; (* Increment. *)
                                        (* Should be SUCC but for 10.2 compiler bug *)
                                end   (* no overflow *) ;
                        end   (* .999 rounded to 1.00 *) ;
                 end   (* get fraction *) ;
        error := error or ((sigma + oflo + li + after + 1) > maxfpstring) ;
        if not error then
                begin
                is := 0 ;
                if sigma = 1 then app('-') ;
                if oflo = 1 then app('1') ; (* Prepend overflow digit. *)
                for i := 1 to li do app(ints[i]) ;
                if lf = (after+1) then
                        begin (* normal case *)
                        for i := 1 to lf do app(fracs[i]) ;
                        end   (* normal case *)
                        else
                        begin (* overflow - fraction only zeros *)
                        app('.') ;
                        for i := 1 to after do app('0') ;
                        end   (* overflow - fraction only zeros *) ;
                s[0] := chr(is) ;
                end ;
        end   (* no error *) ;
statuspostlude( fpcb, trapcoming ) ;
if trapcoming then f32postoutput( 'f32_f_ascii',x,s) ;
end   (* f32_f_ascii *) ;

procedure f32_e_ascii (*
        x : real ; before, after, ew : integer ;
        var s : fp_string ; var error : boolean*) ;

        (* s := ascii representation of finite x displayed in scientific
        notation with before digits before the point, after digits
        after the point, and ew exponent digits.
        The length of s will be minimal: before+after+ew+3+sigma.
        Error is true, and s is undefined, if before<0, after<0, ew<1, ew
        is too small for the exponent of x, or the length of s would exceed
        maxfpstring. *)

var (* f32_e_ascii *)
e, n, nc, i, ls, sigma : integer ;
fpcb : fp_status ;
trapcoming : boolean ;

begin (* f32_e_ascii *)
statusprelude(fpcb) ;
if f32_minus(x) then sigma := 1 else sigma := 0 ; (* Length of sign field. *)
n := before + after ; (* Number of significant digits required. *)
nc := n + ew + 3+sigma ; (* Number of characters required. *)
error := (after < 0 ) or (before < 0) or (ew < 1) or (n < 1) or
        (nc > maxfpstring) ;
if not error then
        begin
        r_eform( x, n, sigma, s, e ) ;
        ls := length(s) ; (* Number of significant digits returned. *)
        e := e + after - n + ls ; (* Adjust e for trailing zeros. *)
        for i := n downto (ls+1) do s[i] := '0' ; (* Add trailing zero. *)
        for i := n downto (before+1) do s[i+1+sigma] := s[i] ;
                (* Make room for sign and point. *)
        s[before+1+sigma] := '.' ;
        if sigma > 0 then
                begin (* make room for sign *)
                for i := before downto 1 do s[i+1] := s[i] ;
                s[1] := '-' ;
                end   (* make room for sign *) ;
        s[nc-ew-1] := 'E' ;
        if e < 0 then
                begin
                s[nc-ew] := '-' ;
                e := -e ;
                end
                else
                s[nc-ew] := '+' ;
        for i := nc downto (nc-ew+1) do
                begin (* exponent digit *)
                s[i] := chr( 48 + e mod 10 ) ;
                e := e div 10 ;
                end   (* exponent digit *) ;
        s[0] := chr(nc) ; (* Set length(s). *)
        error := e <> 0 ; (* Still an error if exponent too big for ew. *)
        end ;
statuspostlude( fpcb, trapcoming ) ;
if trapcoming then f32postoutput( 'f32_e_ascii',x,s) ;
end   (* f32_e_ascii *) ;

procedure f32_nan_ascii(*
        x : real ; width : integer ;
        var s : fp_string ; var error : boolean*) ;
        (* S := ascii representation of NaN x.  Only the sign and fraction
        of x are considered.
        If width=3, the sign will not be displayed if negative.
        Length(S) is minimal but at least 3.
        Error is true if width<3.
                                                        *)

var
pre, post : fp_string ;
kluge : record case boolean of
        false : (xx : real ) ;
        true  : (ff : fourbite) ;
        end ;

begin (* f32_nan_ascii *)
with kluge do 
        begin
        xx := x ;
        ff[1] := ff[1] + 128 ; (* Clear lowest exponent bit. *)
        end ;
fp%hex( @ kluge, 4, @ s) ;
pre := copy( s, 3, 4 ) ;
post := copy( s, 7, 2) ;
fp_nan_ascii( f32_minus(x), width, pre, post, s) ;
error := length(s) > width ;
end   (* f32_nan_ascii *) ;

procedure f32_output ( var f : text ; width : integer ; var s : fp_string ) ;
var i : integer ;
begin (* f32_output *)
if (s[1] <> ' ') and (s[1] <> '-') then
        begin (* force leading blank for positive *)
        write(f,' ') ;
        width := width - 1 ;
        end   (* force leading blank for positive *) ;
for i := 1 to width-length(s) do write(f,' ') ;
write(f,s) ;
end   (* f32_output *) ;

procedure f32_strange ( x : real ; width : integer ; var s : fp_string ) ;
        (* Takes NaN or Inf input and returns string s based upon width. *)
var
error, sign : boolean ;
w : integer ;
begin (* f32_strange *)
sign := f32_minus(x) ;
if sign then w := width else w := width - 1 ; (* Allow space for leading blank. *)
if f32_kind(x) = inf 
        then 
        begin
        if width < 3 then w := w + 3 - width ;
        fp_inf_ascii( sign, w, s, error ) 
        end
        else
        begin
        if w < 4 then w := 4 ;
        f32_nan_ascii(   x, w, s, error ) ;
        end ;
end   (* f32_strange *) ;

procedure f32_w_e (* var f: text; x: real; width: integer *);
var
after : integer ; error : boolean ; s : fp_string ;
w, i, ls : integer ;

begin
w := width ; (* w will be calling width. *)
if w <= 7 then w := 6 else if w > maxfpstring then w := maxfpstring ;
if w  < 7 then after := 0 else after := w - 7 ;
case f32_kind(x) of
zero : 
        begin (* zero *)
        fp_zero_ascii( f32_minus(x), 0, 0, fp_i, s, error ) ;
        ls := length(s) ;
        if after > 0 then
                begin (* blank point and after *)
                for i := ls+1 to ls+1+after do s[i] := ' ' ;
                ls := ls + 1 + after ;
                end   (* blank point and after *);
        for i := ls+1 to ls+4 do s[i] := ' ' ; (* Blank exponent field. *)
        s[0] := chr(ls+4) ;
        end   (* zero *);

nonnormal, normal : 
        begin (* normal *)
        f32_e_ascii( x, 1, after, 2, s, error) ;
        if after = 0 then delete( s, length(s)-4, 1) ; (* Delete point. *)
        end   (* normal *) ;

inf, nan : f32_strange ( x, w, s) ;
end (* case *) ;

f32_output( f, width, s) ;
end; {f32_w_e}

procedure f32_w_f (* var f: text; x: real; width,after: integer *);
var
error : boolean ; s : fp_string ;
ls, w, a, i : integer ;

begin
w := width ;
a := after ;
if a < 0 then a := 0 ;
if w > maxfpstring then w := maxfpstring ;
if (a=0) and (w<2) then w := 2 ; 
if (a>0) and ((w-a)<3) then
        begin (* fix w and a *)
        if (a+3) <= maxfpstring then w := a+3 else a := w-3 ; 
        end   (* fix w and a *) ;
case f32_kind(x) of
zero : 
        begin
        fp_zero_ascii( f32_minus(x), 0, 0, fp_i , s, error ) ;
        if a > 0 then
                begin (* blank point and after *)
                ls := length(s) ;
                for i := ls+1 to ls+1+a do s[i] := ' ' ;
                s[0] := chr(ls+1+a) ;
                end   (* blank point and after *) ;
        end ;

nonnormal, normal : 
        begin
        repeat
        f32_f_ascii( x, true, a, s, error) ;
        if error then a := a - 1 ;
        until not error ;
        if a = 0 then delete(s, length(s), 1) ; (* Remove point. *)
        end ;

inf, nan : f32_strange ( x, w, s) ;
end (* case *) ;

f32_output( f, width, s) ;
end; {f32_w_f}
{$endc }


ÿ