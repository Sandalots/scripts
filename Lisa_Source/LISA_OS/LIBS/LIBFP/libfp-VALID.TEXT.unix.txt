        .title 'VALID (1.6) SUBROUTINE  - 6/17/81 - 14:39:21 - 2.2'
        PAGE
         .proc    mc%vld        ; 0,0
         .def     mc%VALID,mc%vld_long
         .ref     mc%round
;
;******************************************************************************
;
;       revision      date         description
;       --------      ----         -----------
;         1.1       19.nov.80       original - (note ovfl_n_tr can be squeezed
;                                              to share more between RM & RP
;                                              but it would make both slower)
;
;         1.2        7.jan.81       chg FPRB pointer from reg a6 to reg a3;
;                                   chg all references to 'trapstatus' reg
;                                   to be references to 'tempstatus' reg.
;
;         1.3       13.jan.81       modified for EXORMACS download
;
;         1.4       15.jan.81       ***  MICROSYSTEMS RELEASE  ***
;                                   added xrefs for unf_n_tr and ovf__n_tr
;
;         1.5       13.feb.81       permenently converted to resident asm form
;
;         1.6       23.apr.81       added exception bit table
;
;         Hough     17 Jan 83       Removed unf_n_tr and ovf_n_tr entries.
;
;         Hough     18 Jan 83       Added mc%vld_long entry for use by FMULT and FDIV.
;
;         Hough     24 Jan 83       Untrapped overflows always set overflow flag.
;                                   Untrapped underflows do not set flag unless inexact.
;
;         Hough      8 Feb 83       Untrapped overflows always set inexact flag -
;                                   left out of previous fix.
;
;******************************************************************************
;
;
;
;                         mc%valid SUBROUTINE
;
;       Performs check underflow with (add_bias or unf_n_tr), mc%round, and
;       check overflow with (sub_bias or ovf_n_tr) on the internal format
;       extended precision, floating point number in regs. d0-d3 where the
;       destination format is always extended internal precision (FPn register).
;                             ------
;       Called by the dyadic arithmetic routines, ADD, SUB, MUL, DIV,
;       & REM.
;
;       enter with:           d0-d2    contains extended precision, internal
;       ==========                     format floating point number to be
;                                      validated; does not need to be
;                                      properly typed.
;
;                             d3       contains guard-mc%round-stiky bits
;
;                             a3       points to fp register block (FPRB)
;
;
;       exit with:
;       =========
;
;                             d0-d2    contains post-processed result
;                                      (result is not typed - tag field is trash
;
;                             d3       cleared (by mc%round)
;
;                             d4-d7    unchanged
;
;                             a1-a7    unchanged
;
;                             a0       destroyed (by mc%round)
;
;
;       Subroutines Called:   mc%round
;       ==================
;
;
;***********************  F L A G S   ******************************
;
;
;  This procedure is called by:  Add/Sub, Fmult, Fdiv, Decbin, Frem
;       No longer called by Fsqrt
;
;
;  Exception    Set by     Reason
;  =========    ======     ======
;
;  rsvx         ---        Not affected by this routine
;
;  iovf         ---        Not affected by this routine
;
;  inex         mc%round      Result can not be exactly represented
;                          using the current rounding precision.
;
;  dz           ---        Not affected by this routine
;
;  unfl         mc%valid      Result too small for internal extended
;                          precision.
;
;  ovfl         mc%valid      Result too large for internal extended
;                          precision.
;
;  iop          ---        Not affected by this routine
;
;
;*******************************************************************
;
;
;
;
;       The dyadic arithmetic routines which call 'mc%valid' will always
;       have a FP register in the FPRB as the destination operand.
;       This eliminates the need to check for an 'INVALID RESULT'
;       since extended internal format allows unnormalized numbers.
;
 PAGE
;******************************************************************************
;
;
;
;
;                     ***********************
;                     *  mc%valid STARTS HERE  *
;                     ***********************
;
;
mc%vld_long
        debugbegin
        move.l  d0,-(sp)                ; Stack sign and exponent.
                                        ; Exponent is assumed to be 31 bits wide.
        asl.l   #1,d0
        asr.l   #1,d0                   ; Extend sign of exponent.
        cmpi.l  #$ffff8000,d0
        bgt.s   cmpov                   ; Branch if not drastic underflow.
        move.l  (sp)+,d0                ; Restore original sign and exponent.
        bra.s   valid_un                ; Handle as underflow.
cmpov
        cmpi.l  #$7fff,d0          
        blt.s   valid_ok                ; Branch if no drastic exponent spill.
        move.l  (sp)+,d0                ; Restore original sign and exponent.
        jsr     mc%round                ; mc%round result prior to overflow treatment.
        bra     valid_ov                ; Handle as overflow.
valid_ok
        move.l  (sp)+,d0                ; Restore original sign and exponent.
        .if     debugging
        bra.s   mc%valid
        debugend 'MC%VLD_L',0  
        .endc
        
mc%valid
        
        debugbegin
;
;
;                                           If (underflow) then
        cmpi.w #extminxp,d0
        bge    not_unf
;
;         Originally had an 'If (not true zero)' here.  Since true zero's
;         exponent is equal to extminxp, it couldn't slip thru; a 'masquerade
;         zero' from FMUL or FDIV had its exponent adjusted to be less than
;         extminxp in the FMUL and FDIV subroutines to insure that it will
;         be an underflow.
;
valid_un                                ; Underflow detected.

           BTST     #ENUNFL_M,ENABLE(A3)
           beq.s    unf_n_tr                    ; IF (UNF TRAP ENABLED) THEN
              BSET     #STUNFL_M,TEMPSTAT(A3)   ; SET UNF FLAG IN TEMPSTATUS;
              ADDI.W   #EXTBIJST,D0             ; ADD BIAS ADJUST TO EXP_RESULT
;                                                 to wrap exp_result back into
;                                                 mid-range.
              jsr      mc%round                    ; mc%round RESULT
              bra      end_mc%valid                ; ELSE (UNF_NO_TRAP)
;
UNF_N_TR      EQU      *
;
;            Given an extended precision, internal format, floating
;            point intermediate result in regs. d0-d3 which has been
;            found to be an underflow without UNF trap enabled;
;            denormalize and mc%round. Set the UNF status flag only if
;            rounding mode is RN or RZ.
;            Tag field is NOT set to reflect the type of the result
;            (result could be a denorm, norm, or true zero).
;
;            If the difference between the minimum, internal format
;            exponent (extminxp = $C000) and the result exponent is
;            greater than or equal to 65 (i.e. the 64 mantissa bits and
;            1 guard bit would become zero if shifted over), then 'or'
;            the mantissa and guard bits into the r-s bits and zero out
;            the mantissa and guard bits.
;
              SUBI.W   #EXTMINXP,D0             ; SUB EXP_MIN FROM EXP_RESULT
              NEG.W    D0                       ; MAKE EXP_DIFF POSITIVE
              cmp.w    #65,d0
              blt.s    denorm
                                                   ; IF (EXP_DIFF >= 65) THEN
                 OR.L     D1,D2                    ; 'OR' ALL MTISSA BITS
                 OR.L     D2,D3                    ; & G-BIT INTO R-S BITS.
                 SNE      D3                       ; COMPENSATE FOR BIT
;                                                    to be shifted out.
                 LSR.L    #1,D3                    ; CLEAR G-BIT
                 moveq    #0,D1                    ; CLEAR MANTISSA BITS
                 moveq    #0,D2
              bra.s     rnd_rslt                   ; ELSE  (EXP_DIFF < 65)
denorm
;
;               denormalize the mantissa (exp_diff) times, shifting
;               thru g-r bits and 'or'ing stiky bits shifted out back
;               into sticky.
;               exp_diff in reg d0.w is the shift count.
;
                 cmp.w       #32,d0
                 blt.s       shf_16                   ; IF (SHIFT CT >= 32) THEN
                    OR.L     D2,D3                    ; 'OR' D2 INTO STICKY,
                    MOVE.L   D1,D2                    ; MOVE D1 INTO D2, AND
                    moveq    #0,D1                    ; CLEAR D1.
                    SUBI.W   #32,D0                   ; REDUCE SHIFT CNT BY 32
;
shf_16
                 cmp.w       #16,d0
                 blt.s       dec_shf                  ; IF (SHIFT CT >= 16) THEN
                    OR.W     D2,D3                    ; 'OR' D2.W INTO STICKY,
                    MOVE.W   D1,D2
                    SWAP     D2                       ; D2 NOW SHIFTED 16 BITS
                    CLR.W    D1
                    SWAP     D1                       ; D1 NOW SHIFTED 16 BITS
                    SUBI.W   #16,D0                   ; REDUCE SHIFT CT BY 16
;
dec_shf
                 SUBQ.W   #1,D0                       ; DECREMENT SHIFT CNT

                 bmi.s  rnd_rslt                     ; IF (ANY REMAINING SHIFTS)
DNRM_SHF            LSR.L    #1,D1                    ; SHIFT MTISSA RIGHT,
                    ROXR.L   #1,D2                    ; PROPOGATING THRU
                    ROXR.L   #1,D3                    ; G-R-S BITS

                    bcc.s    @1                       ; IF (STIKY-BIT-OUT SET)
                       SCS      D3                       ; RESTORE IT
@1
                    DBRA     D0,DNRM_SHF              ; DECR SHIFT CNT;
;                                                       continue denorm until
;                                                       shift cnt = (-1).
;                                                     END  (EXPDIFF < 65)
;
rnd_rslt
;            The mantissa has now been denormalized with the g-r-s bits
;            set accordingly; the result exponent was trashed to
;            calculate exp_diff.
;            mc%round the result mantissa and set the result exponent to
;            extminxp.
;
              MOVE.W   #EXTMINXP,D0             ; SET EXP_RESULT TO FORMAT'S MIN
              jsr      mc%round                    ; mc%round RESULT
;
;            If result is also inexact, then set underflow flag.
;
              BTST     #stinex_M,tempstat(A3)
              beq.s    end_mc%valid                   ; Branch if exact.          
                 BSET     #STUNFL_M,TEMPSTAT(A3)   ; SET UNDERFLOW FLAG
              bra.s    end_mc%valid
;                                                END  (UNFLOW_NO_TRAP)
;                                            ELSE  (NOT AN UNDERFLOW)
;
not_unf
           jsr      mc%round                    ; mc%round RESULT
           cmp.w    #extmaxp,d0
           blt.s    end_mc%valid
;                                           IF (OVERFLOW) THEN
;
;            Originally had an 'If (not true infinity) then' statement here;
;            since true infinity can only be produced by the component
;            routines (no arithmetic subroutine can produce a true infinity
;            result regardless of the value or type of its operands), no
;            test for this is needed here.
;
valid_ov                                        ; Overflow detected.
                 BSET     #STOVFL_M,TEMPSTAT(A3)   ; SET OVF FLAG IN TEMPSTATUS;

              BTST     #ENOVFL_M,ENABLE(A3)
              beq.s    ovf_n_tr                    ; IF (OVF TRAP ENABLED) THEN
                 SUBI.W   #EXTBIJST,D0             ; SUB BIAS ADJUST FROM EXP_
;                                                    result to wrap exp_result
;                                                    back into mid-range.
;                                                    ELSE  (OVFLOW_NO_TRAP)
                 bra.s    end_mc%valid
;
OVF_N_TR         EQU      *
;
;               Case (rounding mode) to determine action to be taken.
;               Tag field is NOT set to reflect the type of the result
;               (resuult could be norm, unnorm, or infinity).

         Bset     #stinex_M,tempstat(A3)        ; Always set inexact on untrapped
                                                ; overflow.
         BTST     #MDRMRP_M,MODE(A3)
         beq.s    ov_rnrz                    ; Branch if RN or RZ.      
            BTST    #MDRND_LS,MODE(A3)
            beq.s   rnd_rp                   ; IF (RND MODE IS RM)
               TST.L   D0

                   bmi.s   ov_inf                ; IF (RESULT POSITIVE)
;
;                        If the result is positive and normalized, then
;                        change the result (regs d0-d2) to be the largest
;                        positive normalized number.
;
                bra.s ov_max  
;
rnd_rp                                                ; ELSE (RND MODE RP)
                       TST.L    D0
;                                                      IF (RESULT NEGATIVE)
                       bpl.s    ov_inf  
;
;                        If the result is negative and normalized, then
;                        change the result (regs d0-d2) to be the largest
;                        negative normalized number.
;
                        bra.s   ov_max
                        
ov_rnrz
        btst    #mdrnd_ls,mode(a3)
        beq.s   ov_inf                  ; Branch if round to nearest.

ov_max  
                          TST.L    D1
;                                                               IF (RESULT IS
;                                                                   normalized)
                          bpl.s    @3
                             moveq    #0,D1
                             NOT.L    D1                       ; SET ALL MTISSA
                             moveq    #0,D2                    ; BITS TO A '1'.
                             NOT.L    D2
@3                                                           ; END (RESULT IS
;                                                                   normalized)
;
;                        Else the result is negative and unnormalized so
;                        change only the result exponent to be the largest
;                        positive exponent, (extmaxp-1) leaving the
;                        mantissa unnormalized.
;
                          MOVE.W  #EXTMAXP-1,D0             ; CHANGE EXP_RSLT
                          bra.s   end_mc%valid 
;
;

ov_inf    

;                  Change the result (regs d0-d2) to be a properly
;                  signed infinity.
;
                    moveq    #0,D1
                    moveq    #0,D2
                    MOVE.W   #EXTMAXP,D0

end_mc%valid
        
        RTS
        debugend 'MC%VALID',0
    
ÿ