{ Copyright 1983, Apple Computer Inc. }

(* File L:X80Elem, Version 22 February 1983. *)

(* general purpose routines for Exp and Trig functions.  *)

procedure p5eval ( var p : x05array  ; var x : fp_extended ; var z : fp_extended ) ;
        (* z := p(x)  = p[0]*x)+p[1])*x+p[2])*x+...p[5] *)
var i : integer ;
fx : fp_register ;

begin
xmovefp1(x) ; (* FP1 gets x. *)
with fpcb_.block do
        begin
        fx := f[1] ; (* FX gets x.  *)
        f[0] := p[0] ;
        for i := 1 to 5 do
                begin
                f[1] := fx ; (* FP1 gets x. *)
                fpmul ;
                f[1] := p[i] ;
                fpadd ;
                end ;
        end ;
fpmovex(z) ;
end ;

procedure postmonadic( name : fp_string ; var x,z : fp_extended ) ;
        (* Contains the code executed at the end of every monadic elementary
        function procedure if a trap is to occur. *)

var
r : fp_traprecord ;

begin
with r do
        begin (* with *)
        gettexcepset(es) ;
        procname := @ name ;
        op1 := @ x ;
        op2 := nil ;
        result := @ z ;
        optype1 := tfp_extended ;
        resulttype := tfp_extended ;
        end   (* with *) ;
fp_postoperation( r     ) ; (* Go composite a floating point operation. *)
end ;

procedure postdyadic(* name : fp_string ; var x,y,z : fp_extended *) ;
        (* Contains the code executed at the end of every dyadic elementary
        function procedure if a trap is to occur. *)

var
r : fp_traprecord ;

begin
with r do
        begin (* with *)
        gettexcepset(es) ;
        procname := @ name ;
        op1 := @ x ;
        op2 := @ y ;
        result := @ z ;
        optype1 := tfp_extended ;
        optype2 := tfp_extended ;
        resulttype := tfp_extended ;
        end   (* with *) ;
fp_postoperation( r     ) ; (* Go composite a floating point operation. *)
end ;

        (* Routines for exponential and logarithmic calculations. *)

procedure p3eval ( var p : x03array  ; var x : fp_extended ;
        var z : fp_extended ) ;
        (* z := p(x)  = p[0]*x)+p[1])*x+p[2])*x+p[3] *)
var i : integer ;
fx : fp_register ;

begin
xmovefp1(x) ; (* FP1 gets x. *)
with fpcb_.block do
        begin
        fx := f[1] ; (* FX gets x.  *)
        f[0] := p[0] ;
        for i := 1 to 3 do
                begin
                f[1] := fx ; (* FP1 gets x. *)
                fpmul ;
                f[1] := p[i] ;
                fpadd ;
                end ;
        end ;
fpmovex(z) ;
end ;

procedure q1eval ( var q : x01array  ; var x : fp_extended ;
        var z : fp_extended ) ;
        (* z := q(x)  = x+q[0])*x+q[1] *)
var
fx : fp_register ;

begin
xmovefp1(x) ; (* FP1 gets x.  *)
with fpcb_.block do
        begin (* with *)
        fx := f[1] ; (* FX gets x.  *)
        f[0] := q[0] ; (* FP0 gets q[0]. *)
        fpadd ;
                fpmul ;
                f[1] :=  q[1] ;
                fpadd ;
        end   (* with *) ;
fpmovex(z) ;
end ;

procedure  fpnormalizex (  x : fp_extended ; var z : fp_extended  )     ;
        (* z := Normalize(x). No op is x is denormalized.  *)
begin
xmovefp ( x ) ;
with fpcb_.block do
        begin
        fp%normalize( f[0] ) ;
        if f[0].exponent < -$4000 then z := x else fpmovex ( z ) ;
        end ;
end   ;

function ilogb ;
        (* Returns exponent of x in floating point register form,
           so that denormalized numbers are normalized while zero, infinity,
           and NaN are undefined. *)
var 
e : integer ;
fpr, xfpr : fp_register ;
begin (* ilogb *)
if fpkindx(x) = nonnormal then
        begin (* nonnormal *)
        fpmove(fpr) ; (* FPR := FP0. *)
        xmovefp(x) ;
        fpmove(xfpr) ;
        movefp(fpr) ; (* Restore FP0. *)
        fp%cnormalize(xfpr) ;
        e := xfpr.exponent ;
        end   (* nonnormal *)
        else
        e := getexponent(x) ;
ilogb := e ;
end   (* ilogb *) ;

procedure setexponent( var x : fp_extended ; e : longint ; var z : fp_extended ) ;
        (* z := x but with exponent e. *)
var
e1, e2 : integer ;
begin (* setexponent *)
if e < 0 then e := e + 32767 + 1 ; (* Make e non-negative. *)
e1 := e div 256 ;
e2 := e mod 256 ;
if fpminusx(x) then e1 := e1 - 128 ; (* -128 <= e1 <= 127 *)
if e2 >= 128 then e2 := e2 - 256 ; (* -128 <= e2 <= 127 *)
z := x ;
z[0] := e1 ; z[1] := e2 ; (* Set sign and exponent. *)
end   (* setexponent *) ;

procedure fpscalex ;
        (* z gets x * 2^i for integer i.  *)
var
k, p, xlog : integer ;
r : fp_extended ;
kluge : record case boolean of
        false : ( tx : fp_extended ) ;
        true  : ( te : integer  ) ; (* Sign/exponent field. *)
        end ;

begin (* fpscalex *)
if fpkindx(x) in [nonnormal, normal] then
        begin
        xlog := getexponent(x) ;
        p := xlog + i ;
        end
        else
        begin
        xlog := 17000 ; (* For zero, inf, and NaN, force multiply. *)
        end ;
if (abs(xlog) <= 16382) and (abs(p) <= 16382) then
        begin (* scale without multiply *)
        if fpminusx(x) then
                begin (* adjust for negative sign *)
                if p >= 0 then p := (p + 16384) + 16384 ;
                end   (* adjust for negative sign *)
                else
                begin (* adjust for positive sign *)  
                if p < 0  then p := (p + 16384) + 16384 ;
                end   (* adjust for positive sign *);
        with kluge do
                begin (* with kluge *)
                tx := x ;
                te := p ; (* New exponent. *)
                z  := tx ;
                end   (* with kluge *) ;
        end   (* scale without multiply *)
        else
        begin (* scale with multiplies *)
        r := x ; 
        k := i ;
        while k <> 0 do
                begin (* multiply loop *)
                if abs(k) <= 16382 then p := k else
                        if k > 0 then p := 16382 else p := -16382 ;
                k := k - p ;
                if p < 0 then p := (p + 16384) + 16384 ; (* 0 <= p <= 32767 *)
                with kluge do
                        begin (* with kluge *)
                        wmovex( 1, tx ) ;
                        te := p ;
                        fpmulx( r, tx, r ) ;
                        end   (* with kluge *) ;
                end   (* multiply loop *) ;
        z := r ;
        end   (* scale with multiplies *) ;
end   (* fpscalex *) ;

procedure scalb ; (* z gets x * 2^y for small integral y.  *)

var
fpcb : fp_blocktype ;
es : excepset ;
domult, trapcoming : boolean ;
p : integer ;
f,r : fp_extended ;
long : longint ;

begin (* scalb *)
blockprelude(fpcb) ; (* Save old floating point environment and standardize.  *)
if fpkindx(x) = nan then choosenan( x, y, r ) 
                (* Note fpscalex doesn't catch trapping nans. *)
        else 
        begin
        p := xmovew(y) ; (* P gets intended scale factor. *)
        getexcepset(es) ;
        if (es = []) then 
                begin
                fpscalex( x, p, r ) ;
                end 
                else 
                begin
                clrexcepset ; (* Clear exceptions left over from xmovew. *)
                domult := true ;
                if fpkindx(y) in [nonnormal,normal] then
                        begin (* finite y *)
                        if x80_integral(y) then
                                begin (* large integral y *)
                                if getexponent(y) <= 15 
                                        then long := xmovel(y) else (* too big *)
                                        if fpminusx(y) 
                                                then long := -33000 
                                                else long := 33000 ;
                                r := x ;
                                while abs(long) > 0 do
                                        begin (* chunk *)
                                        if abs(long) <= 16382 then p := long else 
                                                if long > 0 
                                                        then p := 16382 
                                                        else p := -16382 ;
                                        fpscalex(r,p,r) ;
                                        long := long - p ;
                                        end   (* chunk *) ;
                                domult := false ;
                                end   (* large integral y *)
                                else
                                begin (* non-integral y causes nan *)
                                errornan( nanexp, f) ;
                                end   (* non-integral y causes nan *);
                        end   (* finite y *)
                        else
                        begin 
                        exp2(y,f) ;
                        end ;
                if domult then fpmulx( x, f, r) ;
                end ;
        end ;

blockpostlude(fpcb, trapcoming) ;
if trapcoming then postdyadic( 'scalb',x,y,r) ;
z := r ;
end   (* scalb *) ;

procedure unzer( var x, z : fp_extended ) ;
        (* z := x but with zero significand. *)
var i : 0..9 ;
begin (* unzer *)
for i := 0 to 1 do z[i] := x[i] ;
for i := 2 to 9 do z[i] := 0 ;
end   (* unzer *) ;

procedure expapprox ( var x : fp_extended ; var z : fp_extended ) ;
        (* For abs(x) <= 0.5, z := 2^x -1.  *)

var xnorm, unzerox : fp_extended ;

procedure usual ;
        (* Computes approximation for normalized x.  *)
        (* Approx is of form
                2^x-1 = 2xp(x^2)/(q(x^2)-xp(x^2))   *)
var x2,p,q : fp_extended ;
begin
fpmulx(xnorm,xnorm,x2) ; (* x2 gets xnorm^2 *)
p3eval(exp21p,x2,p) ; (* p gets p(x2) *)
fpmulx(p,xnorm,p) ;
q1eval(exp21q,x2,q) ; (* q gets q(x2) *)
fpsubx(q,p,q) ;
fpscalex( p, 1, p ) ;
fpdivx(p,q,z) ;
setxcpn(underfl,false) ; (* Underflows are always inconsequential.  *)
setxcpn(inxact, true ) ; (* Always inexact here. *)
end ;

begin
case fpkindx(x) of

zero : z := x ;

NonNormal : begin (* Special routines for unnormalized arguments.  *)
unzer(x, unzerox) ; (* Create an NonNormal zero with sign and exponent of x.  *)
fpnormalizex(x,xnorm) ;
        (* Normalize x to avoid Invalid Operations in warning mode.  *)
usual ; (* Compute function of normalized x.  *)
fpaddx(z, unzerox, z) ; (* Add unnormalized zero to unnormalize result
        if appropriate.  *)
end ;

normal : begin xnorm := x ; usual ; end ;

end ;

end ;

procedure exp2r ( var x : fp_extended ; var z : fp_extended ) ;
        (* Computes 2^x for x that is zero, NonNormal, or normal.  *)
var
aintx, fracx : fp_extended ;

begin (* exp2r *)
        fpintx(x,aintx) ; (* Aintx gets integral part of x.  *)
        setxcpn( inxact, false ) ; (* Turn off Inexact from Fpintegral.  *)
        fpsubx(x,aintx,fracx) ; (* Fracx gets fraction part of x.  *)
        expapprox(fracx,z) ; (* z gets 2^fracx - 1 *)
        fpaddx(z,fpk1,z) ; (* z gets 2^fracx *)
        scalb(z,aintx,z) ; (* z gets 2^x *)
end (* exp2r *) ;

procedure exp2 (* x : fp_extended ; var z : fp_extended *) ; (* z := 2^x *)

var
fpcb : fp_blocktype ;
r : fp_extended ;
trapcoming : boolean ;

begin
blockprelude(fpcb) ; (* Save environment, clear exceptions and traps.  *)
case fpkindx(x) of
zero : begin
        r := fpk1 ; (* 2^0 = 1 *)
        end ;
NonNormal, normal : begin
        exp2r( x, r) ;
        end ;

inf : case fpcomx(x,fpk0) of
        lesser : r := fpk0 ; 
        greater: r := x ;
        unord  : errornan(nanexp,r) ;
        end (* case *) ;

nan : checknan( x, r ) ;

end (* case *);
blockpostlude( fpcb, trapcoming) ;
if trapcoming then postmonadic( 'exp2',x,r) ;
z := r ;
end ;

procedure exp21r ( var x : fp_extended ; var z : fp_extended ) ;
        (* Computes 2^x-1 for x that is zero, NonNormal, or normal.  *)
var
aintx, fracx : fp_extended ;

begin (* exp21r *)
        fpintx(x,aintx) ; (* Aintx gets integral part of x.  *)
        setxcpn( inxact, false ) ; (* Turn off Inexact from Fpintegral.  *)
        fpsubx(x,aintx,fracx) ; (* Fracx gets fraction part of x.  *)
        expapprox(fracx,z) ; (* z gets 2^fracx - 1 *)
        if fpcomx(aintx,fpk0) <> equal then
                begin (* 2^(i+f)-1 = 2^i(2^f-1 +1) -1 *)
                fpaddx(z,fpk1,z) ; (* z := 2^frac *)
                scalb(z,aintx,z) ;
                fpsubx(z,fpk1,z) ; (* z := 2^x -1 *)
                end ;
end (* exp21r *) ;

procedure exp21 (* x : fp_extended ; var z : fp_extended *) ; (* z := 2^x-1 *)
var
fpcb : fp_blocktype ;
r : fp_extended ;
trapcoming : boolean ;

begin
blockprelude(fpcb) ; (* Save environment, clear exceptions and traps.  *)
case fpkindx(x) of
zero : begin
        r := x ;
        end ;
NonNormal, normal : begin
        exp21r(x,r) ;
        end ;

inf : case fpcomx(x,fpk0) of
        lesser : r := fpkm1 ;
        greater: r := x ;
        unord  : errornan(nanexp,r) ;
        end (* case *) ;

nan : checknan(x,r) ;

end (* case *);
blockpostlude( fpcb, trapcoming) ;
if trapcoming then postmonadic( 'exp21',x,r) ;
z := r ;
end ;

procedure expe (* x : fp_extended ; var z : fp_extended *) ; (* z := e^x *)

var
fpcb : fp_blocktype ;
r, fracx, aintx, xdim : fp_extended ;
trapcoming : boolean ;

begin
blockprelude(fpcb) ; (* Save environment, clear exceptions and traps.  *)
if fpkindx(x) in [NonNormal,normal] then
        begin (* Compute exp.  *)
        fpremx(x,fpkloge2,r) ; (* r gets x rem loge2 *)
        fpsubx(x,r,xdim) ; (* xdim gets x - rem so divide will be exact. *)
        fpdivx(r,fpkloge2,fracx) ; (* Fracx gets r/log *)
        fpdivx(xdim,fpkloge2,aintx) ; (* Aintx gets (x-r)/loge2 which should
                                be exact.  *)
        fpintx(aintx,aintx) ; (* Just to make sure.  *)
        expapprox(fracx,r) ; (* r gets 2^fracx - 1 *)
        fpaddx(r,fpk1,r) ; (* r gets 2^fracx *)
        scalb(r,aintx,r) ; (* r gets 2^x *)
        end   (* Compute exp.  *)
else exp2(x,r) ; (* e^x = 2^x *)
blockpostlude( fpcb, trapcoming) ;
if trapcoming then postmonadic( 'expe',x,r) ;
z := r ;
end ;

procedure logapprox ( var x : fp_extended ; var z : fp_extended ) ;
        (* For sqrt(0.5)-1 <= x <= sqrt(2)-1, z := log2(1+x).  *)

        (* Computes approximation for normalized x.  *)
        (* Approx is of form
                   r := x/(2+x)
                   log := r*P(r^2)/Q(r^2)
                   *)
var r,r2,p,q : fp_extended ;

begin
if getexponent(x) <= -$4000 then fpdivx ( x, fpkloge2, z ) (* zero or denorm *) else
        begin
        fpaddx(x,fpk2,r) ; (* r gets 2+x *)
        fpdivx(x,r,r) ; (* r gets x/(2+x) *)
        fpmulx(r,r,r2) ; (* r2 gets r^2 *)
        p5eval(log12p,r2,p) ; (* p gets p(r2) *)
        fpmulx(p,r,p) ;
        q1eval(log12q,r2,q) ; (* q gets q(r2) *)
        setxcpn(underfl, false) ;
        setxcpn(inxact,  true ) ;
        fpdivx(p,q,z) ;
        end ;
end ;

procedure log2r ( var x, z : fp_extended ) ;
        (* Computes z := log2(x) for positive normal or NonNormal x *)
        { log2( 2^e * 0.f) is defined to be
                e - normcount + unnormalized(log12(normalized(0.f)-1))
          where the normalization and unnormalization are by equal amounts. }

var
xlog, xlog2, xlog3 : integer ;
scaledx, normalx, fracx, r, unzero : fp_extended ;

begin (* log2r *)
xlog := ilogb(x) ; (* x is normal non-zero or nonnormal non-zero *)
fpscalex ( x, -xlog, scaledx ) ;
        (* exponent(scaledx) = 0 ;  2^-63 <= scaledx < 2 *)
if (fpkindx(scaledx) = normal) and (getexponent(scaledx) = 0) then
        begin
        xlog2 := 0 ; (* Unnormalization count. *)
        fracx := scaledx ;
        end
        else
        begin
        fpnormalizex( scaledx, normalx ) ; (* -63 <= exponent(normalx) <= -1 *)
        xlog2 := getexponent( normalx ) ; (* -xlog2 is the unnormalization count *)
        fpscalex( normalx, -xlog2, fracx ) ;
        end ;
                (* Now 1 <= fracx < 2. *)
if fpcomx(fracx,fpksqrt2) = greater then
        begin
        fpscalex(fracx,-1,fracx) ;
        xlog3 := 1 ;
        end
        else
        xlog3 := 0 ;
                (* Now sqrt(.5) <= fracx <= sqrt(2). *)
fpsubx(fracx,fpk1,fracx) ; (* Subtract one for log(1+x) *)
logapprox(fracx,r) ; (* sqrt(.5)-1 <= fracx <= sqrt(2)-1 ; -.5 <= r <= +.5 *)
if xlog2 <> 0 then
        begin (* Unnormalize r *)
        unzer( r, unzero ) ; (* Create a NonNormal zero with sign and exponent of r.  *)
        fpscalex(unzero,-xlog2,unzero) ;
        fpaddx(r,unzero,r) ; (* Unnormalize log.  *)
        end   (* Unnormalize r *);
xmovefp(r) ;
wmovefp1( xlog + xlog2 + xlog3 ) ;
fpadd ;   (* Add in exponent part of logarithm.  *)
fpmovex(z) ;
end (* log2r *) ;

procedure log2 (* x : fp_extended ; var z : fp_extended *) ; (* z := log2(x) *)

var
fpcb : fp_blocktype ;
r : fp_extended ;
trapcoming : boolean ;

procedure logzero ;
begin (* logzero *)
        r := fpkinf ; (* log(0) = - inf *)
        fpnegx(r,r) ;
        setxcpn(div0,true ) ; (* Div0 is Kahan idea.  *)
end   (* logzero *) ;

begin
blockprelude(fpcb) ; (* Save environment, clear exceptions and traps.  *)
case fpkindx(x) of
zero : logzero ;

NonNormal, normal :
        begin
        if fpcomx(x,fpk0) = equal then logzero else
                if fpminusx(x) then errornan(nanlog,r) else
                        log2r(x,r) ;
        end ;

inf : if fpcomx(x,fpk0) = greater then r := x else errornan(nanlog,r) ;

nan : checknan(x,r) ;

end (* case *);
blockpostlude( fpcb, trapcoming) ;
if trapcoming then postmonadic( 'log2',x,r) ;
z := r ;
end ;

procedure log12r ( var x : fp_extended ; var z : fp_extended ) ;
        (* Computes z := log2(1+x) for normal and NonNormal x > -1.  *)
var
xplus : fp_extended ;
r, normx, unzerox : fp_extended ;

begin (* log12r *)
fpaddx(x,fpk1,xplus) ;
if (fpcomx(fpksqrthalf,xplus) = lesser) and
    (fpcomx(xplus,fpksqrt2) = lesser) then
        begin
        if fpkindx(x) = NonNormal then
                begin
                unzer( x, unzerox ) ; (* Create NonNormal zero
                    with sign and exponent of x.  *)
                fpnormalizex(x,normx) ;
                logapprox(normx,r) ;
                fpaddx(r,unzerox,r) ; (* Do unnormalize.  *)
                end
                else logapprox(x,r)
        end
        else
        log2r(xplus,r)  ;
z := r ;
end (* log12r *) ;

procedure log12 (* x : fp_extended ; var z : fp_extended *) ; (* z := log2(1+x) *)
var
fpcb : fp_blocktype ;
r : fp_extended ;
trapcoming : boolean ;

begin
blockprelude(fpcb) ; (* Save environment, clear exceptions and traps.  *)
case fpkindx(x) of
zero : begin
        r := x ; (* Log(1+0) = 0 *)
        end ;
NonNormal, normal : begin
        case fpcomx(x,fpkm1) of
        lesser : errornan(nanlog,r) ;
        equal : begin (* Log(0) *)
                r := fpkinf ; (* log(0) = - inf *)
                fpnegx(r,r) ;
                setxcpn(div0,true ) ; (* Div0 is Kahan idea.  *)
                end ;
        greater : log12r(x,r) ;
        end (* case *) ;
        end ;

inf : if fpcomx(x,fpk0) = greater then r := x else errornan(nanlog,r) ;

nan : checknan(x,r) ;

end (* case *);
blockpostlude( fpcb, trapcoming) ;
if trapcoming then postmonadic( 'log12',x,r) ;
z := r ;
end ;

procedure loge (* x : fp_extended ; var z : fp_extended *) ; (* z := loge(x) *)

var
fpcb : fp_blocktype ;
r : fp_extended ;
trapcoming : boolean ;

begin
blockprelude(fpcb) ; (* Save environment, clear exceptions and traps.  *)
log2(x,r) ;
if fpkindx(r) in [NonNormal, normal] then fpmulx(r,fpkloge2,r) ;
blockpostlude( fpcb, trapcoming) ;
if trapcoming then postmonadic( 'loge',x,r) ;
z := r ;
end ;

procedure log10 (* x : fp_extended ; var z : fp_extended *) ; (* z := log10(x) *)

var
fpcb : fp_blocktype ;
r : fp_extended ;
trapcoming : boolean ;

begin
blockprelude(fpcb) ; (* Save environment, clear exceptions and traps.  *)
log2(x,r) ;
if fpkindx(r) in [NonNormal, normal] then fpmulx(r,fpklog102,r) ;
blockpostlude( fpcb, trapcoming) ;
if trapcoming then postmonadic( 'log10',x,r) ;
z := r ;
end ;

procedure xexpy (* var x, y , z : fp_extended *) ;

        (* Computes z := x ** y for the case
                x must be normal or denormal and positive
                 y must be normal or denormal

                 it's just z := exp(y*log(x)) *)

var
logx, p : fp_extended ;

begin (* xexpy *)
        
        (*** Fix for Lisacalc Error # 248 ***)
if fpkindx(x) = zero then
        begin (* x = 0 *)
        if fpminusx(y) then
                begin (* negative y *)
                fpdivx( y, fpk0, z ) ; (* Compute result as -y/0. *)
                fpnegx( z, z) ;
                end   (* negative y *)
                else
                begin (* positive y *)
                fpdivx( fpk0, y, z ) ; (* Compute result as 0/y. *)
                end   (* positive y *) ;
        end   (* x = 0 *)
        else
        begin (* x <> 0 *)
        log2(x,logx) ; (* logx gets log2(abs(x)).  *)
        fpmulx(logx,y,p) ; (* p gets y * log2(abs(x)).  *)
        exp2(p,z) ; (* z gets abs(x)^y.  *)
        end   (* x <> 0 *) ;
        (*** Fix for Lisacalc Error # 248 ***)

end   (* xexpy *) ;

procedure xpwry (* var x : fp_extended ; y : integer ; var z : fp_extended *) ;

        (* Computes z := x ** y for the case
                ax must be normal, denormal, or finite
                 y must be integer

                 it's just z := x * x * x ... y times *)

var
k : integer ;
p, r : fp_extended ;

begin (* xpwry *)
k := abs(y) ;
p := x ;
while not odd(k) do
        begin (* Look for first 1-bit.  *)
        fpmulx(p,p,p) ;
        k := k div 2 ;
        end ;
r := p ;
k := k div 2 ;
while k > 0 do
        begin (* Multiply for each 1-bit.  *)
        fpmulx(p,p,p) ; (* Increment power of x.  *)
        if odd(k) then fpmulx(r,p,r) ;
        k := k div 2 ;
        end ;
if y < 0 then fpdivx(fpk1,r,z) (* Invert if negative power.  *) else z := r ;
end   (* xpwry *) ;

procedure xtoy (* x, y : fp_extended ; var z : fp_extended *) ;
        (* z := x^y computed by logarithms and exponentiation.  *)
var
r, t : fp_extended ;
fpcb : fp_blocktype ;
k : integer ;
es : excepset ;
oddy : boolean ;
trapcoming : boolean ;

procedure axtoy ; (* Sets r to abs(x)^y.  *)
var
ax : fp_extended ; k : integer ; es : excepset ;
begin (* axtoy *)
fpabsx(x,ax) ; (* ax gets abs(x).  *)
k := xmovew(y) ; (* k gets integer value of y.  *)
getexcepset(es) ;
if (fpkindx(y)=normal) and (k<>0) and (es=[]) and (abs(k)<256) then
        xpwry( ax, k, r)
        (* Use the multiplication method if y is normalized and is a small
        integer, except that if y is zero then we don't use the multiplication
        method because we want to catch 0^0 as an error.  *)
else begin (* y is not a small integer.  *)
        clrexcepset     ; (* Don't care about these exceptions.  *)
        xexpy( ax, y, r) ;
        end ;
end (* axtoy *) ;

begin (* xtoy *)
blockprelude(fpcb) ; (* Save old floating point environment and standardize.  *)

if (fpkindx(x) = nan) or (fpkindx(y) = nan) then choosenan(x,y,r)
else
if fpminusx(x) then
        begin (* Negative x only allowed for integer y:
                (-abs(x))^y = (-1)^y * abs(x)^y    *)
        fpintx(y,t) ; (* t gets integral value of y. *)
        getexcepset(es) ;
        if (es<>[]) or (fpkindx(y)=inf)   then
                begin
                clrexcepset ; (* Turn off any exceptions from int.  *)
                errornan(nanpower,r) (* Error if y not an integer.  *)
                end
        else
                begin (* y is an integer.  *)
                fpscalex(y,-1,t) ; (* To determine if y is even or odd.  *)
                fpintx(t,t) ; (* t gets int(y/2) *)
                oddy := getxcpn(inxact) ;
                setxcpn(inxact,false) ; (* Turn off if it was on.  *)
                axtoy ; (* r gets abs(x)^y.  *)
                if oddy   then fpnegx(r,r) ; (* Negate result if odd power.  *)
                end
        end
else axtoy ; (* For nonnegative x, r gets x^y.  *)

blockpostlude(fpcb, trapcoming) ;
if trapcoming then postdyadic( 'xtoy',x,y,r) ;
z := r ;
end (* xtoy *);

procedure compound (* r, p : fp_extended ; var z : fp_extended *) ;
        (* z := (1+r)^p  *)
var
fpcb : fp_blocktype ;
t : fp_extended ;
trapcoming : boolean ;

begin (* compound  *)
blockprelude(fpcb) ; (* Save old floating point environment and standardize.  *)

if (fpkindx(r) = nan) or (fpkindx(p) = nan) then choosenan(r,p,t)
else
case fpcomx( fpkm1, r) of
                lesser  : case fpcomx( fpk0, p) of
                            lesser, greater :
                                       if fpkindx(p) = inf then
                                       begin (* inf p *)
                                         (* Treat this case separately to
                                         avoid setting Inxact flag. *)
                                       if fpcomx(fpk0, r) = equal then
                                                errornan(nanfinan,t)
                                                else
                                       if fpminusx(r) = fpminusx(p)
                                               then t := fpkinf
                                               else t := fpk0 ;
                                        (* t := inf with sign
                                               exor with r *)
                                       end   (* inf p *)
                                       else
                                       begin (* finite p *)
                                       log12(r,t) ;
                                           (* t gets log2(1+r).  *)
                                       fpmulx(t,p,t) ;
                                           (* t gets p*log2(1+r).   *)
                                       exp2(t,t) ;
                                       end   (* finite p *) ;
                            equal: t := fpk1 ;  (* cif( r, 0 ) = 1 for any r *)
                            unord : errornan(nanfinan,t) ;
                            end (* case 0 com p *) ;
                greater, unord : errornan( nanfinan, t) ;
                        (* cif( projinf, p ) and cif( r < -1, p ) = nan *)
                equal   :
                        case fpcomx( fpk0, p) of
                                unord:  errornan(nanfinan,t) ;
                                        (* cif( r, projinf ) = nan *)
                                lesser : t := fpk0 ; (* cif( -1, +p ) = +0 *)
                                equal  : t := fpk1 ; (* cif(-1,0) = 1 *)
                                greater:
                                    begin
                                    t := fpkinf ;
                                    setxcpn(div0, true) ;
                                         (* Set divide by zero for cif(-1,-p) = +inf *)
                                    end ;
                                end (* case 0 com p *) ;
                end (* case -1 com r *) ;

blockpostlude(fpcb, trapcoming) ;
if trapcoming then postdyadic( 'compound',r,p,t) ;
z := t ;
end (* compound *);

procedure annuity (* r, p : fp_extended ; var z : fp_extended *) ;
        (* z := (1-(1+r)^-p )/r *)
var
rnorm, r1, t : fp_extended ;
fpcb : fp_blocktype ;
trapcoming : boolean ;

procedure usual ; (* Procedure for normalized or infinite operands.  *)
begin
if fpkindx(p) = inf then
        begin (* p = inf *)
        if fpminusx(r) = fpminusx(p) then fpdivx(fpk1, r, t)
                else t := p ; (* if p=inf then pv is 1/r or p depending on whether
                        signs agree *)
        end   (* p = inf *)
        else
        begin (* p finite *)
        log12(rnorm,t) ; (* t gets log2(1+rnorm).  *)
        fpmulx(t,p,t) ; (* t gets p*log2(1+rnorm).   *)
        fpnegx(t,t) ; (* t gets -p*log2(1+rnorm).  *)
        exp21(t,t) ;
        fpnegx(t,t) ; (* t gets 1-(1+rnorm)^-p.  *)
        fpdivx(t,rnorm,t) ; (* t gets divided by rnorm.  *)
        end   (* p finite *) ;
end (* usual *) ;

begin (* annuity  *)
blockprelude(fpcb) ; (* Save old floating point environment and standardize.  *)

if (fpkindx(r) = nan) or (fpkindx(p) = nan) then choosenan(r,p,t)
else
case fpcomx( fpkm1, r ) of
        lesser : case fpcomx( fpk0,p) of
                lesser, greater : case fpkindx(r) of
                        normal, nonnormal :
                                begin (* normal  *)
                                rnorm := r ;
                                usual ;
                                end   (* normal  *) ;
                        zero : t := p ; (* pv(0,p) = p *)
                        inf    :
                                if fpcomx( fpk0, p) = lesser then t := fpk0
                                        else case fpcomx( fpkm1, p) of
                                                lesser : copysign( fpkm1, fpk0, t) ;
                                                equal  : t := fpkm1 ;
                                                greater : copysign( fpkm1, fpkinf, t) ;
                                                end (* case -1 com p *) ;
                        end (* case kind(r) *) ;
                unord : errornan( nanfinan, t) ;
                equal : t := p ; (* pv(r,0) = p *)
                end (* case 0 com p *) ;
        greater, unord : errornan( nanfinan, t ) ;
        equal : case fpcomx( fpk0, p) of
                unord : errornan( nanfinan, t ) ; (* projective infinity *)
                equal : t := p ; (* pv(-1,0) = 0 *)
                greater : t := fpkm1 ; (* pv(-1,-p) = -1 *)
                lesser :
                        begin (* 0 < p *)
                        t := fpkinf ; (* pv(-1,+p) = +inf *)
                        setxcpn( div0, true ) ;
                        end   (* 0 < p *) ;
                end (* case 0 com p *) ;
        end (* case -1 com r *) ;

blockpostlude(fpcb, trapcoming) ;
if trapcoming then postdyadic( 'annuity',r,p,t) ;
z := t ;
end (* annuity *);


ÿ