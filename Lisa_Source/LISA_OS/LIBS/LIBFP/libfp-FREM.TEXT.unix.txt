        .title   'FREMAINDER SUBROUTINE'
        .proc    mc%FREM   ; 0,0
        .def     mc%rem_n
        .ref     mc%valid
;
;************************************************************************
;
;         DATE          REVISIONIST     REASON
;       --------        -----------     -----------
;       16.MAR.81       G.WALKER        ORIGINAL (CREATED FROM FDIV)
;       20.MAR.81       G.WALKER        ASSEMBLY ERRORS OUT(PUT IN SCCS)
;       20.MAR.81       G.WALKER        ADD XREF 'mc%valid', (V1.1)
;       23.MAR.81       G.WALKER        CORRECT 'WHILE' BY USING D4 (NOT D3)
;       24.MAR.81       G.WALKER        CORRECT SUBTRACT OF DIVIDEND CONFUSION
;                                          (LOTSA THEM ERRORS IN THIS)
;       24.MAR.81       G.WALKER        AT 'REMADJ' INCLUDE V-BIT IN DIVIDEND
;       24.MAR.81       G.WALKER        CORRECT STORE OF DIVIDEND INTO FPREG
;                                          (OFFSET OF 'SIGN', NOT 'EXP')
;       25.MAR.81       G.WALKER        MAKE ALL JSR'S PC RELATIVE
;       31.MAR.81       G.WALKER        FIX DECREMENT BY BYTE IN LOOP COUNTER
;        1.APR.81       G.WALKER        FIX MANTISSA CMP TO DO UNSIGNED BCC
;                                           AND THOSE 'IF'S AS LONG REG TESTS
;       20.APR.81       G.WALKER        DOCUMENT EXCEPTION BITS
;        7.JUL.81       D.CAWTHRON      CHANGED TO EXORMACS STRUCTURED
;                                           ASM SYNTAX
;       19.JUL.81       G.WALKER        OPTIMIZE FOR SPEED
;       26.AUG.81       G.WALKER        CORRECT TYPO MADE DURING OPTIMIZE STEP
;
;       16 Jan 83       Hough           Add entry to normalize only.
;                                       Fix remainder adjust.
;                                       Fix 17 bit exponent problem.
;
;************************************************************************
;
;               mc%frem  -  68K FLOATING POINT REMAINDER ROUTINE
;
;
;       ENTER WITH:    D0-D2    CONTAINS SOURCE_ARGUMENT
;       ==========              (DOES NOT NEED TO BE PROPERLY TYPED).
;
;                      A3       POINTS TO FP REGISTER BLOCK (FPRB)
;
;                      A2       POINTS TO DESTINATION_FP_REG IN FPRB
;                               CONTAINING DESTINATION_ARGUMENT
;
;
;
;
;
;
;             31              15              0
;             |---------------|---------------|
;        D0   | S |   |  TAG  |  EXPONENT     | <--+
;             |---------------!---------------|    |
;        D1   | MANTISSA MSW  | MANTISSA NMSW |    |-  SOURCE_ARGUMENT
;             |---------------|---------------|    |
;        D2   | MANTISSA NLSW | MANTISSA LSW  | <--+
;             |---------------|---------------|
;        D3   |           $00000000           |
;             |---------------|---------------|
;
;
 PAGE
;
;
;             31              15              0
;             |               |               |   LO MEMORY
;             |---------------|---------------|
;             |                               |
;             |---------------|---------------|
;      A2 --> | S |   |  TAG  |   EXPONENT    | <--+
;             |---------------|---------------|    |
;             | MANTISSA MSW  | MANTISSA NMSW |    |- DESTINATION_FP_REG
;             |---------------|---------------|    |
;             | MANTISSA NLSW | MANTISSA LSW  | <--+
;             |---------------|---------------|
;             |                               |
;             |---------------|---------------|
;             |               |               |   HI MEMORY
;
;
;
;
;
;
;
;
;       EXIT WITH:     D0-D2    CONTAINS (EXACT) RESULT WRITTEN OVER
;       =========               SOURCE_ARGUMENT.  REG. D3 IS TRASH.
;                               (RESULT IS NOT TYPED - TAG FIELD IS TRASH).
;
;                      A0       DESTROYED
;
;                      D3-D7    DESTROYED
;
;                      A1-A7    UNCHANGED
;
;
;
;       EXCEPTION BITS:
;               BIT NAME        SET/NOT SET     CONDITIONS
;               --------        -----------     ----------------
;               OVERFLOW        SET BY mc%valid    RESULT OVERFLOWS
;               UNDERFLOW       SET BY mc%valid    RESULT UNDERFLOWS
;               INEXACT         NOT SET         RESUTL IS ALWAYS EXACT
;
;       SUBROUTINES CALLED:   none  
;       ==================
;
;*******************************************************************************
  PAGE
;
;
;
;
;*******************************************************************************
;
;
;               OVERVIEW  OF  PROCEDURE  FOR  mc%frem
;               -----------------------------------
;
;       DIVIDES DESTINATION_ARG BY SOURCE_ARG AND RETURNS THE REMAINDER.
;       FIGURATIVELY THIS IS:
;
;                          DIVIDEND        FP_REG        DESTINATION_ARG
;            REMAINDER =   --------   =   --------   =   ---------------
;                           DIVISOR         <EA>           SOURCE_ARG
;
;       AS SHOWN ABOVE, THE FP_REG OPERAND IS ALWAYS THE DIVIDEND AND THE <EA>
;       OPERAND IS ALWAYS THE DIVISOR.  REMAIN WILL BE CALLED ONLY IN THOSE
;       CASES WHERE THE DIVISOR IS A NORMALIZED NUMBER AND THE DIVIDEND IS AN
;       UNNORMALIZED OR NORMALIZED NUMBER AS SHOWN BELOW.
;
;                          DESTINATION_ARG        NORM. #          UNNORM. #
;            REMAINDER =   ---------------   =   ---------   OR   -----------
;                            SOURCE_ARG           NORM. #           NORM. #
;
;
;          mc%frem OPERATES AS A STANDARD BINARY DIVISION LOOP WHICH
;       GENERATES (AND DISCARDS) ALL INTEGER QUOTIENT BITS. AS EACH
;       QUOTIENT BIT IS GENERATED, THE DIVIDEND AT THAT POINT (MODIFIED
;       BY SHIFTING AND SUBTRACTION OF THE DIVISOR) IS A PARTIAL
;       REMAINDER. AFTER ALL INTEGER QUOTIENT BITS HAVE BEEN GENERATED,
;       THE DIVIDEND THAT IS LEFT IS THE ACTUAL REMAINDER, REPRESENTING
;       THE FRACTIONAL PART OF THE QUOTIENT.
;           THIS "RAW" REMAINDER WILL FALL IN THE RANGE:
;               0 <= RAW REMAINDER < DIVISOR.
;       THE IEEE REMAINDER ALGORITHM REQUIRES ONE FINAL ADJUSTMENT STEP:
;       IF THE RAW REMAINDER IS GREATER THAN 1/2 THE DIVISOR, THE ACTUAL
;       REMAINDER = RAW REMAINDER - DIVISOR. THIS ADJUSTMENT CAUSES THE
;       REMAINDER TO FALL IN THE RANGE:
;               -(1/2)* DIVISOR <= REMAINDER <= (1/2)* DIVISOR.
;
;           NOTE THAT THE REMAINDER IS EXACT (LOOK, MA, NO ROUNDING!) AND
;       CAN RAISE NO OVERFLOW OR UNDERFLOW EXCEPTIONS.
;
 PAGE
;*******************************************************************************
;
;       THE FOLLOWING FIGURES ILLUSTRATE THESE REGISTER ASSIGNMENTS:
;
;       DATA REGISTER USAGES FOR mc%frem
;       ==============================
;
;             31              15      7       0
;             |--+------------+-------+-------|
;        D0   |SD|            |      EXPD     |  SIGN AND EXPONENT OF DIVIDEND
;             |--+------------+-------+-------|
;        D1   |     MSW            NMSW       | <-+
;             |---------------+---------------|   |- DIVISOR MANTISSA
;        D2   |     NLSW            LSW       | <-+
;             |---------------+-------+-------|
;        D3   |               |       |   0   |  LAST QUOTIENT BIT GENERATED
;             |---------------+-------+-------|
;        D4   |               |       |   *   |  BITCNT (DIFFERENCE IN EXP'S)
;             |---------------+-------+-------|
;        D5   |               |       |   0   |  VBIT (MSBIT OF DIVIDEND)
;             |---------------+-------+-------|
;        D6   |     MSW            NMSW       | <-+
;             |---------------+---------------|   |- DIVIDEND MANTISSA
;        D7   |     NLSW            LSW       | <-+
;             |---------------+---------------|
;
;
;       ADDRESS REGISTER USAGES FOR mc%frem
;       =================================
;
;             31              15              0
;             |---------------+---------------|
;        A0   | S |   |  TAG  |     EXP       |  SAVED DIVISOR S-TAG-EXP
;             |---------------+---------------|
;        A1   |                               |
;             |---------------+---------------|
;        A2   |       POINTER TO FP_REG       |  POINTER TO DESTINATION REG
;             |---------------+---------------|
;
;
 PAGE
;*******************************************************************************
;
;       ALGORITHM FOR REMAIN
;
;       INITIALIZE REG. D4 (VBIT) TO 0.
;
;       SUCCESSIVELY CALCULATE QUOTIENT BITS STARTING WITH MSB AS FOLLOWS:
;
;       BITCNT = DIVIDEND_EXP - DIVISOR_EXP + 1;
;       WHILE BITCNT >0 DO BEGIN
;          IF  (VBIT) = 0  THEN
;             IF  (DIVISOR) = (DIVIDEND)  THEN
;                SET THE Q-BIT
;                REMAINDER WILL BE 0, SO QUOTIENT IS NOW EXACT.
;                GO TO *ADJUST*
;             ENDIF
;
;             BCS  *SHIFT*          CARRY IS SET IF DIVISOR > DIVIDEND
;
;          ENDIF
;
;         *ADJUST*
;
;             SET THE Q-BIT
;             DIVIDEND = DIVIDEND - DIVISOR
;
;          *SHIFT*
;
;             SHIFT DIVIDEND LEFT BY ONE BIT.
;
;             IF THERE IS A CARRY OUT OF DIVIDEND, SET VBIT
;
;             DECREMENT EXPONENT OF DIVIDEND, TO KEEP NUMERIC VALUE
;               THE SAME AFTER THE LEFT SHIFT.
;
;             DECREMENT 'BITCNT'
;
;             END                       LOOP UNTIL BITCNT = 0
;
;       (*      DIVIDEND IS NOW THE RAW REMAINDER.      *)
;
;       IF  REMAINDER > (DIVISOR/2) THEN BEGIN
;          REMAINDER = REMAINDER - DIVISOR;       NOTE: RESULT MAY BE NEGATIVE
;          END
;
;       NORMALIZE REMAINDER (IF MANTISSA IS ZERO, SET RESULT TO NORMAL ZERO)
;
;       RETURN
;
;*******************************************************************************
;
        debugbegin
;
        MOVE.L   D0,A0                ; SAVE DIVISOR S-TAG-EXP IN A0
        MOVEM.L  (A2),D0/D6/D7        ; MOVE DIVIDEND S-EXP INTO REG D4
;                                           AND DIVIDEND MTISSA INTO REGS D6
;                                           AND D7
;
        MOVE.B   #0,D3                ; ZERO OUT Q-BIT AND V-BIT
        MOVE.B   #0,D5
        cmp     a0,d0                   ; Compare exponents of A and B.
        blt.s   remadj                  ; Branch if exp(A) < exp(B) - no action
                                        ; required.
                                        ; This test guarantees that
                                        ; exp(A)-exp(B) >= 0 so that 17 bit exponent
                                        ; will be no problem.
;
        MOVE.W   D0,D4                ; CREATE COUNT OF INTEGER QUOTIENT
        SUB.W    A0,D4                    ; BITS IN D4.W
        ADDQ.W   #1,D4
;
;
;      THE VARIOUS REGISTERS ARE NOW CONFIGURED AS DESCRIBED IN THE ALGORITHM
;      ABOVE.
;
;
lab1                                     ; WHILE LOOP COUNT (IN D4) NON-ZERO
        beq     remadj
           TST.B    D5
           bne  @3                       ; IF 'VBIT' = 0 THEN
              cmp.l     d1,d6
              bne       @2
                 cmp.l  d2,d7               ; IF DIVISOR = DIVIDEND THEN
                 bne    @2
                    moveq    #1,D3                 ; RESULT IS EXACT:
                    SUB.L    D2,D7                 ; SET THE Q-BIT, ZERO OUT
                    SUBX.L   D1,D6                 ; THE REMAINDER,
                    BRA      NOADJ                 ; AND QUIT
@2                                          ; ENDIF
;
              bcc       @3                  ; IF DIVSR > DIVND
                 moveq   #0,D3                      ; Q-BIT IS ZERO
                 BRA     REMSHIFT                   ; AND SKIP SUBTRACT.
@3                                          ; ENDIF
;
;         GET HERE ONLY WHEN DIVISOR LESS THAN DIVIDEND.  THIS CAN
;         OCCUR EITHER BECAUSE 'VBIT' IS SET OR BECAUSE DIVISOR WAS
;         LESS THAN DIVIDEND IN THE ABOVE UNSIGNED COMPARE.  SET
;         THE Q-BIT AND SUBTRACT DIVISOR FROM DIVIDEND.
;         NOTE THAT 'VBIT' IS NOT INVOLVED IN THIS SUBTRAC-
;         TION SINCE THE ALGORITHM INSURES THAT WHEN 'VBIT' IS SET,
;         IT WILL ALWAYS BE 'BORROWED' IN THE SUBTRACTION.
;
           moveq    #1,D3                    ; SET Q-BIT
           SUB.L    D2,D7                    ; SUBTRACT DIVISOR FROM DIVIDEND
           SUBX.L   D1,D6
;
;         SHIFT DIVIDEND LEFT BY ONE BIT
;
REMSHIFT   ADD.L    D7,D7                    ; SHIFT LEFT ONE BIT
           ROXL.L   #1,D6
           SCS      D5                       ; SAVE CARRY OUT IN 'VBIT'
           SUBQ.W   #1,D0                    ; DECREMENT EXPD FOR LEFT SHIFT
;
           SUBQ.W   #1,D4                    ; DECREMENT 'BITCNT'
           bra      lab1              ; REPEAT LOOP UNTIL 'BITCNT' Q-BITS
;                                           HAVE BEEN CALCULATED
;
REMADJ     EQU          *
        LSR.L   #1,D5            ; PUT V-BIT IN CARRY
        bcc     @4
          ROXR.L        #1,D6 ; DENORM V-BIT BACK INTO DIVIDEND
          ROXR.L        #1,D7
          ADDQ.W        #1,D0
@4
        SUB.W           #1,A0         ; CREATE 1/2 DIVISOR
        cmp.w   a0,d0
        bge     @5
          BRA  NOADJ
@5
        bne     @8
          cmp.l d1,d6                  ; (IF D6 < D1 (UNSIGNED)
          bcc   @6
            BRA  NOADJ                  ; DO NOT ADJUST
@6
          bne   @8                     ; (IF D6 = D1 THEN
            cmp.l       d2,d7            ; (IF D7 < D2 (UNSIGNED)
            bcc         @7
              BRA  NOADJ                 ; DO NOT ADJUST
@7
            bne         @8              ; (ELSE BREAK TIE WITH QUOTIENT BIT)
              cmp.b     #0,d3               ; REMAINDER=1/2 DIVISOR,SO
              bne       @8
                BRA  NOADJ                  ; BREAK TIE WITH Q-BIT
@8
;
;       REMAINDER > DIVISOR/2, SO SUBTRACT DIVISOR FROM REMAINDER.
;
        cmp     a0,d0
        bne.s   dosub           ; a0 = exp(div/2) is either equal to or one less
                                ; than d0 = exp(rem).  If equal then left shift div
                                ; by one to align for subtract.
        lsl.l   #1,d2           ; Double significand of divisor.
        roxl.l  #1,d1
dosub
        sub.l   d7,d2           ; D1,D2 := div - rem.
        subx.l  d6,d1
        bchg    #31,d0          ; SET SIGN OF DIVISOR TO OPPOSITE OF REMAINDER SIGN.
        BRA             REMNORM
;
;
NOADJ   EQU     *             ; MOVE DIVIDEND TO REMAINDER POSITION.
        MOVE.L          D6,D1
        MOVE.L          D7,D2
;
REMNORM EQU    *             ; NORMALIZE REMAINDER.
        MOVE.L  D1,D3
        OR.L    D2,D3
        bne     @9                        ; IF MANTISSA IS ZERO, SET EXPONENT
          MOVE.W        #$C000,D0         ; TO NORMAL ZERO.
        bra     @11
@9
          TST.L     D1
@10                                      ; SHIFT MANTISSA LEFT
          blt   @11
            LSL.L         #1,D2          ; AND DECREMENT EXPONENT
            ROXL.L        #1,D1          ; UNTIL MSBIT IS NON-ZERO.
            SUBQ.W        #1,D0
            TST.L         D1
            bra           @10
@11
;
;       NO GUARD, ROUND, OR STICKY BITS EXIST, SO CLEAR D3 AND
;    GO CHECK FOR EXCEPTIONS.
;
      moveq     #0,D3         ; CLEAR G-R-S
      Jmp       mc%valid         ; CHECK FOR EXCEPTIONS
        debugend 'MC%FREM',1

mc%rem_n
        debugbegin
        movem.l (a2),d0-d2      ; Move the unnormalized operand to result area.
        bra     remnorm
        debugend 'MC%REM_N',0

ÿ