{*
** 01SEP82: Written by Jerome Coonen.
** 18JAN83: INFs as ++++/---- and NANs with hexits. (JTC)
** 20 May 83 : (DGH) Invalid set on bad input.
*}

{*
** Format an input decimal record, according to input format
** structure, and write to string.  See SANE Pascal interface
** document for an explanation of the data types.
*}
procedure Dec2Str{(var f: DecForm; var d: Decimal; var s: DecStr)};

var
    i      : integer;
    ds, es : Decstr;



    {*
    ** Convert an integer to a signed decimal string, for printing
    ** exponents.
    *}
    procedure Int2Str(ie: integer; var is: DecStr);
    var
        i: integer;
        s1 : string[1];

    begin
        i := ie;    {make integer local}
        if i < 0 then
            i := -i;
        s1 := '1';

        s1[1] := chr(ord('0') + (i MOD 10));
        is := s1;
        i := i DIV 10;
        while i <> 0 do
        begin
            s1[1] := chr(ord('0') + (i MOD 10));
            insert(s1, is, 1);
            i := i DIV 10
        end;

        if ie < 0 then is := concat('-', is)
    end;



begin  { Dec2Str }
    {*
    ** Cases 0, INFINITY, and NAN are treated separately.
    ** In any case, a lead sign or space is printed.
    *}
    if d.sgn <> 0 then
        s := '-'
    else
        s := ' ';

    {*
    ** If it is a nonzero, finite number, place a decimal
    ** point after the lead digit and print the exponent field.
    ** Exponent must be adjusted to align point after lead digit.
    ** The special cases are handled trivially.
    *}

    case d.sig[1] of

    '0':
        s := concat(s, '0.0');

    'I':
        if d.sgn <> 0 then
            s := '----'
        else
            s := '++++';

    'N':
        {*
        ** Insert the hexits from d.sig and insert a colon
        ** after the leading four.
        *}
        begin
            { d.sig has form Nzzz...z with up to 16 hexits }
            s := concat(s, 'NaN''', copy(d.sig, 2, (length(d.sig) - 1)), '''');
            { s has form *NAN'zzz...z' where * is - or blank }

            { delete trailing 0's, up to lead 9 chars: *NAN'zzzz }
            i := length(s) - 1;  { ignore the ' ending s }
            while (s[i] = '0') and (i > 9) do
                i := i - 1;
            delete(s, (i + 1), (length(s) - i - 1));

            { add a : if any more than 4 hexits left: *NAN'zzzz???'  }
            if length(s) > 10 then
                insert(':', s, 10);

            { delete up to 3 leading 0's }
            i := 6;
            while (s[i] = '0') and (i < 9) do
                i := i + 1;
            delete(s, 6, (i - 6))
        end;

    '?':
        s := concat(s, '???.???');

    otherwise
        if f.style = FloatDecimal then
        begin
            ds := copy(d.sig, 1, f.digits);
            i := d.exp + f.digits - 1;
            insert('.', ds, 2);
            Int2Str(i, es);
            s := concat(s, ds, 'E', es)
        end
        else
        begin
            {*
            ** If it is a finite number, place a decimal
            ** point after the lead digit and print the exponent field.
            ** Exponent must be adjusted to align point after lead digit.
            ** Three cases are handled, respectively:
            **      XXXXXXXXX000..000.  (may be no zeros left of '.')
            **      XXXXX.XXXXX
            **      0.0000...000XXXXXXX (may be no zeros right of '.')
            *}
            if d.exp >= 0 then
            begin
                s := concat(s, d.sig);
                for i := 1 to d.exp do
                    s := concat(s, '0');
                s := concat(s, '.')
            end
            else if (-d.exp) < length(d.sig) then
            begin
                insert('.', d.sig, (length(d.sig) + d.exp + 1));
                s := concat(s, d.sig)
            end
            else
            begin
                s := concat(s, '0.');
                for i := 1 to -(length(d.sig) + d.exp) do
                    s := concat(s, '0');
                s := concat(s, d.sig)
            end
        end
    end
end;




{*
** Receive characters from "input stream" via character
** function NextC, which returns chr(0) = NULL when the
** string is exhausted.
**
** The value the decimal structure represents is as
** follows (^ means exponentiation):
**
**      (-1)^sgn * 10^exp * sig
**
** The implied decimal point in the sig string is at the
** right-hand side.  Infinities are read as strings of
** sign characters, + or -.  NANs are strings of the form
**      NAN'xxxx:yyy...y'
** If there is a y-field, then the x-field is 0-padded on
** the left to width 4.  The colon is dropped from the string
** in d.sig.
*}
procedure strtodec(s: DecStr; var d: Decimal);
var
    ExpAdj, ExpMul, ExpMag, SigCnt, StrIndex, i : integer;
    nc : char;          {input char buffer}
    Guard : boolean;    {for lost sig- and exp-digits}
    ss : string[1];     {for character concatenations}


    {*
    ** Get the next input character and increment string
    ** index.  Return NULL if string exhausted.
    *}
    function NextC: char;
    begin
        if StrIndex <= length(s) then
        begin
            NextC := s[StrIndex];
            StrIndex := StrIndex + 1
        end
        else
            NextC := chr(0)
    end;


begin
    d.sgn  := 0;        {assume positive}
    d.exp  := 0;        {default exponent}
    d.sig  := '';       {default digit string is empty}
    Guard  := false;    {nothing lost yet}

    ExpAdj := 0;        {loc of '.', relative to right digit}
    ExpMul := 1;        {+1 or -1, depending on sign of exp}
    ExpMag := 0;        {magnitude of exponent}

    SigCnt := 0;        {number of digits seen so far}

    ss     := '0';      {dummy 1-elt string}

    StrIndex    := 1;        {beginning of string}

    nc     := NextC;    {get first character}

    {*
    ** Skip leading blanks and tabs (ASCII 9).
    *}
    while (nc = ' ') or (nc = chr(9)) do
        nc := NextC;

    {*
    ** Process sign, if any.
    *}
    if nc = '+' then
        nc := NextC
    else if nc = '-' then
    begin
        d.sgn := 1;  {mark negative}
        nc := NextC
    end;

    {*
    ** Check for infinity, repeated sign.
    *}
    if ((nc = '+') and (d.sgn = 0)) or ((nc = '-') and (d.sgn = 1)) then
        d.sig := 'I'

    {*
    ** Check for 'NAN''...''' or 'nan''...''', with a
    ** hex significant digit field.
    *}
    else if (nc = 'N') or (nc = 'n') then
    begin
        { check whether more than following AN'' }
        if StrIndex > (length(s) - 4) then
            { if not, default to 0, to be replaced in Dec2Z }
            d.sig := 'N0000'
        else
        begin
            { copy up to 17 chars XXXX:YYYYYY... part to sig, after N }
            i := length(s) - StrIndex - 3;
            if i > 17 then
                i := 17;
            d.sig := concat('N', copy(s, (StrIndex + 3), i));
            { if there's a colon, force 4 lead digits }
            i := pos(':', d.sig);
            if i > 0 then
            begin
                delete(d.sig, i, 1);
                while i < 6 do
                begin
                    insert('0', d.sig, 2);
                    i := i + 1
                end
            end
        end
    end

    {*
    ** At last, it might be a number.  Skip over leading zeros,
    ** accept significant digits and discard extras with rounding
    ** information saved in a system-dependent way.
    ** MAC: take SIGDIGLEN-1 digits and append a '1' of nonzero
    **          digits are lost.
    ** III: take SIGDIGLEN digits and increment last (even if 9)
    **          if nonzero digits are lost.
    ** Take care that discarded digits have appropriate effect on
    ** exponent.
    *}
    else
    begin

        {*
        ** If lead 0's are skipped, mark number tentatively as 0
        *}
        while nc = '0' do
        begin
            nc := NextC;
            d.sig := '0'
        end;

        {*
        ** Beyond the leading 0's there may be: an honest
        ** digit 1-9; a decimal point, possibly followed by
        ** more 0's; or an exponent field, in which case the
        ** result is zero.
        *}
        if (ord('1') <= ord(nc)) and (ord(nc) <= ord('9')) then
        begin
            SigCnt := 1;        {first sig digit}
            d.sig := '0';       {make slot for nc}
            d.sig[1] := nc;     {stuff nc}
            nc := NextC;

            while (ord('0') <= ord(nc)) and (ord(nc) <= ord('9')) do
            begin
                {*
                ** MAC vs III difference in count:
                **      MAC -- SIGDIGLEN-1
                **      III -- SIGDIGLEN
                *}
                if (SigCnt < (SIGDIGLEN-1)) then
                begin
                    SigCnt := SigCnt + 1;
                    ss[1] := nc;
                    d.sig := concat(d.sig, ss)
                end
                else
                begin
                    ExpAdj := ExpAdj + 1;
                    Guard := Guard or (nc <> '0')
                end;
                nc := NextC
            end;

            {*
            ** Now the fraction part of the field may begin.
            *}
            if nc = '.' then
            begin
                nc := NextC;

                while (ord('0') <= ord(nc)) and (ord(nc) <= ord('9')) do
                begin
                    if (SigCnt < (SIGDIGLEN-1)) then
                    begin
                        SigCnt := SigCnt + 1;
                        ExpAdj := ExpAdj - 1;
                        ss[1] := nc;
                        d.sig := concat(d.sig, ss)
                    end
                    else
                        Guard := Guard or (nc <> '0');
                    nc := NextC
                end
            end
        end

        {*
        ** In this case, we are still bypassing leading 0's
        ** when the decimal point is found.
        *}
        else if nc = '.' then
        begin
            nc := NextC;        {get following digit}

            while nc = '0' do
            begin
                ExpAdj := ExpAdj - 1;
                d.sig  := '0';
                nc := NextC
            end;

            if (ord('1') <= ord(nc)) and (ord(nc) <= ord('9')) then
                d.sig := '';    {kill any zero string already found}

            while (ord('0') <= ord(nc)) and (ord(nc) <= ord('9')) do
            begin
                if SigCnt < (SIGDIGLEN-1) then
                begin
                    ExpAdj := ExpAdj - 1;
                    SigCnt := SigCnt + 1;
                    ss[1] := nc;
                    d.sig := concat(d.sig, ss)
                end
                else
                    Guard := Guard or (nc <> '0');
                nc := NextC
            end
        end;

        {*
        ** If no legitimate digits, force NAN and go through
        ** the motions to get an exponent.  Use #INVD2B = 12
        ** implicitly.
        *}
        if length(d.sig) = 0 then
            begin
            d.sig := 'N12';
            setxcp( invalid, true ) ;
            end ;
            
        {*
        ** Strip trailing zeros, if no digits lost.
        ** Be careful not to delete all of zero string.
        *}
        if Guard then
        begin
            {*
            ** MAC: append Guard digit
            ** III: 'add' Guard into last digit
            *}
            ss[1] := '1';
            d.sig := concat(d.sig, ss)
        end
        else
            while (length(d.sig) > 1) and
                (d.sig[length(d.sig)] = '0') do
            begin
                ExpAdj := ExpAdj + 1;
                delete(d.sig, length(d.sig), 1)
            end;


        {*
        ** Now scan for an exponent field.
        *}
        if (nc = 'E') or (nc = 'e') then
        begin
            ExpMul := 1;        {assume positive exp}
            ExpMag := 0;

            nc := NextC;

            if (nc = '+') or (nc = '-') then
            begin
                if nc = '-' then
                    ExpMul := -1;
                nc := NextC
            end;

            {*
            ** Use Guard flag to catch outrageous exponents,
            ** which are forced to 5000, a value guaranteed to
            ** cause over/underflow, depending on ExpMul.
            *}
            Guard := false;
            while (ord('0') <= ord(nc)) and (ord(nc) <= ord('9')) do
            begin
                Guard := Guard or (ExpMag >= 500);
                ExpMag := (ExpMag * 10) + (ord(nc) - ord('0'));
                nc := NextC
            end;
            if Guard then
                ExpMag := 5000
        end;

        {*
        ** Finish up number case by storing exponent, assuming
        ** implicit decimal point to the right of the digit
        ** string.
        *}
        d.exp := (ExpMul * ExpMag) + ExpAdj;
    end
end;

ÿ