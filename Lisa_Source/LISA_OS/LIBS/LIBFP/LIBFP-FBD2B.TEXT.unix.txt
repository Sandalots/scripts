;-----------------------------------------------------------
; 12JUL82: WRITTEN BY JEROME COONEN
; 12AUG82: TIDIED (JTC)
; 01SEP82: ENCODING OF RNDXXX CHANGED (JTC)
; 04SEP82: PASCAL ENUM TYPES USE HI-ORDER BYTE OF HOST WORD,
;       SO FLOAT/FIXED, DECFORM.SGN FIELDS CHANGED TO BYTE.
; 18JAN82: NANS HAVE FORM NXXXX (JTC)
; 09JUN83: CHANGE REG CONVENTIONS TO PRESERVE A5,A6. (JTC)
;
; DECIMAL->BINARY CONVERSION.
; INPUT: POINTER TO STRUCTURE WITH SGN, EXP, DGS -- THE
;       FIRST TWO ARE WORDS, THE LAST A STRING.
; OUTPUT: POINTER TO FP CELL OF TYPE GIVEN IN OPCODE.
;
; ASSUME STARTUP REGISTER MASK:
;       OPWORD -- D6.LO
;       STATE PTR -- A0
;       DST PTR -- A1
;       SRC PTR -- A2
;
; INPUT ASSUMPTIONS:
;       SIGN ALWAYS MEANINGFUL
;       EXP MEANINGFUL UNLESS LEAD "DIG" IS 0,I,N
;       DIGITS:
;               LEAD 0 --> ZERO
;               LEAD I --> INFINITY
;               LEAD N --> NAN WITH HEX NIBBLES
;               1-9    --> NUMBER <= 20 DIGITS, LAST
;                          FOR ROUNDING
;
; FOR NOW, AN INPUT NAN IS SET TO AN INNOCUOUS QUIET
; NAN WITH CODE FROM APPLE 3 PASCAL.
;-----------------------------------------------------------

D2B:

;-----------------------------------------------------------
; FIRST ARRANGE INPUT ARGUMENTS IN REG FILE.  MUST SAVE
; OPWORD IN D6 FOR STORE OF RESULT AT LAST STEP.
;-----------------------------------------------------------
        SUBA.W  #20,SP          ; 20-BYTE STACK FRAME
        MOVEA.L SP,A3           ; FRAME PTR
        LEA     4(A2),A4        ; PTR TO STRING
        MOVE.W  2(A2),D3        ; EXP
        SWAP    D6              ; SAVE OPWORD TILL LATER

;-----------------------------------------------------------
; CLEAR OUT DIGIT ACCUMULATOR AND INITIALIZE COUNTERS.
;-----------------------------------------------------------
        CLR.L   D4              ; DIGIT BUFFER
        MOVE.L  D4,D5
        MOVE.L  D4,D7           ; LO BYTE IS 'LOST' FLAG

        MOVEQ   #19,D2          ; MAX DIGIT COUNTER

        MOVE.B  (A4)+,D6        ; DIGIT STRING LENGTH COUNT
        BEQ.S   DBZSTO          ; ZERO LENGTH --> 0.0

;-----------------------------------------------------------
; GET FIRST CHARACTER BUT DON'T AUTOINCREMENT.
;-----------------------------------------------------------
        MOVE.B  (A4),D0         ; FIRST CHAR

;-----------------------------------------------------------
; CHECK FOR 'I' -- INFINITY.
;-----------------------------------------------------------
        CMPI.B  #$49,D0         ; IS IT 'I'?
        BEQ.S   DBNFIN

;-----------------------------------------------------------
; CHECK FOR 'N', IF SO GET HEXITS FOR SIGNIFICAND.  IF THERE
; ARE FEWER THAN THREE, FORCE LEAD ZEROS.
;-----------------------------------------------------------
        CMPI.B  #'N',D0         ; ALLOW ONLY CAPITAL N
        BNE.S   DBZER

        MOVE.B  -1(A4),D2       ; CHARACTER COUNT
        ADDQ.L  #1,A4           ; POINT TO FIRST HEXIT
        SUBQ.B  #1,D2           ; DON'T COUNT 'N'

        MOVEQ   #8,D0           ; ASSUME 8 DIGITS
        CMPI.B  #4,D2           ; OK IF AT LEAST 4
        BGE.S   @31
        SUBQ.B  #4,D0           ; FOUR 0'S AND WHAT'S THERE
        ADD.B   D2,D0
@31:
        BSR.S   @35
        MOVE.L  D5,D4
        CLR.L   D5
        MOVEQ   #8,D0
        BSR.S   @35
        BRA.S   @39

;-----------------------------------------------------------
; ROUTINE TO GET D0 DIGITS TO D5, UP TO COUNT IN D2
;-----------------------------------------------------------
@35:
        ROL.L   #4,D5           ; ALIGN BITS SO FAR
        SUBQ.B  #1,D2           ; DEC STRING COUNT
        BMI.S   @37

        MOVE.B  (A4)+,D1
        CMPI.B  #'9',D1
        BLE.S   @36
        ADDI.B  #9,D1           ; TRUE NIBBLE VALUE
@36:
        ANDI.B  #$0F,D1         ; NIBBLE MASK
        OR.B    D1,D5
@37:
        SUBQ.W  #1,D0
        BNE.S   @35
        RTS

;-----------------------------------------------------------
; CLEAR IRRELEVANT LEAD BIT AND TEST FOR ANYTHING NONZERO.
;-----------------------------------------------------------
@39:
        ANDI.L  #$7FFFFFFF,D4
        BNE.S   DBNFIN
        MOVEQ   #nanzero,D4
        SWAP    D4              ; ALIGN LEAD BITS
DBNFIN:
        MOVE.W  #$7FFF,D0       ; STORE HUGE EXP
        BRA.S   DBSSTO

;-----------------------------------------------------------
; GET HERE IF ALL DIGITS ZERO: FORCE SIGNED 0 AND STORE
;-----------------------------------------------------------
DBZER:
        CMPI.B  #$30,D0         ; IS IT '0'?
        BNE.S   SIGDIGS
DBZSTO:
        CLR.L   D0
DBSSTO:
;-----------------------------------------------------------
; DECIMAL.SGN ENUM TYPE TEST USES HI BYTE ONLY
;-----------------------------------------------------------
        TST.B   (A2)            ; CHECK OPERAND SIGN
        BEQ.S   @1
        BSET    #15,D0
@1:
        BSR     TOA3
        BRA     DBSTORE

;-----------------------------------------------------------
; PROCEDURE:
; MULTIPLY D4,5 BY 10, USING D0,1 AS BUFFER FOR (*2)
;-----------------------------------------------------------
DIG10:
        ADD.L   D5,D5           ; DIG * 2
        ADDX.L  D4,D4
        MOVE.L  D5,D1
        MOVE.L  D4,D0
        ADD.L   D5,D5           ; DIG * 4
        ADDX.L  D4,D4
        ADD.L   D5,D5
        ADDX.L  D4,D4           ; DIG * 8
        ADD.L   D1,D5           ; DIG * (8 + 2)
        ADDX.L  D0,D4
        RTS

;-----------------------------------------------------------
; LOOP TO GET SIGNIFICANT DIGITS.
;-----------------------------------------------------------
SIGDIGS:
        BSR.S   DIG10           ; CURRENT * 10
        MOVEQ   #$0F,D0         ; NIBBLE MASK
        AND.B   (A4)+,D0        ; NEXT DIGIT
        ADD.L   D0,D5           ; ADD IT IN
        CLR.W   D0              ; TO PROPAGATE X
        ADDX.L  D0,D4

        SUBQ.B  #1,D6           ; STRING LENGTH
        BEQ.S   CANON           ; ASSURE CANONICAL
        SUBQ.B  #1,D2           ; MAX DIG COUNT
        BNE.S   SIGDIGS

;-----------------------------------------------------------
; GET HERE WHEN MORE THAN 19 DIGITS INPUT.  PLACE 20-TH INTO
; D7.LO FOR LATER ROUNDING TWITCH.
;-----------------------------------------------------------
TRAILDIG:
        MOVE.B  #$0F,D7         ; NIBBLE MASK
        AND.B   (A4),D7         ; NO AUTOINC -- LAST CHAR
        BRA.S   DONEDIG

;-----------------------------------------------------------
; GET HERE WHEN AT MOST 19 DIGITS INPUT.  GET TO CANON
; FORM BY 'APPENDING TRAILING 0S' WHILE DECREMENTING
; POSITIVE EXPONENT.  THIS TENDS TO CUT DOWN MAGNITUDE OF
; EXPONENT AND, CONSEQUENTLY, ITS ROUNDING ERROR.
; NOTE THAT ON ENTRY, MAX DIGIT COUNT MUST BE DECREMENTED,
; AND CHECKED FOR INSTANT EXIT.
; CAN STOP SHIFTING ONCE EXP IS LESS THAN 27, WHICH IS THE
; MAX EXACT CASE.
; MUST WATCH FOR OVERFLOW OF 16-BIT SIGNED EXPONENT; IF IT
; OVERFLOWS, ANY HUGE VALUE WITH CORRECT SIGN WILL DO.
;-----------------------------------------------------------
CANONTOP:
        CMPI.W  #27,D3          ; EXP ALREADY REASONABLE?
        BLE.S   DONEDIG
        BSR.S   DIG10           ; D4,5 TIMES 10
        SUBQ.W  #1,D3
        BVC.S   CANON           ; $8000 --> $7FFF ??
        ADDQ.W  #1,D3           ; IF SO RESTORE $8000
CANON:
        SUBQ.W  #1,D2           ; MAX DIG COUNT
        BNE.S   CANONTOP

;-----------------------------------------------------------
; NOW PLACE A SIGN ON THE NONZERO DIGIT FIELD IN D4,5 AND
; STUFF IT INTO THE FIRST FRAME ELEMENT FOR SCALING.
; TRICK: JUST STUFF IT WITHOUT PRENORMALIZATION SINCE IT'S
; KNOWN TO BE A NUMBER.
;-----------------------------------------------------------
DONEDIG:
        MOVE.W  #$403E,D0       ; BIASED 63, FOR EXP
;-----------------------------------------------------------
; DECIMAL.SGN ENUM TYPE TEST USES HI BYTE ONLY
;-----------------------------------------------------------
        TST.B   (A2)            ; CHECK OPERAND SIGN
        BEQ.S   @1
        BSET    #15,D0
@1:

;-----------------------------------------------------------
; USE  UTILITY TO SAVE EXTENDED VALUE D0.W,D4,D5 IN (A3)
;-----------------------------------------------------------
        BSR.S   TOA3

;-----------------------------------------------------------
; THE CORE COMPUTATION IN D2B AND B2D IS THE SAME
;-----------------------------------------------------------
        BSR.S   DBCORE

;-----------------------------------------------------------
; SET UP STORE TO DESTINATION FIELD USING ORIGINAL OPWORD
;-----------------------------------------------------------
DBSTORE:
        SWAP    D6              ; OPWORD TO D6.LO
        ANDI.W  #OPFOMASK,D6
        ORI.W   #OPEXT2,D6      ; SET TO CONVERT FROM EXT

        PEA     (A3)            ; SRC IS CURRENT EXT'D
        MOVE.L  LKADR1(A6),-(SP) ; DST IS AS SAVED
        MOVE.W  D6,-(SP)
        BSR     REFP68K

        ADDA.W  #20,SP          ; CLEAR STACK WORK AREA
        BRA     POP2


.PAGE
;-----------------------------------------------------------
; STORE OPERAND IN D0.W,D4.L,D5.L TO (A3)
;-----------------------------------------------------------
TOA3:
        MOVE.W  D0,(A3)+
        MOVE.L  D4,(A3)+
        MOVE.L  D5,(A3)
        SUBQ.L  #6,A3
        RTS


;-----------------------------------------------------------
; LUCKILY, CONVERSION IN BOTH DIRECTIONS USES THE SAME CORE
; ROUTINE.  THIS CORRESPONDS TO STEPS B4-B7 OF ALGORITHM B
; AND STEPS D5 TO D8 OF ALGORITHM D OF J. COONEN'S PAPER
; (MAY 1982 DRAFT) ON CONVERSIONS.  IN BOTH CASES THE LAST
; ROUND STEP IS UNIQUE TO THE PARTICULAR CONVERSION.
;-----------------------------------------------------------
DBCORE:

;-----------------------------------------------------------
; SET THE ROUNDING DIRECTION FOR THE SCALE FACTOR BASED
; ON CURRENT MODE, SIGN OF OPERAND, AND SIGN OF EXPONENT.
;-----------------------------------------------------------
        MOVE.B  (A0),D6         ; MODE INFO
        move.b  d6,-(sp)        ; Save old rounding mode/flags on stack.
        ANDI.B  #RNDMSK,D6      ; ISOLATE ROUND MODE BITS
        BEQ.S   @51             ; EASY IF ZERO (NEAREST)

        MOVEQ   #RNDDN,D1       ; ASSUME TOWARD -INF

        TST.B   (A3)            ; SIGN OF OPERAND
        BPL.S   @30

        CMPI.B  #RNDDN,D6       ; NEG AND #RNDDN
        BEQ.S   @37
        BRA.S   @40
@51:
        move.b  d6,(a0)         ; Round to nearest - clear flags.
        bra.s   @50

@30:
        CMPI.B  #RNDUP,D6       ; POS AND #RNDUP
        BNE.S   @40
@37:
        EORI.B  #RNDMSK,D1      ; SET TO TOWARD +INF
@40:
        TST.W   D3              ; IS EXP < 0?
        BPL.S   @43
        EORI.B  #RNDMSK,D1      ; FLIP +INF <-> -INF
@43:
        MOVE.B  D1,(A0)         ; STUFF NEW MODES and clear flags.
@50:

;-----------------------------------------------------------
; COMPUTE 10^ABS(D3.W) IN 10(A3).
;-----------------------------------------------------------
        LEA     10(A3),A1       ; ADRS FOR POW10
        BSR     POW10

;-----------------------------------------------------------
; FORCE ROUNDING TOWARD 0.
;-----------------------------------------------------------
        ORI.B   #RND0,(A0)

;-----------------------------------------------------------
; SET UP CALL TO MUL/DIV TO DO SCALING.
;-----------------------------------------------------------
        PEA     10(A3)          ; SRC IS SCALE FACTOR
        PEA     (A3)            ; DST IS NUM
        MOVEQ   #OPMUL,D0
        TST.W   D3              ; SCALE NEG?
        BPL.S   @5
        MOVEQ   #OPDIV,D0
@5:
        MOVE.W  D0,-(SP)
        BSR     REFP68K

;-----------------------------------------------------------
; LOGICALLY OR THE LOST INFO INTO TRAILING BITS OF EXT'D
; VALUE (A3).  BE SURE D7.B IS SET TO ZERO IN B->D CONVERT
; WHERE IT ISN'T USED.
;-----------------------------------------------------------
        BTST    #ERRX,(A0)      ; RND ERROR IN power of ten, multiply, or DIVIDE?
        SNE     D0
        TST.B   D7              ; LOST DIGITS?
        SNE     D7
        OR.B    D7,D0
        
        move.b  (a0),d1         ; d1 gets new modes and flags.
        andi.b  #$1f,d1         ; d1 gets new flags.
        or.b    (sp)+,d1        ; d1 gets old modes + old flags + new flags.
        move.b  d1,(a0)         ; Store old modes + old flags + new flags.
        
        NEG.B   D0              ; 0 OR 1

        beq.s   dbcorereturn    ; Status of D0 was set on exit.
                                ; Branch if exact.
        
        bset    #errx,(a0)      ; Set inexact for lost digits in case we missed it.
        
        tst.b   d6              ; Round bits conveniently in D6.b.
        beq.s   jam1lsb         ; Round Nearest - hopeless.
                                ; nothing better than Jam 1.
        cmpi.b  #rnd0,d6
        beq.s   dbcorereturn    ; Round Zero - ignore extra info - don't jam.
                                ; Directed roundings - add lsb hard way.
                                ; Add one half in least significant bit.
        link    a1,#-10         ; Make room for an extended.
        move.l  #1,-4(a1)       ; Set one bit in significand.
        clr.l   -8(a1)          ; Clear high order bits.
        move.w  (a3),d0         ; D0 gets sign/exponent.
        move.w  d0,d1           ; d1 gets sign/exponent.
        bclr    #15,d1          ; Clear sign.
        tst.w   d1
        beq.s   storeexp        ; Don't decrement minimal exponent.
        subq.w  #1,d0           ; Make one HALF LSB.

storeexp
        move.w  d0,-10(a1)      ; Store sign/exponent.
        pea     -10(a1)
        pea     (a3)
        move.w  #foadd,-(sp)    ; Set up ADD.
        bsr     refp68k         ; Grind through entire process.
        unlk    a1              ; Restore stack.
        bra.s   dbcorereturn    ; Now we are ready to round.
        
jam1lsb                         ; Force LSB to be one.
        move.w  (a3),d0         ; d0 gets sign/exponent.
        bclr    #15,d0          ; d0 gets exponent.
        cmpi.w  #$7fff,d0
        beq.s   dbcorereturn    ; Branch if inf or nan.
        bset    #0,9(a3)        ; Turn it on!
        
dbcorereturn
        RTS

ÿ