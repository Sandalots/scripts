;
; File LibFP/ScanDec, Version 22 February 1984.
;
        .proc   %scan_decimal
;
;       procedure %scan_decimal
;               (
;               var s : longint ;
;                       input : address of first character to scan
;                       output: 1 + address of last character accepted as
;                               part of a valid prefix of a floating point
;                               token.
;               var t : longint ;
;                       output: 1 + address of last character accepted as
;                               part of a valid complete floating point token.        
;                               t <= s.
;               maxsig : integer ;
;                       input : maximum number of characters allowed in 
;                               d.sig.  0 <= maxsig <= 255.
;               var d : decimal ;
;                       output: decimal record.
;                               sgn : 0..1 ;
;                               exp : integer ;
;                               sig : string[maxsig]
;               var more : boolean ;
;                       output: True iff additional significant digits were
;                               ignored.
;               ) ;

        link    a6,#0
                                ;  0(a6) is link.
retad   .equ    4               ;  4(a6) contains return address.
more    .equ    8               ;  8(a6) contains address of MORE.
d       .equ    12              ; 12(a6) contains address of D.
maxsig  .equ    16              ; 16(a6) contains MAXSIG.
t       .equ    18              ; 18(a6) contains address of T.
s       .equ    22              ; 22(a6) contains address of S.
stackbase .equ  26              ; 26(a6) is the base of the stack.

        movem.l d0-d5/a0-a3,-(sp)
        
                        ; Conventions:
                        ; A0 points to next character in S.
                        ; A1 points to next character beyond token for T.
                        ; A2 points to D.SIG[1+MAXSIG] and acts as general
                        ;       scratch A register while storing results.
                        ; A3 points to D.SIG[1+length(D.SIG)].
                        ; lsb(D0) contains the character at (S-1)^.
                        ;     D1  contains
                        ;     .w        a count of consecutive zeros before point,
                        ;  .w,.l        explicit exponent,
                        ;     .w        nan code.
                        ; lsw(D2) contains
                        ;     .w        a count of consecutive zeros after point,
                        ;     #0        sign of explicit exponent.
                        ; lsb(D3) contains MORE.
                        ;     D4  contains D.SGN and D.EXP.
                        ; lsb(D5) contains 0 except
                        ;     #0  contains 0 if no nonzero significand digit seen,
                        ;                  1 if any nonzero significand digit seen.
                        ;     #1  contains 0 if no leading zero significand digit seen,
                        ;                  1 if any leading zero significand digit seen.
                        
        .macro  nexts
        move.b  (a0)+,d0        ; Update D0 and increment S.
        .endm
        
        .macro  backups
        subq.l  #1,a0           ; Back up S.
        .endm
        
        .macro  maket
        movea.l a0,a1           ; Update T to S.
        .endm
        
        .macro  nextsig
        move.b  %1,(a3)+        ; Move %1 to D.SIG and update A3.
        .endm
        
        .macro  upcase
        bclr    #5,d0           ; Upper case (letters only!).
        .endm
        
        .macro nextupcase
        move.b  (a0)+,d0
        bclr    #5,d0
        .endm
        
        .macro  moretrue
        bset    #0,d3           ; Set MORE true.
        .endm
        
        .macro  zeroseen
        bset    #1,d5           ; Set zero significand digit seen.
        .endm
        
        .macro nonzeroseen
        bset    #0,d5           ; Set nonzero significand digit seen.
        .endm
        
        movea.l s(a6),a0
        movea.l (a0),a0         ; Start out S pointing to S[1].
        maket                   ; Start out T pointing to S[1].
        movea.l d(a6),a3
dsgn    .equ    0
dexp    .equ    2
dsig    .equ    4
        addq.l  #dsig+1,a3      ; Start out pointing to D.SIG[1].
        movea.l a3,a2
        adda.w  maxsig(a6),a2
        
nextblank                       ; Skip leading blanks.
        nexts                          
        cmpi.b  #' ',d0  
        beq.s   nextblank       ; Branch if leading blank.
        
        clr.l   d4              ; Default sign + and exponent 0.
        cmpi.b  #'-',d0  
        bne.s   notminus        ; Branch if not minus.
        bset    #24,d4          ; Negative sign.
        bra.s   passsign
        
notminus
        cmpi.b  #'+',d0  
        bne.s   pastsign

passsign
        nexts

pastsign
        clr.w   d1              ; Clear nancode/number of zeros before point.
        clr.b   d3              ; Set MORE false.
        clr.b   d5              ; Indicate no significand digits seen.
        cmpi.b  #'9',d0
        bgt     notanumber
        cmpi.b  #'0',d0
        bgt.s   nextsignificand
        beq.s   nextleadingzero
        cmpi.b  #'.',d0
        beq.s   dotfound
        bra     notanumber
        
nextleadingzero
        nexts
        cmpi.b  #'0',d0
        beq.s   nextleadingzero
        zeroseen
        maket                   ; 000...000 found!
        subq.l  #1,a1           ; Back up T by 1.
        bra.s   checkdigit

nextzero
        nextsig #'0'            ; Append intervening zero.

nextdigit
        cmpa.l  a3,a2
        dbeq    d1,nextzero     ; Branch to nextzero if more zeros are required
                                ; AND we have not yet overflowed MAXSIG.
        beq.s   sigfull         ; Branch if we have overflowed.

nextsignificand
        nextsig d0              ; Append the nonzero digit.
        bra.s   resetzerocount
        
sigfull
        addq.w  #1,d1           ; Undo last decrement.
        add.w   d1,d4           ; Increment exponent to reflect lost zeros.
        moretrue
        
resetzerocount
        clr.w   d1              ; Reinitialize count of leading zeros.
        nonzeroseen

getnextbefore
        nexts

checkdigit
        cmpi.b  #'0',d0
        blt.s   pastdigits
        bgt.s   check9
        addq.w  #1,d1           ; Increment count of zeros before point.
        bra.s   getnextbefore
        
check9
        cmpi.b  #'9',d0
        ble.s   nextdigit

pastdigits
        maket                   ; DIGITS found! Maybe all zeros.
        subq.l  #1,a1           ; Back up T by 1.
        cmpi.b  #'.',d0
        bne.s   endofdigits
        maket                   ; DIGITS.  found!
        
dotfound
        nexts
        clr.w   d2              ; Clear count of digits after point.
        cmpi.b  #'9',d0
        bgt.s   endofdigits
        cmpi.b  #'0',d0
        bge.s   aftdigit
        bra.s   endofdigits
        

;       ********        Begin code to handle dddd00000.0000d

prenextzero
        nextsig #'0'            ; Append intervening zero.

prenextdigit
        cmpa.l  a3,a2
        dbeq    d1,prenextzero  ; Branch to prenextzero if more zeros are required
                                ; AND we have not yet overflowed MAXSIG.
        bne.s   preresetzerocount
        addq.w  #1,d1           ; Undo last decrement.
        add.w   d1,d4           ; Increment exponent to reflect lost zeros.
        
preresetzerocount
        clr.w   d1              ; Reinitialize count of leading zeros.
        nonzeroseen
        bra.s   aftnextdigit

;       ********          End code to handle dddd00000.0000d

;       ********        Begin code to handle ddd.d000d

aftnextzero
        nextsig #'0'            ; Append intervening zero.
        subq.w  #1,d4           ; Decrement exponent.
        
aftnextdigit
        cmpa.l  a3,a2
        dbeq    d2,aftnextzero  ; Branch to aftnextzero if more zeros are required
                                ; AND we have not yet overflowed MAXSIG.
        beq.s   aftsigfull      ; Branch if we have overflowed.

aftnextsignificand
        nextsig d0              ; Append the nonzero digit.
        subq.w  #1,d4           ; Decrement exponent.
        bra.s   aftresetzerocount
        
aftsigfull
        moretrue
        
aftresetzerocount
        clr.w   d2              ; Reinitialize count of leading zeros.
        nonzeroseen

;       ********          End code to handle ddd.d000d

aftnextbefore
        nexts
        cmpi.b  #'0',d0
        blt.s   afterdigits

aftdigit
        bgt.s   aftcheck9
        zeroseen
        addq.w  #1,d2           ; Increment count of zeros after point.
        bra.s   aftnextbefore
        
aftcheck9
        cmpi.b  #'9',d0
        bgt.s   afterdigits
        tst.w   d1
        bne.s   prenextdigit    ; Branch if trailing zeros still hanging around
                                ; from before point.
        btst    #0,d5
        bne.s   aftnextdigit    ; Branch if this is not the first nonzero digit.
        sub.w   d2,d4           ; Decrement exponent for leading zeros.
        bra.s   aftnextsignificand

afterdigits
        maket                   ; .DIGITS found!
        subq.l  #1,a1           ; Back up T by 1.

endofdigits
        tst.b   d5
        beq.s   storebackone    ; If no digits seen E or .E is no good.
        add.w   d1,d4           ; Increment implicit exponent for unstored zeros.
                
;       ********        Begin code to handle explicit exponent.

        upcase
        cmpi.b  #'E',d0
        bne.s   storebackone
        nexts
        bclr    #0,d2           ; Default sign +.
        clr.w   d1              ; Default exponent 0.
        cmpi.b  #'-',d0  
        bne.s   expnotminus     ; Branch if not minus.
        bset    #0,d2           ; Negative sign.
        bra.s   nextexpdigit
        
expnotminus
        cmpi.b  #'+',d0  
        beq.s   nextexpdigit
        bra.s   checkexpdigit

expdigfound
        maket                   ; NumberEsdddd Found!
        mulu    #10,d1
                        ; Begin using D1 as a LONG exponent.
        subi.b  #'0',d0
        ext.w   d0
        ext.l   d0
        add.l   d0,d1           ; Add new digit to exponent.
        cmpi.l  #16384,d1
        ble.s   nextexpdigit
                        ;   End using D1 as a LONG exponent.
        move.w  #16384,d1       ; Substitute smaller exponent.

nextexpdigit
        nexts

checkexpdigit
        cmpi.b  #'0',d0
        blt.s   finalexponent
        cmpi.b  #'9',d0
        ble.s   expdigfound
        
finalexponent
        btst    #0,d2
        beq.s   addexponent
        neg.w   d1              ; Negate explicit exponent.

addexponent
        add.w   d1,d4           ; Combined exponent.
        bvc.s   storebackone
        move.w  #16384,d4       ; Max exponent.
        btst    #0,d2           ; Test sign of explicit exponent.
        beq.s   storebackone    ; Branch if positive overflow.
        neg.w   d4              ; Negate if negative overflow.

;       ********          End code to handle explicit exponent.

storebackone                    ; Current character in D0 is unacceptable, so
                                ; back up S by one to point to it,
                                ; then store results.
        backups
        bra     storeresults

notanumber
        upcase
        cmpi.b  #'I',d0
        bne.s   trynan
        nextupcase
        cmpi.b  #'N',d0
        bne.s   storebackone
        nextupcase
        cmpi.b  #'F',d0
        bne.s   storebackone
        maket                   ; INF found!
        nextsig #'I'
        bra     storeresults
        
trynan
        cmpi.b  #'N',d0
        bne.s   storebackone
        nextupcase
        cmpi.b  #'A',d0
        bne.s   storebackone
        nextupcase
        cmpi.b  #'N',d0
        bne.s   storebackone
        maket                   ; NAN found!
        nexts
        cmpi.b  #'(',d0
        bne.s   backnancode
        bra.s   nextnandigit

addnansig
        mulu    #10,d1
        add.w   d0,d1
        sub.w   #'0',d1

nextnandigit
        nexts
        cmpi.b  #'0',d0
        blt.s   endnancode
        cmpi.b  #'9',d0
        ble.s   addnansig
        
endnancode
        cmpi.b  #')',d0
        beq.s   testnancode
        
backnancode
        backups
        bra.s   zeronancode     ; Syntax error in midst of NaN.

testnancode
        maket                   ; NAN(digits) !
        tst.b   d1
        bne.s   storenancode

zeronancode
        move.w  #nanzero,d1

storenancode
        nextsig #'N'
        move.b  d1,d0
        lsr.b   #4,d0
        add.b   #'0',d0
        cmpi.b  #'9',d0
        ble.s   nextnansig
        add.b   #7,d0           ; 'A' .. 'F' := '7' + 10..15.

nextnansig
        nextsig d0
        move.b  d1,d0
        and.b   #$f,d0
        add.b   #'0',d0
        cmpi.b  #'9',d0
        ble.s   lastnansig
        add.b   #7,d0           ; 'A' .. 'F' := '7' + 10..15.

lastnansig
        nextsig d0

storeresults
        movea.l s(a6),a2
        move.l  a0,(a2)         ; Write final value of s.
        movea.l t(a6),a2
        move.l  a1,(a2)         ; Write final value of t.
        movea.l d(a6),a2
        move.l  d4,(a2)         ; Store D.SGN and D.EXP.
        lea     dsig+1(a2),a2   ; a2 gets address of D.SIG[1].
        suba.l  a2,a3           ; A3 gets length(D.SIG).
        move.w  a3,d0
        bne.s   setlengthdsig   ; Branch if significand nonempty.
        move.w  #$0130,-1(a2)   ; D.SIG := '0' by default.
        bra.s   storemore

setlengthdsig
        move.b  d0,-1(a2)       ; Set D.SIG[0] to length(D.SIG).

storemore
        movea.l more(a6),a0
        move.b  d3,(a0)         ; Write MORE.
        
        move.l  retad(a6),stackbase-4(a6)    ; Relocate return address.
        movem.l (sp)+,d0-d5/a0-a3
        unlk    a6
        adda.l  #stackbase-4-retad,sp
        debugrts '%SCAN_DE'

ÿ