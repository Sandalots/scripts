        .title   'LOAD FP OPERAND TO REGISTERS D0-D3'
        .proc   mc%loadfp ; 0,0
        .def    mc%ldfp
        .ref    mc%type,mc%float,mc%decbin,mc%iop
;
;***********************************************************************
;
SZTAB   .word   4,8,10,1,2,4,14,14    ; SIZE OF OPERAND TYPES
;
;************************************************************************
;
;       SIZE --
;               THIS MACRO TAKES THE 'mc%type' INFORMATION IN THE LOW-
;       ORDER WORD OF IT FIRST ARGUMENT AND CHANGES IT TO THE SIZE
;       OF THE MEMORY FORMAT FOR THAT mc%type.
;
;       ARGUMENTS:
;               %1 -- REGISTER TO RECEIVE DATA OPERAND SIZE
;       DESTROYS: A2.
;******
;       NOTE:
;               YOU MUST INCLUDE A LINE LIKE THE FOLLOWING IN YOUR
;       DATA DECLARATIONS:
;
;    SZTAB   DC.W    4,8,10,1,2,4,14,14              ARGUMENT SIZE TABLE
;
        .macro  SIZE
        MOVE.B      DATYP(A1),%1  ; GET OPERAND mc%type IN WORD OF D7
        EXT.W       %1
        LSL.W       #1,%1             ; CHANGE mc%type TO SIZE INDEX IN D1
        LEA         SZTAB,A2
        MOVE.W      0(A2,%1),%1
        EXT.L       %1                    ; AND EXTEND
       .endm
;
;************************************************************************
;
;       mc%ldfp --
;               THIS ROUTINE WILL LOAD THE SOURCE ARGUMENT INTO
;       REGISTERS D0-D2, CONVERTING FROM THE OPERAND'S FORMAT TO
;       INTERNAL FLOATING-POINT FORMAT.
;
;       INPUT:
;               D0,A0 -- SOURCE POINTER AND DESCRIPTOR
;               A1    -- POINTER TO LOCAL STORAGE (SEE EQUATES)
;
;       OUTPUT:
;               D0-D2 -- INTERNAL FLOATING-POINT ARGUMENT.
;               D3    -- CLEARED TO RECEIVE GUARD, round, & STICKY BITS
;               D4    --  = 0 IF TRAPPING NAN NOT SEEN
;                         = 1 IF TRAPPING NAN SEEN
;               CCR   --  FOLLOWS D4 AS TRAPPING NAN INDICATOR
;
;       BASHES:  A0,D6,D7.
;       RESTORES:  A1-A6.
;
;      THIS PROCEDURE CALLS:  mc%float, mc%decbin, mc%type,mc%iop,USER FETCH/STORE.
;
;      THIS PROCEDURE IS CALLED BY:  FRNT.
;
;***********************  F L A G S   ******************************
;
;  EXCEPTION    SET BY     REASON
;
;  mc%iop          SELF       SET IF TRAPPING NAN SEEN.
;
;*********************************************************************
;
;
;       REVISION HISTORY:
;         DATE          REVISIONIST     REASON
;       15.FEB.81       G.WALKER        MAKE PIC AND CORRECT COMMENTS
;       05.MAY.81       G.WALKER        DOC. EXCEPTIONS, REMOVE TRAP-BIT
;                                          FROM TRAPPING NAN'S, AND CORRECT
;                                          ERROR REPORTING CODE.
;       05.MAY.81       G.WALKER        REPLACE CALL TO mc%iop WITH EQUIV  CODE
;
;       07.JUL.81       D.CAWTHRON      CHANGED TO EXORMACS STRUCTURED
;                                          ASM SYNTAX
;       06.JUL.81       G.WALKER        OPTIMIZE FOR SPEED AND DOCUMENT
;       15.JUL.81       G.WALKER        CORRECT EXTENDED NORMALIZE SHIFT
;       27.AUG.81       G.WALKER        CORRECT BNE ERRXIT FOR TRAPPING NAN
;                                          FOR SINGLE-PREC CONVERT
;       16 Jan 83       Hough           Correct loading of single and 
;                                       double precision NaNs.
;       17 Jan 83       Hough           Do not normalize denormalized extendeds.
;
;       18 Jan 83       Hough           Removed padding NOPs inserted by Laura Okin.
;
;        9 Feb 83       Hough           Remove rounding on loads; it is unnecessary.
;
;********************
;
TRAPNAN EQU     5     ; mc%iop CODE FOR TRAPPING NAN ON INPUT
;
OPRBUF  EQU     0     ; BUFFER FOR MEMORY OPERAND
SAVA1   EQU     26    ; SAVE REGISTER A1
SAVA2   EQU     30    ; SAVE REGISTER A2
SAVA3   EQU     34    ; SAVE REGISTER A3
;
mc%ldfp
        debugbegin
  MOVEM.L       A1/A2/A3,-(SP)        ; SAVE REGISTERS
  LEA           -26(SP),SP            ; RESERVE LOCAL STORAGE
  BTST          #1,D0
  bne           @1                       ; IF SOURCE IS FP REGISTER
    MOVEM.L     (A0)+,D0-D2              ; MOVE OPERAND FROM FP REGISTER BLOCK
    moveq       #0,D3                    ; CLEAR G-R-STICKY BITS
    BRA         LDFPXIT                  ; AND EXIT FROM mc%ldfp
@1
;
;         ELSE FETCH A MEMORY OPERAND TO A TEMPORARY BUFFER.
;
  SIZE        D7                      ; CREATE SIZE OF OPERAND IN WORD OF D7
  BTST          #0,D0
  beq           @3                    ; IF OPERAND IS IN SUPERVISOR SPACE
    LEA         OPRBUF(SP),A2
@2
    cmp.b       #0,d7                   ; FETCH OPERAND DIRECTLY
    ble         @69
      MOVE.B    (A0)+,(A2)+
      SUB.B     #1,D7
      bra       @2
@69
    bra         @6
@3                                        ; ELSE
    MOVE.L      A0,A3                     ; FETCH OPERAND FROM USER SPACE
    LEA         OPRBUF(SP),A2
    BTST        #2,D0
    bne         @4                        ; IF OPERAND IN USER PROGRAM SPACE
      MOVE.B    #FALSE_B,D6               ; SET FLAG ACCORDINGLY
      bra       @5
@4
      MOVE.B    #TRUE_B,D6            ; ELSE OPERAND IS IN USER DATA SPACE
@5
    JSR         (A4)                  ; JUMP TO USER FETCH/STORE ROUTINE
@6
;
;       JUMP THROUGH TABLE TO FORMAT CONVERSION CODE. (USING mc%type SIZE
;           INDEX IN D1.)
;
  MOVE.B        DATYP(A1),D1          ; GET OPERAND mc%type IN WORD OF D1
  EXT.W         D1
  LSL.W         #1,D1                 ; CHANGE mc%type TO SIZE INDEX IN D1
  MOVE.L        SAVA3(SP),A3          ; RESTORE A3 POINTER TO FP REGS
  MOVE.W        LDFPTAB(D1),A0
  JMP           LDFPTAB(A0)
;
;       BELOW IS THE TABLE OF OFFSETS TO FORMAT CONVERSION ROUTINES.
;
LDFPTAB EQU     *
  .word         LDSGL-LDFPTAB       ; SINGLE PRECISION
  .word         LDDBL-LDFPTAB       ; DOUBLE PREC.
  .word         LDEXT-LDFPTAB       ; EXTENDED PREC.
  .word         LDBYT-LDFPTAB       ; BYTE INTEGER
  .word         LDWRD-LDFPTAB       ; WORD INTEGER
  .word         LDLNG-LDFPTAB       ; LONG INTEGER
  .word         LDBCD-LDFPTAB       ; BCD WITH STATIC 'K'  (K IGNORED
  .word         LDBCD-LDFPTAB       ; BCD WITH DYNAMIC 'K'   ON INPUT )
;
;******************************************************************
;
;       UNPACK SINGLE MEMORY FORMAT TO INTERNAL FORMAT.
;
LDSGL   EQU     *
  MOVE.L        OPRBUF(SP),D0         ; FETCH MEMORY FORMATTED NUMBER
  MOVE.L        D0,D1
  MOVE.L        #0,D2                 ; CLEAR LOW-ORDER BITS OF SINGLE NUMBER
  MOVE.L        #0,D3                 ; INSERT LOW-ORDER ZEROS INTO G-R-STICKY
;
;        31  30-23   22    --     0           THE REGISTERS CONTAIN:
;       +---+------+----------------+
;       | S |  EXP |  MANTISSA      |    D0        S = SIGN
;       +---+------+----------------+              E = EXPONENT
;       | S |  EXP |  MANTISSA      |    D1        M = MANTISSA
;       +---+------+----------------+              X = JUNK
;       |           0               |    D2
;       +---------------------------+
;       |           0               |    D3
;       +---------------------------+
;
  SWAP          D0                    ; PUT EXPONENT IN LOW WORD
  EXT.L         D0                    ; SET SIGN OF FP NUMBER
  LSR.W         #7,D0                 ; RIGHT-JUSTIFY EXPONENT
  BCLR          #8,D0                 ; REMOVE FP SIGN BIT
  SUB.W         #SGLBIAS,D0           ; SUBTRACT EXPONENT BIAS
  LSL.L         #8,D1                 ; NORMALIZE FRACTION
;
;        31  -- 23   22    --     0            THE REGISTERS CONTAIN:
;       +----------+----------------+
;       | SSSS(16) |   EXPONENT(16) |    D0         S = SIGN
;       +-+--------+---------+------+               E = EXPONENT
;       |J| MANTISSA(23)     | 0(8) |    D1         M = MANTISSA
;       +-+------------------+------+               X = JUNK
;       |           0               |    D2
;       +---------------------------+
;       |           0               |    D3
;       +---------------------------+
;
  cmp.w         #sglmaxp,d0
  bne           @11
;                                         IF IS MAX SINGLE EXPONENT
    BCLR        #31,D1                  ; INFINITIES AND NANS HAVE J-BIT CLEAR
    MOVE.W      #EXTMAXP,D0             ; SET TO INTERNAL MAX. EXPONENT
    cmp.l       #0,d1
    beq         @109
;                                           IF THIS IS A NAN
      BCLR        #30,D1                  ; TEST AND CLEAR TRAP BIT
      beq         @10                     ; IF TRAP BIT SET
        ANDI.B       #$03,STATUS(A3)        ; CLEAR OLD mc%iop CODE
        ORI.B        #TRAPNAN*4,STATUS(A3)    ; SET mc%iop CODE FOR TRAPPING NAN
        BSET         #ENIOP_M,TEMPSTAT(A3)    ; SET mc%iop STATUS BIT
        BTST         #ENIOP_B,ENABLE(A3)
        BNE       ERRXIT                      ; GO TAKE A TRAP IF IT IS ENABLED
@10
@109
      bra          @15
@11                                   ; ELSE
    cmp.w       #sglminxp,d0
    bne         @14
;                                               IF EXPONENT IS SINGLE MINIMUM
      BCLR      #31,D1
      cmp.l     #0,d1
      beq       @13
;                                               IF IT IS DENORM NUMBER
        ADD.W   #1,D0                         ; ADD ANOTHER BIAS
        MOVE.B  MODE(A3),D4                   ; GET NORMALIZING MODE BIT
        AND.B   #MDNORM_B,D4
        beq     @129                             ; IF BIT SET, THEN
          TST.L        D1                        ; NORMALIZE THIS GUY
@12
            blt         @129
            LSL.L       #1,D1                    ; LOOP TILL
            SUB.W       #1,D0                      ; MANTISSA IS NORMALIZED
            TST.L       D1
            bra         @12
@129
          bra    @139
@13                                        ; ELSE MANTISSA IS ZERO, SO SET TO
        MOVE.W  #EXTMINXP,D0               ; NORMAL ZERO BY SETTING EXPONENT
@139
        bra     @15                        ; TO INTERNAL MIN.
@14                                  ; ELSE NUMBER HAS UNEXCEPTIONAL EXPONENT
      BSET      #31,D1               ; INSERT IMPLIED INTEGER BIT
@15
  jsr           mc%type          ; SET TAG BITS
  BRA           LDFPXIT
;
;***********************************************************************
;
;       UNPACK  DOUBLE-PRECISION OPERAND TO INTERNAL FORMAT.
;
LDDBL   EQU     *
  MOVE.W        OPRBUF(SP),D0         ; FETCH SIGN AND EXPONENT
  MOVE.L        OPRBUF(SP),D1         ; FETCH HIGH-ORDER FRACTION
  MOVE.L        OPRBUF+4(SP),D2       ; FETCH LOW-ORDER FRACTION
  MOVE.L        #0,D3                 ; CLEAR GUARD-round-STICKY
;
;        31  -- 16   15 14 --4   3-0           THE REGISTERS CONTAIN:
;       +----------+--+----------+----+
;       |   XXXXX  |S | EXP(11)  |X(3)|    D0       S = SIGN
;       +-------+--+--+-------+--+----+             E = EXPONENT
;       | X(12) |M(4A)| M(11B)| M(5C) |    D1       M = MANTISSA
;       +-------+-----+-------+-------+
;       | M(11D)|M(5E)| M(11F)| M(5G) |    D2
;       +-------+-----+-------+-------+
;
  EXT.L         D0                    ; SET INTERNAL SIGN BIT
  BCLR          #15,D0                ; REMOVE SIGN BIT FROM EXP. FIELD
  LSR.W         #4,D0                 ; RIGHT-JUSTIFY EXPONENT
  SUB.W         #DBLBIAS,D0           ; SUBTRACT BIAS
;
;        31 30 --16     15   --    0           THE REGISTERS CONTAIN:
;       +-+-----------+---------------+
;       |S|   XXXX    |   EXP(16)     |    D0       S = SIGN
;       +-+-----+-----+-------+-------+             E = EXPONENT
;       | X(12) |M(4A)| M(11B)| M(5C) |    D1       M = MANTISSA
;       +-------+-----+-------+-------+             X = JUNK
;       | M(11D)|M(5E)| M(11F)| M(5G) |    D2
;       +-------+-----+-------+-------+
;
  ROR.L         #5,D1                 ; ROTATE MANTISSA TO WORD BOUNDARY
  ROR.L         #5,D2
;
;        31 30 --16     15   --    0           THE REGISTERS CONTAIN:
;       +-+-----------+---------------+
;       |S|   XXXX    |   EXP(16)     |    D0       S = SIGN
;       +-+----+------+-+-----+-------+             E = EXPONENT
;       | M(5C)| X(11)|J|M(4A)| M(11B)|    D1       M = MANTISSA
;       +------+------+-+-----+-------+             X = JUNK
;       | M(5G)|M(11D)| M(5E) | M(11F)|    D2
;       +------+------+-------+-------+
;
  SWAP          D1                    ; PUT UPPER MANTISSA WORDS IN PLACE
  SWAP          D2
;
;        31 30 --16      15  --    0           THE REGISTERS CONTAIN:
;       +-+-------------+-------------+
;       |S|    XXXX     | EXP(16)     |    D0       S = SIGN
;       +-+-----+-------+-+----+------+             E = EXPONENT
;       |J|M(4A)| M(11B)| M(5C)| X(11)|    D1       M = MANTISSA
;       +-+-----+-------+------+------+             X = JUNK
;       | M(5E) | M(11F)| M(5G)|M(11D)|    D2
;       +-------+-------+------+------+
;
  MOVE.W        D2,D4                 ; MOVE THE M(11D) FIELD INTO
  ANDI.W        #$7FF,D4                 ; PROPER PLACE IN D1
  ANDI.W        #$F800,D2
  ANDI.W        #$F800,D1
  OR.W          D4,D1
;
;        31 30 --16      15  --    0           THE REGISTERS CONTAIN:
;       +-+-------------+-------------+
;       |S|    XXXX     | EXP(16)     |    D0       S = SIGN
;       +-+-----+-------+-+----+------+             E = EXPONENT
;       |J|M(4A)| M(11B)| M(5C)|M(11D)|    D1       M = MANTISSA
;       +-+-----+-------+------+------+             X = JUNK
;       | M(5E) | M(11F)| M(5G)|  0   |    D2
;       +-------+-------+------+------+
;
  cmp.w         #dblmaxp,d0
  bne           @20
;                                          IF EXPONENT IS MAXIMUM
    BCLR        #31,D1                   ; CLEAR INTEGER BIT
    MOVE.W      #EXTMAXP,D0              ; SET EXPONENT TO INTERNAL MAX.
    MOVE.L      D1,D4
    OR.L        D2,D4
    beq         @199                      ; IF MANTISSA IS NOT ZERO
      BCLR        #30,D1                   ; TEST AND CLEAR TRAP BIT
      beq       @199                 ; IF TRAP BIT SET IN NAN
        ANDI.B       #$03,STATUS(A3) ; CLEAR OLD mc%iop CODE
        ORI.B        #TRAPNAN*4,STATUS(A3) ; SET mc%iop CODE FOR TRAPPING NAN
        BSET         #ENIOP_M,TEMPSTAT(A3) ; SET mc%iop STATUS BIT
        BTST      #ENIOP_B,ENABLE(A3)         ; IF TRAP IS ENABLED
        BNE       ERRXIT                        ; GO TAKE A TRAP
@199
      bra       @25
@20                                   ; ELSE EXPONENT IS NOT MAXIMUM
    cmp.w       #dblminxp,d0
    bne         @24
;                                       IF EXPONENT IS MINIMUM FOR DOUBLE
      BCLR      #31,D1                ; CLEAR MSBIT
      MOVE.L    D1,D4
      OR.L      D2,D4
      beq       @23                      ; IF MANTISSA IS NOT ZERO
        ADD.W   #1,D0                    ; ADDITIONAL BIAS FOR DENORM. NUMBERS
        MOVE.B  MODE(A3),D4              ; GET NORMALIZING MODE BIT
        AND.B   #MDNORM_B,D4
        beq     @22                      ; IF NORMALIZE MODE SET,
          TST.L         D1               ; THEN NORMALIZE THIS GUY
@21
          blt           @22
            LSL.L       #1,D2           ; LOOP TILL MANTISSA IS NORMALIZED
            ROXL.L      #1,D1
            SUB.W       #1,D0
            TST.L       D1
            bra         @21
;@22
        ;jsr mc%round                     ; round IF NEEDED
        ;bra     @239
@23                                   ; ELSE MANTISSA IFS ZERO,
        MOVE.W    #EXTMINXP,D0        ; SO SET TO NORMAL ZERO BY
;@239
        bra     @25                   ; SETTING EXPONENT TO INTERNAL MINIMUM
@24                           ; ELSE EXPONENT IS NOT EXCEPTIONAL
      BSET      #31,D1        ; SET IMPLIED FRACTION BIT IN NORMAL NUMBER
      ;jsr mc%round               ; round IF NEEDED
@22
@239
@25
  jsr           mc%type          ; SET mc%type FLAGS  (COULD BE DONE IN CONVERSION)
  BRA           LDFPXIT
;
;*************************************************************************
;
;       UNPACK EXTENDED MEMORY FORMAT TO INTERNAL FORMAT.
;
LDEXT   EQU     *
  MOVE.W        OPRBUF(SP),D0 ; FETCH EXPONENT AND SIGN
  MOVE.L        OPRBUF+2(SP),D1 ; FETCH HIGH-ORDER MANTISSA
  MOVE.L        OPRBUF+6(SP),D2 ; FETCH LOW-ORDER MANTISSA
  MOVE.L        #0,D3         ; INSERT LOW-ORDER ZEROS INTO G-R-STICKY
  EXT.L         D0            ; SET FP SIGN BIT
  LSL.W         #1,D0         ; EXTEND EXPONENT SIGN TO 16 BITS
  ASR.W         #1,D0
  cmp.w         #extmaxp,d0
  bne           @30
;                                     IF EXPONENT IS MAX (NAN OR INF)
    BCLR        #30,D1              ; TEST AND CLEAR TRAP BIT
    beq         @299                ; IF TRAP BIT SET
      ANDI.B       #$03,STATUS(A3) ; CLEAR OLD mc%iop CODE
      ORI.B        #TRAPNAN*4,STATUS(A3) ; SET mc%iop CODE FOR TRAPPING NAN
      BSET         #ENIOP_M,TEMPSTAT(A3) ; SET mc%iop STATUS BIT
      BTST      #ENIOP_B,ENABLE(A3)
      BNE       ERRXIT                ; GO TAKE A TRAP IF IT IS ENABLED
@299
      bra       @34
@30                                   ; ELSE EXPONENT IS NOT MAXIMUM
    ;cmp.w       #extminxp,d0
    ;bne         @33
;                                       IF EXPONENT IS MIN (ZERO OR DENORM)
    ;MOVE.L    D1,D4
    ;OR.L      D2,D4
    ;beq       @329                    ; IF IT IS DENORMALIZED
    ;MOVE.B  MODE(A3),D4             ; GET NORMALIZING MODE BIT
    ;AND.B   #MDNORM_B,D4
    ;beq     @32                     ; IF BIT SET, THEN NORMALIZE THIS GUY
    ;TST.L        D1
    ;@31
    ;blt           @32             ; LOOP TILL MANTISSA IS NORMALIZED
    ;LSL.L       #1,D2
    ;ROXL.L      #1,D1
    ;SUB.W       #1,D0
    ;TST.L       D1
    ;bra         @31
    ;@32
    ;jsr mc%round             ; Round  IF NEEDED
    ;@329
    ;bra     @34
    ;@33                           ; ELSE NUMBER HAS NEITHER MIN OR MAX EXPONENT
      ;jsr mc%round               ; round  IF NEEDED
@34
  jsr           mc%type          ; SET mc%type FLAGS
  BRA           LDFPXIT
;
;       LOAD VARIOUS SIZES OF INTEGERS INTO INTERNAL FP REGISTERS.
;
LDBYT   EQU     *               ; BYTE INTEGER
  MOVE.B        OPRBUF(SP),D1
  EXT.W         D1              ; LOAD INTEGER AND EXTEND TO ENTIRE REGISTER
  EXT.L         D1
  MOVE.L        #0,D7           ; SET BYTE INTEGER FLAG
  jsr           mc%float           ; CONVERT INTEGER TO FP
  BRA           LDFPXIT
;
LDWRD   EQU     *               ; WORD INTEGER
  MOVE.W        OPRBUF(SP),D1
  EXT.L         D1              ; LOAD AND EXTEND INTEGER
  MOVE.L        #2,D7           ; SET WORD INTEGER FLAG
  jsr           mc%float           ; CONVERT TO FP
  BRA           LDFPXIT
;
LDLNG   EQU     *               ; LONG INTEGER
  MOVE.L        OPRBUF(SP),D1
  MOVE.L        #4,D7           ; SET LONG INTEGER FLAG
  jsr           mc%float
  BRA           LDFPXIT
;
;       LOAD A BCD NUMBER
;
LDBCD  EQU     *
  MOVE.L        SAVA1(SP),A1  ; RESTORE POINTER TO FP INSTR FIELDS
  MOVE.B        FPREG(A1),D3  ; GET FP REGISTER NUMBER
  EXT.W         D3
  MULU          #SIZEAC,D3    ; CREATE OFFSET TO REGISTER IN D3
  MOVE.L        REGBLK(A1),A2
  ADD.L         #FP0,A2       ; POINTER TO FIRST FPACC IN A2
  ADD.L         D3,A2         ; POINTER TO SPECIFIC FP REGISTER IN A2
  LEA           OPRBUF(SP),A1 ; POINT TO BCD NUMBER
  jsr           mc%decbin          ; AND GO CONVERT IT (TAKE IT AWAY, SHARON!)
  jsr           mc%type          ; SET TAG BITS
  BRA.S         LDFPXIT
;
;        SHOW CALLING ROUTINE TO TAKE A TRAP
;
ERRXIT  EQU     *
  SWAP         D0             ; RESULT IS A NAN, SO SET ITS type
  ANDI.W       #$8000,D0
  ORI.W        #NAN_B,D0
  SWAP         D0
  MOVE.B       #1,D4          ; SET D4 AND CCR CODES FOR ERROR
  BRA.S        LDFPOUT
;
;        SHOW NO TRAP TO BE TAKEN
;
LDFPXIT EQU     *             ; END OF LOAD FP OPERAND ROUTINE
  MOVE.B        #0,D4

;
LDFPOUT  EQU    *
  LEA           26(SP),SP     ; REMOVE LOCAL STORAGE
  MOVEM.L       (SP)+,A1/A2/A3
  RTS
        debugend 'MC%LDFP',1

ÿ