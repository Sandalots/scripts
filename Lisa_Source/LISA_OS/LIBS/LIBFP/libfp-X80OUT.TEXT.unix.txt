{ Copyright 1983, Apple Computer Inc. }


(* File L:X80Out, Version 10 January 1983   *)

procedure x80_free_ascii (*
        var x : fp_extended ; width, maxsig : integer ; format : fp_format ;
        var s : fp_string ; var error : boolean*) ;

        (* S := ascii representation of finite x with maximum information
        within the field size Width.
                Error is true, and s is undefined, if
        width < 1 or > maxfpstring or width is too small to represent x.
                Formats supported are fp_free and fp_lisa.

                FP_FREE:
                Different representations tried are:

        1.  If x is normalized and is exactly representable as an
                integral value, s := that value.
        2.  If an integral value was found and it ends in three
                or more trailing zeros, integral E notation is used:
                1230000 becomes 123E4.
        3.  If the integral representation exceeds width, it is
                rejected.
        4.  If x is normalized a fixed-point representation is
                attempted.  No more than three leading nonsignificant zeros
                are allowed in the representation: '.00000001' is not acceptable.
                Trailing zeros are removed, but not a trailing
                point.
        5.  If no representation has been found, or x is unnormalized,
                scientific
                notation is tried with before=1 and ew=1,2,3,4
                until a fit is found.

                FP_LISA:
                Maxsig limits the number of significant
        digits in various situations.  Different representations tried are:

        1.  If x is normalized and is exactly representable as an
                integral value of no more than maxsig digits, s := that value.
                If the integral representation exceeds width, it is
                rejected.
        2.  If x is normalized a fixed-point representation of no more than
                maxsig significant digits is attempted.
                A fixed point representation is acceptable as long as there is
                at least one non-zero digit: '.00000001' is acceptable.
                Trailing zeros are deleted, as is a trailing point.
        3.  If no representation has been found, or x is unnormalized,
                scientific
                notation is tried with before=1, (before+after) <= maxsig,
                and ew=1,2,3,4 until a fit is found.
                                                        *)

type
statetype = ( start, tryi, tryf, retrye, trye, donebad, donegood, done ) ;

var
state : statetype ; (* state of finite state machine *)
e, ew, ls : integer ;
sigma : integer ; (* 0 or 1 for + or - *)
ae : integer ; (* abs(getexponent(x)) *)
fpcb : fp_blocktype ;
trapcoming : boolean ;

procedure dostart ;
var maxsd : integer ;
begin (* dostart *)
if (width < 1) or (width > maxfpstring) then state := donebad else
        begin (* width ok *)
        if (format = fp_free) or (maxsig > maxfpstring) then maxsig := maxfpstring ;
        case fpkindx(x) of
        zero         : begin (* zero *)
                       fp_zero_ascii( fpminusx(x), 0, 0, format, s, error) ;
                       state := donegood ;
                       end   (* zero *) ;
        normal  :
                       begin (* normal *)
                       if fpminusx(x) then sigma := 1 else sigma := 0 ;
                       ae := abs(getexponent(x)) ;
                       maxsd := width - sigma ;
                                (* Max sig digits that fit assuming I format. *)
                       if (format <> fp_lisa) and (maxsd > maxsig) then 
                                maxsd := maxsig ;
                       if (3 * ae) > (10 * maxsd) then state := trye
                       else
                       if x80_integral(x) then state := tryi else state := tryf ;
                       end   (* normal *) ;
        nonnormal    :
                       begin
                       if fpminusx(x) then sigma := 1 else sigma := 0 ;
                       ae := abs(getexponent(x)) ;
                       state := trye ;
                       end ;
        inf          : begin (* inf *)
                       fp_inf_ascii( fpminusx(x), width, s, error) ;
                       if error then state := donebad else state := donegood ;
                       end   (* inf *) ;
        nan          : begin (* nan *)
                       x80_nan_ascii( x, width, s, error ) ;
                       if error then state := donebad else state := donegood ;
                       end   (* nan *) ;
        end (* case fpkindx(x) *) ;
        end   (* width ok *) ;
end   (* dostart *) ;

procedure doi ; (* x is guaranteed normal and integral *)

var
i, k : integer ;
c : char ;

begin (* doi *)
x_iform ( x, sigma, s, e) ;
if getxcpn(inxact) then
        begin (* i string bad *)
        state := retrye;
        end   (* i string bad *)
        else
        begin (* have i-string *)
        ls := length(s) ;
        if format <> fp_lisa then
                begin (* try to shorten string *)
                i := ls ;
                while s[i] = '0' do i := i-1 ; (* Look for nonzero *)
                e := e + ls - i ; (* Adjust e. *)
                ls := i ; (* Adjust ls. *)
                if e >= 3 then
                        begin (* replace zeros by e notation *)
                        ls := ls + 1 ;
                        s[ls] := 'E' ;
                        if e < 10 then
                                begin (* only one exponent digit *)
                                ls := ls + 1 ;
                                end   (* only one exponent digit *)
                                else
                                begin (* two exponent digits *)
                                ls := ls + 2 ;
                                s[ls-1] := chr(48 + e div 10) ;
                                end   (* two exponent digits *) ;
                        s[ls] := chr(48 + e mod 10) ; (* lower exponent digit *)
                        e := 0 ;
                        end   (* replace zeros by e notation *) ;
                end   (* try to shorten string *) ;
        if ((sigma + ls + e) > width) {or ((ls + e) > maxsig)}
                then state := trye
                else
                begin
                if sigma = 1 then
                        begin (* insert sign *)
                        for i := ls downto 1 do s[i+1] := s[i] ;
                        s[1] := '-' ;
                        ls := ls + 1 ;
                        end   (* insert sign *) ;
                for i := (ls+1) to (ls+e) do s[i] := '0' ; (* Append zeros. *)
                s[0] := chr(ls + e) ; (* Set length. *)
                state := donegood ;
                end ;
        end   (* have i-string *) ;
end   (* doi *) ;

procedure dof ;

var
is, li, lf, i, oflo : integer ;
intx, fracx : fp_extended ;
ints, fracs : fp_string ;
izero, fzero : boolean ;
after, postzero, prezero : integer ;
ilast, sige, sigf : integer ;

procedure app ( c : char ) ; (* Appends one character to s *)
begin
is := is + 1 ;
s[is] := c ;
end ;

begin (* dof *)
x80_break ( x, intx, fracx, izero, fzero ) ; (* Break x up into pieces. *)
if izero then
        begin (* integral part is zero *)
        after := width - sigma - 1 ; (* Allowable digits after point. *)
        li := 0 ; (* Null integer string. *)
        postzero := 0 ; (* No zeros between ints and point. *)
        end   (* integral part is zero *)
        else
        begin (* integral part nonzero *)
        x_iform( intx, sigma, ints, postzero ) ; (* Get string for int part. *)
        li := length(ints) ; (* li gets length of ints. *)
        after := width - (sigma + li + postzero + 1) ; (* Allowable digits after. *)
        end   (* integral part nonzero *) ;
error := (after < 0) or ((li + postzero) > maxsig) ;
if error and fzero then
        begin (* set up retry *)
        e := postzero ;
        s := ints ;
        state := retrye ;
        end   (* set up retry *)
        else
if not error then
        begin (* after OK *)
        if format = fp_lisa then (* enforce maximum significance for Lisa form *)
                begin
                if after > (maxsig-li-postzero) then after := maxsig - li - postzero ;
                end
                else             (* enforce maximum significance for Free form *)
                begin
                if after > maxsig then after := maxsig ;
                end ;
        oflo := 0 ; (* Default case. *)
        if getxcpn(inxact) or fzero then
                begin (* no fraction string *)
                lf := 0 ;
                end   (* no fraction string *)
                else
                 begin (* get fraction *)
                 if after = 0 then
                        begin (* after = 0 *)
                        fpintx ( fracx, fracx ) ; (* Round to +- 0 or 1. *)
                        if fpkindx(fracx) = zero then fracs := '.' else fracs := '1.' ;
                        end   (* after = 0 *)
                        else
                        begin (* after >= 1 *)
                        x_fform( fracx, after, false, fracs) ;
                        end   (* after >= 1 *) ;
                 lf := length(fracs) ;
                 if fracs[1] <> '.' then
                        begin (* .999 rounded to 1.00 *)
                        if postzero > 0 then postzero := postzero - 1 else
                                begin (* propagate carry *)
                                fracs[1] := '.' ; lf := 1 ;
                                i := li ;
                                while (i >= 1) and (ints[i] = '9') do
                                        begin (* decimal carry *)
                                        ints[i] := '0' ;
                                        i := i - 1 ;
                                        end   (* decimal carry *) ;
                                if i < 1 then
                                        begin (* overflow *)
                                        izero := false ;
                                        oflo := 1 ;
                                        end   (* overflow *)
                                        else
                                        begin (* no overflow *)
                                        ints[i] := succ(ints[i]) ;
                                                (* Increment. *)
                                        end   (* no overflow *) ;
                                end   (* propagate carry *) ;
                        end   (* .999 rounded to 1.00 *) ;
                 while (lf > 1) and (fracs[lf] = '0') do lf := lf - 1 ;
                        (* Trim trailing zeros. *)
                 if (format = fp_lisa) and (fracs[lf] = '.') then lf := lf - 1 ;
                        (* Trim trailing point. *)
                 end   (* get fraction *) ;
        error := error or ((sigma + oflo + li + postzero + lf) > width) ;
        if not error then
                begin
                is := 0 ;
                if sigma = 1 then app('-') ;
                if oflo = 1 then app('1') ; (* Prepend overflow digit. *)
                for i := 1 to li do app(ints[i]) ;
                for i := 1 to postzero do app('0') ; (* Append zeros. *)
                for i := 1 to lf do app(fracs[i]) ;
                s[0] := chr(is) ;
                if izero then
                        begin (* test for no significance *)
                        ilast := is ;
                        while s[ilast] = '0' do ilast := ilast-1 ;
                                (* Find last nonzero digit. *)
                        error := izero and ((s[ilast] < '1') or (s[ilast] > '9')) ;
                                (* Don't accept s if no significant digits. *)
                        if format = fp_free then
                                begin (* test for insufficient significance *)
                                i := 2 + sigma ; (* first digit position *)
                                while (s[i] = '0') and (i <= is) do i := i + 1 ;
                                        (* Find non zero. *)
                                if i >= ilast then error := true else
                                        begin (* compare significance of f and e *)
                                        sigf := ilast - i + 1 + width - is ;
                                        if sigf > fpbuffer then sigf := fpbuffer ;
                                                (* Max f-format significance. *)
                                        sige := width - sigma - 4 ;
                                        if sige > maxek then sige := maxek ;
                                                (* Max e-format significance. *)
                                        error := sige > sigf ;
                                        end   (* compare significance of f and e *) ;
                                end   (* test for insufficient significance *) ;
                        end   (* test for no significance *) ;
                end ;
        if error then state := trye else state := donegood ;
        end   (* after OK *)
        else state := trye ;
end   (* dof *) ;

procedure makeeform ;
        (* S := (-) d.dddd E +- e.  S, Ls, Sigma, E, are globals. *)
var (* makeeform *)
i,k : integer ;

begin (* makeeform *)
while s[ls] = '0' do ls := ls - 1 ; (* trim excess zeros. *)
if ls < 1 then ls := 1 ; (* But leave at least one. *)
if sigma = 0 then
        begin (* no sign *)
        for i := ls downto 2 do s[i+1] := s[i] ; (* Move for point. *)
        s[2] := '.' ;
        end   (* no sign *)
        else
        begin (* sign *)
        for i := ls downto 2 do s[i+2] := s[i] ;
                (* Move for point and sign.  *)
        s[3] := '.' ;
        s[2] := s[1] ;
        s[1] := '-';
        end   (* sign *) ;
if (format = fp_lisa) and (ls = 1) then (* trim trailing point *)
        ls := sigma + ls + 2 else ls := sigma + ls + 3 ;
s[ls-1] := 'E' ;
if e < 0 then s[ls] := '-' else s[ls] := '+' ;
k := abs(e) ;
for i := ew downto 1 do
        begin (* generate exponent digit *)
        s[ls + i] := chr( 48 + k mod 10 ) ;
        k := k div 10 ;
        end   (* generate exponent digit *) ;
s[0] := chr(ls + ew) ; (* Revise length. *)
end   (* makeeform *) ;

procedure doe ;

var (* doe *)
maxe, n, i, k : integer ;

begin (* doe *)
if ae <= 33 then
        begin
        ew := 1 ;
        maxe := 9 ;
        end
        else
if ae <= 332 then
        begin
        ew :=2 ;
        maxe := 99 ;
        end
        else
if ae <= 3321 then
        begin
        ew := 3 ;
        maxe := 999 ;
        end
        else
        begin
        ew := 4 ;
        maxe := 9999 ;
        end ;
error := width < (sigma + 4 + ew) ;
if not error then
        begin (* try different ew *)
        repeat (* until we find a representation *)
        if error then
                begin (* adjust spec *)
                ew := ew + 1 ;
                maxe := 10 * maxe + 9 ;
                end   (* adjust spec *) ;
        n := width - (sigma + 3 + ew) ; (* Number of significant digits. *)
        if n > maxsig then n := maxsig ;
        clrexcepset ; (* Start with clean slate. *)
        x_eform( x, n, sigma, s, e ) ;
        ls := length(s) ;
        e := e + ls - 1 ; (* Adjust e to position decimal point after first digit. *)
        error := abs(e) > maxe ;
        until (not error) or (n <= 1) ;
        if not error then
                begin (* found an ew *)
                makeeform ;
                end   (* found an ew *) ;
        end   (* try different ew *) ;
if error then state := donebad else state := donegood ;
end   (* doe *) ;

procedure doretry ;
        (* x_iform has already been called ; we know x = sign * s * 10^e.
        Try to work out an E format with this information without doing a
        reformatting. *)

begin (* doretry *)
ls := length(s) ;
while s[ls] = '0' do
        begin (* trim extra zero *)
        ls := ls - 1 ;
        e := e + 1 ;
        end   (* trim extra zero *) ;
e := e + ls - 1 ; (* Adjust e to position decimal point after first digit. *)
if e <= 9 then ew := 1 else
if e <= 99 then ew := 2 else
if e <= 999 then ew := 3 else ew := 4 ;
error := (ls > maxsig) or ((sigma + ls + 3 + ew) > width) ;
if error then state := trye else
        begin (* OK *)
        makeeform ;
        state := donegood ;
        end   (* OK *) ;
end   (* doretry *) ;

begin (* x80_free_ascii *)
blockprelude( fpcb ) ;
state := start ;
repeat (* finite state machine selector *)
case state of

start   : dostart ;
tryi    : doi ;
tryf    : dof ;
retrye  : doretry ;
trye    : doe ;

donebad :
        begin
        error := true ;
        state := done ;
        end ;

donegood :
        begin
        error := length(s) > width ;
        state := done ;
        end ;

end (* case state *) ;
until state = done ;
blockpostlude( fpcb, trapcoming ) ;
if trapcoming then postoutput( 'x80_free_ascii',x,s) ;
end   (* x80_free_ascii *) ;

procedure x80_ascii(*
 var X : fp_extended ; Width, Before, After : integer ; Format : fp_Format ;
        var S : fp_string ; var Error : boolean*) ;

        (* Error is true if Width <= 0 or Width > MaxFpString or Before or
        After are inappropriate for Format or X can't be formatted according
        to the parameters.  If Error is true then S will be a string of
        '?' of length Width but between 1 and MaxFpString. *)


var
i, ls, first, count : integer ;

begin (* x80_ascii *)

Error := ( Width <= 0 ) or ( Width > MaxFpString ) ;
if (not Error) and (Format >= fp_f) then
        Error := ( After < 0 ) or ( After > MaxFpString ) ;
if (not Error) and (Format >= fp_e1) then
        Error := ( Before < 0 ) or ((Before + After) <= 0 ) or
                 ((Before + After) > MaxFpString ) ;

case fpkindx(X) of

zero :
        begin (* zero *)
        if width = 1 then s := '0' else
        fp_zero_ascii ( fpminusx(X), Before, After, Format, S, Error ) ;
        Error := Error or ( length(s) > Width ) ;
        end   (* zero *) ;

nonnormal, normal :
        begin (* nonnormal, normal *)
        case format of
        fp_lisa, fp_free : x80_free_ascii ( x, width, maxlisa, format, s, error ) ;
        fp_i :  x80_i_ascii ( x, s, error ) ;
        fp_f :  x80_f_ascii ( x, before >= 1, after, s, error ) ;
        fp_e :
                begin (* fp_e *)
                x80_e_ascii(x, before, after, 4, s, error) ;
                if not error then
                        begin
                        ls := length(s) ;
                        first := ls - 3 ;
                        i := ls - 3 ; (* begin scan for zeros at s[i] *)
                        while (s[i] = '0') and (i < ls) do i := i + 1 ;
                        count := i + 3 - ls ;
                                (* count of characters to delete *)
                        if count > 0 then
                                begin (* delete characters *)
                                for i := (first + count) to ls do
                                        s[i - count] := s[i] ;
                                s[0] := chr(ls - count) ;
                                end   (* delete characters *) ;
                        end ;
                end   (* fp_e *) ;
        otherwise
            x80_e_ascii ( x, before, after, 1+ord(format)-ord(fp_e1), s, error ) ;
        end (* case format *) ;
        Error := Error or ( length(s) > Width ) ;
        end   (* nonnormal, normal *) ;

inf :   fp_inf_ascii( fpminusx(X), Width, S, Error ) ;

nan :   x80_nan_ascii ( X, Width, S, Error ) ;

end (* case fpkindx(X) *) ;

if Error then
        begin (* Error *)
        if Width <= 1 then S := '?' else
                begin (* string of ? *)
                if Width < MaxFpString then count := Width
                        else count := MaxFpString ;
                for i := 1 to count do S[i] := '?' ;
                S[0] := chr(count) ; (* Set length *)
                end   (* string of ? *) ;
        end   (* Error *) ;
end   (* x80_ascii *) ;

ÿ