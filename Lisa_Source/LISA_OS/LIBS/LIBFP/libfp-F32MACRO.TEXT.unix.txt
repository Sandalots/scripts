; Copyright 1983, Apple Computer Inc.


;       File F32Sub:Macro, Version 24 January 1983.

;       Macros used in 32 bit floating point routines.
                                
                                ; Macro INITFPCB loads the address of %fpcb
                                ; into A3, then clears Texcep.
        .MACRO  initfpcb 
        move.l  a0,-(sp)        ; Save A0 since it gets wiped out by f32_fpcb.
        clr.l   -(sp)           ; Make space for function result.
        .ref    f32_fpcb
        jsr     f32_fpcb        ; Call function to return pointer to %fpcb.
        movea.l (sp)+,a3        ; Set A3 to point.
        movea.l (sp)+,a0        ; Restore A0.
        clr.b   texcep(a3)      ; Clear TEXCEP.
        .ENDM

                        ; GETstatus macros return the appropriate status
                        ; in the least significant byte of D7.
                        ; SETstatus macros set the appropriate status from
                        ; the least significant byte of D7.
                        ; A3 should contain the address of the floating
                        ; point control block.  These macros  assume
                        ; that A3 is properly set when called.
                        
        .MACRO  GETCOND 
        move.b   FPCOND(a3),D7  ; Load lsbyte(d7) with info.
        .ENDM
        .MACRO  GETEXCEP 
        move.b  fpexcep(a3),D7
        .ENDM
        .MACRO  GETMODE 
        move.b   fpmode(a3),D7
        .ENDM
        .MACRO  GETTRAP 
        move.b   fptrap(a3),D7
        .ENDM
        .MACRO  GETTEXCEP 
        move.b  texcep(a3),D7
        .ENDM
        .MACRO  SETCOND 
        move.b   D7,fpcond(a3)
        .ENDM
        .MACRO  SETEXCEP 
        move.b     D7,fpexcep(a3)
        .ENDM
        .MACRO  SETMODE 
        move.b   D7,fpmode(a3)
        .ENDM
        .MACRO  SETTRAP 
        move.b     D7,fptrap(a3)
        .ENDM
        .MACRO  SETTEXCEP 
        move.b     D7,texcep(a3)
        .ENDM
                                ; Macro SETEX sets the exception whose bit
                                ; number is %1.
        .MACRO  SETEX
        bset    #%1,texcep(a3)  ; Turn on bit in TEXCEP.
        .ENDM
        
                                ; Macro RETURNB sets the result in D0,D1,D2
                                ; to the argument B in D3,D4,D5.
        .MACRO  RETURNB
        MOVE.L  D3,D0
        MOVE    D4,D1
        MOVE.L  D5,D2
        .ENDM
                        ; Macro SETIOC sets the IOP-CODE in FPCOND
                        ; according to %1.
        .MACRO  SETIOC
        GETCOND
        ROR     #2,D7           ; Align IOP-CODE field in byte.
        ANDI    #$FFF0,D7       ; Clear IOP-CODE field.
        ORI     #%1,D7          ; Insert new IOP-CODE.
        ROL     #2,D7           ; Realign IOP-CODE.
        SETCOND
        .ENDM

                        ; Macro MAKENAN creates a non-trapping NAN in 
                        ; %1, %2, and %3 using the iop code in %4.
                        ; The iop code is placed in bits 8-15 of the
                        ; significand and in FPCOND.
        .MACRO  MAKENAN
        SETEX   XIOP            ; Set exception flag.
        SETIOC  %4              ; Set IOP-CODE.
        MOVE.L  #%4,%3          ; %3 gets IOP-CODE.
        SWAP    %3           ; Align iop code for NAN.
        MOVE    #KNAN,%1        ; Set NAN kind.
        .ENDM
                                ; Macro CHOOSENAN operates upon A in D0,D1,D2,
                                ; and B in D3,D4,D5.   
                                ; At least one of A or B must be a NAN.
                                ; The result R in D0,D1,D2 is set to whichever
                                ; is a NAN, unless they both are; then R
                                ; is set to whichever has the greater 
                                ; significand.
        .MACRO  CHOOSENAN
        CMP     D0,D3
        BEQ.S   @22             ; Branch if both NAN.
        BLT.S   @23             ; Branch if A is the NAN.
@24     RETURNB                 ; B is the NAN.
        BRA.S   @23
@22     CMP.L   D2,D5           ; Compare significands.
        Bgt.S   @24             ; If B is the larger, return it.
@23
        .ENDM
                        ; Macro ENDOP does the end of operation exception
                        ; handling.  The exceptions are ORed into FPEXCEP.
                        ; Then any traps due are taken by calling F32_TRAP.
                        ; A3 is assumed to be pointing to FPCB by the time
                        ; endop is executed.
        .MACRO  ENDOP
        move.b  texcep(a3),d7
        or.b    d7,fpexcep(a3)
        and.b   fptrap(a3),d7   ; Get trap set.
        beq.s   @25             ; Skip following if no enabled traps occurred.
        .ref    f32_trap
        movem.l a0-a7/d0-a7,-(sp)
        jsr     f32_trap
        movem.l (sp)+,a0-a7/d0-d7
@25
        .ENDM

                                ; Macro NORMQ quick normalizes operands known
                                ; not to be zero or normalized.
        .MACRO  NORMQ
@20     SUBQ    #1,%2
        LSL.L   #1,%1
        BPL.S   @20
        .ENDM
                                ; Macro NORM
                                ; Left normalizes significand in %1,
                                ; keeping count in %2.
        .MACRO  NORM
        TST.L   %1
        Ble.S   @20             ; Branch if already normalized.
        NORMQ   %1,%2
@20
        .ENDM
                                ; Macro UNPACKR unpacks a 32 bit float X
                                ; in %1 into:
                                ; lsw(%1) := kind(X)
                                ;    KZERO, KFINITE, KINF, or KNAN
                                ; msb(%1) := sign(X)
                                ; lsw(%2) := unbiased exponent(X)
                                ;   exponent only defined for KFINITE
                                ; %3      := significand(X), left justified.
                                ;               Binary point is between bits
                                ;               30 and 31.
                                ;   significand only defined for KFINITE
                                ;   and NAN.
        .MACRO  UNPACKR
        MOVE.L  %1,%3           ; %3 gets X.
        ROL.L   #8,%3           ; Rotate to align significand.
        MOVE.L  %3,%2           ;
        ANDI.L  #$7FFFFF00,%3   ; %3 contains significand only.
        ROL.L   #1,%2           ; Align exponent.
        ANDI    #$FF,%2         ; %2 contains biased exponent only.
        BNE.S   @60         
        TST.L   %3              ; X has minimum exponent.
        BNE.S   @61
        MOVE    #KZERO,%1        ; True zero - set kind.
        BRA.S   @69
@61 MOVE    #-126,%2        ; Denormalized - set exponent.
        GETMODE
        BTST  #MODEN,D7
        BEQ.S   @62         ; Branch if warning mode.
        NORMQ   %3,%2           ; Normalize.
        BRA.S   @62
@60 CMPI    #$FF,%2         ; Not minimum exponent.
        BNE.S   @64         ; Branch if not maximum exponent.
        TST.L   %3              ; INF or NAN.
        BNE.S   @63            ; Branch if NAN.
        MOVE    #KINF,%1         ; INF - set kind.
        BRA.S   @69
@63    MOVE    #KNAN,%1         ; NAN - set kind.
        BTST  #TRAPBITNO,%3   ; Check trapping bit.
        BEQ.S   @69         ; Branch if not trapping NAN.
        BCLR    #TRAPBITNO,%3   ; Clear trapping bit.
        tst.l   %3      
        bne.s   @631            ; Branch if clearing trapping bit created
                                ; a NAN with zero significand.
        move    #nantrap,%3      ; %3 gets 0005.
        swap    %3              ; %3 gets 0500.
@631    
        setex   xiop            ; Set invalid exception.
        setioc  nantrap          ; Set trapping NAN code.
        BRA.S   @69
@64      SUBI    #127,%2         ; Normalized - remove exponent bias.
        BSET  #31,%3          ; Set implicit bit of significand.
@62 MOVE    #KFINITE,%1      ; Set kind.
@69
        .ENDM

                        ; Macro RIGHT does sticky right shift of %1 
                        ; according to count in lsw(%2).
                        ; Uses msw(D7) for scratch.
        .MACRO  RIGHT
        CMPI    #31,%2
        BGE.S   @30            ; Branch if shift count >= 31.
        TST     %2
        BLE.S   @31         ; Return if shift count <= 0.
        cmpi    #16,%2          
        blt.s   @32          ; Branch if shift count <= 15.
        tst     %1
        beq.s   @33         ; Branch if lsw(%1) = 0.
        clr     %1              ; Clear lsw(%1).
        swap    %1              ; Right shift %1 by 16.
        bset    #0,%1           ; Turn on sticky bit of %1.
        bra.s   @34
@33      swap    %1              ; Right shift %1 by 16.
@34  subi    #16,%2          ; Decrement shift count by 16.
        beq.s   @31             ; Exit if done shifting.
@32  swap    d7              ; Save flags in msw(D7).
        clr     d7              ; Clear lsw(d7).
        bset    %2,d7           ; Set bit whose bit number = shift count.
        subq    #1,d7           ; Form a mask of bits due to be shifted.
        and     %1,d7           ; D7 gets all the shifted bits.
        beq.s   @35            ; Branch if all shifted bits zero.
        bset    %2,%1           ; Set the sticky bit on.
@35    swap    d7              ; Restore lsw(d7).
        LSR.L   %2,%1           ; Do right shift.
        BRA.S   @31
@30    TST.L   %1              ; Big shift - result is either 0 or 1.
        BEQ.S   @31
        MOVEQ  #1,%1           ; Set bit 0 to 1 if any bit on.
@31
        .ENDM
                        ; Macro DORN does round to nearest.
                        ; %1 is ignored.
                        ; lsw(%2) is the exponent.
                        ; %3 is the significand.
        .MACRO  DORN
        ADDI.L  #$80,%3         ; Add the round bit.
        BCC.S   AMBIG          ; Branch if no carry out.
        BSET  #31,%3          ; Restore carry out.
        ADDQ    #1,%2           ; Increment exponent.
        BRA.S   DORNRET
AMBIG  TST.B   %3              ; Test lower byte.
        BNE.S   DORNRET         ; Branch if not ambiguous case.
        BCLR  #8,%3           ; Set least significant bit even.
DORNRET
        .ENDM
                        ; Macro DORO does round away from zero.
                        ; %1 is ignored.
                        ; lsw(%2) is the exponent.
                        ; %3 is the significand.
        .MACRO  DORO
        ADDI.L  #$FF,%3         ; Force round out if any bit on.
        BCC.S   RORET         ; Branch if no carry out.
        BSET  #31,%3          ; Restore carried out bit.
        ADDQ    #1,%2           ; Increment exponent.
RORET
        .ENDM
                        ; Macro roundf32 does rounding operation according to
                        ; mode bits.
                        ; msb(%1) is the sign.
                        ; lsw(%2) is the exponent.
                        ; %3 is the significand.
        .MACRO  roundf32
        TST.B   %3              ; Check extra bits.
        BEQ.S   ROUNDRET
        SETEX   XINEX           ; Set inexact flag.
        GETMODE
        ANDI    #MODER,D7       ; D7 gets rounding mode bits.
        BEQ.S   RNr
        CMPI    #2,D7
        BLT.S   CLEAR          ; RZ=1.
        BEQ.S   RPs             
        TST.L   %1              ; RM=3.
        BGE.S   CLEAR
DODORO DORO    %1,%2,%3        ; Round outward.
        BRA.S   CLEAR
RPs    TST.L   %1              ; RP=2.
        BGE.S   DODORO         ; Round out if positive.
        BRA.S   CLEAR
RNr    DORN    %1,%2,%3        ; RN=0.
CLEAR  CLR.B   %1              ; Clear out extra bits.
ROUNDRET
        .ENDM
                        ; Macro PACKRR packs a 32 bit float R into %1 from
                        ; kind(R) := lsw(%1)
                        ; sign(R) := msb(%1)
                        ; unbiased exponent(R) := lsw(%2)
                        ; significand(R) := %3
        .MACRO  PACKRR
        bra.s   packstart         ; Branch around Round code.
packround roundf32 %1,%2,%3        ; Round result.
        rts
packstart
        CMP     #KFINITE,%1
        BEQ.S   @62 
        BLT.S   @65
        CMP     #KINF,%1
        BNE.S   @63
@66    MOVE.L  #$7F800000,%3   ; INFINITY - set packed exp and significand.
        BRA   @67
@63    MOVE    #$FF,%2         ; NAN - set max exponent.
        ANDI.L  #$7FFFFF00,%3   ; Clear extra significand bits.
        BNE   @68
        MOVE.L  #$3FFFFF00,%3   ; Generate nonzero, nontrapping NAN signif.
        BRA   @68
@65   ANDI.L  #$80000000,%1   ; Normal ZERO - clear all but sign.
        BRA   @82
@62 CMPI    #-126,%2
        BGE.S   @69          ; Branch if no underflow occurred.
        GETTRAP
        BTST  #XUNFL,D7       
        BEQ.S   @78        ; Branch if underflow trap disabled.
        SETEX   XUNFL           ; Underflow trap enabled - set excep flag.
        ADDI    #192,%2         ; Add exponent offset.
        CMPI    #-126,%2
        BLT     @81           ; Branch if severe underflow.
        TST.L   %3
        BPL     @81             ; Unnormalized causes invalid result.
        BRA.S   @69
@78     ADDI   #126,%2         ; %2 gets 126+exponent.
        NEG     %2              ; %2 gets -126-exponent = shift count.
        RIGHT   %3,%2        ; Right shift to denormalize.
        MOVE    #-126,%2        ; %2 gets minimum exponent.
        tst.b   %3
        beq.s   @72
        SETEX   XUNFL           ; Underflow flag set in RN mode.
@69     bsr.s   packround
@83
        CMPI    #128,%2
        BLT.S   @72          ; Branch if no overflow occurred.
        SETEX   XOVFL           ; Set overflow flag.
        GETTRAP
        BTST    #XOVFL,D7
        BEQ.S   @70        ; Branch if overflow trap is disabled.
        SUBI    #192,%2         ; Subtract exponent offset.
        CMPI    #128,%2         ; Check for severe overflow.
        BGE.s   @81            ; If severe, return invalid result.
        BRA.S   @72
@70     SETEX   XINEX           ; Set inexact flag.
        GETMODE
        ANDI    #MODER,D7
        BEQ.S   @66        ; Branch to usual overflow treatment if RN.
        CMPI    #2,D7
        BLT.S   @71        ; Special treatment if RZ.
        BEQ.S   @74
        TST.L   %1              ; RM mode - check sign.
        BMI.S   @66        ; Branch to usual treatment if neg in RM.
@71     MOVE    #$FE,%2         ; %2 gets largest finite exponent.
        MOVE.L  #$FFFFFF00,%3   ; %3 gets largest finite significand.
        BRA.s   @68
@74     TST.L   %1
        Bmi.S   @71        ; Branch to special treatment if neg in RP.
        BRA.S   @66            ; Return infinity if pos in RP.
@72     TST.L   %3              ; Check for invalid result.
        BMI.S   @75       ; Branch if normalized.
        CMPI    #-126,%2
        BLE.S   @77           ; Branch if exponent <= -126.
@81     GETTEXCEP               ; Invalid result processing.               
        BCLR  #XINEX,D7       ; Turn off inexact exception.
        SETTEXCEP
        MAKENAN %1,%2,%3,nanresult  ; Make it into a NAN.
        BRA.S   @63
@77   CLR     %2              ; Set minimum exponent.
        BRA.S   @68
@75 ADDI  #127,%2         ; Add exponent bias.
@68   ANDI.L  #$FF,%2         ; Clear extra exponent bits.
        ROR.L   #1,%2           ; Align exponent with significand.
        ANDI.L  #$7FFFFF00,%3   ; Clear msb of significand.
        OR.L    %2,%3           ; Join exp and significand.
        ROR.L   #8,%3           ; Align exp and significand with sign.
@67  ANDI.L  #$80000000,%1   ; Clear all but sign bit from %1.
        OR.L    %3,%1           ; Join exp and signficand with sign.
@82 ENDOP                   ; Join current and past exceptions.
        .ENDM

; interal single extended format is
        ;       internal = record
        ;               exponent : integer ;
        ;               significand : longint ;
        ;               end ;
        
        ; Macro SAVERET saves the RTS return address in a0.
        .macro  saveret
        move.l  (sp)+,a0
        .ENDM
        
        ; Macro STOREX stores the extended in %2 and %3 into the address
        ; in %1, and then exits through a0.
        .macro  storex
        move    %2,(%1)+        ; Store exponent.
        move.l  %3,(%1)+        ; Store significand.
        jmp     (a0)            ; Exit from routine.
        .ENDM
        
        ; Macro XLOAD gets an address from %1, puts it in %2,
        ; and gets the extended from %2,
        ; putting the exponent in %3
        ; and the significand in %4.
        
        .macro  xload
        movea.l %1,%2           ; %2 gets address of x.
        move    (%2)+,%3        ; %3 gets exponent of x.
        move.l  (%2),%4         ; %4 gets significand of x.
        .ENDM
        
        
ÿ