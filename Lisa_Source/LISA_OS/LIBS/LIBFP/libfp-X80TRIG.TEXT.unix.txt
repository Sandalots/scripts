{ Copyright 1983, Apple Computer Inc. }


(* File L:X80Trig, Version 6 February 1983.  *)

procedure p0eval ( var p : x00array  ; var x : fp_extended ; 
        var z : fp_extended ) ;
        (* z := p(x)  = p[0] *)
begin
fpcb_.block.f[0] := p[0] ;
fpmovex( z ) ;
end ;

procedure p4eval ( var p : x04array  ; var x : fp_extended ; 
        var z : fp_extended ) ;
        (* z := p(x)  = p[0]*x)+p[1])*x+p[2])*x+...p[4] *)
var i : integer ;
fx : fp_register ;

begin
xmovefp1(x) ; (* FP1 gets x. *)
with fpcb_.block do
        begin
        fx := f[1] ; (* FX gets x.  *)
        f[0] := p[0] ;
        for i := 1 to 4 do 
                begin
                f[1] := fx ; (* FP1 gets x. *)
                fpmul ;
                f[1] := p[i] ;
                fpadd ;
                end ;
        end ;
fpmovex(z) ;
end ;

procedure p7eval ( var p : x07array  ; var x : fp_extended ; 
        var z : fp_extended ) ;
        (* z := p(x)  = p[0]*x)+p[1])*x+p[2])*x+...p[7] *)
var i : integer ;
fx : fp_register ;

begin
xmovefp1(x) ; (* FP1 gets x. *)
with fpcb_.block do
        begin
        fx := f[1] ; (* FX gets x.  *)
        f[0] := p[0] ;
        for i := 1 to 7 do 
                begin
                f[1] := fx ; (* FP1 gets x. *)
                fpmul ;
                f[1] := p[i] ;
                fpadd ;
                end ;
        end ;
fpmovex(z) ;
end ;

procedure q0eval ( var q : x00array  ; var x : fp_extended ; 
        var z : fp_extended ) ;
        (* z := q(x)  = x+q[0] *)
begin
fpcb_.block.f[1] := q[0] ;
xmovefp(x) ; (* FP0 gets x *)
fpadd ;
fpmovex( z ) ;
end ;

procedure q2eval ( var q : x02array  ; var x : fp_extended ; 
        var z : fp_extended ) ;
        (* z := q(x)  = x+q[0])*x+...q[2] *)
var i : integer ;
fx : fp_register ;

begin
xmovefp1(x) ; (* FP1 gets x.  *)
with fpcb_.block do
        begin (* with *)
        fx := f[1] ; (* FX gets x.  *)
        f[0] := q[0] ; (* FP0 gets q[0]. *)
        fpadd ;
        for i := 1 to 2 do 
                begin
                f[1] := fx  ;
                fpmul ;
                f[1] :=  q[i] ;
                fpadd ;
                end ;
        end   (* with *) ;
fpmovex(z) ;
end ;

procedure q7eval ( var q : x07array  ; var x : fp_extended ; 
        var z : fp_extended ) ;
        (* z := q(x)  = x+q[0])*x+...q[7] *)
var i : integer ;
fx : fp_register ;

begin
xmovefp1(x) ; (* FP1 gets x.  *)
with fpcb_.block do
        begin (* with *)
        fx := f[1] ; (* FX gets x.  *)
        f[0] := q[0] ; (* FP0 gets q[0]. *)
        fpadd ;
        for i := 1 to 7 do 
                begin
                f[1] := fx  ;
                fpmul ;
                f[1] :=  q[i] ;
                fpadd ;
                end ;
        end   (* with *) ;
fpmovex(z) ;
end ;

procedure pivalue ;
        (* Procedure to return constant PI *)
begin
z := fpkpi ;
end ;

procedure argred ( var x : fp_extended ; var r : fp_extended ; var q : quadrant ) ;
        (* Trigonometric argument reduction routine.  *)
        (* Computes R and Q so that
                X = R + Q * (pi/2) + N * (2*pi)
        with abs(X) <= pi/4.  *)
var
rt : fp_extended ;
begin (* argred *)
fpremx( x, fpk2pi, rt ) ; (* abs(rt) <= pi.  *)
fpremx( rt, fpkpi2, r ) ; (* abs(r) <= pi/4.  *)
fpsubx( rt, r, rt ) ;     (* rt gets q * pi/2.  *)
fpdivx( rt, fpkpi2, rt ) ; (* rt gets q.  *)
q := xmovew(rt) ; (* abs(q) <= 2.  *)
if (q<>0) and (fpkindx(r)=zero) then fpnegx(r,r) ;
        (* In some circumstances reverse sign of zero result. *)
clrexcepset ; (* Exceptions here don't matter.  *)
end   (* argred *) ;

procedure sinapprox ( var x : fp_extended ; var z : fp_extended ) ;
        (* Computes z := sinx(x) for abs(x) <= pi/4.  *)
        (* Uses approximation of form z := x * p(x^2) *)
        
var x2 : fp_extended ;
r, xoct : fp_extended ;

begin (* sinapprox *)
fpdivx(x,fpkpi4,xoct) ; (* Convert x to octants.  *)
fpmulx(xoct,xoct,x2) ; (* X2 gets xoct^2.  *)
p7eval(sinp,x2,r) ;
setxcpn( underfl, false ) ;
setxcpn( inxact, true ) ;
fpmulx(xoct,r,z) ; (* Z gets xoct * p(xoct^2).  *)
end   (* sinapprox *) ;

procedure cosapprox ( var x : fp_extended ; var z : fp_extended ) ;
        (* Computes z := cosx(x) for abs(x) <= pi/4.  *)
        (* Uses approximation of form z := 1-(x^2)*p(x^2)/q(x^2) *)

var xoct, x2, p, q, r: fp_extended ;

begin (* cosapprox *)
fpdivx(x,fpkpi4,xoct) ; (* Convert x to octants.  *)
fpmulx(xoct,xoct,x2) ;
p5eval(cosp,x2,p) ;
q0eval(cosq,x2,q) ;
fpmulx(x2,p,p) ;
fpdivx(p,q,r) ; (* r gets x^2 p(x^2)/q(x^2) *)
fpsubx(fpk1,r,z) ; (* Z gets 1 - above.  *)
setxcpn( underfl, false ) ;
setxcpn( inxact, true ) ;
end   (* cosapprox *) ;

procedure tanapprox ( var x : fp_extended ; var z : fp_extended ) ;
        (* Computes z := tanx(x) for abs(x) <= pi/4.  *)
        (* Uses approximation of form z := x * p(x^2)/q(x^2) *)
        
var xoct, x2, p, q : fp_extended ;

begin (* tanapprox *)
fpdivx(x,fpkpi4,xoct) ; (* Convert x to octants.  *)
fpmulx(xoct,xoct,x2) ; (* X2 gets x^2.  *)
p4eval(tanp,x2,p) ;
q2eval(tanq,x2,q) ;
fpmulx(p,xoct,p) ;
setxcpn( underfl, false ) ;
setxcpn( inxact, true ) ;
fpdivx(p,q,z) ;
end   (* tanapprox *) ;

procedure atancore ( var x : fp_extended ; var z : fp_extended ) ;
        (* Computes z := atan(x) in radians for abs(x) <= tanx(pi/16).  *)
        (* Uses approximation of form z := x * p / q(x^2) *)

var x2, p, q : fp_extended ;

begin (* atancore *)
fpmulx(x,x,x2) ; (* x2 gets x^2 *)
p0eval(atanp,x2,p) ;
q7eval(atanq,x2,q) ;
fpmulx(x,p,p) ;
setxcpn( underfl, false ) ;
setxcpn( inxact, true ) ;
fpdivx(p,q,z) ;
end   (* atancore *) ;

procedure atanapprox ( var x : fp_extended ; var z : fp_extended ) ;
        (* Computes z := atan(x) for finite x.  *)
        (* Uses argument reduction scheme from Hart:
                atan(x) = atan(xi) + atan((x-xi)/(1+x*xi)) *)

var p, q, r, ax, tt, xx : fp_extended ;

begin (* atanapprox *)
fpabsx(x,ax) ;
if fpcomx(ax,atanb1) <> greater then atancore(ax,r)
        else
        begin
        if fpcomx(ax,atanb4) <> lesser then
                begin
                tt := atant5 ;
                fpdivx(fpkm1,ax,xx) ;
                end
                else
                begin
                if fpcomx(ax,atanb2) <> greater then
                        begin
                        tt := atant2 ;
                        xx := atanx2 ;
                        end
                        else if fpcomx(ax,atanb3) <> greater then
                                begin
                                tt := atant3 ;
                                xx := atanx3 ;
                                end
                                else
                                begin
                                tt := atant4 ;
                                xx := atanx4 ;
                                end ;
                        fpsubx(ax,xx,p) ;
                        fpmulx(ax,xx,q) ;
                        fpaddx(fpk1,q,q) ;
                        fpdivx(p,q,xx) ;
                        end ;
        atancore(xx,r) ;
        fpaddx(r,tt,r) ;
        end ;
copysign(x,r,z) ;
end   (* atanapprox *) ;

procedure sinx (* x : fp_extended ; var z : fp_extended *) ; 
        (* z := sinx(x) *)

var
fpcb : fp_blocktype ;
q : quadrant ;
r : fp_extended ;
trapcoming : boolean ;

begin (* sinx *)
blockprelude(fpcb) ; (* Save environment, clear exceptions and traps.  *)
case fpkindx(x) of

zero :  begin (* zero *)
        r := x ;
        end   (* zero *) ;

NonNormal, normal : 
        begin (* NonNormal/normal *)
        argred(x,r,q) ;
                (* sinx(x +- pi/2) = +- cosx(x)
                   sinx(x +- pi) = - sinx(x) *)
        if abs(q) = 1 then cosapprox(r,r) else sinapprox(r,r) ;
        if (abs(q)=2) or (q=-1)   then fpnegx(r,r) ;
        end   (* NonNormal/normal *) ;

inf :   begin (* inf *)
        errornan(nantrig,r) ;
        end   (* inf *) ;
        
nan : checknan( x, r ) ;

end (* case *);
blockpostlude( fpcb, trapcoming ) ;
if trapcoming then postmonadic( 'sinx', x, r) ;
z := r ;
end   (* sinx *) ;

procedure cosx (* x : fp_extended ; var z : fp_extended *) ; 
        (* z := cosx(x) *)

var
fpcb : fp_blocktype ;
q : quadrant ;
r : fp_extended ;
trapcoming : boolean ;

begin (* cosx *)
blockprelude(fpcb) ; (* Save environment, clear exceptions and traps.  *)
case fpkindx(x) of

zero :  begin (* zero *)
        r := fpk1 ;
        end   (* zero *) ;

NonNormal, normal : 
        begin (* NonNormal/normal *)
        argred(x,r,q) ;
                (* cosx(x +- pi/2) = -+ sin(x)
                   cosx(x +- pi) = - cosx(x) *)
        if abs(q) = 1 then sinapprox(r,r) else cosapprox(r,r) ;
        if (abs(q)=2) or (q=+1)  then fpnegx(r,r) ;
        end   (* NonNormal/normal *) ;

inf :   begin (* inf *)
        errornan(nantrig,r) ;
        end   (* inf *) ;
        
nan : checknan( x, r ) ;

end (* case *);
blockpostlude( fpcb, trapcoming ) ;
if trapcoming then postmonadic( 'cosx', x, r) ;
z := r ;
end   (* cosx *) ;

procedure tanx (* x : fp_extended ; var z : fp_extended *) ; 
        (* z := tanx(x) *)

var
fpcb : fp_blocktype ;
q : quadrant ;
r : fp_extended ;
trapcoming : boolean ;

begin (* tanx *)
blockprelude(fpcb) ; (* Save environment, clear exceptions and traps.  *)
case fpkindx(x) of

zero :  begin (* zero *)
        r := x ;
        end   (* zero *) ;

NonNormal, normal : 
        begin (* NonNormal/normal *)
        argred(x,r,q) ;
                (* tanx(x +- pi/2) = -1/tanx(x) 
                   tanx(x +- pi  ) =    tanx(x) *)
        tanapprox(r,r) ;
        if abs(q) = 1 then fpdivx(fpkm1,r,r) ; (* tanx is -1/r *)
        end   (* NonNormal/normal *) ;

inf :   begin (* inf *)
        errornan(nantrig,r) ;
        end   (* inf *) ;
        
nan : checknan( x, r ) ;

end (* case *);
blockpostlude( fpcb, trapcoming ) ;
if trapcoming then postmonadic( 'tanx', x, r) ;
z := r ;
end   (* tanx *) ;

procedure asin (* x : fp_extended ; var z : fp_extended *) ; 
        (* z := asin(x) *)

var
fpcb : fp_blocktype ;
ax, p, q, r : fp_extended ;
trapcoming : boolean ;

begin (* asin *)
blockprelude(fpcb) ; (* Save environment, clear exceptions and traps.  *)
case fpkindx(x) of

zero :  begin (* zero *)
        r := x ;
        end   (* zero *) ;

NonNormal, normal : 
        begin (* NonNormal/normal *)
        fpabsx(x,ax) ;
        if fpcomx(ax,fpk1) = greater then errornan(naninvtrig,r) 
        else
                begin (* abs(x) <= 1 *)
                fpsubx(fpk1,x,p) ;
                fpaddx(fpk1,x,q) ; 
                fpmulx(p,q,r) ;
                fpsqrtx(r,r) ; (* Z gets sqrt(1-x)*(1+x) *)
                if fpcomx(ax,r) <> greater then
                                (* Choose form to minimize cancellation *)
                        begin (* Small x <= sqrt(.5) *)
                                (* asin(x) = sign(x) *  
                                atan(abs(x)/sqrt) *)
                        fpdivx(ax,r,r) ;
                        atanapprox(r,r) ;
                        end
                        else
                        begin (* Large x *)
                                (* asin(x) = sign(x) * 
                                pi/2 - atan(sqrt/abs(x)) *)
                        fpdivx(r,ax,r) ;
                        atanapprox(r,r) ;
                        fpsubx(fpkpi2,r,r) ;
                        end ;
                copysign(x,r,r) ;
                end   (* abs(x) <= 1 *) ;
        end   (* NonNormal/normal *) ;

inf :   begin (* inf *)
        errornan(naninvtrig,r) ;
        end   (* inf *) ;
        
nan : checknan( x, r ) ;

end (* case *);
blockpostlude( fpcb, trapcoming ) ;
if trapcoming then postmonadic( 'asin', x, r) ;
z := r ;
end   (* asin *) ;

procedure acos (* x : fp_extended ; var z : fp_extended *) ; 
        (* z := acos(x) *)

var
fpcb : fp_blocktype ;
ax, p, q, r  : fp_extended ;
trapcoming : boolean ;

begin (* acos *)
blockprelude(fpcb) ; (* Save environment, clear exceptions and traps.  *)
case fpkindx(x) of

zero :  begin (* zero *)
        r := fpkpi2 ;
        end   (* zero *) ;

NonNormal, normal : 
        begin (* NonNormal/normal *)
        fpabsx(x,ax) ;
        if fpcomx(ax,fpk1) = greater then errornan(naninvtrig,r) 
        else
                begin (* abs(x) <= 1 *)
                fpsubx(fpk1,x,p) ;
                fpaddx(fpk1,x,q) ; 
                fpmulx(p,q,r) ;
                fpsqrtx(r,r) ; (* Z gets sqrt(1-x)*(1+x) *)
                if fpcomx(ax,r) <> greater then
                                (* Choose formula to minimize cancellation *)
                        begin (* Small x *)
                                (* acos(x) = pi/2 - atan(x/sqrt) *)
                        fpdivx(x,r,r) ;
                        atanapprox(r,r) ;
                        setxcpn( underfl, false ) ; (* Can't happen. *)
                        fpsubx(fpkpi2,r,r) ;
                        end
                        else
                        begin (* Large x *)
                        if fpminusx(x) then 
                                begin (* Large negative *)
                                        (* acos(x) = pi + atan(sqrt/x) *)
                                fpdivx(r,x,r) ;
                                atanapprox(r,r) ;
                                fpaddx(fpkpi,r,r) ;
                                end   (* Large negative *)
                                else
                                begin (* Large positive *)
                                        (* acos(x) = atan(sqrt/x) *)
                                fpdivx(r,x,r) ;
                                atanapprox(r,r) ;
                                end   (* Large positive *) ;
                        end ;
                end   (* abs(x) <= 1 *) ;
        end   (* NonNormal/normal *) ;

inf :   begin (* inf *)
        errornan(naninvtrig,r) ;
        end   (* inf *) ;
        
nan : checknan( x, r ) ;

end (* case *);
blockpostlude( fpcb, trapcoming ) ;
if trapcoming then postmonadic( 'acos', x, r) ;
z := r ;
end   (* acos *) ;

procedure atan (* x : fp_extended ; var z : fp_extended *) ; 
        (* z := atan(x) *)

var
fpcb : fp_blocktype ;
r : fp_extended ;
trapcoming : boolean ;

begin (* atan *)
blockprelude(fpcb) ; (* Save environment, clear exceptions and traps.  *)
case fpkindx(x) of

zero :  begin (* zero *)
        r := x ;
        end   (* zero *) ;

NonNormal, normal : 
        begin (* NonNormal/normal *)
        atanapprox(x,r) ;
        end   (* NonNormal/normal *) ;

inf :   begin (* inf *)
                (* According to Kahan, treat projective infinity same
                as affine *)
        copysign( x, fpkpi2, r ) ;
        end   (* inf *) ;
        
nan : checknan( x, r ) ;

end (* case *);
blockpostlude( fpcb, trapcoming ) ;
if trapcoming then postmonadic( 'atan', x, r) ;
z := r ;
end   (* atan *) ;

ÿ