        .title 'BINSTR (3.2) SUBROUTINE  - %H% - %T% - %I%'
        PAGE
;
        .proc   mc%BINSTR  ; 0,0
;
;
;*******************************************************************************
;
;       REVISION      DATE        DESCRIPTION
;       --------      ----        -----------
;         1.1       14.JAN.81      ORIGINAL    ??? OPTIMIZE BY DEPENDING ON
;                                              BCD STRING TO NEVER OVERFLOW -
;                                              THEN ANDI TO CCR NOT NEEDED &
;                                              ORI TO CCR CAN BE REPLACED BY
;                                              A VARIABLE 'ONE' IN UPPER HALF
;                                              OF REG D0.
;
;         1.2       24.JAN.81      ***  MICROSYSTEMS RELEASE  ***
;                                  RESIDENT ASM WILL NOT ACCEPT 'ANDI.B #,CCR'-
;                                  IT WANTS 'ANDI.B #,SR'; DITTO WITH THE
;                                  'ORI.B #,CCR'.
;                                  ADDED COMMENTS ABOVE THESE TWO INSTRUCTIONS
;                                  CONCERNING CHANGES FOR CROSS ASM
;
;         1.3       13.FEB.81      PERMENENTLY CONVERTED TO RESIDENT ASM FORM
;
;         3.1        7.JUL.81      CHANGED TO EXORMACS STRUCTURED ASM SYNTAX
;
;         3.2       21.SEP.81      ONLY ADDED A COMMENT
;
;                   13 Jan 83      Changed comments to reflect max 18 digits output.
;
;*******************************************************************************
;                            ****************
;                            *   ATTENTION  *
;                            ****************
;
;       THIS SUBROUTINE MUST BE CHANGED TO BE ASSEMBLED ON THE CROSS
;       ASSEMBLER.  THE CROSS ASSEMBLER WILL NOT WORK WITH WHAT THE
;       RESIDENT WANTS.
;
;       SEE COMMENTS BELOW MARKED @@@@.
;
;
;*******************************************************************************
;
;
;
;                         BINSTR SUBROUTINE
;
;
;       BINSTR IS CALLED BY BINDEC TO CONVERT A BINARY FP NUMBER WHICH HAS
;       BEEN ROUNDED TO AN INTEGER INTO A PACKED BCD STRING.
;
;       ENTER WITH:           D0.W     CONTAINS 'LOOPCNT', ONE LESS THAN
;       ==========                     THE NUMBER OF TIMES THE MAIN BINSTR
;                                      LOOP MUST BE EXECUTED.
;
;                                      WHEN CONVERTING (OUTPUT MANTISSA STRING),
;                                      'LOOPCNT'  =  {EXP. OF ABS[INT('Y')]
;                                                                 IN FPN-REG)}
;
;                                      WHEN CONVERTING (OUTPUT EXPONENT STRING),
;                                      'LOOPCNT'  =  15 - (NUMBER OF SHIFTS
;                                                          REQUIRED TO LEFT
;                                                          JUSTIFY ABS('ILOG')
;                                                          IN FPN-REG MANTISSA
;                                                          FIELD)
;
;                          FPN_REG     CONTAINS THE BINARY FP NUMBER TO
;                                      BE CONVERTED TO A PACKED BCD STRING.
;
;                                      WHEN CONVERTING (OUTPUT MANTISSA STRING),
;                                      THIS SHOULD CONTAIN ABS[INT('Y')].
;
;                                      WHEN CONVERTING (OUTPUT EXPONENT STRING),
;                                      THIS SHOULD CONTAIN ABS('ILOG') LEFT
;                                      JUSTIFIED IN THE MANTISSA FIELD.  THE
;                                      SIGN, TAG, & EXPONENT FIELD ARE DON'T
;                                      CARES SINCE WHAT WOULD BE THE EXPONENT
;                                      IS PASSED IN 'LOOPCNT' AS EXPLAINED ABOVE
;
;       EXIT WITH:
;       =========
;
;                             D0-D7    DESTROYED
;
;                             A0-A7    UNCHANGED
;
;
;
;       SUBROUTINES CALLED:   NONE
;       ==================
;
;       FLAGS WHICH MAY BE SET:         NONE
;       ======================
;
;
;
 PAGE
;*******************************************************************************
;
;
;      OVERVIEW OF ALGORITHM
;
;
;      SINCE THE INPUT TO 'BINSTR' IS GUARANTEED TO HAVE ALREADY BEEN ROUNDED
;      TO AN INTEGER, THE 64 BIT MANTISSA FIELD OF THE FPN-REG CAN BE VIEWED
;      AS CONTAINING A BINARY INTEGER, LEFT JUSTIFIED, OF LENGTH ['LOOPCNT' + 1]
;
;      THIS VIEW OF 'BINSTR' SHOWS THAT IT ACTUALLY CONVERTS A POSITIVE
;      BINARY INTEGER, WHICH JUST HAPPENS TO BE STORED IN THE MANTISSA FIELD
;      OF FPN-REG, INTO A PACKED BCD STRING.
;
;      THE BCD ARRAY STARTS OUT AS ALL ZEROES.  EACH TIME THRU THE LOOP
;      IT IS MULTIPLIED BY TWO (BY ADDING THE ARRAY TO ITSELF). IF THE
;      BINARY INTEGER BIT IN FPN-REG BEING EXAMINED IS A BINARY '1' THEN
;      THE BCD ARRAY HAS BCD '1' ADDED TO IT BEFORE LOOPING BACK.
;      THE BINARY INTEGER BITS IN FPN-REG ARE SCANNED FROM LEFT TO RIGHT; IN
;      OTHER WORDS STARTING WITH THE MOST SIGNIFICANT FIRST SINCE THE BINARY
;      INTEGER IS LEFT JUSTIFIED IN THE FIELD.
;
;      THE CONVERSION IS COMPLETED WHEN ['LOOPCNT' +1] ITERATIONS THRU
;      THE MAIN LOOP HAVE BEEN COMPLETED (THIS IS DUE TO THE NATURE OF THE
;      DBCC INSTRUCTION).
;
;      THIS ENTIRE ALGORITHM IS PREDICATED UPON BINDEC HAVING ALREADY
;      SCALED AND ROUNDED TO AN INTEGER THE BINARY VALUE TO BE CONVERTED.
;      THE SCALING HAS GUARANTEED THAT THE (OUTPUT MANTISSA STRING) WILL
;      BE AT MOST 18 BCD DIGITS LONG WHICH CAN BE REPRESENTED EXACTLY IN
;      60 BINARY BITS (HENCE MAX_'LOOPCNT' IS 59);  THE (OUTPUT EXPONENT
;      STRING) IS GUARANTEED TO FIT INTO 4 BCD DIGITS.
;
;      THIS ALGORITHM HAS BEEN OPTIMIZED FOR CONVERTING THE (OUTPUT
;      MANTISSA STRING) SINCE IT IS THE LONGER.  INSPECTION WILL SHOW THAT
;      THE BCDBL AND BCDINC SECTIONS OF THE ALGORITHM ALWAYS WORK ON 10
;      BCD DIGITS BEFORE TESTING TO SEE IF THE OTHER 8 BCD DIGITS ARE
;      INVOLVED.  SINCE ANY EARLIER TEST WOULD INVOLVE, AT BEST, A 10 CYCLE
;      CONDITIONAL BRANCH, THIS EXTRA PRECISION DOES NOT ACTUALLY COST THAT
;      MUCH WHEN CONVERTING THE (OUTPUT EXPONENT STRING) AND MAKES THE
;      (OUTPUT MANTISSA STRING) CONVERSION THAT MUCH FASTER.
;
;      NOTE THAT THE FIRST ITERATION SKIPS OVER THE 'BCDBL' SINCE A STRING
;      OF '0'S DOUBLED IS STILL A STRING OF '0'S.  FOR NORMALIZED INTEGERS
;      THE SECOND ITERATION WILL ALWAYS INVOLVE DOUBLING A NON-ZERO STRING.
;      UNNORM INTEGERS CAN LIVE WITH THE SPEED PENALTY BUT THE RESULT WILL
;      STILL BE VALID.
;
;*******************************************************************************
;
;
;      VARIABLES AND REGISTER USAGES
;
;
;      TO USE THE 'BTST' INSTRUCTION TO SCAN THE MANTISSA BITS IN THE FPN-REG
;      NEED TWO VARIABLES;  'WHATBYTE' IS AN OFFSET TO THE FPN-REG POINTER
;      INDICATING WHICH OF THE 8 BYTES IN THE 64 BIT MANTISSA FIELD IS
;      BEING 'BTST'ED.  'TSTBIT' INDICATES WHICH OF THE BITS IN THE BYTE
;      POINTED TO BY ['WHATBYTE' + 'FRACT'](A2) IS BEING 'BTST'ED.
;
;      INITIALIZE 'WHATBYTE' = 0
;                   'TSTBIT' = 7
;
;      THE VARIABLE 'ZERO' IS A BYTE OF ZEROES USED IN ADDING BCD '1' TO
;      THE BCD ARRAY.
;
;      THE BCD ARRAY IS SCATTERED THRU THE REMAINING DATA REGISTERS TO
;      TAKE ADVANTAGE OF THE REGISTER DIRECT FORM OF THE 'ABCD' INSTRUCTION.
;
;      CONSIDER THE PACKED 18 BCD DIGIT (OUTPUT MANTISSA STRING) TO BE
;      COMPOSED OF 9 PACKED BYTES WITH 2 BCD DIGITS PACKED INTO EACH BYTE.
;      THE FOLLOWING DIAGRAM SHOWS THE NOTATION USED.
;
;       PACKED 18 BCD DIGIT STRING
;
;        MS                                                               LS
;       +---+---+---+---+---+---+---+---+---+---+---+---+---+---+---+---+---+
;       |   |   |   |   |   |   |   |   |   |   |   |   |   |   |   |   |   |
;       +---+---+---+---+---+---+---+---+---+---+---+---+---+---+---+---+---+
;     ^   ^   ^   ^   ^   ^   ^   ^   ^   ^   ^   ^   ^   ^   ^   ^   ^   ^
;     |   |   |   |   |   |   |   |   |   |   |   |   |   |   |   |   |   |
;     +-+-+   +-+-+   +-+-+   +-+-+   +-+-+   +-+-+   +-+-+   +-+-+   +-+-+
;       |       |       |       |       |       |       |       |       |
;      B9      B8      B7      B6      B5      B4      B3      B2      B1
;
;
;
;
;       DATA REGISTER USAGES FOR FDIV  WITH INITIAL VALUES
;       ==================================================
;
;             31              15      7       0
;             |---------------|-------|-------|
;        D0   |               |    *******    |  0 <= 'LOOPCNT' <= 59
;             |---------------!-------|-------|
;        D1   |  'ZERO' = 0   |    $0000      |  'WHATBYTE'
;             |-------|-------|-------|-------|
;        D2   |       |       |       |   7   |  'TSTBIT'
;             |-------|-------|-------|-------|
;        D3   |       |       |       |  B5   |  <-+
;             |-------|-------|-------|-------|    |
;        D4   |       |  B9   |       |  B4   |    |
;             |-------|-------|-------|-------|    |
;        D5   |       |  B8   |       |  B3   |    +- 18 DIGIT BCD STRING
;             |-------|-------|-------|-------|    |
;        D6   |       |  B7   |       |  B2   |    |  INITIALIZED TO ALL '0'S
;             |-------|-------|-------|-------|    |
;        D7   |       |  B6   |       |  B1   |  <-+
;             |-------|-------|-------|-------|
;
;
;      WHAT IS THE CONDITIONAL TEST IN 'BCDBL' ON 'WHATBYTE' FOR?
;
;      SINCE THE BINARY INTEGER 2^33 WILL CONVERT TO NO MORE THAN 10 BCD
;      DIGITS ANYTIME 'LOOPCNT' <= 32, THE UPPER 8 BCD DIGITS WILL NEVER BE
;      INVOLVED IN THE ALGORITHM.  ANOTHER WAY OF STATING THIS IS THAT
;      ANYTIME 'WHATBYTE' >= 4, THE UPPER 8 BCD DIGITS WILL BE
;      INVOLVED IN THE ALGORITHM (EXPLANATION FOLLOWS).
;
;         LOOK AT THE ALGORITHM FOR THE WORST CASE HERE AND IGNORE THE INITIAL
;         JUMP INTO THE MIDDLE OF THE LOOP AS A SIMPLE OPTIMIZATION.
;         THAT IS, WHERE THE STRING IS DOUBLED AND HAS '1' ADDED
;         IN EVERY ITERATION.  AFTER THE 33RD ITERATION THE BCD STRING HAS
;         REACHED THE POINT WHERE THE NEXT ITERATION SHOULD INVOLVE THE UPPER
;         8 BCD DIGITS IN THE DOUBLING; ON THE 33RD ITERATION THE 33RD INTEGER
;         BIT WAS TESTED FOR 'BCDINC'.  SINCE THE 33RD INTEGER BIT COUNTING
;         FROM THE LEFT IS IN THE 5TH BYTE COUNTING FROM THE LEFT, 'WHATBYTE'
;         WAS INCREMENTED TO '4' DURING THE 32ND ITERATION.  BY TESTING FOR
;         'WHATBYTE' >= 4 TO DETERMINE WHEN TO INVOLVE THE UPPER 8 BCD DIGITS
;         IN THE DOUBLING, WE ARE GUARANTEED TO START INVOLVING THESE DIGITS
;         ON THE 33RD ITERATION WHICH IS ONE ITERATION BEFORE IT IS NECESSARY.
;
;      A SIMILAR CONDITIONAL IS FOUND IN 'BCDINC' TO PREVENT THE UPPER 8
;      BCD DIGITS FROM BEING INVOLVED IN THE INCREMENT UNLESS THERE IS A
;      CARRY OUT OF THE 10TH BCD DIGIT.
;
 PAGE
;*******************************************************************************
;
;
;
;                     ************************
;                     *  BINSTR STARTS HERE  *
;                     ************************
;
;
        debugbegin
;
;
;      INITIALIZE THE VARIABLES AND THE BCD STRING.
;
;
        moveq    #0,D1                       ; 'WHATBYTE' = 'ZERO' = 0
        moveq    #7,D2                       ; 'TSTBIT' = 7
        moveq    #0,D3                       ; 'BCD STRING' = 0
        moveq    #0,D4
        moveq    #0,D5
        moveq    #0,D6
        moveq    #0,D7
        BRA.S    BCDINC                      ; SKIP 'BCDBL' THE FIRST TIME
;                                              THRU SINCE (BCD STRING = 0)
;                                              DOUBLED IS STILL = 0.
;
;
;       CONVERT THE LEFT JUSTIFIED INTEGER IN THE FPN-REG'S MANTISSA FIELD
;       INTO A BCD STRING.
;
;
BD_CNVT EQU      *
;
;*******************************************************************************
;
;       @@@@ TO RUN ON CROSS ASSEMBLER - CHANGE THE NEXT INSTRUCTION TO
;            'ANDI.B   #$0F,CCR'
;
;*******************************************************************************
;
BCDBL   ANDI.B   #$0F,ccr                    ; CLEAR X-BIT IN CCR
        ABCD     D7,D7                       ; MULTIPLY THE BCD STRING BY 2
        ABCD     D6,D6                       ; BY ADDING IT TO ITSELF
        ABCD     D5,D5
        ABCD     D4,D4
        ABCD     D3,D3
;
;       SEE EXPLANATION ON THIS CONDITIONAL ABOVE.
;
        cmp.w   #4,d1                         ; IF ('WHATBYTE >=4) THEN
        blt     @1
           SWAP     D7                          ; UPPER 8 BCD DIGITS MUST
           SWAP     D6                          ; NOW BECOME INVOLVED IN
           SWAP     D5                          ; THE DOUBLING.
           SWAP     D4
           ABCD     D7,D7
           ABCD     D6,D6
           ABCD     D5,D5
           ABCD     D4,D4
           SWAP     D7
           SWAP     D6
           SWAP     D5
           SWAP     D4
@1                                         ; END
;
;
;      ADJUST 'WHATBYTE' IF NECESSARY.
;
;
        SUBQ.B   #1,D2                       ; DECREMENT 'TSTBIT'
        bpl      @2                          ; IF (NEXT INTEGER BIT IN NEXT BYTE
           moveq    #7,D2                       ; RE-INITIALIZE 'TSTBIT'
           ADDQ.B   #1,D1                       ; INCREMENT 'WHATBYTE'
@2                                           ; END
;
;
;      CHECK THE BINARY BIT CURRENTLY POINTED TO BY ['WHATBYTE' + 'FRACT'](A2).
;      IF IT IS A '1' THEN ADD BCD '1' TO THE LEAST SIGNIFICANT DIGIT OF THE
;      BCD STRING AND PROPOGATE ANY CARRY UP THRU THE STRING.
;
;
BCDINC  BTST     D2,FRACT(A2,D1.W)
        beq      @4                             ; IF (CURRENT BINARY BIT IS A '1')
;
;*******************************************************************************
;
;       @@@@ TO RUN ON CROSS ASSEMBLER - CHANGE THE NEXT INSTRUCTION TO
;            'ORI.B    #$10,CCR'
;
;*******************************************************************************
;
           ORI.B    #$10,ccr                    ; SET THE X-BIT IN CCR
           SWAP     D1                          ; ACCESS THE 'ZERO' BYTE
           ABCD     D1,D7                       ; ADD BCD '1' TO BCD STRING
           ABCD     D1,D6
           ABCD     D1,D5
           ABCD     D1,D4
           ABCD     D1,D3
           bcc      @3
              SWAP     D7                          ; PROPOGATE CARRY ONLY WHEN
              SWAP     D6                          ; NEEDED
              SWAP     D5
              SWAP     D4                          ; NOTE:  SINCE 'BCD' DIGIT
              ABCD     D1,D7                       ; ARRAY WAS JUST DOUBLED,
              ABCD     D1,D6                       ; ADDING A 'BCD' 1 TO THE
              ABCD     D1,D5                       ; LS DIGIT CAN NEVER CAUSE
              ABCD     D1,D4                       ; A CARRY INTO NEXT LS
              SWAP     D7                          ; DIGIT; HOWEVER, CODE IS
              SWAP     D6                          ; LEFT UNCHANGED FOR NOW.
              SWAP     D5
              SWAP     D4
@3
           SWAP     D1                          ; RESTORE 'WHATBYTE' TO D1
@4
;
        DBRA     D0,BD_CNVT                  ; EXECUTES LOOP ['LOOPCNT' + 1]
;                                              TIMES
;
        RTS
        debugend 'MC%BINST',0

ÿ