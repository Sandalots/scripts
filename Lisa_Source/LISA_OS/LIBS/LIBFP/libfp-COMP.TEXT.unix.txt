      .proc   mc%COMP  ; 0,0
      .def    mc%rtnea,mc%rtnfpn,mc%add_a,mc%add_c,mc%rtzero,mc%rtinf,mc%rtminf
      .def    mc%eorsgn,mc%eorzero,mc%eorinf,mc%div_a,mc%div_b,mc%mul_b,mc%rem_a
      .def    mc%sqr_c,mc%sqr_b
      .def    mc%cdnorm,mc%canorm
      .ref    mc%sqr_a,mc%iop

;     16 Jan 83    Hough                Added conditional normalization routines
;                                       to implement normalizing mode.
;
;************************************************************************
;
;
;       C O M P O N E N T  R O U T I N E S
;
;
;************************************************************************
;
;       These routines represent nodes of the operation tables
;       in the MC68341 spec that are trivial or appear in more
;       that one place in the tables.
;
;       All the following subroutines assume the following calling
;       sequences.
;
;       On entry:
;       d0-d2   contains the source argument from <ea> for dyadic
;               and FPn for monadic
;       d3      zero
;       a2      points to FP argument in the FPn
;
;
;       d0-d3   contain the result
;       d4      may be destroyed
;       a2-a7   restored
;
;******************* w h o  c a l l s  w h o ************************
;
;  This procedure calls: mc%iop
;
;  This procedure is called by: dispat
;
;***********************  F L A G S   ******************************
;
;  Exception    Set by     Reason
;
;  rsvx         ---        Not affected by this routine
;
;  iovf         ---        Not affected by this routine
;
;  inex         ---        Not affected by this routine
;
;  dz           ---        Not affected by this routine
;
;  unfl         ---        Not affected by this routine
;
;  ovfl         ---        Not affected by this routine
;
;  iop=1        iop(sqr_b)  square root of: infinity in projective mode
;                               or not norm number
;  iop=2        iop(add_c)   (+ infinity) + (-infinity) in affine mode
;  iop=4        iop(div_b)   infinity/infinity, 0/0 , not norm/not norm
;                               or norm/not norm
;  iop=7        iop(add_c)   projective closure use of +,- infinity
;  iop=8        iop(mul_b)   0 * infinity
;  iop=9        iop(rem_a)   denominator is zero or not normalized or
;                               numerator is zero
;
;*********************************************************************
;
;*************************************************************************
;
;

;       routine mc%cdnorm normalizes D0/D1/D2 if and only if the extended is
;       denormalized and normalizing mode is on.

mc%cdnorm
        debugbegin
        cmp.w   #extminxp,d0
        bgt.s   retd           ; Return if exponent too big.
        tst.l   d1
        blt.s   retd           ; Return if significand normalized.
        bgt.s   dod            ; Branch if definitely denormalized.
        tst.l   d2
        beq.s   retd           ; Return if significand zero.
dod
        btst    #mdnorm_m,mode(a3)
        beq.s   retd           ; Return if warning mode.
loopd
        lsl.l   #1,d2
        roxl.l  #1,d1           ; Shift left once.
        sub.w   #1,d0           ; Decrement exponent.
        tst.l   d1
        bge.s   loopd          ; Branch if still not normalized.
retd
        rts
        debugend 'MC%CDNOR',0
        
;       routine mc%CaNORM conditionally normalizes (A2).

        
mc%canorm
        debugbegin
        movem.l d0-d2,-(sp)
        movem.l (a2),d0-d2
        bsr.s   mc%cdnorm
        movem.l d0-d2,(a2)
        movem.l (sp)+,d0-d2
        rts
        debugend 'MC%CANOR',0

;****************************
;
;       Return <ea>
;
;****************************
;
mc%rtnea   EQU     *
        debugbegin
        RTS           ; NOP SINCE <EA> IS ALREADY IN D0-D3
        debugend 'MC%RTNEA',0
;
;****************************
;
;       Return FPn (when FPn isn't already in d0-d3)
;
;****************************
;
mc%rtnfpn  EQU     *
        debugbegin
        MOVEM.L (A2),D0-D2    ; MOVE FPN TO RESULT
        RTS
        debugend 'MC%RTNFP',0
;
;****************************
;
;       Add routine 'a'. Both <ea> and FPn are zero
;
;               <ea>
;           |   -0      +0
;       ----+------------------
;       -0  |   <ea>     d
;       +0  |    d      <ea>
;
;       d = +0 in RN,RZ, or RP
;         = -0 in RM
;
;****************************
;
mc%add_a   EQU     *
        debugbegin
        MOVE.L  (A2),D4       ; GET SIGN OF FPN
        EOR.L   D0,D4         ; EOR WITH SIGN OF <EA>
        ANDI.L  #$80000000,D4
        beq     @2            ; IF SIGNS NOT EQUAL THEN
          MOVE.B  MODE(A3),D4
          ANDI    #$3,D4        ; GET ROUNDING MODE
          cmp     #rm,d4
          bne     @1
;                                    IF ROUND MINUS
            BSET #31,D0            ; SET TO -0
            bra  @2
@1                                 ; {RN, RZ OR RP}
            BCLR #31,D0            ; SET TO +0
@2
        RTS
        debugend 'MC%ADD_A',0
;
;****************************
;
;       Add routine 'c'. When both <ea> and FPn are infinity.
;
;       If affine closure:
;
;                   <ea>
;                |  + inf   -inf
;           -----+-----------------
;           +inf |  <ea>     c1
;           -inf |   c1      <ea>
;
;           c1. mc%iop=2; d0-d3 = newnan
;       Else projective closure
;           mc%iop=7; d0-d3 = newnan
;
;****************************
;
mc%add_c   EQU     *
        debugbegin
        BTST    #MDCLS_M,MODE(A3)
        beq     @10           ; IF AFFINE MODE THEN
          jsr mc%eorsgn          ; GET EOR OF SIGN
          beq    @009         ; IF SIGNS NOT EQUAL
            MOVE #2,D0
            jmp  mc%iop          ; mc%iop=2, AND RETURN NAN
@009
            bra  @11
@10                           ; {PROJECTIVE MODE}
          MOVE #7,D0
          jmp   mc%iop           ; mc%iop=7, AND RETURN NAN
@11
        RTS
        debugend 'MC%ADD_C',0

;*********************************
;
;       return zero
;
;*********************************
;
mc%rtzero  EQU     *
        debugbegin
        MOVE.L  #$C000+ZERO_L,D0
        CLR.L   D1
        CLR.L   D2
        RTS
        debugend 'MC%RTZER',0 
;
;*********************************
;
;       return + infinity
;
;*********************************
;
mc%rtinf   EQU     *
        debugbegin
        MOVE.L  #$3FFF+INF_L,D0
        CLR.L   D1
        CLR.L   D2
        RTS
        debugend 'MC%RTINF',0
;
;*********************************
;
;       return - infinity
;
;*********************************
;
mc%rtminf  EQU     *
        debugbegin
        BSR     mc%rtinf ; GET PLUS INFINITY
        BSET    #31,D0 ; SET MINUS SIGN
        RTS
        debugend 'MC%RTMIN',0
;
;*********************************
;
;       Do eor of the signs of <ea> and FPn
;       Result is left in d4 upper bit (other bits = zero)
;       Z=1 iff eor'ed sign = 0
;
;*********************************
;
mc%eorsgn  EQU     *
        debugbegin
        MOVE.L  (A2),D4
        EOR.L   D0,D4
        AND.L   #$80000000,D4
        RTS
        debugend 'MC%EORSG',0 
;
;*********************************
;
;       return a zero with an eor'ed sign
;
;*********************************
;
mc%eorzero EQU     *
        debugbegin
        BSR     mc%eorsgn ; GET EOR OF SOGNS IN D4
        BSR     mc%rtzero ; GET + ZERO
        OR.L    D4,D0 ; OR IN CORRECT SIGN
        RTS
        debugend 'MC%EORZE',0
;
;*********************************
;
;       return infinity with an eor'ed sign
;
;*********************************
;
mc%eorinf  EQU     *
        debugbegin
        BSR     mc%eorsgn ; GET EOR OF SIGNS IN D4
        BSR     mc%rtinf ; GET + INFINTY
        OR.L    D4,D0 ; OR IN THE CORRECT SIGN
        RTS
        debugend 'MC%EORIN',0 
;
;*********************************
;
;       mc%div_a - divide routine when <ea> is zero and
;               FPn is normalized or not normalized
;
;*********************************
;
mc%div_a   EQU     *
        debugbegin
        BSET    #STDZ_M,TEMPSTAT(A3)  ; SET DIVIDE BY ZERO STATUS
        bra     mc%eorinf                ; GET INFINITY WITH PROPER SIGN
        debugend 'MC%DIV_A',0
;
;*********************************
;
;       mc%div_b - divide routine when inf/inf, 0/0, notnorm/notnorm
;               or norm/notnorm
;
;*********************************
;
mc%div_b   EQU     *
        debugbegin
        MOVE    #4,D0 ; mc%iop=4
        jmp     mc%iop   ; SET mc%iop=4, AND RETURN A NAN
        debugend 'MC%DIV_B',0
;
;*********************************
;
;       mc%mul_b - multiply routine when 0*infinity
;
;*********************************
;
mc%mul_b   EQU     *
        debugbegin
        MOVE    #8,D0 ; mc%iop=8,
        jmp     mc%iop    ; AND RETURN NEW NAN
        debugend 'MC%MUL_B',0
;
;*********************************
;
;       mc%rem_a - Remainder when <ea> = zero or unnormalized and
;               FPn = infinity
;
;*********************************
;
mc%rem_a   EQU     *
        debugbegin
        MOVE    #9,D0 ; mc%iop=9
        jmp     mc%iop     ; AND RETURN NEW NAN
        debugend 'MC%REM_A',0
;
;*********************************
;
;       mc%sqr_b - square root when FPn is unnormalized
;
;*********************************
;
mc%sqr_b  EQU     *
        debugbegin
        bsr.s   mc%cdnorm
        tst.l   d1
        bmi     jsqrt_a          ; Branch to regular processing if now normalized.
        MOVE    #1,D0 ; mc%iop=1
        jmp     mc%iop     ; AND RETURN NEW NAN
jsqrt_a jmp     mc%sqr_a
        debugend 'MC%SQR_B',0
;
;*********************************
;
;       mc%sqr_c - square root of infinity
;
;*********************************
;
mc%sqr_c  EQU     *
        debugbegin
        BTST    #MDCLS_M,MODE(A3)     ; GET CLOSURE BIT
        bne     @20                   ; IF PROJECTIVE CLOSURE
          Bra.s mc%sqr_b                  ; SET iop=1 AND RETURN NAN
          bra   @21
@20                                   ; {AFFINE MODE}
          TST.L D0
          bpl   @21           ; IF MINUS THEN
          bra.s mc%sqr_b        ; RETURN iop=1 AND RETURN NAN
@21
        RTS
        debugend 'MC%SQR_C',0

ÿ