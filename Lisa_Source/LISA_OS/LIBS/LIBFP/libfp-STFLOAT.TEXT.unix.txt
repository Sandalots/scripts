        PAGE
        .proc   mc%STFLOAT ; 0,0
        .def    mc%stf_A,mc%stf_B,mc%stf_C,mc%stf_D,mc%denorm
        .ref    mc%rnd_opt,mc%iop,mc_calcea
;
;************************************************************************
;
;       S T O R E   F L O A T I N G   V A L U E
;
;       ROUTINE A - FPN IS NORMALIZED OR UNNORMALIZED
;
;       STFLOAT - STORE A FPN TO MEMORY WHEN THE MEMORY FORMAT IS
;                 SINGLE, DOUBLE OR EXTENDED.
;
;       ON ENTRY:
;       A1 = ADDRESS OF DISASSEMBLED INSTRUCTION WD
;       A3 = ADDRESS OF FPRB
;       A4 = POINTER TO CALLER SUPPLIED FETCH/STORE ROUTINE
;       D0-D3 = FPN
;       D7 = DESTINATION SIZE INDEX
;               0 = SINGLE
;               2 = DOUBLE
;               4 = EXTENDED
;
;       ON EXIT:
;       MOST DATA REGISTERS ARE TRASHED
;       A0
;
;       ON ERROR EXIT:
;         D0-D2 = NAN CREATED BY mc%iop ROUTINE
;         MOST OTHER DATA REGISTERS ARE TRASHED.
;         A0
;
;       REVISIONS:
;       06 FEB 81       G.WALKER        CHANGE SGL AND DBL BOUNDARIES
;       24 MAR 81       J.BONEY         PERFORMANCE IMPROVEMENTS
;        7.MAY.81       G.WALKER        REMOVE NAN CREATION AFTER mc%iop CALL
;                                           AND CHANGE DOCUMENTATION
;        7.JUL.81       D.CAWTHRON      CHANGED TO EXORMACS STRUCTURED
;                                           ASM SYNTAX
;        7.SEP.81       D.CAWTHRON      DO NOT TRASH A1
;       16.SEP.81       D.CAWTHRON      DELETE CHECK FOR TRAPPING NANS IN
;                                           mc%stf_D
;       16 Jan 83       Hough           Corrected loading of significands of NaNs.
;       24 Jan 83       Hough           Underflow flag not set if exact.
;                                       Overflow flag set in all modes.
;                                       Overflow result according to draft 10.0.
;        7 Feb 83       Hough           Fixed bug in storing single or double
;                                       denormalized which rounded to normalized.
;       27 Mar 83       Hough           Deferred exponent adjust for mc%denorm.
;******************* W H O  C A L L S  W H O ************************
;
;  THIS PROCEDURE CALLS: mc%rnd_opt, mc%iop, mc_calcea
;
;  THIS PROCEDURE IS CALLED BY: STFP
;
;***********************  F L A G S   ******************************
;
;  EXCEPTION    SET BY     REASON
;
;  RSVX         ---        NOT AFFECTED BY THIS ROUTINE
;
;  IOVF         ---        NOT AFFECTED BY THIS ROUTINE
;
;  INEX         mc%rnd_opt  RESULT CANNOT BE EXACTLY REPRESENTED IN
;                            THE DESTINATION SIZE.
;               STFLOAT    OVERFLOW WITH TRAP DISABLED
;
;  DZ           ---        NOT AFFECTED BY THIS ROUTINE
;
;  UNFL         STFLOAT    NUMBER TOO SMALL FOR DESTINATION (RN OR RZ
;                            OR TRAPS ENABLED)
;
;  OVFL         STFLOAT    NUMBER TOO BIG FOR DESTINATION (RN OR RZ OR
;                            TRAPS ENABLED).
;
;  iop=13       mc%iop        ILLEGAL INSTRUCTION
;  iop=13       mc_calcea     ILLEGAL INSTRUCTION
;  iop=12       mc%iop        INVALID RESULT
;
;*********************************************************************
;
;
;*************************************************************************
;
;
RM_OR_RP  EQU  0      ; BIT THAT DISTINGUISHES RM OR RP ROUNDING MODE
RM_AND_RP EQU  1      ; BIT THAT DISTINGUISHES EITHER RM OR RP
;
; EQUATES FOR THE DESTINATION SIZE INDEX
;
ST_EXT  EQU     4
ST_SGL  EQU     0
ST_DBL  EQU     2
;
; MINIMUM AND MAXIMUM EXPONENT VALUES FOR SINGLE AND DOUBLE DESTINATIONS
;
STFP_MIN .word $FF82,$FC02
STFP_MAX .word $80,$400
;
;    MAXIMUM POSITIVE NUMBERS
;
MAXPOS  EQU *
  .long   $7F,$FFFFFF00,0           ; SINGLE PRECISION
  .long   $3FF,$FFFFFFFF,$FFFFF800  ; DOUBLE PRECISION
        PAGE
;
;
; ENTER HERE
;
;
mc%stf_A EQU *
        debugbegin
  cmp   #st_ext,d7
  beq   sav_sign                   ; IF NOT EXTENDED
;
;   SINGLE OR DOUBLE
;
;   SETUP FOR EVENTUAL CALL TO mc%rnd_opt. THE FOLLOWING
;   REGISTERS ARE NEEDED:
;       D5   PRECISION INDEX (2=SINGLE,4=DOUBLE)
;       A0   FPRB MODE BYTE (RIGHT JUSTIFIED)
;
    MOVE.B  MODE(A3),D5
    MOVE    D5,A0                     ; A0 NOW CONTAINS MODE BYTE
    MOVE    D7,D5                     ; MAP 0 TO 2 AND 2 TO 4
    ADD     #2,D5                     ; FOR INDEX FOR CALL TO mc%rnd_opt
;
    cmp stfp_min(d7),d0
    blt unf_dis                       ; IF NOT UNDERFLOW
      TST.L D1
      bpl   ex_mc%denorm                        ; IF NORMALIZED
        jsr mc%rnd_opt                        ; ROUND RESULT
        cmp stfp_max(d7),d0
        blt end_if_ovfl                      ; IF OVERFLOW
;
;         OVERFLOW
;
              BSET #STOVFL_M,TEMPSTAT(A3)          ; SET OVERFLOW BIT
          BTST   #ENOVFL_M,ENABLE(A3)          ; TEST OVERFLOW TRAP ENABLE
          bne    stfp_nor                        ; Exit without result if trapped.
            BSET  #STINEX_M,TEMPSTAT(A3)         ; SET INEXACT BIT
            BTST  #RM_AND_RP,MODE(A3)
            bne.s mult_dest                        ; IF ROUNDING MODE = RN OR RZ
            btst  #mdrnd_ls,mode(a3)
            beq         mc%stf_c                 ; Round to nearest - STORE INFINITY.
                                        ; Round to zero.
            move        d7,d6                   ; Multiply destination index.
            mulu        #6,d6
            tst.l       d0
            bmi.s       ov_max_neg
            bra.s       ov_max_pos
mult_dest
              MOVE  D7,D6                          ; MULT DEST INDEX BY 6
              MULU  #6,D6
              BTST  #RM_OR_RP,MODE(A3)
              beq.s tst_neg                          ; IF RM
              TST.L D0                             ; IF RESULT IS POSITIVE
              bmi   mc%stf_c
              bra.s ov_max_pos      
tst_neg
                  TST.L  D0
                  bpl    mc%stf_c                       ; IF RESULT IS NEGATIVE
ov_max_neg
                  LEA     MAXPOS,A0
                  MOVEM.L 0(A0,D6),D0-D2               ; LOAD MAX POSITIVE NUMBE
                  BSET    #31,D0                          ; THEN CHANGE IT TO MAX N
                  bra.s   end_ovfl_trap
ov_max_pos                              ; Load maximum number for format.
                  LEA     MAXPOS,A0
                  MOVEM.L 0(A0,D6),D0-D2               ; LOAD MAX POSITIVE NUMBE
                  bra.s   end_ovfl_trap

ex_mc%denorm
          LEA STFP_MIN,A0
          cmp     0(a0,d7),d0
          bne.s   mc%iop_12                        ; IF EXACTLY DENORMALIZED THEN
            SUBQ  #1,D0                         ; FIX EXPONENT SO RESULTING
          bra.s   end_not_norm
mc%iop_12
            MOVE  #12,D0        ; iop = 12
            jsr   mc%iop
            BTST  #ENIOP_M,ENABLE(A3) ; IF iop TRAP NOT ENABLED
            BEQ   mc%stf_D              ; GO JOIN NORMAL NAN PROCESSING
            BRA    STFP_NOR           ; ELSE (ENABLED):  EXIT WITHOUT RESULT
                                     ; {IF EXACTLY DENORMALIZED}
                                    ; {ELSE NOT NORMALIZED}
unf_dis
        BTST  #ENUNFL_M,ENABLE(A3)    ; IF UNDERFLOW TRAP DISABLED
        bne.s trap_enb
          jsr mc%denorm                  ; GO DENORMALIZE RESULT AND ROUND
          BTST  #stinex_m,tempstat(A3)
          beq.s   pln_van             ; No underflow flag if exact.
            BSET  #STUNFL_M,TEMPSTAT(A3) ; SET UNDERFLOW BIT
          bra.s   pln_van
trap_enb                              ; {TRAP ENABLED}
          jsr mc%rnd_opt
          BSET #STUNFL_M,TEMPSTAT(A3) ; SET UNDERFLOW BIT
          BRA STFP_NOR                ; EXIT WITHOUT A RESULT

;                      {ELSE UNDERFLOW}
;
;     PLAIN VANILLA NUMBERS
;
end_ovfl_trp
end_if_ovfl
end_not_norm
pln_van
      TST.L   D7
      bne     dbl_prec        ; IF SINGLE PRECISON
        ADD  #127,D0          ; ADD BIAS TO EXPONENT
        BCLR  #31,D1          ; CLEAR IMPLIED BIT
        LSR.L #8,D1           ; PUT FRACTION IN RIGHT PART OF D1
        MOVE.L D0,D2          ; SAVE SIGN
        AND.L  #$80000000,D2
        SWAP   D0             ; PUT EXPONENT IN HIGH WD OF D0
        CLR    D0             ; CLEAR LOW WD
        LSL.L  #7,D0          ; LEFT JUSTIFY EXPONENT IN D0
        OR.L   D2,D0          ; OR IN SIGN
        OR.L   D1,D0          ; OR  IN THE FRACTION
GSSGL   EQU    *              ; ENTER HERE TO STORE SINGLE RESUTL AND EXIT
        MOVE.L D0,-(A7)       ; PUT RESULT ON STACK
        STRSLT 4,STFP_NOR     ; GO STORE RESULT IN CALLER'S MEMORY
      bra      ext_lab        ; IF DOUBLE
dbl_prec
        ADD     #1023,D0      ; ADD BIAS TO EXPONENT
        MOVE.L  D0,D3         ; PRESERVE SIGN
        AND.L   #$80000000,D3
        LSL.L   #1,D2         ; THROW AWAY THE IMPLIED BIT
        ROXL.L  #1,D1
        SWAP    D0            ; PUT EXPONENT IN HIGH WD (RIGHT JUSTIFIED)
        SWAP    D1            ; HIGH FRACT INTO LOW WD
        MOVE    D1,D0         ; INTO LOW WD OF D0 (HIGH WD OF D1 CONTAINS NEXT 1
        SWAP    D2
        MOVE    D2,D1         ; D0-D2 NOW CONTAINS THE CORRECT
;                               NUMBER BUT 4 SHIFTS TOO FAR RIGHT
;
;       D0 = 00,E0,E1,E2,F0,F1,F2,F3      FORMAT OF ORIGINAL NBR
;       D1 = F4,F5,F6,F7,F8,F9,F10,F11
;       D2 = F12,00,00,00,00,00,00,00
;
        LSL.L   #4,D0         ; D0 = E0,E1,E2,F0,F1,F2,F3
        ROL.L   #4,D1         ; D1 = F5,F6,F7,F8,F9,F10,F11,F4
        MOVE.L  D1,D4         ; D4 = F5,F6,F7,F8,F9,F10,F11,F4
        ROL.L   #4,D2         ; D2 = 0,0,0,0,0,0,0,F12
        MOVEQ   #$F,D5        ; MASK
        AND     D5,D4         ; D4 = F5,F6,F7,F8,0,0,0,F4
        MOVEQ   #-16,D6       ; MASK
        AND     D6,D1         ; D1 = F5,F6,F7,F8,F9,F10,F11,0
        OR      D4,D0         ; D0 = E0,E1,E2,F0,F1,F2,F3,F4
        OR      D2,D1         ; D1 = F5,F6,F7,F8,F9,F10,F11,F12
        OR.L    D3,D0          ; OR THE SIGN INTO THE RESULT
GSDBL   EQU     *             ; ENTER HERE TO STORE DBL RESULT AND EXIT
        MOVEM.L D0/D1,-(A7)   ; PUT RESULT ON STACK
        STRSLT  8,STFP_NOR    ; GO STORE IN CALLER'S MEMORY
ext_lab
;
;   EXTENDED
;
    bra   stfp_nor
sav_sign
      MOVE.L D0,D3    ; SAVE SIGN
      AND.L  #$80000000,D3 ; ISOLATE SIGN
      SWAP   D0       ; PUT EXPONENT IN HIGH WORD
      BCLR   #31,D0   ; CLEAR SIGN BIT
      OR.L   D3,D0    ; OR IN THE SIGN
      SWAP   D1       ; MOVE EVERYTHING ELSE UP 16 BITS
      MOVE   D1,D0
      MOVE   D2,-(A7) ; PUT 16 LSB'S ON STACK
      SWAP   D2
      MOVE   D2,D1
GSEXT EQU    *        ; ENTER HERE TO STORE EXT AND EXIT (LOWER 16 BITS
;                       MUST ALREADY BE ON STACK WHEN ENTERING HERE)
      MOVEM.L D0/D1,-(A7) ; PUT OTHER 64 BITS ON STACK
      STRSLT 10,STFP_NOR
;
; EXIT HERE IF NO RESULT IS TO BE RETURNED
;
STFP_NOR EQU *
      RTS
        debugend 'MC%STF_A',0
  PAGE
;
;************************************************************************
;
;       S T O R E   F L O A T I N G   V A L U E
;
;       ROUTINE D - FPN IS NAN
;
;       STFLOAT - STORE A NAN TO MEMORY WHEN THE MEMORY FORMAT IS
;                 SINGLE, DOUBLE OR EXTENDED.
;
;       ON ENTRY:
;       A1 = ADDRESS OF DISASSEMBLED INSTRUCTION WD
;       A4 = POINTER TO CALLER SUPPLIED FETCH/STORE ROUTINE
;       D0-D3 = FPN
;       D7 = DESTINATION SIZE INDEX
;               0 = SINGLE
;               2 = DOUBLE
;               4 = EXTENDED
;
;       ON EXIT:
;       MOST DATA REGISTERS ARE TRASHED
;       A0 IS ALSO TRASHED
;
;
;
;*************************************************************************
;
mc%stf_D EQU *
        debugbegin
        and.l   #$80000000,d0           ; D0 gets sign bit only.
        cmp     #st_dbl,d7
        bgt.s   xnan                    ; Branch if extended nan.
        blt.s   snan                    ; Branch if single nan.
        or.l    #$7ff00000,d0           ; D0 gets double NaN exponent.
        lsr.l   #1,d1                   ; Align significand.
        roxr.l  #1,d2
        lsr.l   #1,d1                   ; Align significand.
        roxr.l  #1,d2
        lsr.l   #1,d1                   ; Align significand.
        roxr.l  #1,d2
        lsr.l   #1,d1                   ; Align significand.
        roxr.l  #1,d2
        lsr.l   #1,d1                   ; Align significand.
        roxr.l  #1,d2
        lsr.l   #1,d1                   ; Align significand.
        roxr.l  #1,d2
        lsr.l   #1,d1                   ; Align significand.
        roxr.l  #1,d2
        lsr.l   #1,d1                   ; Align significand.
        roxr.l  #1,d2
        lsr.l   #1,d1                   ; Align significand.
        roxr.l  #1,d2
        lsr.l   #1,d1                   ; Align significand.
        roxr.l  #1,d2
        lsr.l   #1,d1                   ; Align significand.
        roxr.l  #1,d2
        and.l   #$fffff,d1              ; Clear excess bits.
        bne.s   sdnan                   ; Branch if still a NaN.
        tst.l   d2
        bne.s   sdnan                   ; Branch if still a NaN.
        move.l  #dnanzero,d1            ; Zero-significand NaN.
sdnan
        or.l    d1,d0                   ; Combine sign, exponent, and significand.
        move.l  d2,d1
        bra     gostore
snan                                    ; Single Nan.
        or.l    #$7f800000,d0           ; D0 gets single NaN exponent.
        lsr.l   #8,d1                   ; D1 gets aligned significand.
        and.l   #$3fffff,d1             ; Clear extra bits.
        bne.s   ssnan                   ; Branch if still a NaN.
        move.l  #snanzero,d1            ; Zero-significand NaN.
ssnan
        or.l    d1,d0                   ; Combine sign, exponent and significand.
        bra     gostore
xnan
        or.l    #$3fff0000,d0           ; D0 gets extended NaN exponent.
        and.l   #$7fffffff,d1           ; Clear j bit.
        bne.s   sxnan                   ; Branch if still a NaN.
        tst.l   d2
        bne.s   sxnan                   ; Branch if still a NaN.
        move.l  #xnanzero,d1            ; Zero-significand NaN.
sxnan
        swap    d1                      ; D1 gets (S1,S0).
        swap    d2                      ; D2 gets (S3,S2).
        move    d1,d0                   ; D0 gets (3fff,S0).
        move    d2,d1                   ; D1 gets (S1,S2).
        bra.s   gostore
        debugend 'MC%STF_D',0
  PAGE
;
;************************************************************************
;
;       S T O R E   F L O A T I N G   V A L U E
;
;       ROUTINE C - FPN IS INFINITY
;
;       STFLOAT - STORE A PROPERLY SIGNED INFINITY TO
;                 SINGLE, DOUBLE OR EXTENDED.
;
;       ON ENTRY:
;       A1 = ADDRESS OF DISASSEMBLED INSTRUCTION WD
;       A4 = POINTER TO CALLER SUPPLIED FETCH/STORE ROUTINE
;       D0-D3 = FPN
;       D7 = DESTINATION SIZE INDEX
;               0 = SINGLE
;               2 = DOUBLE
;               4 = EXTENDED
;
;       ON EXIT:
;       MOST DATA REGISTERS ARE TRASHED
;       A0 IS ALSO TRASHED
;
;
;
;*************************************************************************
;
mc%stf_C EQU *
        debugbegin
  AND.L #$80000000,D0   ; SAVE THE SIGN BIT AND ZERO THE OTHER BITS
  TST D7
  bne     @11             ; IF SINGLE PRECISION
    OR.L  #$7F800000,D0   ; OR HEADER WITH SIGN
    bra   @13
@11
    cmp    #st_dbl,d7
    bne    @12                   ; IF DOUBLE
      OR.L #$7FF00000,D0         ; OR HEADER WITH SIGN
      CLR.L D1
      bra  @13
@12
      OR.L  #$3FFF0000,D0        ; OR HEADER WITH SIGN
      CLR.L  D1
      CLR.L  D2
@13
  BRA  GOSTORE                ; GO TO MUTUAL STORE EXIT
        debugend 'MC%STF_C',0
  PAGE
;
;************************************************************************
;
;       S T O R E   F L O A T I N G   V A L U E
;
;       ROUTINE B - FPN IS NORMAL ZERO
;
;       STFLOAT - STORE A PROPERLY SIGNED ZERO TO
;                 SINGLE, DOUBLE OR EXTENDED.
;
;       ON ENTRY:
;       A1 = ADDRESS OF DISASSEMBLED INSTRUCTION WD
;       A4 = POINTER TO CALLER SUPPLIED FETCH/STORE ROUTINE
;       D0-D3 = FPN
;       D7 = DESTINATION SIZE INDEX
;               0 = SINGLE
;               2 = DOUBLE
;               4 = EXTENDED
;
;       ON EXIT:
;       MOST DATA REGISTERS ARE TRASHED
;       A0 IS ALSO TRASHED
;
;
;
;*************************************************************************
;
mc%stf_B EQU *
        debugbegin
  AND.L  #$80000000,D0 ; SAVE THE SIGN BIT AND ZERO THE OTHER BITS
  CLR.L  D1           ; CLEAR THE FRACTION BITS
  CLR.L  D2
  cmp    #st_ext,d7
  bne    gostore
    BSET  #30,D0      ; SET EXPONENT TO $4000
                      ; FALL INTO GOSTORE
;
;
;       JUMP HERE TO STORE A RESULT AND EXIT. THIS CHUNK OF CODE DETERMINEES
;       THE SIZE OF THE RESULT AND JUMPS INTO THE MAIN LINE TO DO THE
;       ACTUAL STORE AND EXIT. I KNOW THIS IS VERY UNSTRUCTURED, BUT IN
;       ORDER TO BOOST THE PERFORMANCE OF THE MAIN LINE WITHOUT MAKING
;       THIS CODE HUGE, IT WAS NECESSARY. MAYBE SOMEDAY TONY HOARE WILL
;       FORGIVE ME.
;
GOSTORE EQU     *
        LSL     #1,D7 ; DOUBLE THE SIZEE INDEX
        JMP     GSTAB(D7)
GSTAB   EQU     *
        BRA     GSSGL ; GO JUMP INTO SINGLE STORE AND EXIT
        BRA     GSDBL ; GO JUMP INTO DOUBLE STORE AND EXIT
; HERE ON EXTENDED
        SWAP    D2    ; PUSH THE 16 LSB BEFORE JUMPING
        MOVE    D2,-(A7)
        BRA     GSEXT ; GO JUMP INTO EXTENDED AND EXIT
        debugend 'MC%STF_B',0
  PAGE
;
;************************************************************************
;
;
;       DENORMALIZE A SINGLE OR DOUBLE VALUE IN D0-D2 THEN
;               ROUND THE DENORMALIZED RESULT.
;
;       NOTE: THIS ROUTINE WILL NOT WORK FOR EXTENDED VALUES
;       BECAUSE IT INDISCRIMINATELY OR'S BITS INTO BIT 31 OF
;       D3 WHICH IS THE G BIT FOR EXTENDED BUT IS ONLY A ROUND
;       OR STICKY BIT FOR SINGLE OR DOUBLE.
;
;       ON ENTRY:
;       D7      DESTINATION SIZE INDEX (0=SINGLE; 2=DOUBLE)
;       D0-D3   UNROUNDED FP VALUE TO BE DENORMALIZED
;       D5      INDEX FOR ROUND CALL (2=SINGLE; 4=DOUBLE)
;       A0      FPRB MODE BYTE (RIGHT JUSTIFIED)
;
;       ON EXIT:
;       D0-D2   ROUNDED DENORMALIZED RESULT (POSSIBLY TRUE ZERO)
;       D3      ZERO
;       D4,D6   TRASHED
;
;
;************************************************************************
;
;  MAXIMUM NUMBER OF RIGHT SHIFTS THAT WILL NORMALIZE A NUMBER. MORE
;  SHIFTS THAN THIS WILL CAUSE ALL THE BITS TO BE STICKY BITS.
;
STFP_SHFT EQU *
          .word  25,54
;
;  MINIMUM VALID EXPONENT FOR THE PRECISION. INDEXED BY THE
;  DESTINATION SIZE INDEX (D7)
;
ST_MIN2 .word $FF82,$FC02
;
;
;  ENTER HERE
;
;
mc%denorm EQU *
        debugbegin
  MOVE ST_MIN2(D7),D4     ; GET THE MINIMUM EXPONENT FOR PRECISION
  SUB  D0,D4              ; D4 NOW IS THE NUMBER OF SHIFTS TO DENORMALIZE
  MOVE ST_MIN2(D7),D0     ; SET THE DENORMALIZED EXPONENT TO THE MIN EXP
  cmp     stfp_shft(d7),d4
  ble     shf_more        ; IF TOO MANY SHIFTS THEN
    OR.L  D2,D3           ; OR EVERYTHING INTO STICKY
    OR.L  D1,D3
    CLR.L D2              ; CLEAR OUT FRACTION
    CLR.L D1
  bra     prog_end      ; { IF NOT TOO MANY SHIFTS}
shf_more
;
;   FAST SHIFT THE NUMBER RIGHT D4 BITS. PUT ALL STICKY BITS INTO D3
;
    cmp      #32,d4
    blt      shf_16           ; IF SHIFT CT > 32
      OR.L  D2,D3             ; OR D2 INTO STICKY
      MOVE.L D1,D2            ; D1 INTO D2
      CLR.L  D1
      SUB    #32,D4           ; REDUCE SHIFT COUNT BY 32
shf_16
    cmp      #16,d4
    blt      shf_8            ; IF SHIFT CT > 16
      OR D2,D3                ; LOW WD OF D2 INT STICKY
      CLR D2                  ; LOW WD = 0
      MOVE D1,D2
      SWAP D2                 ; D2 NOW SHIFTED 16 BITS
      CLR D1
      SWAP D1                 ; D1 NOW SHIFTED 16 BITS
      SUB  #16,D4             ; REDUCE SHIFT CT BY 16
shf_8
    cmp      #8,d4
    blt      end_shf_8        ; SHIFT CT STILL > 8
      CLR.L  D6               ; SAVE LOWER BYTE OF D1 IN D6 UPPER BYTE
      MOVE.B D1,D6
      SWAP D6
      LSL.L #8,D6
      LSR.L #8,D1             ; D1 IS SHIFTED 8 BITS RIGHT
      OR.B  D2,D3             ; LOW BYTE OF D2 INTO STICKY
      LSR.L #8,D2             ; D2 NOW SHIFTED 8 BITS RIGHT BUT MISSING HIGH BYT
      OR.L  D6,D2             ; OR IN HIGH BYTE
      SUB   #8,D4             ; REDUCE SHIFT CT BY 8
end_shf_8
      BRA   ST_01             ; GO TO END OF LOOP FIRST
ST_LOOP EQU *
      LSR.L  #1,D1            ; SHIFT RIGHT 1
      ROXR.L #1,D2
      ROXR.L #1,D3
      bcc     go_again        ; IF STICKY BIT SHIFTED OUT
        BSET  #31,D3          ; SHIFT IT BACK IN
go_again
ST_01 DBRA D4,ST_LOOP         ; GO AGAIN IF MORE TO DO

prog_end
        jsr mc%rnd_opt               ; GO ROUND THE RESULT
        tst.l   d1              ; Check if it rounded up to normalized.
        bmi.s   @1              ; Branch if rounded result is not normalized.
        subq.w  #1,d0           ; Decrement exponent to denormalized exponent.
@1
        rts
        debugend 'MC%DENOR',0 
    
ÿ