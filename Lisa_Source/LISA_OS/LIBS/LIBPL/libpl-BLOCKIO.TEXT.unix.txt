
{-----------------------------------------------------------------------------}
{                                                                             }
{               Copyright 1983, Apple Computer Inc.                           }
{                                                                             }
{-----------------------------------------------------------------------------}

(* ------------------------------------------------------------------------- *)
(* File:  BLOCKIO.TEXT                                                       *)
(*                                                                           *)
(* Date:  01-Feb-82     Yu-Ying Chow    Block I/O support for UCSD files in  *)
(*                                      Stand Alone OS                       *)
(*        25-Jun-82     Ron Johnston    I/O support for both UCSD and OS     *)
(*                                      files in Stand Alone OS              *)
(*        3-Jan-83      Yu-Ying Chow    No more I/O support for UCSD files.  *)
(* ------------------------------------------------------------------------- *)

    (***********************************************************************)
    (*                                                                     *)
    (*  BLK IO routines that are supported in stand alone OS for accessing *)
    (*  UCSD files.  Some are taken from Apple ][ Pascal's SYSSEGS.A.TEXT  *)
    (*                                                                     *)
    (*  In this file,  blockio,  the following routines are defined:       *)
    (*    BlockioInit, SetMPrefix, UCSDFileName,  MapToUCSD.               *)
    (*                                                                     *)
    (***********************************************************************)

UNIT blockio;           {block io support for UCSD files in stand alone OS}
INTRINSIC SHARED;       (* shared intrinsic unit in PASLIB *)

        (************************************************)
        (*                                              *)
        (*   Taken from  MGlobals.text :                *)
        (*   GLOBALS FOR 68000 MONITOR  SYSTEM          *)
        (*   RELEASE LEVEL:  0.3         JUN 01, 1981   *)
        (*                                              *)
        (************************************************)

{$%+}
INTERFACE

 USES
   {$U libos/syscall.obj}    syscall,
   {$U libos/psyscall.obj}   psyscall,
   {$U libpl/blkiointr.obj}  blkiointr;           {the block io intrinsic unit}


 CONST
   CclearScreen = 1;           {clear the whole screen}
   CclearEScreen = 2;          {clear to the end of the screen}
   CclearELine = 3;            {clear to end of line}

   CgoHome = 11;               {move cursor to home position}
   CleftArrow = 12;            {move cursor left one character position}
   CrightArrow = 13;           {move cursor right one character position}
   CupArrow = 14;              {move cursor up one line position}
   CdownArrow = 15;            {move cursor down one line position}

 TYPE
   consoledest = (alscreen, mainscreen, xsorocA, xsorocB, folder, spare1,
                  spare2, spare3);      (* max 8 options *)

   dsProcCode = (dsResProg, dsSoftPwbtn, dsPrintDev, dsSetGPrefix, dsEnbDisk,
                 dsGetDiskEnbF);

   dsProcParam = record
                  case ProcCode : dsProcCode of
                    dsResProg    : (RProcessId : longint); {must be called before
                                                    the process starts running.}
                    dsSoftPwbtn  : (SPButton : boolean);   {result}
                    dsPrintDev   : (PrDevice : e_name);
                    dsSetGPrefix : (errnum : INTEGER;      {result}
                                    prefix : pathname);
                    dsEnbDisk    : (toEnbDisk : boolean);
                    dsGetDiskEnbF : (diskEnbF : BOOLEAN);       {result}
                  end;

   pdsProcCode = (pdsSomething);

   pdsProcParam = record                        {stub for expansion}
                    case ProcCode : pdsProcCode of
                      pdsSomething : (something : longint);
                  end;



 { ------------------------ Pascal built-in procedures ------------------------- }

    PROCEDURE %_FINIT (VAR F: FIB; WINDOW: WINDOWP; {RECWORDS} recbytes: INTEGER);

    PROCEDURE %_FOPEN (VAR F: FIB; VAR FTITLE: pathname;   {to allow OS pathname}
                       FOPENOLD: BOOLEAN; JUNK : longint);        {Reset, Rewrite}

    PROCEDURE %_FCLOSE (VAR F: FIB; FTYPE: integer); {compiler passes FTYPE in integer}

    FUNCTION %_FBLOCKIO (VAR F: FIB; A: longint;
                       NBLOCKS,RBLOCK: INTEGER; DOREAD: BOOLEAN) : INTEGER ;

    PROCEDURE %_FGET (VAR F: FIB);

    PROCEDURE %_FPUT (VAR F: FIB);

    PROCEDURE %_WRITELN (VAR F: FIB);

    PROCEDURE %_WRITECHAR (VAR F: FIB; charaddr : longaddr; count : integer);

    PROCEDURE %_READLN (VAR F: FIB);

    FUNCTION %_READCHAR (VAR F: FIB) : CHAR;

    PROCEDURE %_FSEEK (VAR F : FIB; RECORDNUM : integer);

    PROCEDURE %_FGOTOXY (X,Y:INTEGER);



{ ------- Public procedures but not built-in (Compiler does not know about) ----- }
{----------------------------- defined in PASLIBCALL -----------------------------}

    function PAbortFlag : boolean;                {Apple-period entered or not}

    PROCEDURE GetGPrefix (Var prefix : pathname); {get global working directory}

    procedure ScreenCtr (contrfun : integer);  {standard screen control functions}

    procedure GetPrDevice (var PrDevice : e_name);

    PROCEDURE PTranLisaChar (toTranslate : boolean);

    FUNCTION PaslibVersion : INTEGER;          {return PASLIB version}



{ ------ Procedures called by Shell only, some by LDS Shell only ---------------- }
{----------------------------- defined in PPASLIBC -------------------------------}

    PROCEDURE BlockIOInit;        {entire blockio unit init, once per system}

    procedure BlockIOdisinit;     {blockio unit clean up, called by shell only}

    procedure lockPaslib (var errnum : integer);   {lock PASLIB1 for Filer}

    procedure lockPasiolib (var errnum : integer); {lock PASIOLIB for Filer}

    procedure moveconsole (var errnum : integer; applconsole : consoledest);

    PROCEDURE ExecReset (VAR errnum : INTEGER; VAR execfile : pathname;
                         stopexec : BOOLEAN);        {open/stop exec file}

    FUNCTION ExecFlag : BOOLEAN;        {return TRUE if EXEC file is active}

    PROCEDURE OutputRedirect (VAR errnum : INTEGER; VAR outfile : pathname;
                              stopoutput : BOOLEAN); {open/stop output file}

    FUNCTION OutputRFlag : BOOLEAN;     {return true if output is redirected}

    procedure DSPaslibCall (VAR ProcParam : dsProcParam);

    procedure %_PDSPaslibCall (VAR ProcParam : pdsProcParam); {by LDS Shell only}
                                        {stub for later expansion}

{ ------- Procedures for communications between assembly part and this unit ----- }

    PROCEDURE %_blockiosetup (firsttime : boolean; var regis : envirRec); {by %_init}

    function %_CkResident (var regis : envirRec) : boolean;
          {by %_term, %_halt to check resident and get the registers for restarting.}

    function %_pkeypress (var f : fib) : boolean;
                {by %_keypress in appastext. Any key pressed or not }




{ ---------- Procedures not supported on OS but recognized by Compiler ---------- }

    procedure %_funitiocall (unitno : integer; bufaddr : longint; nbytes : integer;
                          blocknum : integer; mode : integer; unitcode : integer);
                   {for unitread, unitwrite, and unitclear}

    function %_funitbusy (unitno : integer) : boolean;




IMPLEMENTATION


{$IFC fRANGECK}
{$R+}
{$ELSEC}
{$R-}
{$ENDC}

{$IFC fSYMBOL}
{$D+}
{$ELSEC}
{$D-}
{$ENDC}



{$S PASIOLIB }


 CONST
   plibversion = 1105;          {first two digits for interface change}

   ESC = $1b;
   RS  = $1e;                   {home}
   BS  = 8;                     {left arrow}
   FF  = $c;                    {right arrow}
   VT  = $b;                    {up arrow}
   LF  = 10;                    {line feed}
   EOL = 13;                    {END-OF-LINE...ASCII CR}
   AbortKey = 14;               {Apple (Control) Period}
   DLE = 16;                    {BLANK COMPRESSION CODE}

   SO  = $0E;                   {shift out}
   SI  = $0F;                   {shift in}

   openpattern = 55;            {0..100}
   closepattern = 97;           {0..100}

   FType_textfile = 2261;

   RewriteFname = '<REWRITETEMPFILE>';

{error values - PASLIB's range is from 6000 to 6200 : IO - 6001..6100,
                                       (6050..6052 - print result in QuickPort),
                                FPLIB - 6101..6150, HEAP - 6151..5200}
   inotopen     = 6001;         {file not opened}
   inotclosed   = 6002;         {file not closed in reset/rewrite}
   ibadunit     = 6003;         {file not opened for the operation - fput to input}
   e_no_printer = 6004;         {no printer device}
   e_chardev    = 6005;         {reset of non-byte sized typed file to char dev}
   w_opentext   = -6003;        {reset of a non-text file with a text type}
   w_opendata   = -6004;        {reset of a text file with a typed file type}
   e_readint    = 6006;         {read of integer does not get valid integer,
                                 this is set in appastext}
   inotimpl     = 6010;         {illegal function}
   e_badtextfile = 6011;        {illegal text file format encountered}

{$IFC MONITOR}
   e_bigdir     = 6080;         {#5 is large directory in close call}
{$ENDC}
   e_execeof    = -6081;        {exec file hit eof in execread}
   e_execreset  = 6081;         {exec file hit eof prematurely in execreset}
   e_execfile   = 6082;         {illegal exec file name in execreset}
   e_prefix     = 6083;         {null prefix name in setGprefix }
   e_moveconsole = 6090;        {exec file or output file open when moveconsole}
   (*
   e_movefolder = 6091;         {move from folder to screen not allowed}
   *)


 VAR
   initflag : boolean;          {must be the first var, OS init the highest
                                 integer in the shared data seg to 0 - false.}
(*$IFC MONITOR*)
   unitable : ARRAY [unitnum] OF utblentry;     (*UNIT TABLE FOR VOLUMES*)
   thedate: daterec;                            (*TODAY...SET IN FILER OR SIGN ON*)
   dkvid: vid;                                  (*DEFAULT VOLID*)
   syvid: vid;                                  (*SYSUNIT VOLID*)
   fsmode : (osmode, monitormode, bothmode);    (* file system indication for
                                                   RESET/REWRITE *)
(*$ENDC*)
   inputfile     : FIB;         {global INPUT fib for exec file only}
   inputfwindow  : char;
   outputfile    : FIB;         {global OUTPUT fib for output redirect file only}
   outputfwindow : char;
   apconsole     : integer;     {application console}
   gfirstchar    : CHAR;        {exec file's first char - for skipping}
   greadahead    : CHAR;        {exec file's read ahead char buffer}
   gexecflag     : BOOLEAN;     {exec file opened flag}
   goutputrflag  : BOOLEAN;     {output redirected flag}
   globWorkDir   : pathname;
   consoleRefnum : integer;     {save console refnum here since -console and
                                 and -keyboard need to use it in fget.}
   consoleFunit  : integer;     {save console funit here in case redirection.}
   shellProcessid : longint;    {the first one calling blockioinit}
   ResProcId     : longint;     {the newly created resident process id}
   SoftPwBtn     : boolean;     {soft power button pressed or not}
   CkDskEventF   : boolean;     {check disk events in readln or not}
   PrintDevice   : e_name;      {printer device name}

   tracefence : integer;        {for trace fence value}






PROCEDURE %_SetIOFilePtr (infptr , outfptr : longaddr); external;  {in PASINIT.TEXT}
                                {set i/o file pointers in 8/12 offset from A5}



{$S PASLIB1 }
                                {procedures called by %_init}

{ -------------------------------BlockIOSetup ----------------------------------- }
PROCEDURE %_blockiosetup {(firsttime : boolean; var regis : envirRec)};
                        {block io setup for each process, called by PASINIT only}
                        {Note that resident mechanism is for workshop only.}
  VAR saveresid: longint;

      PROCEDURE InitIOFile (fp : fibp; window : WindowP);
        BEGIN
          WITH fp^ DO
            BEGIN
              FWindow := window;
              FIsOS   := TRUE;
              FState  := FNeedChar;
              FIsOpen := TRUE;
              FSoftBuf := false;
              HiByt3 := openpattern;      {this is a simple flag to mean inited}
              FUnit := unitScConsole;
              FVIDstuff.FTrefnum := consoleRefnum;
              FHeader.Dfkind := TextFile;         {text file processing}
              FReptCnt := 0;
              FEOLN := FALSE;
              FEOF := FALSE;
            END;
        END;

  BEGIN   {%_blockiosetup}
    if firsttime then              {first time call from %_init}
      begin
        %_SetIOFilePtr (@PInputfile, @POutputfile);
                       {always set i/o file ptrs 8/12 offset from A5 to local fibs.}
        PasTerm := NIL;
        PConsWindow := FALSE;
                                {init PInputfile and POutputfile to console fib}
        InitIOFile (@PInputfile, @PInputfwindow);
        InitIOFile (@POutputfile, @POutfwindow);

        saveresid := ResProcId; {in case it's changed after my_id returns}
        PMyid := my_id;
        PResProgram := false;
        if initflag then        {if already initialized, then check for resident}
                                {i.e. shell process can not be resident}
          if saveresid = PMyid then PResProgram := true;

        PBeginEnv := regis;
      end
    else  {second time call from %_init and second and up run of resident program}
      begin
        IORslt := ORD (INoError);
        if not gexecflag then
         with inputfile do
          begin
            FReptCnt := 0;
            FEOLN := FALSE;
            FEOF := FALSE;
          end;
        PInWindow := 0;
        PAbortKeyF := false;
        PTrLisaChar := true;
        if initflag and (globWorkDir <> '') then
          set_working_dir (iorslt, globworkdir);
                     {set global working directory for each process if already
                        initialized and setGprefix has been called.}
      end;
  END;  {%_blockiosetup}



procedure lockPaslib { var errnum : integer };          {lock PASLIB1 for Filer}
  begin
    lockseg (errnum);
  end;  {lockPaslib}



function %_CkResident {(var regis : envirRec) : boolean};
        {called by %_term to check resident and get the registers for restarting.}
  var errnum : integer;

  procedure sendevent;
    var refnum : integer;
        devname : e_name;
        event_name : pathname;
        excep_name : t_ex_name;
        event_ptr : p_s_eventblk;
        event : s_eventblk;
        interval : timestmp_interval;
        clktime : time_rec;
    begin
        osbootvol (errnum, devname);

        event_name := concat ('-', devname, '-', 'resident_channel');
        excep_name := '';
        open_event_chn (errnum, event_name, refnum, excep_name, false);
        if errnum > 0 then                 {if error, kill the process}
            kill_process (errnum, PMyid);

        event [0] := 1;         {to mean the resident process}
        event_ptr := @event;
        interval.sec := 0;
        interval.msec := 0;
        send_event_chn (errnum, refnum, event_ptr, interval, clktime);
        if errnum > 0 then                 {if error, kill the process}
            kill_process (errnum, PMyid);

        close_event_chn (errnum, refnum);
    end;

  begin  {%_CkResident}
    %_CkResident := false;

    if PConsWindow AND (PInWindow = 0) then PasTerm.CleanUp
         {Window env, and not nested call, go to wait for Set Aside, or Put Away.}
    else
      if PResProgram or (ResProcId = PMyid) then
        begin
          PResProgram := true;
          sendevent;
          suspend_process (errnum, PMyid, false); {suspend itself only}
                                  {Shell will activate it at sencod time to run}
          regis := PBeginEnv;
          %_CkResident := true;
        end;
  end;  {%_CkResident}




{$S PASIOLIB}


{ Trace --------------------------------------------------------------------- }
FUNCTION Trace (level:INTEGER) : BOOLEAN;

  VAR fence : ^INTEGER;

  BEGIN  {Trace}
    fence := @tracefence;
    trace := level >= fence^;
  END;   {Trace}




{ BlockioInit (01-Feb-82) ---------------------------------------------------- }
PROCEDURE BlockioInit;
  { blockio init - taken from PROCEDURE INITUNITABLE; in SYSSEGS.A.TEXT}
  { Called by the SYSTEM.SHELL at boot time, once for the entire system. }

  VAR lunit: unitnum;
      ldir: dirp;
      errnum : integer;
      filename : pathname;

  BEGIN  {BlockioInit}
    if not initflag then
      begin             {init only once}
      (*$IFC MONITOR*)
        syvid := '#5';
        dkvid := syvid;
        thedate.year := 0;          (* means date has not been read in from #5 *)
        FOR lunit := 0 TO maxunit DO
          BEGIN  (* FOR lunit *)
          WITH unitable[lunit] DO
            BEGIN  (* WITH unitable[lunit] *)
            uvid := '';
            lockdir := false;
            uisblkd := lunit IN [5,9..20];
            IF uisblkd THEN
              BEGIN  (* IF uisblkd *)
              ueovblk := MMAXINT;
              END;   (* IF uisblkd *)
            END;   (* WITH unitable[lunit] *)
          END;   (* FOR lunit *)

        unitable[1].uvid := 'CONSOLE';
        fsmode := bothmode;           (* file system mode is both os and monitor *)
      (*$ENDC*)

        filename := 'Copyright 1983, Apple Computer Inc.';
        tracefence := 100;            {fence value, all trace levels must be lower than
                                       this value for tracing messages to show up}
        lockPaslib (errnum);          {lock the paslib segment}

        gexecflag := false;
        goutputrflag := false;
        globWorkDir := '';

        consoleFunit := unitScConsole;              {3.2, YC, 2/28/83}
        apconsole := -1;                   {to affect moveconsole}
        consoleRefnum := -1;
        moveconsole (errnum, alscreen);    {to open OS driver}
        (*if errnum > 0 then moveconsole (errnum, mainscreen);
                                                {if error, use main screen}
        *) {so that in production system, writelns won't show up}
        filename := '-CONSOLE';
        with inputfile do
          begin
            FIsOpen := False;{Bug fix to prevent user from opening an opened file}
            %_FInit (inputfile, @inputfwindow, 0); {reset (inputfile) - interactive}
            %_Fopen (inputfile, filename, true, 0);
          end;
        with outputfile do
          begin
            FIsOpen := False;{Bug fix to prevent user from opening an opened file}
            %_FInit (outputfile, @outputfwindow, 0); {reset (outputfile) - interactive}
            %_Fopen (outputfile, filename, false, 0);
          end;
        shellProcessid := PMyid;
        ResProcId := 0;
        SoftPwBtn := false;
        CkDskEventF := false;           {3.2, YC, 3/4/83}
        PrintDevice := '';
        filename := RewriteFname;
        kill_object (errnum, filename);  {kill temp file if any}

        initflag := true;        {done with init}
      end;
  END;   {BlockioInit}



procedure BlockIOdisinit;     {blockio unit clean up, called by shell}
  begin
    if shellProcessid = PMyid then
      close_object (iorslt, consoleRefnum);
              {close console only for the first shell that calls blockioinit}
  end;  {Blkiodisinit}



procedure lockPasiolib { var errnum : integer }; {lock PASIOLIB for Filer}
  begin
    lockseg (errnum);
  end;  {lockPasiolib}



FUNCTION PaslibVersion {: integer};     {return PASLIB version}
begin
  PaslibVersion := plibversion;
end;  {PaslibVersion}




PROCEDURE %_FGOTOXY {(X,Y:INTEGER)};        {called by %_gotoxy in pasmisc}
  VAR SEND: STRING[4];
  BEGIN
    if PConsWindow AND (PInWindow = 0) then PasTerm.VGotoxy (x, y)
                           {Window env, and not nested call}
    else
      begin
        IF X > 87 THEN X := 87 ELSE IF X < 0 THEN X := 0;
        IF Y > 31 THEN Y:=31 ELSE IF Y < 0 THEN Y := 0;
        SEND := '    ';
        SEND[1]:=CHR(27); {LEAD-IN}
        SEND[2]:='=';     {DC1}
        SEND[3]:=CHR(32+Y);
        SEND[4]:=CHR(32+X);
        WRITE(SEND);
      end;
  END;   {%_fgotoxy}




procedure resetinput;
  var fname : pathname;
  begin
    %_fclose (inputfile, ord (CNormal));
    %_FInit (inputfile, @inputfwindow, 0);  {reset (inputfile) - interactive}
    fname := '-CONSOLE';
    %_Fopen (inputfile, fname, true, 0);
    inputfile.FUnit := unitScConsole;      {inputfile can not go to window for now}
  end;  {resetinput}



procedure resetoutput;
  var fname : pathname;
  begin
    %_fclose (outputfile, ord (Clock));
    %_FInit (outputfile, @outputfwindow, 0);  {reset (outputfile) - interactive}
    fname := '-CONSOLE';
    %_Fopen (outputfile, fname, true, 0);
    outputfile.FUnit := unitScConsole;      {outputfile can not go to window for now}
    goutputrflag := FALSE;
  end;  {resetoutput}




{------------------------------ GetGPrefix -------------------------------------}
PROCEDURE GetGPrefix {(Var prefix : pathname)};

  BEGIN  {GetGPrefix}
    prefix := globWorkDir;
  END;   {GetGPrefix}





function %_keyInQ (applconsole : integer;
                   VAR appleperiod : boolean) : boolean;  external;  {in appastext}
                {Called from CkKeyEvents and %_pkeypress only. Call hwint's
                 corresponding keybdpeek to see if there is a real key in the
                 queue. Special keys skipped are Apple-s, and Apple-Shift-1 (sony
                 eject). Also return appleperiod if Apple-. is in q.
                 Get called if console is screen only.}


function CkKeyEvents : boolean;
              {called only by PAbortFlag (by %_fget from exec file) to check abort
               key, and %_pkeypress (%_keypress) only. should not be called by
               %_writechar, would be too slow}
              {called in non-window world only}

var errnum : integer;
    cparm :dctype;
    dcontrolname : pathname;
    done : boolean;
    packchar : packed array [0..1] of char;
    actual : longint;

begin  {CkKeyEvents}
    CkKeyEvents := false;
    if (apconsole = ord (alscreen)) or (apconsole = ord (mainscreen)) then
      begin
        CkKeyEvents := %_keyInQ (apconsole, PAbortKeyF);
                                {asm routine to use HWINT to see if key event in q}
        if PAbortKeyF then      {flush input up to apple . key}
          begin
            done := false;
            repeat
              read_data (errnum, consoleRefnum, ord (@packchar), 1,
                             actual, sequential, 0);      {get from OS console}
              if errnum > 0 then done := true
              else if actual = 1 then
                     if packchar [0] = chr (AbortKey) then done := true;
            until done;
          end;
      end
    else  {rs232a or b : 3 os calls for every keypress}
      begin
        if apconsole = ord (xsorocA) then dcontrolname := '-RS232A'
        else if apconsole = ord (xsorocB) then dcontrolname := '-RS232B';
        cparm.dcversion := 2;
        cparm.dccode := 19;                         {get status of rs232}
        device_control (errnum, dcontrolname, cparm);
        if errnum <= 0 then
          CkKeyEvents := cparm.dcdata [9] > 0;      {# of chars in input buffer}
      end;
end;  {CkKeyEvents}




function %_pkeypress {(var f : fib) : boolean };
                     {called by %_keypress in appastext. Any key pressed or not }
  begin                                 {use f in window environment}
    if f.funit < unitScPrinter then     {console or keyboard}
      if PConsWindow AND (PInWindow = 0) then %_pkeypress := PasTerm.VKeyPress
                                        {Window env, and not nested call}
      else                              {non-window env}
        if gexecflag then %_pkeypress := true
        else %_pkeypress := CkKeyEvents
    else                                        {printer or OS file}
     with f do
      begin
        if funit <> unitOSFile then %_pkeypress := false     {printer}
        else if (hibyt4 > ord(mainscreen)) then
               %_pkeypress := true              {assume there are keys for RS232A,B}
        else if (hibyt4 = ord(alscreen)) then
               %_pkeypress := %_keyInQ (ord (alscreen), PAbortKeyF)
        else if (hibyt4 = ord(mainscreen)) then
               %_pkeypress := %_keyInQ (ord (mainscreen), PAbortKeyF);
      end;
  end;  {%_pkeypress}



function PAbortFlag {: boolean };      { Apple-period entered or not }
var b : boolean;
begin
    if not PAbortKeyF then
      if PConsWindow AND (PInWindow = 0) then PAbortKeyF := PasTerm.VAbortKey
                                        {Window env, and not nested call}
      else
        b := CkKeyEvents;  {any key pressed ?,}                   {3.2, YC, 2/28/83}
                                {can not use %_pkeypress since it checks gexecflag}
    PAbortFlag := PAbortKeyF;
    PAbortKeyF := false;
end;  {PAbortFlag}



procedure ScreenCtr {(contrfun : integer)};
                                {standard screen control functions}
  var s : string [6];
  begin
    if PConsWindow AND (PInWindow = 0) then PasTerm.VScreenCtr (contrfun)
                                                  {Window env, or nested call}
    else
      begin
        if contrfun <= CclearELine then
          begin
            s := '  ';
            s [1] := chr (ESC);
          end
        else s := ' ';
        case contrfun of
          CclearScreen  : s [2] := '*';       {clear the whole screen}
          CclearEScreen : s [2] := 'Y';       {clear to the end of the screen}
          CclearELine   : s [2] := 'T';       {clear to end of line}

          CgoHome     : s [1] := chr (RS);    {move cursor to home position}
          CleftArrow  : s [1] := chr (BS);    {move cursor left one char position}
          CrightArrow : s [1] := chr (FF);    {move cursor right one char position}
          CupArrow    : s [1] := chr (VT);    {move cursor up one line position}
          CdownArrow  : s [1] := chr (LF);    {move cursor down one line position}
          otherwise exit (screenctr);
        end;  {case}

        write (s);
      end;
  end;  {ScreenCtr}



procedure GetPrDevice {(var PrDevice : e_name)};

  begin
    prdevice := PrintDevice;
  end;  {GetPrDevice}



PROCEDURE PTranLisaChar (toTranslate : boolean);
                                {to turn on or off translation for C.itoh}
  BEGIN
    PTrLisaChar := toTranslate;
  END;  {CiTranLisaCar}


procedure CkDiskEvents;
              {called only by %_fget from console to check disk and soft
               power button events, and by DSPaslibcall checking for softpowerbtn.
               should not be called by %_writechar, would be too slow}
  var errnum : integer;
      vname, devname : e_name;
      cparm :dctype;
      dcontrolname : pathname;
      fsparms : ut_parmt;
  begin
                                        {for non-window world only}
    if apconsole = ord (alscreen) then dcontrolname := '-ALTCONSOLE'
    else if apconsole = ord (mainscreen)then dcontrolname := '-MAINCONSOLE'
    else if apconsole = ord (xsorocA) then dcontrolname := '-RS232A'
    else if apconsole = ord (xsorocB) then dcontrolname := '-RS232B';

    cparm.dcversion := 2;
    cparm.dccode := 16;                 {now check for disk etc. events}
    device_control (errnum, dcontrolname, cparm);
    case cparm.dcdata [0] of
      1, 3, 11 :
             begin                      {twiggy or built-in sony disk inserted}
               if cparm.dcdata [0] = 1 then devname := 'UPPER'
               else devname := 'LOWER';
               mount (errnum, vname, devname, devname);
               if (errnum > 0) and (errnum <> 1052) then {call speaker};
             end;

      2, 4, 15 :
             begin      {twiggy disk button or Apple-Shift-1 to eject built-in sony}
               if cparm.dcdata [0] = 2 then devname := 'UPPER'
               else devname := 'LOWER';
               unmount (errnum, devname);
               if (errnum <= 0) or (errnum = 1062) then
                 begin
                   with fsparms do
                     begin
                       command := eject;
                       dev_name := devname;
                     end;
                   fs_utilities (errnum, fsparms);
                 end
               else
                 {call speaker};
             end;

      12, 13, 14 :
             begin              {sony inserted in Slot 1 - 3 with sony card}
               if cparm.dcdata [0] = 12 then devname := 'SLOT1CHAN1'
               else if cparm.dcdata [0] = 13 then devname := 'SLOT2CHAN1'
                    else devname := 'SLOT3CHAN1';
               mount (errnum, vname, devname, devname);
               if (errnum > 0) and (errnum <> 1052) then {call speaker};
             end;

      8 : SoftPwBtn := true;              {soft power button}
    end;
  end;  {CkDiskEvents}



procedure DSPaslibCall {(VAR ProcParam : dsProcParam)};
  var i : integer;
      ch : char;
  begin
    with ProcParam do
      case ProcCode of
        dsResProg : ResProcId := RProcessId;     {must be called before the
                                                  process starts running.}
        dsSoftPwbtn  : begin
                         CkDiskEvents;
                         SPButton := SoftPwBtn;
                         SoftPwBtn := false;
                       end;
        dsPrintDev   : begin
                         PrintDevice := PrDevice;
                         i := 1;                 {convert name to upper case}
                         WHILE i <= LENGTH(PrintDevice) DO
                           BEGIN
                           ch := PrintDevice[i];
                           IF (ch >= 'a') AND (ch <= 'z') THEN
                             PrintDevice[i] := CHR(ORD(ch)-ORD('a')+ORD('A'));
                           i := i+1;
                           END;
                       end;
        dsSetGPrefix : begin
                         errnum := 0;
                         IF prefix = '' THEN errnum := e_prefix
                         ELSE globWorkDir := prefix;
                       end;
        dsEnbDisk    : CkDskEventF := toEnbDisk;        {3.2, YC, 3/4/83}
        dsGetDiskEnbF : diskEnbF := CkDskEventF;       {result}
      end;
  end;  {DSPaslibCall}



  procedure %_PDSPaslibCall {(VAR ProcParam : pdsProcParam)}; {by LDS Shell only}
  begin
    with ProcParam do
      case ProcCode of
        pdsSomething : begin
                       end;
      end;
  end;  {%_PDSPaslibCall}




(*$IFC MONITOR*)

(* UCSDFileName (06-Jul-82) --------------------------------------------------- *)
FUNCTION UCSDFileName (VAR filename: pathname): BOOLEAN;

  (* Scans filename and returns TRUE if it is a UCSD name, else FALSE, which *)
  (* implies that it is an OS filename.  The algorithm for determining this  *)
  (* is open to change.  For now, the following test will be used:           *)
  (* If the filename contains a colon ':' and no leading '-', it will be     *)
  (* considered UCSD.                                                        *)

  BEGIN  (* UCSDFileName *)
  case fsmode of
    osmode : UCSDFileName := false;
    monitormode : UCSDFileName := true;
    otherwise
  (* Only considered a UCSD file name if it has a ':' in it and no leading '-' *)
      UCSDFileName := (POS (':', filename) > 0) and
                      (POS ('-', filename) <> 1);
    end; (* case *)

  END;   (* UCSDFileName *)
(*$ENDC*)



(* MapToUCSD (24-Jun-82) ----------------------------------------------------- *)
PROCEDURE MapToUCSD (var oserrnum: INTEGER);

  (* Maps OS error numbers to the coarser UCSD set *)
  (* The current plan is to map the OS error numbers, where possible, to  *)
  (* the 20 or so UCSD error numbers.  Where there is no correspondence   *)
  (* with a UCSD number, the OS number will be preserved.  This will      *)
  (* prevent loss of significant information about the cause of the error.*)

  BEGIN  (* MapToUCSD *)
  (*$IFC DEBUG*)
  IF trace (50) THEN
    writeln ('e MapToUCSD : oserrnum = ', oserrnum);
  (*$ENDC*)

{$IFC MONITOR}
  case oserrnum of
    882 :                       (* no space on volume for new data page *)
      oserrnum := ord (inoroom);
    921, 946 :             (* pathname invalid, or no such device - make and open*)
      oserrnum := ord (ibadtitle);
    948 :                       (* entry not found in open *)
      oserrnum := ord (inofile);
    894 :                       (* entry not found in kill_object *)
      oserrnum := ord (ilostfile);
    941 :                       (* pathname invalid or no such device in kill *)
      oserrnum := ord (ilostunit);
  end;  {case}
{$ENDC}

  END;   (* MapToUCSD *)




(*$IFC MONITOR*)
(*$I libpl/blockio1.text *)
(*$ENDC*)
(*$I libpl/blockio2.text *)
(*$I libpl/blockio3.text *)

END.
ÿ