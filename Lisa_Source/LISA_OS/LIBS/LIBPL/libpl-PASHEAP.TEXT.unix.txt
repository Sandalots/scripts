
  {**************************************************************************}
  {                                                                          }
  {                           Lisa Pascal Heap                               }
  {                           ---- ------ ----                               }
  {                                                                          }
  {                          By    Barry Haynes                              }
  {                                                                          }
  {                 (C) Copyright 1982 Apple Computer Inc.                   }
  {                                                                          }
  {**************************************************************************}

(*$%+*)


{$setc test := false }

{ifc test }
  (*

  {$setc heapd := true }

  UNIT tpasheap; {intrinsic;}

  *)
{elsec }

  {$S PASIOLIB }

  {$setc heapd := false }

  UNIT pasheap; intrinsic;

{endc }


INTERFACE

   USES
     (*$U libos/syscall.obj*) syscall;

   TYPE
     PHTB = -128..127;
     PHTP = ^PHTB;

     { To Initialize the heap, size and delta are put on 512 byte block
       boundries }
   procedure PLINITHEAP(var ernum,refnum: integer; size,delta: longint;
                        ldsn: integer; swapable: boolean);
     { NEW }
   procedure %_NEW(var ptr: PHTP; size: integer);

     { NEWL }
   procedure %_NEWL(var ptr: PHTP; lsize: longint);

     { MARK }
   procedure %_MARK(var ptr: PHTP);

     { RELEASE }
       {  }
   procedure %_RELSE(var ptr: PHTP);

     { MEMAVAIL }
   function  %_MEMAV: LONGINT;

     { HeapResult }
   function %_HHeapRes:integer;

     { Called by the OS when a process starts to init to the null heap }
   procedure %_pphhpreinit;

     { Called by the OS when a resident process restarts to reinit the heap }
   procedure %_pphhreinit;

     { The assembly MEMAVAIL calls this routine for the guts of MEMAVAIL }
   function  %_phwordsavail : longint;

     { DISPOSE, This is currently not implemented }
   procedure %_disp(var ptr: PHTP; size: longint);

IMPLEMENTATION

   CONST
       { The largest LDSN the OS allows }
     MAXLDSN = 16;

   TYPE
     STR128  = STRING[128];
     string5 = string[5];

   var
     HEAPSTART,
       {  }
     HEAPEND,
       { What the heap end starts out at in it's initial state. }
     INITIALHEAPEND,
       { The maximum heap words available depending on the LDSN you used for
         the heap }
     MAXMEMAV,
     HEAPDELTA:LONGINT;
     HEAPPTR:PHTP;
     HERNUM,
     HREFNUM,
       { Heap errors are :
         6151.Plinitheap can be called ONCE, BEFORE the heap is used.
         6152.Tried to NEW something of negative size.
         6153.Not enough Memavail for New.
         6154.Give me a break! No RELEASEs outside the Pascal heap.
         Other: an OS error Passed back in HeapResult }
     HerrResult:INTEGER;
       { True if we want debugging messages }
     hdebug,
       { True if this is not a memory resident heap, default is false. }
     heapswapable,
       { True if the heap has been initialized, if a data segment has been made. }
     heapinited,
       { True if it is legal to call plinitheap at this time }
     plinitok:boolean;


   function %_MEMAV { : LONGINT } ; EXTERNAL;

   (*
   PROCEDURE HEAPERROR(STR:STR128);
     VAR SP:CHAR;
   BEGIN
     WRITELN('Pascal Heap Error ',STR);
     WRITE(', Space to Continue at Own Risk: ');
     READ(SP);
     WRITELN
   END;

     { procedure to convert an integer to a string value. }
   procedure InttoStr (int : integer;  var str : string5) ;
     var
       i : integer;
       intval : longint;
       power : longint;
       n : integer;
       start : 1..2;
       digit : 0..9;
   begin
     intval := abs(int);
     if int < 0 then start := 2
       else start := 1;

     n := 1;   power := 1;
     while intval DIV power <> 0 do
       begin
         n := n + 1;   power := power * 10
       end;
     n := n - 1;   power := power DIV 10;

     for i := start to start + n - 1 do
       begin
         digit := intval DIV power;
         str[i] := chr(ord('0') + digit);
         intval := intval MOD power;
         power := power DIV 10
       end;

     {$R-}
     if int > 0 then str[0] := chr(n)
     else if int < 0 then
       begin
         str[1] := '-';
         str[0] := chr(n+1)
       end
     else
       str := '0';
     {$R+}
   end { InttoStr };
   *)

   procedure PLINITHEAP{(var ernum,refnum: integer; size,delta: longint;
                        ldsn: integer; swapable: boolean)};

      var
        PHNAME :PATHNAME;
        disksize:longint;
        ch:char;
        proc_info : procinfoRec;

      procedure roundblock(var s:longint);
      begin
        s := ((s + 511) div 512) * 512;
      end;

    begin

      {$ifc test }
      {$ifc heapd }
      writeln('heapdebug?');
      read(ch);
      hdebug := (ch='Y') or (ch='y');
      {$endc }
      {$endc }

      {$ifc heapd }
      if hdebug then
        writeln('enter plinitheap');
      {$endc }

      HerrResult := 0;
      if not plinitok then
        begin
          HerrResult := 6151;
          exit(plinitheap)
        end;

      PLINITOK := FALSE;

      if not heapinited then
        begin
            { put size and delta on 512 byte block boundries }
          roundblock(size);
          roundblock(delta);
          PHNAME := 'PASCALHEAP';
          info_process (ernum, my_id, proc_info);    {use program's volume if any}
          if ernum <= 0 then
            phname := proc_info.progpathname;   {filename part is ignored by OS}

          HEAPDELTA := delta;
          heapswapable := swapable;
          if heapswapable then
            disksize := size
          else
            disksize := 0;
          MAXMEMAV := (MAXLDSN - LDSN + 1) * 65536;
          MAKE_DATASEG(ERNUM,PHNAME,SIZE,DISKSIZE,HREFNUM,HEAPSTART,LDSN,DS_PRIVATE);
          refnum := HREFNUM;
          IF ERNUM = 0 THEN
            begin
              HEAPPTR := POINTER(HEAPSTART);
              HEAPEND := HEAPSTART+SIZE-256 { 256 is slop for string assignment } ;
              INITIALHEAPEND := HEAPEND;
              HEAPINITED := TRUE
            END
          ELSE
            PLINITOK := TRUE;
        end;

      {$ifc heapd }
      if hdebug then
        writeln('exit plinitheap heapend = ',heapend,' heapptr = ',ord4(heapptr));
      {$endc }

    end; { PLINITHEAP }

  {$ifc not test }
  {$S PASLIB1 }
  {$endc }

    { Called by the OS when a process starts to init to the null heap }
  procedure %_pphhpreinit;
  begin
    HerrResult := 0;
    plinitok := true;
    hdebug  := false;
    heapinited := false;
    heapptr := pointer(0);
    heapend := 0
  end;

    { Called by the OS when a resident process restarts to reinit the heap }
  procedure %_pphhreinit;
    VAR
      TMPPTR:PHTP;
  begin
    HerrResult := 0;
    plinitok := true;
    TMPPTR := pointer(heapstart);
    if heapinited then
      %_relse(TMPPTR)
  end;

  {$ifc not test }
  {$S PASIOLIB }
  {$endc }

    { Returns in ammount the maximum number of bytes I could increase the heap by.  This is always
      a multible of 512 bytes.  Returns false if this number is <= 0. }
  function getsafeammount(var ammount:longint):boolean;
    const
        codesegspace = 65536;
    var swapspace,dataspace,curcodesize,maxcodesize:longint;
        herstr:string5;
  begin

    {$ifc heapd }
    if hdebug then
      writeln('enter getsafeammount');
    {$endc }

    getsafeammount := false;
    ammount := 0;
    MEM_INFO(HERNUM,swapspace,dataspace,curcodesize,maxcodesize);
    IF HERNUM > 0 THEN
      begin
        HerrResult := hernum;
        exit(getsafeammount)
      end;
    ammount := ((swapspace-codesegspace) div 512) * 512;
    if ammount < 0 then
      ammount := 0;

    getsafeammont := ammount > 0;

    {$ifc heapd }
    if hdebug then
      writeln('exit getsafeammount, ammount =',ammount);
    {$endc }

  end;

    { Returns true if the size of the heap was increased. }
  function growheap:boolean;
    const    defaultsize  = 16384;
             defaultdelta = 16384;
             defaultldsn  = 5;
             dfaultswap   = false;
    var      herstr:string5;
             safeammount,
             nmemsize,ndisksize,diskchange,thedelta:longint;

  begin

    {$ifc heapd }
    if hdebug then
      writeln('enter growheap heapend = ',heapend,' heapptr = ',ord4(heapptr));
    {$endc }

    growheap := false;
    if not heapinited then
        { Make a new heap }
      begin
        PLINITHEAP(HERNUM,HREFNUM,defaultsize,defaultdelta,defaultldsn,dfaultswap);
        if HERNUM > 0 then
          HerrResult := HerNum
        else
          growheap := true
      end
    else
        { Increase size of current heap }
      if getsafeammount(safeammount) then
        begin
          if heapdelta < safeammount then
            thedelta := heapdelta
          else
            thedelta := safeammount;
          if heapswapable then
            diskchange := thedelta
          else
            diskchange := 0;

          {$ifc heapd }
          if hdebug then
            writeln('calling size_dataseg');
          {$endc }

          size_dataseg(HERNUM,HREFNUM,thedelta,nmemsize,diskchange,ndisksize);

          {$ifc heapd }
          if hdebug then
            writeln('called size_dataseg, nmemsize = ',nmemsize,' ndisksize = ',ndisksize);
          {$endc }

          if HERNUM > 0 then
            HerrResult := HerNum
          else
            begin
              heapend := heapend + thedelta;
              growheap := true
            end
        end;

      {$ifc heapd }
      if hdebug then
        writeln('exit growheap heapend = ',heapend,' heapptr = ',ord4(heapptr));
      {$endc }

  end;

  procedure %_NEW(* var ptr: PHTP; size: integer*);
    var lsize:longint;
  begin
    HerrResult := 0;
    ptr := nil;
    lsize := size;
    if not heapinited THEN
      begin
          { Make a heap }
        if not growheap then
          EXIT(%_NEW)
      end
    else
      PLINITOK := FALSE;
      { -32768 is an execption since we really want +32k }
    if lsize = -32768 then
      lsize := 32768;

    {$ifc heapd }
    if hdebug then
      writeln('enter new of ',lsize,' heapend = ',heapend,' heapptr = ',ord4(heapptr));
    {$endc }

    IF LSIZE < 0 THEN
      HerrResult := 6152
    ELSE
      BEGIN
        IF ODD(LSIZE) THEN
          LSIZE := LSIZE + 1;
        WHILE HEAPEND-ORD4(HEAPPTR) < LSIZE DO
          IF NOT growheap THEN
            begin
              HerrResult := 6153;
              EXIT(%_NEW);
            end;
        PTR := HEAPPTR;
        HEAPPTR := POINTER(ORD4(HEAPPTR)+LSIZE)
      END;

    {$ifc heapd }
    if hdebug then
      writeln('exit %_new heapend = ',heapend,' heapptr = ',ord4(heapptr));
    {$endc }

  end; (*%_NEW*)

  procedure %_NEWL(* var ptr: PHTP; lsize: longint*);
  begin
    HerrResult := 0;
    ptr := nil;
    if not heapinited THEN
      begin
          { Make a heap }
        if not growheap then
          EXIT(%_NEWL)
      end
    else
      PLINITOK := FALSE;
      { -32768 is an execption since we really want +32k }
    if lsize = -32768 then
      lsize := 32768;

    {$ifc heapd }
    if hdebug then
      writeln('enter newl of ',lsize,' heapend = ',heapend,' heapptr = ',ord4(heapptr));
    {$endc }

    IF LSIZE < 0 THEN
      HerrResult := 6152
    ELSE
      BEGIN
        IF ODD(LSIZE) THEN
          LSIZE := LSIZE + 1;
        WHILE HEAPEND-ORD4(HEAPPTR) < LSIZE DO
          IF NOT growheap THEN
            begin
              HerrResult := 6153;
              EXIT(%_NEWL);
            end;
        PTR := HEAPPTR;
        HEAPPTR := POINTER(ORD4(HEAPPTR)+LSIZE)
      END;

    {$ifc heapd }
    if hdebug then
      writeln('exit %_newl heapend = ',heapend,' heapptr = ',ord4(heapptr));
    {$endc }

  end; (*%_NEWL*)

  procedure %_MARK{(var ptr: PHTP)};
  begin

    {$ifc heapd }
    if hdebug then
      writeln('enter %_mark, heapend = ',heapend,' heapptr = ',ord4(heapptr));
    {$endc }

    HerrResult := 0;
    if not heapinited THEN
      begin
          { Make a heap }
        if not growheap then
          EXIT(%_MARK)
      end
    else
      PLINITOK := FALSE;
    PTR := HEAPPTR;

    {$ifc heapd }
    if hdebug then
      writeln('exit %_mark, heapend = ',heapend,' heapptr = ',ord4(heapptr));
    {$endc }

  end;(*%_MARK*)

  procedure %_RELSE{(var ptr: PHTP)};
    var      nmemsize,ndisksize,diskchange:longint; herstr:string5;
  begin
    HerrResult := 0;
    if not heapinited THEN
      begin
          { Make a heap }
        if not growheap then
          EXIT(%_RELSE)
      end
    else
      PLINITOK := FALSE;

    {$ifc heapd }
    if hdebug then
      writeln('enter relse of ptr =',ord4(ptr),' heapend = ',heapend,' heapptr = ',
              ord4(heapptr));
    {$endc }

    IF (ORD4(PTR)>=HEAPSTART) AND (ORD4(PTR) < HEAPEND) THEN
      BEGIN
        WHILE ORD4(PTR) < HEAPEND-HEAPDELTA DO
            { size down the heap }
          BEGIN
            if heapswapable then
              diskchange := -HEAPDELTA
            else
              diskchange := 0;
            size_dataseg(HERNUM,HREFNUM,-HEAPDELTA,nmemsize,diskchange,
                         ndisksize);
            if HERNUM > 0 then
              HerrResult := HerNum
            else
              HEAPEND := HEAPEND - HEAPDELTA;
          END;
        HEAPPTR := PTR;
      END
    ELSE
      HerrResult := 6154;

    {$ifc heapd }
    if hdebug then
      writeln('exit relse of ptr =',ord4(ptr),' heapend = ',heapend,' heapptr = ',
              ord4(heapptr));
    {$endc }

  end;(*%_RELSE*)

    { The assembly MEMAVAIL calls this routine for the guts of MEMAVAIL }
  function %_phwordsavail;
    var maxwordsavail,curldsnwordsavail,cursafeammount,temp:longint;
  begin
    HerrResult := 0;
    %_phwordsavail := 0;
    if not heapinited THEN
      begin
          { Make a heap }
        if not growheap then
          EXIT(%_PHWORDSAVAIL)
      end
    else
      PLINITOK := FALSE;

    {$ifc heapd }
    if hdebug then
      writeln('enter memavail of  heapend = ',heapend,' heapptr = ',ord4(heapptr));
    {$endc }

      { we want a value for cursafeammount even if it is 0. }
    if not getsafeammount(cursafeammount) then;
    maxwordsavail := (
                        { The ammount left in the heap dataseg }
                      (HEAPEND - ORD4(HEAPPTR)) +
                        { The safe ammount we can still get from the memman }
                      cursafeammount
                        { make it into words }
                      ) DIV 2;
    curldsnwordsavail := (
                            { The max number of words we can get by the LDSN
                              we chose }
                          MAXMEMAV -
                            { The number of words we have already used }
                          (((ORD4(HEAPPTR)-HEAPSTART)+1) DIV 2));
    if maxwordsavail > curldsnwordsavail then
      temp := curldsnwordsavail
    else
      temp := maxwordsavail;
    %_phwordsavail := temp;

    {$ifc heapd }
    if hdebug then
      writeln('exit memavail of memavail = ',temp,' heapend = ',heapend,
              ' heapptr = ',ord4(heapptr));
    {$endc }

  end;

     { HeapResult }
   function %_HHeapRes{:integer};
   begin
     %_HHeapRes := HerrResult
   end;


     { DISPOSE, This is currently not implemented }
   procedure %_disp{(var ptr: PHTP; size: longint)};
   begin
     HerrResult := 0;
   end;

end.



ÿ