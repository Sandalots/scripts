; File: APPASTEXT.TEXT
; for application PASLIB only

;        .PROC   %%%TEXT
        .PROC   %_keypress

        .include        libpl/paslibdefs.text

        .if     libproducing=appaslib   ; for appaslib only - the whole file
        .DEF    %W_LN,%W_C,%W_I,%W_B,%W_STR,%W_PAOC,%_PAGE
        .DEF    %R_C,%R_I,%R_STR,%R_PAOC,%R_LN
        .def    %_keyInQ, %_GetDWMapTableAddr
;        .def    %_keypress

        .ref    %_writeln, %_writechar, %_readln, %_readchar, %_setiorslt
        .ref    %_pkeypress

;
; Macro TrapToHW is used to call the HWINT routines
; parameter %1 is routine number
;
        .macro  TrapToHW
        move.l  d7,-(sp)                ; save register
        move.w  #%1,d7                  ; routine number
        trap    #5
        move.l  (sp)+,d7
        .endm

;
; Routine numbers for HWINT
;
KeybdPeek       equ     $3c
AltKeyPeek      equ     $92


;
; Keypress function
;
; Parameters:  ST.L - File address
;
; Returns:     ST.B - Boolean Result
;
; This routine preserves all registers.

;%_keypress
        MOVEM.L D0-D7/A0-A6,-(SP)
        clr.w   -(sp)           ; function result for %_pkeypress (var f : fib)
                                ; temp borrowed to be used for Pkeypress
        move.l  66(sp),-(sp)    ; get the file address
        jsr     %_pkeypress
        move.w  (sp)+,66(sp)    ; put the function result in proper place
        move.l  60(sp),62(sp)
        movem.l (sp)+,d0-d7/a0-a6
        ADDQ.L  #2,SP           ; Bump SP
        RTS

;
;  function %_keyInQ (applconsole : integer;
;                     VAR appleperiod : boolean) : boolean;  external;
;       Called from CkKeyEvents, call hwint's corresponding keybdpeek to see if
;       there is a real key in the queue. Special keys skipped are Apple-s, and
;       Apple-Shift-1 (sony eject). Also return appleperiod if Apple-. is in q.
;       Get called if console is screen only.

;     destroy A0

%_keyInQ
        movem.l d0-d7,-(sp)
        clr.w   -(sp)           ; index := 0    (actually starts from 1)
        clr.w   44(sp)          ; init function result to false
        move.l  38(sp),a0       ; @appleperiod
        clr.b   (a0)            ; init it to false
next
        move.l  #1,d0
        add.w   #1,(sp)
        move.w  (sp),d1         ; peek (repeat, index)
        tst.w   42(sp)          ; apconsole par
        beq     @1
        TrapToHW        KeybdPeek
        bra.s   @2
@1      TrapToHW        AltKeyPeek
@2      tst.b   d0              ; if no event then result = false, exit
        beq.s   ZK.exit
        tst.b   d2              ; is key event ?
        beq.s   next            ; 0 is disk event, skip it
        cmp.b   #1,d2           ; 1 is mouse event, skip it
        beq.s   next
        btst    #3,d3           ; command (Apple) key down ?
        beq.s   @4              ; no
        cmp.b   #'s',d2         ; key = 's' ?
        beq.s   next            ; skip command s
        cmp.b   #'S',d2
        beq.s   next            ; skip command S
        cmp.b   #'.',d2         ; Apple . ?
        bne.s   @3              ; no, keep checking
        move.b  #1,(a0)         ; yes, appleperiod := true
@3      btst    #1,d3           ; shift key down ?
        beq.s   @4              ; no
        cmpi.b  #$74,d1         ; Apple-Shift-1 ?
        beq.s   next            ; yes, skip it, not real key

@4      move.w  #$101,44(sp)    ; result = true (low byte) if key event
        bra.s   next            ; continue to check for apple .
ZK.exit
        move.w  (sp)+,d0        ; discard index
        movem.l (sp)+,d0-d7
        move.l  (sp)+,a0        ; return pc
        addq    #6,sp           ; pop parameter
        jmp     (a0)


;
; ASCII Character EQUs
;

RUBOUT  .EQU    127
BACKSP  .EQU    8

feoln           equ     4       ; feoln byte offset in fib, for console
fstate          equ     7       ; fstate byte offset in fib
fgotchar        equ     2       ; fstate value, should agree with blkiointr
;
;
;

; OUTCH - Display a character on the CRT
;
; Parameters:  D0.B - Character to be output
;              A0.L - Address of output file
;

OUTCH:  MOVEM.L D0-D7/A0-A6,-(SP)
        move.b  d0,-(sp)        ; save the char on the stack
        MOVE.L  A0,-(SP)        ; Push the address of the file
        pea     4(sp)           ; Push the char address
        move.w  #1,-(sp)        ; output 1 char
        jsr     %_writechar     ; console handled by %_writechar too
        move.b  (sp)+,d0        ; pop the char
O.DONE: MOVEM.L (SP)+,D0-D7/A0-A6
        RTS



;
; %W_LN - WRITELN
;
; Parameters:  ST.L - Address of output file
;
; Scratches A0,A2
;

%W_LN:
        MOVE.L  (SP)+,A2        ; Pop return address
        MOVE.L  (SP)+,A0        ; Pop file address
        MOVEM.L D0-D7/A0-A6,-(SP)
        MOVE.L  A0,-(SP)        ; Push the address of the file
        jsr     %_writeln

WL.DONE:MOVEM.L (SP)+,D0-D7/A0-A6
        JMP     (A2)




;
; %W_C - Display a character on the CRT
;
; Parameters:  ST.L - Address of output file
;              ST.B - Character to be output
;              ST.W - Size of field to print
;
; Scratches:   D2,D3,A0,A1
;

%W_C:
        MOVE.L  (SP)+,A1        ; return address
        MOVE.W  (SP)+,D2        ; size of field to print
        MOVE.B  (SP)+,D3        ; character to be output
        MOVE.L  (SP)+,A0        ; address of output file
        SUBQ.W  #1,D2           ; get # of space fill
        BLE.S   WC.TEST
        movem.l d3/a0-a1,-(sp)  ; save registers
        move.l  a0,-(sp)        ; file address
        clr.l   -(sp)           ; string address nil to mean space fill
        move.w  d2,-(sp)        ; count
        jsr     %_writechar     ; write the spaces out
        movem.l (sp)+,d3/a0-a1  ; restore registers

WC.TEST:MOVE.B  D3,D0
        JSR     OUTCH
        JMP     (A1)



;
; %W_STR - WRITE STRING
;
; Parameters:  ST.L - Address of output file
;              ST.L - Address of string
;              ST.W - Size of field to print
;
; Scratches:   D0,D2,D3,A0,A1,A2
;

%W_STR:
        MOVE.L  (SP)+,A2        ; return address
        MOVE.W  (SP)+,D3        ; size of field to print
        MOVE.L  (SP)+,A1        ; address of string
        MOVE.L  (SP)+,A0        ; address of output file
        CLR.W   D2
        tst.b   (a1)            ; make resident in case it is in code seg
        MOVE.B  (A1)+,D2        ; Get string size
        andi.w  #$ff,d3         ; truncate field size to 255 max
        cmp.w   d2,d3           ; check for extra blank and field size
        bgt.s   @1
        tst.w   d3
        ble.s   s.test1         ; field size is <= 0, use string size
        bra.s   s.test2         ; filed size is less, use field size
@1      SUB.W   D2,D3           ; get extra blanks
        movem.l d2/a0-a2,-(sp)  ; save registers
        move.l  a0,-(sp)        ; file address
        clr.l   -(sp)           ; string address nil to mean space fill
        move.w  d3,-(sp)        ; count
        jsr     %_writechar     ; write the spaces out
        movem.l (sp)+,d2/a0-a2  ; restore registers
s.test1:move.w  d2,d3           ; now use string size for output

S.TEST2:move.l  a2,-(sp)        ; save return address on stack
        move.l  a0,-(sp)        ; file address
        move.l  a1,-(sp)        ; string address, beyond length byte
        move.w  d3,-(sp)        ; count
        jsr     %_writechar     ; write the whole string, must copy string first
        rts



;
; %W_PAOC - WRITE PACKED ARRAY OF CHARACTER
;
; Parameters:  ST.L - Address of output file
;              ST.L - Address of string
;              ST.W - Acutal length
;              ST.W - Size of field to print
;
; Scratches:   D0,D2,D3,A0,A1,A2
;

%W_PAOC:
        MOVE.L  (SP)+,A2        ; return address
        MOVE.W  (SP)+,D3        ; size of field to print
        MOVE.W  (SP)+,D2        ; actual length
        MOVE.L  (SP)+,A1        ; address of string
        tst.b   (a1)            ; make resident in case it is in code seg
        MOVE.L  (SP)+,A0        ; address of output file
        andi.w  #$ff,d3         ; truncate field size to 255 max
        cmp.w   d2,d3           ; check for extra blank and field size
        bgt.s   @1
        tst.w   d3
        ble.s   p.loop1         ; field size is <= 0, use actual size
        bra.s   p.loop2         ; filed size is less, use field size
@1      SUB.W   D2,D3           ; Check for xtra blanks
        movem.l d2/a0-a2,-(sp)  ; save registers
        move.l  a0,-(sp)        ; file address
        clr.l   -(sp)           ; string address nil to mean space fill
        move.w  d3,-(sp)        ; count
        jsr     %_writechar     ; write the spaces out
        movem.l (sp)+,d2/a0-a2  ; restore registers
p.loop1:move.w  d2,d3           ; now use actual length

P.LOOP2:move.l  a2,-(sp)        ; save return address on stack
        move.l  a0,-(sp)        ; file address
        move.l  a1,-(sp)        ; packed array address
        move.w  d3,-(sp)        ; count
        jsr     %_writechar
        rts



;
; %W_I - WRITE INTEGER
;
; Parameters:  ST.L - Address of output file
;              ST.L - Value to print
;              ST.W - Size of field
;
; Scratches:   D0,D1,D2,D3,A0,A2
;

;%W_I:
;        MOVE.L  (SP)+,A2        ; return address
;        MOVE.W  (SP)+,D1        ; size of field
;        MOVE.L  (SP)+,D0        ; value to print
;        MOVE.L  (SP)+,A0        ; address of output file
;        MOVEM.L D4-D6,-(A7)
;        CLR.W   D2              ; # of chars on stack
;        MOVE.L  D0,D6
;        BGE.S   I.POS
;        NEG.L   D0
;I.POS:  MOVE.L  D0,D3
;I.LOOP: MOVE.L  D3,D4
;        CLR.W   D4
;        SWAP    D4              ; D4 = 0000 MSHV
;        DIVU    #10,D4          ; D4 = MSHR MSHQ
;        MOVE.L  D4,D5           ; D5 = MSHR MSHQ
;        MOVE.W  D3,D5           ; D5 = MSHR LSHV
;        DIVU    #10,D5          ; D5 = REMN LSHQ
;        SWAP    D4              ; D4 = MSHQ MSHR
;        MOVE.L  D4,D3           ; D3 = MSHQ MSHR
;        MOVE.W  D5,D3           ; D3 = MSHQ LSHQ
;        SWAP    D5
;        ADDI.W  #'0',D5
;        MOVE.W  D5,-(SP)
;        ADDQ.W  #1,D2
;        TST.L   D3
;        BNE.S   I.LOOP
;        TST.L   D6
;        BGE.S   I.POS2
;        ADDQ.W  #1,D2
;        MOVE.W  #'-',-(SP)
;I.POS2: MOVE.W  D1,D3
;        SUB.W   D2,D3
;        BLE.S   I.LOOP3
;I.LOOP2:MOVEQ   #' ',D0
;        JSR     OUTCH
;        SUBQ.W  #1,D3
;        BGT.S   I.LOOP2
;I.LOOP3:MOVE.W  (SP)+,D0
;        JSR     OUTCH
;        SUBQ.W  #1,D2
;        BGT.S   I.LOOP3
;        MOVEM.L (A7)+,D4-D6
;        JMP     (A2)

; Optimized version : scratches A1 too.

%W_I:
        MOVE.L  (SP)+,A2        ; return address
        MOVE.W  (SP)+,D1        ; size of field
        MOVE.L  (SP)+,D0        ; value to print
        MOVE.L  (SP)+,A0        ; address of output file
        MOVEM.L D4-D6,-(A7)
        CLR.W   D2              ; # of chars on stack
        MOVE.L  D0,D6
        BGE.S   I.POS
        NEG.L   D0
I.POS:  MOVE.L  D0,D3
        move.l  sp,a1           ; use a1 as string pointer to stack,
                                ; so can advance one byte at a time
I.LOOP: MOVE.L  D3,D4
        CLR.W   D4
        SWAP    D4              ; D4 = 0000 MSHV
        DIVU    #10,D4          ; D4 = MSHR MSHQ
        MOVE.L  D4,D5           ; D5 = MSHR MSHQ
        MOVE.W  D3,D5           ; D5 = MSHR LSHV
        DIVU    #10,D5          ; D5 = REMN LSHQ
        SWAP    D4              ; D4 = MSHQ MSHR
        MOVE.L  D4,D3           ; D3 = MSHQ MSHR
        MOVE.W  D5,D3           ; D3 = MSHQ LSHQ
        SWAP    D5
        ADDI.W  #'0',D5
        MOVE.b  D5,-(a1)        ; put one byte of char on stack
        ADDQ.W  #1,D2
        TST.L   D3
        BNE.S   I.LOOP
        TST.L   D6              ; see if negative
        BGE.S   I.POS2
        ADDQ.W  #1,D2
        MOVE.b  #'-',-(a1)
I.POS2:
        move.l  a1,d0           ; see if string address is even
        btst    #0,d0
        beq     @1              ; yes
        subq.l  #1,a1           ; no, make it even
@1      move.l  sp,-(a1)        ; save sp before string is put on stack
        move.l  a1,sp           ; now sp points to old sp and string
        move.l  d0,a1           ; put string pointer back, in case changed

        MOVE.W  D1,D3
        SUB.W   D2,D3           ; d2 = size of integer in # of bytes
        BLE.S   I.LOOP3

I.LOOP2:movem.l d2/a0-a2,-(sp)  ; save registers
        move.l  a0,-(sp)        ; file address
        clr.l   -(sp)           ; string address nil to mean space fill
        move.w  d3,-(sp)        ; count
        jsr     %_writechar     ; output the spaces
        movem.l (sp)+,d2/a0-a2  ; restore registers

I.LOOP3:move.l  a2,-(sp)        ; save return pc
        move.l  a0,-(sp)        ; file address
        move.l  a1,-(sp)        ; string address
        move.w  d2,-(sp)        ; count
        jsr     %_writechar     ; output the integer
        move.l  (sp)+,a2        ; restore return pc
        move.l  (sp)+,sp        ; restore sp before string was put on stack
        MOVEM.L (A7)+,D4-D6
        JMP     (A2)



;
; %W_B - WRITE BOOLEAN
;
; Parameters:  ST.L - Address of output file
;              ST.B - Value to print
;              ST.W - Size of field
;
; Scratches D2,D3,A2

%W_B:
        MOVE.L  (SP)+,A2        ; return address
        MOVE.W  (SP)+,D2        ; Size of field
        MOVE.B  (SP)+,D3        ; Value to print
        BEQ.S   B.FALSE
        PEA     C.TRUE          ; Print 'TRUE'
        BRA.S   B.GOON
B.FALSE:PEA     C.FALSE         ; Print 'FALSE'
B.GOON: MOVE.W  D2,-(SP)
        MOVE.L  A2,-(SP)
        JMP     %W_STR

C.FALSE:.BYTE   5,70,65,76,83,69        ; 5,'FALSE'
C.TRUE: .BYTE   5,84,82,85,69,32        ; 5,'TRUE '



;
; %_PAGE - PAGE
;
; Parameters:  ST.L - Address of output file
;
; Scratches D0,A0,A1

%_PAGE:
        MOVE.L  (SP)+,A1        ; Pop return address
        MOVE.L  (SP)+,A0        ; Pop file address
        MOVEQ   #12,D0          ; ASCII FF
        MOVE.L  A1,-(SP)
        JSR     OUTCH
        RTS



;
; INCH - Reads a character from the console
;
; Parameters:  A0 - Address of input file
;
; Returns:     D0 - the character
;
INCH:   MOVEM.L D1-D7/A0-A6,-(SP)
        MOVE.w  #0,-(SP)        ; Reserve space for func result
        MOVE.L  A0,-(SP)        ; Push the address of the file
        jsr     %_readchar      ; INPUT handled by freadchar too

I.DONE: MOVE.W  (A7)+,D0        ; Fetch the result
        MOVEM.L (SP)+,D1-D7/A0-A6
        RTS



;
; %R_C - Read a character
;
; Parameters: ST.L - File Address
;
; Returns:    ST.B - The character read
;
; Scratches D0,A0,A2
;

%R_C:
        MOVEM.L D0/A0,-(SP)
        MOVE.L  12(SP),A0       ; A0 = file address
        JSR     INCH
        MOVE.B  D0,14(SP)
        MOVE.L  8(SP),A0
        MOVE.L  A0,10(SP)
        MOVEM.L (SP)+,D0/A0
        ADDQ.L  #2,SP
        RTS



;
; %R_LN - READLN
;
; Parameters:  ST.L - Address of input file
;
; Scratches A0,A2
;

%R_LN:
        MOVE.L  (SP)+,A2                ; return address
        MOVE.L  (SP)+,A0                ; address of input file
        MOVEM.L D0-D7/A0-A6,-(SP)
        MOVE.L  A0,-(SP)                ; Push the address of the file
        jsr     %_readln

RL.DONE:MOVEM.L (SP)+,D0-D7/A0-A6
        JMP     (A2)



;
; %R_PAOC - Read Packed Array of Character
;
; Parameters: ST.L - File Address
;             ST.L - Array Address
;             ST.W - Size of array in bytes
;
; Scratches D0,D1,D2,D3,A0,A1,A2
;

%R_PAOC:
        MOVE.L  (SP)+,A2        ; return address
        MOVE.W  (SP)+,D2        ; Size of array
        MOVE.W  D2,D3           ; Save size for RUBOUT and BACK-SPACE
        MOVE.L  (SP)+,A1        ; Address of PAOC
        MOVE.L  (SP)+,A0        ; Address of File
        MOVE.L  D4,-(A7)
        TST.B   feoln(a0)       ; check for end of line
        BNE.S   RP.EOLN         ; PAOCs never eat an EOLN
RP.LOOP:JSR     INCH
        TST.B   feoln(a0)       ; check for end of line
        BNE.S   RP.EOLN
        CMP.B   #RUBOUT,D0      ; Was it a RUBOUT?
        BEQ.S   RP.RUB
        CMP.B   #BACKSP,D0      ; Was it a BACK-SPACE?
        BEQ.S   RP.BAC
        MOVE.B  D0,(A1)+
        SUBQ.W  #1,D2
        BGT.S   RP.LOOP
        move.l  (a7)+,d4
        JMP     (A2)
RP.RUB: MOVE.W  D3,D4
        SUB.W   D2,D4           ; How many to erase?
        BEQ.S   RP.LOOP         ; Exit if none
        SUBA.W  D4,A1           ; Reset pointer
        MOVE.W  D3,D2           ; Reset counter
        JSR     ERASED4
        BRA.S   RP.LOOP
RP.BAC: CMP.W   D2,D3           ; See if anything to erase
        BEQ.S   RP.LOOP
        ADDQ.W  #1,D2           ; Yes. Bump counter
        SUBQ.W  #1,A1           ; Bump character pointer
        MOVEQ   #1,D4
        JSR     ERASED4
        BRA.S   RP.LOOP
RP.LOP2:MOVE.B  #' ',(A1)+
RP.EOLN:SUBQ.W  #1,D2
        BGE.S   RP.LOP2
        MOVE.L  (A7)+,D4
        JMP     (A2)



;
; ERASED4 - Type BS,SP,BS (D4) times
;

ED4LOOP:MOVEQ   #BACKSP,D0
        JSR     OUTCH
        MOVEQ   #' ',D0
        JSR     OUTCH
        MOVEQ   #BACKSP,D0
        JSR     OUTCH
ERASED4:dbra    D4,ED4LOOP      ; dec d4 and branch if d4 <> -1
        RTS



;
; %R_STR - Read String
;
; Parameters: ST.L - File Address
;             ST.L - String Address
;             ST.W - Max size of string
;
; Scratches D0,D1,D2,A0,A1,A2
;

%R_STR:
        MOVE.L  (SP)+,A2        ; Pop return address
        MOVE.W  (SP)+,D2        ; Size of array
        MOVE.L  (SP)+,A1        ; Address of String
        MOVE.L  (SP)+,A0        ; Address of File
        MOVE.L  A2,-(SP)        ; Push return address
        MOVE.L  D4,-(A7)
        MOVE.L  A1,A2           ; Save pointer to length byte
        CLR.B   (A1)+
        TST.B   feoln(a0)       ; check for end of line
        BNE.S   RS.EOLN         ; Strings never eat an EOLN
RS.LOOP:JSR     INCH
        TST.B   feoln(a0)       ; check for end of line
        BNE.S   RS.EOLN
        CMP.B   #RUBOUT,D0      ; Was it a RUBOUT?
        BEQ.S   RS.RUB
        CMP.B   #BACKSP,D0      ; Was it a BACK-SPACE?
        BEQ.S   RS.BAC
        MOVE.B  D0,(A1)+
        ADDQ.B  #1,(A2)
        SUBQ.W  #1,D2
        BGT.S   RS.LOOP
RS.EOLN:MOVE.L  (A7)+,D4
        RTS
RS.RUB: CLR.W   D4
        MOVE.B  (A2),D4
        BEQ.S   RS.LOOP         ; Anything to erase?
        CLR.B   (A2)
        ADD.W   D4,D2
        SUBA.W  D4,A1
        JSR     ERASED4
        BRA.S   RS.LOOP
RS.BAC: TST.B   (A2)            ; Anything to erase?
        BEQ.S   RS.LOOP
        SUBQ.B  #1,(A2)
        ADDQ.W  #1,D2
        SUBQ.L  #1,A1
        MOVEQ   #1,D4
        JSR     ERASED4
        BRA.S   RS.LOOP



;
; %R_I - Read Integer
;
; Parameters: ST.L - File Address
;
; Returns:    ST.L - The integer read
;

%R_I:
        MOVEM.L D0-D7/A0-A6,-(SP)
        MOVE.L  64(SP),A0       ; File Address
        CLR.L   D2              ; Value
        CLR.W   D6              ; D6 = #chars
        CLR.B   D7              ; Sign
RI.LOPB:JSR     INCH
        CMPI.B  #' ',D0         ; skip space
        BEQ.S   RI.LOPB
        CMPI.B  #13,D0          ; and cr
        BEQ.S   RI.LOPB
        CMPI.B  #'+',D0
        BEQ.S   RI.PLUS
        CMPI.B  #'-',D0
        BNE.S   RI.NUM
        SUBQ.B  #1,D7
RI.PLUS:ADDQ.W  #1,D6
RI.LOOP:JSR     INCH
RI.NUM: CMP.B   #RUBOUT,D0      ; Was it a RUBOUT?
        BEQ.S   RI.RUB
        CMP.B   #BACKSP,D0      ; Was it a BACK-SPACE?
        BEQ.S   RI.BAC
        SUBI.B  #'0',D0
        BLT.S   RI.TEST
        CMPI.B  #9,D0
        BGT.S   RI.TEST
        ADDQ.W  #1,D6
        EXT.W   D0
        EXT.L   D0
        MOVE.L  D2,D3
        MULU    #10,D2
        SWAP    D3
        MULU    #10,D3
        ASL.L   #8,D3
        ASL.L   #8,D3
        ADD.L   D3,D2
        ADD.L   D0,D2
        BRA.S   RI.LOOP
RI.TEST:tst.w   d6                      ; set iorslt if no characters in
        bne.s   @1
        movem.l d2/a0,-(sp)             ; save registers
        move.w  #6006,-(sp)
        jsr     %_setiorslt
        movem.l (sp)+,d2/a0
@1      TST.B   D7
        BEQ.S   RI.DONE
        NEG.L   D2
RI.DONE:MOVE.L  D2,64(SP)
        move.b  #fgotchar,fstate(a0)    ; adjust for look ahead
        MOVEM.L (SP)+,D0-D7/A0-A6
        RTS
RI.RUB: TST.W   D6
        BEQ.S   RI.LOPB
        MOVE.L  D6,D4
        JSR     ERASED4
RI.RUB2:CLR.W   D6
        CLR.W   D7
        CLR.L   D2
        BRA.S   RI.LOPB
RI.BAC: TST.W   D6
        BEQ.S   RI.LOPB
        MOVEQ   #1,D4
        JSR     ERASED4
        SUBQ.W  #1,D6
        BEQ.S   RI.RUB2
        MOVE.L  D2,D3
        SWAP    D2              ; D3 = MSHS LSHV   D2 = LSHV MSHV
        EXT.L   D2              ; D3 = MSHV LSHV   D2 = 0000 MSHV
        DIVU    #10,D2          ; D3 = MSHV LSHV   D2 = MSHR MSHQ
        SWAP    D3              ; D3 = LSHV MSHV   D2 = MSHR MSHQ
        SWAP    D2              ; D3 = LSHV MSHV   D2 = MSHQ MSHR
        MOVE.W  D2,D3           ; D3 = LSHV MSHR   D2 = MSHQ MSHR
        SWAP    D3              ; D3 = MSHR LSHV   D2 = MSHQ MSHR
        DIVU    #10,D3          ; D3 = LSHR LSHQ   D2 = MSHQ MSHR
        MOVE.W  D3,D2           ; D3 = LSHR LSHQ   D2 = MSHQ LSHQ
        BRA.S   RI.LOOP


;
; function %_GetDWMapTableAddr : longint;         {return DW mapping table}
;
%_GetDWMapTableAddr
        lea     DWMap,a0    ; put the map table address into result
        move.l  a0,4(sp)
        rts



DWMap   .word   $8C00           ; 128, lisa character value
        .word   $8F00           ; 129
        .word   $432C           ; 130
        .word   $4586           ; 131
        .word   $9000           ; 132
        .word   $8D00           ; 133
        .word   $8E00           ; 134
        .word   $6186           ; 135
        .word   $9A00           ; 136
        .word   $6188           ; 137
        .word   $9200           ; 138
        .word   $6187           ; 139
        .word   $9500           ; 140
        .word   $A500           ; 141
        .word   $9700           ; 142
        .word   $9900           ; 143
        .word   $6588           ; 144
        .word   $6589           ; 145
        .word   $A486           ; 146
        .word   $A100           ; 147
        .word   $A488           ; 148
        .word   $A489           ; 149
        .word   $9600           ; 150
        .word   $6F86           ; 151
        .word   $A200           ; 152
        .word   $6F88           ; 153
        .word   $9300           ; 154
        .word   $6F87           ; 155
        .word   $7586           ; 156
        .word   $9800           ; 157
        .word   $7588           ; 158
        .word   $9400           ; 159
        .word   $BD00           ; 160
        .word   $A000           ; 161
        .word   $8200           ; 162
        .word   $8100           ; 163
        .word   $BB00           ; 164
        .word   $A800           ; 165
        .word   $BC00           ; 166
        .word   $A600           ; 167
        .word   $9C00           ; 168
        .word   $9E00           ; 169
        .word   $BE00           ; 170
        .word   $8600           ; 171
        .word   $8900           ; 172
        .word   $3D2F           ; 173
        .word   $9100           ; 174
        .word   $4F2F           ; 175
        .word   $3F00           ; 176
        .word   $2B5F           ; 177
        .word   $3C5F           ; 178
        .word   $3E5F           ; 179
        .word   $592D           ; 180
        .word   $3F00           ; 181
        .word   $3F00           ; 182
        .word   $3F00           ; 183
        .word   $3F00           ; 184
        .word   $3F00           ; 185
        .word   $3F00           ; 186
        .word   $615F           ; 187
        .word   $6F5F           ; 188
        .word   $3F00           ; 189
        .word   $A300           ; 190
        .word   $6F2F           ; 191
        .word   $8B00           ; 192
        .word   $8A00           ; 193
        .word   $A700           ; 194
        .word   $3F00           ; 195
        .word   $6600           ; 196
        .word   $872D           ; 197
        .word   $3F00           ; 198
        .word   $3C00           ; 199
        .word   $3E00           ; 200
        .word   $3F00           ; 201
        .word   $2000           ; 202
        .word   $4186           ; 203
        .word   $4187           ; 204
        .word   $4F87           ; 205
        .word   $8D00           ; 206
        .word   $9300           ; 207
        .word   $2D00           ; 208
        .word   $2D00           ; 209
        .word   $2200           ; 210
        .word   $2200           ; 211
        .word   $2700           ; 212
        .word   $2700           ; 213
        .word   $2D3A           ; 214
        .word   $7688           ; 215
        .word   $7989           ; 216

        .endc

        .END

ÿ