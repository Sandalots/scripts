
 (* file blockio1.text *)

    (***********************************************************************)
    (*                                                                     *)
    (*  BLK IO routines that are supported in stand alone OS for accessing *)
    (*  UCSD files. These are taken from SYSTEM.B.TEXT from SYSTEM.PASCAL  *)
    (*                                                                     *)
    (*  In this file,  blockio1,  the following routines are defined:      *)
    (*    ScanTitle, DelEntry, InsEntry, FetchDir, WriteDir, VolSearch,    *)
    (*    DirSearch.                                                       *)
    (*                                                                     *)
    (***********************************************************************)

{(*$S PASLIB2 *) wait until tfolder test }

(* ScanTitle ------------------------------------------------------------- *)
FUNCTION ScanTitle (ftitle: FULLID; VAR fvid: VID; VAR ftid: TID;
                    VAR fblocks: INTEGER; VAR fkind: FILEKIND) : BOOLEAN;
  (* Scan the file title and return volume name, file name, number of  *)
  (* blocks specified : -1 for [*], 0 for [0] or unspecified, and file *)
  (* kind, untyped file, text file etc.  Return false if bad title.    *)

  VAR i, rbrack: INTEGER;
    ch: CHAR;
    ok: BOOLEAN;

  BEGIN  (* ScanTitle *)
  (*$IFC DEBUG*)
  IF trace (50) THEN            (* trace level for blkio routines, fence is 100 *)
    WRITELN ('e ScanTitle: ', ftitle);
  (*$ENDC*)
  fvid := '';
  ftid := '';
  fblocks := 0;
  fkind := UNTYPEDFILE;
  ScanTitle := FALSE;
  i := 1;
  WHILE i <= LENGTH(ftitle) DO
    BEGIN  (* WHILE *)
    ch := ftitle[i];
    IF ch <= ' ' THEN
      DELETE(ftitle,i,1)            (* delete control char *)
    ELSE
      BEGIN
      IF (ch >= 'a') AND (ch <= 'z') THEN       (* convert to upper case *)
        ftitle[i] := CHR(ORD(ch)-ORD('a')+ORD('A'));
      i := i+1
      END
    END;   (* WHILE *)
  IF LENGTH(ftitle) > 0 THEN
    BEGIN  (* LENGTH (ftitle) > 0 *)
    IF ftitle[1] = '*' THEN
      BEGIN  (* System Volume *)
      fvid := SYVID;
      DELETE(ftitle,1,1);
      END;   (* System Volume *)
    i := POS(':',ftitle);
    IF i <= 1 THEN
      BEGIN  (* i <= 1 *)
      IF LENGTH(fvid) = 0 THEN
        fvid := DKVID;
      IF i = 1 THEN
        DELETE(ftitle,1,1)
      END    (* i <= 1 *)
    ELSE
      IF i-1 <= VIDLENG THEN
        BEGIN
        fvid := COPY(ftitle,1,i-1);
        DELETE(ftitle,1,i)
        END;
    IF LENGTH(fvid) > 0 THEN
      BEGIN  (* LENGTH (fvid) > 0 *)
      i := POS('[', ftitle);
      IF i > 0 THEN
        i := i-1
      ELSE
        i := LENGTH(ftitle);
      IF i <= TIDLENG THEN
        BEGIN  (* i <= TIDLENG *)
        IF i > 0 THEN
          BEGIN
          ftid := COPY(ftitle,1,i);
          DELETE(ftitle,1,i);
          END;
        IF LENGTH(ftitle) = 0 THEN
          ok := TRUE
        ELSE
          BEGIN  (* LENGTH > 0 *)
          ok := FALSE;
          rbrack := POS(']',ftitle);
          IF rbrack = 2 THEN
            ok := TRUE
          ELSE
            IF rbrack > 2 THEN
              BEGIN  (* rbrack > 2 *)
              ok := TRUE;
              i := 2;
              REPEAT ch := ftitle[i];
                IF (ch >= '0') and (ch <= '9')  {ch IN DIGITS} THEN
                  fblocks := fblocks*10+(ORD(ch)-ORD('0'))
                ELSE
                  ok := FALSE;
                i := i+1
              UNTIL (i = rbrack) OR NOT ok;
              IF (i = 3) AND (rbrack = 3) THEN
                IF ftitle[i-1] = '*' THEN
                  BEGIN
                  fblocks := -1;
                  ok := TRUE;
                  END
              END;   (* rbrack > 2 *)
          END;   (* LENGTH > 0 *)
        ScanTitle := ok;
        IF ok AND (LENGTH(ftid) > 5) THEN
          BEGIN  (* Check Suffix *)
          ftitle := COPY(ftid,LENGTH(ftid)-4,5);
          IF      ftitle = '.TEXT' THEN fkind := TEXTFILE
          ELSE IF ftitle = '.CODE' THEN fkind := CODEFILE
          ELSE IF ftitle = '.BACK' THEN fkind := TEXTFILE
          ELSE IF ftitle = '.INFO' THEN fkind := INFOFILE
          ELSE IF ftitle = '.GRAF' THEN fkind := GRAFFILE
          ELSE IF ftitle = '.FOTO' THEN fkind := FOTOFILE
          END;   (* Check Suffix *)
        END;   (* i <= TIDLENG *)
      END;   (* LENGTH (fvid) > 0 *)
    END;   (* LENGTH (ftitle) > 0 *)

  (*$IFC DEBUG*)
  IF trace (50) THEN
   WRITELN ('l ScanTitle: ', ok);
  (*$ENDC*)

  END;   (* ScanTitle *)


(* VOLUME AND DIRECTORY HANDLERS *)

(* DelEntry ---------------------------------------------------------------- *)
PROCEDURE DelEntry (finx: DIRRANGE; fdirp: DIRP);
   (* delete a directory entry, dec DNUMFILES *)
  VAR i: DIRRANGE;

  BEGIN  (* DelEntry *)
  (*$IFC DEBUG*)
  IF trace (50) THEN
    WRITELN ('e DELENTRY: finx = ', finx, ' fdirp =', ORD (fdirp));
  (*$ENDC*)

  WITH fdirp^[0] DO
    BEGIN  (* WITH *)
    FOR i := finx TO DNUMFILES-1 DO
      fdirp^[i] := fdirp^[i+1];
    fdirp^[DNUMFILES].DTID := '';
    DNUMFILES := DNUMFILES-1
    END;   (* WITH *)
  END;   (* DelEntry *)


(* InsEntry ----------------------------------------------------------------- *)
PROCEDURE InsEntry (VAR fentry: DIRENTRY; finx: DIRRANGE; fdirp: DIRP);

  (* insert a directory entry, inc DNUMFILES *)

  VAR i: DIRRANGE;
  BEGIN  (* InsEntry *)
 (*$IFC DEBUG*)
   IF trace (50) THEN
     WRITELN ('e INSENTRY: finx = ', finx, ' fdirp =', ORD (fdirp));
 (*$ENDC*)
  WITH fdirp^[0] DO
    BEGIN
    FOR i := DNUMFILES DOWNTO finx DO
      fdirp^[i+1] := fdirp^[i];
    fdirp^[finx] := fentry;
    DNUMFILES := DNUMFILES+1
    END;
  END;   (* InsEntry *)




(* FetchDir ---------------------------------------------------------------- *)
FUNCTION FetchDir (funit: UNITNUM; fdirp: DIRP; VAR iorslt: INTEGER)
                                                                : BOOLEAN;
  (* fetch and validate directory from the disk unit into FDIR, return true *)
  (* IF good directory, ELSE return false, and set ioresult to non-zero     *)
  (* directory space must be allocated by caller                            *)
  (* called by volsearch and fopen only                                     *)

  VAR linx: dirrang1; ok: BOOLEAN;
      volname, password, devname : e_name;
      errnum : INTEGER;
      firstdigit, seconddigit : STRING [1];
      actual : longint;

  BEGIN  (* FetchDir *)
  (*$IFC DEBUG*)
  IF trace (50) THEN
    WRITELN ('e FETCHDIR: funit =', funit, ' fdirp =', ORD (fdirp));
  (*$ENDC*)
  FETCHDIR := FALSE;
  WITH UNITABLE[funit] DO
    BEGIN  (* WITH UniTable[funit] *)
    firstdigit := '0';
    seconddigit := '0';
    (* convert unit number to ascii *)
    firstdigit[1] :=  CHR (funit div 10 + ORD ('0'));
    seconddigit[1] := CHR (funit mod 10 + ORD ('0'));
    IF firstdigit = '0' THEN
      devname := CONCAT ('DEV', seconddigit)
    ELSE
      devname := CONCAT ('DEV', firstdigit, seconddigit);

    mount (errnum, volname, password, devname);
                       (* see IF the volume is allowed to do block io *)
    CASE errnum OF

    0    :
      BEGIN                        (* a fs volume, blockio not allowed *)
      iorslt := ORD(ibadunit);
      unmount (errnum, volname);   (* so other program can do mount *)
      END;

    1052, 1051 :
      iorslt := ORD (ibadunit);    (* a fs volume and already mounted, or bad name *)

    866, 663, 803, 1054 :
      iorslt := ORD(inoerror);     (* not a fs volume, can do reset, rewrite*)

    OTHERWISE
      BEGIN
      iorslt := ORD(ideverror);    (* io error *)
      END;
    END;  (* CASE *)

    ok := iorslt = ORD (inoerror);
    IF ok THEN
      BEGIN  (* ok *)
      UnitIO (iorslt, funit,ORD (fdirp),dirnblocks, DIRBLK, actual,
              without_header, readop);
                                                    (*READ IN AND VALIDATE DIR*)
      ok := iorslt = ORD (INOERROR);
      IF ok THEN
        WITH fdirp^[0] DO
          BEGIN  (* WITH fdirp^[0] *)
          ok := FALSE; (*CHECK OUT DIR*)
          IF DLASTBLK = 6 THEN      (* female dir *)
            BEGIN  (* female dir *)
            ok := TRUE; (*SO FAR SO GOOD*)
            IF DVID <> UVID THEN
              BEGIN (*NEW VOLUME IN UNIT...CAREFUL*)
              (* validate every directory entry, IF
                 not valid, delete the entry *)
              linx := 1;
              WHILE linx <= DNUMFILES DO
                WITH fdirp^[linx] DO
                  IF (LENGTH(DTID) <= 0) OR
                     (LENGTH(DTID) > TIDLENG) OR
                     (DLASTBLK < DFIRSTBLK) OR
                     (DLASTBYTE > FBLKSIZE) OR
                     (DLASTBYTE <= 0) OR
                     (DACCESS.YEAR >= 100) THEN
                    BEGIN
                    ok := FALSE;
                    DelEntry(linx,fdirp);
                    END
                  ELSE
                    linx := linx+1;
              LOGGED := TRUE;     (* set logged flag in UNITABLE entry*)
              IF NOT ok THEN
                BEGIN (*MUST HAVE BEEN CHANGED...WRITEIT*)
                UnitIO (iorslt, funit,ORD (fdirp),dirnblocks,
                        DIRBLK, actual, without_header,  writeop);
                ok := iorslt = ORD (INOERROR);
                END;  (*MUST HAVE BEEN CHANGED...WRITEIT*)
              END  (*NEW VOLUME IN UNIT...CAREFUL*)
            END;   (* female dir *)
          IF ok THEN
            BEGIN
            UVID := DVID;
            UEOVBLK := DEOVBLK;   (* update UNITABLE entry
                                    IF valid directory *)
            END
          END; (* WITH fdirp^[0] *)
      END;  (* ok *)
    FETCHDIR := ok;
    IF NOT ok THEN
      BEGIN
      UVID := '';
      UEOVBLK := MMAXINT;
      END
    END;   (* WITH UniTable[funit] *)
  (*$IFC DEBUG*)
    IF trace (50) THEN
      WRITELN ('l FETCHDIR: iorslt =', iorslt, ' ok =', ok);
  (*$ENDC*)
  END;   (* FetchDir *)


(* WriteDir (01-Feb-82) ------------------------------------------------------- *)
PROCEDURE WriteDir (funit: UNITNUM; fdirp: DIRP);
                                        (* write directory out to disk *)
  VAR ok: BOOLEAN;
      actual : longint;

  BEGIN  (* WriteDir *)
  (*$IFC DEBUG*)
  IF trace (50) THEN
    BEGIN
    WRITELN ('e WriteDir: funit =', funit, ' fdirp =', ORD (fdirp));
    END;
  (*$ENDC*)
  WITH UNITABLE[funit],fdirp^[0] DO
    BEGIN  (* WITH *)
    ok := (UVID = DVID) AND ((DFKIND = UNTYPEDFILE) OR (DFKIND = SECUREDIR));
    IF ok THEN
      BEGIN  (* ok *)
      (*WE GUESS ALL IS SAFE...WRITE IT*)
      UnitIO (iorslt, funit, ORD (fdirp),dirnblocks, DIRBLK,
                actual, without_header, writeop);
      ok := iorslt = ORD(INOERROR);
      END;   (* ok *)
    IF NOT ok THEN
      BEGIN  (* NOT ok *)
      UVID := '';
      UEOVBLK := MMAXINT
      END;   (* NOT ok *)
    (*$IFC DEBUG*)
    IF trace (50) THEN
      WRITELN ('l WriteDir: iorslt =', iorslt, ' ok =', ok);
    (*$ENDC*)
    END;   (* WITH *)
END;   (* WriteDir *)


(* VolSearch (01-Feb-82) ---------------------------------------------------- *)
FUNCTION VolSearch (VAR fvid: VID; lookhard: BOOLEAN; VAR fdirp: DIRP)
                                                        : UNITNUM;
  (* search volume name and return the corresponding unit #,                   *)
  (* return 0 for unit number IF unable to find the specified volume name.     *)
  (* lookhard - needs to fetch directory from each disk unit or not            *)
  (* fdirp is the pointer to directory, its space must be allocated by caller. *)
  (* Note: it will nil fdirp IF the unit is not a block device                 *)
  (* called by fopen, fclose only                                              *)

  VAR lunit: integer;
    ok, physunit: BOOLEAN;
    hnow : INTEGER;
    errnum : INTEGER;

  BEGIN  (* VolSearch *)
  (*$IFC DEBUG*)
  IF trace (50) THEN
    WRITELN ('e VolSearch: fvid =', fvid, ' fdirp =', ORD (fdirp));
  (*$ENDC*)
  VolSearch := 0;
  ok := FALSE; physunit := FALSE;
  IF LENGTH(fvid) > 0 THEN
    BEGIN  (* LENGTH(fvid) > 0 *)
    IF (fvid[1] = '#') AND (LENGTH(fvid) > 1) THEN
      BEGIN  (* '#' specified *)
      ok := TRUE;
      lunit := 0;
      hnow := 2;
      REPEAT
        IF (fvid[hnow] >= '0') AND (fvid[hnow] <= '9') THEN
          {fvid[hnow] IN DIGITS}
          (* convert ascii to no.*)
          lunit := lunit*10+ORD(fvid[hnow])-ORD('0')
        ELSE
          ok := FALSE;
        hnow := hnow+1
      UNTIL (hnow > LENGTH(fvid)) OR NOT ok;

      physunit := ok AND (lunit > 0) AND (lunit <= MAXUNIT);
                                            (* true IF valid unit # *)
      lookhard := lookhard AND NOT physunit;  (* false IF valid unit # *)
      END;   (* '#' specified *)

    IF NOT physunit THEN                      (* IF no physical unit #, search
                                              the UNITABLE for the entry *)
      BEGIN  (* no phys unit# *)
      ok := FALSE;
      lunit := MAXUNIT;
      REPEAT
        ok := fvid = UNITABLE[lunit].UVID;
        IF NOT ok THEN
          lunit := lunit-1
      UNTIL ok OR (lunit = 0)
      END;   (* no phys unit# *)
    END;   (* LENGTH(fvid) > 0 *)

  IF ok THEN                            (* found volume name in UNITABLE, but
                                           make sure the volume name match WITH
                                           the name on directory *)
    IF UNITABLE[lunit].UISBLKD THEN
      BEGIN  (* blocked device *)
      ok := physunit;
      IF FETCHDIR(lunit, fdirp, iorslt) THEN  (* read and validate directory*)
        IF NOT physunit THEN
          ok := fvid = fdirp^[0].DVID
        ELSE
      ELSE
        begin
          if fdirp^[0].dlastblk <> 6 then ok := false (* not female, short directory *)
          else
            ok := iorslt = ORD(INOERROR);  {Rely on IORESULT from FETCHDIR}
        end;
      END;   (* blocked device *)

  IF NOT ok AND lookhard THEN   (* volume name does not match WITH directory or
                                   a bad directory on volume *)
    BEGIN  (*CHECK EACH directory from each DISK UNIT*)
    lunit := MAXUNIT;

    REPEAT
      WITH UNITABLE[lunit] DO
        BEGIN  (* WITH *)
        IF UISBLKD THEN
          BEGIN  (* Block Device *)
          WHILE lockdir DO
            yield_cpu (errnum, true);
                            (* some one ELSE is using the dir, wait *)
          lockdir := true;  (* now lock it, END of fopen, fclose, cantstretch
                               will unlock it, IF ok is true. *)
          IF FETCHDIR(lunit, fdirp, iorslt) THEN
            ok := fvid = UVID;
          END;   (* Block Device *)
        IF NOT ok THEN
          BEGIN
          lockdir := false;
          lunit := lunit-1;
          END;
        END;   (* WITH *)
    UNTIL ok OR (lunit = 0);

    END;   (*CHECK EACH directory from each DISK UNIT*)

  IF ok THEN
    WITH UNITABLE[lunit] DO
      BEGIN  (* WITH *)
      VolSearch := lunit;
      IF LENGTH(UVID) > 0 THEN
        fvid := UVID;
      IF NOT UISBLKD THEN
        fdirp := NIL;        (* IF non-blocked device, nil fdirp*)
      END;  (* WITH *)
  (*$IFC DEBUG*)
    IF trace (50) THEN
      WRITELN ('l VolSearch: iorslt =', iorslt, ' ok =', ok);
  (*$ENDC*)
  END;   (* VolSearch *)


(* DirSearch --------------------------------------------------------------- *)
FUNCTION DirSearch (ftid: TID; findperm: BOOLEAN; fdirp: DIRP) : DIRRANGE;

  (* search directory for a file and return entry number, ELSE return 0  *)
  (* if not found *)

  VAR i: dirrang1;
    found: BOOLEAN;
  BEGIN  (* DirSearch *)
  (*$IFC DEBUG*)
    IF trace (50) THEN
      WRITELN ('e DirSearch: ftid =', ftid, ' fdirp =', ORD (fdirp));
  (*$ENDC*)
  DirSearch := 0;
  found := FALSE;
  i := 1;
  WHILE (i <= fdirp^[0].DNUMFILES) AND NOT found DO
    BEGIN  (* WHILE *)
    WITH fdirp^[i] DO
      IF DTID = ftid THEN
        IF findperm = (DACCESS.YEAR <> 100) THEN
                                      (* RESET a perm file or REWRITE a temp
                                         file gets entry number - REWRITE a
                                         perm file gets 0 entry number -> to
                                         create another temp file entry *)
          BEGIN
          DirSearch := i;
          found := TRUE
          END;
    i := i+1
    END;   (* WHILE *)
  (*$IFC DEBUG*)
  IF trace (50) THEN
    WRITELN ('l DirSearch: found = ', found, ' i =', i);
  (*$ENDC*)
  END;   (* DirSearch *)


ÿ