; File: PASSET.TEXT
; Date: 09-Oct-81

;        .PROC   %%%SET

;        .DEF    %_UNION,%_DIFF,%_RDIFF,%_INTER
;        .DEF    %_SING,%_RANGE
;        .DEF    %_ADJ,%_SETGE,%_SETLE,%_SETEQ,%_SETNE

;
; The format of a set on the stack is:
;
;  +---------+
;  | 15 - 0  |
;  +---------+
;  | 31 - 16 |
;  +---------+
;  |   ...   |
;  +---------+
;  | Last Wd |
;  +---------+
;  | # Bytes |
;  +---------+
;

;
; %_INTER - Set intersection
;
; Parameters: ST.S - First set
;             ST.S - Second set
;
; Returns:    ST.S - Result set
;
; This procedure saves all registers
;

        .PROC   %_INTER

        CLR.L   -(SP)           ; Need room if both sets []
        MOVEM.L D0-D2/A0-A1,-(SP)
        LEA     28(SP),A0       ; Address of second set
        tst.b   (a0)            ; make resident in case it is in code seg
        MOVE.W  (A0)+,D0        ; Size of second set
        ADDA.W  D0,A0           ; Address of top of set 2 +
        MOVE.L  A0,A1           ; Address of first set
        tst.b   (a1)            ; make resident in case it is in code seg
        MOVE.W  (A1)+,D1        ; Size of first set
        ADDA.W  D1,A1           ; Address of top of set 1 +
        CMP.W   D0,D1
        BGE.S   I.GOON
        MOVE.W  D1,D0
I.GOON: MOVE.W  D0,D1           ; D0 and D1 = smallest size set
        LSR.W   #1,D0           ; D0 = number words
        BRA.S   I.TEST
I.LOOP: MOVE.W  -(A0),D2
        AND.W   D2,-(A1)
I.TEST: dbra    D0,I.LOOP       ; dec d0 and branch if d0 <> -1
        MOVE.W  D1,-(A1)        ; Store set size
        MOVE.L  24(SP),A0       ; Set up return address
        MOVE.L  A0,-(A1)        ; Can't do MOVE.L 24(SP),-(A0)
        MOVE.L  A1,20(SP)       ; Set up new SP value
        MOVEM.L (SP)+,D0-D2/A0-A1
        MOVE.L  (SP)+,SP        ; Pop new SP value
        RTS

;
; %_SING - Singleton set
;
; Parameters: ST.W - Singleton value
;
; Result:     ST.S - Resulting set
;
; This routine saves all registers
;

        .PROC   %_SING

        MOVEM.L D0-D4/A0-A2,-(SP)
        LEA     36(SP),A0       ; address of singleton value
        MOVE.W  (A0),D0         ; D0 = singleton value
        CLR.W   (A0)            ; Default 1st word of result to zero
        MOVE.W  D0,D1
        LSR.W   #4,D1           ; D1 = # Words of leading zeroes
        MOVE.W  D1,D2
        ADD.W   #1,D2           ; D2 = # Words to insert into stack
        MOVE.W  D2,D3
        LSL.W   #1,D3           ; D3 = # Bytes to insert into stack
        MOVE.L  SP,A1           ; Current top of stack
        SUB.W   D3,SP
        MOVE.L  SP,A2           ; New top of stack
        MOVE.W  #18,D4          ; # Words to copy in D4
S.LOOP1:MOVE.W  (A1)+,(A2)+     ; Copy each remaining word
        SUB.W   #1,D4
        BNE.S   S.LOOP1
ZEROSTK:MOVE.L  A2,A0
S.LOOP2:CLR.W   (A2)+           ; Default each word of result to zero
        SUB.W   #1,D2
        BNE.S   S.LOOP2
        MOVE.W  D3,(A0)+        ; Copy #bytes in last word of set
        CLR.W   D2
        ANDI.W  #$F,D0          ; Mask off 4 bits
        BSET    D0,D2           ; Set the bit
        MOVE.W  D2,(A0)         ; Copy word with bit set into stack
        MOVEM.L (SP)+,D0-D4/A0-A2
        RTS

;
; %_UNION - Set union
;
; Parameters: ST.S - First set
;             ST.S - Second set
;
; Returns:    ST.S - Result set
;
; This procedure saves all registers
;

        .PROC   %_UNION

        CLR.L   -(SP)
        MOVEM.L D0-D3/A0-A1,-(SP)
        LEA     32(SP),A0       ; Address of second set
        tst.b   (a0)            ; make resident in case it is in code seg
        MOVE.W  (A0)+,D0        ; Size of second set
        ADDA.W  D0,A0           ; Address of top of set 2 +
        MOVE.L  A0,A1           ; Address of first set
        MOVE.W  (A1)+,D1        ; Size of first set
        ADDA.W  D1,A1           ; Address of top of set 1 +
        MOVE.W  D0,D3
        CMP.W   D0,D1
        BGE.S   U.GOON
        MOVE.W  D1,D3           ; D3 = smallest set size
U.GOON: LSR.W   #1,D3           ; D3 = # Words
        BRA.S   U.TEST
U.LOOP: MOVE.W  -(A0),D2
        OR.W    D2,-(A1)
U.TEST: dbra    D3,U.LOOP       ; dec d3 and branch if d3 <> -1
        SUB.W   D1,D0           ; Compare set sizes
        BEQ.S   U.DONE          ; Finish up if same size
        BLT.S   U.A.BIG
        ADD.W   D0,D1           ; D1 = Biggest Size
        LSR.W   #1,D0           ; D0 = # Words bigger
UB.LOOP:MOVE.W  -(A0),-(A1)
        SUBQ.W  #1,D0
        BGT.S   UB.LOOP
        BRA.S   U.DONE
U.A.BIG:ADDA.W  D0,A1           ; First set is bigger
U.DONE: MOVE.W  D1,-(A1)        ; Store set size
        MOVE.L  28(SP),A0       ; Set up return address
        MOVE.L  A0,-(A1)
        MOVE.L  A1,24(SP)       ; Set up new SP value
        MOVEM.L (SP)+,D0-D3/A0-A1
        MOVE.L  (SP)+,SP        ; Pop new SP value
        RTS

;
; %_DIFF - Set difference
;
; Parameters: ST.S - First set
;             ST.S - Second set
;
; Returns:    ST.S - Result set
;
; This procedure saves all registers
;

        .PROC   %_DIFF

        CLR.L   -(SP)
        MOVEM.L D0-D3/A0-A1,-(SP)
        LEA     32(SP),A0       ; Address of second set
        tst.b   (a0)            ; make resident in case it is in code seg
        MOVE.W  (A0)+,D0        ; Size of second set
        ADDA.W  D0,A0           ; Address of top of set 2 +
        MOVE.L  A0,A1           ; Address of first set
        MOVE.W  (A1)+,D1        ; Size of first set
        ADDA.W  D1,A1           ; Address of top of set 1 +
        MOVE.W  D0,D3
        CMP.W   D0,D1
        BGE.S   D.GOON
        MOVE.W  D1,D3           ; D3 = smallest set size
D.GOON: LSR.W   #1,D3           ; D3 = # Words
        BRA.S   D.TEST
D.LOOP: MOVE.W  -(A0),D2
        NOT.W   D2
        AND.W   D2,-(A1)
D.TEST: dbra    D3,D.LOOP       ; dec d3 and branch if d3 <> -1
        SUB.W   D1,D0           ; Compare set sizes
        BGT.S   D.DONE          ; Finished if A <= B in size
        ADDA.W  D0,A1           ; First set is bigger
D.DONE: MOVE.W  D1,-(A1)        ; Store set size
        MOVE.L  28(SP),A0       ; Set up return address
        MOVE.L  A0,-(A1)
        MOVE.L  A1,24(SP)       ; Set up new SP value
        MOVEM.L (SP)+,D0-D3/A0-A1
        MOVE.L  (SP)+,SP        ; Pop new SP value
        RTS

;
; %_RDIFF - Reverse set difference
;
; Parameters: ST.S - First set
;             ST.S - Second set
;
; Returns:    ST.S - Result set
;
; This procedure saves all registers
;

        .PROC   %_RDIFF

        CLR.L   -(SP)
        MOVEM.L D0-D3/A0-A1,-(SP)
        LEA     32(SP),A0       ; Address of second set
        tst.b   (a0)            ; make resident in case it is in code seg
        MOVE.W  (A0)+,D0        ; Size of second set
        ADDA.W  D0,A0           ; Address of top of set 2 +
        MOVE.L  A0,A1           ; Address of first set
        MOVE.W  (A1)+,D1        ; Size of first set
        ADDA.W  D1,A1           ; Address of top of set 1 +
        MOVE.W  D0,D3
        CMP.W   D0,D1
        BGE.S   V.GOON
        MOVE.W  D1,D3           ; D3 = smallest set size
V.GOON: LSR.W   #1,D3           ; D3 = # Words
        BRA.S   V.TEST
V.LOOP: NOT.W   -(A1)
        MOVE.W  -(A0),D2
        AND.W   D2,(A1)
V.TEST: dbra    D3,V.LOOP       ; dec d3 and branch if d3 <> -1
        SUB.W   D1,D0           ; Compare set sizes
        BLE.S   V.DONE          ; Finished if A <= B in size
        ADD.W   D0,D1           ; D1 = Biggest Size
        LSR.W   #1,D0           ; D0 = # Words bigger
VB.LOOP:MOVE.W  -(A0),-(A1)
        SUBQ.W  #1,D0
        BGT.S   VB.LOOP
V.DONE: MOVE.W  D1,-(A1)        ; Store set size
        MOVE.L  28(SP),A0       ; Set up return address
        MOVE.L  A0,-(A1)
        MOVE.L  A1,24(SP)       ; Set up new SP value
        MOVEM.L (SP)+,D0-D3/A0-A1
        MOVE.L  (SP)+,SP        ; Pop new SP value
        RTS

;
; %_RANGE - Set range
;
; Parameters: ST.W - Minimum value
;             ST.W - Maximum value
;
; Result:     ST.S - Resulting set
;
; This procedure saves all registers.
;

        .PROC   %_RANGE

        MOVEM.L D0-D4/A0-A2,-(SP)
        MOVE.W  36(SP),D1       ; D1 = max value
        MOVE.W  38(SP),D0       ; D0 = min value
        CLR.L   36(SP)          ; Zero the parameters
        LEA     40(SP),A0       ; A0 = address of set + 1
        TST.W   D0              ; if Min < 0 then Min := 0
        BPL.S   MINPOS
        CLR.W   D0
MINPOS: CMP.W   D0,D1           ; Compare Min to Max
        BLT.S   NULLSET
        MOVE.W  D1,D2
        LSR.W   #4,D2           ; D2 = Size in words - 1 of final set
        MOVE.W  D2,D3
        LSL.W   #1,D3           ; D3 = Size in bytes - 2 of final set
        BEQ.S   NOEXPAN
        MOVE.L  SP,A1           ; Current top of stack
        SUB.W   D3,SP
        MOVE.L  SP,A2           ; New top of stack
        MOVE.W  #18,D4          ; # Words to copy in D4
R.LOOP1:MOVE.W  (A1)+,(A2)+     ; Copy each remaining word
        SUB.W   #1,D4
        BNE.S   R.LOOP1
R.LOOP3:CLR.W   (A2)+           ; Default each word of result to zero
        SUB.W   #1,D2
        BNE.S   R.LOOP3
NOEXPAN:ADD.W   #2,D3           ; D3 = Size in bytes
        MOVE.W  D3,36(SP)       ; Copy #bytes in last word of set
R.LOOP2:MOVE.W  D0,D2
        MOVE.W  D2,D3
        LSR.W   #4,D2
        LSL.W   #1,D2
        NEG.W   D2
        ANDI.W  #$F,D3
        CLR.W   D4
        BSET    D3,D4           ; Build the set one bit at a time
        OR.W    D4,-2(A0,D2)
        ADDQ.W  #1,D0
        CMP.W   D0,D1
        BGE.S   R.LOOP2
        MOVEM.L (SP)+,D0-D4/A0-A2
        RTS
NULLSET:MOVE.L  32(SP),A0       ; Move return address up one word
        MOVE.L  A0,34(SP)
        MOVEM.L (SP)+,D0-D4/A0-A2
        TST.W   (SP)+           ; Delete extra word
        RTS

;
; %_ADJ - Set adjust
;
; Parameters: ST.S - Set
;             ST.W - Desired size in bytes
;
; Returns:    ST.S' - Adjusted set without size word
;
; This routine preserves all registers
;

        .PROC   %_ADJ

        SUBA.W  #256,SP         ; Allow for worst-case set expansion
        MOVEM.L D0-D1/A0-A1,-(SP)
        MOVE.W  276(SP),D0      ; Desired size
        MOVE.W  278(SP),D1      ; Current size
        LEA     280(SP),A0      ; Points to end of set
        MOVE.L  272(SP),A1      ; Return address
        SUB.W   D0,D1
        BGE.S   A.OK
A.LOOP: CLR.W   -(A0)
        ADDQ.W  #2,D1
        BMI.S   A.LOOP
        BRA.S   A.DONE
A.OK:   ADDA.W  D1,A0
A.DONE: MOVE.L  A1,-(A0)        ; Set up return address
        MOVE.L  A0,16(SP)       ; Set up new SP
        MOVEM.L (SP)+,D0-D1/A0-A1
        MOVE.L  (SP)+,SP
        RTS

;
; %_SETNE - Set inequality test
;
; Parameters: ST.S - First set
;             ST.S - Second set
;
; Returns:    ST.W - Result
;
; This procedure saves all registers
;

        .PROC   %_SETNE
        .DEF    %_SETEQ

;%_SETNE:
        CLR.L   -(SP)
        MOVEM.L D0-D3/A0-A2,-(SP)
        MOVEQ   #1,D3           ; Result if <>
        BRA.S   EQ.NE

;
; %_SETEQ - Set equality test
;
; Parameters: ST.S - First set
;             ST.S - Second set
;
; Returns:    ST.B - Result
;
; This procedure saves all registers
;

%_SETEQ:
        CLR.L   -(SP)
        MOVEM.L D0-D3/A0-A2,-(SP)
        CLR.W   D3              ; Result if <>
EQ.NE:  LEA     36(SP),A0       ; Address of second set
        tst.b   (a0)            ; make resident in case it is in code seg
        MOVE.W  (A0)+,D0        ; Size of second set
        ADDA.W  D0,A0           ; Address of top of set 2 +
        MOVE.L  A0,A1           ; Address of first set
        MOVE.W  (A1)+,D1        ; Size of first set
        ADDA.W  D1,A1           ; Address of top of set 1 +
        MOVE.L  A1,A2           ; Save address for result
        MOVE.W  D0,D2
        CMP.W   D0,D1
        BGE.S   EQ.GOON
        MOVE.W  D1,D2           ; D2 = smallest set size
EQ.GOON:SUBA.W  D2,A0
        SUBA.W  D2,A1
        LSR.W   #1,D2           ; D2 = # Words
        BRA.S   EQ.TEST
EQ.LOOP:CMPM.W  (A0)+,(A1)+
        BNE.S   EQ.FALS
EQ.TEST:dbra    D2,EQ.LOOP      ; dec d2 and branch if d2 <> -1
        SUB.W   D0,D1           ; Compare set sizes
        BEQ.S   EQ.TRUE         ; Finish up if same size
        BMI.S   E.TOP.S
        SUBA.W  D0,A1           ; Set A1 to address of first
                                ; word in extra stuff + 2
        LSR.W   #1,D1           ; D1 = # Words bigger
EQLOOP2:TST.W   -(A1)
        BNE.S   EQ.FALS
        SUBQ.W  #1,D1
        BGT.S   EQLOOP2
        BRA.S   EQ.TRUE
E.TOP.S:SUBA.W  D0,A0           ; Top set is smaller
        SUBA.W  D1,A0
        ASR.W   #1,D1
EQLOOP3:TST.W   -(A0)
        BNE.S   EQ.FALS
        ADDQ.W  #1,D1
        BMI.S   EQLOOP3
EQ.TRUE:EORI.W  #1,D3
EQ.FALS:CLR.B   -(A2)           ; Push result as if
        MOVE.B  D3,-(A2)        ;   MOVE.B -(SP)
        MOVE.L  32(SP),A0       ; Set up return address
        MOVE.L  A0,-(A2)
        MOVE.L  A2,28(SP)       ; Set up new SP value
        MOVEM.L (SP)+,D0-D3/A0-A2
        MOVE.L  (SP)+,SP        ; Pop new SP value
        RTS

;
; %_SETGE - Set inclusion test
;
; Parameters: ST.S - First set
;             ST.S - Second set
;
; Returns:    ST.B - Result
;
; This procedure saves all registers
;

        .PROC   %_SETGE
        .DEF    %_SETLE

;%_SETGE:
        CLR.L   -(SP)
        MOVEM.L D0-D3/A0-A2,-(SP)
        LEA     36(SP),A1       ; Address of second set
        tst.b   (a1)            ; make resident in case it is in code seg
        MOVE.W  (A1)+,D1        ; Size of second set
        ADDA.W  D1,A1           ; Address of top of set 2 +
        MOVE.L  A1,A0           ; Address of first set
        MOVE.W  (A0)+,D0        ; Size of first set
        ADDA.W  D0,A0           ; Address of top of set 1 +
        MOVE.L  A0,A2           ; Save address for result
        BRA.S   LE.GE           ; Compute (A1) <= (A0)

;
; %_SETLE - Set inclusion test
;
; Parameters: ST.S - First set
;             ST.S - Second set
;
; Returns:    ST.B - Result
;
; This procedure saves all registers
;

%_SETLE:
        CLR.L   -(SP)
        MOVEM.L D0-D3/A0-A2,-(SP)
        LEA     36(SP),A0       ; Address of second set
        tst.b   (a0)            ; make resident in case it is in code seg
        MOVE.W  (A0)+,D0        ; Size of second set
        ADDA.W  D0,A0           ; Address of top of set 2 +
        MOVE.L  A0,A1           ; Address of first set
        MOVE.W  (A1)+,D1        ; Size of first set
        ADDA.W  D1,A1           ; Address of top of set 1 +
        MOVE.L  A1,A2           ; Save address for result
LE.GE:  MOVE.W  D0,D2
        CMP.W   D0,D1
        BGE.S   LE.GOON
        MOVE.W  D1,D2           ; D2 = smallest set size
LE.GOON:LSR.W   #1,D2           ; D2 = # Words
        BRA.S   LE.TEST
LE.LOOP:MOVE.W  -(A0),D3
        NOT.W   D3
        AND.W   -(A1),D3
        BNE.S   LE.FALS
LE.TEST:dbra    D2,LE.LOOP      ; dec d2 and branch if d2 <> -1
        SUB.W   D0,D1           ; Compare set sizes
        BEQ.S   LE.TRUE         ; Finish up if same size
        BMI.S   LE.TRUE         ; True if (A1) shorter
        LSR.W   #1,D1           ; D1 = # Words bigger
LELOOP2:TST.W   -(A1)
        BNE.S   LE.FALS
        dbra    D1,LELOOP2      ; dec d1 and branch if d1 <> -1
        BRA.S   LE.TRUE
LE.TRUE:MOVEQ   #1,D3
        BRA.S   LE.DONE
LE.FALS:CLR.W   D3
LE.DONE:CLR.B   -(A2)           ; Push the result as if
        MOVE.B  D3,-(A2)        ;   MOVE.B -(SP)
        MOVE.L  32(SP),A0       ; Set up return address
        MOVE.L  A0,-(A2)
        MOVE.L  A2,28(SP)       ; Set up new SP value
        MOVEM.L (SP)+,D0-D3/A0-A2
        MOVE.L  (SP)+,SP        ; Pop new SP value
        RTS

        .END

ÿ