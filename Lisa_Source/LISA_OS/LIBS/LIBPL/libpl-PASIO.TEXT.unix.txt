; File: PASIO.TEXT
; Date: 09-Oct-81, Modified : 8-Dec-81

;        .PROC   %%%IO
        .PROC   %_REWRT

        .include        libpl/paslibdefs.text

        .def    %_UREAD,%_UWRIT,%_IORES,%_UCLR,%_UBUSY

        .DEF    %_RESET,%_CLOSE,%_EOF,%_EOLN
        .DEF    %_BLKRD,%_BLKWR
        .DEF    %_GET,%_PUT,%_UPARR,%_SEEK
;        .DEF    %_REWRT

        .if     libproducing=appaslib
        .ref    %_fioresult, %_finit, %_fopen, %_fclose, %_fblockio, %_fget, %_fput
        .ref    %_funitiocall, %_funitbusy, %_fseek
        .endc

;
; %_REWRT - Rewrite a file
;
; Parameters: ST.L - File Address
;             ST.L - Address of Name String
;             ST.W - Kind: -2=text, -1=file, >0=#words/item, should be #bytes
;
;%_REWRT:
        CLR.W   D3              ; 0 = Rewrite
        BRA.S   RSETWRT

;
; %_RESET - Reset a file
;
; Parameters: ST.L - File Address
;             ST.L - Address of Name String
;             ST.W - Kind: -2=text, -1=file, >0=#words/item, should be #bytes
;
%_RESET:
        .if     libproducing=ospaslib
        MOVEQ   #1,D3           ; 1 = Reset
RSETWRT:MOVE.L  (SP)+,A2        ; Pop return address
        MOVE.W  (SP)+,D2        ; Pop File Kind
        BMI.S   MINUS           ; See if bytes or special
        LSR.W   #1,D2           ; Turn bytes into words
MINUS:  MOVE.L  (SP)+,D0        ; Pop address of string
        MOVE.L  (SP)+,D1        ; Pop file address
        MOVE.L  A2,-(SP)        ; Push return address so can RTS
        MOVEM.L D0-D7/A0-A6,-(SP)
        MOVE.L  D1,-(SP)        ; Params for RESET: 1) File address
        MOVE.L  D0,-(SP)        ;    2) String address
        MOVE.W  D3,-(SP)        ;    3) 0 = Rewrite, 1 = Reset
        CLR.L   -(SP)           ;    4) 0 JUNK parameter
        PEA     R.DONE2
        MOVE.L  D1,-(SP)        ; Params for INIT: 1) File address
        ADD.W   #600,D1         ;
        MOVE.L  D1,-(SP)        ;    2) File address + 600 bytes
        MOVE.W  D2,-(SP)        ;    3) File Kind
        PEA     R.DONE1
        MOVE.W  #24,-(A7)
        TRAP    #$E             ; Call FINIT
R.DONE1 MOVE.W  #28,-(A7)
        TRAP    #$E             ; Call FOPEN
R.DONE2 MOVEM.L (SP)+,D0-D7/A0-A6
        RTS
        .endc

        .if     libproducing=appaslib
        move.w  #$101,d3        ; 1 = Reset, to lower address byte too
RSETWRT:MOVE.L  (SP)+,A2        ; Pop return address
        MOVE.W  (SP)+,D2        ; Pop File Kind
;        BMI.S   MINUS           ; See if bytes or special
;        LSR.W   #1,D2           ; Turn bytes into words
;                               ; pass bytes to finit, not words
MINUS:  MOVE.L  (SP)+,D0        ; Pop address of string
        move.l  d0,a0
        tst.b   (a0)            ; make string resident in case it is in code seg
        MOVE.L  (SP)+,D1        ; Pop file address
        MOVE.L  A2,-(SP)        ; Push return address so can RTS
        MOVEM.L D0-D7/A0-A6,-(SP)
        MOVE.L  D1,-(SP)        ; Params for RESET (fopen): 1) File address
        MOVE.L  D0,-(SP)        ;    2) String address
        MOVE.W  D3,-(SP)        ;    3) 0 = Rewrite, 1 = Reset
        CLR.L   -(SP)           ;    4) 0 JUNK parameter

        MOVE.L  D1,-(SP)        ; Params for INIT (finit): 1) File address
        ADD.W   #600,D1         ;
        MOVE.L  D1,-(SP)        ;    2) File address + 600 bytes
        MOVE.W  D2,-(SP)        ;    3) File Kind
        jsr     %_finit         ; in blockio

        jsr     %_fopen         ; in blockio

R.DONE2 MOVEM.L (SP)+,D0-D7/A0-A6
        RTS
        .endc


;
; %_CLOSE - Close a file
;
; Parameters: ST.L - File Address
;             ST.W - Mode: 0=Normal, 1=Lock, 2=Purge, 3=Crunch
;

%_CLOSE:
        .if     libproducing=ospaslib
        MOVE.L  (SP)+,A2        ; Pop return address
        MOVE.W  (SP)+,D1        ; Pop mode
        MOVE.L  (SP)+,D0        ; Pop file address
        MOVE.L  A2,-(SP)        ; Push return address so can RTS
        MOVEM.L D0-D7/A0-A6,-(SP)
        MOVE.L  D0,-(SP)        ; Push File address
        MOVE.W  D1,-(SP)        ; Push Mode
        PEA     C.DONE          ; Push return address
        MOVE.W  #76,-(A7)
        TRAP    #$E             ; Call FCLOSE
C.DONE: MOVEM.L (SP)+,D0-D7/A0-A6
        RTS
        .endc

        .if     libproducing=appaslib
        MOVE.L  (SP)+,A2        ; Pop return address
        MOVE.W  (SP)+,D1        ; Pop mode
        MOVE.L  (SP)+,D0        ; Pop file address
        MOVE.L  A2,-(SP)        ; Push return address so can RTS
        MOVEM.L D0-D7/A0-A6,-(SP)
        MOVE.L  D0,-(SP)        ; Push File address
        MOVE.W  D1,-(SP)        ; Push Mode
        jsr     %_fclose        ; in blockio

C.DONE: MOVEM.L (SP)+,D0-D7/A0-A6
        RTS
        .endc

;
; %_EOF - End of file predicate
;
; Parameters:  ST.L - File address
;
; Returns:     ST.B - Boolean Result
;
; This routine preserves all registers.
;

%_EOF:
        MOVE.L  A0,-(SP)        ; Save old A0
        MOVE.L  8(SP),A0        ; Get file address
        MOVE.W  4(A0),A0
        EXG     A0,D0
        MOVE.B  D0,10(SP)       ; Get the answer
        EXG     A0,D0
        MOVE.L  4(SP),A0        ; Get Return Address
        MOVE.L  A0,6(SP)        ; Patch it into stack
        MOVE.L  (SP)+,A0        ; Restore A0
        ADDQ.L  #2,SP           ; Bump SP
        RTS

;
; %_EOLN - End of line predicate
;
; Parameters:  ST.L - File address
;
; Returns:     ST.B - Boolean Result
;
; This routine preserves all registers.
;
;LOW CORE READLN/WRITELN DATA ALLOCATION:
;  ( *** NEEDS TO BE MOVED ELSEWHERE, PERHAPS TO SYSGLOBAL *** )
ateol           equ     $1ef    ;'eol' flag for console driver             (byte)

%_EOLN:
        .if     libproducing=ospaslib
        MOVE.L  A0,-(SP)        ; Save old A0
        MOVE.L  8(SP),A0        ; Get file address
        move.b  ateol,a0
;       MOVE.W  4(A0),A0
        EXG     A0,D0
;       LSR.W   #8,D0
        MOVE.B  D0,10(SP)       ; Get the answer
        EXG     A0,D0
        MOVE.L  4(SP),A0        ; Get Return Address
        MOVE.L  A0,6(SP)        ; Patch it into stack
        MOVE.L  (SP)+,A0        ; Restore A0
        ADDQ.L  #2,SP           ; Bump SP
        RTS
        .endc

        .if     libproducing=appaslib
        MOVE.L  A0,-(SP)        ; Save old A0
        MOVE.L  8(SP),A0        ; Get file address (saved reg + return pc )
        MOVE.W  4(A0),A0
        EXG     A0,D0
        LSR.W   #8,D0
        MOVE.B  D0,10(SP)       ; Get the answer
        EXG     A0,D0
        MOVE.L  4(SP),A0        ; Get Return Address
        MOVE.L  A0,6(SP)        ; Patch it into stack
        MOVE.L  (SP)+,A0        ; Restore A0
        ADDQ.L  #2,SP           ; Bump SP
        RTS
        .endc

;
; %_BLKRD - Blockread
;
; Parameters: ST.L - File Address
;             ST.L - Buffer address
;             ST.W - # Blocks to read
;             ST.W - Block Number, -1 = Sequential
;
; Returns:    ST.W - # Blocks actually read
;
%_BLKRD:
        .if     libproducing=ospaslib
        MOVEQ   #1,D0           ; 1 = blockread
        BRA.S   B.RD.WR
        .endc

        .if     libproducing=appaslib
        move.w  #$101,D0           ; 1 = blockread, to lower address byte too
        BRA.S   B.RD.WR
        .endc


;
; %_BLKWR - Blockwrite
;
; Parameters: ST.L - File Address
;             ST.L - Buffer address
;             ST.W - # Blocks to write
;             ST.W - Block Number, -1 = Sequential
;
; Returns:    ST.W - # Blocks actually written
;
; Note that the compiler does not generate code to reserve room for function
; result !
;

%_BLKWR:
        .if     libproducing=ospaslib
        CLR.W   D0              ; 0 = blockwrite
B.RD.WR:MOVE.L  (SP)+,A2        ; Pop return address
        MOVE.W  (SP)+,D1        ; Block Number
        MOVE.W  (SP)+,D2        ; # Blocks
        MOVE.L  (SP)+,D3        ; Buffer Address
        MOVE.L  (SP)+,A1        ; File Address
        MOVEM.L D1-D7/A0-A6,-(SP)
        CLR.L   -(SP)           ;       Room for function result
        MOVE.L  A1,-(SP)        ;    1) File address
        MOVE.L  D3,-(SP)        ;    2) BYTE address of buffer
        MOVE.W  D2,-(SP)        ;    3) # Blocks
        MOVE.W  D1,-(SP)        ;    4) Block Number
        MOVE.W  D0,-(SP)        ;    5) 0 = write,  1 = read
        PEA     B.DONE
        MOVE.W  #32,-(A7)
        TRAP    #$E             ; Call BLKIO
B.DONE: MOVE.W  (A7)+,D0        ; Get function result
        MOVEM.L (SP)+,D1-D7/A0-A6
        MOVE.W  D0,-(SP)        ; Push the result
        JMP     (A2)
        .endc

        .if     libproducing=appaslib
        CLR.W   D0              ; 0 = blockwrite
B.RD.WR:MOVE.L  (SP)+,A2        ; Pop return address
        MOVE.W  (SP)+,D1        ; Block Number
        MOVE.W  (SP)+,D2        ; # Blocks
        MOVE.L  (SP)+,D3        ; Buffer Address
        MOVE.L  (SP)+,a0        ; File Address
        MOVEM.L D1-D7/A0-A6,-(SP)
        CLR.w   -(SP)           ;       Room for function result (2 bytes) - modified
        MOVE.L  a0,-(SP)        ;    1) File address
        MOVE.L  D3,-(SP)        ;    2) BYTE address of buffer
        MOVE.W  D2,-(SP)        ;    3) # Blocks
        MOVE.W  D1,-(SP)        ;    4) Block Number
        MOVE.W  D0,-(SP)        ;    5) 0 = write,  1 = read
        jsr     %_fblockio      ; in blockio

B.DONE: MOVE.W  (A7)+,D0        ; Get function result
        MOVEM.L (SP)+,D1-D7/A0-A6
        MOVE.W  D0,-(SP)        ; Push the result
        JMP     (A2)
        .endc


;
; %_UREAD - Unitread
;
; Parameters: ST.W - Unit number
;             ST.L - Buffer address
;             ST.W - # Bytes to read
;             ST.W - Block Number
;             ST.W - Mode
;

%_UREAD:
        .if     libproducing=ospaslib
        MOVE.W  #56,D0
        BRA.S   U.RD.WR
        .endc

        .if     libproducing=appaslib
        clr.w   d0              ; unitcode for unitread
        BRA.S   U.RD.WR
        .endc

;
; %_UWRIT - Unitwrite
;
; Parameters: ST.W - Unit number
;             ST.L - Buffer address
;             ST.W - # Bytes to write
;             ST.W - Block Number
;             ST.W - Mode
;

%_UWRIT:
        .if     libproducing=ospaslib
        MOVE.W  #60,D0
U.RD.WR:MOVE.L  (SP)+,A2        ; Pop return address
        MOVE.W  (SP)+,D1        ; Mode
        MOVE.W  (SP)+,D2        ; Block Number
        MOVE.W  (SP)+,D3        ; # Bytes
        MOVE.L  (SP)+,A1        ; Buffer Address
        MOVE.W  (SP)+,A0        ; Unit number
        MOVEM.L D1-D7/A0-A6,-(SP)
        PEA     U.DONE
        MOVE.W  A0,-(SP)        ; Params for UNITRW: 1) Unit number
        MOVE.L  A1,-(SP)        ;    2) BYTE address of buffer
        MOVE.W  D3,-(SP)        ;    3) # Bytes
        MOVE.W  D2,-(SP)        ;    4) Block Number
        MOVE.W  D1,-(SP)        ;    5) Mode
        MOVE.W  D0,-(SP)
        TRAP    #$E
U.DONE: MOVEM.L (SP)+,D1-D7/A0-A6
        JMP     (A2)
        .endc

        .if     libproducing=appaslib
        MOVE.W  #1,D0           ; unitcode for unitwrite
U.RD.WR:MOVE.L  (SP)+,A2        ; Pop return address
        MOVE.W  (SP)+,D1        ; Mode
        MOVE.W  (SP)+,D2        ; Block Number
        MOVE.W  (SP)+,D3        ; # Bytes
        MOVE.L  (SP)+,A1        ; Buffer Address
        MOVE.W  (SP)+,A0        ; Unit number
        MOVEM.L D1-D7/A0-A6,-(SP)
        MOVE.W  A0,-(SP)        ; Params for funitiocall: 1) Unit number
        MOVE.L  A1,-(SP)        ;    2) BYTE address of buffer
        MOVE.W  D3,-(SP)        ;    3) # Bytes
        MOVE.W  D2,-(SP)        ;    4) Block Number
        MOVE.W  D1,-(SP)        ;    5) Mode
        MOVE.W  D0,-(SP)        ;    6) unitcode
        jsr     %_funitiocall   ; in blockio

U.DONE: MOVEM.L (SP)+,D1-D7/A0-A6
        JMP     (A2)

        .endc

;
; %_IORES - Ioresult
;
; Parameters: None.
;
; Returns:    ST.W - Ioresult
;
; This routine preserves all registers
;

%_IORES:

        .if     libproducing=ospaslib
        CLR.W   -(SP)           ; Make room for the result for IORESULT call
                                ; since compiler does not generate code to
                                ; reserve room for it
        MOVEM.L D0-D7/A0-A6,-(SP)
        PEA     IO.DONE
        MOVE.W  #68,-(SP)
        TRAP    #$E
IO.DONE:MOVE.W  (SP)+,D0        ; Get function result
        MOVE.L  62(SP),A0       ; Get return address
        MOVE.W  D0,64(SP)       ; Store result
        MOVE.L  A0,60(SP)       ; Set up return address
        MOVEM.L (SP)+,D0-D7/A0-A6
        RTS
        .endc

        .if     libproducing=appaslib
        CLR.W   -(SP)           ; Make room for the result for IORESULT call
                                ; since compiler does not generate code to
                                ; reserve room for it
        MOVEM.L D0-D7/A0-A6,-(SP)
        CLR.W   -(SP)           ; Make room for the result for fioresult call
        jsr     %_fioresult     ; in blockio

IO.DONE:MOVE.W  (SP)+,D0        ; Get function result
        MOVE.L  62(SP),A0       ; Get return address
        MOVE.W  D0,64(SP)       ; Store result
        MOVE.L  A0,60(SP)       ; Set up return address
        MOVEM.L (SP)+,D0-D7/A0-A6
        RTS
        .endc


;
; %_UCLR - Unit Clear
;
; Parameters: ST.W - Unit to clear
;

%_UCLR:
        .if     libproducing=ospaslib
        MOVE.L  (SP)+,A2        ; Pop return address
        MOVE.W  (SP)+,D0        ; Pop unit number
        MOVE.L  A2,-(SP)        ; Push return address so can RTS
        MOVEM.L D0-D7/A0-A6,-(SP)
        PEA     UC.DONE
        MOVE.W  D0,-(SP)        ; Parameter: Unit Number
        MOVE.W  #52,-(SP)
        TRAP    #$E
UC.DONE:MOVEM.L (SP)+,D0-D7/A0-A6
        RTS
        .endc

        .if     libproducing=appaslib
        MOVE.L  (SP)+,A2        ; Pop return address
        MOVE.W  (SP)+,D0        ; Pop unit number
        MOVE.L  A2,-(SP)        ; Push return address so can RTS
        MOVEM.L D0-D7/A0-A6,-(SP)
        MOVE.W  d0,-(SP)        ; Params for funitiocall: 1) Unit number
        clr.l   -(SP)           ;    2) BYTE address of buffer
        clr.W   -(SP)           ;    3) # Bytes
        clr.W   -(SP)           ;    4) Block Number
        clr.W   -(SP)           ;    5) Mode
        MOVE.W  #2,-(SP)        ;    6) unitcode
        jsr     %_funitiocall   ; in blockio

UC.DONE:MOVEM.L (SP)+,D0-D7/A0-A6
        RTS
        .endc

;
; %_UBUSY - Unit Busy
;
; Parameters: ST.W - Unit to check
;
; Returns:    ST.B - Boolean Result
;
; This routine preserves all registers.
;

%_UBUSY:
        .if     libproducing=ospaslib
        MOVE.L  D0,-(SP)        ; Save D0
        MOVE.W  8(SP),D0        ; Fetch unit number
        MOVEM.L D1-D7/A0-A6,-(SP)
        PEA     UB.DONE
        MOVE.W  D0,-(SP)        ; Parameter: Unit Number
        MOVE.W  #64,-(SP)
        TRAP    #$E
UB.DONE:MOVE.W  (A7)+,D0        ; Fetch function result
        MOVEM.L (SP)+,D1-D7/A0-A6
        MOVE.B  D0,8(SP)        ; Place it on the stack
        MOVE.L  (SP)+,D0        ; Restore D0
        RTS
        .endc

        .if     libproducing=appaslib
        MOVE.L  D0,-(SP)        ; Save D0
        MOVE.W  8(SP),D0        ; Fetch unit number
        MOVEM.L D1-D7/A0-A6,-(SP)
        clr.w   -(sp)           ; leave room for function result
        MOVE.W  D0,-(SP)        ; Parameter: Unit Number
        jsr     %_funitbusy     ; in blockio

UB.DONE:MOVE.W  (A7)+,D0        ; Fetch function result
        MOVEM.L (SP)+,D1-D7/A0-A6
        MOVE.B  D0,8(SP)        ; Place it on the stack
        MOVE.L  (SP)+,D0        ; Restore D0
        RTS
        .endc

;
; %_GET - Get
;
; Parameters: ST.L - File Address
;

%_GET:
        .if     libproducing=ospaslib
        MOVE.W  #104,D0
        BRA.S   GET.PUT
        .endc

        .if     libproducing=appaslib
        MOVE.L  (SP)+,A2        ; Pop return address
        MOVE.L  (SP)+,D1        ; Pop file address
        MOVEM.L D0-D7/A0-A6,-(SP)
        MOVE.L  D1,-(SP)        ; Params for GET.PUT: 1) File address
        jsr     %_fget

        MOVEM.L (SP)+,D0-D7/A0-A6
        JMP     (A2)
        .endc
;
; %_PUT - Put
;
; Parameters: ST.L - File Address
;

%_PUT:
        .if     libproducing=ospaslib
        MOVE.W  #108,D0
GET.PUT:MOVE.L  (SP)+,A2        ; Pop return address
        MOVE.L  (SP)+,D1        ; Pop file address
        MOVEM.L D0-D7/A0-A6,-(SP)
        MOVE.L  D1,-(SP)        ; Params for GET.PUT: 1) File address
        PEA     GP.DONE
        MOVE.W  D0,-(SP)
        TRAP    #$E
GP.DONE:MOVEM.L (SP)+,D0-D7/A0-A6
        JMP     (A2)
        .endc

        .if     libproducing=appaslib
GET.PUT:MOVE.L  (SP)+,A2        ; Pop return address
        MOVE.L  (SP)+,D1        ; Pop file address
        MOVEM.L D0-D7/A0-A6,-(SP)
        MOVE.L  D1,-(SP)        ; Params for GET.PUT: 1) File address
        jsr     %_fput

GP.DONE:MOVEM.L (SP)+,D0-D7/A0-A6
        JMP     (A2)
        .endc
;
; %_UPARR - Computes the address of F^
;
; Parameters: ST.L - Address of file
;
; Returns:    ST.L - Address of F^
;
; This routine saves all registers.
;

%_UPARR:
        MOVE.L  A0,-(SP)
        MOVE.L  8(SP),A0       ; Address of file
        MOVE.L  (A0),8(SP)     ; Fetch address of F^, store it as result
        MOVE.L  (SP)+,A0
        RTS

;
; %_SEEK - Seek
;
; Parameters: ST.L - Address of file
;             ST.W - Record number to seek
;

%_SEEK:
        .if     libproducing=ospaslib
        MOVE.L  (SP)+,A2        ; Pop return address
        MOVE.W  (SP)+,D0        ; Pop record number
        MOVE.L  (SP)+,D1        ; Pop file address
        MOVEM.L D0-D7/A0-A6,-(SP)
        MOVE.L  D1,-(SP)        ; Params: 1) Address of file
        MOVE.W  D0,-(SP)        ;   2) Record number
        PEA     S.DONE
        MOVE.W  #112,-(SP)
        TRAP    #$E
S.DONE: MOVEM.L (SP)+,D0-D7/A0-A6
        JMP     (A2)
        .endc

        .if     libproducing=appaslib
        move.l  (sp)+,a2        ; pop return address
        move.w  (sp)+,d0        ; pop record number
        move.l  (sp)+,d1        ; pop file address
        movem.l d0-d7/a0-a6,-(sp)
        move.l  d1,-(sp)        ; params : address of file
        move.w  d0,-(sp)        ;          record number
        jsr     %_fseek         ; in blockio
        movem.l (sp)+,d0-d7/a0-a6
        jmp     (a2)
        .endc
;
        .END






ÿ