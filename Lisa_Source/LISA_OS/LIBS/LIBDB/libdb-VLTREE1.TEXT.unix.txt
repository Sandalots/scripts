{#f     libdb/vltree1.text }
{$IFC PRERELEASE }
{$S debug }
{************************************************************************

                                dumpps

*************************************************************************}

procedure dumpps(fileid : integer);
var
        i : integer;
begin
{$IFC fTraceDB}    LogCall;     {$ENDC}
        if bdebug then
            begin
            writeln(dumpfile,'numkeys','index':7,'page':14,'vact':7,
                    'rec_rpage':14,'rec_lpage':14);
            for i := 1 to pfiletable^[fileid]^.depth do
                with pstack^[i] do
                    writeln(dumpfile,numkeys:7,index:7,page:14,
                            ord(vact):7,rec_rpage:14,rec_lpage:14);
            end;
end;
{$ENDC}
{$S access }
{^E}
{***************************************************************************

                                fixstack

        iteratively adjusts pstack to construct an exact path from
        root to the leaf page.  this must be done if duplicates are
        allowed, findduplicates set stackinvalid and a page split,
        rotate or coallesce desired. algorithm:

        repeat until we have found the second level page which contains
        a pointer to the found leaf page (if we don't find the ptr, then
        the index is damaged)
            for every new second level page we adjust pstack:
                from the third level down to the root level search until
                we find a page which still has an entry we haven't
                looked at. (call this page a) then do
                    decrement or increment the entry index for a in pstack
                    from the level below a to the second level adjust pstack
                    to point at the left or right extreme path from a to
                    the second level page


        Input Parameters
                stackinvalid - whether findduplicates stayed on the original
                    leafpage or went left or right of it
                fileid - id of file

        Output Parameters
                kresult - return result

        Return Results

***************************************************************************}

{$S access }
procedure fixstack(var kresult : integer;
                       stackinvalid : integer;
                       fileid : integer);
label
        2,3;
var
        locdepth,
        lresult,
        lev1prio,
        relprio,
        buffer,
        descoff,
        i,
        level,
        downlevel,
        last        : integer;
        downpage,
        spage       : fileptr;
        pb : ptrdata;
        pd : ptrpagedesc;
        po : ptroffsets;
        pptr   : ^ fileptr;
        ps : ^ stackrec;
begin
{$IFC fTraceDB}    LogCall;     {$ENDC}
{$IFC debug }
        if bdebug then
            begin
            writeln(dumpfile,'fixs:',stackinvalid);
            dumpps(fileid);
            end;
{$ENDC }

        if stackinvalid = ok then
            begin
            lresult := noerr;
            goto 2;
            end;
        with pfiletable^[fileid]^ do
            begin
            locdepth := depth;
            if locdepth = 2 then lev1prio := isroot
            else lev1prio := isnonleaf;
            descoff := pagesize-sizeof(pagedesc);
            spage := pstack^[locdepth].page;
            ps := @pstack^[locdepth-1];
            repeat
                getpage(lresult,buffer,fileid,ps^.page,true);
                if lresult <> noerr then goto 2;

{$IFC debug }
                if bdebug then dumpp(fileid,buffer);
{$ENDC }

                pb := buffpool^[buffer].pbuff;
                pd := pointer(ord(pb)+descoff);
                po := pointer(ord(pd)-sizeof(integer));
                ps^.numkeys := pd^.nkeys;

            {   try to find it in this 2nd level page }

                for i := 0 to pd^.nkeys-1 do
                    begin
                    pptr := pointer(ord(pb)+po^[-i]);
                    if pptr^ = spage then
                        begin
                        relbuff(lresult,buffer,lev1prio);
                        ps^.index := i;
                        goto 2;
                        end;
                    end;


            {   no luck; move to next sibling (and properly adjust path to
                the sibling }

                relbuff(lresult,buffer,lev1prio);
                if lresult <> noerr then goto 2;

                level := locdepth-2;
                relprio := isnonleaf;
                while level >= 1 do
                    begin
                    if level = 1 then relprio := isroot;
                    with pstack^[level] do
                        begin
                        if ((stackinvalid = right)and(index < (numkeys-1)))
                                              or
                           ((stackinvaid = left) and (index > 0))
                        then
                            begin
                            getpage(lresult,buffer,fileid,page,true);
                            if lresult <> noerr then goto 2;

{$IFC debug }
                            if bdebug then dumpp(fileid,buffer);
{$ENDC }

                            pb := buffpool^[buffer].pbuff;
                            pd := pointer(ord(pb)+descoff);
                            po := pointer(ord(pd)-sizeof(integer));
                            if stackinvalid = right then
                                index := index + 1
                            else
                                index := index - 1;
                            pptr := pointer(ord(pb)+po^[-index]);
                            downpage := pptr^;
                            pstack^[level+1].page := downpage;
                            relbuff(lresult,buffer,relprio);
                            if lresult <> noerr then goto 2;
                            for downlevel := level+1 to locdepth-2 do
                                begin
                                getpage(lresult,buffer,fileid,downpage,true);
                                if lresult <> noerr then goto 2;
                                pd := pointer(ord(pb)+descoff);
                                po := pointer(ord(pd)-sizeof(integer));

{$IFC debug }
                                if bdebug then dumpp(fileid,buffer);
{$ENDC }

                                with pstack^[downlevel] do
                                    begin
                                    if stackinvalid = right then
                                        index := 0
                                    else
                                        index := pd^.nkeys - 1;
                                    numkeys := pd^.nkeys;
                                    pptr := pointer(ord(pb)+po^[-index]);
                                    downpage := pptr^;
                                    end;
                                pstack^[downlevel+1].page := downpage;
                                relbuff(lresult,buffer,isnonleaf);
                                if lresult <> noerr then goto 2;
                                end; { of for }
                            goto 3; { continue }
                            end
                        else
                            level := level - 1;
                        end; { with }
                    end; { while }

                {   index is broken -- there exists a leaf that isn't
                    recorded in the non-leaf nodes }

                lresult := inconsistent;
                fstatus := inconsistent;
                goto 2;

            3 : { continue }

            until false;

        end; { with }

2 :     { exit }

{$IFC debug }
        dumpps(fileid);
{$ENDC }

        kresult := lresult;

{$IFC debug }
        if bdebug then writeln(dumpfile,'fixs:re',kresult:6);
{$ENDC }
end;
{^E}
{****************************************************************************

                              find

        see interface

****************************************************************************}

{$S access }
procedure find{(var kresult : integer;
                   which : integer;
                   nsearch : integer;
                   fileid : integer;
                   psdesc : ptrrecptr;
                   pkey : ptrdata;
               var buffer : integer)};
label
        2;
var
        ps : ^ stackrec;
        pptr : ^ fileptr;
        sresult : boolean;
        lresult,
        relprio,
        nleafkeys,
        leafkeys,
        index,
        i,
        descoff : integer;
        pd : ptrpagedesc;
        pb : ptrdata;
        po : ptroffsets;
begin

{$IFC fTraceDB}    LogCall;     {$ENDC}
{$IFC debug }
        if bdebug then writeln(dumpfile,'findm');
{$ENDC }

        with pfiletable^[fileid]^ do begin
            leafkeys := nkeys;
            nleafkeys := nsort;
            if nsearch < leafkeys then
                begin
                leafkeys := nsearch;
                if nsearch < nleafkeys then nleafkeys := nsearch;
                end;
            descoff := pagesize - sizeof(pagedesc);
            if root = nilptr then
                lresult := fileempty
            else
                begin
                ps := @pstack^[1];
                ps^.page := root;
                relprio := isroot;
                for i := 1 to depth do
                    begin
                    getpage(lresult,buffer,fileid,ps^.page,true);
                    if lresult <> noerr then goto 2;
                    pb := buffpool^[buffer].pbuff;
                    pd := pointer(ord(pb)+descoff);
                    po := pointer(ord(pd) - sizeof(integer));
                    ps^.numkeys := pd^.nkeys;

{$IFC debug }
                    if bdebug then dumpp(fileid,buffer);
{$ENDC }

                    if i < depth then { nonleaf }
                        begin

                    {   the first key of a nonleaf page is used only
                        to hold the lowest key in the tree rooted at
                        that page.  it is not need for the search.  in
                        fact, the first keys on the left edge of the
                        whole tree do not even hold the lowest key of
                        the subtree, but rather only the initially
                        lowest key.  but no matter, splits always occur
                        to the right of the left edge }

                        index := 0;
                        if pd^.nkeys > 1 then
                            begin
                            if which >= approx then
                                begin
                                bsearchv(sresult,
                                         pointer(ord(pb)+sizeof(fileptr)),
                                         pointer(ord(po)-sizeof(integer)),
                                         pkey,
                                         pd^.nkeys-1,
                                         nleafkeys,
                                         index,
                                         pedesc,
                                         psdesc);
                                if sresult then index:=index+1;
                                end
                            else if which = last then index := pd^.nkeys-1;
                            end;

                        ps^.index := index;

{$IFC debug }
                        if bdebug then writeln(dumpfile,'sr,i:',
                                               ord(sresult):3,
                                               ps^.index:4);
{$ENDC }

                        pptr := pointer(ord(pb)+po^[-index]);
                        pstack^[i+1].page := pptr^;

                        relbuff(lresult,buffer,relprio);
                        if lresult <> noerr then goto 2;
                        relprio := isnonleaf;
                        end
                    else   { leaf }
                        begin
                        if which >= approx then
                            begin
                            bsearchv(sresult,
                                     pb,
                                     po,
                                     pkey,
                                     pd^.nkeys,
                                     leafkeys,
                                     ps^.index,
                                     pedesc,
                                     psdesc);
                            if sresult then lresult := noerr
                            else lresult := notfound;

{$IFC debug }
                            if bdebug then writeln(dumpfile,'sr,i:',
                                                   ord(sresult):3,
                                                   ps^.index:4);
{$ENDC }
                            end
                        else
                            begin
                            lresult := noerr;
                            if which = first then ps^.index := 0
                            else ps^.index := pd^.nkeys;
                            end;

                        end; { else }
                    ps := pointer(ord(ps) + sizeof(stackrec))
                    end; { for }
                end; { else }
        end; { with }
2 :

{$IFC debug }
        dumpps(fileid);
{$ENDC }

        kresult := lresult;
end; { find }
{******************************************************************************

                        Creche

        given it's stack level, computes then right and left siblings of a
        child page.  if a sibling doesn't exist, that pointer is set to nilptr.

        Input Parameters

        slevel - stack level of child page
        descoff - offset in page for pagedesc

        Output Parameters

        kresult - return result
                        noerr
            nobuffs
            ioerr
            noroom
            ioerr
            inconsistent

        rightsib,
        leftsib - the siblings


******************************************************************************}

{$S access }
procedure Creche(var kresult : integer;
                     fileid : integer;
                     slevel : integer;
                     descoff : integer;
                 var rightsib,
                     leftsib : fileptr);
var
        relprio : integer;
        ps : ptrstackrec;
        parent : integer;
        pb : ptrdata;
        po : ptroffsets;
        pd : ptrpagedesc;
        pfptr : ptrfileptr;
begin
{$IFC fTraceDB}    LogCall;     {$ENDC}
{$IFC debug }
        if bdebug then writeln('Creche ',slevel);
{$ENDC }

        if slevel > 1 then { parent exists }
            begin
            ps := @pstack^[slevel-1];
            getpage(kresult,parent,fileid,ps^.page,true);
            if kresult = noerr then
                begin

                pb := buffpool^[parent].pbuff;
                pd := pointer(ord(pb)+descoff);
                po := pointer(ord(pd)-sizeof(integer));

                if ps^.numkeys = 1 then { one key -> no siblings }
                    begin
                    leftsib := nilptr;
                    rightsib := nilptr;
                    end
                else
                    begin
                    if ps^.index = (ps^.numkeys-1) then rightsib := nilptr
                    else
                        begin
                        pfptr := pointer(ord(pb)+po^[-ps^.index-1]);
                        rightsib := pfptr^;
                        end;
                    if ps^.index = 0 then leftsib := nilptr
                    else
                        begin
                        pfptr := pointer(ord(pb)+po^[-ps^.index+1]);
                        leftsib := pfptr^;
                        end;
                    end;
                relprio := isnonleaf;
                if slevel = 2 then relprio := isroot;
                relbuff(kresult,parent,relprio);
                end
            end
        else { this is the root, therefore there exist no siblings }
            begin
            kresult := noerr;
            leftsib := nilptr;
            rightsib := nilptr;
            end;
{$IFC debug }
        if bdebug then writeln('Creche ',kresult,slevel,leftsib,rightsib);
{$ENDC }
end; { Creche }
ÿ