LTVAL           .EQU    0
GTVAL           .EQU    1
EQVAL           .EQU    2

                .SEG    "sortseg"
                .PROC   QSORT
                .REF    COMPARE

;--------------------------------------------------------------------------
;
;       PROCEDURE QSORT(RPTRS : PTRPTRDATA;
;                       PLAST : PTRPTRDATA;
;                       PS    : PTRPTRPTRDATA;
;                       PKEY  : PTRRECPTR;
;                       NSFLDS: INTEGER);
;
;       QUICKSORT.  PS PTS TO AN EMPTY STACK (WHICH GROWS DOWN) OF SORT
;       PAIRS.  RPTRS AND PLAST DELIMIT AN ARRAY OF PTRS TO RECORDS.  THIS
;       ARRAY HAS AN EXTRA ENTRY (PLAST) WHICH IS USED AS A SENTINEL.
;       PKEY DESCRIBES THE RECORD TO SORT AND NSFLDS IS THE NUMBER OF SORT
;       FIELDS
;
NREGISTERS      .EQU    44
RPTRS           .EQU    NREGISTERS+18
PLAST           .EQU    NREGISTERS+14
PS              .EQU    NREGISTERS+10
PKEY            .EQU    NREGISTERS+6
NSFLDS          .EQU    NREGISTERS+4
NPARMS          .EQU    18

TWOPTR          .EQU    8
PTRSIZE         .EQU    4
TOLERANCE       .EQU    100


        MOVEM.L D2-D7/A2-A6,-(SP)
        MOVE.L  SP,A6                   ;SETUP PARAMETER POINTER IN A6
        MOVE.L  PS(A6),A0               ;A0 IS TEMPORARY HOME
        MOVE.L  PKEY(A6),D3             ;D3 := PKEY
        MOVE.W  NSFLDS(A6),D4           ;D4 := NSFLDS

        MOVE.L  PLAST(A6),-(A0)         ;PUSH (RPTRS,PLAST-4) ON SORT STACK
        SUBQ.L  #PTRSIZE,(A0)           ;PLOWER := RPTRS
        MOVE.L  RPTRS(A6),-(A0)         ;PUPPER := PLAST-4


SORT    MOVE.L  A0,D2                   ;D2 WILL BE THE STACK POINTER
SORTIN  CMP.L   PS(A6),D2               ;BRANCH HERE FROM INSERT SORT
        BNE.S   @1                      ;
        MOVEM.L (SP)+,D2-D7/A2-A6       ;
        MOVE.L  (SP)+,A0                ;
        MOVEQ   #NPARMS,D0              ;
        ADDA.L  D0,SP                   ;
        JMP     (A0)                    ;

@1      MOVE.L  D2,A0                   ;
        MOVE.L  (A0)+,A3                ;POP PLOWER
        MOVE.L  (A0)+,A2                ;POP PUPPER
        MOVE.L  A0,D2                   ;
        MOVE.L  A2,D6                   ;IF (PUPPER-PLOWER) < TOLERANCE THEN
        SUB.L   A3,D6                   ;    BEGIN
        CMPI.L  #TOLERANCE,D6           ;    { INSERTION SORT }
        BHI.S   QUICKST                 ;
                                        ;    IF PUPPER <> PLOWER THEN
        MOVE.L  A3,A4                   ;        PI := PLOWER
        BRA.S   LOOPEXT                 ;
INSORT  ADDQ.L  #PTRSIZE,A4             ;        REPEAT
        MOVE.L  A3,A1                   ;            PI++;PJ := PLOWER
        MOVE.L  (A4),D5                 ;            PK := PI^
INSPT   MOVE.L  A1,-(SP)                ;
        CLR.W   -(SP)                   ;            REPEAT
        MOVE.L  D3,-(SP)                ;                IF COMPARE(PKEY,PKEY,
        MOVE.L  D3,-(SP)                ;                           NSFLDS,
        MOVE.W  D4,-(SP)                ;                           PK,
        MOVE.L  D5,-(SP)                ;                           PJ) <> GT
        MOVE.L  (A1),-(SP)              ;                THEN
        JSR     COMPARE                 ;                    BEGIN
        CMPI.W  #GTVAL,(SP)+            ;                    MOVERF(PJ,PJ+4,
        MOVEA.L (SP)+,A1                ;
        BEQ.S   INCRPJ                  ;                           (PI-PJ))
        CMPA.L  A1,A4                   ;                    PJ^ := PK
        BEQ.S   LOOPEXT                 ;                    BREAK
        MOVE.L  A4,A0                   ;                    END
@1      MOVE.L  -(A0),PTRSIZE(A0)       ;
        CMPA.L  A1,A0                   ;
        BNE.S   @1                      ;
        MOVE.L  D5,(A1)                 ;
        BRA.S   LOOPEXT                 ;
INCRPJ  ADDQ.L  #PTRSIZE,A1             ;                PJ++
        BRA.S   INSPT                   ;            UNTIL FALSE

LOOPEXT CMPA.L  A4,A2                   ;        UNTIL PI = PUPPER
        BNE.S   INSORT                  ;
        BRA.S   SORTIN                  ;    END
                                        ;ELSE
QUICKST LSR.L   #3,D6                   ;    BEGIN { QUICK SORT - MEDIAN OF 3}
        LSL.L   #2,D6                   ;    MIDPT := (PUPPER-PLOWER) DIV 8 * 4
        MOVE.L  D6,D5                   ;    QUAD := MIDPT DIV 8 * 4
        LSR.L   #3,D5                   ;
        LSL.L   #2,D5                   ;
        ADD.L   A3,D6                   ;    PM := PLOWER+MIDPT
        MOVE.L  D6,A4                   ;
        MOVE.L  A1,-(SP)                ;
        CLR.W   -(SP)                   ;    CLM:= COMPARE(PKEY,PKEY,NSFLDS,
        MOVE.L  D3,-(SP)                ;                  PLOWER^[QUAD],PM^)
        MOVE.L  D3,-(SP)                ;
        MOVE.W  D4,-(SP)                ;
        MOVE.L  0(A3,D5.L),-(SP)        ;
        MOVE.L  (A4),-(SP)              ;
        JSR     COMPARE                 ;
        MOVE.W  (SP)+,D7                ;
        MOVEA.L (SP)+,A1                ;

        MOVE.L  A1,-(SP)                ;
        CLR.W   -(SP)                   ;    CLU:= COMPARE(PKEY,PKEY,NSFLDS,
        MOVE.L  D3,-(SP)                ;                  PLOWER^[QUAD],
        MOVE.L  D3,-(SP)                ;                  PUPPER^[-QUAD])
        MOVE.W  D4,-(SP)                ;
        MOVE.L  0(A3,D5.L),-(SP)        ;
        NEG.L   D5                      ;
        MOVE.L  0(A2,D5.L),-(SP)        ;
        JSR     COMPARE                 ;
        MOVE.W  (SP)+,D6                ;
        MOVEA.L (SP)+,A1                ;

        MOVE.L  A1,-(SP)                ;
        CLR.W   -(SP)                   ;    CMU:= COMPARE(PKEY,PKEY,NSFLDS,
        MOVE.L  D3,-(SP)                ;                  PM^,PUPPER^[-QUAD])
        MOVE.L  D3,-(SP)                ;
        MOVE.W  D4,-(SP)                ;
        MOVE.L  (A4),-(SP)              ;
        MOVE.L  0(A2,D5.L),-(SP)        ;
        JSR     COMPARE                 ;
        MOVE.W  (SP)+,D0                ;
        MOVEA.L (SP)+,A1                ;

        CMPI.W  #GTVAL,D0               ;    IF (CMU <> GT AND CLM <> GT) OR
        BEQ.S   @3                      ;       (CMU <> LT AND CLM <> LT) THEN
        CMPI.W  #GTVAL,D7               ;        PLOWER^ <-> PM^
        BEQ.S   @3                      ;
@2      MOVE.L  (A4),D0                 ;
        MOVE.L  (A3),(A4)               ;
        MOVE.L  D0,(A3)                 ;
        BRA.S   @8                      ;
@3      TST.W   D0                      ;
        BEQ.S   @4                      ;
        TST.W   D7                      ;
        BNE.S   @2                      ;

@4      TST.W   D6                      ;    ELSE IF(CLU<>LT AND CMU<>GT) OR
        BEQ.S   @6                      ;          (CLU<>GT AND CMU<>LT) THEN
        CMPI.W  #GTVAL,D0               ;        PLOWER^ <-> PUPPER^[-QUAD]
        BEQ.S   @6                      ;
@5      MOVE.L  A2,A0                   ;
        ADD.L   D5,A0                   ;
        MOVE.L  (A0),D0                 ;
        MOVE.L  (A3),(A0)               ;
        MOVE.L  D0,(A3)                 ;
        BRA.S   @8                      ;
@6      CMPI.W  #GTVAL,D6               ;
        BEQ.S   @7                      ;
        TST.W   D0                      ;
        BNE.S   @5                      ;

@7      MOVE.L  A3,A0                   ;    ELSE PLOWER^ <-> PLOWER^[QUAD]
        SUB.L   D5,A0                   ;
        MOVE.L  (A0),D0                 ;
        MOVE.L  (A3),(A0)               ;
        MOVE.L  D0,(A3)                 ;


@8      MOVE.L  A2,A1                   ;   PJ := PUPPER + SIZEOF(PTR)
        ADDQ.L  #PTRSIZE,A1             ;
        MOVE.L  A3,A4                   ;   PI := PLOWER
        MOVE.L  (A3),D5                 ;   PK := PM^
        MOVE.L  PLAST(A6),A0            ;   PLAST^ := PK
        MOVE.L  D5,(A0)                 ;   { GUARANTEE SENTINEL }

PARTING                                 ;   REPEAT
SHIFTL  ADDQ.L  #PTRSIZE,A4             ;       REPEAT PI++
        MOVE.L  A1,-(SP)                ;
        CLR.W   -(SP)                   ;       UNTIL LT<> COMPARE(PKEY,
        MOVE.L  D3,-(SP)                ;                  PKEY,
        MOVE.L  D3,-(SP)                ;                  NSFLDS,
        MOVE.W  D4,-(SP)                ;                  PI^,
        MOVE.L  (A4),-(SP)              ;                  PK)
        MOVE.L  D5,-(SP)                ;
        JSR     COMPARE                 ;
        TST.W   (SP)+                   ;
        MOVEA.L  (SP)+,A1                ;
        BEQ.S   SHIFTL                  ;

SHIFTR  SUBQ.L  #4,A1                   ;
        MOVE.L  A1,-(SP)                ;
        CLR.W   -(SP)                   ;        REPEAT
        MOVE.L  D3,-(SP)                ;        UNTIL GT<> COMPARE(PKEY,
        MOVE.L  D3,-(SP)                ;                   PKEY,
        MOVE.W  D4,-(SP)                ;                   NSFLDS,
        MOVE.L  (A1),-(SP)              ;                   --PJ^,
        MOVE.L  D5,-(SP)                ;                   PK)
        JSR     COMPARE                 ;
        CMPI.W  #GTVAL,(SP)+            ;
        MOVEA.L (SP)+,A1
        BEQ.S   SHIFTR                  ;

        CMPA.L  A4,A1                   ;        IF PI < PJ THEN PI^ <-> PJ^
        BLS.S   PARTEXT                 ;        ELSE BREAK
        MOVE.L  (A4),D0                 ;
        MOVE.L  (A1),(A4)               ;
        MOVE.L  D0,(A1)                 ;
        BRA.S   PARTING                 ;   UNTIL FALSE

PARTEXT MOVE.L  (A3),D0                 ;   PLOWER^ <-> PJ^
        MOVE.L  (A1),(A3)               ;
        MOVE.L  D0,(A1)                 ;

        MOVE.L  D2,A0                   ;   { GET STACK POINTER }

        MOVE.L  A1,A4                   ;   A4 := PJ + SIZEOF(PTR)
        ADDQ.L  #PTRSIZE,A4             ;
        SUBQ.L  #PTRSIZE,A1             ;   A1 := PJ - SIZEOF(PTR)

        MOVE.L  A1,D0                   ;   IS PLOWER,PJ-SIZEOF(PTR) <=1?
        SUB.L   A3,D0                   ;
        BHI.S   @1                      ;   IF SO SET INTERVAL TO 0
        CLR.L   D0                      ;

@1      MOVE.L  A2,D1                   ;   IS PJ+SIZEOF(PTR),PUPPER <=1?
        SUB.L   A4,D1                   ;
        BHI.S   @2                      ;   IF SO SET INTERVAL TO 0
        CLR.L   D1

@2      CMP.L   D0,D1                   ;   PUSH THE LONGER INTERVAL 1ST
        BHI.S   @4                      ;
                                        ;   PLOWER.. IS LONGER
        MOVE.L  A1,-(A0)                ;   PUSH PLOWER..
        MOVE.L  A3,-(A0)                ;
        TST.W   D1                      ;   IS .. PUPPER <= 1?
        BEQ     SORT                    ;
        MOVE.L  A2,-(A0)                ;   PUSH PUPPER..
        MOVE.L  A4,-(A0)                ;
        BRA.S   SORT                    ;

@4                                      ;   .. PUPPER IS LONGER
        MOVE.L  A2,-(A0)                ;   PUSH PUPPER..
        MOVE.L  A4,-(A0)                ;
        TST.W   D0                      ;   IS .. PLOWER <= 1?
        BEQ     SORT                    ;
        MOVE.L  A1,-(A0)                ;   PUSH PLOWER..
        MOVE.L  A3,-(A0)                ;
        BRA     SORT                    ;

        .PROC MOVERQ,10

;--------------------------------------------------------------------------
;
;       PROCEDURE MOVERQ(P1,P2 : PTRDATA;NBYTES : LONGINT);
;
;       DOES A FAST MOVERIGHT.  ASSUMES P1,P2 POINT TO WORD BOUNDARIES.
;       NBYTES MUST BE A MULTIPLE OF 4 AND >= 4.
;
;
;       USES
;               A0,A1
;               D0,D1

        MOVE.L  (SP)+,D1                ;POP RETURN ADDRESS
        MOVE.L  (SP)+,D0                ;D0 := NBYTES
        MOVE.L  (SP)+,A1                ;A1 := P2
        MOVE.L  (SP)+,A0                ;A0 := P1
        MOVE.L  D1,-(SP)                ;PUSH RETURN ADDRESS FOR RTS

        ADDA.L  D0,A0
        ADDA.L  D0,A1
        LSR.L   #2,D0
                           ;CONVERT D0 INTO LONG COUNT
XFER    MOVE.L  -(A0),-(A1)
        SUBQ.L  #1,D0
        BNE.S   XFER


RETURN  RTS


        .END


ÿ