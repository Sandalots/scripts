{#F libdb/lmopen.text }
{$S lmopen }
{**************************************************************************

                                setuprealfiles

        after diffopen has analyzed the query and sort and has chosen the
        on and via files and setup the high and low bounds keys,
        setuprealfiles positions the real files so that the solution set
        can be retrieved.

        Input Parameters

        scanid - diff scan

        Output Parameters

        lmresult - return result

        noerr
        nobuffs
        ioerr
        inconsistent


***************************************************************************}

procedure setuprealfiles(var lmresult : integer;
                             scanid : integer);

label
        1,10,20;

var
        realwhich,
        nsflds,
        which,
        viaid,
        offoff,
        lresult,
        buffer,
        index : integer;

        nextpage : fileptr;

        pkey,
        pb : ptrdata;
        po : ptroffsets;
        pd : ptrpagedesc;

        pvia : ptrfiledesc;
        pdesc,
        psdesc : ptrrecptr;


        procedure getnextpage;
        label
                1;
        begin
                relbuff(lresult,buffer,isleaf);
                if lresult <> noerr then goto 1;
                getpage(lresult,buffer,viaid,nextpage,true);
                if lresult <> noerr then goto 1;
                pb := buffpool^[buffer].pbuff;
                po := pointer(ord(pb)+offoff);
                pd := pointer(ord(po)+sizeof(integer));
        1 :
        end;
begin
        pstack_validate(lresult);
        if lresult <> noerr then goto 1;

    {   if reversescan is true then we want to scan the real file in the
        opposite direction to the way the differential file is being
        built }

        with pdifftable^[scanid]^ do
            begin
            if reversescan then
                begin
                if whichway = prior then realwhich := next
                else realwhich := prior
                end
            else realwhich := whichway;

            (*dbwriteln(dumpfile,ord(plow):14,ord(phigh):14);*)

        {   if we are scanning the real file from the low to the high
            (realwhich = next) then we either find first (if there is
            not low key or find approx on the low key and then fetch
            prior until we find a record less than the low key.  this
            is a good place to start the scan.

            we do the obvious analogue in the case of realwhich = prior }

            viaid := via;
            pvia := pfiletable^[viaid];
            if realwhich = prior then
                begin
                if nhflds = 0 then which := last
                else which := approx;
                pkey := phigh;
                nsflds := nhflds;
                end
            else
                begin
                if nlflds = 0 then which := first
                else which := approx;
                pkey := plow;
                nsflds := nlflds;
                end;

            pdesc := pvia^.pedesc;{##}
            if via = on then psdesc := pdesc{##}
            else psdesc := pvia^.pkdesc;{##}

            (*dbif lmdebug then
                begin
                writeln(dumpfile,'which,nsflds,viaid:',which:7,nsflds:7,
                        viaid:7);
                printkey(psdesc,nsflds,pkey);
                writeln(dumpfile,ord(pkey));
                end;*)

            find(lresult,which,nsflds,viaid,psdesc,pkey,buffer);
            if lresult = fileempty then
                begin
                whichway := noway;
                lresult := noerr;
                end
            else if lresult <= notfound then
                begin
                index := pstack^[pvia^.depth].index;
                if which = approx then
                    begin
                    offoff:=pvia^.pagesize-(sizeof(pagedesc)+sizeof(integer));
                    pb := buffpool^[buffer].pbuff;
                    po := pointer(ord(pb)+offoff);
                    pd := pointer(ord(po)+sizeof(integer));
                    if index >= pd^.nkeys then index := pd^.nkeys-1;
                    if realwhich = prior then
                        begin
                        while compare(pdesc,{##}
                                      psdesc,
                                      nsflds,
                                      pointer(ord(pb)+po^[-index]),
                                      pkey) <> gt do
                            begin
                            if (index + 1) = pd^.nkeys then
                                begin
                                nextpage := pd^.next;
                                if nextpage = nilptr then goto 10;
                                getnextpage;
                                if lresult <> noerr then goto 1;
                                index := 0;
                                end
                            else index := index + 1;
                            end;
                        10 :
                        end
                    else
                        begin
                        while compare(pdesc,{##}
                                      psdesc,
                                      nsflds,
                                      pointer(ord(pb)+po^[-index]),
                                      pkey) <> lt do
                            begin
                            if index  = 0 then
                                begin
                                nextpage := pd^.prior;
                                if nextpage = nilptr then goto 20;
                                getnextpage;
                                if lresult <> noerr then goto 1;
                                index := pd^.nkeys-1;
                                end
                            else index := index - 1;
                            end;
                        20 :
                        end;
                    end
                else if which = last then index := index-1;

                {  bias index for first nextprior in realnext }

                if realwhich = prior then vindex := index + 1
                else vindex := index - 1;
                vpage := buffpool^[buffer].page;
                relbuff(lresult,buffer,isleaf);
                end;
            end; { with }
1 :     lmresult := lresult;
end; { setuprealfiles }
{**********************************************************************

                              diffopen
        see interface

****************************************************************************}

procedure diffopen{(var lmresult : integer;
                        scanid : integer;
                        qwhichway : integer;
                        pexpr  : ptrexpr;
                        ntriples : integer;
                        pconst : ptrdata;
                        constsize : integer;
                        nskeys : integer;
                        psort : ptridesc;
                        tempdir1,
                        tempdir2 : ptrpathname;
                        temp_ldsn : integer)};
label
        1,100,200; {##}
var
        pold_approx,
        pnew_approx : ptrapproxkey;
        pnewquery : ptrquery;
        loc_compact_state : states_de_compact;
        sorted : boolean;
        ploclow,
        plochigh : ptrdata;
        cost : fileptr;
        dscan,
        high_flds,
        low_flds,
        lowsize,
        highsize,
        nflds,
        on_id,
        key_id,
        tresult,
        lresult,
        querysize,
        i : integer;
begin

{       1) if there is a query, then allocate space for the query and copy
           in the expression and record of constants needed for the query

        2) if there is a query, then

           a) call cost_out on every access path to determine the cheapest one
              (this also computes high and low key information and determines
              if we must sort)

           b) call key_sizes to compute the high and low key record sizes

           c) call fill_keys to extract the high and low key records from the
              constant record of the query.


           if there is no query then call cost_out on the data file to find
           out if we have to sort.

        3) call setuprealfiles to get every position to scan the access
           path we have chosen.

        4) if necessary, call sort_diff to build the sorted differential file. }

        with pdifftable^[scanid]^ do
            begin
            dscan := diffid;
            have_an_update := false;
            whichway := qwhichway;
            reversescan := false;

            {  by default we scan the whole on file }

            with pscantable^[realid]^ do
                begin
                on_id := onfile;
                on := on_id;
                via := viafile;
                end;
            end;

        nflds := pfiletable^[on_id]^.nfields;

        if ntriples <> 0 then
            begin

            loc_compact_state := init_compact_state;
            querysize:=constsize+sizeof(query)+(ntriples-1)*sizeof(triple);

        100 : { compaction retry place }

            lresult := nnew(@czone,querysize,@pnewquery);
            if lresult <> noerr then goto 200;

            lresult := nnew(@czone,nflds*2*sizeof(approxrec),@pnew_approx);
            if lresult <> noerr then
                begin
                ndispose(@czone,@pnewquery^);
                goto 200;
                end;

            pold_approx := @pnew_approx^[nflds];
            pnewquery^.ntriples := ntriples;
            movelf(@pexpr^,@pnewquery^.qexp,ntriples*sizeof(triple));
            movelf(pconst,@pnewquery^.qexp[ntriples],constsize);

{$IFC debug }
            if lmdebug then printquery(scanid,pnewquery);
{$ENDC}
            {   first we see if we can just use the primary key sort order }

            key_id := on_id;
            cost := nilptr;
            while key_id <> nilval do
                begin
                cost_out(scanid,pnewquery,pold_approx,pnew_approx,key_id,
                         nskeys,psort,low_flds,high_flds,cost,sorted);
                key_id := pfiletable^[key_id]^.pkeyfiles;
                end;

            (*dbif lmdebug then
                begin
                writeln(dumpfile,'cost,sorted:',cost:14,ord(sorted):3);
                with pdifftable^[scanid]^ do
                    begin
                    writeln(dumpfile,'via:',via);
                    writeln(dumpfile,'nhflds,nlflds:',high_flds:7,low_flds:7);
                    writeln(dumpfile,'high triples');
                    for i := 0 to high_flds-1 do
                        writeln(dumpfile,pold_approx^[i].high);
                    writeln(dumpfile,'low triples');
                    for i := 0 to low_flds-1 do
                        writeln(dumpfile,pold_approx^[i].low);
                    end;
                end;*)

          { here we allocate high & low search keys and fill them in using
            pold_approx and constants of query }

            key_sizes(scanid,pnewquery,pold_approx,
                      low_flds,high_flds,lowsize,highsize);

            with pdifftable^[scanid]^ do
                 begin
                 lresult := noerr;
                 if lowsize <> 0 then lresult:=nnew(@czone,lowsize,@ploclow);
                 if (highsize <> 0) and (lresult = noerr) then
                     begin
                     lresult := nnew(@czone,highsize,@plochigh);
                     if (lresult <> noerr) and (lowsize <> 0) then
                         ndispose(@czone,@ploclow^);
                     end;
                 if lresult <> noerr then
                     begin
                     ndispose(@czone,@pnew_approx^);
                     ndispose(@czone,@pnewquery^);
                     goto 200;
                     end;
                 end;

            fill_keys(scanid,pnewquery,pold_approx,
                      ploclow,plochigh,low_flds,high_flds);
            ndispose(@czone,@pnew_approx^);

    200:    if retry_compact(lresult,loc_compact_state) then goto 100;
            if lresult <> noerr then goto 1;

            with pdifftable^[scanid]^ do
                begin
                pdfilter := @pnewquery^;
                if low_flds <> 0 then plow := ploclow;
                if high_flds <> 0 then phigh := plochigh;
                nhflds := high_flds;
                nlflds := low_flds;
                end;
            end
        else { no query - check out whether we must sort }
            begin
            pdifftable^[scanid]^.pdfilter := nil;
            cost_out(scanid,nil,pold_approx,pnew_approx,on_id,
                     nskeys,psort,low_flds,high_flds,cost,sorted);
            end;


        setuprealfiles(lresult,scanid);
        if (not sorted) and (lresult = noerr) then
            begin
            sort_diff(lresult,scanid,set_sort,nskeys,psort,tempdir1,tempdir2,
                      temp_ldsn);
            if lresult <> noerr then
                begin
                diffTruncate(tresult,pscantable^[dscan]^.onfile);
                if tresult <> noerr then lresult := tresult;
                end;
            end;

        if lresult = noerr then checkptQuery(lresult,scanid,false); {##}

        if lresult <> noerr then
            with pdifftable^[scanid]^ do
                begin
                if ntriples <> 0 then
                    begin
                    ndispose(@czone,@pdfilter^);
                    pdfilter := nil;
                    end;
                if nlflds <> 0 then
                    begin
                    ndispose(@czone,@plow^);
                    plow := nil;
                    nlflds := 0;
                    end;
                if nhflds <> 0 then
                    begin
                    ndispose(@czone,@phigh^);
                    phigh := nil;
                    nhflds := 0;
                    end;
                end;
1 :     lmresult := lresult;
end; { diffopen }
{**********************************************************************

                              diffWADelete

        see interface

****************************************************************************}

procedure diffWADelete{(var lmresult : integer;
                                  scanid : integer;
                                  pexpr : ptrexpr;
                                  ntriples : integer;
                                  pconst : ptrdata;
                                  constsize : integer;
                                  temp_ldsn : integer)};
label
        1,2,3,10,60,70;
var
        pscan : ptrscandesc;
        pdscan : ptrdiffdesc;
        pdtable : ptrdata;
        psd : ^ ptrlongint;
        loc_compact_state : states_de_compact;
        psdesc : ptrrecptr;
        pcos : ptrdata;
        pon,
        pvia : ptrfiledesc;
        iscan,
        delScan,
        totalsize,
        on_id,
        via_id,
        tresult,
        lresult : integer;
begin
        (*clobbered;*)

{       0) clone a new diffscan, !!but setsize can be 0!!

        1) if there is a query, then allocate space for the query and copy
           in the expression and record of constants needed for the query

        2) if there is a query, then

           a) call cost_out on every access path to determine the cheapest one
              (this also computes high and low key information and determines
              if we must sort)

           b) call key_sizes to compute the high and low key record sizes

           c) call fill_keys to extract the high and low key records from the
              constant record of the query.


        3) call setuprealfiles to get every position to scan the access
           path we have chosen.

        4) retrieve and delete all qualifying records

        5) destroy cloned diffscan  }

        loc_compact_state := init_compact_state;

        repeat   { compact retry point }

            psd := @pdifftable^;
            for iscan := 0 to ndiffs-1 do
                begin
                if psd^ = nil then goto 60;
                psd := pointer(ord(psd)+sizeof(ptrdiffdesc));
                end;

        {   expand the difftable }

            lresult := nnew(@czone,(ndiffs+1)*sizeof(ptrdiffdesc),@pdtable);
            if lresult <> noerr then goto 70;
            movelf(@pdifftable^,pdtable,ndiffs*sizeof(ptrdiffdesc));
            ndispose(@czone,@pdifftable^);
            pdifftable := @pdtable^;
            pdifftable^[ndiffs] := nil;
            ndiffs := ndiffs + 1;

        60: lresult := nnew(@czone,sizeof(diffdesc),@pdscan);
            if lresult <> noerr then goto 70;
            with pdscan^ do
                begin
                on := nilval;
                via := nilval;
                curhandle.high := nilptr;
                curhandle.low := nilptr;
                pkfilter := nil;
                pdfilter := nil;
                phigh := nil;
                plow := nil;
                nhflds := 0;
                nlflds := 0;
                end;

        70 :   { continue }
        until not retry_compact(lresult,loc_compact_state);
        if lresult <> noerr then goto 1;

        pdifftable^[iscan] := pdscan;
        with pdifftable^[scanid]^ do
            begin
            pdscan^.realid := realid;
            pdscan^.diffid := diffid;
            end;

        diffopen(lresult,iscan,next,pexpr,ntriples,pconst,constsize,
                 0,nil,nil,nil,temp_ldsn);

        if lresult = noerr then
            begin
            with pdifftable^[iscan]^ do
                begin
                if whichway = noway then goto 2; { nothing to delete }
                on_id := on;
                via_id := via;
                if on_id = via_id then delScan := realid;
                end;

            if on_id <> via_id then { need to open scan via index }
                begin
                loc_compact_state := init_compact_state;

                repeat { loop on compaction and/or heap growth }
                    {  make the scan descriptor }

                    psd := @pscantable^;
                    for delScan := 0 to nscans-1 do
                        begin
                        if psd^ = nil then goto 10;
                        psd := pointer(ord(psd)+sizeof(ptrscandesc));
                        end;

                    {  expand scantable }

                    lresult := nnew(@czone,(nscans+1)*sizeof(ptrscandesc),
                                    @pdtable);
                    if lresult <> noerr then goto 3;
                    movelf(@pscantable^,pdtable,nscans*sizeof(ptrscandesc));
                    ndispose(@czone,@pscantable^);
                    pscantable := @pdtable^;
                    pscantable^[nscans] := nil;
                    nscans := nscans + 1;

            10:     pon := pfiletable^[on_id];
                    pvia := pfiletable^[via_id];
                    totalsize := sizeof(scandesc)-sizeof(bits) +
                                 wordsize*((pon^.nfields-1) div elemsize + 1);
                    lresult := nnew(@czone,totalsize,@pscan);
                    if lresult = nomem then goto 3;

                    with pscan^ do
                        begin
                        onfile := on_id;
                        viafile := via_id;
                        with pvia^ do
                            begin
                            writers := writers + 1;
                            intent := update;
                            fnext := scans;
                            scans := delScan;
                            end;
                        ptrcos := nil;
                        touched := touch_by_vltree;
                        position := first;
                        end;
                    pscantable^[delScan] := pscan;
                    pon^.writers := pon^.writers + 1;

{$IFC debug }
                    if lmdebug then
                        begin
                        writeln(dumpfile,'make WA Scan ',delScan);
                        dumpheap(@czone);
                        printcos(pscantable^[delScan]);
                        end;
{$ENDC }

            3 :     { continue }
                until not retry_compact(lresult,loc_compact_state);
                if lresult <> noerr then goto 2;
                end;

            loc_compact_state := init_compact_state;

            repeat { loop on compaction and/or heap growth }
                with pdifftable^[iscan]^ do
                    begin
                    if nlflds <> 0 then
                        begin
                        efetch(lresult,delScan,approx,nlflds,plow);
                        if lresult = notfound then
                            efetch(lresult,delScan,next,0,nil);
                        end
                    else
                        begin
                        efetch(lresult,delScan,first,0,nil);
                        if lresult <= eos then
                            efetch(lresult,delScan,next,0,nil);
                        end;
                    end;
            until not retry_compact(lresult,loc_compact_state);

            while lresult = noerr do
                begin
                with pdifftable^[iscan]^ do
                    begin
                    pcos := pscantable^[delScan]^.ptrcos;
                    if nhflds <> 0 then
                        begin
                        with pfiletable^[via_id]^ do
                            begin
                            if on_id <> via_id then psdesc := pkdesc
                            else psdesc := pedesc;
                            end;
                        if compare(psdesc,
                                   psdesc,
                                   nhflds,
                                   pcos,
                                   phigh) = gt then
                            lresult := eos;
                        end;
                    end;
                if lresult = noerr then {##}
                    if dfilter(iscan,pcos) then
                        edelete(lresult,delScan,current,0,nil);
                if lresult = noerr then
                    efetch(lresult,delScan,next,0,nil);
                end;

            if lresult = eos then lresult := noerr;
            if on_id <> via_id then
                begin
                closescan(tresult,delScan);
                if tresult <> noerr then lresult := tresult;
                end;
            end;

2 :     pdscan := pdifftable^[iscan];
        pdifftable^[iscan] := nil;
        with pdscan^ do
            begin
            if ntriples <> 0 then ndispose(@czone,@pdfilter^);
            if nlflds <> 0 then ndispose(@czone,@plow^);
            if nhflds <> 0 then ndispose(@czone,@phigh^);
            end;
        ndispose(@czone,@pdscan^);

1 :     lmresult := lresult;
end; { diffWADelete }



ÿ