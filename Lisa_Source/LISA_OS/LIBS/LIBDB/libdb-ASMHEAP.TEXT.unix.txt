        .SEG    "access"
;zonedesc =
;   record
;       avail : heapptr;
;       heapmin,
;       heapmax : ptr;
;       lastdiff : integer;
;       availsize : integer;
;   end;
avail           .equ    0
heapmin         .equ    4
heapmax         .equ    8
lastdiff        .equ   12
availsize       .equ   14

;heapblk =
;   record case boolean of
;   true : (
;       size : integer;
;       prep : heapptr;
;       next : heapptr);
;   false: (
;       blk : array[0..0] of -128..127);
;   end;

size            .equ    0
prep            .equ    2
next            .equ    6
blk             .equ    0

lhead           .equ    2               ;sizeof header for allocated block
                                        ;and footer for free and allocated
lhead2          .equ    4               ;2 * lhead
uhead           .equ    10              ;sizeof header for free block
freeover        .equ    12              ;total overhead for free block
allocover       .equ    8               ;total overhead for allocated block

noerr           .equ    0               ;okay return status
nomem           .equ    3411            ;error return status

                .func   nnew,10
                .def    ndispose,init_heap,grow_heap

;---------------------------------------------------------------------------
;
;       function nnew(pzone : ptrzonedesc;
;                     size  : integer;
;                     pdataptr : ptrptr) : integer;
;
;
;       HEAP MANAGER.  Allocates a block of a requested size.  The size
;       of the object is passed in the "size" parameter.  The routine uses a
;       first fit method for the block allocation.  The routine returns a
;       pointer into the heap, "pdata", which points to the start of the
;       usable data area.  This displacement is set to "nil" if the
;       block can not be allocated.

;       The requested block size is automatically adjusted to take into
;       account the overhead involved with heap information.  The additional
;       heap information is:
;           size : the size of the current heap block ( < 0 if avialable )
;           prep : pointer to previous heap block
;           next : pointer to next heap block
;       The prep/next pointers form a doubly linked list.  No block smaller
;       than that big enough to to store the heap information for an unused
;       block is allocated.  If too small of a block is asked for, then
;       the size to rounded up.

;       An available block will have the above information at the top
;       of the block.  The bottom of the block will only have the size
;       information.  A used block will have
;       only the size information at both the top and bottom of the block
;       (no prep/next pointers).  A used block will have sizes that are positive
;       while an available block will have negative sizes.
;
;
;       Uses
;               a0,a1,a2,a3,a4
;               d0,d1,d2,d3
;       Saves
;               a2-a4
;               d2-d3

        move.l  (sp)+,a1                ;save return address
        move.l  (sp)+,d0                ;d0 := pdataptr
        move.w  (sp)+,d1                ;d1 := reqsize
        move.l  (sp)+,a0                ;a0 := pzone
        move.l  a1,-(sp)                ;push return address
        movem.l d2-d3/a2-a4,-(sp)       ;save registers
        moveq   #freeover,d2            ;d2 := free overhead

        btst    #0,d1                   ;make reqsize even
        beq.s   @1
        addq.w  #1,d1

@1      add.w   #allocover,d1           ;add allocate overhead to reqsize
        cmp.w   d2,d1                   ;make sure reqsize >= free overhead
        bge.s   @2
        move.w  d2,d1
@2      move.w  d1,d3                   ;pzone^.lastdiff := reqsize -
        sub.w   availsize(a0),d3        ;                   pzone^.availsize
        move.w  d3,lastdiff(a0)
        bgt     noblock                 ;if pzone^.lastdiff > 0 then exit

        neg.w   d2                      ;d2 := -free overhead

        clr.l   d3
        move.l  d3,a2                   ;pprevcell := nil
        move.l  avail(a0),a1            ;pcurcell := pzone^.avail

        move.l  a1,d3                   ;while pcurcell <> nil do
        beq.s   noblock                 ;

findblk move.w  size(a1),d3             ;  remainder := psize+pcurcell^.size
        add.w   d1,d3                   ;
        cmp.w   d3,d2                   ;  if remainder <= free overhead then
        blt.s   nofree                  ;    ptemp := @pcurcell^.blk[reqsize]

        move.l  a1,a3
        adda.w  d1,a3
        tst.l   prep(a1)                ;    if prep = nil then
        bne.s   @1                      ;      pzone^.avail := ptemp
        move.l  a3,avail(a0)
        bra.s   @2                      ;    else
@1      move.l  prep(a1),a4             ;      pcurcell^.prep^.next := ptemp
        move.l  a3,next(a4)
@2      tst.l   next(a1)                ;    if pcurcell^.next <> nil then
        beq.s   @3                      ;      pcurcell^.next^.prep := ptemp
        move.l  next(a1),a4             ;
        move.l  a3,prep(a4)
@3      move.w  d3,size(a3)             ;    ptemp^.size := remainder
        move.l  next(a1),next(a3)       ;    ptemp^.next := pcurcell^.next
        move.l  prep(a1),prep(a3)       ;    ptemp^.prep := pcurcell^.prep
        suba.w  d3,a3
        move.w  d3,-lhead(a3)           ;    size at end of ptemp := remainder
        bra.s   gotblock                ;    successful exit

nofree  tst.w   d3                      ;  else if remainder <= 0 then
        bgt.s   noluck                  ;  { no free block left }

        sub.w   d3,d1                   ;    reqsize :=- remainder
        move.l  a2,d3                   ;    if pprevcell = nil then
        bne.s   @1                      ;
        move.l  next(a1),d3             ;       pzone^.avail := pcurcell^.next
        move.l  d3,avail(a0)            ;       if pzone^.avail <> 0 then
        beq.s   gotblock                ;         pzone^.avail^.prep := nil
        move.l  d3,a3                   ;
        clr.l   prep(a3)
        bra.s   gotblock

@1      move.l  next(a1),d3             ;    else
        move.l  d3,next(a2)             ;       pprevcell^.next := pcurcell^.next
        beq.s   gotblock                ;       if pcurcell^.next <> nil then
        move.l  d3,a3                   ;         pprevcell^.next^.prep :=
        move.l  prep(a1),prep(a3)       ;           pcurcell^.prep
        bra.s   gotblock

noluck  move.l  a1,a2                   ;   else
        move.l  next(a1),a1             ;     pprevcell := pcurcell
        move.l  a1,d3                   ;     pcurcell := pcurcell^.next
        bne.s   findblk

noblock                                 ;error exit - no suitable block

        movem.l (sp)+,d2-d3/a2-a4
        move.w  #nomem,4(sp)            ;nnew := nomem
        rts

gotblock                                ;successfull exit
        sub.w   d1,availsize(a0)        ;pzone^.availsize :=- reqsize
        move.w  d1,size(a1)             ;pcurcell^.size := reqsize
        move.l  a1,a0                   ;size at end of pcurcell := reqsize
        move.w  d1,-lhead(a0,d1)
        move.l  d0,a0                   ;pdataptr^ := pcurcell+lhead
        addq.l  #lhead,a1
        move.l  a1,(a0)
        movem.l (sp)+,d2-d3/a2-a4
        move.w  #noerr,4(sp)            ;nnew := noerr
        rts


;--------------------------------------------------------------------------------
;
;       procedure ndispose(pzone : ptrzonedesc;
;                          pdata : ptr)
;
;
;       dispose routine.  This uses the boundary taging technique to do data
;       compaction.  The boundary tags consists of the size information
;       stored as part of the heap overhead.  The compaction takes place
;       if two adjacent block are on the available list (thus begining
;       compacted into one large block).  Ndispose is passed a ptr to the
;       data part of the block,  this pointer must be relocated to point to
;       the size information at the start of the block.
;
;
;       Uses
;               a0,a1,a2
;               d0,d1
;       Saves
;               a2
;
;

;---------------------------------------------------
;
;       local procedure linkin
;            pcell : pheapptr in a1
;
;       links pcell into available list
;
;

linkin  clr.l   prep(a1)                ;pcell^.prep := nil
        move.l  avail(a0),next(a1)      ;pcell^.next := pzone^.avail
        tst.w   size(a1)                ;pcell^.size := - abs(pcell^.size)
        blt.s   @1                      ;
        neg.w   size(a1)
@1      tst.l   avail(a0)               ;if pzone^.avail <> nil then
        beq.s   @2
        move.l  avail(a0),a2            ;  pzone^.avail^.prep := pcell
        move.l  a1,prep(a2)
@2      move.l  a1,avail(a0)            ;pzone^.avail := pcell
        move.l  a1,a2                   ;size of end of pcell := pcell^.size
        suba.w  size(a1),a2
        move.w  size(a1),-lhead(a2)
        rts

ndispose
        move.l  (sp)+,d0                ;pop return address
        move.l  (sp)+,a1                ;pop pdata
        move.l  (sp)+,a0                ;pop pzone
        move.l  d0,-(sp)                ;push return address

quickdispose                            ;quickie entry for growheap

        move.l  a2,d1                   ;save a2

        subq.l  #lhead,a1               ;set pcell to real start of block
        move.w  size(a1),d0             ;pzone^.availsize :=+ block size
        add.w   d0,availsize(a0)
        tst.l   avail(a0)               ;if pzone^.avail = nil then
        bne.s   @1                      ;
        bsr     linkin                  ;  linkin(pcell)
        bra.s   exitndispose            ;

@1                                      ;else
        cmpa.l  heapmin(a0),a1          ;  if pcell > pzone^.heapmin then
        bls.s   dolink                  ;     if size of prev block < 0 then
        tst.w   -lhead(a1)              ;       pneighbor := pcell+
        bge.s   dolink                  ;          size of prev block
        move.l  a1,a2                   ;
        adda.w  -lhead(a1),a2           ;
        move.w  size(a1),d0             ;
        sub.w   d0,size(a2)             ;       pneighbor^.size :=- pcell^.size
        adda.w  size(a1),a1             ;       set size (at end of old block)
        move.w  size(a2),-lhead(a1)     ;         to size of merged block
        move.l  a2,a1                   ;       pcell := pneighbor
        bra.s   follow                  ;    else linkin(pcell)
dolink  bsr     linkin                  ;  else linkin(pcell)

follow  move.l  a1,a2                   ;if lastword of pcell = pzone^.heapmax
        suba.w  size(a1),a2             ;  then
        subq.l  #lhead,a2
        cmpa.l  heapmax(a0),a2          ;     pneighbor := pcell - pcell^.size
        beq.s   exitndispose
        addq.l  #lhead,a2
        tst.w   size(a2)                ;     if pneighbor^.size < 0 then
        bge.s   exitndispose
        tst.l   prep(a2)                ;     if pneighbor^.prep <> nil then
        beq.s   @1                      ;       pneighbor^.prep^.next :=
        move.l  prep(a2),a0             ;           pneighbor^.next
        move.l  next(a2),next(a0)
        bra.s   @2                      ;     else
@1      move.l  next(a2),avail(a0)      ;       pzone^.avail:=pneighbor^.next
@2      tst.l   next(a2)                ;     if pneighbor^.next <> nil then
        beq.s   @3
        move.l  next(a2),a0             ;       pneighbor^.next^.prep :=
        move.l  prep(a2),prep(a0)       ;         pneighbor^.prep
@3      move.w  size(a2),d0             ;     pcell^.size :=+ pneighbor^.size
        add.w   d0,size(a1)             ;
        move.l  a1,a0                   ;     size at end of whole mess :=
        suba.w  size(a1),a0             ;       new size
        move.w  size(a1),-lhead(a0)

exitndispose

        move.l  d1,a2                   ;restore a2
        rts


;--------------------------------------------------------------------------
;
;       procedure init_heap(pzone : ptrzonedesc;
;                          initsize : integer;
;                          where : ptr);
;
;       initializes a heap to have one big free block
;
;
;       Uses
;               a0,a1
;               d0,d1
;       Saves
;
;

init_heap
        move.l  (sp)+,d0                ;return address
        move.l  (sp)+,a1                ;a1 := where
        move.w  (sp)+,d1                ;d1 := initsize
        move.l  (sp)+,a0                ;a0 := pzone

        move.w  d1,availsize(a0)        ;pzone^.availsize := initsize
        neg.w   d1                      ;d1 := -initsize
        move.l  a1,avail(a0)            ;pzone^.avail := where
        move.l  a1,heapmin(a0)          ;pzone^.heapmin := where
        move.w  d1,size(a1)             ;pzone^.avail^.size := -initsize
        clr.l   prep(a1)                ;pzone^.avail^.prep := nil
        clr.l   next(a1)                ;pzone^.avail^.next := nil
        suba.w  d1,a1                   ;pzone^.heapmax := where+initsize-
        subq.l  #lhead,a1               ;                  lhead
        move.l  a1,heapmax(a0)          ;
        move.w  d1,size(a1)             ;set lower size of block

        move.l  d0,a0                   ;return
        jmp     (a0)

;--------------------------------------------------------------------------
;
;       function grow_heap(pzone : ptrzonedesc;
;                         growincr : integer) : integer;
;
;       adds growincr bytes to end of heap pzone. if growincr < 0 then
;       it shrinks the heap by growincr bytes. returns noerr if all went
;       well, and nomem if it couldn't do the shrink. before a shrink
;       the heap must be compacted and must contain at least one allocated
;       block!!!!
;
;
;       Uses
;               a0,a1
;               d0,d1
;       Saves
;
;

grow_heap
        move.l  (sp)+,a1                ;save return address
        move.w  (sp)+,d0                ;d0 := growincr
        move.l  (sp)+,a0                ;a0 := pzone
        move.l  a1,-(sp)                ;stash return address

        tst.w   d0                      ;if growincr >= 0  then
        blt.s   shrink                  ;

        move.l  heapmax(a0),a1          ;  ptemp := pzone^.heapmax
        move.l  a1,d1                   ;  savemax := pzone^.heapmax
        move.w  d0,lhead(a1)            ;  *(ptemp+lhead) := growincr
        adda.w  d0,a1                   ;  pzone^.heapmax :=+ growincr
        move.l  a1,heapmax(a0)          ;
        move.w  d0,size(a1)             ;  pzone^.heapmax^.size := growincr

        move.l  d1,a1                   ;  ptemp := savemax + 2*lhead
        addq.l  #lhead2,a1              ;

        bsr     quickdispose            ;  ndispose(pzone,ptemp)
        move.w  #noerr,4(sp)            ;  nnew := noerr
        rts                             ;  exit

shrink  move.l  avail(a0),d1            ;else
        beq.s   badnews                 ;  if pzone^.avail = nil then error
        move.l  d1,a1                   ;  if pzone^.avail^.size>growincr then
        cmp.w   size(a1),d0             ;      error
        bge.s   okay                    ;

badnews move.w  #nomem,4(sp)            ;      (error exit)
        rts

okay    move.w  size(a1),d1             ;  else
        sub.w   d0,d1                   ;    pzone^.avail^.size :=- growincr

        cmp.w   #-freeover,d1           ;    if pzone^.avail^.size > -freeover
                                        ;    then
        ble.s   @1
        cmpa.l  heapmin(a0),a1          ;      if only avail in heap error
        beq.s   badnews                 ;
        clr.l   avail(a0)               ;      pzone^.avail := nil
        sub.w   -lhead(a1),a1           ;      tack dribble onto previous block
        sub.w   d1,size(a1)             ;      and reset sizes
        add.w   d1,d0                   ;      availsize is lessened by dribble
        move.w  size(a1),d1             ;
        add.w   d1,a1
        bra.s   @2
                                        ;    else
@1      move.w  d1,size(a1)             ;      reduce size of avail block and
        sub.w   d1,a1                   ;      set heapmax to end of block

@2      subq.l  #lhead,a1               ;
        move.w  d1,size(a1)             ;
        move.l  a1,heapmax(a0)          ;

        add.w   d0,availsize(a0)        ;    pzone^.availsize :=+ growincr
        move.w  #noerr,4(sp)
        rts


        .end
ÿ