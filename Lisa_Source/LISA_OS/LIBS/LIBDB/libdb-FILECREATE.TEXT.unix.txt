{#f     libdb/filecreate.text        }
{$S swapaccs }

{^T20}
{**********************************************************************

                               makefile

        see interface.

**********************************************************************}

{$S swapaccs }
procedure makefile{(var ddresult : integer;
                        pname : ptrpathname;
                        pfdesc : ptrfiledesc)};
label
        1,2,3,4;
var
        F_info : Fs_info;
        pbuffer,
        pd,
        prec : ptrdata;
        pleaf : ptrpagedesc;
        writeat,
        writesize,
        actual : fileptr;
        xsize,
        edescsize,
        buffer,
        lresult,
        tresult : integer;
begin

{$IFC fTraceDB}    LogCall;     {$ENDC}
    {   1) create the file
        2) open it
        3) get the uniqueid of the file and put it in the filelabel
        4) copy in the file label and the recptr for the file from
           pfdesc }

        lockbuff(lresult,buffer);
        if lresult = noerr then
            begin
            with pfdesc^ do
                begin
                prefix_name(pname);

{af}            make_secure(lresult,prefix,password);
               {make_file(lresult,prefix,0);}
                if lresult <> noerr then goto 2; { error exit - nofile }

{af}            open_secure(lresult,prefix,frefno,
                            [DRead,Dwrite,Private,Global_RefNum],password);

{!8-15-83       open(lresult,prefix,frefno,[DRead,Dwrite,Private,Global_RefNum]);}
                if lresult > noerr then goto 3; { error exit }

                info(lresult,frefno,F_info);
                if lresult <> noerr then goto 4;
                funiqueid := F_info.fuid;

                edescsize := nfields * sizeof(flddesc);
                firstpage := ((sizeof(filelabel)+edescsize+511) div 512 ) * 512;

                setbit(@flags,ffileok);
                setbit(@flags,fisbtree);
                nrecs := 0;
                depth := 1;

                root := nilptr;

            {#O later - have to account for allocating whole file at create }

                lastpage := firstpage;
                freelist := nilptr;
                labellist := nilptr;
                labeltickets := 0;
                tickets.low := 1;
                tickets.high := 0;

            {   set fields used to check if it's a data base file }

                check_1_it := leaftype;
                check_2_it := nonleaftype;

                pbuffer := buffpool^[buffer].pbuff;
                movelf(@pfdesc^,pbuffer,sizeof(filelabel));
                xsize := psize - sizeof(filelabel);
                if xsize > edescsize then xsize := edescsize;
                pd := @pedesc^;
                movelf(pd,@pbuffer^[sizeof(filelabel)],xsize);
                writeat := 0;
                repeat
                    pd := @pd^[xsize];
                    writesize := firstpage - writeat;
                    if writesize > psize then writesize := psize;
                    write_data(lresult,frefno,ord(pbuffer),writesize,
                               actual,absolute,writeat);
                    writeat := writeat + writesize;
                    if lresult <> noerr then
                        begin
                        if no_room(lresult) then lresult := noroom
                        else lresult := ioerr;
                        goto 4;
                        end;

                    edescsize := edescsize - xsize;
                    if edescsize <> 0 then
                        begin
                        if psize > edescsize then xsize := edescsize
                        else xsize := psize;
                        movelf(pd,pbuffer,xsize);
                        end;
                until edescsize = 0;;


           {#O  later - allocate to lastpage  }

                relbuff(lresult,buffer,0);
                if lresult <> noerr then
                    begin
                    close_object(tresult,frefno);
{af}                kill_secure(tresult,prefix,password);
                   {kill_object(tresult,prefix);}
                    end;
                goto 1;


    4 : { ioerr exit }

                close_object(tresult,frefno);

    3 :   { error exit - have file  }

{af}            kill_secure(tresult,prefix,password);
               {kill_object(tresult,prefix);}

    2 :   { exit : no file }

                relbuff(tresult,buffer,0);
                if tresult <> noerr then lresult := tresult;

                end; { with }
            end; { if }

1 :     ddresult := lresult;

{$IFC debug }
        if dbdebug then writeln('mk:',ddresult);
{$ENDC debug }
end; { makefile }
{****************************************************************************

                            schemadelete

        deletes the fileschema associated with entry enum.  only alters
        ddresult if an error was encountered.

        input parameters

        scanid - label scan where schema is
        enum - entry for schema

        output parameters

        ddresult - on error, returns scan error code here

****************************************************************************}
{$S swapaccs }
procedure schemadelete(var ddresult : integer;
                           scanid : integer;
                           enum : fileptr);
var
        plab : ptrlabelentry;
        loclab : labelentry;
        tresult : integer;
begin

{$IFC fTraceDB}    LogCall;     {$ENDC}
        loclab.ltype := fileschema;
        loclab.enum := enum;
        lfetch(tresult,scanid,approx,2,@loclab);
        while tresult = noerr do
            begin
            plab := @pscantable^[scanid]^.ptrcos^;

{$IFC debug }
            if dbdebug then
                writeln(dumpfile,'enum ',plab^.enum);
{$ENDC debug }

            if (plab^.enum = enum) and (plab^.ltype = fileschema) then
                ldelete(tresult,scanid,current,0,nil)
            else tresult := eos;
            if tresult = noerr then lfetch(tresult,scanid,next,0,nil);
            end;
        if tresult > eos then ddresult := tresult;
end;
{^T20}
{***************************************************************************

                                clonefile

        see interface.


**********************************************************************}

{$S swapaccs }
procedure clonefile{(var ddresult : integer;
                        sourcefile,
                        filename : ptrpathname;
                        needtomakefile : boolean)};
label
        1,
        2,
        3,
        4,
        5,
        10;
var
        Has_Schema,
        closeIt : boolean;
        actual,
        readsize,
        firstpage,
        readat,
        newenum,
        oldenum : fileptr;
        pf : ^ filelabel;
        pb : ptrdata;
        plab,
        pnew : ptrlabelentry;
        lsize,
        lresult,
        tresult,
        sref,
        copybuff,
        newid,
        sid : integer;
begin
{$IFC fTraceDB}    LogCall;     {$ENDC}
        closeIt := false;
        has_schema := false;

        lockbuff(lresult,copybuff);
        if lresult <> noerr then goto 1;
        pb := buffpool^[copybuff].pbuff;
        openscan(lresult,sourcefile,sourcefile,sid,labelreadonly);
        if lresult <> noerr then goto 2;

        if needtomakefile then
            begin
            sref := pfiletable^[pscantable^[sid]^.onfile]^.frefno;
            read_data(lresult,sref,ord(pb),sizeof(filelabel),actual,
                      absolute,0);
            if lresult <> noerr then goto 3;

            prefix_name(filename);
{af}        make_secure(lresult,prefix,password);
           {make_file(lresult,prefix,0);}
            if lresult <> noerr then goto 3; { error exit - nofile }

{af}        open_secure(lresult,prefix,newid,[DRead,Dwrite,Private],password);
           {open(lresult,prefix,newid,[DRead,Dwrite,Private]);}
            if lresult > noerr then goto 4; { error exit }
            closeIt := true;

            pf := @pb^;
            firstpage := pf^.firstpage;
            readat := 0;

    {       copy over the low level description first, but stomp on the
            filelabel so that we have a empty file  }

            repeat
                readsize := firstpage - readat;
                if readsize > psize then readsize := psize;
                read_data(lresult,sref,ord(pb),readsize,actual,absolute,0);
                if lresult <> noerr then goto 4;
                if readat = 0 then
                    with pf^ do
                        begin
                        depth := 1;
                        lastpage := firstpage;
                        freelist := nilptr;
                        labellist := nilptr;
                        labeltickets := 0;
                        tickets.low := 1;
                        tickets.high := 0;
                        nrecs := 0;
                        root := nilptr;
                        end;
                write_data(lresult,newid,ord(pb),readsize,actual,absolute,0);
                if lresult <> noerr then
                    begin
                    if no_room(lresult) then lresult := noroom
                    else lresult := ioerr;
                    goto 4;
                    end;
                readat := readat + readsize;
            until readat = firstpage;
            close_object(lresult,newid);
            closeIt := false;
            if lresult <> noerr then goto 4;
            end;

{       find out if there is a schema to copy over }

        pnew := @pb^;
        pnew^.ltype := fileschema;
        lfetch(lresult,sid,approx,1,pnew);
        if lresult <> noerr then
            begin
            if lresult <= eos then lresult := noerr;
            goto 4;
            end;
        with pscantable^[sid]^ do
            begin
            plab := @ptrcos^;
            lsize := cossize;
            end;
        movelf(@plab^,@pnew^,lsize);

{       now open the new file and copy over the schema }

        openscan(lresult,filename,filename,newid,labelupdate);
        if lresult <> noerr then goto 4;

        lfetch(lresult,newid,approx,1,pnew);
        if lresult = noerr then
            begin
            has_schema := true;
            plab := @pscantable^[newid]^.ptrcos^;
            oldenum := plab^.enum;
            end
        else
            begin
            has_schema := false;
            if lresult > eos then goto 5;
            end;

        linsert(lresult,newid,pnew,lsize,true);
        if lresult <> noerr then goto 5;
        newenum := pnew^.enum;

        repeat
            lfetch(lresult,sid,next,0,nil);
            if lresult = noerr then
                begin
                with pscantable^[sid]^ do
                    begin
                    plab := @ptrcos^;
                    lsize := cossize;
                    end;
                if plab^.ltype <> fileschema then goto 10;
                end
            else
                begin
                if lresult = eos then lresult := noerr;
                goto 10;
                end;
            movelf(@plab^,@pnew^,lsize);
            pnew^.enum := newenum;
            linsert(lresult,newid,pnew,lsize,false);
        until lresult <> noerr;
    10: if lresult <> noerr then
            begin
            schemadelete(lresult,newid,newenum);
            goto 5;
            end;

{  successfull insert of new form - delete the old one }

        if has_schema then schemadelete(lresult,newid,oldenum);

5:      closescan(tresult,newid);
        if tresult <> noerr then lresult := tresult;
4:      if closeIt then
            begin
            close_object(tresult,newid);
            if tresult <> noerr then lresult := tresult;
            end;
        if lresult <> noerr then
            begin
            prefix_name(filename);
{af}        kill_secure(tresult,prefix,password);
           {kill_object(tresult,prefix);}
            end;
3:      closescan(tresult,sid);
        if tresult <> noerr then lresult := tresult;
2:      relbuff(tresult,copybuff,0);
        if tresult <> noerr then lresult := tresult;
1:      ddresult := lresult;
end; { clonefile }
{^T20}
{***************************************************************************

                                makedescfile

        see interface.


**********************************************************************}

{$S swapaccs }
procedure makedescfile{(var ddresult : integer;
                           pname : ptrpathname;
                           pfdesc : ptrfiledesc)};
var
        tresult : integer;
begin
{$IFC fTraceDB}    LogCall;     {$ENDC}
        with pfdesc^ do
            begin
            pagesize := psize;
            DBVers := myprod_vers;
            updateversion := 0;
            setbit(@flags,fdata);
            if vflds > maxrsize then ddresult := rectoobig
            else
                begin
                makefile(ddresult,pname,pfdesc);
                if ddresult = noerr then
                    begin
                    close_object(ddresult,frefno);
{af}                if ddresult <> noerr then kill_secure(tresult,prefix,password);
                   {if ddresult <> noerr then kill_object(tresult,prefix);}
                    end;
                end;
            end;

end; { makedescfile }


ÿ