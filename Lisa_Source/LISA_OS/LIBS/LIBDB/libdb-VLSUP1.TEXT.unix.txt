{#f     libdb/vlsup1.text }
{$S access }

{**************************************************************************

                            rotatepoint

        Computes the new break point for the virtual page composed of the
        concatenation of leftbuff,rightbuff and the inserted record.
        recomputes the insertion index in virtual coordinates.

        returns -1 if there exists no break point such that both pages are
        not overfull.

        Input Parameters

        leftbuff - left page buffer
        rightbuff - right page buffer
        insertbuff - the buffer in which the insert will go
        index -index in insertbuff where record will be inserted
        size - size of record to insert

        Output Parameters

        index in virtual page (concatenation of left & right pages) for insert
        return result - last record # for left page or -1 if we fail

***************************************************************************}


{$S access }
function rotatepoint(    leftbuff,
                         rightbuff,
                         insertbuff : integer;
                     var index : integer;
                         size  : integer;
                         pagesize : integer) : integer;
var
        i,
        Lnkeys,
        Rnkeys,
        vindex,
        leftsize,
        rightsize : integer;

        poR,poL : ptroffsets;
        pdR,pdL : ptrpagedesc;

{***************************************************************************

                                csizeof

        computes the size of entry i in the hypothetical complete page.
        this requires simulating the insertion of record at index.

        note:
                assert(0 <= i <= nkeys)

***************************************************************************}

{$S access }
function csizeof(i : integer) : integer;
begin
{$IFC fTraceDB}    LogCall;     {$ENDC}
        if i < vindex then
            begin
            if i < Lnkeys then csizeof := poL^[-i-1] - poL^[-i]
            else csizeof := poR^[-i+Lnkeys-1] - poR^[-i+Lnkeys];
            end
        else if i = vindex then csizeof := size
        else
            begin
            i := i - 1;
            if i < Lnkeys then csizeof := poL^[-i-1] - poL^[-i]
            else csizeof := poR^[-i+Lnkeys-1] - poR^[-i+Lnkeys];
            end;
end; { csizeof }


{$S access }
begin
{$IFC fTraceDB}    LogCall;     {$ENDC}
        pdR:=pointer(ord(buffpool^[rightbuff].pbuff)+pagesize-sizeof(pagedesc));
        poR := pointer(ord(pdR)-sizeof(integer));
        pdL:=pointer(ord(buffpool^[leftbuff].pbuff)+pagesize-sizeof(pagedesc));
        poL := pointer(ord(pdL)-sizeof(integer));
        Lnkeys := pdL^.nkeys;
        Rnkeys := pdR^.nkeys;
        i := Lnkeys-1;
        leftsize := poL^[-Lnkeys];
        rightsize := leftsize + poR^[-Rnkeys]+size;
        if insertbuff = rightbuff then vindex := index + Lnkeys
        else
            begin
            vindex := index;
            i := i + 1;
            leftsize := leftsize + size;
            end;
        rightsize := rightsize-leftsize;
        i := solomon(leftsize,rightsize,i,csizeof);
        pagesize := pagesize - sizeof(pagedesc);
        if (((i+2)*2 + leftsize) > pagesize) or
           (((Lnkeys+Rnkeys-i+1)*2 + rightsize) > pagesize) then i := -1;
        rotatepoint := i;
        if i >= 0 then index := vindex;

{$IFC debug }
        if bdebug then writeln('rotatepoint ',i,index);
{$ENDC }
end;
{**************************************************************************

                                ManyRotates

        Rotates records either left or right so that rotPt is the last
        record in leftbuff.  Marks the buffers as dirty afterwards.  Returns
        the rotPt that would be used to undo the rotate.  can be called
        even when no rotate is necessary.

        Input Parameters

        leftbuff - left page buffer
        rightbuff - right page buffer
        rotPt - last record # for left page

        Output Parameters

        ManyRotates - the rotPt that, if passed to ManyRotates, will undo
            the rotate

***************************************************************************}


{$S access }
function ManyRotates(leftbuff,
                     rightbuff : integer;
                     rotPt : integer;
                     descoff : integer) : integer;
var
        Lnkeys,
        Rnkeys,
        nRnkeys,
        rotSize,
        nRotate : integer;

        pRO,
        pLO,
        PLRO : ^ integer;

        pbdR,pbdL : ^ buffdesc;
        pbR,pbL : ptrdata;
        poR,poL : ptroffsets;
        pdR,pdL : ptrpagedesc;
begin
{$IFC fTraceDB}    LogCall;     {$ENDC}
{$IFC debug }
        if bdebug then
            writeln(dumpfile,'manyrots ',leftbuff,rightbuff,rotPt,descoff);
{$ENDC }

        pbdR := @buffpool^[rightbuff];
        pbR := pbdR^.pbuff;
        pdR := pointer(ord(pbR)+descoff);
        poR := pointer(ord(pdR)-sizeof(integer));
        pbdL := @buffpool^[leftbuff];
        pbL := pbdL^.pbuff;
        pdL:= pointer(ord(pbL)+descoff);
        poL := pointer(ord(pdL)-sizeof(integer));
        Lnkeys := pdL^.nkeys;
        ManyRotates := Lnkeys -1;
        Rnkeys := pdR^.nkeys;
        nRnkeys := Rnkeys+Lnkeys-rotPt-1;
        nRotate := Rnkeys - nRnkeys;

        pRO := @poR^[-Rnkeys];
        pLO := @poL^[-LnKeys];
        pLRO := @poL^[-Lnkeys-nRotate];

        if nRotate > 0 then { rotate right to left }
            begin
            rotSize := poR^[-nRotate];

{$IFC debug }
            if bdebug then
                writeln(dumpfile,'rtol:rotSize, nrotate ',rotSize,nRotate);
{$ENDC }

            movelf(pbR,pointer(ord(pbL)+pLO^),rotSize);
            movr_inc(pointer(ord(poR)-sizeof(integer)),
                     pointer(ord(pLO)-sizeof(integer)),
                     nRotate-1,
                     pLO^);
            movelf(pointer(ord(pbR)+rotSize),pbr,pRO^-rotSize);
            movr_inc(@poR^[-nRotate],poR,nRnkeys,-rotSize);
            end
         else if nRotate < 0 then { rotate left to right }
            begin
            nRotate := -nRotate;
            rotSize := pLO^-pLRO^;

{$IFC debug }
            if bdebug then
                writeln(dumpfile,'ltor:rotSize, nrotate ',rotSize,nRotate);
{$ENDC }
            moverf(pbR,pointer(ord(pbR)+rotSize),pRO^);
            movl_inc(@pRO^,@poR^[-nRnkeys],Rnkeys,rotSize);
            movelf(pointer(ord(pbL)+pLRO^),pbR,rotSize);
            movr_inc(@pLRO^,poR,nRotate-1,-pLRO^);
            end;
        pdR^.nkeys := nRnkeys;
        pdL^.nkeys := rotPt + 1;
        pbdR^.state := dirty;
        pbdL^.state := dirty;
end; { ManyRotates }
{**************************************************************************

                             splitORrotate

        see interface

        Conceptual record: imagine record were inserted at index.
        Note that si is in terms of this conceptual record.

        r[0] r[1] ... r[si]  r[si+1] .. r[nkeys] r[nkeys+1]
                           ^
                       splitpoint

        Case 0:

        <----r[index]----->


        Case 1:

                             <-----r[index]---->


            special case 0: (0 & 1 are not mutually exclusive)

                             r[index]

            special case 1:

                                         r[index]


***************************************************************************}

{$S access }
function SplitorRotate{(var kresult : integer;
                        fileid : integer;
                        pagetype : integer;
                        pnewrec : ptrdata;
                        size : integer;
                        pfptr : ptrfileptr;
                        canmove : boolean;
                        index,
                        buff1 : integer;
                        leftsib,
                        rightsib : fileptr;
                    var buff2 : integer;
                    var ipage : fileptr;
                    var iindex : integer;
                        ps : ptrstackrec;
                    var sibpage : fileptr;) : integer};
label
        1;
var
        pbd1,pbd2 : ^ buffdesc;
        prec,
        pb1,pb2 : ptrdata;
        po1,po2 : ptroffsets;
        pdb,pd1,pd2 : ptrpagedesc;
        pfd : ptrfiledesc;
        have2,
        splitP,
        t : boolean;
        lbuff,
        rbuff,
        rotPt,
        undoPt,
        relprio,
        lresult,
        blink,
        tresult,
        descoff,
        pagesize,
        si,afteri,
        total,ioff : integer;
        psi,psi_1 : ^ integer;

{**************************************************************************

                                moverecord

        moves record into slot in page.  if canmove is true then the
        record is already in one piece so we can block move it in.  if
        canmove is not true then it must be assembled from

        1) if leaf, must be assembled from a data record (this is an
           index file)

        2) if nonleaf, must be assembled from a leaf page entry (we only
           want the first nsort fields)


**************************************************************************}

{$S access }
procedure moverecord(pfd : ptrfiledesc;
                     prec,
                     pnewrec : ptrdata;
                     size : integer);
var
        pfileptr : ptrfileptr;
begin
{$IFC fTraceDB}    LogCall;     {$ENDC}
        if pagetype = nonleaftype then
            begin
            if canmove then movelf(pnewrec,prec,size)
            else extractkey(pfd^.pedesc,pfd^.pedesc,pfd^.nsort,
                            pnewrec,
                            pointer(ord(prec)+sizeof(fileptr)),pfd^.kvflds);
            pfileptr := @prec^;
            pfileptr^ := pfptr^;
            end
        else
            begin
            if canmove then movelf(pnewrec,prec,size)
            else extractkey(pfd^.pkdesc,pfd^.pedesc,pfd^.nfields,
                            pnewrec,prec,pfd^.vflds);
            end;
end; { moverecord }

{$S access }
begin
{$IFC fTraceDB}    LogCall;     {$ENDC}

        pfd := pfiletable^[fileid];
        pbd1 := @buffpool^[buff1];
        pagesize := pfd^.pagesize;
        descoff := pagesize-sizeof(pagedesc);

{$IFC debug }
        if bdebug then writeln(dumpfile,'splitorrotate ');
{$ENDC }

        ktouched(pfd^.scans,pbd1^.page);

    {   Lets see if we can rotate left or right first }

        if pagetype = leaftype then relprio := isleaf
        else relprio := isnonleaf;
        have2 := false;

        sibpage := leftsib;
        rotPt := -1;
        if leftsib <> nilptr then
            begin
            getpage(lresult,lbuff,fileid,leftsib,true);
            if lresult <> noerr then goto 1;
            rotPt := rotatepoint(lbuff,buff1,buff1,index,size,pagesize);
            if rotPt < 0 then
                begin
                relbuff(lresult,lbuff,relprio);
                if lresult <> noerr then goto 1;
                end
            else
                begin
                buff2 := lbuff;
                rbuff := buff1;
                have2 := true;
                end;
            end;

        if (rightsib <> nilptr) and (rotPt < 0) then
            begin
            getpage(lresult,rbuff,fileid,rightsib,true);
            if lresult <> noerr then goto 1;
            rotPt := rotatepoint(buff1,rbuff,buff1,index,size,pagesize);
            if rotPt < 0 then
                begin
                relbuff(lresult,rbuff,relprio);
                if lresult <> noerr then goto 1;
                end
            else
                begin
                buff2 := rbuff;
                have2 := true;
                lbuff := buff1;
                sibpage := rightsib;
                end;
            end;

        if rotPt >= 0 then { rotate left or right }
            begin

{$IFC debug }
            if bdebug then writeln(dumpfile,'rotate ',rotPt,leftsib,
                                   rightsib,sibpage);
{$ENDC }

            ktouched(pfd^.scans,sibpage);
            if ps <> nil then ps^.insindex := index;
            if index <= rotPt then
                begin
                pbd1 := @buffpool^[lbuff];
                rotPt := rotPt - 1;
                end
            else
                begin
                pbd1 := @buffpool^[rbuff];
                index := index - rotPt - 1;
                end;
            undoPt := manyrotates(lbuff,rbuff,rotPt,descoff);
            if ps <> nil then
                with ps^ do
                    begin
                    vact := irot;
                    unrotPt := undoPt;
                    rec_lpage := buffpool^[lbuff].page;
                    rec_rpage := buffpool^[rbuff].page;
                    end;
            pb1 := pbd1^.pbuff;
            pd1 := pointer(ord(pb1)+descoff);
            po1 := pointer(ord(pd1)-sizeof(integer));
            ioff := po1^[-index];
            ipage := pbd1^.page;
            iindex := index;
            ioff := po1^[-index];

            moverf(pointer(ord(pb1)+ioff),
                   pointer(ord(pb1)+ioff+size),
                   po1^[-pd1^.nkeys]-ioff);
            movl_inc(@po1^[-pd1^.nkeys],@po1^[-pd1^.nkeys-1],
                     pd1^.nkeys-index,size);
            pd1^.nkeys := pd1^.nkeys+1;
            moverecord(pfd,pointer(ord(pb1)+ioff),pnewrec,size);
            relbuff(lresult,lbuff,relprio);
            buff1 := rbuff;
            splitORrotate := rotate;
            buff2 := rbuff;
            end
        else
            begin

    {   Have to split the page

        1) allocate a page in the file for the new page

        2) call splitpoint to determine where to split

        3) split the page and copy in the new record

        4) setup the pointers for the new page

        5) post the new page so we will no if there is no room on disk

        6) if the post didn't work, undo the split
           else fixup the backlink of the following page }

            pb1 := pbd1^.pbuff;
            pd1 := pointer(ord(pb1)+descoff);
            po1 := pointer(ord(pd1)-sizeof(integer));
            total := po1^[-pd1^.nkeys];
            ioff := po1^[-index];
            allocpage(lresult,fileid,buff2);
            if lresult = noerr then
                begin
                have2 := true;
                pbd2 := @buffpool^[buff2];
                pb2 := pbd2^.pbuff;
                pd2 := pointer(ord(pb2)+descoff);
                po2 := pointer(ord(pd2)-sizeof(integer));

                splitpoint(po1,pd1^.nkeys,index,size,si);

{$IFC debug }
                if bdebug then writeln(dumpfile,'split i,si:',index:5,si:5);
{$ENDC }

                pd2^.nkeys := pd1^.nkeys-si;
                psi := @po1^[-si];
                psi_1 := @po1^[-si-1];

                if index <= si then { Case 0 - insert in old page }
                    begin

                    ipage := pbd1^.page;
                    iindex := index;

                {   copy off all of buff2 in one hunk }

                    movelf(pointer(ord(pb1)+psi^),pb2,total-psi^);
                    movr_inc(@psi^,po2,pd2^.nkeys,-psi^);

                {   then do a simple insert into buff1 }

                    moverf(pointer(ord(pb1)+ioff),
                           pointer(ord(pb1)+ioff+size),psi^-ioff);
                    movl_inc(@psi^,@psi_1^,si-index,size);
                    prec := pointer(ord(pb1)+ioff);
                    end
                else
                    begin { index > si --  Case 1, insert in new page }
                    afteri := index-si;
                    ipage := pbd2^.page;
                    iindex := afteri-1;

                {   copy over records up to insertion point (maybe none ) }

                    movelf(pointer(ord(pb1)+psi_1^),pb2,ioff-psi_1^);
                    movr_inc(@psi_1^,po2,iindex,-psi_1^);

                {   copy over records after insertion point (maybe none) }

                    movr_inc(@po1^[-index],@po2^[-afteri],
                             pd2^.nkeys-afteri,-psi_1^+size);
                    movelf(pointer(ord(pb1)+ioff),
                           pointer(ord(pb2)+po2^[-afteri]),total-ioff);

                    prec := pointer(ord(pb2)+po2^[-iindex]);
                    end;
                pd1^.nkeys := si+1;
                moverecord(pfd,prec,pnewrec,size);

            {   setup pointers and write new page to make sure there's room }

                with pd2^ do
                    begin
                    if pagetype <> nonleaftype then
                        begin
                        next := pd1^.next;
                        prior := pbd1^.page;
                        end;
                    ptype := pagetype;
                    end;
                post(lresult,buff2);

                if lresult <> noerr then { if error - unsplit the sucker }
                    begin
                    if index < pd1^.nkeys then delrecord(pb1,po1,pd1,index)
                    else delrecord(pb2,po2,pd2,index-pd1^.nkeys);
                    t := mergepages(buff1,buff2,pagesize);
                    pbd2^.state := empty; { undo dirty from mergepages }
                    pd1^.next := pd2^.next;
                    end
                else { if no error - fix up prior and next pointers }
                    begin
                    pbd1^.state := dirty;
                    if ps <> nil then
                        with ps^ do
                            begin
                            vact := split;
                            rec_lpage := pbd1^.page;
                            rec_rpage := pbd2^.page;
                            end;

{$IFC debug }
                    if bdebug then
                        begin
                        dumpp(fileid,buff2);
                        dumpp(fileid,buff1);
                        end;
{$ENDC }

                    if pagetype <> nonleaftype then
                        begin
                        pd1^.next := pbd2^.page;

                    {   the only error that relbuff can return is
                        inconsistent and getpage will return that too! }

                        if pd2^.next <> nilptr then { set back link }
                            begin
                            getpage(lresult,blink,fileid,pd2^.next,true);
                            if lresult = noerr then
                                begin
                                with buffpool^[blink] do
                                    begin
                                    pdb := pointer(ord(pbuff)+descoff);
                                    pdb^.prior := pbd2^.page;
                                    state := dirty;
                                    end;
                                relbuff(lresult,blink,isleaf);
                                end;
                            end; { set back link }
                        end; { fix up prior & next }
                    end; { got buff2 ok }
                end;
            splitORrotate := isplit;
            end;
1 :     if lresult <> noerr then
            begin
            relbuff(tresult,buff1,0);
            if have2 then relbuff(tresult,buff2,0);
            if lresult <> noroom then pfd^.fstatus := inconsistent;
            splitORrotate := noflow;
            end;
        kresult := lresult;

{$IFC debug }
        if bdebug then writeln(dumpfile,'split or rotate ',kresult);
{$ENDC }
end; { SplitorRotate }

ÿ