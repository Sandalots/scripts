{#F     libdb/lmfetch.text }
{$S lmscanseg }
{*************************************************************************

                                get_phys

        given a differential scan whose cos is within a range, computes
        the page address and record index in that page.  fetches the
        page with that record and returns the size of the record, a
        pointer the record and the buffer # of the page.

        Input Parameters
                scanid - differential scan index
                range - the range in which the cur_handle lie`s

        Output Parameters
                lmresult - return result
                prec - ptr to cos record (in buffer)
                rsize - size of cos record
                buffer - buffer containing the cos record


        Return Results
                noerr
                ioerr
                inconsistent
                nobuffs

**************************************************************************}

procedure get_phys(var lmresult : integer;
                       scanid : integer;
                       range : range_rec;
                   var prec : ptrdata;
                   var rsize,
                       buffer : integer);
var
        fileid,
        diff,
        tresult,
        index,
        psize : integer;
        physaddr,
        pageaddr,
        fpage : fileptr;
        pb : ptrdata;
        po : ptroffsets;
begin
        with pdifftable^[scanid]^ do
            begin
            fileid := on;
            diff := range.handle.high - curhandle.high;
            if reversescan then physaddr := range.physaddr + diff
            else physaddr := range.physaddr - diff;
            end;
        with pfiletable^[fileid]^ do
            begin
            psize := pagesize;
            fpage := firstpage;
            end;
        pageaddr := div424(physaddr-fpage,psize,index);
        pageaddr := mult424(pageaddr,psize) + fpage;
        getpage(lmresult,buffer,fileid,pageaddr,true);
        if lmresult = noerr then
            begin
            pb := buffpool^[buffer].pbuff;
            po := pointer(ord(pb)+psize-(sizeof(pagedesc)+sizeof(integer)));
            rsize := po^[-index];
            prec := pointer(ord(pb)+rsize);
            rsize := po^[-index-1] - rsize;
            end;
end; { get_physical }
{**************************************************************************

                                in_range

        returns true iff prange is a range record and ahandle is in
        its range. note that for efficiency, the code for a range is
        0 and the code field is in the high order bits of code.

**************************************************************************}

function in_range(prange : ptrrangerec;
                  a_hand : lmhandle): boolean;
var
        range : range_rec;
begin
        range := prange^;
        in_range := false;
        if (extract(@range.code,diff_nbits,diff_sbit) = 0) and
           (a_hand.low = 0) then
            with range do
                in_range := (comp44(a_hand.high,handle.high-code) <> lt)
                                           and
                            (comp44(a_hand.high,handle.high) <> gt);
end; { in_range }
(*db{$S noidebug }
{***************************************************************************

                                printdcos

        dumps the cos of diffscan scanid.

***************************************************************************}

procedure printdcos(scanid : integer);
var
        prec : ptrdata;
        tresult,
        buffer,
        rsize,
        diff_code : integer;
        range : range_rec;
begin
        with pdifftable^[scanid]^ do
            begin
            writeln(dumpfile,'whichway,reversescan:',whichway:3,
                    ord(reversescan):3);
            if whichway <> noway then
                begin
                writeln(dumpfile,'vpage,vindex:',vpage:14,vindex:7);
                end;
            writeln(dumpfile,'curhandle:<',curhandle.high,',',
                    curhandle.low,'>');
            printcos(pscantable^[diffid]);
            if pscantable^[diffid]^.ptrcos <> nil then
                begin
                movelf(pscantable^[diffid]^.ptrcos,
                       @range,
                       sizeof(range));
                diff_code := extract(@range.code,diff_nbits,diff_sbit);
                if diff_code = tretrieve then
                    begin
                    writeln(dumpfile,'<1st,nth>:',
                            (range.handle.high-range.code):14,
                            range.handle.high:14);
                    if in_range(@pscantable^[diffid]^.ptrcos^,curhandle) then
                        begin
                        get_phys(tresult,scanid,range,prec,rsize,buffer);
                        if tresult = noerr then
                            begin
                            with pfiletable^[on]^ do
                                printkey(pedesc,nfields,prec);
                            relbuff(tresult,buffer,isleaf);
                            end;
                        end
                    end
                else
                    begin
                    with pfiletable^[on]^ do
                        printkey(pedesc,nfields,
                                 @pscantable^[diffid]^.ptrcos^[sizeof(preamble)]);
                    end;
                end;
            end;

end; { printdcos }
procedure print_rec(fileid : integer;
                    buffer : integer;
                    recno  : integer);
var
        pb : ptrdata;
        po : ptroffsets;
begin
        with pfiletable^[fileid]^ do
            begin
            pb := buffpool^[buffer].pbuff;
            po := @pb^[pagesize-sizeof(pagedesc)-sizeof(integer)];
            printkey(pedesc,nfields,@pb^[po^[-recno]]);
            end;
end;*)
{$S lmreadseg }
{************************************************************************

                                kfilter

        used if scan is via an index file only.  returns true if record
        index in buffer passes the search filter for the index file.

        Input Parameters
                scanid - scan index of differential scan
                buffer - buffer in which candidate record is
                index - index of candidate record

        Output Parameters
                function result - true iff the record obeys all the
                    qualifications of the expression pointed to by kfilter


************************************************************************}

{##}{not implemented }

{************************************************************************

                                dfilter

        returns true if record passes the search filter
        for the data file.

        note : the code takes advantage of the fact that only and conditions
        are allowed.  also takes advantage of the relative placement of
        relational operators and their field and constant triples.


        Input Parameters
                scanid - scan index of differential scan
                prec - pts to record

        Output Parameters
                function result - true iff the record obeys all the
                    qualifications of the expression pointed to by dfilter


************************************************************************}
function dfilter(scanid : integer;{##}
                 prec : ptrdata) : boolean;{##}
label
        1,10;
var
        pconst : ptrdata;
        pquery : ptrquery;
        pexpr : ptrtriple;
        pedesc : ptrrecptr;
        pon : ptrfiledesc;
        cmp,
        fldnum,
        action,
        i : integer;
begin

    {   check first is the query is null - if so, record passes automatically }

        dfilter := true;
        with pdifftable^[scanid]^ do
            begin
            pon := pfiletable^[on];
            if pdfilter = nil then goto 1;
            pquery := @pdfilter^;
            end;

        pexpr := @pquery^.qexp;
        pconst := pointer(ord(pexpr) + sizeof(triple)*pquery^.ntriples);
        pedesc := pon^.pedesc;

    {   we take advantage of the fact that all queries are just ands of
        relationals between a field and a constant }

        repeat
            action := pexpr^.action;
            if action > ex_and then
                begin
                pexpr := pointer(ord(pexpr) + sizeof(triple));
                fldnum := pexpr^.fldid;
                pexpr := pointer(ord(pexpr) + sizeof(triple));

                {  note that we compare the field and constant in reverse
                   order.  thus we reverse the inequality.  this is necessary
                   so that the comparison always uses direction = ascending

                   please pardon the short circuit boolean kludge  }

                cmp := compare(@pexpr^.desc,@pedesc^[fldnum],1,pconst,prec);
                if cmp = eq then
                    begin
                    if (action = ex_ne)or(action = ex_lt)or(action = ex_gt)
                    then goto 10
                    end
                else if cmp = gt then
                    begin
                    if (action = ex_eq)or(action = ex_gt)or(action = ex_ge)
                    then goto 10
                    end
                else
                    begin
                    if (action = ex_eq)or(action = ex_lt)or(action = ex_le)
                    then
                        begin
                    10: dfilter := false;
                        goto 1;
                        end;
                    end;
                end;
            pexpr := pointer(ord(pexpr) + sizeof(triple));
        until ord(pexpr) = ord(pconst);

1 :
end; { dfilter }
{***********************************************************************

                                select_next

        fetches the next (prior) record from the "real" file and
        leaves it in buffer. only returns eos if there are no more
        records in direction "which". assumes that the caller has run
        off the differential file in the direction which already.


        !!!!warning!!!! expect pstack_validate to have been called
        where necessary

        Input Parameters
                scanid - differential scan index
                which - next or prior, which way to go in the logical
                    scan

        Output Parameters
                lmresult - return result
                sbuffer - buffer in which data record lies
                sindex - index of data record

        Return Results
                noerr
                eos
                ioerr
                nobuffs
                inconsistent

**********************************************************************}

procedure select_next(var lmresult : integer;
                          scanid : integer;
                          which : integer;
                      var sbuffer,
                          sindex : integer);
label
        1,2,20;
var
        pdesc,{##}
        psdesc,
        prime_desc : ptrrecptr;
        prec,
        pb : ptrdata;
        po : ptroffsets;
        pon,
        pvia : ptrfiledesc;
        realwhich,
        diffscan,
        dbuffer,
        buffer,
        dindex,
        index,
        offoffset,
        tresult,
        lresult : integer;
        by_index : boolean;
begin

    {   we are building differential file on the fly as we scan the real
        file.  if the real file has been exhausted, we set whichway := first.
        we return immediately if the real file has been exhausted or if
        which is opposite the direction in which the differential file is
        being built (that is, we have run off the end of the differential
        file in whose direction we have already scannned the real file }

        with pdifftable^[scanid]^ do
            begin
            by_index := on <> via;
            diffscan := diffid;
            if whichway <> which then
                begin
                if which = next then {##}
                    begin {##}
                    curhandle.high := nilptr; {##}
                    curhandle.low := nilptr; {##}
                    end {##}
                else {##}
                    begin {##}
                    curhandle.high := 0; {##}
                    curhandle.low := 0; {##}
                    end; {##}
                lresult := eos;
                goto 1;
                end;
            end;

        with pdifftable^[scanid]^ do
            begin

        {   if reversescan is true then we are scanning the real file in the
            opposite order that we are building the differential scan.  (we
            do this to take advantage of an index or primary key sort order }

            if reversescan then
                 begin
                 if which = prior then realwhich := next
                 else realwhich := prior
                 end
            else realwhich := which;
            pon := pfiletable^[on];
            pvia := pfiletable^[via];
            pdesc := pvia^.pedesc;{##}
            if by_index then {##}
                begin{##}
                psdesc := pvia^.pkdesc;{##}
                prime_desc := @pdesc^[pvia^.nfields-pon^.nkeys];{##}
                end{##}
            else psdesc := pdesc;{##}

        {   vpage and vindex give the previous record.

            we do nextprior on the via file until one of the following:
               1) we hit eos on the via file
               2) we hit a record on the via file that lies beyond the
                  high (low) record that bounds the scan
               3) we find a record that passes the query

            if 1 or 2 occur, we set whichway to first to indicate that we've
               exhausted the real file }

            offoffset := pvia^.pagesize-(sizeof(pagedesc)+sizeof(integer));
            getpage(lresult,buffer,via,vpage,true);
            if lresult <> noerr then goto 1;

            index := vindex;
            repeat { fetch loop for data records }
                if realwhich = prior then index := index - 1
                else index := index + 1;
                nextprior(lresult,via,buffer,index,realwhich);
                if lresult <> noerr then goto 20;

                pb := buffpool^[buffer].pbuff;
                po := pointer(ord(pb)+offoffset);
                prec := pointer(ord(pb)+po^[-index]);
                if realwhich = prior then
                    begin
                    if nlflds > 0 then
                        begin
                    {##}if compare(pdesc,psdesc,nlflds,prec,plow) = lt then
                            begin
                            lresult := eos;
                            goto 20;
                            end;
                        end
                    end
                else if nhflds > 0 then
                    begin
                {##}if compare(pdesc,psdesc,nhflds,prec,phigh) = gt then
                        begin
                        lresult := eos;
                        goto 20;
                        end;
                    end;

                if by_index then { scanning via index }
                    begin
                    findkeyed(lresult,exact,0,on,
                              prime_desc,prec,dbuffer,dindex);
                    if lresult = notfound then
                        begin
                        relbuff(lresult,dbuffer,isleaf);
                        lresult := inconsistent;
                        end;
                    if lresult <> noerr then goto 2;

                    pb := buffpool^[dbuffer].pbuff;{##}
                {##}po := pointer(ord(pb)+pon^.pagesize-(sizeof(pagedesc) +
                          sizeof(integer)));{##}
                    if dfilter(scanid,pointer(ord(pb)+po^[-dindex])) then{##}
                        begin
                        vpage := buffpool^[buffer].page;
                        relbuff(lresult,buffer,isleaf);
                        buffer := dbuffer;
                        if lresult <> noerr then goto 2;
                        goto 20;
                        end
                    else {##}
                        begin {##}
                        relbuff(lresult,dbuffer,isleaf); {##}
                        if lresult <> noerr then goto 2; {##}
                        end; {##}
                    end
                else if dfilter(scanid,prec) then { primary key }{##}
                    begin
                    dindex := index;
                    vpage := buffpool^[buffer].page;
                    goto 20;
                    end;
            until false;

    20:     if lresult = eos then
                begin
                with pdifftable^[scanid]^ do
                    begin
                    whichway := noway;
                    if which = next then {##}
                        begin {##}
                        curhandle.high := nilptr; {##}
                        curhandle.low := nilptr; {##}
                        end {##}
                    else {##}
                        begin {##}
                        curhandle.high := 0; {##}
                        curhandle.low := 0; {##}
                        end; {##}
                    end;
                goto 2;
                end
            else if lresult > eos then goto 1;
            vindex := index;

            end; { with }
            goto 1;

2 :     relbuff(tresult,buffer,isleaf);
        if tresult <> noerr then lresult := tresult;

1 :     lmresult := lresult;
        sbuffer := buffer;
        sindex := dindex;
end; { select_next }
{$S lmscanseg }
{***********************************************************************

                                realnext

        fetches the next (prior) record from the "real" file and logs an
        entry in the differential file for the record.  if possible, does
        run encoding.  only returns eos if there are no more records
        (real or not) in direction "which".

        Input Parameters
                scanid - scan index of differential scan
                which - direction : next or prior

        Output Parameters
                lmresult - return result

        Return results

            noerr
            nobuffs
            ioerr
            inconsistent
            noroom
            duplkey
            rectoobig
            eos
            nomem
            badscanid
            oddrecsize

**********************************************************************}

procedure realnext(var lmresult : integer;
                       scanid : integer;
                       which : integer);
label
        1;
var
        range : range_rec;
        low_handle,
        physaddr : fileptr;
        newhandle : lmhandle;
        gowhich,
        offender,
        diff_code,
        buffer,
        index,
        diffscan,
        tresult,
        lresult : integer;
        reversed,
        maderun : boolean;
begin
        (*dbif lmdebug then
            begin
            writeln(dumpfile,'>realnext');
            printdcos(scanid);
            end;*)

        pstack_validate(lresult);
        if lresult <> noerr then goto 1;

    {   1) select_next skips to the next (or prior) record from the
           real file.  returns eos only if there are no records in
           the given direction

        2) compute physical address for the current record (page#+record#)

        3) if there already is a range record which describes a range of
           records such that:

           a) the current record is on the same page
           b) the current record is the record immediately before the
              first record of the range or is immediately after the last
              record of the range.  by the construction of the physical
              address and the fact that any record requires at least 4
              bytes (record and record offset), it is guaranteed that if
              two records lie in different pages, then their physical
              addresses differ by more than 1

           then add the current record to that range, else create a new range
           consisting of just the current record.  }

        select_next(lresult,scanid,which,buffer,index);

        if lresult = noerr then
            begin
            physaddr := buffpool^[buffer].page + index;
            relbuff(tresult,buffer,isleaf);
            if tresult <> noerr then
                begin
                lresult := tresult;
                goto 1;
                end;

            with pdifftable^[scanid]^ do
                begin
                diffscan := diffid;
                reversed := reversescan;
                end;

            newhandle.low := 0;
            maderun := false;

        {   maderun is true iff the current record can be added to the cos
            range record of the differential file }

            if which = next then gowhich := prior
            else gowhich := next;
            efetch(lresult,diffscan,gowhich,0,nil);
            if lresult > eos then goto 1;

            if lresult = noerr then { diff. file not empty }
                begin
                movelf(pscantable^[diffscan]^.ptrcos,
                       @range,
                       sizeof(range));
                diff_code := extract(@range.code,diff_nbits,diff_sbit);

                {  compute next handle }

                newhandle.high := range.handle.high;
                if which = next then newhandle.high := newhandle.high + 1
                else if diff_code = 0 then
                    newhandle.high := newhandle.high - range.code - 1
                else newhandle.high := newhandle.high - 1;

                if diff_code = 0 then { a retrieval range }
                    begin
                    low_handle := range.handle.high - range.code;
                    if reversed then { handle to physaddr map is negative }
                        begin
                        if ((range.physaddr+range.code+1) = physaddr) and
                           ((low_handle-1) = newhandle.high) then
                            begin
                            maderun := true;
                            end
                        else if ((range.physaddr-1) = physaddr) and
                                ((range.handle.high+1) = newhandle.high) then
                            begin
                            maderun := true;
                            range.physaddr := physaddr;
                            range.handle := newhandle;
                            end;
                        end
                    else  { handle to physaddr map is positive }
                        begin
                         if ((range.physaddr-range.code-1) = physaddr) and
                            ((low_handle-1) = newhandle.high) then
                             begin
                             maderun := true;
                             end
                         else if ((range.physaddr+1) = physaddr) and
                                 ((range.handle.high+1) = newhandle.high) then
                             begin
                             maderun := true;
                             range.physaddr := physaddr;
                             range.handle := newhandle;
                             end;
                         end;
                    end;
                end
            else { nothing in diff. file yet - invent handle }
                begin
                if which = prior then newhandle.high := PriorStart
                else newhandle.high := NextStart;
                end;

            if maderun then
                begin
                range.code := range.code + 1;
                eupdate(lresult,offender,diffscan,current,0,nil,@range,
                        sizeof(range));
                end
            else
                begin
                range.handle := newhandle;
                range.physaddr := physaddr;
                range.code := 0;
                einsert(lresult,offender,diffscan,@range,sizeof(range),
                        false);
                end;
            if lresult = noerr then
                pdifftable^[scanid]^.curhandle := newhandle;
            end;


1 :     lmresult := lresult;
        (*dbif lmdebug then
            begin
            writeln(dumpfile,'<realnext:',lmresult);
            printdcos(scanid);
            end;*)
end; { realnext }
{$S lmreadseg }
{***************************************************************************

                                check_nulls

        checks for null not allowed violations in the cos of the
        scan on the differential file.  if pnulls is not nil, it sets the
        bits for all the offending fields.  returns true iff some field
        offended.

        Input Parameters
                scanid - differential scan index
                pnulls - pts to bits for recording the offenders

        Output Parameters
                bit i in pnulls^ is set iff field i offended
                return result true iff one field offended


***************************************************************************}
function check_nulls(scanid : integer;
                     pnulls : ptrbits) : boolean;
label
        10;
var
        pdesc : ptrflddesc;
        prec,
        pmask : ptrdata;
        isnull : boolean;
        pf : ptr_field;
        size,
        ftype,
        i,
        j,
        flds : integer;
begin
        with pdifftable^[scanid]^ do
            begin
            pmask := @nullset;
            with pfiletable^[on]^ do
                begin
                flds := nfields;
                pdesc := @pedesc^;
                end;
            prec := pointer(ord(pscantable^[diffid]^.ptrcos)+sizeof(preamble));
            end;

        check_nulls := false;
        for i := 0 to flds-1 do
            begin
            if not inpset(pmask,i) then
                begin
                ftype := pdesc^.ftype;
                pf.ptr := pointer(ord(prec)+pdesc^.offset);
                if ftype = ftyppack2 then
                    isnull:=extract(pf.ptr,pdesc^.bo.nbits,pdesc^.bo.sbit) = 0
                else if ftype >= ftypvstr then
                    isnull := pf.pvfld^.size = 0
                else
                    begin
                    size := pdesc^.size;
                    if (ftype >= ftypint1) and (ftype <= ftypint8) then
                        begin
                        isnull := pf.ptr^[0] = nullint1;
                        size := size - 1;
                        pf.ptr := pointer(ord(pf.ptr) + 1);
                        end;
                    for j := 1 to size do
                        begin
                        if pf.ptr^[0] <> 0 then
                            begin
                            isnull := false;
                            goto 10; { break }
                            end;
                        pf.ptr := pointer(ord(pf.ptr) + 1);
                        end;
                10 :
                    end;
                if isnull then
                    begin
                    check_nulls := true;
                    if pnulls <> nil then setbit(@pnulls^,i);
                    end;
                end;
            pdesc := pointer(ord(pdesc)+sizeof(flddesc));
            end;
end; { check_nulls }
{$S lmscanseg }
{*************************************************************************

                                key_highlights

        sets bit i iff field i of the datafile participates in the
        primary key of the offending file.  used after a duplicates are
        not allowed violation.

        Input Parameters
                scanid - differential scan index
                offender - offending file
                pnulls - pts to bits for recording the offenders

        Output Parameters
                bit i in pnulls^ is set iff field i offended
                return result true iff one field offended

**********************************************************************}

procedure key_highlights(scanid : integer;
                         offender : integer;
                         pnulls : ptrbits);
label
        10;
var
        i,
        j : integer;
        pon : ptrfiledesc;
        pfdesc : ptrflddesc;
        fdesc : flddesc;
begin
        if pnulls <> nil then
            begin
            with pfiletable^[offender]^ do
                begin
                if inpset(@flags,fdata) then
                    for i := 0 to nkeys-1 do setbit(@pnulls^,i)
                else { an index file! }
                    begin
                    pon := pfiletable^[pdifftable^[scanid]^.on];
                    for j := 0 to nkeys-1 do
                        begin
                        fdesc := pkdesc^[j];
                        pfdesc := @pon^.pedesc^;
                        for i := 0 to pon^.nfields-1 do
                            begin
                            if (pfdesc^.offset = fdesc.offset) and
                               (pfdesc^.size = fdesc.size)
                            then
                                begin
                                setbit(@pnulls^,i);
                                goto 10; { break }
                                end;
                            pfdesc := pointer(ord(pfdesc)+sizeof(flddesc));
                            end;
                    10 :
                        end;
                    end;
                end;
            end;

end; { key_highlights }


{***********************************************************************

                                lmfetch

        see interface

**********************************************************************}

procedure lmfetch{(var lmresult : integer;
                   var user_error : integer;
                       phighlights : ptrbits;
                       scanid : integer;
                       which : integer;
                       s_handle : lmhandle;
                   var recsize : integer)};
label
        10,20;
var
        pscan : ptrscandesc;
        range: range_rec;
        prec : ptrdata;
        nexthandle : lmhandle;
        cresult,
        diffscan,
        diff_code,
        buffer,
        locwhich,
        gowhich,
        tresult,
        lresult : integer;
        scratch,
        stop : boolean;
begin
        (*db if lmdebug then
            begin
            writeln(dumpfile,'>lmfetch');
            printdcos(scanid);
            end;*)


    {   somewhat the same flavor as efetch except for 4 things:

        1) if we obtain an eos error on the differential file, we have to call
        realnext to see if we can pull another record out of the real file

        2) at all times we must watch handles that fall within ranges

        3) we must skip any number of delete records

        4) we don't allocate and copy out the record, but rather return the
           recsize, so the List Manager can allocate the space itself and call
           lmcopy to do the copy  }


        with pdifftable^[scanid]^ do
            begin
            diffscan := diffid;
            if phighlights <> nil then
                clearset(@phighlights^,pfiletable^[on]^.nfields);
            end;

        if (which = next) or (which = prior) then
            begin
            repeat { until we hit eos or a bona fide record }
                stop := true;
                with pdifftable^[scanid]^,pscantable^[diffscan]^ do
                    begin
                    if position = exact then { can we still use run? }
                        begin
                        nexthandle := curhandle;
                        with nexthandle do
                            if low = 0 then
                                if which = next then high := high + 1
                                else high := high - 1;
                        if in_range(@ptrcos^,nexthandle) then
                            begin
                            lresult := noerr;
                            curhandle := nexthandle;
                            goto 10;
                            end;
                        end;
                    end;
                efetch(lresult,diffscan,which,0,nil);
                if lresult = eos then realnext(lresult,scanid,which)
                else if lresult = noerr then
                    with pdifftable^[scanid]^ do
                        begin
                        movelf(pscantable^[diffscan]^.ptrcos,
                               @range,
                               sizeof(range));
                        diff_code := extract(@range.code,diff_nbits,diff_sbit);
                        stop := (diff_code <> tdelete) and
                                (diff_code <> tbefore);
                        if (diff_code = 0) and (which = next) then
                            begin
                            curhandle.low := 0;
                            curhandle.high := range.handle.high - range.code;
                            end
                        else curhandle := range.handle;
                        end;
            until stop;
            end
        else if which = exact then
            begin
            efetch(lresult,diffscan,approx,1,@s_handle);
            pscan := pscantable^[diffscan];
            if lresult = notfound then { check if it's in a range }
                begin
                if in_range(@pscan^.ptrcos^,s_handle) then
                    begin
                    lresult := noerr;
                    pscan^.position := exact;
                    end;
                end;
            if lresult = eos then { fetch into the ozone (Bye Kids!) }
                begin
                repeat
                    realnext(lresult,scanid,next);
                    if lresult = noerr then
                        begin
                        cresult := comphand(s_handle,
                                            pdifftable^[scanid]^.curhandle);
                        if cresult = eq then goto 20
                        else if cresult = lt then
                            begin
                            lresult := notfound;
                            pscantable^[diffscan]^.position := prior;
                            goto 20;
                            end;
                        efetch(lresult,diffscan,next,0,nil);
                        if lresult = eos then lresult := noerr;
                        end;
                until lresult <> noerr;
            20: { break }
                end
            else if lresult = noerr then
                begin

            {   check for deleted record }

                movelf(pscan^.ptrcos,@range,sizeof(range));
                diff_code := extract(@range.code,diff_nbits,diff_sbit);
                if (diff_code = tdelete) or (diff_code = tbefore) then
                    lresult := notfound;
                pdifftable^[scanid]^.curhandle := s_handle;
                end;
            end
        else {which = first or if which = last }
            begin
            locwhich := which;
            if which = first then gowhich := prior
            else gowhich := next;
            repeat
                efetch(lresult,diffscan,locwhich,0,nil);
                if lresult <= eos then realnext(lresult,scanid,gowhich);
                locwhich := gowhich;
            until lresult <> noerr;
            end;


10 :    if lresult = noerr then
           with pscantable^[diffscan]^ do
               begin
               movelf(ptrcos,@range,sizeof(range));
               diff_code := extract(@range.code,diff_nbits,diff_sbit);
               if (diff_code = 0) then
                   begin
                   get_phys(lresult,scanid,range,prec,recsize,buffer);
                   if lresult = noerr then relbuff(lresult,buffer,isleaf);
                   recsize := recsize + sizeof(preamble);
                   user_error := noerr;
                   end
               else { check for tfyi and errors on tupdate and tinsert }
                   begin
                   user_error := extract(@range.code,sub_nbits,sub_sbit);
                   if diff_code = tfyi then user_error := just_fyi;
                   if user_error = null_error then
                       scratch := check_nulls(scanid,phighlights)
                   else if(user_error=dupl_error)or(user_error=just_fyi) then
                       key_highlights(scanid,
                                      extract(@range.code,idx_nbits,idx_sbit),
                                      phighlights);
                   recsize := cossize;
                   end;
               end;
        lmresult := lresult;

        (*if lmdebug then
            begin
            writeln(dumpfile,'<lmfetch:',lmresult:7,recsize:7);
            printdcos(scanid);
            end; *)
end; { lmfetch }

ÿ