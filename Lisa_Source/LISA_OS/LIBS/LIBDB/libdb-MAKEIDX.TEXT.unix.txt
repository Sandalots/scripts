{#F     libdb/makeidx.text }
{$S indexseg }
{^T 20}
{***************************************************************************

                                forcebyte

        aligns aoffset and abit to a byte boundary (abit = 15 or 7)

****************************************************************************}

procedure forcebyte(var aoffset,abit : integer);
begin
{$IFC fTraceDB}    LogCall;     {$ENDC}
        if (15 > abit) and (abit >= 8) then abit := 7
        else if (7 > abit) and (abit >= 0) then
            begin
            abit := 15;
            aoffset := aoffset + 2;
            end;
end; {forcebyte}
{^T 30}
{*************************************************************************

                                assignoffset

        assigns offset for field, packing fields with the following
        constraints:

        1. byte objects (ftypbset,ftypint1,ftypstr) begin on byte boundary
        2. integers (16 bit and 32 bit) and var. len. field descriptors
           begin on word boundary
        3. packed integers (n bit, 1<=n<=15) packed wherever they
           fit, but do not cross word boundaries

        Input Parameters
                aoffset - current allocation offset
                abit - current allocation bit
                pfdesc - descriptor for field whose offset is to be assigned:
                    pfdesc must have the proper ftype and size fields.

        Output Parameters
                aoffset - current allocation offset
                abit - current allocation bit
                pfdesc - descriptor for field whose offset is to be assigned:
                    offset and (if packed) sbit filled in


**************************************************************************}

procedure assignoffset(var aoffset,
                           abit : integer;
                           pfdesc : ptrflddesc);
var
        fldtype : integer;
begin
{$IFC fTraceDB}    LogCall;     {$ENDC}
        with pfdesc^ do
            begin
            fldtype := ftype;
            if fldtype = ftypint1 then
                begin
                forcebyte(aoffset,abit);
                if abit = 7 then
                    begin
                    offset := aoffset + 1;
                    abit := 15;
                    aoffset := aoffset + 2;
                    end
                else
                    begin
                    offset := aoffset;
                    abit := 7
                    end;
                end
            else if fldtype = ftyppack2 then
                begin
                if ((abit - bo.nbits) < - 1) and (abit <> 15) then
                        begin
                        abit := 15;
                        aoffset := aoffset + 2;
                        end;
                bo.sbit := abit - bo.nbits + 1;
                offset := aoffset;
                if (bo.sbit = 0) and (abit <> 15) then
                    begin
                    abit := 15;
                    aoffset := aoffset + 2;
                    end
                else abit := bo.sbit - 1;
                end
            else if (fldtype = ftypstr) or (fldtype = ftypnull) then
                begin
                forcebyte(aoffset,abit);
                if abit = 7 then offset := aoffset + 1
                else offset := aoffset;
                if odd(size) then abit := 22-abit;
                aoffset := offset + size;
                if abit = 7 then aoffset := aoffset - 1;
                end
            else { various types that must be word alligned }
                begin
                if abit <> 15 then
                    begin
                    abit := 15;
                    aoffset := aoffset + 2;
                    end;
                offset := aoffset;
                aoffset := aoffset + size;
                end;
            end; { with }
end; { assignoffset }
{^T 20}
{**********************************************************************

                                makeidesc

        Fills in pedesc and pkdesc recptr's for index file.  Also
        computes vflds and vsflds.

        Input Parameters
                onfile - the file on which the index is built
                pifile - the unfulfilled descriptor for the index file. the
                    following fields are filled in at time of call:

                    updateversion
                    pfldset (nil)
                    pedesc,pkdesc  (unfilled)
                    pagesize
                    lastpage
                    prod_vers
                    flags
                    nfields
                    nkeys
                    nsort
                piflds - sort order description for the index

        Output Parameters
                pifile - descriptor is filled in for makefile, plus
                    pkdesc

***********************************************************************}

procedure makeidesc(onfile : integer;
                    pifile : ptrfiledesc;
                    piflds : ptridesc);
var
        pindexseg : ^isegment;
        pkflddesc,
        pflddesc : ptrflddesc;
        tresult,
        toffset,
        tbit,
        aoffset,
        abit : integer;
        nisort,
        i : integer;
begin

{$IFC fTraceDB}    LogCall;     {$ENDC}
{$IFC debug }
        if dbdebug then writeln('midxd:');
{$ENDC }

    {   index record is sort key following by primary key of data file }

        aoffset := 0;
        abit := 15;
        with pfiletable^[onfile]^ do
            begin
            pflddesc := @pifile^.pedesc^;
            pkflddesc := @pifile^.pkdesc^;
            pindexseg := @piflds^;
            nisort := pifile^.nsort;
            for i := 1 to (pifile^.nfields-nkeys) do
                begin
                with pflddesc^ do
                    begin
                    pflddesc^ := pedesc^[pindexseg^.field];
                    pkflddesc^ := pflddesc^;
                    direction := pindexseg^.aord;
                    pkflddesc^.direction := direction;{##}
                    assignoffset(aoffset,abit,pflddesc);

                    (*if dbdebug then
                        begin
                        write(dumpfile,'f,o,d,t:',pindexseg^.field,
                                       offset:5,direction:2,ftype:3);
                        if ftype = ftyppack2 then
                            writeln(dumpfile,'nb,sb:',bo.nbits:3,bo.sbit:3)
                        else
                            writeln(dumpfile,'size:',size:4);
                        end;*)

                    end;
                if i = nisort then
                    begin
                    toffset := aoffset;
                    tbit := abit;
                    forcebyte(toffset,tbit);
                    if tbit = 7 then toffset := toffset + 1;
                    pifile^.kvflds := toffset;
                    end;
                pflddesc := pointer(ord(pflddesc)+sizeof(flddesc));
                pkflddesc := pointer(ord(pkflddesc)+sizeof(flddesc));
                pindexseg := pointer(ord(pindexseg)+sizeof(isegment));
                end;

            movelf(@pedesc^,@pflddesc^,nkeys*sizeof(flddesc));
            movelf(@pedesc^,@pkflddesc^,nkeys*sizeof(flddesc));
            for i := 1 to nkeys do
                begin
                with pflddesc^ do
                    begin
                    assignoffset(aoffset,abit,pflddesc);

                    (*dbif dbdebug then
                        begin
                        write(dumpfile,'o,d,t:',offset:5,direction:2,ftype:3);
                        if ftype = ftyppack2 then
                            writeln(dumpfile,'nb,sb:',bo.nbits:3,bo.sbit:3)
                        else
                            writeln(dumpfile,'size:',size:4);
                        end;*)

                    end;
                pflddesc := pointer(ord(pflddesc)+sizeof(flddesc));
                end;
            forcebyte(aoffset,abit);
            if abit = 7 then aoffset := aoffset + 1;
            pifile^.vflds := aoffset;
            end; { with }

{$IFC debug }
        if dbdebug then
           begin
           writeln(dumpfile,'midxd:',pifile^.vflds:4,pifile^.kvflds:4);
           end;
{$ENDC }

end; { makeidesc }


{************************************************************************

                                buildindex

        builds an index on onfile, giving it the name ifilename.

        Input Parameters
                ifilename
                filename
                tempdir1
                tempdir2
                temp_ldsn
                nisort
                nikeys
                piflds
                duplok  - as for makeindex

        Output Parameters
                ddresult - return result
                onscan - scanid of scan on the onfile
                needtoopen - true iff the index file must be opened (because
                    there is an open update scan on the onfile)
                indexuniqueid - uniqueid of index file

        Return Results

        noerr
        nomem
        inconsistent
        ioerr
        duplkey
        noroom
        nobuffs
        rectoobig
        temperror
        wrongfile
        notdbfile
        OS(make_file,open,lookup,make_dataseg,close_dataseg,info,close_object)

************************************************************************}

procedure buildindex(var ddresult : integer;
                         ifilename,
                         filename,
                         tempdir1,
                         tempdir2 : ptrpathname;
                         temp_ldsn : integer;
                         nisort,
                         nikeys : integer;
                         piflds : ptridesc;
                         duplok : boolean;
                     var onscan : integer;
                     var needtoopen : boolean;
                     var indexunique : uniqueid);
        forward;

{^T 20}
{*******************************************************************

                        makeindex

        See interface for description

*******************************************************************}

procedure makeindex{(var ddresult : integer;
                         ifilename,
                         filename,
                         tempdir1,
                         tempdir2 : ptrpathname;
                         temp_ldsn : integer;
                         nisort,
                         nikeys : integer;
                         piflds : ptridesc;
                         duplok : boolean)};
label
        1,100;
var
        loc_compact_state : states_de_compact;
        ppath : ptrpathname;
        indexunique : uniqueid;
        pirec : ptrientry;
        trash_flag,
        needtoopen : boolean;
        entrysize,
        tresult,
        i,
        ifile,
        onscan,
        onfile : integer;
begin

{$IFC fTraceDB}    LogCall;     {$ENDC}
    {   1) build index
        2) convert scan on onfile to labelupdate scan
        3) insert label entry for index in data file label
        4) if the data file has an open update scan, open index and
           link it into the on file descriptor  }


        buildindex(ddresult,ifilename,filename,tempdir1,tempdir2,temp_ldsn,
                   nisort,nikeys,piflds,
                   duplok,onscan,needtoopen,indexunique);
        if ddresult <> noerr then goto 1;

{       cvt_scan can never return an error here }

        cvt_scan(ddresult,onscan,labelupdate,pscantable^[onscan]^.onfile);
        loc_compact_state := init_compact_state;

100 :   {  compact retry point - here we want to preserve index }

        entrysize:=sizeof(ientry)+nikeys*sizeof(isegment)+length(ifilename^)-1;
        if odd(entrysize) then entrysize := entrysize + 1;
        ddresult := nnew(@czone,entrysize,@pirec);
        if ddresult = noerr then
            begin
            with pirec^ do
                begin
                lab.ltype := indexfile;
                lab.seq := 0;
                lab.varfld.offset := sizeof(labelentry);
                lab.varfld.size := entrysize-sizeof(labelentry);
                iunique := indexunique;
                iduplok := duplok;
                isort := nisort;
                ikeys := nikeys;
                movelf(@piflds^,@iflds,nikeys*sizeof(isegment));
                ppath := pointer(ord(@iflds) + nikeys*sizeof(isegment));
                movelf(@ifilename^,@ppath^,length(ifilename^)+1);
                end;

            linsert(ddresult,onscan,@pirec^,entrysize,true);
            ndispose(@czone,@pirec^);
            end;
        if ddresult <> noerr then
            begin
            if retry_compact(ddresult,loc_compact_state) then goto 100;
            prefix_name(ifilename);
{af}        kill_secure(tresult,prefix,password);
           {kill_object(tresult,prefix);}
            closescan(tresult,onscan);
            goto 1;
            end;

    {   if the onfile has an update scan going, then we have to open this
        index and link it to the list of indexes for the onfile }

        if loc_compact_state <> no_try then compact_state := no_try;
        if (ddresult = noerr) and needtoopen then
            openindexes(ddresult,ifile,onscan,ifilename,update,new_index);

        closescan(tresult,onscan);
        if tresult <> noerr then ddresult := tresult;
        if ddresult <> noerr then deleteindex(tresult,ifilename,filename);
        goto 1;


1 :  { normal exit }

{$IFC debug }
        if dbdebug then writeln('ic:',ddresult);
{$ENDC }

end; { makeindex }


ÿ