{ Copyright 1983,Apple Computer Inc. }
{$R-}
{$U-}
{$S swapaccs }
{#F     libdb/dbdecl1.text }
Unit DBDecl1;
       intrinsic;

Interface
{$SETC OSBUILT := TRUE }

Uses
 {$IFC OSBUILT }
{$U libsm/unitstd.obj} unitstd,
{$U libdb/dbenv.obj } dbenv,
{$U libos/syscall.obj } syscall,
{$U libos/psyscall.obj } psyscall,
{$U libin/intrlibp.obj } international;
{$ELSEC }
{$U OBJ:dbenv.obj } dbenv,
{$U INTRLIB.obj } international;
{$ENDC }

{$SETC PRERELEASE := FALSE }
{$SETC DOLLARD := NOT fSymOk }

{$SETC doTraceDB := TRUE}
{$SetC fTraceDB := doTraceDB AND fTRACE}

Const
        {  compare codes }

        lt = 0;
        gt = 1;
        eq = 2;

        wordsize = 2;
        elemsize = 16;
        halfelemsize = 8;
        maxrsize = 1000;      { maximum record size }

        ascending = 0;
        descending = 1;


        {  field types }

        ftypnull   = 0;                   { packed array of 0..255 }
        ftypint1   = 1;                   { byte }
        ftypint2   = 2;                   { 16 bit signed integer }
        ftypint4   = 3;                   { 32 bit signed integer }
        ftypint8   = 4;                   { 64 bit signed integer }
        ftypfptr   = 5;                   { 32 bit unsigned integer }
        ftyptick   = 6;                   { 48 bit ticket (unsigned) }
        ftyppack2  = 7;                   { packed subrange of integer }
        ftypstr    = 8;                   { packed array of char }
        ftypvstr   = 9;                   { var. len. packed array of char }
        ftypvnull  = 10;                  { var. len. packed array of byte }
        ftypsvstr   = 11;                 { short var. len. pa of char }
        ftypsvnull  = 12;                 { short var. len. pa of byte }
        ftypes     = 12;                  { last type }

        { null integer values }
        nullint1 = -128;
        nullint2 = -32768;
        nullint4 = -2147483648;

        {  integer nil value for relative offsets in field record }

        fnilval = -1;

        {   option flags  }

            {  justification flags }

            dbleft = 0;   dbright = 1;  dbcentered = 2;  dbwordwrap = 3;

            {  null allowed flag }

            dbnullok = 4;

            {  descending (vs. ascending) }

            dbdescend = 5;

            {  is this field visible to user? }

            dbisvisible = 6;

            {  more to come ... }

        { data field types }
        none = 0;
        ss = 1;         { social security }
        chk = 2;        { check box }
        zip = 3;        { zip code }
        ph = 4;         { telephone number }
        dol = 5;        { money }
        num = 6;        { number }
        tim = 7;        { time }
        dt = 8;         { date }
        lets = 9;       { text }
        tick = 10;      { ticket }

        { oem field types

          OEMtick uses ticket editcheck
          OEMFlets use lets edit check, except length included in syntax
          OEMSVlets use lets edit check
          OEMFGen use new edit check that includes only length
          OEMVGen,OEMSVGen need no edit check }

        OEMtick    = 11;   { 1-6 byte ticket, byte boundary }
        OEMFlets   = 12;   { Fixed length, packed array of char }
        OEMSVlets  = 13;   { Short var. len., packed array of char }
        OEMFGen    = 14;   { Fixed length array of unsigned byte }
        OEMVGen    = 15;   { Var. len. array of unsigned byte }
        OEMSVGen   = 16;   { Short var. len. array of unsigned byte }


        {edit check string length }

        ecstrBnd = 40;

Type

        {  stuff for lowest level of intrinsics }

        short = -32767..32767;

        ptrinteger = ^integer;
        ptrlongint = ^longint;

        fileptr = longint;
        ptrfileptr = ^ fileptr;

        ticket =
            record
                high : integer;
                low  : longint;
            end;
        ptrticket = ^ ticket;

        fstring = packed array[0..0] of char;
        ptrfstring = ^ fstring;

        fint8 =
            record
                high : longint;
                low  : longint;
            end;
        ptrfint8 = ^ fint8;

        vfld =
            record
                size : integer;
                offset : integer;
            end;
        ptrvfld = ^ vfld;

        svfld =
            packed record
                offset : 0..1023;
                size : 0..63;
            end;
        ptrsvfld = ^ svfld;

        bitrange = 0..15;
        elemset = set of bitrange;
        bits = array[0..0] of elemset;
        ptrbits = ^ bits;


        fieldrange = ftypnull .. ftypes;

        ptr_field =
            record case fieldrange of
                    ftypnull,
                    ftypint1,
                    ftyppack2 : ( ptr : ptrdata);
                    ftypint2 : ( pint2 : ptrinteger);
                    ftypint4 : ( pint4 : ptrlongint);
                    ftypint8 : ( pint8 : ptrfint8 );
                    ftypfptr : ( pfptr : ptrfileptr);
                    ftyptick : ( ptick : ptrticket);
                    ftypstr  : ( pstr  : ptrfstring);
                    ftypvstr,ftypvnull : (pvfld : ptrvfld);
                    ftypsvstr,ftypsvnull : (psvfld : ptrsvfld)
            end;

         bitoffset =
             record
                    nbits : bitrange;
                    sbit : bitrange;
             end;

        flddesc =
            record
                offset : integer;
                direction : byte;
                ftype : byte;
                case { ftype = ftyppack2 } boolean of
                false: (size : integer);
                true : (bo : bitoffset)
            end;

        recptr = array[0..0] of flddesc;
        ptrrecptr = ^ recptr;

        {  higher level file descriptor stuff }

        { Form header.  The following record appears at
          the beginning of each form. thus the form looks like:

                        ----------------
                        form header     |
                                        |
                        ----------------|
                                        |
                        field 0         |
                                        |
                        ----------------|
                                        |
                             ...        |
                                        |
                        ----------------|
                                        |
                        field nfields-1 |
                                        |
                        ----------------

        }

        V0header = { version 0 }
            record
                space: integer;                 { number of bytes for
                                                  rest of form }
                version : integer;              { software version }
                nfields: short;                 { # of fields in form }
                nkeys: short;                   { # of key fields in file }
                nsort: short;                   { # of key fields to put
                                                  in nonleaf pages }
                keybytes: short;                { bytes in fixed part of key}
                recsize: integer;               { bytes in fixed part of rec}
                flags: elemset;                 { Flags: future expansion}
            end;

        header =
            record
                space: integer;                 { number of bytes for
                                                  rest of form }
                version : integer;              { software version }
                nfields: short;                 { # of fields in form }
                nkeys: short;                   { # of key fields in file }
                nsort: short;                   { # of key fields to put
                                                  in nonleaf pages }
                keybytes: short;                { bytes in fixed part of key}
                recsize: integer;               { bytes in fixed part of rec}
                flags: elemset;                 { Flags: future expansion}
                MarketCode : integer;           { duh }
            end;


        {   A field record contains all the information on the field.
            it is composed of a fixed part and five variable length
            parts.  the variable length parts are found by adding the
            appropriate offset to the start of the field record.  if
            a part doesn't exist then the corresponding offset will be
            equal to fnilval.  all variable part offsets must be even,
            and, if where not = fnilval they must follow the relationship:

                name < display < highval < lowval < defval

                              --------------------
                                                  |
                               fixed part         |
                                                  |
                           ----name               |
                         --|---display            |
                       --|-|---highval            |
                     --|-|-|---lowval             |
                   --|-|-|-|---defval             |
                   | | | | |  --------------------|
                   | | | | |->name (string)       |
                   | | | |    --------------------|
                   | | | |--> edit check (string) |
                   | | |      --------------------|
                   | | |----> highval (same type  |
                   | |        as field)           |
                   | |        --------------------|
                   | |------> lowval (same type   |
                   |          as field)           |
                   |          --------------------|
                   |--------> defval (same type   |
                              as field)           |
                              --------------------




                secret meanings of the editcheck checkinfo
                ------------------------------------------

        type dt (date)

           for checkinfo[1]..checkinfo[3],
               1 stands for mm, 2 for dd and 3 for yy

           checkinfo[1]  =  1st field (mm/dd/yy) encountered
           checkinfo[2]  =  2nd field (mm/dd/yy) encountered
           checkinfo[3]  =  3rd field (mm/dd/yy) encountered
           checkinfo[4]  =  # digits in year field
           checkinfo[5]  =  type of month
                                (0 = numeric, 1 = abbr, 2 = full name )

     *  type num (number), tick (ticket), dol (money), or OEMTick (OEM ticket)

           checkinfo[1]  =  placement of sign (which char. position)
           checkinfo[2]  =  unused (cf. lawrence of toolkit)
           checkinfo[3]  =  # of digits in integer part
           checkinfo[4]  =  char position of decimal pt
           checkinfo[5]  =  # of digits after decimal pt

        type chk (check box)

           all unused

      * type zip (zip code), ss (social security number), lets (text)
           OEMFlets (fixed length text), OEMSVlets (short variable length text),

            checkinfo[1] = maximum number of chars
            checkinfo[2] = not used (for compatibility, set to 1)
            checkinfo[3] = allowed letters:
                    8 bit => special chars allowed
                    4 bit => digits allowed
                    2 bit => lower case alphabetics allowed
                    1 bit => upper case alphabetics allowed

            for zip checkinfo[2] must be 1 and checkinfo[3] must be 4
            for ss checkinfo[2] must be 1 and checkinfo[3] must not have
                bits 2 or 1 set

      * type OEMFGen (fixed uninterpreted unsigned array of byte),
           OEMVGen (variable uninterpreted unsigned array of byte) or
           OEMSVGen ( variable uninterpreted unsigned array of byte)

           checkinfo[1] = maximum number of chars

        type ph (phone number)

            all unused

        type tim (time)

            for checkinfo[1] .. checkinfo[3], 1 is hour, 2 minute, 3 second

            checkinfo[1] = 1st field type encountered (hh:mm:ss)
            checkinfo[2] = 2nd field type encountered (hh:mm:ss)
            checkinfo[3] = 3rd field type encountered (hh:mm:ss:am:pm)
            checkinfo[4] = 4th field type encountered (am:pm)
            checkinfo[5] = non - zero for 24 hour time (no am/pm)

            }

        checkinfo = array[1..5] of integer;

{       Actual string that hold the edit check }

        ecstr = string[ecstrBnd];

        ptrdbfield = ^dbfield;
        dbfield = record
            fldsize:    integer;
            name:       short;
            display:    short;
            highval:    short;
            lowval:     short;
            defval:     short;
            editcheck:  checkinfo;
            fldtype:    integer;
            options:    elemset;
            reptype:    short;
            offset:     integer;
            case {ispacked:} boolean of
                false:  (size: integer);
                true:   (bo:   bitoffset);
            end;


        monthstr = string[9];
        longstr = string[255];


        { various assembly kludges }

{***************************************************************************

                                movelg

        does a moveleft for the general case.  p1,p2,nbytes can all be
        odd or even.  about as fast as moveleft.

        Input Parameters
                p1 - pointer to source data
                p2 - pointer to dest data

        Output Parameters
                nbytes - # of bytes to transfer

***************************************************************************}

procedure movelg(p1,p2 : ptrdata;nbytes : integer);



{***************************************************************************

                                moverg

        does a moveright for the general case.  p1,p2,nbytes can all be
        odd or even.  about as fast as moveright.

        Input Parameters
                p1 - pointer to source data
                p2 - pointer to dest data

        Output Parameters
                nbytes - # of bytes to transfer

***************************************************************************}

procedure moverg(p1,p2 : ptrdata;nbytes : integer);


{*************************************************************************

                                movelf

        does an optimized moveleft.  p1 and p2 must be even. nbytes
        need not be.  3 times faster than moveleft for sufficiently
        large nbytes.

        Input Parameters
                p1 - pointer to source data
                p2 - pointer to dest data

        Output Parameters
                nbytes - # of bytes to transfer

**************************************************************************}

procedure movelf(p1,p2 : ptrdata;nbytes : integer);

{*************************************************************************

                                moverf

        does an optimized moveright.  p1 and p2 must be even. nbytes
        need not be.  3 times faster than moveright for sufficiently
        large nbytes.


        Input Parameters
                p1 - pointer to source data
                p2 - pointer to dest data

        Output Parameters
                nbytes - # of bytes to transfer

**************************************************************************}

procedure moverf(p1,p2 : ptrdata;nbytes : integer);

{*************************************************************************

                             extract

        extracts a packed field (unsigned).

        Input Parameters

                pbyte - ptr to word in which field is
                nbits - field width in bits (1..15)
                sbit  - rightmost bit # (lsb = 0)

        Output Parameter

                result is field value

**************************************************************************}

function extract(pbyte : ptrdata;nbit,sbit : integer) : integer;

{*************************************************************************

                             deposit

        deposits a packed field (unsigned).

        Input Parameters

                value - new value
                pbyte - ptr to word in which field is
                nbits - field width in bits (1..15)
                sbit  - rightmost bit # (lsb = 0)


**************************************************************************}

procedure deposit(value : integer;pbyte : ptrdata;nbit,sbit : integer);

{*************************************************************************

                             mult424

        multiplies an unsigned 32 bit # and an unsigned 16 bit #

        Input Parameters

                m4 - 32 bit multiplicand
                m2 - 16 bit multiplicand

        Output Parameters

                function value - 32 bit result

*************************************************************************}

function mult424(m4 : fileptr;m2 : integer) : fileptr;

{*************************************************************************

                              comp44

        unsigned 32 bit compare.

        Input Parameters

                s1 - first number
                s2 - next number

        Output Parameters

                result - result of comparison:lt,eq,gt

***************************************************************************}

function comp44(s1,s2 : fileptr) : integer;

{*************************************************************************

                              comphand

        unsigned 64 bit compare.

        Input Parameters

                s1 - first number
                s2 - next number

        Output Parameters

                result - result of comparison:lt,eq,gt

***************************************************************************}
function comphand(var s1,s2 : fint8) : integer;

{**************************************************************************

                              div424

        does an unsigned 32 bit by 16 bit divide, with remainder

        Input Parameters

                m4 - 32 bit dividend
                m2 - 16 bit divisor

        Output Parameters

                function result  - 32 bit quotient
                r2 - 16 bit remainder

***************************************************************************}

function div424(m4 : fileptr;m2 : integer;
                 var r2 : integer) : fileptr;

{**************************************************************************

                                setset

        creates the set of 0..lastelement-1 in a set of size
        nelements in a pageelements set

***************************************************************************}

procedure setset(pset : ptrdata;
                 lastelement,
                 nelements : integer);

{*****************************************************************************

                                clearset

        clears a pageelements set

*****************************************************************************}

procedure clearset(pset : ptrdata;
                    elements : integer);

{*****************************************************************************

                                clearbit

        clears a bit in a pageelements set

*****************************************************************************}

procedure clearbit(pset : ptrdata;
                    bit : integer);

{*****************************************************************************

                                inpset

        returns true in an element is in a pageelements set

*****************************************************************************}

function inpset(pset : ptrdata;
                bit : integer) : boolean;

{*****************************************************************************

                                setbit

        sets a bit in a pageelements set

*****************************************************************************}

procedure setbit(pset : ptrdata;
                  bit : integer);

{*****************************************************************************

                                pintersect

        determines if the intersection of two sets of pagebits is null.

*****************************************************************************}
function pintersect(pset1,
                    pset2 : ptrbits;
                    setelements : integer) : boolean;

{*****************************************************************************

                                compare

         performs a nfields-field compare between the entry pointed at by
         prec1 and the entry pointed at by prec2.  desc1 points to a field
         by field descriptor for prec1 which gives:

             location
             length
             type
             sort direction

         for each field of the entry. desc2 is for prec2.

*****************************************************************************}

function compare(desc1,
                 desc2 : ptrrecptr;
                 nfields : integer;
                 prec1,
                 prec2 : ptrdata) : integer;

{^T 20 }
{*************************************************************************

                                 extractkey

        unpacks fields out of a data record to form (part) of an index
        record.

        Input Parameters
                pdesc1 - describes location of flds in source
                pdesc2 - describes location of flds in dest
                nflds   - # of flds
                pentry  - ptr to source record
                pwhere  - ptr to dest record
                vflds   - where dest var. len. flds start



***************************************************************************}

procedure extractkey(pdesc1,
                     pdesc2 : ptrrecptr;
                     nflds : integer;
                     pentry : ptrdata;
                     pwhere : ptrdata;
                     vflds  : integer);

{***************************************************************************

                                findchanges

        finds all fields that are different in pbefore and pafter.  if
        field i differs then bit i is set in pset.  if a ticket field
        is changed then returns true else returns false

        Input Parameters
                pdesc1 - describes location of flds in pbefore and pafter
                nfields - # of fields
                pbefore - old record
                pafter - new record
                pset - pts to bitmap to record changes

         Output Parameters
                pset - bit i is set iff field i has changed
                findchanges - true iff a ticket field has changed

************************************************************************}

function findchanges(pdesc1 : ptrrecptr;
                     nfields : integer;
                     pbefore,
                     pafter : ptrdata;
                     pset : ptrbits) : boolean;

{**************************************************************************

                        GetRepType

   This procedure determines the representation type of a field given the
   high level data type and the checkinfo array generated by the
   EditCheck procedure. It also returns the size in bytes of the field.
   The edit check array need only be set if the data type is either dollars
   or numbers.

   GetRepType uses the representation type that the List Manager would use
   for a field.  Not all high level types nor all possible low level
   representations of a given high level type are supported.

   The following High Level Types are supported:

       SS
       Zip
       Ph
       Tick (6 byte, word boundary version only)
       Tim
       Dt
       Lets (Ftypvstr representation only)
       Dol
       Num (no ftyppacked representation)

   The following High Level Types are not supported
       Chk (check box)

   Input Paramters

       DataType - high level datatype of field
       CheckArray - edit check info for field, only needed for Dol and Num

   Return Results

       result - error code,

       0 - all ok
       42 - something was wrong

       RepType - low level data type
       Size - size in bytes of low level type


**************************************************************************}

PROCEDURE GetRepType(var result : integer;
                         DataType: INTEGER;
                         CheckArray: CheckInfo;
                     VAR RepType: Short;
                     VAR Size: INTEGER);

{!4-18-83.  ValidRepType works like GetRepType, except that it takes
            a reptype and size as input parameters and checks them for validity
            against CheckInfo array and high level data type.

                        ValidRepType

   This procedure determines the representation type of a field given the
   high level data type and the checkinfo array generated by the
   EditCheck procedure. It also returns the size in bytes of the field.
   The edit check array need only be set if the data type is either dollars
   or numbers.

   ValidRepType uses the representation type that the List Manager would use
   for a field.  Not all high level types nor all possible low level
   representations of a given high level type are supported.

   The following High Level Types are supported:

       SS
       Zip
       Ph
       Tick (6 byte, word boundary version only)
       Tim
       Dt
       Lets (Ftypvstr representation only)
       Dol
       Num (no ftyppacked representation)

   The following High Level Types are not supported
       Chk (check box)

   Input Paramters

       DataType - high level datatype of field
       CheckArray - edit check info for field, only needed for Dol and Num
       RepType - low level data type
       Size - size in bytes of low level type

   Return Results

       result - error code,

       0 - all ok
       42 - something was wrong
}

PROCEDURE ValidRepType(var result : integer;
                         DataType: INTEGER;
                         CheckArray: CheckInfo;
                         RepType: Short;
                         Size: INTEGER);

Implementation


procedure movelf{(p1,p2 : ptrdata;nbytes : integer)};
        external;

procedure moverf{(p1,p2 : ptrdata;nbytes : integer)};
        external;

procedure movelg{(p1,p2 : ptrdata;nbytes : integer)};
        external;

procedure moverg{(p1,p2 : ptrdata;nbytes : integer)};
        external;

function extract{(pbyte : ptrdata;nbit,sbit : bitrange) : integer};
        external;

procedure deposit{(value : integer;pbyte : ptrdata;nbit,sbit : bitrange)};
        external;

function mult424{(    m4 : fileptr;
                       m2 : integer) : fileptr};
        external;

function comp44{(s1,s2 : fileptr) : integer};
        external;

function comphand{(var s1,s2 : fint8) : integer};
        external;

function div424{(    m4 : fileptr;
                      m2 : integer;
                  var r2 : integer) : fileptr};
       external;

procedure setset{(pset : ptrdata;
                 lastelement,
                 nelements : integer)};
        external;

procedure clearset{(pset : ptrdata;
                    elements : integer)};
        external;

procedure clearbit{(pset : ptrdata;
                    bit : integer)};
        external;

function inpset{(pset : ptrdata;
                bit : integer) : boolean};
        external;


procedure setbit{(pset : ptrdata;
                  bit : integer)};
        external;

function pintersect{(pset1,
                    pset2 : ptrbits;
                    setelements : integer) : boolean};
        external;


function compare{(desc1,
                  desc2 : ptrrecptr;
                  nfields : integer;
                  prec1,
                  prec2 : ptrdata) : integer};
        external;

procedure extractkey{(pdesc1,
                      pdesc2 : ptrrecptr;
                      nflds : integer;
                      pentry : ptrdata;
                      pwhere : ptrdata;
                      vflds  : integer)};
        external;


function findchanges{(pdesc1 : ptrrecptr;
                     nfields : integer;
                     pbefore,
                     pafter : ptrdata;
                     pset : ptrbits) : boolean};
         external;

{$IFC DOLLARD }
{$D- }
{$ENDC}

{**************************************************************************

                        GetRepType

   This procedure determines the representation type of a field given the
   high level data type and the checkinfo array generated by the
   EditCheck procedure. It also returns the size in bytes of the field.
   The edit check array need only be set if the data type is either dollars,
   numbers, OEMtick, OEMFlets, OEMSVlets, OEMFGen, OEMVGen or OEMSVGen.

   GetRepType uses the representation type that the List Manager would use
   for a field.  Not all high level types nor all possible low level
   representations of a given high level type are supported.

   The following High Level Types are supported:

       SS
       Zip
       Ph
       Tick (6 byte, word boundary version only)
       Tim
       Dt
       Lets (Ftypvstr representation only)
       Dol
       Num (no ftyppacked representation)

       OEMtick
       OEMFlets
       OEMSVlets
       OEMFGen
       OEMVGen
       OEMSVGen

   The following High Level Types are not supported
       Chk (check box)

   Input Paramters

       DataType - high level datatype of field
       CheckArray - edit check info for field, only needed for Dol and Num

   Return Results

       result - error code,

       0 - all ok
       42 - something was wrong

       RepType - low level data type
       Size - size in bytes of low level type


**************************************************************************}



{$S swapaccs }
PROCEDURE GetRepType{(var result : integer;
                         DataType: INTEGER;
                         CheckArray: CheckInfo;
                     VAR RepType: Short;
                     VAR Size: INTEGER)};

   VAR TotalDigits: INTEGER;

   BEGIN

{$IFC fTraceDB}    LogCall;     {$ENDC}
   result := 0;

   CASE DataType OF
      SS: BEGIN
          RepType := FTypNull;
          Size := 5;
          END;

      Zip: BEGIN
           RepType := FTypNull;
           Size := 5;
           END;

      Ph: BEGIN
          RepType := FTypNull;
          Size := 5;
          END;

      Tick: BEGIN
            RepType := FTypTick;
            Size := 6;
            END;

      Tim: BEGIN
           RepType := FTypNull;
           Size := 3;
           END;

      Dt: BEGIN
          RepType := FTypNull;
          Size := 4;
          END;

      Lets: BEGIN
            RepType := FTypVStr;
            Size := 4;
            END;

      Dol,
      Num: BEGIN
           TotalDigits := CheckArray[3] + CheckArray[5];
           IF TotalDigits <= 2 THEN
              BEGIN
              RepType := FTypInt1;
              Size := 1;
              END

           ELSE IF TotalDigits <= 4 THEN
              BEGIN
              RepType := FTypInt2;
              Size := 2;
              END

           ELSE IF TotalDigits <= 9 THEN
              BEGIN
              RepType := FTypInt4;
              Size := 4;
              END

           ELSE IF TotalDigits <= 18 THEN
              BEGIN
              RepType := FTypInt8;
              Size := 8;
              END

           ELSE result := 42;
           END;

      OEMtick:
           BEGIN
           RepType := FTypTick;
           TotalDigits := CheckArray[3] + CheckArray[5];
           IF TotalDigits <= 2 THEN
              BEGIN
              Size := 1;
              END

           ELSE IF TotalDigits <= 4 THEN
              BEGIN
              Size := 2;
              END

           ELSE IF TotalDigits <= 7 THEN
              BEGIN
              Size := 3;
              END

           ELSE IF TotalDigits <= 9 THEN
              BEGIN
              Size := 4;
              END

           ELSE IF TotalDigits <= 12 THEN
              BEGIN
              Size := 5;
              END

           ELSE IF TotalDigits <= 14 THEN
              BEGIN
              Size := 6;
              END

           ELSE result := 42;
           END;

      OEMFlets :
           BEGIN
           reptype := FTypStr;
           size := CheckArray[1];
           if (size <= 0) or (size > maxrsize) then result := 42;
           END;

      OEMSVlets :
           BEGIN
           reptype := FTypSVstr;
           size := 2;
           END;

      OEMFGen :
           BEGIN
           reptype := FTypNull;
           size := CheckArray[1];
           if (size <= 0) or (size > maxrsize) then result := 42;
           END;

      OEMVGen :
           BEGIN
           reptype := FTypVNull;
           size := 4;
           END;

      OEMSVGen :
           BEGIN
           reptype := FTypSVNull;
           size := 2;
           END;

      OTHERWISE result := 42;

      END; {Case on datatype}
   END {GetRepType};

{!4-18-83.  Please note: ValidRepType should be put in dbdecl1 with getreptype after
            1st release.  ValidRepType works like GetRepType, except that it takes
            a reptype and size as input parameters and checks them for validity
            against CheckInfo array and high level data type.

                        ValidRepType

   This procedure determines the representation type of a field given the
   high level data type and the checkinfo array generated by the
   EditCheck procedure. It also returns the size in bytes of the field.
   The edit check array need only be set if the data type is either dollars
   or numbers.

   ValidRepType uses the representation type that the List Manager would use
   for a field.  Not all high level types nor all possible low level
   representations of a given high level type are supported.

   The following High Level Types are supported:

       SS
       Zip
       Ph
       Tick (6 byte, word boundary version only)
       Tim
       Dt
       Lets (Ftypvstr representation only)
       Dol
       Num (no ftyppacked representation)

   The following High Level Types are not supported
       Chk (check box)

   Input Paramters

       DataType - high level datatype of field
       CheckArray - edit check info for field, only needed for Dol and Num
       RepType - low level data type
       Size - size in bytes of low level type

   Return Results

       result - error code,

       0 - all ok
       42 - something was wrong



**************************************************************************}

{$S swapaccs }
PROCEDURE ValidRepType{(var result : integer;
                         DataType: INTEGER;
                         CheckArray: CheckInfo;
                         RepType: Short;
                         Size: INTEGER)};
var
        locreptype : short;
        locsize : integer;
begin
{$IFC fTraceDB}    LogCall;     {$ENDC}
GetRepType(result,DataType,CheckArray,locreptype,locsize);
if result = 0 then
   begin
   case DataType of
   Dol,
   Num :
       begin
       if (reptype <> locreptype) and (size < locsize) then result := 42;
       end;
   otherwise
       if (reptype <> locreptype) or (size <> locsize) then result := 42;
   end; { case }
   end;
end;

end.

ÿ