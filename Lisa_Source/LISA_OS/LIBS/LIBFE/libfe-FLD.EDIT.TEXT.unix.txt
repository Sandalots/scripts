
{****************************************************************************}
{$S FEothrwk}
procedure FirstEdit {hf:hndField; hfs:hndFState; Errnum:integer};
{
Function: Moves space to the insertion point to allow room for characters to be
          typed, pasted, etc.  Saves the previous state of the field in the undo
          buffer.

Assumes:  hfs is not nil, and a selection has been set in the field.

Note:     Handles must be re-dereferenced after calls to this procedure.

Errors:   NotUndoable

Calls:    DeleteFmt
          CompressedLp
}
var n, i:       integer;
    intSize:    integer;
    f:          ptrField;
    fs:         ptrFState;
    frp, urp:   ptrruns;
    fv, uv:     ptrdata;
    rlp:        integer;
    dummy:      boolean;

begin
{$IFC fTraceFE}    LogCall;     {$ENDC}
if fldTest then writeln ('Entering FirstEdit');

errnum := 0;

{ reset the new selection flag }
with hfs^^ do
    begin
    newSel := false;
    intSize := space.lpLim - bsPtr;
    end; { of with hfs^^ }

{ copy the current length, value, selection, backspace pointer, and space
    interval into the undo buffer }

with uInfo do
    begin
    { allocate space for undo buffer }                  { removed f := hf^ to fix #FE33 }
    ChangeSizeH (fldheap, pointer(ord(curValue)), hf^^.curLen);
    if cbDataofH(fldheap, pointer(ord(curValue))) < hf^^.curlen then
       begin
       errnum := FEnotundoable;
       exit(firstedit);
       end
    else
       begin
       ChangeSizeH (fldHeap, pointer(ord(fmtInfo)), hf^^.curFmts * fmtSize);
       if cbDataofH(fldheap, pointer(ord(fmtInfo))) < hf^^.curFmts * fmtSize then
          begin
          errnum := FEnotundoable;
          exit(firstedit);
          end
       end;

    if errnum = 0 then
       begin
       { move data into undo buffer }
       f := hf^;
       fs := hfs^;
       fv := f^.curvalue^;
       uv := curvalue^;
       curLen := f^.curLen;
       MoveLeft (fv^[0], uv^[0], fs^.bsPtr);
       MoveLeft (fv^[fs^.space.lpLim], uv^[fs^.bsPtr], (f^.maxlen - fs^.space.lpLim));

       { move formats into undo buffer }
       f := hf^;
       fs := hfs^;
       frp := f^.fmtInfo^;
       urp := fmtInfo^;
       curFmts := f^.curFmts;
       MoveLeft (frp^[0], urp^[0], f^.curFmts * fmtSize);

       { adjust formats for space }
       for i := 1 to (curFmts-1) do
          if urp^[i].lpFst > fs^.bsPtr then
              urp^[i].lpFst := urp^[i].lpFst - intSize;

       { adjust selection and anchor for space }
       select := fs^.select;
       if select.int.lpFst > fs^.bsPtr then
           select.int.lpFst := select.int.lpFst - intSize;
       if select.int.lpLim > fs^.bsPtr then
           select.int.lpLim := select.int.lpLim - intSize;
       anchor := fs^.anchor;
       if anchor.int.lpFst > fs^.bsPtr then
           anchor.int.lpFst := anchor.int.lpFst - intSize;
       if anchor.int.lpLim > fs^.bsPtr then
           anchor.int.lpLim := anchor.int.lpLim - intSize;
       end;{ if errnum = 0 }
    end; { of operations on uInfo }

with f^, fs^ do
    begin
    { move characters to make the space after the selection }
    if (select.int.lpLim >= space.lpLim) then
        begin
        { adjust formats in affected area (0th is never adjusted) }
        for i := 1 to (curFmts-1) do
            begin
            rlp := frp^[i].lpFst;
            if (rlp >= space.lpLim) and (rlp < select.int.lpLim) then
                frp^[i].lpFst := rlp - intSize
            else if (rlp > bsPtr) and (rlp < space.lpLim) then
                frp^[i].lpFst := bsPtr
            else if i < (curFmts-1) then
                if (rlp = select.int.lpLim) and (rlp = frp^[i+1].lpFst) then
                    frp^[i].lpFst := rlp - intSize;
            end;

        { adjust data }
        n := select.int.lpLim - space.lpLim;
        MoveLeft (curValue^^[space.lpLim], curValue^^[bsPtr], n);
        select.int.lpLim := bsPtr + n;
        if (select.int.lpFst >= space.lpLim) then
            select.int.lpFst := select.int.lpFst - intSize;
        if (anchor.int.lpFst >= space.lpLim) then
            anchor.int.lpFst := anchor.int.lpFst - intSize;
        if (anchor.int.lpLim >= space.lpLim) then
            anchor.int.lpLim := anchor.int.lpLim - intSize;
        end { of action to move the space right }
    else if (select.int.lpLim < bsPtr) then { move space to the left }
        begin
        { adjust formats in affected area (0th is never adjusted) }
        for i := 1 to (curFmts-1) do
            begin
            rlp := frp^[i].lpFst;
            if (rlp > select.int.lpLim) and (rlp <= bsPtr) then
                frp^[i].lpfst := rlp + intSize
            else if (rlp = select.int.lpLim) then
                { adjust the ending format only if either the selection or
                    the format are not 0-length }
                if (select.int.lpFst <> select.int.lpLim) then
                    frp^[i].lpFst := rlp + intSize
                else if (i = curFmts-1) then
                    begin
                    if  (rlp <> curLen) then
                        frp^[i].lpFst := rlp + intSize;
                    end
                else { i < curFmts-1 }
                    if (rlp <> frp^[i + 1].lpFst) then
                        frp^[i].lpFst := rlp + intSize;
            end; { of action for adjusting formats }

        { adjust data }
        n := bsPtr - select.int.lpLim;
        MoveRight (curValue^^[select.int.lpLim], curValue^^[space.lpLim - n], n);
        end; { of action to move space left }

    space.lpLim := select.int.lpLim + intSize;
    space.lpFst := select.int.lpLim;
    bsPtr := space.lpFst;
    validLp := space.lpFst;
    end; { of with f^, fs^ }

{ remove all null formats except at selection }
i := 0;
while i < (f^.curFmts-1) do
    if (frp^[i].lpFst = frp^[i + 1].lpFst) and
        not ((fs^.select.int.lpFst = fs^.select.int.lpLim) and
        (frp^[i].lpFst = fs^.select.int.lpFst)) then
        begin
        DeleteFmt (hf,i)
        end
    else i := i + 1;


if (CompressedLp (frp^[f^.curFmts-1].lpFst, hf, hfs, dummy) = f^.curLen)
    and (frp^[f^.curFmts-1].lpFst <> fs^.select.int.lpFst) then
    begin
    if fldTest then writeln ('Deleting last format; select begins at ',
        fs^.select.int.lpFst);
    DeleteFmt (hf, f^.curFmts-1);
    end;

if fldTest then
    begin
    writeln('Leaving FirstEdit');
{$IFC FLDDEBUG}
    DumpFldInfo (hf, hfs);
{$ENDC}
    end;

end; { of FirstEdit }

{****************************************************************************}
{$S FEothrwk}
procedure LastEdit {hf:hndField; hfs:hndFState};
{
Function: Save the selection which exits after the operation.

Assumes:  hfs is not nil, and a selection has been set in the field.
          FirstEdit was called to save off the undo information.

Note:     Handles must be re-dereferenced after calls to this procedure.

Calls:
}
begin
{$IFC fTraceFE}    LogCall;     {$ENDC}
with uinfo, hfs^^ do
   begin
   endselect := select;
   endanchor := anchor;
   end;
end;

{*****************************************************************************}
{$S FEothrwk}
procedure GrowFieldValue (hf:hndField; hfs:hndFState; growFactor:integer; var errnum: integer);
{
Function: Increases the size of the value array for the field identified by hf
          by the growLen * growFactor.

Assumes:  hfs may be nil; if it is not, pointers in hfs will be adjusted

Note:     Handles must be re-dereferenced after calls to this procedure.

Errors:   FEAllocationFailed
}
var growAmt:    integer;
    i, temp:    integer;
    rp:         ptrRuns;
    f:          ptrField;

begin
{$IFC fTraceFE}    LogCall;     {$ENDC}
if fldTest then writeln ('Entering GrowFieldValue');

errnum := 0;


{removed with and rereferenced handle to fix bug #FE37}
f := hf^;
growAmt := growFactor * f^.growLen;
{ increase the size by the growth amount }
ChangeSizeH (hzfromh(pointer(ord(f^.curvalue))), pointer(ord(f^.curValue)), f^.maxLen + growAmt);
f := hf^;
temp := cbDataofH(hzfromh(pointer(ord(f^.curValue))), pointer(ord(f^.curValue)));
if temp < f^.maxLen + growAmt then
   errnum := FEallocationFailed;
growAmt := temp - f^.maxlen;
f^.maxlen := temp;

{ move data past the space to the right, if applicable }
if hfs <> nil then
    with hf^^, hfs^^ do
        begin
        MoveRight (curValue^^[space.lpLim], curValue^^[space.lpLim + growAmt],
            maxLen - growAmt - space.lpLim);
        if select.int.lpFst > bsPtr then
            select.int.lpFst := select.int.lpFst + growAmt;
        if select.int.lpLim > bsPtr then
            select.int.lpLim := select.int.lpLim + growAmt;
        if anchor.int.lpFst > bsPtr then
            anchor.int.lpFst := anchor.int.lpFst + growAmt;
        if anchor.int.lpLim > bsPtr then
            anchor.int.lpLim := anchor.int.lpLim + growAmt;

        { adjust run logical positions }
        rp := fmtInfo^;
        for i := 1 to curFmts - 1 do
            { adjust them if they start after the backspace pointer, or if they
                start at the backspace pointer and are not zero length.
              Implementors note: At some point I ripped out the "or if they..."
              part but it won't work when insch causes a grow because the near
              walk into the unmoved fmt. }
            if (rp^[i].lpFst > bsPtr) then
                rp^[i].lpFst := rp^[i].lpFst + growAmt
            else
               if (rp^[i].lpFst = bsPtr) then
                  if (i < curFmts -1) then
                      begin
                      if not (rp^[i+1].lpfst in [bsPtr, space.lplim]) then
                         rp^[i].lpFst := rp^[i].lpFst + growAmt
                      end
                  else
                      rp^[i].lpFst := rp^[i].lpFst + growAmt;

        { now set the space right }
        space.lpLim := space.lpLim + growAmt;
        end; { of with and action if non-nil field state }

if fldTest then
    begin
    writeln ('Exiting GrowFieldValue; errnum is ',errnum:2,' field follows');
{$IFC FLDDEBUG}
    DumpFldInfo (hf, hfs);
{$ENDC}
    end;
end; { of GrowFieldValue }


{*****************************************************************************}
{$S FEothrwk}
function ActualIntSize (fs:ptrfstate; lpFst:integer; lpLim:integer): integer;
{
Function: Given a field state and an interval, checks to see if the interval
          includes invalid space and subtracts it off if it does.  Returns the
          size without space included.
}
var asize: integer;

begin
{$IFC fTraceFE}    LogCall;     {$ENDC}
asize := lpLim - lpFst;
if ((lpFst <= fs^.bsPtr) and (lpLim >= fs^.space.lpLim)) then
    asize := asize - (fs^.space.lpLim - fs^.bsPtr);
ActualIntSize := asize;
end; { of ActualIntSize }


{*****************************************************************************}
{$S FEothrwk}
procedure FldReplace1 (hf:hndField; hfs:hndFState; lpFstDel:integer; lpLimDel:
    integer; insBytes:integer);
{
Function: Replaces the deletion interval with another interval of specified
          length.  Moves other characters in the field to make exactly the
          right amount of room for the new interval.  Updates space and
          backspace pointers appropriately.

Assumes:  Hfs may be nil.
          Any space interval follows the deletion interval.
          There is enough room in the field for the replacement values less the
              replaced values.

Calls:    FmtFromLp
          DeleteFmt
}
var moveAmt:  integer;
    numBytes: integer;
    fmt, i:   integer;
    f:        ptrField;
    rp:       ptrRuns;
    dummy:    boolean;

begin
{$IFC fTraceFE}    LogCall;     {$ENDC}
if fldTest then
   writeln('entering FldRepace1 delete from ',lpFstDel,' to  ',lpLimDel,
   ' insBytes is ',insBytes);

moveAmt := (lpLimDel - lpFstDel) - insBytes;
f := hf^;
rp := f^.fmtinfo^;

{ delete all formats entirely within the delete interval and adjust the last
    formats as needed }
if f^.curFmts > 1 then
    begin
    fmt := FmtFromLp (hf, hfs, lpLimDel);
    if (rp^[fmt].lpFst > lpFstDel) and (hfs <> nil) and
       (compressedlp(lplimdel,hf,hfs,dummy) < f^.curlen) then
         rp^[fmt].lpFst := hfs^^.space.lpLim;
    i := 1;
    while i <= (f^.curFmts-1)  do
        if (rp^[i].lpFst > lpFstDel) and (rp^[i].lpFst < lpLimDel) then
             begin
             if (rp^[i].font <> rp^[0].font) and (hfs <> nil)
                 then
                 hfs^^.validLp := 0;
             DeleteFmt (hf, i);
             end
        else
             i := i + 1;
    end;

if (hfs <> nil) then
    with hfs^^ do
        begin
        bsPtr := bsPtr - moveAmt;
        space.lpFst := bsPtr;
        if (validLp > lpFstDel) then
            validLp := lpFstDel;
        end;
f^.curLen := f^.curLen - moveAmt;
if fldtest then writeln('leaving fldreplace1');
end; { of FldReplace1 }


{*****************************************************************************}
{$S FEothrwk}
procedure Replace (hfDel:hndField; hfsDel:hndFState; lpFstDel:integer;
    lpLimDel:integer; hfIns:hndField; hfsIns:hndFState; lpFstIns: integer;
    lpLimIns:integer; replaceFmt: boolean);
{
Function: Replaces (and deletes) an interval of data in the curValue array of
          the hfDel field with an interval of data in the curValue array of the
          hfIns field. If repaceFmt is true then replaces (and deletes) any
          format runs associated with the data in hfDel with format runs associated
          with data in hfIns.

Assumes:  There is enough space in the field for the replacement values less the
              replaced values.
          There is enough space for the replacement formats less the replaced
              formats.
          Any invalid space in the Ins interval will be ignored.
          Either field state may be nil.
          If there are characters following the deletion interval, they also
              follow space (thus hfsDel is not nil).

Calls:    FmtFromLp
          FldReplace1
          ActualIntSize
          CompressedLp
          ExpandedLp
          GetFmt
          RemoveDupFmts
}
var temp:          byte;
    fIns, fDel:    ptrField;
    lpDel,lpIns:   integer;
    limDel,fstDel: integer;
    fstIns,limIns: integer;
    insAmt:        integer;
    i,spot:        integer;
    dummy,fmtAtEnd:boolean;
    oldFmt:        run;
    irp, drp:      ptrRuns;
    oldFDelLen:    integer;
    tempLpLimIns:  integer;

begin
{$IFC fTraceFE}    LogCall;     {$ENDC}
if fldTest then writeln ('Entering Replace (', lpFstDel, lpLimDel, ' with ',
    lpFstIns, lpLimIns, ')');
fIns := hfIns^;
fDel := hfDel^;

{ insert an interval of the appropriate length }
oldFDelLen := fDel^.curLen;
if (hfsIns <> nil) then
    insAmt := ActualIntSize (hfsIns^, lpFstIns, lpLimIns)
else
    insAmt := lpLimIns - lpFstIns;

FldReplace1 (hfDel, hfsDel, lpFstDel, lpLimDel, insAmt);

{ if there are characters to be substituted or moved, do so now }
if lpFstIns <> lpLimIns then
    begin
    { if needed, insert the formats into the destination }
    if replaceFmt then
        begin
        irp := fIns^.fmtInfo^;
        drp := fDel^.fmtInfo^;
        fstDel := FmtFromLp (hfDel, hfsDel, lpFstDel);
        oldFmt := drp^[fstDel];
        limDel := FmtFromLp (hfDel, hfsDel, lpLimDel);
        fstIns := FmtFromLp (hfIns, hfsIns, lpFstIns);
        { we don't want to add a 0-length run at the very end of the interval,
            so back up one }
        if hfsIns <> nil then
            begin
            if lpLimIns = hfsIns^^.space.lpLim then
                tempLpLimIns := hfsIns^^.bsPtr - 1
            else
                tempLpLimIns := lpLimIns - 1;
            end
        else
            tempLpLimIns := lpLimIns - 1;
        limIns := FmtFromLp (hfIns, hfsIns, tempLpLimIns);
        if fldTest then
            writeln ('Inserting ', limIns - fstIns, ' runs');

        { check if there is a format in the destination at the end of the
            insertion interval; if not, we'll have to add one there }
        fmtAtEnd := false;
        if (limDel < fDel^.curFmts-1) and (hfsDel <> nil) then
            if (drp^[limDel + 1].lpFst = hfsDel^^.space.lpLim) then
                fmtAtEnd := true;

        if fldTest then
            writeln ('Replacing formats ', fstDel, limDel, ' with ', fstIns,
                limIns);

    {Deal with that nasty first format record. the three logical cases are
      1) if limdel format = fstins fmt then do nothing
      2) if limdel.lpst = lpfstdel then stuff fstins into limdel
      3) get a format and stuff fstins into it}

        if (drp^[limDel].font = irp^[FstIns].font) and
                    (drp^[limDel].face = irp^[FstIns].face) then
           {do nothing}
        else
            if (lpFstDel = drp^[LimDel].lpfst) then
                begin
                drp^[LimDel].face := irp^[FstIns].face;
                drp^[LimDel].font := irp^[FstIns].font;
                end
            else
                begin
                LimDel := LimDel + 1;
                GetFmt(hfdel,LimDel,lpFstDel);
                drp^[LimDel].face := irp^[FstIns].face;
                drp^[LimDel].font := irp^[FstIns].font;
                end;

        { put each format in the source into the destination }
        if FstIns <> LimIns then
          for i := fstIns + 1 to limIns do
            begin
            { calculate the lp for the run relative to the start of the insert
                interval }
            spot := CompressedLp (irp^[i].lpFst, hfIns, hfsIns, dummy);
            if spot <= lpFstIns then
                  spot := 0
            else
                  spot := spot - lpFstIns;

            { if there is a font change redraw the entire field }
            if (irp^[i].font <> drp^[fstDel].font) and (hfsDel <> nil) then
                hfsDel^^.validLp := 0;

            LimDel := LimDel + 1;
            GetFmt (hfDel, limDel, lpFstDel + spot);
            drp^[limDel].face := irp^[i].face;
            drp^[limDel].font := irp^[i].font;

            end; { of loop for putting in ins formats }

        { add a format to the end of the interval if there isn't one already and
            we are not at the end of the field }
        if (not fmtAtEnd) and (lpLimDel < oldFDelLen) and
            (limDel < fDel^.maxFmts-1) then
            begin
            if fldTest then writeln ('Adding format at the end');
            limDel := limDel + 1;
            GetFmt (hfDel, limDel, 0);
            drp^[limDel] := oldFmt;
            drp^[limDel].lpFst := ExpandedLp (lpFstDel + insAmt, hfDel, hfsDel);
            end; { of action to add a format at the end }

        end; { of action to replace the runs }

    { put the characters from the ins array into the del array at lpFstDel }
    lpIns := lpFstIns;
    for lpDel := lpFstDel to (lpFstDel + insAmt - 1) do
        begin
        if (hfsIns <> nil) then
            if (lpIns = hfsIns^^.bsPtr) then
                lpIns := hfsIns^^.space.lpLim;
        fDel^.curValue^^[lpDel] := fIns^.curValue^^[lpIns];
        lpIns := lpIns + 1;
        (*
        if (hfsIns <> nil) then
            if (lpIns = hfsIns^^.bsPtr) then
                lpIns := hfsIns^^.space.lpLim;
        *)
        end;
    end; { of action for inserting a > 0-length interval }

if replaceFmt then
    RemoveDupFmts (hfDel);

if fldTest then begin
writeln('leaving replace - field is ');
{$IFC FLDDEBUG}
DumpFldInfo (hfDel, hfsDel);
{$ENDC}
end;

end; { of Replace }


{*****************************************************************************}
{$S FEothrwk}
procedure InsCh {ch:char; hf:hndField; hfs:hndFState; var errNum:integer};
{
Function: Inserts a new character from the keyboard into the array of characters
          underlying the displayed field on the screen.

Assumes:  Selection has been set in field
          Flag indicates if a new selection was just done

Note:     Handles must be re-dereferenced after calls to this procedure.

Calls:    FirstEdit
          ActualIntSize
          Replace
          RemoveBSRuns
          GrowFieldValue

Errors:   field full (i.e., no room in curvalue array)
          field protected
}
label 100;      { error exit }

var i: integer;
    saveNewSel: boolean;

begin
{$IFC fTraceFE}    LogCall;     {$ENDC}
if fldTest then writeln ('Entering InsCh');

errnum := 0;           {******** to fix bug fe29 **********}

with hf^^, hfs^^ do
    begin
    { see if the field is protected or the field is full; if so, exit }
    if protect then
        begin
        errNum := fldProtected;
        goto 100;
        end;
    if (curLen - ActualIntSize (hfs^, select.int.lpFst, select.int.lpLim))
        = maxLen then
        if (growLen = 0) or (maxlen = maxFieldLngth) then
            begin
            errNum := fldFull;
            goto 100;
            end
        else
            begin
            GrowFieldValue (hf, hfs, 1, errnum);
            if errnum <> 0 then
               begin
               uInfo.lastOp := noOP;
               goto 100;
               end;
            end;
    end; { of with hf^^, hfs^^ }

{ if this is the first edit after a selection then }
saveNewSel := hfs^^.newsel;
if saveNewSel then  { set up space for insertion }
    begin
    FirstEdit (hf, hfs, errnum);
    if errnum > 0 then
       goto 100
    end
else   { otherwise, remove any runs backspaced over }
    RemoveBSRuns (hf, hfs);

{ save the operation type }
if errnum = 0 then
   uInfo.lastOp := chInputOp
else
   uInfo.lastOp := noOP;

{ if necessary, replace the selection with a 0-length selection and reset
    pointers }
if hfs^^.select.tyHilight <> tyHiCaret then
    begin
    Replace (hf, hfs, hfs^^.select.int.lpFst, hfs^^.select.int.lpLim, hf, hfs,
        hfs^^.select.int.lpFst, hfs^^.select.int.lpFst, false);
    with hfs^^ do
        begin
        select.tyHilight := tyHiCaret;
        selectRect.botRight.h := selectRect.topLeft.h + 1;
        end;
    end;

with hf^^, hfs^^ do
    begin
    { do the actual insertion of the character and update pointers and lengths }
    curValue^^[bsPtr] := ord(ch);
    bsPtr := bsPtr + 1;
    space.lpFst := bsPtr;
    curLen := curLen + 1;
    { set the valid flag to indicate that the display is different than the actual
        contents }
    valid := false;

    { update the selection pointers }
    select.int.lpFst := select.int.lpFst + 1;
    select.int.lpLim := select.int.lpFst;
    anchor := select;

    (*
    if saveNewSel then          {***** removed to fix bug fe28 ******}
    *)
       LastEdit (hf, hfs);

    { set the changed flag to true }
    changed := true;
    end; { of with hf^^, hfs^^ }

errNum := 0;

100: end; { of InsCh }


{****************************************************************************}
{$S FEothrwk}
procedure Backspace {hf:hndField; hfs:hndFState; var errNum:byte};
{
Function: Backspaces one character position; updates the pointer in the array
          of characters which reflects the display of the field.

Assumes:  Selection already exists in field.

Errors:   Invalid edit - operation not done
          Field protected

Calls:    FirstEdit
}
label 100;      { error exit }

var bsAmt: integer;
    savespace:  integer;
    saveNewSel: boolean;
    dummy:      boolean;

begin
{$IFC fTraceFE}    LogCall;     {$ENDC}
if fldTest then writeln ('Entering Backspace');

{ set the error flag to indicate no errors }
errNum := 0;

with hf^^, hfs^^ do
    begin
    { if the field is protected then set an error condition and exit }
    if protect then
        begin
        errNum := fldProtected;
        goto 100;
        end;

    { if the selection's length is greater than 0 or the selection is at the
        beginning of the field, then set an error condition and exit }
    if (compressedLp(select.int.lpLim, hf, hfs, dummy) = 0) then  {added compressedLp to fix FE40}
        begin
        errNum := invalidEdit;
        goto 100;
        end;

    { if this is the first edit after a selection then set up the field for typing }
    saveNewSel := newSel;
    if saveNewSel then
        FirstEdit (hf, hfs, errnum);
    end; { of with hf^^, hfs^^ }

if errnum = 0 then
   uInfo.lastOp := chInputOp
else
   uInfo.lastOp := noOp;

with hf^^, hfs^^ do
    begin
    bsAmt := select.int.lpLim - select.int.lpFst;
    if bsAmt = 0 then bsAmt := 1
    else { selection is greater than 0-length; change it to caret type }
        begin
        select.tyHilight := tyHiCaret;
        select.tySel := tySelPt;
        end;

    curLen := curLen - bsAmt;
    bsPtr := bsPtr - bsAmt;
    if validLp > 0 then
        validLp := bsPtr;
    { update the selection pointers }
    select.int.lpFst := bsPtr;
    select.int.lpLim := bsPtr;
    anchor := select;

    (*
    if saveNewSel then          {***** removed to fix bug fe30 ******}
    *)
        LastEdit (hf, hfs);

    { set the field valid and changed flags to indicate a change }
    valid := false;
    changed := true;
    end;

100: end; { of BackSpace }



{****************************************************************************}
{$S FEcold}
procedure ForwardSpace {hf:hndField; hfs:hndFState; var errNum:byte};
{
Function: Forward spaces one character position, providing a backspace was
          previously done.  Updates the backspace pointer in the array of
          characters which reflects the display of the field.

Assumes:  Selection exists in the field.

Errors:   Invalid edit (no backspace to forwardspace over) - operation not done
}
label 100;

begin
{$IFC fTraceFE}    LogCall;     {$ENDC}
if fldTest then writeln ('Entering ForwardSpace');

with hfs^^ do
    begin
    { if the backspace pointer points to the beginning of space or beginning of >
        0-length selection, then set an error condition (no backspace) and exit }
    if (bsPtr = space.lpFst) or (select.int.lpFst <> select.int.lpLim) or newSel
        then
        begin
        errNum := invalidEdit;
        goto 100;
        end;

    { update the backspace and selection pointers }
    bsPtr := bsPtr + 1;
    hf^^.curLen := hf^^.curLen + 1;
    select.int.lpFst := bsPtr;
    select.int.lpLim := bsPtr;
    anchor.int := select.int;

    { set the field valid flag to indicate a change }
    valid := false;
    changed := true;
    end; { of with }

{ set the error flag to indicate no errors }
errNum := 0;

100: end; { of ForwardSpace }


ÿ