{$S FEothrwk}
{*****************************************************************************}
procedure growFormats(hf: hndfield; growRequest:integer; var errnum: integer);
{
Function: Attempts to allocate enough space for growRequest number of format records.
Errors:   ChangeSizeH returned insufficient space.
}
var
  tempZone: THz;
  f: ptrfield;
  actualGrow: integer;
begin
{$IFC fTraceFE}    LogCall;     {$ENDC}
    errnum := 0;
    f := hf^;
    tempZone := hzfromh(pointer(ord(f^.fmtInfo)));
    ChangeSizeH (tempZone, pointer(ord(f^.fmtInfo)), growRequest * fmtsize);
 { check allocation }
    f := hf^;
    actualGrow := cbdataofH(tempZone, pointer(ord(f^.fmtInfo)));
    f^.maxFmts := actualGrow div fmtsize;
    if actualGrow < growRequest * fmtsize then
       errnum := FEallocationFailed;
end;

{*****************************************************************************}
{$S FEothrwk}
procedure GetFmt (hf:hndField; index, lpFst:integer);
{
Function: Frees up a run at array location index, if there is one available,
          in the field pointed to by hf.

Assumes: Space for the additional format has been allocated.
}
label 100; { error exit }

var i: integer;
    rp:ptrRuns;
    f: ptrField;

begin
{$IFC fTraceFE}    LogCall;     {$ENDC}
    f := hf^;
    rp := f^.fmtInfo^;

    if f^.curFmts + 1 > f^.maxFmts then
        begin
        if fldTest then
            writeln ('Tried to get format when none was available');
        goto 100;
        end;

    for i := f^.curFmts downto index+1 do
        rp^[i] := rp^[i-1];
    f^.curFmts := f^.curFmts + 1;
    if index = 0 then
        rp^[0].face := []
    else
        rp^[index] := rp^[index-1];
    rp^[index].lpFst := lpFst;

100: end; { of GetFmt }


{*****************************************************************************}
{$S FEothrwk}
procedure DeleteFmt (hf:hndField; index:integer);
{
Function:  Deletes the index'th entry from the run array for the field pointed
           to by hf.
}
var i: integer;

begin

{$IFC fTraceFE}    LogCall;     {$ENDC}
with hf^^ do
    if curFmts > 1  then
         begin
         {if (curFmts > 2) then}
         for i := index to curFmts - 2 do
             fmtInfo^^[i] :=  fmtInfo^^[i + 1];
         curFmts := curFmts - 1;
         end;
end; { of DeleteFmt }


{*****************************************************************************}
{$S FEothrwk}
procedure RemoveDupFmts (hf:hndField);
{
Function:  Removes redundant formats.

Calls: DeleteFmt
}
var found: boolean;
    i:     integer;
    rp:    ptrRuns;

begin
{$IFC fTraceFE}    LogCall;     {$ENDC}
{ Remove redundant formats }
found := false;
i := 0;
with hf^^ do
    begin
    rp := fmtInfo^;
    while not found do
        if i < curFmts-1 then
            if (rp^[i].font = rp^[i+1].font) and (rp^[i].face = rp^[i+1].face)
                then
                begin
                if fldTest then writeln ('Deleting format ', i + 1);
                DeleteFmt(hf, i + 1);
                end
            else
                i := i + 1
        else
            found := true;
    end; { of with }
end; { of RemoveDupFmts }


{*****************************************************************************}
{$S FEothrwk}
procedure RemoveBSRuns (hf:hndField; hfs:hndFState);
{
Function: Removes runs backspaced over, and sets the last one to the end of the
          space interval, if necessary.

Assumes:  hfs is not nil.

Calls:    RemoveDupFmts
          DeleteFmt
}
var i:  integer;
    rp: ptrRuns;

begin
{$IFC fTraceFE}    LogCall;     {$ENDC}
with hf^^, hfs^^ do
    if curFmts > 1 then
        for i := (curFmts-1) downto 1 do
            begin
            rp := fmtInfo^;
            if (rp^[i].lpFst > bsPtr) and (rp^[i].lpFst < space.lpLim) then
                begin
                rp^[i].lpFst := space.lpLim;
                if (i < curFmts-1) then
                    begin
                    if rp^[i].lpFst = rp^[i+1].lpFst then
                        begin
                        if (rp^[i].font <> rp^[i+1].font) then
                            { removing a font may change the content
                                coordinates }
                            validLp := 0;
                        DeleteFmt (hf, i);
                        end;
                    end
                else { i = curFmts-1 }
                    if rp^[i].lpFst = maxLen then { no characters at the end }
                        begin
                        if (rp^[i].font <> rp^[0].font) then
                            validLp := 0;
                        DeleteFmt (hf, i);
                        end;
                end; { of action for deleting a single format run }
            end; { of for loop }
RemoveDupFmts (hf);
end; { of RemoveBSRuns }

{*****************************************************************************}
{$S FEothrwk}
procedure SetRunInterval {hf:hndField; hfs:hndFState; fontNum:integer;
              format:integer; int:interval; var errNum:integer};
{
Function:  Sets the interval specified to the type of formatting in format and
           removes any redundant runs generated. For all types of formatting
           except setNormal the formatting change is "or'd" with the existing
           formatting. SetNormal sets all formatting in the interval to false.

Assumes:   hfs may be nil.

Note:      Handles must be re-dereferenced after calls to this procedure.

Errors:    not enough room for the new runs (noRoomForRuns)
           field is protected (fldProtected)

Calls:     FmtFromLp
           GetFmt
           SetFmt
           RemoveDupFmts
           FirstEdit
}
label 100; { error exit }

var
   fmtLst,fmtFst: integer;
   tempLpFst:     integer;
   runIndex:      integer;
   f:             ptrField;
   fs:            ptrFState;
   rp:            ptrruns;
   spaceNeeded:   integer;
   dummy:         boolean;
   intSize:       integer;
   fmtsToAdd:     integer;
   tempZone:      THz;
   growRequest, actualGrow:    integer;

begin
{$IFC fTraceFE}    LogCall;     {$ENDC}
if fldTest then
    writeln ('Entering SetRunInterval');
errNum := 0;
f := hf^;
rp := f^.fmtInfo^;

with int do
    begin
    { find the format associated with lpFst and lpLim }
    tempLpFst := lpFst;
    if hfs <> nil then
        begin
        fs := hfs^;
        if lpFst = fs^.space.lpLim then
            tempLpFst := fs^.bsPtr;
        end;
    fmtFst := FmtFromLp (hf, hfs, tempLpFst);
    fmtLst := FmtFromLp (hf, hfs, lpLim);

    { determine if there is enough run space available }
    spaceNeeded := 0;
    if (rp^[fmtLst].lpFst < lpLim) then { a run is needed at the end for the
        remainder of text not included in the selection }
        spaceNeeded := spaceNeeded + 1;
    if (rp^[fmtFst].lpFst < lpFst) then  { a run is needed to start the new format
        sequence }
        spaceNeeded := spaceNeeded + 1;
    if (spaceNeeded = 0) and (lpFst = lpLim) then
        { need to add a zero-length run unless there is already one there }
        if ((fmtFst = f^.curFmts-1) and
            (CompressedLp (rp^[fmtFst].lpFst, hf, hfs, dummy) < f^.curLen)) then
            spaceNeeded := 1
        else
            if (fmtFst < f^.curFmts-1) then
                if (CompressedLp (rp^[fmtFst].lpFst, hf, hfs, dummy) <>
                    CompressedLp (rp^[fmtFst+1].lpfst, hf, hfs, dummy)) then
                    spaceNeeded := 1;
    if f^.curFmts + spaceNeeded > f^.maxFmts then
        if f^.growFmts = 0 then
            begin
            errNum := noRoomForRuns;
            goto 100;
            end
        else
         { allocate more run space }
            begin
            fmtsToAdd := f^.growFmts;
            if f^.maxFmts + fmtsToAdd < f^.curFmts + spaceNeeded then
                fmtsToAdd := fmtsToAdd + f^.growFmts;
            growFormats(hf, f^.maxFmts + fmtsToAdd, errnum);
            if errnum <> 0 then
               goto 100
            end; { of action for allocating more run space }

    if hfs <> nil then
        begin
        fs := hfs^;
        { if this is the first edit after the selection save stuff for undo }
        if fs^.newSel then
            begin
            { adjust the interval if it is after the space }
            intSize := fs^.space.lpLim - fs^.bsPtr;
            if (lpFst >= fs^.space.lpLim) and (lpFst <= fs^.select.int.lpLim) then
                lpFst := lpFst - intSize;
            if (lpLim >= fs^.space.lpLim) and (lpLim <= fs^.select.int.lpLim) then
                lpLim := lpLim - intSize;
            FirstEdit (hf, hfs, errnum);
            fmtFst := FmtFromLp (hf, hfs, lpFst);
            fmtLst := FmtFromLp (hf, hfs, lpLim);
            fs := hfs^;
            end; { of action for new selection }

        fs^.validLp := 0;
        fs^.changed := true;
        fs^.valid := false;
        end; { of action for non-nil field state }

    { adjust end of change interval }
    f := hf^;
    rp := f^.fmtInfo^;
    if rp^[fmtLst].lpFst < lpLim then { add format }
        begin
        { keep track of space needed that has been actually allocated }
        spaceNeeded := spaceNeeded - 1;
        fmtLst := fmtLst + 1;
        if CompressedLp (lpLim, hf, hfs, dummy) <> f^.curLen then
            { there are characters after this in the field, so add the run }
            begin
            GetFmt (hf, fmtLst, lpLim);
            f := hf^;
            rp := f^.fmtInfo^;
            end;
        if hfs <> nil then
            begin
            fs := hfs^;
            if lpLim = fs^.bsPtr then
                rp^[fmtLst].lpFst := fs^.space.lpLim;
            end;
        rp^[fmtLst].font := rp^[fmtLst-1].font;
        rp^[fmtLst].face := rp^[fmtLst-1].face;
        end; { of action for adjusting end of selection }

    { adjust beginning of selection }
    if rp^[fmtFst].lpFst < lpFst then { add format }
        begin
        spaceNeeded := spaceNeeded - 1;
        fmtFst := fmtFst + 1;
        fmtLst := fmtLst + 1;
        GetFmt (hf, fmtFst, lpFst);
        end
    else
        if (hfs <> nil) and (spaceNeeded = 0) then
            with hfs^^ do
                if (lpFst = bsPtr) and (hf^^.fmtInfo^^[fmtFst].lpFst =
                    space.lpLim) then
                    hf^^.fmtInfo^^[fmtFst].lpFst := bsPtr;
    { see if we added all the formats we figured we needed }
    if spaceNeeded > 0 then
        begin
        fmtLst := fmtLst + 1;
        GetFmt (hf, fmtFst, lpFst);
        with hfs^^ do
            if hf^^.fmtInfo^^[fmtLst].lpFst = bsPtr then
                hf^^.fmtInfo^^[fmtLst].lpFst := space.lpLim;
        end;

    SetFmt (hf^^.fmtInfo^^[fmtFst], fontNum, format);

    { adjust runs totally enclosed within this interval }
    if fmtFst < (fmtLst-1) then
        for runIndex := fmtFst + 1 to fmtLst - 1 do
            SetFmt (hf^^.fmtInfo^^[runIndex], fontNum, format);
    RemoveDupFmts (hf);

    end; { of with }
100:
if fldtest then
  begin
  writeln('exiting setruninterval');
{$IFC FLDDEBUG}
  dumpfldinfo(hf,hfs);
{$ENDC}
  end;
end; { of SetRunInterval }


{*****************************************************************************}
{$S FEothrwk}
procedure SetRunFormat {hf:hndField; hfs:hndFState; fontNum:integer;
              format:integer; var errNum:byte};
{
Function:  Sets the selection to the type of formatting in format at the
           request of the application, as opposed to the user.  The difference
           between this and UpdateRun is that changes done through SetRunFormat
           are not separately undoable as are changes done through UpdateRun,
           which is intended to be used when a menu request from the user to
           change a format is received.

Assumes:   If the field state is nil, the update will be applied all runs.
           The protection flag is not checked.

Note:      Handles must be re-dereferenced after calls to this procedure.

Errors:    not enough room for the new runs (noRoomForRuns)

Calls:     SetFmt
           SetRunInterval
}
var
  i: integer;

begin { of SetRunFormat }
{$IFC fTraceFE}    LogCall;     {$ENDC}
if fldtest then
   writeln('setrunformat');

errNum := 0;

{ if hfs is nil apply change to entire field }
if hfs = nil then
    with hf^^ do
        for i := 0 to curfmts - 1 do
           begin
           SetFmt (fmtInfo^^[i], fontNum, format);
           end
else
    SetRunInterval (hf, hfs, fontNum, format, hfs^^.select.int,
        errNum);

if fldtest then
   writeln('exiting setrunformat');
end; { of SetRunFormat }


{*****************************************************************************}
{$S FEothrwk}
procedure UpdateRun {hf:hndField; hfs:hndFState; fontNum:integer;
              format:integer; var errNum:byte};
{
Function:  Sets the selection to the type of formatting in format and removes
           any redundant runs generated. For all types of formatting except
           setNormal the formatting change is "or'd" with the existing
           formatting. SetNormal sets all formatting in the selection to false.
           This procedure should be used for all user-requested format changes.

Assumes:   hfs may not be nil (i.e., the field must be set up for editing).

Note:      Handles must be re-dereferenced after calls to this procedure.

Errors:    not enough room for the new runs (noRoomForRuns)
           field is protected (fldProtected)

Calls:     SetRunFormat
}
begin

{$IFC fTraceFE}    LogCall;     {$ENDC}
if hf^^.protect then
    errNum := fldProtected
else { ok to do the update }
    begin
    { establish the new run }
    SetRunFormat (hf, hfs, fontNum, format, errNum);

    { even though the selection hasn't been changed, we want this operation to
        be separately undoable; set the newSel flag to cause this }
    hfs^^.newSel := true;
    if errnum = 0 then
        uInfo.lastOp := formatOp
    else
        uInfo.lastOp := noOp;
    end;

end; { of UpdateRun }

ÿ