
{******************************************************************************}
{$S FEcutCopy}
procedure DrawScrap{var errnum: integer};
{
Function: Draws the scrap field in ScrapFolder.

Assumes:  A port, in which the scrap will be shown, exists and is pointed to by
              ScrapFolder (a GrafPtr).

Errors:   Scrap is not of type field.
          Failed on call to StartPutScrap

Calls:    DrawField
}
var tempPort:    Grafptr;
    scrapRect:   Rect;
    ScrapKind:   ScrapType;
    Hnd:         TH;
    dummy:       TC;

begin

{$IFC fTraceFE}    LogCall;     {$ENDC}
    if fldtest then writeln('entering drawscrap');

    StartPutScrap(Errnum);
 { Check for failed bind_dataseg}
    if Errnum <> 0 then
        begin
        endputscrap(dummy);
        Exit(DrawScrap);
        end;

    if not(scrapFE in currscrapset) then
        begin
        errnum := invscraptype;
        endputscrap(dummy);
        Exit(DrawScrap);
        end;

    GetScrap(ScrapKind, Hnd);
{ set the port, clipping, etc. }
    GetPort(tempPort);
    SetPort(ScrapFolder);
    scrapRect := ScrapFolder^.portRect;
    with scrapRect do
       begin
       right := (right - dhsBox) + 1;
       bottom := (bottom - dvsBox) + 1;
       end;
    ClipRect (scrapRect);
    EraseRect (scrapRect); { erase the whole folder, less scroll bars }

{ now draw the scrap field }
    ScrapHnd := pointer(ord(Hnd));
    scrapHnd^^.ScrapFld.coords.right := scrapRect.right;
    DrawField (Pointer(ord(scrapHnd)), nil, false, true);

    EndPutScrap(Errnum);

    SetPort(tempPort);

    if fldtest then writeln('leaving drawscrap');

end; { of DrawScrap }


{*****************************************************************************}
{$S FEothrwk}
procedure RemoveField {hf:hndField};
{
Function:  Deallocates all heap space used by the field whose handle is given.

Assumes:   The field and its value and format arrays were allocated off the
           heap specified to FEInit.

Note:      Handles must be re-dereferenced after calls to this procedure.
}
begin
{$IFC fTraceFE}    LogCall;     {$ENDC}
if fldTest then writeln ('Entering RemoveField');

FreeH (hzfromh(pointer(ord(hf^^.curValue))), pointer(ord(hf^^.curValue)));
FreeH (hzfromh(pointer(ord(hf^^.fmtInfo))), pointer(ord(hf^^.fmtInfo)));
FreeH (hzfromh(pointer(ord(hf))), pointer(ord(hf)));

if fldTest then writeln ('Exiting RemoveField');
end; { of RemoveField }


{*******************************************************************************}
{$S FEothrwk}
function CheckWord (hf:hndField; hfs:hndFState): boolean;
{
Function:  Determines if the current selection is a word according to Bill
           Atkinson's definition, for cut and copy operations.

Assumes:   A selection must exist, begin and end before the space interval, and
               contain at least one character.

Calls:     GetFChar
           InAlpha
}
var
    fstC, limC, NextC, prevC:     integer;
    leftword, rightword: boolean;
    dummy: boolean;
    lp: integer;

begin
{$IFC fTraceFE}    LogCall;     {$ENDC}
if fldTest then
    writeln('entering  checkword');

leftword := false;
rightword := false;

with hfs^^.select.int do
   begin
   lp := compressedlp(lpFst, hf, hfs, dummy);
   fstC := lp;
   PrevC := lp;
   if lpFst > 0 then
      PrevC := lp -1;
   if (qualifies(fstc, hf, hfs) and not qualifies(prevC, hf, hfs)) or (lp = 0) then
      leftword := true;

   lp := compressedlp(lplim, hf, hfs, dummy);
   limC := lp -1;
   NextC := lp;
   if (qualifies(limC, hf, hfs) and not qualifies(NextC, hf, hfs)) or (lp = hf^^.curlen) then
      rightWord := true;
   end;

if leftword and rightword then
   checkword :=true
else
   checkword := false;

if fldTest then writeln('Leaving CheckWord');
end; { of CheckWord }

{******************************************************************************}
{$S FEact}
procedure FETerminate;
{
Function: Creates universal text if scrap is type ScrapFE.
Calls:    CreateFldUT
          GetCsScrap
}
var
    ScrapKind:  ScrapType;
    Hnd:        TH;
    index:      TB;
    PtrFldUT:   PFldUT;
    errnum:     integer;

begin
{$IFC fTraceFE}    LogCall;     {$ENDC}
if fldtest then writeln('entering FETerminate');

if (scrapfe in currscrapset)  and (scrapProcess = My_Id) then
   begin
   GetScrap(ScrapKind,Hnd);
   BindUTDseg(errnum);
{ create a field and stream }
   index := createfldut(fldHeap);
   if fldtest then writeln('created index ',index);

{ point the ICS at the field }
   PtrFldUT := pointer(ord(rghcs^[index]^));
   PtrFldUT^.utfield := pointer(ord(hnd));

 { Yes, I am ignoring these errors. }
   StartGetScrap(errnum);
   PutCsScrap(index,Errnum);
   freeics(index);
   EndGetScrap(Errnum);
   UnbindUTDseg(errnum);

   if fldtest then
      begin
      getcsscrap(index);
      writeln('the rghcs index is ',index);
      end;
   end;
end;

{******************************************************************************}
{$S FEcutCopy}
procedure CutCopyField {hf:hndField; hfs:hndFState; fCut:boolean; fIntoScrap:
    boolean; var errNum:integer};
{
Function: Cuts or copies the selection into the scrap unless an error is detected
          while binding and erasing the scrap. In that case the cut or copy is
          abandoned, the undo scrap is made the current scrap, and the damaged
          scrap is made inaccesable. If fIntoScrap is false, no change to the
          scrap is made(useful for cutting from both the wide-angle and regular
          view without updating the scrap twice).

Assumes:  The field state exists and a selection has been set.
          OpenScrap has been called.
          A port, in which the scrap will be shown, exists and is pointed to by
              ScrapFolder (a GrafPtr).
          The scrap will take on the font, formatting, and alignment of the field
              from which the cut or copy is being done.

Note:     Handles must be re-dereferenced after calls to this procedure.
          If the call to EraseScrapData fails calling cutcopyfield with
          fintoscrap = false will still cut the field.

Errors:   field protected
          selection is 0-length
          Failed on call to StartPutScrap
          Failed on call to EraseScrapData(retry with fintoscrap = false);
          FEallocationFailed

Calls:    Replace
          DrawScrap
          FirstEdit
          SetSel
          CheckWord
          GetFChar
          GetScrap
          PutScrap
          EraseScrapData
}
label 100;      { error exit }

var dummyFHnd:   hndField;
    expLpNext:   integer;
    lp:          integer;
    complplim,complpfst:integer;
    tempField:   hndField;
    insSpace:    integer;
    selIsWord:   boolean;
    selSize:     integer;
    selFmts:     integer;
    ScrapKind:   ScrapType;
    Hnd:         TH;
    ScrapHeap:   THz;
    TempRect:    rect;
    ScrapPtr:    PtrField;
    dummy:       boolean;

begin
{$IFC fTraceFE}    LogCall;     {$ENDC}
if fldTest then writeln ('Entering CutCopyField');

with hf^^, hfs^^ do
    begin
    { if the selection is 0-length, set a status condition and exit }
    if select.int.lpFst = select.int.lpLim then
        begin
        errNum := nullSelect;
        goto 100;
        end;

    { see if a cut was requested }
    if fCut then
        { if the field is protected, set an error condition and exit }
        if protect then
            begin
            errNum := fldProtected;
            goto 100;
            end;

{ Establish the scrap first so that if it fails we can leave gracefully }
if fintoscrap then
  begin
  inheritScrap(true);
  EraseScrapData (Errnum);
{ Can I separate the errors into those that damage the scrap and those that dont?}
  if Errnum <> 0 then
    begin
    if fldtest then writeln('backing out of scrap - error is ',errnum);
    BackOutOfScrap;
    goto 100;
    end;
  end;

    errNum := 0;

{ check to see if selection is a word }
    selIsWord := CheckWord (hf, hfs);
    if fldtest then writeln('selisword is ',selisword);

{ Determine size of cut to allocate enough space off the heap }
    selSize := hfs^^.select.int.lpLim - hfs^^.select.int.lpFst;
    selFmts := FmtFromLp (hf, hfs, hfs^^.select.int.lpLim) -
        FmtFromLp (hf, hfs, hfs^^.select.int.lpFst) + 1;

end; { of with hf^^, hfs^^ }

if fIntoScrap then
    begin

  StartPutScrap(Errnum);
  if Errnum <> 0 then goto 100;

    ScrapHeap := HzOfScrap;

    SetRect (tempRect, 1, 1, 350, 30);

{******* Loop holeing is being done here. TEMPFIELD and SCRAP actually
         point to the same place. A field record or a scrap record  ******}
    ScrapHnd := Pointer(ord(Hallocate(ScrapHeap,SizeOf(ScrapRec))));
    if ord(scrapHnd) = ord(hnil) then
       begin
       EndPutScrap(errnum);
       errnum := FEallocationFailed;
       goto 100;
       end;
    TempField := Pointer(ord(scrapHnd));
    HzCreateField (ScrapHeap,tempField,tempRect, SelSize, 0, left, 3,
                                 SelFmts,  0, systext, true);
    if ord(tempField) = ord(hnil) then
       begin
       EndPutScrap(errnum);
       errnum := FEallocationFailed;
       freeh(scrapheap, pointer(ord(tempfield)));
       goto 100;
       end;

    ScrapPtr := tempField^;

    Hnd := pointer(ord(ScrapHnd));
    PutScrap(ScrapFE,Hnd, Errnum);

{ set the current scrap up as a field with the relevant characteristics of
   the one being cut or copied from }

    ScrapPtr^.align := left;
    ScrapPtr^.drawPad := hf^^.drawPad;
    scrapHnd^^.isWord := selIsWord;

    { replace all of the scrap with all of the selection }
    ScrapPtr^.maxLen := selSize;
    ScrapPtr^.curLen := selSize;
    ScrapPtr^.maxFmts := selFmts;
    ScrapPtr^.curFmts := 1;

    if fldTest then writeln (selFmts, ' formats to be placed in scrap');
    if fldTest then writeln ('Replacing scrap value now');

    Replace (tempField, nil, 0, selSize, hf, hfs, hfs^^.select.int.lpFst,
            hfs^^.select.int.lpLim, true);

    if fldtest then
    begin
          writeln('new scrap is ');
{$IFC FLDDEBUG}
          DumpFldInfo (tempField, nil);
{$ENDC}
          writeln ('selIsWord is ', selIsWord);
    end;

{ Unbind the scrap }
    EndPutScrap(ErrNum);


{ redraw the scrap }
    if fIntoScrap then
        DrawScrap(errnum);


end; { if fintoscrap }

{ if this is a cut, modify the field now }
if fCut then
    begin
    { replace all of the selection with a null selection }
    { cut all spaces before or after, but not both }
    with hfs^^ do
        begin
        if selIsWord then
           with select.int do
               begin
               complplim := compressedlp(lplim, hf, hfs, dummy);
               complpfst := compressedlp(lpFst, hf, hfs, dummy);
               explpnext := expandedlp(complplim + 1,hf,hfs);

   {if the word is at begining of field and the selection is not the entire field}
               if complpfst = 0 then
                  begin
                  if (GetFChar (complplim, 0, hf, hfs) = ' ') then
                     lplim := explpnext
                  end
               else   { if there is a space in front of the word }
                  if (GetFChar (complpfst, -1, hf, hfs) = ' ') then
                      lpfst := Expandedlp(complpfst - 1,hf,hfs)
                  else    { if there is a space after the word }
                     if (GetFChar (complplim, 0, hf, hfs) = ' ') and
                                         (complplim < hf^^.curlen) then
                        lplim := explpnext;
           end;

        end; { of with hfs^^ }
     FirstEdit (hf, hfs, errnum);

     with hfs^^ do
        begin
        { set the changed flag }
        changed := true;
        valid := false;
        lp := select.int.lpfst;
        validLp := lp;
        if fldTest then writeln ('Replacing field value now');
        Replace (hf, hfs, lp, select.int.lpLim, hf, hfs, lp, lp, true);
        end; { of with hfs^^ }

    { update the selection interval }
    SetSel (hf, hfs, lp, lp, TySelPt);
    LastEdit(hf,hfs);
    if errnum = 0 then
       uInfo.lastOp := CutOp
    else
       uInfo.lastOp := noOp;
    end { of action for cut }

else { copy operation }
    begin
    uInfo.lastOp := CopyOp;
    hfs^^.newSel := true;
    end;

if fldTest then
    begin
    writeln ('On exiting CutCopyField:');
{$IFC FLDDEBUG}
    DumpFldInfo (hf, hfs);
{$ENDC}
    end;

100: end; { of CutCopyField }


{******************************************************************************}
{$S FEcutCopy}
procedure PasteField {hf:hndField; hfs:hndFState; var errNum:integer;
    pasteFmt:boolean; PasteLimit: integer};
{
Function: Pastes the contents of the scrap(stream), if it is a field(type tycsfld),
          into the field, replacing the current selection. If pasting a stream,
          never paste more than pastelimit characters. If pasteFmt is true then all
          format information in the scrap will be pasted with the text; pasteFmt
          false pastes just text.

Assumes:  The scrap and its folder port, or a stream, exist, as described under CutCopyField.
          The clipping region for the field has been set appropriately by the
              caller.

Note:     Handles must be re-dereferenced after calls to this procedure.

Errors:   field protected
          invalid scrap type
          field full
          no room for format runs
          Failed on call to StartGetScrap
          FEallocationFailed

Calls:    Replace
          FirstEdit
          SetSel
          ActualIntSize
          FmtFromLp
          CreateField
          GetFChar
          GrowFieldValue
          SetLpd
          SeqLpd
}
label 75, 100;      { error exits }

var lp, i:         integer;
    tempFld:    hndField;
    dummyerr:   boolean;
    f:          ptrField;
    compSel:    interval;
    roomInFld:  integer;
    newFmts:    integer;
    ScrapKind:  ScrapType;
    Hnd:        TH;
    index:      TB;
    dummy:      TL;

{$S FEcutCopy}
  procedure pasteFF(hf: hndfield; PasteLimit: TC; var marker: TL; var errnum: TC);
{
  Function: Sequences through a formatted file for no more than PasteLimit
            characters placing them in the field;
  Assumes:
}
  var
      lpd: TAlpd;
      Achad: TAchad;
      cfmt: TC;
      i: integer;
      strarray: array[0..30] of char;
      f: ptrfield;

  begin
{$IFC fTraceFE}    LogCall;     {$ENDC}
      if fldtest then
         if not fCheckHz(hzFromh(pointer(ord(hf))),i) then
            writeln('bad heap at beginning of pasteff');
      GetCsScrap(index);
      if fldtest then writeln('entering pasteff with index ',index:1);

      BindUTDseg(errnum);

{ Establish the tyset }
      with lpd.Tyset do
         begin
         frce := true;
         fparbnds := false;
         frpe := true;
         end;

      hf^^.curfmts := 0;

{ Point the achad at the field's data array and set the interval }
      with achad do
         begin
         rgch := pointer(ord(hf^^.curvalue^));
         ichfst := 0;
         ichlim := PasteLimit;

         if marker = 0 then
            begin
      { Call SetLpd with the index passed in to pasteff }
            SetLpd(@Lpd,index,0,lpd.Tyset,achad);

      { Peel off leading tab }
            if rgch^[ichfst] = chtab then
               begin
               if fldtest then writeln('peeling tab');
               {***** changed pastelpfst := 1 to moveleft tofix bug fe31 *****}
               moveleft(rgch^[1],rgch^[0],ichlim -1);
               ichlim := ichlim -1;
               end;
            end
         else     { This is a repeat call }
            begin
            lpd.lplim := marker;
            seqlpd(@lpd,achad);
            end;
         end; {with}

{ While there is still text left to get, process the achad and get more }
      while achad.ichfst <> achad.ichlim do begin
    { if there is text then make sure there are enough formats }
          if not lpd.arce.fvan then begin
             f := hf^;
             f^.curlen := achad.ichlim;
             if f^.curfmts = f^.maxfmts then
                begin
                if fldtest then
                    writeln('allocating more space for formats in pasteff');
                growFormats(hf, f^.maxfmts + f^.growfmts, errnum);
                if errnum <> 0 then
                   exit(pasteff);
                end;

    { redo the handle after the growformats }
             achad.rgch := pointer(ord(hf^^.curvalue^));

    { do the format stuff }
             with hf^^.fmtinfo^^[hf^^.curfmts],lpd.rce^ do
                begin
                face := [];
                lpfst := lpd.lp;
                font := argfam[ifnt];
                if fbold then
                    face := face + [bold];
                if fitalic then
                    face := face + [italic];
                if funderline then
                    face := face + [underline];
                if foutline then
                    face := face + [outline];
                if fshadow then
                    face := face + [shadow];
                end;
             hf^^.curfmts := hf^^.curfmts + 1;

             with achad do
                begin
      { Check for tab or for carrage return and stop pasting }
                i := ichfst;
                while i < ichlim do
                   begin
                   if (rgch^[i] = chtab) or (rgch^[i] = chcr) then
                      begin
                      if fldtest then writeln('found a ',rgch^[i]);
                      hf^^.curlen := i;
                      achad.ichlim := achad.ichfst;
                      end;
                   i := i + 1;
                   end;
                 end;

          end;
          with achad do
             begin
      { Reset ichlim and call seqlpd }
             if ichfst <> ichlim then
                begin
                achad.ichfst := lpd.lplim;
                achad.ichlim := pastelimit;
                seqlpd(@lpd,achad);
                end;
             end;
      end; { while }

      unbindutdseg(i);

      if fldtest then
         if not fCheckHz(hzFromH(pointer(ord(hf))),i) then
            writeln('bad heap at end of pasteff');

  { No more text - wrap things up }
      RemoveDupFmts(hf);
      if fldtest then
         begin
         writeln('leaving pasteff ');
{$IFC FLDDEBUG}
         dumpfldinfo(hf,nil);
{$ENDC}
         end;
  end;

{$S FEcutCopy}
procedure pasteFE;
var i: integer;
begin
{$IFC fTraceFE}    LogCall;     {$ENDC}
if fldtest then writeln('doing standard field paste');

startGetScrap(errnum);
if errnum <> 0 then
   exit(pasteFE);

ScrapHnd := pointer(ord(hnd));

{ test against maximum field size }
with scrapHnd^^.scrapfld do
    begin
    if curlen + hf^^.curlen > maxfieldlngth then
       begin
       errnum := fldfull;
       EndGetScrap(i);
       goto 100;
       end;

{ set up a temporary field to act as a buffer for adding spaces to the value
    array and adjusting format positions for intelligent pastes }
    tempFld := CreateField (coords, curLen+2, 0, align, drawPad, curFmts, 0,
        sysText, protect);
    if ord(tempfld) = ord(hnil) then
       begin
       errnum := FEallocationFailed;
       goto 100;
       end;

    tempFld^^.curLen := curLen;
    MoveLeft (curValue^^[0], tempFld^^.curValue^^[0], curLen);
    tempFld^^.curFmts := curFmts;
    MoveLeft (fmtInfo^^[0], tempFld^^.fmtInfo^^[0], curFmts*fmtSize);
    end; { of with for setting up the temporary field }

with tempFld^^, hfs^^.select.int do
   begin
   { if the scrap is a word then add spaces to the left and right sides of
       the value in the temporary field as per Larry Tessler's memo of  }
   if scrapHnd^^.isWord then
      begin
      compSel.lpFst := CompressedLp (lpFst, hf, hfs, dummyErr);
      compSel.lpLim := CompressedLp (lpLim, hf, hfs, dummyErr);
      if qualifies (compSel.lpfst -1, hf, hfs) then
         begin
         for i := (curLen-1) downto 0 do
             curValue^^[i + 1] := curValue^^[i];
         curValue^^[0] := ord(' ');
         curLen := curLen + 1;
         if curFmts > 1 then
             for i := 1 to (curFmts-1) do
                 fmtInfo^^[i].lpFst := fmtInfo^^[i].lpFst + 1;
         end; { of action for adding the space at the beginning }
      if qualifies (compSel.lpLim, hf, hfs) then
           begin
           curValue^^[curLen] := ord(' ');
           curLen := curLen + 1;
           end; { of action for adding the space at the end }

      end; { of action for adding blanks as appropriate in the temporary field }
   end;{ with tempfld^^, hfs^^.select.int }

endGetScrap(i);
end;

{$S FEcutCopy}
begin
{$IFC fTraceFE}    LogCall;     {$ENDC}
if fldTest then writeln ('Entering PasteField');

{ Three things can happen on a paste:
    1) The scrap is a field. Create tempfld, place the scrap text into it, and
         adjust it for words.
    2) The scrap is not a field but the universal text is a formated file. Create
         tempfld and fill it using pasteff.
    3) The scrap cannot be pasted - join a monestary, life's a lot easier there.
  After one of the first two events tempfld is pasted into hf.}

{ if the field is protected, set an error condition and exit }
if hf^^.protect then
    begin
    errNum := fldProtected;
    goto 100;
    end;

{ Bail if we can't paste this field }
if not((scrapFE in currScrapSet) or (ScrapCS in currScrapSet)) then
   begin
   errNum := invScrapType;
   goto 100;
   end;

errNum := 0;

GetScrap(ScrapKind,Hnd);
GetCsScrap(index);

if ScrapKind = ScrapFE then
   begin
   pasteFE;
   if errnum <> 0 then goto 75;
   end
else
   begin    { Paste the Stream }
   { Set up tempory field used as achad.rgch }
   tempFld := CreateField (hf^^.coords, PasteLimit, 0, 0, 0, 4, 2, sysText, false);
   if ord(tempfld) = ord(hnil) then
      begin
      errnum := FEallocationFailed;
      goto 100;
      end;
   dummy := 0;
   PasteFF(tempfld, pastelimit, dummy, Errnum);
   if errnum <> 0 then goto 75
   end;

with tempFld^^, hfs^^.select.int do begin

   f := hf^;
   { Check if the combined size is greater then pastelimit }
   roomInFld := f^.curLen - ActualIntSize (hfs^, lpFst, lpLim);
   if curlen + roomInFld > pastelimit then
       begin
       errNum := fldFull;
       goto 75;
       end;

   { if the pasted text won't fit, set an error condition and exit }
   roomInFld := f^.maxLen - f^.curLen + ActualIntSize (hfs^, lpFst, lpLim);
   if (curLen > roomInFld) and (f^.growLen = 0) then
       begin
       errNum := fldFull;
       goto 75;
       end;

   { make sure the formats will fit }
   if pasteFmt then
       begin
       newFmts := curFmts + (f^.curFmts - (FmtFromLp (hf, hfs, lpLim) -
           FmtFromLp (hf, hfs, lpFst))) + 1;
       if fldTest then writeln (newFmts, ' formats to be placed in the field');
       if (newFmts > f^.maxFmts) then
           if (f^.growFmts = 0) then
               begin
               errNum := noRoomForRuns;
               goto 75;
               end { of error action }
           else { not enough room, but we can grow the format array }
               begin
               if fldtest then writeln('gowing the formats in pastefield');
               growFormats(hf, newfmts, errnum);
               if errnum <> 0 then
                  begin
                  goto 75;
                  end { of error action }
               end; { of action for growing the format array }
       end; { of action for making room for pasting formats }
   end; { of with tempFld^^ }

{ now make room for the new value, if necessary }
if (tempFld^^.curLen > roomInFld) then
    begin
    if fldtest then writeln('growing the destination in pastefield');
    GrowFieldValue (hf, hfs, (tempFld^^.curLen div f^.growLen + 1), errnum);
    if errnum <> 0 then
       begin
       goto 75;
       end;
    end;
{ see if this is the first edit after a selection - if so, save the field state
    and ready it for editing }
if hfs^^.newSel then
    FirstEdit (hf, hfs, errnum)
else
    RemoveBSRuns(hf,hfs);    { Added to fix #FE32 }

{ save the operation type }
if errnum = 0 then
   uInfo.lastOp := PasteOp
else
   uInfo.lastOp := noOp;

{ replace the selection with the whole contents of the scrap }
with hfs^^ do
    begin
    changed := true;
    valid := false;
    validLp := select.int.lpFst;
                        {***** changed pastelpfst to 0 to fix bug fe31 *****}
    Replace (hf, hfs, select.int.lpFst, select.int.lpLim, tempFld, nil, 0,
        tempFld^^.curLen, pasteFmt);
    end; { of with hfs^^ }
lp := hfs^^.select.int.lpFst + tempFld^^.curLen;
SetSel (hf, hfs, lp, lp, TySelPt);
LastEdit(hf,hfs);

{ deallocate space for the temporary field }
75: RemoveField (tempFld);

if fldTest then
    begin
{$IFC FLDDEBUG}
    writeln ('On exiting PasteField field is:');
    DumpFldInfo (hf, hfs);
    writeln('scrap is');
    DumpFldInfo (Pointer(ord(scrapHnd)), nil);
{$ENDC}
    end;
100: end; { of PasteField }


{******************************************************************************}
{$S FEcold}
procedure UndoEdit {hf:hndField; hfs:hndFState; var errNum:integer};
{
Function: Undoes the last editing operation for a field only.

Assumes:  The clipping region for the field has been set appropriately by the
              caller.
          Unless the field is automatically growable, its size has not been
              decreased.

Note:     Handles must be re-dereferenced after calls to this procedure.
          There is not a direct parallelism between unding a field and undoing
          the scrap. The suggested sequence is:
             UndoEdit(...);
             if (Uinfo.lastop = cutop) or (Uinfo.lastop = copyop) then
                UndoInheritScrap(...);

Errors:   invalid operation type (last operation cannot be undone)
          FEallocationFailed

Calls:    GrowFieldValue
}
label 100;      { error exit }

var dummyFld:   field;
    dummyFS:    fieldState;
    dummyFHnd:  hndField;
    dummyWord:  boolean;
    dataBuf:    hndData;
    fmtBuf:     hndRuns;
    intSize,i:  integer;
    f:          ptrField;
    fs:         ptrFState;

begin
{$IFC fTraceFE}    LogCall;     {$ENDC}
if fldTest then writeln ('Entering UndoEdit');
errNum := 0;

{ see if there is an undoable operation - if not, exit }
with uInfo do
    if not(lastop in [cutop,copyop,pasteop,chinputop,clearop,formatop]) then
        begin
        errNum := invOpType;
        goto 100;
        end;

{ undo the field changes for all operations except Copy }
if (uInfo.lastOp <> CopyOp) then
    begin
    { allocate the data areas }
    dataBuf := pointer(ord(HAllocate(fldHeap, uinfo.curLen)));
    MoveLeft (uinfo.curValue^^[0], dataBuf^^[0], uinfo.curLen);
    ChangeSizeH (fldHeap, pointer(ord(uinfo.curValue)), hf^^.curLen);
    f := hf^;
    if f^.maxLen < uinfo.curLen then
       begin
       { the field value array isn't big enough - expand it }
       GrowFieldValue (hf, hfs, ((f^.maxLen - uinfo.curLen) div f^.growLen + 1),
                                                                           errnum);
       f := hf^;
       end;
 { check the allocations }
    if (ord(dataBuf) = ord(hnil)) or
       (cbdataofh(fldHeap, pointer(ord(uinfo.curValue))) < f^.curLen) or
       (errnum <> 0) then
       begin
       if ord(databuf) <> ord(hnil) then
          freeH(fldheap, pointer(ord(databuf)));
       errnum := FEallocationFailed;
       Uinfo.lastop := noOp;
       goto 100;
       end;

    { allocate the format areas }
    fmtBuf := pointer(ord(HAllocate(fldHeap,uinfo.curFmts*fmtSize)));
    MoveLeft (uinfo.fmtInfo^^[0], fmtBuf^^[0], uinfo.curFmts * fmtSize);
    ChangeSizeH (fldHeap, pointer(ord(uinfo.fmtInfo)), hf^^.curFmts * fmtSize);
    f := hf^;
    if f^.maxFmts < uinfo.curFmts then
        { the format array isn't big enough - expand it }
        begin
        f^.maxFmts := uinfo.curFmts;
        ChangeSizeH (hzfromh(pointer(ord(f^.fmtInfo))),
                                  pointer(ord(f^.fmtInfo)), uinfo.curFmts * fmtSize);
        f := hf^;
        end;
  { check the allocations }
    if (ord(fmtBuf) = ord(hnil)) or
       (cbdataofh(fldHeap, pointer(ord(uinfo.fmtInfo))) < f^.curFmts * fmtSize) or
       (cbdataofh (hzfromh(pointer(ord(f^.fmtInfo))),
                                  pointer(ord(f^.fmtInfo))) < uinfo.curFmts * fmtSize) then
       begin
       if ord(fmtBuf) <> ord(hnil) then
          freeH(fldheap, pointer(ord(fmtBuf)));
       freeH(fldheap, pointer(ord(databuf)));
       errnum := FEallocationFailed;
       Uinfo.lastop := noOp;
       goto 100;
       end;

    f := hf^;
    fs := hfs^;

    { exchange the data values }
    intSize := fs^.space.lpLim - fs^.space.lpFst;
    MoveLeft (f^.curValue^^[0], uinfo.curValue^^[0], fs^.bsPtr);
    MoveLeft (f^.curValue^^[fs^.space.lpLim], uinfo.curValue^^[fs^.bsPtr],
        (f^.maxLen - fs^.space.lpLim));
    MoveLeft (dataBuf^^[0], hf^^.curValue^^[0], uinfo.curLen);

    { exchange the formats }
    MoveLeft (f^.fmtInfo^^[0], uinfo.fmtInfo^^[0], f^.curFmts * fmtSize);
    MoveLeft (fmtBuf^^[0], f^.fmtInfo^^[0], uinfo.curFmts * fmtSize);
    { adjust formats for space }
    for i := 1 to (f^.curFmts-1) do
        if uinfo.fmtInfo^^[i].lpFst > fs^.bsPtr then
           uinfo.fmtInfo^^[i].lpFst := uinfo.fmtInfo^^[i].lpFst - intSize;

    { deallocate temporary heap space }
    FreeH (fldHeap, pointer(ord(dataBuf)));
    FreeH (fldHeap, pointer(ord(fmtBuf)));

 { rereferenced for #FE36 or maybe #FE38 }
    f := hf^;
    fs := hfs^;

    { save the current field and field state information }
    dummyFld := f^;
    dummyFS := fs^;

    { restore the old field information }
    with f^, fs^ do
        begin
        curLen := uInfo.curLen;
        curFmts := uInfo.curFmts;
        select := uInfo.select;
        anchor := uInfo.anchor;
        space.lpFst := curLen;
        space.lpLim := maxLen;
        bsPtr := space.lpFst;
        valid := false;
        validLp := 0;
        newSel := true;
        changed := true;
        end; { of with f^, fs^ and restoring old field information }

    { put the saved field information in the undo structure }
    with uInfo do
        begin
        curLen := dummyFld.curLen;
        dummyFS.select := endselect;
        endselect := select;
        select := dummyFS.select;
        curFmts := dummyFld.curFmts;
        if select.int.lpFst > dummyFS.bsPtr then
            select.int.lpFst := select.int.lpFst - intSize;
        if select.int.lpLim > dummyFS.bsPtr then
            select.int.lpLim := select.int.lpLim - intSize;
        anchor := dummyFS.anchor;
        if anchor.int.lpFst > dummyFS.bsPtr then
            anchor.int.lpFst := anchor.int.lpFst - intSize;
        if anchor.int.lpLim > dummyFS.bsPtr then
            anchor.int.lpLim := anchor.int.lpLim - intSize;
        end;

    if fldTest then
        begin
{$IFC FLDDEBUG}
        writeln ('Field undone - current def:');
        DumpFldInfo (hf, hfs);
{$ENDC}
        end;

    end; { of undoing field changes }


100: end; { of UndoEdit }


{******************************************************************************}
{$S FEcutCopy}
procedure ClearField {hf:hndField; hfs:hndFState; var errNum:integer};
{
Function: Clears the entire contents of the field, regardless of the selection.

Assumes:  The clipping region has been set appropriately for the field.

Calls:    FirstEdit
          HilightSel
          SetSel

Errors:   field protected
          Notundoable
}
label 100;      { error exit }
var saveNewSel: boolean;

begin
{$IFC fTraceFE}    LogCall;     {$ENDC}
if fldTest then writeln ('Entering ClearField');

{ if the field is protected, set an error status and exit }
if hf^^.protect then
    begin
    errNum := fldProtected;
    goto 100;
    end;

errNum := 0;

{ if this is the first edit after a selection, save stuff for undo and ready
    the field for editing }
if hfs <> nil then
    begin
    saveNewSel := hfs^^.newsel;
    if saveNewSel then
        FirstEdit (hf, hfs, errnum);

    { save the operation type }
    if errnum = 0 then
       uInfo.lastop := ClearOp
    else
       uInfo.lastOp := noOp;
    end;

{ set pointers and lengths to indicate that the field is empty }
with hf^^ do
    begin
    curLen := 0;
    curFmts := 1;
    fmtInfo^^[0].lpFst := 0;
    end;
if hfs <> nil then
    with hfs^^ do
        begin
        bsPtr := 0;
        space.lpFst := 0;
        space.lpLim := hf^^.maxLen;
        validLp := 0;
        valid := false;
        changed := true;
        end;

if hfs <> nil then
   begin
{ set a null selection }
   SetSel (hf, hfs, 0, 0, TySelPt);
   if saveNewSel then
      LastEdit (hf, hfs);
   end;


100: end; { of ClearField }


ÿ