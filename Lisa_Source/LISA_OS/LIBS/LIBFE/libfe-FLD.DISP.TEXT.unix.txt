{*****************************************************************************}
{$S FEothrwk}
procedure DrawHilight (hfs:hndFState; r:Rect; fShow:boolean);
{
Function: Turns the specified hilighting rectangle on or off, depending on the
          value of fShow.  Sets visCaret in the field state accordingly.
}
var tempPort:  GrafPtr;
    oldPnPat:  pattern;
    oldPnMode: integer;

begin
{$IFC fTraceFE}    LogCall;     {$ENDC}
if fldTest then writeln ('Entering DrawHilight');

with hfs^^ do
    begin
    if select.tyHilight = tyHiCaret then
        begin
        if fShow then
            begin
            if not visCaret then
                begin
                InvertRect (r);
                visCaret := true;
                end;
            end
        else
            if visCaret then
                begin
                InvertRect (r);
                visCaret := false;
                end;
        end
    else if select.tyHilight = tyHiInvert then
            InvertRect (r)
    else if select.tyHilight = tyHiDimCaret then
        begin
        if fShow then
            FillRect (r, dimCaret)
        else
            EraseRect (r)
        end
    else if select.tyHilight = tyHiDimInvert then
        begin
        GetPort (tempPort);
        oldPnPat := tempPort^.pnPat;
        oldPnMode := tempPort^.pnMode;
        PenPat (dimGray);
        PenMode (patXor);
        PaintRect (r);
        PenPat (oldPnPat);
        PenMode (oldPnMode);
        end;
    end; { of with }

if fldTest then writeln ('Exiting DrawHilight');
end; { of DrawHilight }


{*****************************************************************************}
{$S FEothrwk}
procedure MoveField {hf:hndField; hfs:hndFState; dx:Point};
{
Function:  Sets hf^^.coords to the coordinates of the field computed around dx.

Note:      Handles must be re-dereferenced after calls to this procedure.

Calls:     FieldIntWidth
           ExpandedLp
           VertCoords
}
var w:          integer;
    ascent:     integer;
    descent:    integer;

begin
{$IFC fTraceFE}    LogCall;     {$ENDC}
if fldTest then
        writeln ('Entering MoveField');

{ set up the upper and lower coordinates of the field }
VertCoords (hf, ascent, descent);
with hf^^ do
    begin
    coords.topLeft.v := dx.v - ascent;
    coords.botRight.v := dx.v + descent;

    { set up the left and right coordinates of the field }
    w := FieldIntWidth (hf, hfs, 0, ExpandedLp (curLen, hf, hfs));
    case align of
        left:   begin
                coords.topLeft.h := dx.h - drawPad;
                coords.botRight.h := dx.h + w + drawPad;
                end;
        right:  begin
                coords.topLeft.h := dx.h - w - drawPad;
                coords.botRight.h := dx.h + drawPad;
                end;
        center: begin
                coords.topLeft.h := (dx.h - ((w+1) div 2)) - drawPad;
                coords.botRight.h := (dx.h + ((w+1) div 2)) + drawPad;
                end;
        end; { of case }
        { changed from %50 to %75 of height for #FE36 or maybe #FE38 }
        if italic in fmtinfo^^[curfmts -1].face then
            coords.botRight.h := coords.botRight.h +
                                              (((ascent + descent) * 75) div 100);
    end; { of with hf^^ }
end; { of MoveField }


{****************************************************************************}
{$S FEothrwk}
procedure DrawContents (hf:hndField; hfs:hndFState; invRect:Rect; start:Point;
    showSelect, erase:boolean);
{
Function: Draws the contents of the field once the erasing and text starting
          positions have been figured out.

Assumes:  hfs may be nil.
          Hilighting has already been turned off for the field (if it was on).

Note:     Handles must be re-dereferenced after calls to this procedure.

Calls:    HilightSel
}
var runIndex:       integer;
    lpLst,lpFstRun: integer;
    contents:       QDPtr;
    rp:             ptrRuns;
    LongFont:       TLfntid;
    err:            integer;

begin
{$IFC fTraceFE}    LogCall;     {$ENDC}
if fldTest then writeln ('Entering DrawContents');

if erase then
   begin
   { erase the rectangle - uses old background pattern }
   if fldtest then
       writeln ('Erasing ', invRect.topLeft.h, ' to ', invRect.botRight.h);
   CaretErased(hf,hfs);
   EraseRect (invRect);
   end;

{ check for characters in the field }
if (hf^^.curLen <> 0) then
    begin
    { move to horizontal starting coordinate depending on justification }
    MoveTo (start.h, start.v);

    { for each run draw the text }
    for runIndex := 0 to (hf^^.curFmts-1) do
        begin

        {check for substitute font }
        with hf^^.fmtInfo^^[runIndex], longfont do
           begin
           fam := font;
           seteface := face;
           end;

        if SubFont then
            begin
            LongFont.dev := SubDev;
            FMMapFont(LongFont,err);
            end;

        with longfont do
           begin
           TextFont (fam);
           TextFace (seteface);
           end;

        with hf^^ do
            begin
            { find first lp to draw for this run }
            rp := fmtInfo^;
            lpFstRun := rp^[runIndex].lpFst;
            if hfs <> nil then
                with hfs^^ do
                    begin
                    if (rp^[runIndex].lpFst >= bsPtr) and (rp^[runIndex].lpFst <
                        space.lpLim) then
                        lpFstRun := space.lpLim;
                    end;

            { find the limit lp to draw for this run }
            if runIndex < (curFmts-1) then
                begin
                lpLst := rp^[runIndex + 1].lpFst;
                if hfs <> nil then
                    with hfs^^ do
                        if (rp^[runIndex + 1].lpFst > bsPtr) and
                            (rp^[runIndex + 1].lpFst <= space.lpLim) then
                            lpLst := bsPtr;
                end
            else
                lpLst := ExpandedLp (curLen, hf, hfs);

            if lpLst > lpFstRun then { the run is larger than 0-length; draw it }
                begin
                { save the starting location for possible underline }
                contents := @curValue^^;
                if hfs <> nil then
                    with hfs^^ do
                        if (lpFstRun <= bsPtr) and (lpLst >= space.lpLim) then
                            begin
                            DrawText (contents, lpFstRun, bsPtr - lpFstRun);
                            lpFstRun := space.lpLim;
                            end;
                DrawText(contents, LpFstRun, LpLst - LpFstRun);
                end; { of action for drawing a run with more than 0 characters }
            end; { of with hf^^ }
        end; { for loop on runs }
    end; { of actual drawing for non-zero-length contents }

{ update field state info }
if (hfs <> nil) then
    with hfs^^ do
        begin
        { set the valid flag to true }
        valid := true;
        validLp := bsPtr;
        end; { of action for field state existing }

{ see if the selection is to be shown }
if showSelect then
    if hfs <> nil then
       with hfs^^ do
          if not savecaret and (select.tyhilight = tyhicaret) then
             begin
             {the caret was off so don't redraw it but compute it}
             selecton := true;
             selrect(hf,hfs,select.int,selectrect);
             end
          else
            { turn on the selection }
             HilightSel (hf, hfs, true);

end; { of DrawContents }


{****************************************************************************}
{$S FEothrwk}
procedure DrawField {hf:hndField; hfs:hndFState; showSelect, erase:boolean};
{
Function: Draws the field pointed to by hf on the screen, according to the
          alignment specified for the field.   If the field is in the process
          of being edited, hfs points to editing information maintained by field
          level routines and understood by DrawField.  Otherwise hfs must be nil.
          If showSelect is true and hfs exists, then the selection will be
          hilighted. If erase is false then the field will be redrawn without
          erasing first.  This procedure may be used for either the initial
          display of the field, or any subsequent displays needed due to editing,
          uncovering, etc.

Assumes:  Field has been initialized or filled appropriately.
          Port and clipping have been set by calling procedure.
          Space has been allocated for the field and (optionally) its state (if
              no state exists, characters must be left justified in the curvalue
              array).
          Background pattern has been set according to the caller's wishes.

Note:     Handles must be re-dereferenced after calls to this procedure.

Calls:    VertCoords
          TextStartPos
          DrawContents
          FieldIntWidth
          DrawHilight
}
var invRect:        Rect;
    fldPort:        GrafPtr;
    start:          Point;
    ascent,descent: integer;

begin
{$IFC fTraceFE}    LogCall;     {$ENDC}
if fldTest then writeln('Entering DrawField');

{ turn off old hilighting }
savecaret := false;
if hfs <> nil then
   with hfs^^ do
     if selectOn then
        begin
        if viscaret and (select.tyhilight = tyhicaret) then
           savecaret := true;
        DrawHilight (hfs, hfs^^.selectRect, false);
        end;


{ find the actual upper coordinate of the text }
VertCoords (hf, ascent, descent);
with hf^^ do
    begin
    start.v := coords.topLeft.v + (((coords.botRight.v-coords.topLeft.v) -
        (ascent + descent) + 1) div 2);
    { find the baseline }
    start.v := start.v + ascent;
    { find the horizontal starting position }
    start.h := TextStartPos (hf, hfs);

    { find the area to be erased }
    invRect := coords;
    if (align = left) and (hfs <> nil) and erase then
        with hfs^^ do
            if (validLp <> 0) then
                begin
                if validlp > select.int.lpfst then
                    validlp := select.int.lpfst;
                if validlp <> 0 then
                   invRect.topLeft.h := start.h +
                                        FieldIntWidth (hf, hfs, 0, validLp) - 1;
                end;

    end; { of with hf^^ }

{ draw the field contents }
DrawContents (hf, hfs, invRect, start, showSelect, erase);

end; { of DrawField }


{****************************************************************************}
{$S FEothrwk}
procedure DrawFldAt {hf:hndField; hfs:hndFState; dx:Point; showSelect,
    erase:boolean};
{
Function: Draws the field pointed to by hf on the screen, according to the
          alignment specified for the field, relative to the point dx.  Defines
          the field coordinates (hf^^.coords) around its contents.  If the field
          is in the process of being edited, hfs points to editing information
          maintained by field level routines.  Otherwise hfs must be nil.  If
          showSelect is true and hfs exists, then the selection will be hilighted.
          If erase is false then the field will be redrawn without erasing first.
          If erase is true the superset of the old hf^^.coords left in the field
          and the new rectangle computed around dx is erased before redrawing.

Assumes:  Field has been initialized or filled appropriately.
          Port and clipping have been set by calling procedure.
          Space has been allocated for the field and (optionally) its state (if
              no state exists, characters must be left justified in the curValue
              array).
          Background pattern has been set according to the caller's wishes.

Note:     Handles must be re-dereferenced after calls to this procedure.
          90% of the users of this routine call movefield before calling this
                procedure and do their own erasing - this should be ironed out!

Calls:    TextStartPos
          FieldIntWidth
          DrawContents
          MoveField
          HilightSel
          DrawHilight
}
var invRect:    Rect;
    fldPort:    GrafPtr;
    start:      Point;

begin
{$IFC fTraceFE}    LogCall;     {$ENDC}
if fldTest then writeln('Entering DrawFldAt');

{ turn off old hilighting, if it is on }
savecaret := false;
if hfs <> nil then
    with hfs^^ do
     if selectOn then
        begin
        if viscaret and (select.tyhilight = tyhicaret) then
           savecaret := true;
        DrawHilight (hfs, selectRect, false);
        end;

{ The saving of the coordinates and the drawing optimization should be removed since they are
  both being circumvented by users now }
with hf^^ do
    begin
    { save the horizontal coordinates of the old rectangle to compute the superset }
    invRect := coords;

    if (align = left) and (coords.topLeft.h = dx.h - drawPad) and (hfs <> nil)
        then
        with hfs^^ do
           if validlp <> 0 then
               invRect.topLeft.h := dx.h + FieldIntWidth (hf, hfs, 0, validLp);
    end; { of with hf^^ }

{ find the coordinates of the rectangle }
MoveField (hf, hfs, dx);

{ This should also go away since when the above goes away there will be no old coords }
with hf^^ do
if align <> left then
    begin
  { set the erase rectangle to the larger, taking the left and right sides
         independantly, of the two rectangles }
    if coords.topleft.h < invrect.topleft.h then
        invrect.topleft.h := coords.topleft.h;
    if coords.botright.h > invrect.botright.h then
        invrect.botright.h := coords.botright.h;
    end; { of with hf^^ }

start.h := TextStartPos (hf, hfs);
start.v := dx.v;

{ Draw the field contents }
DrawContents (hf, hfs, invRect, start, showSelect, erase);

end; { of DrawFldAt }


{******************************************************************************}
{$S FEothrwk}
procedure SetSel {hf:hndField; hfs:hndFState; lpFst:integer; lpLim:integer;
                  selType: TtySel};
{
Function: Sets the selection defined by lpFst and lpLim in the field state.

Assumes: hfs is not nil.
}
begin
{$IFC fTraceFE}    LogCall;     {$ENDC}
if fldTest then
        writeln('Entering SetSel');
with hfs^^ do
    begin
    { set up the new selection }
    if (lpFst = lpLim) then
        begin
        select.tyHilight := tyHiCaret;
        select.tySel := tySelPt;
        end
    else
        begin
        select.tyHilight := tyHiInvert;
        select.tySel := selType;
        end;
    { set the new selection flag }
    newSel := true;
    { set the select and anchor intervals }
    select.int.lpFst := lpFst;
    select.int.lpLim := lpLim;
    anchor := select;
    end; { of with }

end; { of SetSel }


{****************************************************************************}
{$S FEothrwk}
procedure ErrorSelect {hf:hndField; hfs:hndFState; lpFst:integer; lpLim:integer;
    var t:integer};
{
Function:  Sets up the selection interval given by lpFst and lpLim in a field
           in which an error has been detected so that the user may go back
           and correct the error.  Sets the timeout variable to start the caret
           blinking.  This procedure would usually be called after an editcheck
           failure.

Assumes:   The field must not have been edited since ExitField was last called
               for the field.
           Port and clipping have been set by the caller.
           Hfs is not nil.
           The field has been drawn (i.e., its display image is valid).

Calls:     SetSel
           HilightSel
           StartBlink
}
begin
{$IFC fTraceFE}    LogCall;     {$ENDC}
if fldTest then writeln ('Entering ErrorSelect');

{ set the selection interval to that specified }
SetSel (hf, hfs, lpFst, lpLim, tySelPar);

{ turn on the hilighting }
HilightSel (hf, hfs, true);

{ set the timeout to start the caret blinking }
StartBlink (t);

end; { of ErrorSelect }


{****************************************************************************}
{$S FEothrwk}
procedure SelectAll {hf:hndField; hfs:hndFState; var t:integer};
{
Function: To be called when a field is first entered from another via the
          keyboard.  Selects the whole field, if it contains anything.  Other-
          wise, positions the insertion point at the beginning, end, or center
          of the field (depending on the justification).

Assumes:  Space for fieldstate has been allocated; the fieldstate structure will
              be initialized.
          The field must not have been edited since ExitField was last called
              for this field.
          Port and clipping have been set by the caller.
          The field's display image is valid.

Calls:    ErrorSelect
          InitFState
}
begin
{$IFC fTraceFE}    LogCall;     {$ENDC}
if fldTest then writeln ('Entering SelectAll');

{ initialize the field state }
InitFstate (hf, hfs);

{ set the selection to include all of the field; ErrorSelect will do it if we
    just pass the right interval }
ErrorSelect (hf, hfs, 0, hf^^.curLen, t);

end; { of SelectAll }


{****************************************************************************}
{$S FEothrwk}
procedure ExitField {hf:hndField; hfs:hndFState};
{
Function: To be called when a field is exited in order that another can be
          selected.  Turns off the selection and packs characters toward the
          left in the field contents.  Field state can be thrown away after
          this procedure has completed.  It should be called whether exit is
          via a keyboard character (tab or return) or via a selection in
          another field with the mouse.  If it isn't and the field has been
          edited, fieldstate must be kept around.

Assumes:  Hfs may be nil, in which case it will be assumed that the field was
              not edited.
          The field's display image is valid.

Calls:    HilightSel
          RemoveBSRuns
          RemoveDupFmts
}
label 100;

var
    fmt, runIndex, int: integer;
    rp:                 ptrRuns;
    i:                  INTEGER;


begin
{$IFC fTraceFE}    LogCall;     {$ENDC}
if fldTest then
   writeln('entering ExitField');

{if fs is null, then the field was not edited }
if (hfs = nil) then goto 100;

{ turn off the selection }
HilightSel (hf, hfs, false);

with hf^^, hfs^^ do
    begin
    { remove any formats backspaced over }
    RemoveBSRuns (hf, hfs);

    int := space.lpLim - bsptr;

    { adjust the formats for the removed space }
    rp := fmtinfo^;
    for runIndex := curFmts-1 downto 0 do
        if rp^[runIndex].lpFst >= space.lpLim then
            rp^[runIndex].lpFst := rp^[runIndex].lpFst - int;

{ move any space in the field to the end of it; i.e., pack all actual
    characters at the beginning; update pointers appropriately }
    if (curLen <> bsPtr) then { the space interval is not at the end }
        begin
        MoveLeft (curValue^^[space.lpLim], curValue^^[bsPtr],
            (maxLen - space.lpLim));
        with select.int do
            begin
            if lpFst > bsPtr then
                lpFst := lpFst - int;
            if lpLim > bsPtr then
                lpLim := lpLim - int;
            end;
        with anchor.int do
            begin
            if lpFst > bsPtr then
                lpFst := lpFst - int;
            if lpLim > bsPtr then
                lpLim := lpLim - int;
            end;
        end { of action moving the space interval }

        { ** added else part to "forget" the font of any deleted text ** }
      else
      with hf^^ do
         if (fmtinfo^^[curfmts - 1].lpfst = curlen) and (curlen <> 0) then
           { begin}
              curfmts := curfmts - 1;
              {fmtinfo^^[0].font := dfltfont;}
             { dfltfont := dfltfont;}
          {  end;}
        {** added to delete a fmt that's been backspaced over that has the same lpfst as a good fmt **}
    with hf^^ do
    begin
      i := 0;
      While i <> curfmts - 1 DO
        IF fmtinfo^^[i].lpfst = fmtinfo^^[i+1].lpfst THEN
           begin
            deleteFmt(hf, i);
            i := curfmts - 1;
           end
        else i := i + 1;
    end;
    RemoveDupFmts (hf);
    bsPtr := curLen;
    space.lpFst := curLen;
    space.lpLim := maxLen;
    end; { of with }
100: end; { of ExitField }


{****************************************************************************}
{$S FEothrwk}
procedure HilightInt (hf:hndField; hfs:hndFState; fInt:interval; fShow: boolean;
   var r:Rect);
{
Function:  Hilights or unhilights (depending on the value of fShow; true =>
           hilight) the rectangle covering the text in the input interval.
           Returns the rectangle hilighted.

Assumes:   The field's display image is valid.

Calls:     SelRect
           DrawHilight
}
begin
{$IFC fTraceFE}    LogCall;     {$ENDC}
if fldTest then writeln('entering HilightInt');

{ set up a rectangle for the hilighted area depending on the justification,
    maximum displayable length, and character coordinates for the field }
SelRect (hf, hfs, fInt, r);

{ turn on the hilighting }
DrawHilight (hfs, r, fShow);

end; { of HilightInt }


{****************************************************************************}
{$S FEothrwk}
procedure HilightSel {hf:hndField; hfs:hndFState; fShow: boolean};
{
Function: Turns the selection reflected in fstate on or off depending on the
          value of fShow (true => turn it on).

Assumes:  Pane is appropriately set.
          The clipping region has been set appropriately by the caller.
          The field's display image is valid (i.e., it matches its contents).

Calls:    HilightInt
}
label 100;

begin
{$IFC fTraceFE}    LogCall;     {$ENDC}
if fldTest then writeln ('Entering HilightSel');

{ exit if there is no selection, the selection is already as desired, or the
    hilight type of the selection is nil }
if (hfs <> nil) then
    with hfs^^ do
        begin
        if (selectOn = fShow) or (select.tyHilight = tyHiNil) then goto 100;

        { hilight the selection interval }
        HilightInt (hf, hfs, select.int, fShow, selectRect);

        { set the selectOn flag := fShow }
        selectOn := fShow;
        end; { of with }

100: end; { of HilightSel }


{****************************************************************************}
{$S FEcold}
procedure ChangeSelHilight {hf:hndField; hfs:hndFState; toDim:boolean};
{
Function: Changes the hilighting for the selection from caret or inversion to
          dim or vice versa, depending on the value of toDim.

Assumes:  The field will not be edited while the selection is dimly hilighted.
          The field's display image is valid (i.e., it matches its contents).

Calls:    DrawField
}
var tempPort:  GrafPtr;
    oldPnPat:  pattern;
    oldPnMode: integer;

begin
{$IFC fTraceFE}    LogCall;     {$ENDC}
if fldTest then writeln ('Entering ChangeSelHilight');

with hfs^^ do
    begin
    if (select.tyHilight = tyHiCaret) and toDim then
        { change caret from regular to dim hilighting }
        begin
        FillRect (selectRect, dimCaret);
        select.tyHilight := tyHiDimCaret;
        end { of action for changing caret to light caret }
    else if (select.tyHilight = tyHiDimCaret) and not toDim then
        { change caret from dim hilighting to regular hilighting }
        begin
        if visCaret then
            FillRect (selectRect, black)
        else
            EraseRect (selectRect);
        select.tyHilight := tyHiCaret;
        end { of action for changing from dim caret to regular caret }
    else if ((select.tyHilight = tyHiInvert) and toDim) then
        begin
        InvertRect (selectRect);
        GetPort (tempPort);
        oldPnPat := tempPort^.pnPat;
        oldPnMode := tempPort^.pnMode;
        PenPat (dimGray);
        PenMode (patOr);
        PaintRect (selectRect);
        select.tyHilight := tyHiDimInvert;
        PenPat (oldPnPat);
        PenMode (oldPnMode);
        end
    else if ((select.tyHilight = tyHiDimInvert) and not toDim) then
        begin
        select.tyHilight := tyHiInvert;
        selectOn := false;
        validlp := 0;
        DrawField (hf, hfs, true, true);
        end
    else
        if fldTest then writeln
            ('Tried to change hilighting brightness to the state it was already in');
    end; { of with }

if fldTest then writeln ('Exiting ChangeSelHilight');
end; { of ChangeSelHilight }


{*****************************************************************************}
{$S FEothrwk}
procedure BlinkCaret {hf:hndField; hfs:hndFState; var t:integer};
{
Function: Inverts the caret within the field and sets the timeout interval to
          turn it back off or on.  This is the routine to call when a timeout
          event is received.

Assumes:  On input, the timeout should be set to what it was the last time it was
              blinked.
          The clipping region is set appropriately by the caller.
          The field may or may not have a caret in it.
}
begin

{$IFC fTraceFE}    LogCall;     {$ENDC}
{ if there is a field state and the selection type is a caret, invert the caret }
if (hfs <> nil) then
    begin
    with hfs^^ do
        begin
        if fldTest then
            if (select.tyHilight = tyHiDimCaret) then
                writeln ('Dim caret - should not be blinked');
        if t = 0 then
            begin
            if visCaret then
                t := CaretOffTime
            else
                t := CaretOnTime;
            end
        else if (select.tyHilight = tyHiCaret) and selectOn then
            begin
            { invert the caret }
            InvertRect (selectRect);
            visCaret := not visCaret;
            end;
        end;

    if t = -2 then t := CaretOnTime;
    { set the timeout interval for the next inversion (85-last timeout interval) }
    t := CaretOnTime + CaretOffTime - t;
    end;

end; { of BlinkCaret }


{*****************************************************************************}
{$S FEothrwk}
procedure CaretOn {hf:hndField; hfs:hndFState};
{
Function: Checks for a caret in the field and turns it on if there is one.
          This procedure should be called before keyboard input, backspace,
          forwardspace, backword, and forwardword are processed.

Assumes:  The clipping region has been set appropriately.
}
begin

{$IFC fTraceFE}    LogCall;     {$ENDC}
{ if there is a field state and the selection type is caret, turn on the caret }
if (hfs <> nil) then
  with hfs^^ do
      begin
      if fldTest then
          if select.tyHilight = tyHiDimCaret then
              writeln ('Dim caret - editing should not be attempted');
      if select.tyHilight = tyHiCaret then
          { turn the caret on }
          if (not visCaret) and selecton then
              begin
              InvertRect (selectRect);
              visCaret := true
              end;
      end;

end; { of CaretOn }

{****************************************************************************}
{$S FEothrwk}
procedure CaretErased{hf:hndField; hfs:hndFState};
{
Function: For the wimps and sissys that like to erase there own caret, this
          routine will keep this caret state and the screen state in synch.
Assumes: hfs is not nil.
}
begin
{$IFC fTraceFE}    LogCall;     {$ENDC}
if hfs <> nil then
   with hfs^^ do
      begin
      viscaret := false;
      selecton := false;
      end;
end;



ÿ