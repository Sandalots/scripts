
{*****************************************************************************}
{$S FEothrwk}
function CompressedLp {lp:integer; hf:hndField; hfs:hndFState; var valid:boolean):
         integer};
{
Function: Returns the compressed logical position of the mouse within the field.
          Compressed means that temporary editing spaces have been squeezed out.
          If the lp passed is in space valid is set to false but compressedlp
          still tries to return a lp.
}
begin
{$IFC fTraceFE}    LogCall;     {$ENDC}
valid := true;
CompressedLp := lp;
if hfs <> nil then
    with hfs^^ do
        begin
        if (lp > bsPtr) and (lp < space.lpLim) and (bsPtr <> hf^^.curLen) then
            valid := false;
        if (lp > bsPtr) and (hf^^.curLen > 0) then
            CompressedLp := lp - (space.lpLim - bsPtr);
        end; { of with }

end; { of CompressedLp }


{*****************************************************************************}
{$S FEothrwk}
function ExpandedLp {lp:Integer; hf:hndField; hfs:hndFState): integer};
{
Function: Translates a 'compressed logical position' (temp editing spaces
          squeezed out) to an expanded log pos (temp editing space restored).
          Returns a position in the curvalue array (i.e., hfs^^.space.lpLim
          rather than hfs^^.bsPtr).
}
begin
{$IFC fTraceFE}    LogCall;     {$ENDC}
ExpandedLp := lp;
if hfs <> nil then
    with hfs^^ do
        if (lp >= bsPtr) and (hf^^.curLen > 0) and (bsPtr <> hf^^.curLen) then
            ExpandedLp := lp + (space.lpLim - bsPtr)

end; { of ExpandedLp }


{*****************************************************************************}
{$S FEothrwk}
function GetFChar (lp:integer; lpInc:integer; hf:hndField; hfs:hndFState): char;
VAR
    temp:  integer;
{
Function: Returns the character associated with compressed logical position lp
          modified by lpInc. Returns the null character (0) if the lp is outside
          the range of the curValue array.

Assumes:  The lp may be between the backspace pointer and the beginning of the
              space interval.

Calls:    ExpandedLp
}
    begin
{$IFC fTraceFE}    LogCall;     {$ENDC}
    lp := lp + lpInc;
    with hf^^ do
        if (lp < 0) or (lp >= curLen) then
            GetFChar := chr(0)
        else
            BEGIN
            temp := curValue^^[ExpandedLp(lp, hf, hfs)];
            IF temp < 0 THEN temp := temp + 256;
            GetFChar := CHR(temp);
            END;
    end; { of GetFChar }

{*****************************************************************************}
{$S FEothrwk}
Function charClass(ch: char): char;
{
Function:  Returns the class of chr(ch).
}
var
  kludge: packed record
          case boolean of
            true: (c: 0..255);
            false:(temp: char);
          end;
begin
{$IFC fTraceFE}    LogCall;     {$ENDC}
with kludge do
  begin
  temp := ch;
  if c in [65..90, 97..122, 128..159, 167, 174..175, 187, 188, 190..191, 202..207, 216] then
     ch := 'A'
  else
     if (48 <= c) and (c <= 57) then
        ch := '9'
     else
        if (c = 162) or (c = 163) or (c = 180) then ch := '$';
  charclass := ch;
  end;
end;

{*****************************************************************************}
{$S FEothrwk}
Function qualifies {(lp: integer; hf: hndfield; hfs: hndfstate): boolean};
{
Function:  Returns true if lp is in a word as per Larry Tessler's memo of
           Septmeber 23, 1982.
Assumes: lp is a compressed logical position.
}
var
   i,j: integer;
   left, this, right: char;
begin
{$IFC fTraceFE}    LogCall;     {$ENDC}
   left := charClass(GetFChar(lp, -1, hf, hfs));
   this := charClass(GetFChar(lp, 0, hf, hfs));
   right := charClass(GetFChar(lp, +1, hf, hfs));
   for i := 1 to (length(fldtriple^) div 4) + 1 do
      begin
      j := 4*i-2;
      if ((fldtriple^[j-1] = left) or (fldtriple^[j-1] = 'x')) and
         (fldtriple^[j] = this) and
         ((fldtriple^[j+1] = right) or (fldtriple^[j+1] = 'x')) then
         begin
         qualifies := true;
         exit(qualifies)
         end;
      end;
   qualifies := false;
end;


{*****************************************************************************}
{$S FEothrwk}
procedure LwFromLp{orig:integer; hf:hndField; hfs:hndFState;
                                      var lWord:interval; Var FoundWord: boolean};
{
Function:  Returns the interval which corresponds to the word that contains orig.
           If no word contains orig then return false in FoundWord. LwFromLp uses
           the algorightm in Marian Catelain's memo.
}
var
   dummy: boolean;

begin
{$IFC fTraceFE}    LogCall;     {$ENDC}
orig := compressedlp(orig,hf,hfs,dummy);
if fldtest then writeln('entering lwfromlp orig is ',orig);
with lword do
   begin
   lpfst := orig;
   lplim := orig;
   foundword := false;
   if qualifies(orig, hf, hfs) then
      begin
      foundword := true;
      while qualifies(lpfst -1, hf, hfs) do
          lpfst := lpfst -1;
      while qualifies(lplim +1, hf, hfs) do
          lplim := lplim +1;

{adjust interval so it will hilight correctly}
      lword.lplim := lword.lplim + 1;
      end;

   lpfst := expandedlp(lpfst, hf, hfs);
   lplim := expandedlp(lplim, hf, hfs);
   end;

if fldtest then writeln('exiting lwfromlp - interval is ',lword.lpfst, lword.lplim);
end; { LwFromLp }


{**************************************************************************}
{$S FEcold}
function SkipBackDelims (hf:hndField; hfs:hndFState; lp:integer): integer;
{
Function: Finds the first non delimeter character in contents to the left
          of lp.

Assumes:  The backspace pointer is at the beginning of the space interval.
          Hfs is not nil.

Calls:    CompressedLp
          GetFChar
          ExpandedLp
          InDelims
}
var
  dummy:  boolean;

begin
{$IFC fTraceFE}    LogCall;     {$ENDC}
if fldTest then writeln ('Entering SkipBackDelims; lp = ', lp);

lp := CompressedLp (lp, hf, hfs, dummy);

While not qualified(lp,hf,hfs) do
   if lp <= 0 then
      begin
      SkipBackDelims := 0;
      exit(SkipBackDelims);
      end
   else
      lp := lp -1;

SkipBackDelims := ExpandedLp (lp, hf, hfs);

end; { of SkipBackDelims }


{**************************************************************************}
{$S FEcold}
function SkipForwardDelims (hf:hndField; hfs:hndFState; lp, max:integer): integer;
{
Function: Finds the first non-delimeter character in contents to the right
          of lp.

Assumes:  hfs is not nil.
}
var
   dummy: boolean;
begin
{$IFC fTraceFE}    LogCall;     {$ENDC}
lp := compressedlp(lp, hf, hfs, dummy);

if fldtest then writeln('entering skipforwarddelims - lp is ',lp);

while not qualified(lp, hf, hfs) do
   if lp >= hf^^.curlen then
      begin
      SkipForwardDelims := hf^^.curlen;
      exit(SkipForwardDelims);
      end
   else
      lp := lp + 1;

SkipForwardDelims := expandedlp(lp, hf, hfs);
end; { of SkipForwardDelims }


{***************************************************************************}
{$S FEothrwk}
procedure SetNbrhood (hf:hndField; hfs:hndFState; dxy:Point; var n:Rect);
{
Function:   Given a logical position in sLp set the neighborhood to halfway
            into the character that occurs before sLp and halfway into
            the character that occurs after it.

Calls:      FieldIntWidth.
}
var selloch:    integer;
    width:      integer;
    prev,next:  interval;
    slp:    integer;
    isword,
    dummy:      boolean;
    int:        interval;

{$S FEothrwk}
procedure SetPixel(left,right: integer);
begin
{$IFC fTraceFE}    LogCall;     {$ENDC}
with hfs^^, hf^^ do
   begin
   if left > 0 then
       { subtract half the width of the preceeding character }
       n.topLeft.h := selloch - (FieldIntWidth (hf, hfs, left-1, left) div 2) - 1;

   if right < maxLen then
       { find the next character, if there is one }
       if (right <> bsPtr) then
           n.botRight.h := selloch + (FieldIntWidth (hf, hfs, right, right+1)
               div 2) + 1
       else
           if (right <> curLen) then { there is at least on character after it }
               n.botRight.h := selloch + (FieldIntWidth (hf, hfs, space.lpLim,
                   space.lpLim + 1) div 2) + 1;
   end;
end;


{$S FEothrwk}
begin
{$IFC fTraceFE}    LogCall;     {$ENDC}
   if fldTest then
     writeln('Entering SetNbrhood; point is ', dxy.h, dxy.v);

with hf^^, hfs^^ do
    begin
    n := coords;
    slp := lpfromx(dxy.h,hf,hfs,select.tySel);
    if select.tySel = tySelWd then { word at a time selection }
        begin
        LwFromLp (slp, hf, hfs, int, isword);
        if isword then
           begin
           n.botRight.h := XfromLP(hf, hfs, int.lplim) +1;
           n.topLeft.h := XfromLP(hf, hfs, int.lpfst) -1;
           end
        else
           begin
           n.topLeft.h := XfromLP(hf, hfs, slp) -1;
           n.botRight.h := XfromLP(hf, hfs, slp+1) +1;
           end
        end
    else { not a word }
       begin
       if (anchor.int.lpLim = select.int.lpLim) then
           selloch := selectRect.topLeft.h
       else
           selloch := selectRect.botRight.h;
       SetPixel(slp,slp);
       end
   end; { of with hf^^ }

{ now make sure the neighborhood includes the current mouse location }
if dxy.h < n.left then n.left := dxy.h
else if dxy.h >= n.right then n.right := dxy.h + 1;
if dxy.v < n.top then n.top := dxy.v
else if dxy.v >= n.bottom then n.bottom := dxy.v + 1;

if fldTest then
    writeln('Neighborhood is ', n.left, n.top, n.right, n.bottom);

end; { of SetNbrhood }


{*****************************************************************************}
{$S FEothrwk}
function  LpFromX {x: integer; hf:hndField; hfs:hndFState;
                                                seltype: ttysel): integer};
{
Function: When seltype = tyselWd returns the logical position of the character
          that contains x. Otherwise returns the rounded caret logical position
          corresponding to x.

Assumes:  The field's display image is valid.

Calls:    TextStartPos
          FieldIntWidth
}
var lp,w:     integer;
    lastLp, actuallp:   integer;
    fs:       PtrFstate;

begin
{$IFC fTraceFE}    LogCall;     {$ENDC}
if fldTest then
     writeln('entering LpFromX');

if hfs <> nil then
   fs := hfs^;

with hf^^ do
    begin
    lp := 0;
    actuallp := 0;
    if curLen > 0 then
        begin
        w := TextStartPos (hf, hfs);
        if hfs <> nil then
          if lp = fs^.bsPtr then
            lp := fs^.space.lpLim;
        if (x > w + 3) then
            begin
            while (w < x) and (lp < maxLen) do
                begin
                w := w + FieldIntWidth (hf, hfs, lp, lp + 1);
                lp := lp + 1;
                if hfs <> nil then
                  if lp = fs^.bsPtr then
                    lp := fs^.space.lpLim;
                end;
            if hfs <> nil then
              if lp = fs^.space.lpLim then
                lp := fs^.bsPtr;

            actualLp := lp;
            if actualLp > 0 then
               begin
               actualLp := actualLp -1;
               if hfs <> nil then
                 if actualLp = fs^.space.lpLim then
                   actualLp := fs^.bsPtr;
               end;

            { see which half of the previous character it is pointing to and
                adjust the lp appropriately }
            if lp > 0 then
                if ((w - (FieldIntWidth (hf, hfs, lp-1, lp) div 2)) > x) then
                    begin
                    lp := actuallp;
                    end; { of action for setting the lp to the previous position }
            end; { of action if x isn't at the very beginning of the field }
        end; { of action for non-empty field }
    end; { of with hf^^, hfs^^ }

if seltype = tyselwd then
  LpFromX := actualLp
else
  LpFromX := lp;

if fldTest then
   if seltype = tyselwd then
     writeln ('caret logical position: ', actualLp)
   else
     writeln ('caret logical position: ', lp);

end; { of LpFromX }


{*****************************************************************************}
{$S FEothrwk}
procedure RepSelect {lp:integer; hf:hndField; hfs:hndFState};
{
Function: Replicates a selection given the logical position of the character
          corresponding to the original x-coordinate.  Generally, this procedure
          is called after a select to replicate the selection in a duplicate of
          the wide-angle view.

Assumes:  Select was previously called.
          Port and clipping must be set by the caller.
          The field's display image is valid.

Calls:    SetSel
          HilightSel
}
begin
{$IFC fTraceFE}    LogCall;     {$ENDC}
  if fldTest then
      writeln('Entering RepSelect');

  SetSel(hf, hfs, lp, lp, TySelPt);

{ highlight the selection }
  HilightSel (hf, hfs, true);

end; { of RepSelect }


{*****************************************************************************}
{$S FEothrwk}
procedure Select {dxy:Point; hf:hndField; hfs:hndFState; var n:Rect; var t:
        integer};
{
Function: To be called when a mouse-button-down event is received.  Initializes
          things to receive subsequent drags or multiple clicks.  Also sets the
          first-edit-after-selection flag to enable saving of information when
          an edit occurs for subsequent undos.  Sets timeout to stop the caret from blinking.

Assumes:  Caller has turned off the previous selection (ExitField or HilightSel)
              (it may be in another field).
          Pane and clipping have been set.
          The neighborhood is set to indicate when the mouse moves halfway into
              the next or previous character.
          The field's display image is valid.

Calls:    RepSelect
          SetNbrhood
          LpFromX
}
var  chLp:      integer;
     fldPort:   GrafPtr;
     dummy:     integer;

begin
{$IFC fTraceFE}    LogCall;     {$ENDC}
if fldTest then
    writeln ('Entering Select; mouse at ', dxy.h, dxy.v);

{ find the character position for the location }
chLp := LpFromX (dxy.h, hf, hfs, tyselpt);

{ establish the new selection interval and hilight the selection }
RepSelect (chLp, hf, hfs);

{ set the neighborhood rectangle to indicate when it crosses into the next or
    previous character }
SetNbrhood (hf, hfs, dxy, n);

{ set the timeout to infinity to stop the caret from blinking }
t := -1;

end; { of Select }


{*****************************************************************************}
{$S FEothrwk}
procedure RepGrowSel {chlp:integer; hf:hndField; hfs:hndFState};
{
Function: Replicates a extension of a selection given the logical position of
          the character corresponding to the original x-coordinate. Generally,
          this procedure is called to replicate the selection in a duplicate
          of the wide-angle view.

Assumes:  The appropriate selection type was used to obtain chlp.
          Growsel must have been called previously.
          Timeout and neighborhood are irrelevant for the replicate case.
          Clipping has been set appropriately by the caller.
          The field's display image is valid.

Calls:    HilightSel
          HilightInt
          LwFromLp
}
var  newSel:       txSel;
     oldAnchorInt: interval;
     selOn,
     IsWord:       boolean;
     r:            Rect;
     fldPort:      GrafPtr;
     chLw:         interval;
     f:            ptrField;
     fs:           ptrFState;


{$S FEothrwk}
procedure SwitchLps (var sInt:interval);

var temp:  integer;

begin
{$IFC fTraceFE}    LogCall;     {$ENDC}
if sInt.lpFst > sInt.lpLim then
    begin
    temp := sInt.lpFst;
    sInt.lpFst := sInt.lpLim;
    sInt.lpLim := temp;
    end;
end; { of SwitchLps }


{$S FEothrwk}
begin
{$IFC fTraceFE}    LogCall;     {$ENDC}
if fldTest then
   begin
   if hfs^^.select.tysel = tyselwd then
      writeln('Entering RepGrowSel chlp is ',chlp:1,' this is a word grow')
   else
      writeln('Entering RepGrowSel chlp is ',chlp);
   if (hfs^^.select.tyHilight = tyHiDimCaret) or (hfs^^.select.tyHilight =
       tyHiDimInvert) then
       writeln ('Selection should not be grown when hilighting is dim');
   end;

f := hf^;
fs := hfs^;
{if selection type is word then grow selection by words }
if fs^.select.tySel = tySelWd then
    begin
    LwFromLp (chlp, hf, hfs, chLw, IsWord);
    if isword then
       begin
       if (chLw.lpFst > fs^.anchor.int.lpFst) then
           chlp := chLw.lpLim
       else
           chlp := chLw.lpFst
       end;
    end;
newSel.int.lpFst := chlp;
newSel.int.lpLim := chlp;

if fldtest then writeln('taking off with chlp ',chlp);
{ save information about the existing anchor; set the new selection to start
    and end at the character position corresponding to the x-coordinate }
oldAnchorInt := fs^.anchor.int;
newSel.tyHilight := tyHiInvert;

{ *** In order to fix a fatal bug in extend select, added following line  - Steve Rhodes  12/6/83 *** }
hfs^^.newSel := TRUE;

{ see if the beginning of the new selection is at the anchor }
if newSel.int.lpFst = oldAnchorInt.lpFst then
    begin
    with fs^.select do
        begin
        { see if the old selection was inverted and was of type point }
        if (tyHilight = tyHiInvert) and ((tySel = tySelPt) or
            (tySel = tySelWd)) then
            { selection was going the other direction }
            begin
            { turn off the old hilighting }
            HilightSel (hf, hfs, false);
            { reset selection to 0-length at the anchor, type caret }
            int.lpFst := oldAnchorInt.lpFst;
            int.lpLim := oldAnchorInt.lpLim;
            if oldAnchorInt.lpFst = oldAnchorInt.lpLim then
                tyHilight := tyHiCaret
            else
                tyHilight := tyHiInvert;
            { turn on hilighting }
            HilightSel (hf, hfs, true);
            end; { of action for selection going the other direction }
        end; { of with fs^.select }
    end { of action if beginning of new selection is at the anchor }
else { the beginning of the new selection is not at the anchor }
    begin
    with fs^.select, fs^.selectRect do
        begin
        { see if the type of hilighting is caret }
        if tyHilight = tyHiCaret then
            begin
            { turn off the caret }
            HilightInt (hf, hfs, fs^.select.int, false, r);
            fs^.visCaret := false;
            { set the current type of hilighting to invert }
            tyHilight := tyHiInvert;
            end;
        { see if the beginning of the anchor is less than the beginning of
            the new selection }
        if oldAnchorInt.lpFst <= newSel.int.lpFst then
            { grow forward }
            begin
            { set the beginning of the new selection to the end of the old }
            newSel.int.lpFst := int.lpLim;
            { Add one to the new selection to hilight correctly }
            if (tysel = tyselWd) and not isword then
               if newSel.int.lplim = fs^.bsptr then
                  newSel.int.lplim := fs^.space.lplim + 1
               else
                  newSel.int.lplim := newSel.int.lplim + 1;
            { see if the beginning of the anchor is not the beginning of the
                old selection (i.e., the selection was going the other way) }
            if oldAnchorInt.lpFst <> int.lpFst then
                begin
                { turn off the old selection }
                HilightInt (hf, hfs, fs^.select.int, false, r);
                topLeft.h := botRight.h + 1;
                { set the beginning of the old selection to the anchor }
                int.lpFst := oldAnchorInt.lpFst;
                { set the beginning of the new selection to the anchor }
                newSel.int.lpFst := oldAnchorInt.lpFst;
                end;
            { see if the end of the new selection is not equal to the end of
                the old selection }
            if newSel.int.lpLim <> int.lpLim then
                { grow the selection }
                begin
                { hilight the new selection }
                SwitchLps (newSel.int);
                HilightInt (hf, hfs, newSel.int, true, r);
                if newSel.int.lpLim > int.lpLim then
                    begin { continuing to grow forward }
                    int.lpLim := newSel.int.lpLim;
                    botRight.h := r.botRight.h;
                    end
                else { backing up }
                    begin
                    int.lpLim := newSel.int.lpFst;
                    botRight.h := r.topLeft.h;
                    end;
                end; { of code for growing the selection }
            end { of code for growing forward }
        else { grow backward }
            begin
            { set the end of the new selection to the beginning of the old }
            newSel.int.lpLim := int.lpFst;
            { see if the end of the anchor is not the end of the old selection
                (the selection was going the other way) }
            if oldAnchorInt.lpLim <> int.lpLim then
                begin
                { turn off the old selection }
                HilightInt (hf, hfs, fs^.select.int, false, r);
                botRight.h := topLeft.h + 1;
                { set the end of the old selection to the anchor }
                int.lpLim := oldAnchorInt.lpLim;
                { set the end of the new selection to the anchor }
                newSel.int.lpLim := oldAnchorInt.lpLim;
                end;
            { see if the beginning of the new selection is not equal to the
                beginning of the old selection }
            if newSel.int.lpFst <> int.lpFst then
                { grow or shrink the selection }
                begin
                { hilight the new selection }
                SwitchLps (newSel.int);
                HilightInt (hf, hfs, newSel.int, true, r);
                if newSel.int.lpFst < int.lpFst then
                    begin { continuing to grow backward }
                    int.lpFst := newSel.int.lpFst;
                    topLeft.h := r.topLeft.h;
                    end
                else { backing up }
                    begin
                    int.lpFst := newSel.int.lpLim;
                    topLeft.h := r.botRight.h;
                    end;
                end; { of code for growing the selection }
            end { of code for growing backward }
        end; { of with select, selectRect }
    end; { of growing or shrinking an inverted selection }
end; { of RepGrowSel }


{*****************************************************************************}
{$S FEothrwk}
procedure GrowSel {dxy:Point; hf:hndField; hfs:hndFState; var n:rect};
{
Function: To be called when a drag event is received.  Handles appropriate
          extending of the selection, both in the in-memory data structure and
          on the display.

Assumes:  Select and/or SelectWord was previously called.
          Clipping has been set appropriately by the caller.
          The field's display image is valid.

Calls:    LpFromX
          RepGrowSel
          SetNbrhood
}
var  nCenter:      integer;
     chLp:         integer;
     fldPort:      GrafPtr;

begin
{$IFC fTraceFE}    LogCall;     {$ENDC}
if fldTest then writeln ('Entering Growsel; mouse at ', dxy.h, dxy.v);

{ find the character position for the x coordinate }
chlp := LpFromX (dxy.h, hf, hfs, hfs^^.select.tysel);

{ grow the selection, given its logical position }
RepGrowSel (chlp, hf, hfs);

SetNbrhood (hf, hfs, dxy, n);

end; { of GrowSel }


{*****************************************************************************}
{$S FEothrwk}
procedure StartBlink {var t:integer};
{
Function: Sets the timeout interval to start the caret blinking.  Should be
          called when a mouse-button-up event is received.
}
begin
{$IFC fTraceFE}    LogCall;     {$ENDC}
{ set the timeout interval to 1/2 second to turn the caret off if nothing
    happens within that time-frame }
t := CaretOffTime;
end; { of StartBlink }

ÿ