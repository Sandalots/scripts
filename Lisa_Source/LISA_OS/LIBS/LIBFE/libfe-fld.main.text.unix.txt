
{ Copyright 1983,Apple Computer Inc. }

Unit FieldEdit;
Intrinsic;

Interface

USES      {$U libsm/unitStd }   UnitStd,
          {$U libsm/unitHz}     UnitHz,
          {$U libqd/Storage}    Storage,
          {$U libqd/QuickDraw}  QuickDraw,
          {$U libfm/FontMgr}    FontMgr,
          {$U libos/SysCall}    SysCall,
          {$U libwm/Events}     Events,
          {$U libwm/Folders}    Folders,
          {$U libsb/WMLStd}     WMLStd,
          {$U libsb/WMLSb}      WMLSb,
          {$U libdb/dbenv}      dbenv,
          {$U libfe/FEDec}      FEDec,
          {$U libsu/Scrap}      Scrap,
          {$U libsu/UnitFmt}    UnitFmt,
          {$U libpm/PmDecl}     PmDecl,
          {$U libpr/PrStdInfo}  PrStdInfo,
          {$U libsu/UnitCs}     UnitCs,
          {$U libfe/Fldut}      FldUT;

{$SETC FLDDEBUG := FDBGOK }  {FDBGOK}

{$SETC FLDSYMBOLS := FSYMOK }

const
        MaxFontNumber = 24;
        {$IFC NOT FLDDEBUG }
        fldTest = false;
        {$ENDC }

var
{$IFC FLDDEBUG }
    fldTest:    boolean;
{$ENDC }

    ToModern,
    ToClassic:  array [0 .. MaxFontNumber] of -128 .. 127;
    ScrapHnd:   HndScrap;

    fldHeap:    THz;
    fmtSize:    integer;
    uInfo:      undoInfo;

    SubFont:    Boolean;
    SubDev:     Integer;

    procedure FEInit (hz:THz; passtrpl: Ttriple; var errnum: integer);

    procedure FETerminate;

{$IFC FLDDEBUG}
    procedure SetFldTest (testOn:boolean);

    procedure DumpFldInfo (hf:hndField; hfs:hndFState);
{$ENDC}

    procedure LdFESeg;

    procedure UseSubstituteFont(Substitute: Boolean; DevtoSub: Integer);

    procedure SetFCoords (hf:hndField; hfs:hndFState; c:Rect);

    procedure SetFAlign (hf:hndField; hfs:hndFState; a:byte);

    procedure SetFPad (hf:hndField; hfs:hndFState; pad:byte);

    procedure SetFProtect (hf:hndField; hfs:hndFState; p:boolean);

    procedure InitFState (hf:hndField; hfs:hndFState);

    function  CreateField (c:Rect; iLen, gLen:integer; a:byte; pad:byte; iFmts,
                  gFmts:integer; fontNum:integer; p:boolean): hndField;

    procedure RemoveField (hf:hndField);

    function  FmtFromLp (hf:hndField; hfs:hndFState; lp:integer): integer;

    procedure FmtFromInt(hf:hndField; hfs:HndFState; Int:interval; var Plain: boolean;
                         var result:run);

    procedure SetRunInterval (hf:hndField; hfs:hndFState; fontNum:integer;
                  format:integer; int:interval; var errNum:integer);

    procedure SetRunFormat (hf:hndField; hfs:hndFState; fontNum:integer;
                  format:integer; var errNum:integer);

    procedure UpdateRun (hf:hndField; hfs:hndFState; fontNum:integer;
                  format:integer; var errNum:integer);

    procedure VertCoords (hf:hndField; var ascent, descent:integer);

    procedure MoveField(hf:hndField;  hfs:hndFState; dx:Point);

    procedure DrawField (hf:hndField; hfs:hndFState; showSelect, erase:boolean);

    procedure DrawFldAt (hf:hndField; hfs:hndFState; dx:Point; showSelect,
                  erase:boolean);

    procedure SetSel (hf:hndField; hfs:hndFState; lpFst:integer; lpLim:integer;
                  selType:TtySel);

    procedure SelectAll (hf:hndField; hfs:hndFState; var t:integer);

    procedure ExitField (hf:hndField; hfs:hndFState);

    procedure CompactField (hf:hndField);

    procedure ErrorSelect (hf:hndField; hfs:hndFState; lpFst:integer;
              lpLim:integer; var t:integer);

    procedure HilightSel (hf:hndField; hfs:hndFState; fShow:boolean);

    procedure ChangeSelHilight (hf:hndField; hfs:hndFState; toDim:boolean);

    function  LpFromX (x: integer; hf:hndField; hfs:hndFState;
                                                seltype: ttysel): integer;

    procedure LwFromLp(orig:integer; hf:hndField; hfs:hndFState;
                  var lWord:interval; Var FoundWord: boolean);

    function  CompressedLp (lp:integer; hf:hndField; hfs:hndFState;
                  var valid:boolean): integer;

    function  ExpandedLp (lp:integer; hf:hndField; hfs:hndFState): integer;

    procedure RepSelect (lp:integer; hf:hndField; hfs:hndFState);

    procedure Select (dxy:Point; hf:hndField; hfs:hndFState; var n:Rect;
                  var t:integer);

    procedure RepWordSelect (int:interval; hf:hndField; hfs:hndFState);

    procedure SelectWord (dxy:Point; hf:hndField; hfs:hndFState; var n:rect;
                  var t: integer);

    procedure RepGrowSel (chlp:integer; hf:hndField; hfs:hndFState);

    procedure GrowSel (dxy:Point; hf:hndField; hfs:hndFState; var n:Rect);

    procedure CaretErased(hf:hndField; hfs:hndFState);

    procedure StartBlink (var t:integer);

    procedure BlinkCaret (hf:hndField; hfs:hndFState; var t:integer);

    procedure CaretOn (hf:hndField; hfs:hndFState);

    procedure InsCh (ch:char; hf:hndField; hfs:hndFState; var errNum:integer);

    procedure Backspace (hf:hndField; hfs:hndFState; var errNum:integer);

    procedure Forwardspace (hf:hndField; hfs:hndFState; var errNum:integer);

    procedure BackWord (hf:hndField; hfs:hndFState; var errNum:integer);

    procedure ForwardWord(hf:hndField; hfs:hndFState; var errNum:integer);

    procedure DrawScrap(var errnum:integer);

    procedure CutCopyField (hf:hndField; hfs:hndFState; fCut:boolean;
                  fIntoScrap:boolean; var errNum:integer);

    procedure PasteField (hf:hndField; hfs:hndFState; var errNum:integer;
                  pasteFmt:boolean;PasteLimit: integer);

    procedure UndoEdit (hf:hndField; hfs:hndFState; var errNum:integer);

    procedure ClearField (hf:hndField; hfs:hndFState; var errNum:integer);

    Function qualifies (lp: integer; hf: hndfield; hfs: hndfstate): boolean;

implementation

{$R-}

{$IFC FLDSYMBOLS}
   {$D+}
{$ELSEC}
   {$D-}
{$ENDC}

{$S fldsel}

Const
        deltaFids = 20;

  { Classes for word search }
        ACh = 0;
        DCh = 1;
        DotCh = 2;
        PctCh = 3;
        CommaCh = 4;
        QuoteCh = 5;
        DollarCh = 6;
        OtherCh = 7;


Var
    dimGray:    pattern;
    dimCaret:   pattern;

    fldtriple:  TTriple;              { pointer to Afldtriple }
    Afldtriple: string[50];           { Contains triples for word stuff }
    savecaret:  boolean;

procedure FirstEdit (hf:hndField; hfs:hndFState; var Errnum:integer); forward;
procedure LastEdit (hf:hndField; hfs:hndFState); forward;

{$IFC FLDDEBUG }
{$S FEcold}
procedure SetFldTest {testOn:boolean};
begin
fldTest := testOn;
end;
{$ENDC }

{$S FEothrwk}
{*****************************************************************************}
procedure LdFESeg;
{
Function: Dummy to load Field Editor segment.
}
begin
{$IFC fTraceFE}    LogCall;     {$ENDC}
end;

{$IFC FLDDEBUG}

{*****************************************************************************}
{$S FEcold}
procedure DumpFldInfo {hf:hndField; hfs:hndFState};
{
Function:  Debug routine to print out field and field state information.
}
var i: integer;

begin
{$IFC fTraceFE}    LogCall;     {$ENDC}
with hf^^ do
    begin
    writeln;
    writeln ('Field location: ', coords.topLeft.h, coords.topLeft.v,
        coords.botRight.h, coords.botRight.v);
    writeln ('Max length: ', maxLen, '   Cur length: ', curLen, '   Grow length: ',
        growLen);
    writeln ('Alignment: ', ord(align), 'Padding: ', ord(drawPad),
        '   Protection: ', ord(protect));
    writeln ('maxFmts: ',maxFmts, '   curFmts: ',curFmts, '   Grow formats: ',
        growFmts);
    writeln('runs - ');
    for i := 0 to (curFmts-1) do
        begin
        writeln('starting at ',fmtInfo^^[i].lpFst);
        write('font = ', fmtInfo^^[i].font:3,'   ');
        if bold in fmtInfo^^[i].face then
            write('bold ');
        if italic in fmtInfo^^[i].face then
            write('italic ');
        if shadow in fmtInfo^^[i].face then
            write('shadow ');
        if underline in fmtInfo^^[i].face then
            write('underline ');
        if outline in fmtInfo^^[i].face then
            write('outline ');
        writeln;
        end;
    writeln;
    writeln ('Value: ');
    for i := 0 to (curLen-1) do
        write(i:3);
    writeln;
    for i := 0 to (curLen-1) do
        write (chr(curValue^^[i]):3);
    writeln;
    end;

if (hfs <> nil) then
    with hfs^^ do
        begin
        writeln ('Selection (', selectOn, ' type ', ord(select.tySel),
        ord(select.tyhilight),'): ', select.int.lpFst,
            select.int.lpLim, '   Anchor: ', anchor.int.lpFst, anchor.int.lpLim);
        writeln ('Backspace: ', bsPtr, '   validLp:',validLp,'  Space: ',
            space.lpFst, space.lpLim);
        end;

end; { of DumpFldInfo }
{$ENDC}


{*********************************************************************************}
{$S FEcold}
procedure UseSubstituteFont{Substitute: Boolean; DevtoSub: Integer};
{
Function: Enables or disables using fonts substituted for printing.
          Substitute = true enables font substitution. Substitute = false disables
          substitution.
}
begin
{$IFC fTraceFE}    LogCall;     {$ENDC}
if fldtest then writeln('entering UseSubstituteFont');
SubFont := Substitute;
SubDev := DevtoSub;
end;


{*****************************************************************************}
{$S FEothrwk}
procedure ResetDspState (hf:hndField; hfs:hndFState);
{
Function: Resets all field state variables affected by a change in the display
          characteristics (font, alignment, or coordinates) of a field so that
          a subsequent DrawField of the field will display it correctly.

Assumes: hfs may be nil.
}
begin
{$IFC fTraceFE}    LogCall;     {$ENDC}
if hfs <> nil then
    begin
    hfs^^.validLp := 0;
    hfs^^.valid := false;
    end;

end; { of ResetDspState }


{$I libfe/fld.font.text}


{*****************************************************************************}
{$S FEothrwk}
procedure SelRect (hf:hndField; hfs:hndFState; fInt:interval; var r:Rect);
{
Function: Calculates the rectangle which would shown to select the interval
          specified by fInt.

Assumes:  The field's display image is valid.

Calls:    VertCoords
          XFromLp
}
var
    ascent, descent: integer;
    dummy: boolean;

begin
{$IFC fTraceFE}    LogCall;     {$ENDC}
VertCoords (hf, ascent, descent);
with hf^^ do
    r.top := coords.top + (((coords.bottom - coords.top) - (ascent + descent)
        + 1) div 2);
r.bottom := r.top + ascent + descent;

r.left := XFromLp (hf, hfs, fInt.lpFst);
r.right := XFromLp (hf, hfs, fInt.lpLim);

if fint.lpfst <> fint.lplim then
   begin
   if fint.lpfst = 0 then
      begin
      r.left := r.left - hf^^.drawpad;
      if r.left <= hf^^.coords.topleft.h + 1 then
         r.left := hf^^.coords.topleft.h;
      end;

   if compressedlp(fint.lplim, hf, hfs, dummy) = hf^^.curlen then
      r.right := r.right + hf^^.drawpad;
   end;

if r.left = r.right then
    r.left := r.left - 1;
end; { of SelRect }


{*****************************************************************************}
{$S FEothrwk}
procedure SetFCoords {hf:hndField; hfs:hndFState; c:Rect};
{
Function: Sets the coordinates in the field definition to those given in c.  If
          there is a field state, resets relevant fields so redisplay will show
          the right thing.

Assumes: hfs should be nil if the field is not being edited.

Calls: ResetDspState
       SelRect
}
begin
{$IFC fTraceFE}    LogCall;     {$ENDC}
if fldTest then
     writeln('entering SetFCoords');
hf^^.coords := c;
ResetDspState (hf, hfs);

{ reset the selection rectangle }
if hfs <> nil then
    SelRect (hf, hfs, hfs^^.select.int, hfs^^.selectRect);

end; { of SetFCoords }


{*****************************************************************************}
{$S FEothrwk}
procedure SetFAlign {hf:hndField; hfs:hndFState; a:integer};
{
Function: Sets the alignment in the field definition to that given in a.  If
          there is a field state, resets relevant fields so redisplay will show
          the right thing.

Assumes: hfs should be nil if the field is not being edited.

Calls: ResetDspState
}
begin

{$IFC fTraceFE}    LogCall;     {$ENDC}
if fldTest then
        writeln('entering SetFAlign');
hf^^.align := a;
ResetDspState (hf, hfs);

end; { of SetFAlign }


{*****************************************************************************}
{$S FEcold}
procedure SetFPad {hf:hndField; hfs:hndFState; pad:byte};
{
Function: Sets the padding for drawing in the field definition to that given
          in pad.  If there is a field state, resets relevant fields so
          redisplay will show the right thing.

Assumes:  hfs should be nil if the field is not being edited.

Calls:    ResetDspState
}
begin
{$IFC fTraceFE}    LogCall;     {$ENDC}

if fldTest then
        writeln('entering SetFPad');
hf^^.drawPad := pad;
ResetDspState (hf, hfs);

end; { of SetFPad }


{*****************************************************************************}
{$S FEothrwk}
procedure SetFProtect {hf:hndField; hfs:hndFState; p:boolean};
{
Function: Sets the protect flag in the field definition to that given in p.
}
begin

{$IFC fTraceFE}    LogCall;     {$ENDC}
if fldTest then
        writeln('entering SetFProtect');
hf^^.protect := p;

end; { of SetFProtect }


{********************************************************************************}
{$S FEothrwk}
    procedure FmtFromInt{hf:hndField; hfs:HndFState; Int:interval; var Plain: boolean;
                         var result:run};
{
Function: Takes the intersection of all the format records in the interval INT
          and returns it in result. That is only those formatting character-
          istics that cover the entire interval are returned. If the fonts
          changes in the interval a font of -1 is returned. If the union of
          all the runs is normal then plain is set to true.

Assumes:  The field state may be nil.

Calls:    FmtFromLp
}
var
   start,stop,i:  integer;
   PlainResult: Run;

begin

{$IFC fTraceFE}    LogCall;     {$ENDC}
    start := FmtFromLp(hf,hfs,int.lpfst);
    stop := FmtFromLp(hf,hfs,int.lpLim);
    result := hf^^.fmtinfo^^[start];
    PlainResult := result;
    for i := start + 1 to stop do begin
        if hf^^.fmtinfo^^[i].font <> hf^^.fmtinfo^^[start].font then
           result.font := -1;
        result.face := result.face * hf^^.fmtinfo^^[i].face;
        PlainResult.face := PlainResult.face + hf^^.fmtinfo^^[i].face;
    end;
    if PlainResult.face = [] then
       Plain := true
    else
       Plain := false;
end; {FmtFromInt}


{****************************************************************************}
{$S FEothrwk}
procedure CompactField {hf:hndField};
{
Function: Deallocates any extra space in the curValue or fmtInfo arrays when
          editing of a field has been completed.

Assumes:  ExitField has been called for the field, so that values are packed to
              the left in the curValue array and format starting positions have
              been adjusted.
          Field may not be compactable.

Note:     Handles must be re-dereferenced after calls to this procedure.
}
begin
{$IFC fTraceFE}    LogCall;     {$ENDC}
if fldTest then writeln ('Entering CompactField');

{ get rid of extra space in the value array }
with hf^^ do
    if growLen > 0 then
        begin
        while (maxLen - growLen) >= curLen do
            maxLen := maxLen - growLen;
        ChangeSizeH (hzfromh(pointer(ord(curValue))), pointer(ord(curValue)), maxLen);
        end;

{ get rid of extra space in the format array }
with hf^^ do
    if growFmts > 0 then
        begin
        while (maxFmts - growFmts) >= curFmts do
            maxFmts := maxFmts - growFmts;
        ChangeSizeH (hzfromh(pointer(ord(fmtInfo))), pointer(ord(fmtInfo)), (maxFmts * fmtSize));
        end;

if fldTest then writeln ('Exiting CompactField');
end; { of CompactField }


{*****************************************************************************}
{$S FEothrwk}
procedure HzCreateField (heap: Thz; hf: hndField; c:Rect; iLen, gLen:integer;
 a:integer;pad:integer;iFmts, gFmts:integer; fontNum:integer; p:boolean);
{
Function: does everything CreateField says it does

Calls:   SetRunFormat
}
var
    err:  integer;
    hData:hndData;
    hFmt: hndRuns;

begin
{$IFC fTraceFE}    LogCall;     {$ENDC}
if fldTest then
    writeln ('Entering HzCreateField');

with hf^^ do
    begin
    coords := c;
    maxLen := iLen;
    growLen := gLen;
    curLen := 0;
    align := a;
    if pad < 1 then
     drawPad := 1
    else
     drawPad := pad;
    maxFmts := iFmts;
    growFmts := gFmts;
    curFmts := 1;
    protect := p;
    end;
hData := pointer(ord(HAllocate(Heap, iLen)));
hf^^.curValue := hData;

hFmt := pointer(ord(HAllocate(Heap, iFmts*fmtSize)));
hf^^.fmtInfo := hFmt;

if (ord(hData) = ord(hnil)) or (ord(hFmt) = ord(hnil)) then
   begin
   hf := pointer(ord(hnil));
   if ord(hfmt) <> ord(hnil) then
      freeh(heap, pointer(ord(hfmt)));
   if ord(hdata) <> ord(hnil) then
      freeh(heap, pointer(ord(hdata)));
   end;

{ establish the first run }
if ord(hf) <> ord(hnil) then
   with hf^^ do
      begin
      fmtinfo^^[0].lpfst := 0;
      SetFmt( fmtinfo^^[0], fontNum, setFont);
      SetFmt( fmtinfo^^[0], fontNum, setNormal);
      end;

if fldTest then
    writeln ('Exiting HzCreateField');
end; { of CreateField }

{*****************************************************************************}
{$S FEothrwk}
function CreateField {c:Rect; iLen, gLen:integer; a:integer; pad:integer;
    iFmts, gFmts:integer; fontNum:integer; p:boolean): hndField};
{
Function: Allocates a field, and all needed value and format arrays, in the
          heap specified to FEInit.  Establishes the coords, maxLen, growLen,
          curLen, align, pad, curValue, maxFmts, growFmts, curFmts, fmtInfo, and
          protect values in the field, and sets the first run to the font
          specified with normal formatting. Minimun pad size is 1 pixel.

Assumes: The heap has been established and initialized, and it has been
             identified via its THz to FEInit.
         iFmts must be >= 1.
         iLen must be >= 0.

Note:    Handles must be re-dereferenced after calls to this procedure.

Calls:   HzCreateField
}
var
    hf: hndField;

begin
{$IFC fTraceFE}    LogCall;     {$ENDC}
if fldtest then
    writeln('entering createfield');


hf := pointer(ord(HAllocate(FldHeap, sizeof(field))));

if ord(hf) <> ord(hnil) then
   HzCreateField(fldheap,hf,c,ilen,glen,a,pad,ifmts,gfmts,fontnum,p);

createfield := hf;

if fldtest then writeln('leaving createfield');
end;

{*****************************************************************************}
{$S FEopen1}
procedure FEInit {hz:THz; passtrpl: Ttriple; var errnum: integer};
{
Function: Initializes the Field Editor, specifically: The patterns for dim
          gray and dim caret; the internal copy of passtrple; the
          heap zone fldHeap; and allocates the undo structures.

Assumes: Must be called before any other Field Editor procedures are.

Note:    Handles must be re-dereferenced after calls to this procedure.

Errors:  FEallocationFailed
}
var tempRect: Rect;
    i: integer;

begin
{$IFC fTraceFE}    LogCall;     {$ENDC}

   for i := 0 to MaxFontNumber do {most fonts just map into themselves}
   begin
      ToModern[i] := i;
      ToClassic[i] := i;
   end;
   ToModern[cent12]   := tile12;   { 12 point }
   ToModern[cent18]   := tile18;   { 18 point }
   ToModern[cent24]   := tile24;   { 24 point }
   ToModern[p12cent]  := p12tile;  { 10 point 12 pitch }
   ToModern[p10cent]  := p10tile;  { 12 point 10 pitch }
   ToModern[courier]  := elite;    { 14 point }
   ToClassic[tile12]  := cent12;   { 12 point }
   ToClassic[tile18]  := cent18;   { 18 point }
   ToClassic[tile24]  := cent24;   { 24 point }
   ToClassic[p12tile] := p12cent;  { 10 point 12 pitch }
   ToClassic[p10tile] := p10cent;  { 12 point 10 pitch }
   ToClassic[elite]   := courier;  { 14 point }


   errnum := 0;
   moveleft(passtrpl^,afldtriple,length(passtrpl^)+1);

   fldtriple := @afldtriple;

   StuffHex (@dimGray, '8800220088002200' {'8000080080000800'});
   StuffHex (@dimCaret, 'FFFFFF00FFFFFF00');

   fldHeap := hz;

   fmtSize := sizeof (run);

   SubFont := false;

   { allocate space for the undo info }
   with uInfo do
       begin
       curValue := pointer(ord(HAllocate(FldHeap, 0)));
       fmtInfo := pointer(ord(HAllocate(FldHeap, fmtSize)));
       if (ord(curvalue) = ord(hnil)) or (ord(fmtinfo) = ord(hnil)) then
          errnum := FEallocationFailed;
       end;



end; { of FEInit }

{*****************************************************************************}
{$S FEothrwk}
procedure InitFState {hf:hndField; hfs:hndFState};
{
Function: Initializes fields in the fieldstate record, according to the values
          in hf.  This routine does not need to be called if a field is entered
          via SelectAll before editing begins. InitFState also sets the
          hilighting for this selection to Nil, so that a subsequent DrawField
          will not draw the selection.

Assumes: Space has already been allocated for the field and fieldstate.
         If the field has contents, they are packed at the beginning of the
             array pointed to by curValue and the length is reflected in curLen.
             If it is empty, curLen is 0.  Other values have been set in hf as
             appropriate.
}
var f: ptrField;

begin

{$IFC fTraceFE}    LogCall;     {$ENDC}
if fldTest then
     writeln('entering InitFState');
f := hf^;
with hfs^^ do
    begin
    select.tyHilight := tyHiNil;
    select.int.lpFst := 0;
    select.int.lpLim := 0;
    anchor.tyHilight := tyHiNil;
    selectOn := false;
    visCaret := false;
    newSel := false;

    bsPtr := f^.curLen;

    { set the space interval }
    space.lpFst := f^.curLen;
    space.lpLim := f^.maxLen;
    validLp := 0;

    { set flags so that no redraw occurs }
    valid := true;
    changed := false;
    end; { of with }

end; { of InitFstate }


{$I libfe/fld.fmt.text}

{$I libfe/fld.disp.text}

{$I libfe/fld.sel.text}

{$I libfe/fld.word.text}

{$I libfe/fld.edit.text}

{$I libfe/fld.cmds.text}

End.

ÿ