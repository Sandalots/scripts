
{$R-}

UNIT UnitFmt;

(* Copyright 1983, Apple Computer Inc. *)

INTRINSIC SHARED;

{ med  7/8/84   --  fixed yLd in initialization for 1 1/2 spacing default}
{ jrm  3/30/83  --  wrote zzGenParFops }
{ s_w 11/1/82   --  D+/D- made dependent on fSymOk }
{ jpy 8/5/82    --  declared  itbdLst, tab array, and argfam[14] }
{ jrm and jpy  6/ 4/82  --  Scrap }
{ crc  6/02/82  --  Conditional compilation }
{ crc  5/28/82  --  Expanded ifntLst }
{ crc  4/02/82  --  integration with new font manager }
{ bas 12/10/81  --  integration with font manager }

INTERFACE

USES {$U libsm/UnitStd} UnitStd,
     {$U libsm/UnitHz} UnitHz,
     {$U libqd/QuickDraw} QuickDraw,
     {$U libfm/FontMgr} FontMgr;

{$SETC doTraceSU := TRUE}
{$SetC fTraceSU := doTraceSU AND fTRACE}

CONST
      {$IFC NOT fDbgOk} fTstFmt = FALSE; {$ENDC}
      ifldQuad = 0;
      ifldXLFst = 1;
      ifldXLBody = 2;
      ifldXRt = 3;
      ifldXtb = 4;
      ifldTbQuad = 5;
      ifldRgtbd = 6;
      ifldDxtb = 7;
      ifldBold = 8;
      ifldItalic = 9;
      ifldUnderline = 10;
      ifldFont = 11;
      ifldVan = 12;
      ifldSuperscript = 13;
      ifldYLine = 14;
      ifldYLd = 15;
      ifldMovTb = 16;
      ifldClrTb = 17;
      ifldFilTb = 18;
      ifldKeep = 19;
      ifldLpFig = 20;
      ifldIcsFig = 21;
      ifldTyfam = 22;
      ifldFsp = 23;
      ifldLst = 23;

      fopcEnd = -1;
      ibRgtbd = 14;
      cbRceVan = 2;

      ifnt12Tile =   0;
      ifntp10Tile =  1;
      ifntp12Tile =  2;
      ifnt18Tile =   3;
      ifnt24Tile =   4;
      ifnt12Cent =   5;
      ifnt18Cent =   6;
      ifnt24Cent =   7;
      ifntFBold  =   8;
      ifntp10Cent =  9;
      ifntp12Cent = 10;
      ifntelite   = 11;
      ifntcourier = 12;
      ifntp20Tile = 13;
      ifntp15Tile =  14;
      ifntp20Cent = 15;
      ifntp15Cent = 16;
      ifntLst = 16;

      ibXTbd = 0;
      ibChTbd = 2;
      ibRgfbc = 1;
      cbHfccInit = 10;

      itbdLst = 58;

      icsNil = 0;
      lpNil = -2;

      {Font families}
      tyfamModern = 0;
      tyfamClassic = 1;
      tyfamLst = 1;
      tyfam1Fill = 2;
      tyfam2Fill = 3;

      {Font sizes and pitches}
      fsp8P20 = 0;   {8 point, 20 pitch}
      fsp8P15 = 1;   {8 point, 15 pitch}
      fsp12P12 = 2;  {12 point, 12 pitch}
      fsp12P10 = 3;  {12 point, 10 pitch}
      fsp12 = 4;     {12 point, proportional}
      fsp14 = 5;     {14 point, proportional}
      fsp18 = 6;     {18 point, proportional}
      fsp24 = 7;     {24 point, proportional}
      fspLst = 7;


TYPE TFcc = RECORD        { Format-Changing Code }
             cref:   TB;
             argfbc:  TArgb;
             END;
     TPfcc = ^TFcc;
     THfcc = ^TPfcc;

     TQuad = (quadL, quadC, quadR, quadJ);   {quadJ = quadDecimal for tabs}

     TTyfill = (tyfillNil, tyfillDots, tyfillHyph, tyfillUL);

     TTbdOld = PACKED RECORD           {tab descriptor}
             x:      TX;       { tyxyCs }
             fill4:  0..15;    { 4 bits }
             quad:   TQuad;    { 2 bits }
             tyfill: TTyfill;  { 2 bits }
             chLdr:  TCh;      { 8 bits }
            END;

     TTbd = PACKED RECORD           {tab descriptor}
             x:             TX;       { tyxyCs }
             fill3:         0..7;     { 3 bits }
             fDecimalComma: TF;       { 1 bit  }
             quad:          TQuad;    { 2 bits }
             tyfill:        TTyfill;  { 2 bits }
             chLdr:         TCh;      { 8 bits }
            END;
     TArgtbd = ARRAY [0..0] OF TTbd;
     TRgtbd = ^TArgtbd;

     TArcpe = PACKED RECORD
             cb:     TB;
             b1:     TB;
             END;
     TRcpe = ^TArcpe;

     TArce = PACKED RECORD          {run char extension; looks of a char run}
             cb:     0..255;
             fVan:   TF;                 { vanished }
             fBold:  TF;                 { bold }
             fItalic: TF;                { italic }
             fUnderline: TF;             { underlined }
             fill4: 0..15;               { to be defined }
             cbSuperscript: TB;          { number of bits to superscript }
             ifnt:   TB;                 { font index }
             fKeep:  TF;                 { keep on same page }
             fOutLine: TF;               { outline }
             fShadow: TF;                { shadow }
             fFillB: TF;
             fFillC: TF;
             fFillD: TF;
             fFillE: TF;
             fFillF: TF;
             icsFig: TB;
             lpFig: TC;
             END;
     TRce = ^TArce;

       {run para extension; looks of a paragraph run.  All coords are in
        tyxyCs space. }
     TArpe = PACKED RECORD               { all coord tyxy's = tyxyCs }
             cb:     0..255;
             sy:     TB;                 { style index }
             xLftFst: TX;                { left margin of first line }
             xLftBody: TX;               { left margin of subsequent lines }
             xRt:    TX;                 { right margin }
             yLd:    TY;                 { paragraph leading }
             fill1:  TB;                 { byte of filler }
             yLine:  0..63;              { inter-line spacing -- six bits }
             quad:   TQuad;              { margin justification -- two bits }
             itbLim: TW;                 { if < 0 then -itbLim = dxtbStd }
             argtbd: array [0..itbdLst] of TTbd; { table of tabs  }
             END;
     TRpe = ^TArpe;


     TTyfam = 0..3; { Font family }
     TFsp = TB;   { Font size/pitch}
     TFstyle = RECORD tyfam: TTyfam; fsp: TFsp; END; {Font Style }

      { destination type for fops }
     TTydst = (tydstRce, tydstRpe, tydstTbd, tydstNil);

VAR
    {$IFC fDbgOk} fTstFmt: TF; {$ENDC}
    argfam: ARRAY [0..ifntLst] OF TFam;  { Map from ifnt to font families }

    tbdNil: TTbd;
    arceStd: TArce;
    arpeStd: TArpe;

    ampifntfstyle: ARRAY [0..ifntLst] OF TFstyle;

PROCEDURE PxArce(VAR arce: TArce);
PROCEDURE PxArpe(VAR arpe: TArpe);
{ PROCEDURE PxFntd(fntd: TFntd);}  {4/2/82 crc}
{ PROCEDURE PxFont(pfont: TPfont);}

FUNCTION  CfbcApplyIfld(ifld: TC; wsval: TW; pval: TPc; rpe: TRpe; rce: TRce): TC;
FUNCTION  IfbcLstApplyRgfbc(rgfbc: TRgb; rpe: TRpe; rce: TRce) : TC;
PROCEDURE AddRgfbc(VAR hfcc: THfcc; hfccSrc: THfcc; hz: THz);
PROCEDURE AddFop(VAR hfcc: THfcc; ifld: TB; sval: TW; pval: TPc; hz: THz);
PROCEDURE GenFopsDiff(hfcc: THfcc; tydst: TTydst; rcpeStd, rcpe: TP; hz: THz);
PROCEDURE zzGenParFops(hfcc: THfcc; rpe: TRpe; hz: THz);

FUNCTION  ItbFromX(rgtbd: TRgtbd; itbLst: TC; x: TX) : TC;

{ PROCEDURE InitFont(spName: TSp; ifnt: TC; hz: THz); }
PROCEDURE InitFmt;
PROCEDURE PxRgfbc(rgfbc: TRgb);


IMPLEMENTATION

{$IFC fSymOK}
   {$D+}
{$ELSEC}
   {$D-}
{$ENDC}

CONST ibXLftFst = 2;
      ibXLftBody = 4;
      ibXRt = 6;
      ibYLd = 8;
      ibYLine = 10;
      ibQuad = 10;
      ibDxtb = 12;

TYPE TFld = PACKED RECORD
             tydst:  TTydst;
             fTbFop: TF;
             fiveBits: 0..31;
             ib:     TB;
             cbitSh:       TB;
             cbFop:  TB;
             mskNotFld:    TW;
             END;
     TArgfld = ARRAY [0..ifldLst] OF TFld;
     TRgfld = ^TArgfld;

     TFopc = PACKED RECORD
             ifld:   0..63;
             sval:   0..3;
             END;

     TFbcOrFopc = RECORD
             CASE BOOLEAN OF
              FALSE:  (fbc:   TB);
              TRUE:   (fopc:  TFopc);
              END;

     TValOrBb = RECORD
             CASE TC OF
              0:  (val:   TW);
              1:  (b1: TB; b2: TB);
              END;

     TPfoo = RECORD
             CASE TC OF
              0:      (pw:    TP);
              1:      (pa:    TPa);
              2:      (rce:   TRce);
              3:      (rpe:   TRpe);
              END;

     TSval = RECORD
             CASE TC OF
              1:      (sval:  0..3);
              2:      (quad:  TQuad);
              3:      (tyfill: TTyfill);
              END;

     TAmpfspifnt = ARRAY [0..fspLst] OF TB;
     TMpfspifnt = ^TAmpfspifnt;

VAR argfld: TArgfld;
    argmpfspifnt: ARRAY [0..tyfamLst] OF TMpfspifnt;
    modernAmpfspifnt: TAmpfspifnt;
    classicAmpfspifnt: TAmpfspifnt;

{$S SegPxScr }
{------------------------------------}
PROCEDURE PxArce { (VAR arce: TArce) };
{------------------------------------}
VAR iw: TC; pw: ^TW;
BEGIN
{$IFC fTraceSU}    LogCall;     {$ENDC}
{$IFC fDbgOk}
WITH arce DO
     WRITELN('rce: ', ORD(@arce),
             ' cb-', cb:2,
             ' van-', ORD(fVan):1,
             ' bold-', ORD(fBold):1,
             ' ital-', ORD(fItalic):1,
             ' UL-', ORD(fUnderline):1,
             ' ifnt-', ifnt:1,
             ' super-', cbSuperscript:2,
             ' keep-', ORD(fKeep):1, '.');
{$ENDC}
END;

{$S SegPxScr }
{-------------------------------------------}
PROCEDURE PxRgtbd(rgtbd: TRgtbd; itbLim: TC);
{-------------------------------------------}
VAR itb: TC;
BEGIN
{$IFC fTraceSU}    LogCall;     {$ENDC}
{$IFC fDbgOk}
FOR itb := 0 TO itbLim-1 DO
     WITH rgtbd^[itb] DO
          BEGIN
          WRITE('          itb:', itb:2, '; x:', x:4);
          CASE quad OF
           quadL: WRITE('             ');
           quadR: WRITE('; right flush');
           quadC: WRITE('; centered   ');
           quadJ: BEGIN
                  WRITE('; decimal ');
                  IF rgtbd^[itb].fDecimalComma THEN WRITE('comma ')
                                               ELSE WRITE('period ');
                  END;
           END;
          CASE tyfill OF
           tyfillNil:  WRITELN;
           tyfillDots: WRITELN('; dotted');
           tyfillHyph: WRITELN('; hyphens');
           tyfillUL:   WRITELN('; underlined');
           END;
          END;
{$ENDC}
END;

{$S SegPxScr }
{------------------------------------}
PROCEDURE PxArpe { (VAR arpe: TArpe) };
{------------------------------------}
VAR itbLimRpe: TC;
    rgtbd:  TRgtbd;
    p: TP;
BEGIN
{$IFC fTraceSU}    LogCall;     {$ENDC}
{$IFC fDbgOk}
WITH arpe DO
     BEGIN
     itbLimRpe := itbLim;
     WRITELN('rpe: ', ORD(@arpe),
             '; cb:', cb:4,
             '; sy:', sy:3,
             '; xLftFst: ', xLftFst:4,
             '; xLftBody: ', xLftBody:4,
             '; xRt: ', xRt:4);
     WRITE('  quad:', ORD(quad):2, ', itbLim:', itbLim:3);
     END;
p := @arpe;
rgtbd := POINTER(ORD(p) + ibRgtbd);
IF itbLimRpe < 0 THEN
     WRITELN('; dxtb:', -itbLimRpe:4)
ELSE BEGIN
     WRITELN;
     PxRgtbd(rgtbd, itbLimRpe);
     END;
{$ENDC}
END;

{$S SegPxScr }
{--------------------------------}
{ PROCEDURE PxFntd } { (fntd: TFntd) }{;}
{--------------------------------}
{
VAR ch: TC;
BEGIN
(*$IFC fTraceSU*)    LogCall;     (*$ENDC*)
WITH fntd^ DO
     BEGIN
     WRITELN('Fntd: ', ORD(fntd), ', dyHt: ', dyHt, ', dyBase: ',
       dyBase, ', chFst: ', chFst, ', chLim: ', chLim);
     FOR ch := 0 TO chLst DO WRITELN('ch: ', ch, ', dx: ', ampchdx[ch]);
     END;
END;
}  {4/2/82 crc}

{$S SegPxScr }
{------------------------------------------------}
FUNCTION IfbcNxtPxFop(rgfbc: TRgb; ifbc: TC) : TC;
{------------------------------------------------}
LABEL   12;
VAR fbcOrFopc: TFbcOrFopc;
    valOrBb:   TValOrBb;
    i, cbFop: TC;
BEGIN
{$IFC fTraceSU}    LogCall;     {$ENDC}
{$IFC fDbgOk}
fbcOrFopc.fbc := rgfbc^[ifbc];
IF fbcOrFopc.fbc = fopcEnd THEN
     BEGIN
     WRITELN('fopcEnd');
     GOTO 12;
     END;
WRITE('fopc - ifld:', fbcOrFopc.fopc.ifld:3, ', sval:', fbcOrFopc.fopc.sval:2);
ifbc := ifbc + 1;
cbFop := argfld[fbcOrFopc.fopc.ifld].cbFop;
IF cbFop = 0 THEN
     BEGIN
     valOrBb.val := 0;
     valOrBb.b2 := rgfbc^[ifbc];
     cbFop := valOrBb.val;
     ifbc := ifbc + 1;
     END;
CASE cbFop OF
 1:  WRITELN;
 2:  BEGIN
     WRITELN('; bval:', rgfbc^[ifbc]:4);
     ifbc := ifbc + 1;
     END;
 OTHERWISE
     BEGIN
     WRITELN;
     WRITE(' ', cbFop:3, ' bytes;   word vals: ');
     WITH valOrBb DO
          FOR i := 1 TO (cbFop - 1) DIV 2 DO
               BEGIN
               b1 := rgfbc^[ifbc];
               b2 := rgfbc^[ifbc + 1];
               WRITE(val, '; ');
               ifbc := ifbc + 2;
               END;
     WRITELN;
     END;
 END;
{$ENDC}
12:
IfbcNxtPxFop := ifbc;
END;

{$S SegPxScr }
{-----------------------------}
PROCEDURE PxRgfbc{rgfbc: TRgb};
{-----------------------------}
LABEL 999;
VAR ifbc, ifbcNxt: TC;
BEGIN
{$IFC fTraceSU}    LogCall;     {$ENDC}
ifbc := 0;
WHILE TRUE DO
     BEGIN
     ifbcNxt := IfbcNxtPxFop(rgfbc, ifbc);
     IF ifbcNxt = ifbc THEN GOTO 999;
     ifbc := ifbcNxt;
     END;
999:
END;

{$S SUlwwrk}
{-----------------------------------------------------------}
FUNCTION ItbFromX { (rgtbd: TRgtbd; itbLst: TC; x: TX) : TC };
{-----------------------------------------------------------}
VAR itbFst, itbMid: TC;
BEGIN
{$IFC fTraceSU}    LogCall;     {$ENDC}
itbFst := 0;
WHILE itbLst - itbFst > 1 DO
     BEGIN
     itbMid := (itbFst + itbLst) DIV 2;
     IF x < rgtbd^[itbMid].x
          THEN itbLst := itbMid
          ELSE itbFst := itbMid;
     END;
IF x < rgtbd^[itbLst].x
     THEN ItbFromX := itbFst
     ELSE ItbFromX := itbLst;
END;

{$S SUlwwrk}
{---------------------------------------------------------------------------------
-}
FUNCTION CfbcApplyIfld { (ifld: TC; wsval: TW; pval: TPc; rpe: TRpe; rce: TRce): TC
};
{---------------------------------------------------------------------------------
-}
LABEL   14, 800, 999;
VAR fld:    TFld;
    sval: TSval;
    pfbc: TP;
    rgfbc: TRgb;
    pfoo:   TPfoo;
    valOrBb:    TValOrBb;
    itb, itb2, itbLim: TC;
    tbdT: TTbd;
    rgtbd: TRgtbd;
    rgchSrc, rgchDst: TRgch;
    x, xNew, dxTb: TX;
    cbFop: TC;
    fstyleCur : TFstyle;
    mpfspifnt: TMpfspifnt;
BEGIN
{$IFC fTraceSU}    LogCall;     {$ENDC}
IF fTstFmt THEN WRITELN('CfbcApplyIfld -- ifld:', ifld:3, '; wsval:', wsval:4);
sval.sval := wsval;
fld := argfld[ifld];
cbFop := fld.cbFop;
pfbc := POINTER(ORD(pval));
rgfbc := POINTER(ORD(pfbc));
CASE cbFop OF
 0:        BEGIN
           valOrBb.val := 0;
           valOrBb.b2 := pfbc^ + 1;
           cbFop := valOrBb.val;
           pfbc := POINTER(ORD(pfbc) + 1);
           END;
 1:        valOrBb.val := wsval;
 2:        valOrBb.val := pval^;         { Forced to even address. }
 OTHERWISE BEGIN  { Can't do "pval^" because might be odd address. }
           valOrBb.b1 := rgfbc^[0];
           valOrBb.b2 := rgfbc^[1];
           END;
 END;
CfbcApplyIfld := cbFop;
IF fld.tydst <> tydstNil THEN
     BEGIN
     IF fld.tydst = tydstRce THEN pfoo.rce := rce ELSE pfoo.rpe := rpe;
     IF pfoo.pw = NIL THEN GOTO 999;
     END;

CASE ifld OF
 ifldXtb:
     BEGIN
     IF fTstFmt THEN WRITELN('          ifldXtb;');
     MoveRgch(@tbdT, POINTER(ORD(pfbc)), SIZEOF(TTbd));
     itbLim := rpe^.itbLim;
     rgtbd := POINTER(ORD(rpe) + ibRgtbd);
     IF itbLim > 0 THEN
          BEGIN
          IF rgtbd^[0].x <= tbdT.x THEN
               BEGIN
               itb := ItbFromX(rgtbd, itbLim - 1, tbdT.x);
               IF tbdT.x = rgtbd^[itb].x THEN GOTO 14;
               itb := itb + 1;
               END
          ELSE itb := 0;
          MoveOvRgch(@rgtbd^[itb+1], @rgtbd^[itb], SIZEOF(TTbd)*(itbLim-itb));
          END
     ELSE BEGIN
          itb := 0;
          itbLim := 0;
          END;
     pfbc := @rgtbd^[itbLim];
     IF (ORD(pfbc)-ORD(rpe)) > rpe^.cb THEN Break(2000);
     itbLim := itbLim + 1;
     rpe^.itbLim := itbLim;
14:  rgtbd^[itb] := tbdT;
     END;
 ifldClrTb,
 ifldFilTb:
     BEGIN
     itbLim := rpe^.itbLim;
     x := valOrBb.val;
     IF fTstFmt THEN
          WRITELN('          ifldClrTb; x:', x:5, '; itbLim:', itbLim:3);
     IF itbLim > 0 THEN
          BEGIN
          rgtbd := POINTER(ORD(rpe) + ibRgtbd);
          itb := ItbFromX(rgtbd, itbLim - 1, x);
          IF fTstFmt THEN
               WRITELN('                     itb:', itb:3,
                                          '; itb.x:', rgtbd^[itb].x:5);
          IF x <> rgtbd^[itb].x THEN GOTO 800;
          IF ifld = ifldClrTb THEN
               BEGIN
               itbLim := itbLim - 1;
               rpe^.itbLim := itbLim;
               MoveRgch(@rgtbd^[itb], @rgtbd^[itb+1], (itbLim-itb)*SIZEOF(TTbd));
               END
          ELSE rgtbd^[itb].tyfill := sval.tyfill;
          END;
     END;
 ifldMovTb:
     BEGIN
     itbLim := rpe^.itbLim;
     xNew := valOrBb.val;
     IF fTstFmt THEN
          WRITELN('          ifldMovTb; xNew:', xNew:4, '; itbLim:', itbLim:3);
     valOrBb.b1 := rgfbc^[2];
     valOrBb.b2 := rgfbc^[3];
     x := valOrBb.val;
     IF itbLim > 0 THEN
        BEGIN
        rgtbd := POINTER(ORD(rpe) + ibRgtbd);
        itb := ItbFromX(rgtbd, itbLim - 1, x);
        IF fTstFmt THEN
             WRITELN('               ...itb:', itb:2,
                                     '; xtb:', rgtbd^[itb].x:4, '.');
        IF x <> rgtbd^[itb].x THEN GOTO 800;
        dxTb := xNew - x;
        FOR itb2 := itb TO itbLim - 1 DO
           rgtbd^[itb2].x := rgtbd^[itb2].x + dxTb;
        END;
     END;
 ifldRgtbd:
     BEGIN
     itbLim := (cbFop - 1) DIV SIZEOF(TTbd);
     rpe^.itbLim := itbLim;
     MoveRgch(POINTER(ORD(rpe) + ibRgtbd), POINTER(ORD(pfbc)), cbFop - 1);
     END;
 ifldTyfam, ifldFsp:
     BEGIN
     IF rce <> NIL THEN
          BEGIN
          fstyleCur := ampifntfstyle[rce^.ifnt];
          IF ifld = ifldFsp THEN
               fstyleCur.fsp := valOrBb.val
          ELSE fstyleCur.tyfam := valOrBb.val;
          mpfspifnt := argmpfspifnt[fstyleCur.tyfam];
          rce^.ifnt := mpfspifnt^[fstyleCur.fsp];
          END;
     END;
 OTHERWISE
     BEGIN
     IF cbFop > 3 THEN
          MoveRgch(POINTER(ORD(pfoo.pw) + fld.ib), POINTER(ORD(pfbc)), cbFop - 1)
     ELSE BEGIN
          IF fTstFmt THEN
               WRITELN('          rcpe: ', ORD(pfoo.pw), ', ib:',
                        fld.ib:3, ', mskNotFld:', ORD(fld.mskNotFld):5, ' val: ',
                        valOrBb.val:6, ', cbitSh:', fld.cbitSh:3);
          AsgnField(pfoo.pw, fld.ib, fld.mskNotFld, valOrBb.val, fld.cbitSh);
          END;
     END;
 END;
800:
IF fTstFmt THEN
     BEGIN
     IF fld.tydst = tydstRce
          THEN PxArce(rce^)
          ELSE PxArpe(rpe^);
     END;
999:
END;

{$S SUlwwrk}
{---------------------------------------------------------------------}
FUNCTION IfbcLstApplyRgfbc { (rgfbc: TRgb; rpe: TRpe; rce: TRce) : TC };
{---------------------------------------------------------------------}
LABEL 99;
VAR ifbc, ifbcT:  TC;
    ifld: TC;
    fbcOrFopc: TFbcOrFopc;
    val: TW;
    pval: TPc;
BEGIN
{$IFC fTraceSU}    LogCall;     {$ENDC}
IF fTstFmt THEN WRITELN('IfbcLstApplyRgfbc.');
ifbc := 0;
WHILE TRUE DO
     BEGIN
     IF fTstFmt THEN ifbcT := IfbcNxtPxFop(rgfbc, ifbc);
     fbcOrFopc.fbc := rgfbc^[ifbc];
     IF fbcOrFopc.fbc = fopcEnd THEN GOTO 99;
     ifld := fbcOrFopc.fopc.ifld;
     IF argfld[ifld].cbFop = 2 THEN
          BEGIN
          val := rgfbc^[ifbc + 1];
          pval := @val;
          END
     ELSE pval := @rgfbc^[ifbc + 1];
     ifbc := ifbc + CfbcApplyIfld(ifld, fbcOrFopc.fopc.sval, pval, rpe, rce);
     END;
99:
IfbcLstApplyRgfbc := ifbc;
END;

{$S SUlwwrk}
{-------------------------------------------------------------------------}
PROCEDURE SpaceHfcc(VAR hfcc: THfcc; cbMore: TC; VAR ifbcLst: TC; hz: THz);
{-------------------------------------------------------------------------}
VAR ifbcMac: TC;
    cb: TC;
    hfccPrv: THfcc;
BEGIN
{$IFC fTraceSU}    LogCall;     {$ENDC}
IF hfcc = NIL THEN
     BEGIN
     hfcc := POINTER(ORD(HAllocate(hz, cbHfccInit + cbMore)));
     hfcc^^.cref := 1;
     hfcc^^.argfbc[0] := fopcEnd;
     ifbcLst := 0;
     END
ELSE BEGIN
     ifbcMac := CbDataOfH(hz, POINTER(ORD(hfcc))) - ibRgfbc;
     ifbcLst := IfbcLstApplyRgfbc(@hfcc^^.argfbc, NIL, NIL);
     cb := ifbcLst + cbMore + ibRgfbc;
     IF hfcc^^.cref > 1 THEN
          BEGIN
           { Copy the old hfcc into a new heap block. }
          hfcc^^.cref := hfcc^^.cref - 1;
          hfccPrv := hfcc;
          hfcc := POINTER(ORD(HAllocate(hz, cb + 5)));
          MoveRgch(POINTER(ORD(hfcc^)), POINTER(ORD(hfccPrv^)),
              CMin(cb,
                CbDataOfH(HzFromH(POINTER(ORD(hfccPrv))), POINTER(ORD(hfccPrv)))));
          hfcc^^.cref := 1;
          END
     ELSE IF cb >= ifbcMac THEN
          ChangeSizeH(hz, POINTER(ORD(hfcc)), cb + 5);
     END;
END;

{$S SUlwwrk}
{---------------------------------------------------------------}
PROCEDURE AddRgfbc { (VAR hfcc: THfcc; hfccSrc: THfcc; hz: THz) };
{---------------------------------------------------------------}
VAR cbAdd, ifbcLst: TC;
    rgfbc:  TRgb;
BEGIN
{$IFC fTraceSU}    LogCall;     {$ENDC}
IF fTstFmt THEN WRITELN('AddRgfbc.');
cbAdd := IfbcLstApplyRgfbc(@hfccSrc^^.argfbc, NIL, NIL);
SpaceHfcc(hfcc, cbAdd, ifbcLst, hz);
rgfbc := @hfcc^^.argfbc;
MoveRgch(@rgfbc^[ifbcLst], @hfccSrc^^.argfbc, cbAdd + 1);
IF rgfbc^[ifbcLst + cbAdd] <> fopcEnd THEN Break(14);
END;

{$S SUlwwrk}
{----------------------------------------------------------------------------}
PROCEDURE AddFop { (VAR hfcc: THfcc; ifld: TB; sval: TW; pval: TPc; hz: THz) };
{----------------------------------------------------------------------------}
VAR ifbcLst:  TC;
    fbcOrFopc: TFbcOrFopc;
    valOrBb:   TValOrBb;
    fld:    TFld;
    rgfbc:  TRgb;
    val: TW;
    cbFop: TC;
BEGIN
{$IFC fTraceSU}    LogCall;     {$ENDC}
IF fTstFmt THEN WRITELN('AddFop -- ifld:', ifld:3, '; sval:', sval:4,
                                '; pval: ', ORD(pval), '; pval^:', pval^:5);
fld := argfld[ifld];
cbFop := fld.cbFop;
IF cbFop = 0 THEN cbFop := sval + 1;
SpaceHfcc(hfcc, cbFop, ifbcLst, hz);
rgfbc := @hfcc^^.argfbc;
IF rgfbc^[ifbcLst] <> fopcEnd THEN Break(11);

fbcOrFopc.fopc.ifld := ifld;
fbcOrFopc.fopc.sval := sval;
rgfbc^[ifbcLst] := fbcOrFopc.fbc;
ifbcLst := ifbcLst + 1;

CASE fld.cbFop OF
 0:        BEGIN          { 'sval' is cbFop, less the ifld. }
           rgfbc^[ifbcLst] := sval;
           MoveRgch(@rgfbc^[ifbcLst+1], POINTER(ORD(pval)), sval);
           END;
 1:        BEGIN END;
 2:        rgfbc^[ifbcLst] := pval^;
 OTHERWISE MoveRgch(@rgfbc^[ifbcLst], POINTER(ORD(pval)), cbFop - 1);
 END;

ifbcLst := ifbcLst + cbFop - 1;
rgfbc^[ifbcLst] := fopcEnd;
END;

{$S SUlwwrk}
{-----------------------------------------------------------------}
PROCEDURE GenTbFops(hfcc: THfcc; rpeStd: TRpe; rpe: TRpe; hz: THz);
{-----------------------------------------------------------------}
LABEL 31, 999;
VAR itb, itbLim: TC;
    rgtbd, rgtbdStd:  TRgtbd;
    tbd:    TTbd;
BEGIN
{$IFC fTraceSU}    LogCall;     {$ENDC}
IF fTstFmt THEN WRITELN('GenTbFops.');
itbLim := rpe^.itbLim;
IF fTstFmt THEN WRITELN('GenTbFops -- itbLim:', itbLim:3);
IF itbLim < 0 THEN
     BEGIN
     IF rpeStd^.itbLim <> itbLim THEN AddFop(hfcc, ifldDxtb, 0, @itbLim, hz);
     END
ELSE BEGIN
     rgtbd := POINTER(ORD(rpe) + ibRgtbd);
     rgtbdStd := POINTER(ORD(rpeStd) + ibRgtbd);
     IF itbLim = rpeStd^.itbLim THEN
          BEGIN
          FOR itb := 0 TO itbLim - 1 DO
               BEGIN
               tbd := rgtbd^[itb];
               WITH rgtbdStd^[itb] DO
                    IF (tbd.x <> x) OR
                       (tbd.quad <> quad) OR
                       (tbd.tyfill <> tyfill) OR
                       ((tbd.quad = quadJ) AND (quad = quadJ)
                          AND (tbd.fDecimalComma <> fDecimalComma))
                       THEN GOTO 31;
               END;
          GOTO 999;
          END;
31:  AddFop(hfcc, ifldRgtbd, (itbLim * SIZEOF(TTbd)) + 1, POINTER(ORD(rgtbd)), hz);
     END;
999:
END;

{$S SUlwwrk}
{--------------------------------------------------------------------------------}
PROCEDURE GenFopsDiff { (hfcc: THfcc; tydst: TTydst; rcpeStd, rcpe: TP; hz: THz) };
{--------------------------------------------------------------------------------}
VAR ifld:   TC;
    fld:    TFld;
    ibLimStd: TC;
    val: TW;
    rpeStd:  TRpe;
BEGIN
{$IFC fTraceSU}    LogCall;     {$ENDC}
IF fTstFmt THEN WRITELN('GenFopsDiff -- tydst:', ORD(tydst):2);
hfcc^^.argfbc[0] := fopcEnd; hfcc^^.cref := 1;
rpeStd := POINTER(ORD(rcpeStd));
ibLimStd := rpeStd^.cb;
FOR ifld := 0 TO ifldLst DO
     BEGIN
     fld := argfld[ifld];
     IF (tydst = fld.tydst) AND (fld.ib < ibLimStd) THEN
          WITH fld DO
               BEGIN
               val := ValOfField(rcpe, ib, mskNotFld, cbitSh);
               IF val <> ValOfField(rcpeStd, ib, mskNotFld, cbitSh) THEN
                    AddFop(hfcc, ifld, val, @val, hz);
               END;
     END;
IF tydst = tydstRpe THEN GenTbFops(hfcc, rpeStd, POINTER(ORD(rcpe)), hz);
END;

{$S SUcold}
{--------------------------------------------------------}
PROCEDURE zzGenParFops { (hfcc: THfcc; rpe: TRpe; hz: THz) } ;
{--------------------------------------------------------}
VAR ifld:   TC;
    fld:    TFld;
    val: TW;
    rgtbd: TRgtbd;
    itbLim: TC;
BEGIN
{$IFC fTraceSU}    LogCall;     {$ENDC}
IF fTstFmt THEN WRITELN('zzGenParFops :');
hfcc^^.argfbc[0] := fopcEnd;
FOR ifld := 0 TO ifldLst DO
     BEGIN
     fld := argfld[ifld];
     IF (fld.tydst = tydstRpe) THEN
          WITH fld DO
               BEGIN
               val := ValOfField(POINTER(ORD(rpe)), ib, mskNotFld, cbitSh);
               AddFop(hfcc, ifld, val, @val, hz);
               END;
     END;
rgtbd := POINTER(ORD(rpe) + ibRgtbd);
itbLim := rpe^.itbLim;
IF itbLim < 0 THEN AddFop(hfcc, ifldDxtb, 0, @itbLim, hz)
     ELSE AddFop(hfcc, ifldRgtbd, (itbLim * SIZEOF(TTbd)) + 1,
            POINTER(ORD(rgtbd)), hz);
END;

{$S SegPxScrap}
{-------------------------}
PROCEDURE PxIfld(ifld: TC);
{-------------------------}
VAR fld:  TFld;
BEGIN
{$IFC fTraceSU}    LogCall;     {$ENDC}
{$IFC fDbgOk}
fld := argfld[ifld];
WITH fld DO
     WRITELN('Fld - ifld:', ifld:3, '; tydst:', ORD(tydst):2,
               '; ib:', ib:3, '; cbitSh:', cbitSh:3,
               '; cbFop:', cbFop:3, '; mskNotFld: ', mskNotFld:5);
{$ENDC}
END;

{$S SUinit}
{----------------}
PROCEDURE InitFop;
{----------------}
VAR w:  TW;
    argw2toi: ARRAY [0..16] OF TW;
    i: TC;
{$S SUinit}
PROCEDURE SetIfld(ifld: TC; tydst: TTydst; ib: TB; cbitSh: TB; cbFop: TB;
                                                  cbitWidth: TB);
     VAR fld:  TFld;
     BEGIN
    {$IFC fTraceSU}    LogCall;     {$ENDC}
     fld.tydst := tydst; fld.ib := ib; fld.cbitSh := cbitSh;
     fld.cbFop := cbFop;
     fld.mskNotFld := WNot(WShLft(argw2toi[cbitWidth]-1, cbitSh));
     argfld[ifld] := fld;
     IF fTstFmt THEN PxIfld(ifld);
     END;
{$S SUinit}
BEGIN
{$IFC fTraceSU}    LogCall;     {$ENDC}
w := 1;
FOR i := 0 TO 16 DO
     BEGIN
     argw2toi[i] := w;
     w := WShLft(w, 1);
     END;
SetIfld(ifldQuad, tydstRpe, ibQuad, 0, 1, 2);
SetIfld(ifldYLine, tydstRpe, ibYLine, 2, 2, 6);
SetIfld(ifldYLd, tydstRpe, ibYLd, 0, 3, 16);
SetIfld(ifldXLFst, tydstRpe, ibXLftFst, 0, 3, 16);
SetIfld(ifldXLBody, tydstRpe, ibXLftBody, 0, 3, 16);
SetIfld(ifldXRt, tydstRpe, ibXRt, 0, 3, 16);
SetIfld(ifldXtb, tydstTbd, 0, 0, SIZEOF(TTbd) + 1, 16);
SetIfld(ifldTbQuad, tydstTbd, 2, 0, 1, 2);
SetIfld(ifldRgtbd, tydstTbd, 0, 0, 0, 16);
SetIfld(ifldDxtb, tydstTbd, ibDxtb, 0, 3, 16);
SetIfld(ifldBold, tydstRce, 0, 6, 1, 1);
SetIfld(ifldItalic, tydstRce, 0, 5, 1, 1);
SetIfld(ifldUnderline, tydstRce, 0, 4, 1, 1);
SetIfld(ifldFont, tydstRce, 2, 0, 2, 8);
SetIfld(ifldVan, tydstRce, 0, 7, 1, 1);
SetIfld(ifldSuperscript, tydstRce, 2, 8, 2, 8);
SetIfld(ifldMovTb, tydstTbd, 0, 0, 5, 16);
SetIfld(ifldClrTb, tydstTbd, 0, 0, 3, 16);
SetIfld(ifldFilTb, tydstTbd, 0, 0, 3, 16);
SetIfld(ifldKeep, tydstRce, 4, 15, 1, 1);
SetIfld(ifldLpFig, tydstRce, 6, 0, 3, 16);
SetIfld(ifldIcsFig, tydstRce, 4, 0, 2, 8);
SetIfld(ifldTyfam, tydstNil, 0, 0, 1, 2);
SetIfld(ifldFsp, tydstNil, 0, 0, 2, 8);
END;


(* ******************************
{----------------------------------}
FUNCTION CbFont(pfont: TPfont) : TC;
{----------------------------------}
BEGIN
{$IFC fTraceSU}    LogCall;     {$ENDC}
CbFont := 2*(8+pfont^.cwBody+2+pfont^.chLst-pfont^.chFst);
END;

{----------------------------------}
PROCEDURE PxFont { (pfont: TPfont) };
{----------------------------------}
BEGIN
{$IFC fTraceSU}    LogCall;     {$ENDC}
WITH pfont^ DO
     BEGIN
     WRITELN('FORMAT:   ',FORMAT, ', chFst:  ',chFst:6, ', chLst:  ',chLst:6,
       ', MAXWD:    ',MAXWD:6);
     WRITELN('FBBOX:    ',FBBOX:6, ', FBBOY:    ',FBBOY:6, ', FBBDX:    ',FBBDX:6,
       ', dyHt:    ',dyHt:6);
     WRITELN('cwBody:   ',cwBody:6, ', dyBase:   ',dyBase:6, ', DESCENT:
     ',DESCENT:6,
       ', XOFFSET:  ',XOFFSET:6, ', cwStrike:   ',cwStrike:6);
     END;
END;

{-----------------------------------------------------}
PROCEDURE InitFont { (spName: TSp; ifnt: TC; hz: THz) };
{-----------------------------------------------------}
VAR nOrFd:  TNOrFd;
    rgchSrc: TRgch;
    rgchDst: TRgch;
    cbT:    TC;
    hfont:  THfont;
BEGIN
{$IFC fTraceSU}    LogCall;     {$ENDC}
IF fTstFmt THEN WRITELN('InitFont -- loading ', ifnt);
{ Replaces LoadFont(ifnt, spName); }
nOrFd.n := 0;
nOrFd.fd.ifil := IfilOpen(spName, 512, FALSE);
rgchSrc := POINTER(ORD(PMapN(hz, nOrFd.n, FALSE)));
cbT := CbFont(POINTER(ORD(rgchSrc)));
hfont := POINTER(ORD(HAllocate(hz, cbT)));
arghfont[ifnt] := hfont;
WHILE cbT > 0 DO
     BEGIN
     rgchSrc := POINTER(ORD(PMapN(hz, nOrFd.n, FALSE)));
     rgchDst := POINTER(ORD(hfont^)+nOrFd.fd.iblo);
     MoveRgch(rgchDst, rgchSrc, CMin(cbT, 512));
     cbT := cbT-512;
     nOrFd.n := nOrFd.n+512;
     END;
FlushIfil(hz, nOrFd.fd.ifil);
IF fTstFmt THEN PxFont(hfont^);
END;

*********************** *)

{$S SUinit}
{----------------}
PROCEDURE InitFmt;
{----------------}
CONST
        hDotsPerInch    = 100;
        mod12        = 0;
        mod12_10     = 1;
        mod10_12     = 2;
        mod18        = 3;
        mod24        = 4;
        clas12       = 5;
        clas18       = 6;
        clas24       = 7;
        {boldface unused}
        clas12_10    = 9;
        clas10_12    = 10;
        mod14        = 11;
        clas14       = 12;
        mod8_20      = 13;
        mod8_15      = 14;
        clas8_20     = 15;
        clas8_15     = 16;
VAR
fstyle: TFstyle;
ifnt: TB;
BEGIN
{$IFC fTraceSU}    LogCall;     {$ENDC}

argfam[mod12]        := tile12;
argfam[mod12_10]     := p10tile;
argfam[mod10_12]     := p12tile;
argfam[mod18]        := tile18;
argfam[mod24]        := tile24;
argfam[clas12]       := cent12;
argfam[clas18]       := cent18;
argfam[clas24]       := cent24;
argfam[8]               := boldface;
argfam[clas12_10]    := p10cent;
argfam[clas10_12]    := p12cent;
argfam[mod14]        := elite;
argfam[clas14]       := courier;
argfam[mod8_20]      := p20tile; (* was ocrb, before the font jerks destroyed it *)
argfam[mod8_15]      := p15tile;
argfam[clas8_20]     := p20tile;
argfam[clas8_15]     := p15tile;

WITH arceStd DO
     BEGIN
     cb := SIZEOF(TArce);
     ifnt := 0;
     fBold := FALSE;
     fItalic := FALSE;
     fVan := FALSE;
     fUnderline := FALSE;
     fKeep := FALSE;
     fOutline := FALSE;
     fShadow := FALSE;
     cbSuperscript := 0;
     icsFig := icsNil;
     lpFig := lpNil;
     END;
IF SIZEOF(TArpe) > 255 THEN Break(988);
WITH arpeStd DO
     BEGIN
     cb := SIZEOF(TArpe);
     itbLim := -100;
     xLftFst := hDotsPerInch; {was 84: didn't look right-now 1 inch}
     xLftBody := hDotsPerInch; {was 84: didn't look right-now 1 inch}
     xRt := (hDotsPerInch * 15) div 2; {was 530: didn't look right-now 7.5 inches}
     quad := quadL;
     yLd := 6; {changed from 0 to 6 = yOneAndHalfÑsee unitcom}
     yLine := 0;
     END;
WITH tbdNil DO
     BEGIN
     x := MAXINT;
     quad := quadL;
     fill3 := 0;
     fDecimalComma := FALSE;
     chLdr := ORD('.');
     tyfill := tyfillNil;
     END;

{Font style mapping tables initialization}
fstyle.tyfam := tyfamModern; fstyle.fsp := fsp12P12;
FOR ifnt := 0 TO ifntLst DO ampifntfstyle[ifnt] := fstyle;
fstyle.fsp := fsp8P20;  ampifntfstyle[mod8_20] := fstyle;
fstyle.fsp := fsp8P15;  ampifntfstyle[mod8_15] := fstyle;
fstyle.fsp := fsp12P10; ampifntfstyle[mod12_10] := fstyle;
fstyle.fsp := fsp12P12; ampifntfstyle[mod10_12] := fstyle;
fstyle.fsp := fsp12;    ampifntfstyle[mod12] := fstyle;
fstyle.fsp := fsp14;    ampifntfstyle[mod14] := fstyle;
fstyle.fsp := fsp18;    ampifntfstyle[mod18] := fstyle;
fstyle.fsp := fsp24;    ampifntfstyle[mod24] := fstyle;
fstyle.tyfam := tyfamClassic;
fstyle.fsp := fsp8P20;  ampifntfstyle[clas8_20] := fstyle;
fstyle.fsp := fsp8P15;  ampifntfstyle[clas8_15] := fstyle;
fstyle.fsp := fsp12P10; ampifntfstyle[clas12_10] := fstyle;
                        {ampifntfstyle[clas14] := fstyle; appears in error}
fstyle.fsp := fsp12P12; ampifntfstyle[clas10_12] := fstyle;
                        {ampifntfstyle[mod14] := fstyle; appears in error}
fstyle.fsp := fsp12;    ampifntfstyle[clas12] := fstyle;
fstyle.fsp := fsp14;    ampifntfstyle[clas14] := fstyle;
fstyle.fsp := fsp18;    ampifntfstyle[clas18] := fstyle;
fstyle.fsp := fsp24;    ampifntfstyle[clas24] := fstyle;
modernAmpfspifnt[fsp8P20]       := mod8_20;
modernAmpfspifnt[fsp8P15]       := mod8_15;
modernAmpfspifnt[fsp12P10]      := mod12_10;
modernAmpfspifnt[fsp12P12]      := mod10_12;
modernAmpfspifnt[fsp12]         := mod12;
modernAmpfspifnt[fsp14]         := mod14;
modernAmpfspifnt[fsp18]         := mod18;
modernAmpfspifnt[fsp24]         := mod24;
classicAmpfspifnt[fsp8P20]      := clas8_20;
classicAmpfspifnt[fsp8P15]      := clas8_15;
classicAmpfspifnt[fsp12P10]     := clas12_10;
classicAmpfspifnt[fsp12P12]     := clas10_12;
classicAmpfspifnt[fsp12]        := clas12;
classicAmpfspifnt[fsp14]        := clas14;
classicAmpfspifnt[fsp18]        := clas18;
classicAmpfspifnt[fsp24]        := clas24;
argmpfspifnt[tyfamModern]       := @modernAmpfspifnt;
argmpfspifnt[tyfamClassic]      := @classicAmpfspifnt;

InitFop;
END;


END.


ÿ