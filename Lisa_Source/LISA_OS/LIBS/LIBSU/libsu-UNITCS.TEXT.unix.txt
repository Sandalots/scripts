
{$R-}

UNIT UnitCs;

(* Copyright 1983, Apple Computer Inc. *)

INTRINSIC SHARED;

{ bh   1/06/84  --  Converted to New Print Shop Interface }
{ bh   1/06/84  --  Changed PrPrfDefault to PrRec68K }
{ s_w 11/01/82  --  D+/D- made dependent on FSymOk }
{ crc  8/19/82  --  FreeIcs returns if hcs = hNil }
{ jrm  6/29/82  --  USES change: LisaGraf => QuickDraw }
{ jrm and pjy  6/ 4/82  --  Scrap }
{ crc  6/03/82  --  Added tyxyHiDotMx }
{ crc  6/02/82  --  Conditional compilation }
{ crc 05/21/82  --  added printer preference field to streams }
{ crc 05/04/82  --  added cRef field to streams }
{ crc 04/06/82  --  integration with new font manager }
{ tjm 02/11/82  --  add tyxy to pglp }
{ bas 01/27/82  --  add tyxy for Dot Matrix }
{ bas 12/10/81  --  integration with font manager }

INTERFACE

USES
     {$U libsm/UnitStd    } UnitStd,
     {$U libsm/UnitHz     } UnitHz,
     {$U libos/SysCall    } SysCall,
     {$U libqd/Storage    } Storage,
     {$U libqd/QuickDraw  } QuickDraw,
     {$U libfm/FontMgr    } FontMgr,
     {$U libpm/PmDecl     } PmDecl,
     {$U libpr/PrStdInfo  } PrStdInfo,
     {$U libsu/UnitFmt    } UnitFmt;

CONST
   {$IFC NOT fDbgOk} fTstCs = FALSE; {$ENDC}
   clpdDft = 4;

   icsLst = 10;
   ointNil = 0;
   orecNil = MAXINT;
   lpMax = 2000000000;

   iprocSeqLpd = 0;
   iprocFindLpFixed = 1;
   iprocHilight = 2;
   iprocCrd = 3;
   iprocFreeIcs = 4;
   iprocFSelLpBounds = 5;
   iprocPxHcs = 6;
   iprocLstCs = 6;

   iimgpScrn = 0;
   iimgpQume = 1;
   iimgpCIto = 2;
   iimgpLst  = 2;

TYPE TLp = TL;           {logical position}
  TArglp = ARRAY [0..0] OF TLp;
  TRglp = ^TArglp;

  TInt = RECORD       {Interval}
         ointNxt: TC;
         ointPrv: TC;
         ointParent: TC;
         lpFst:  TLp;
         lpLim:   TLp;
         ics:     TB;
         fValid: TF;
         END;
  TPint = ^TInt;

  TArgint = ARRAY [0..0] OF TInt;
  TRgint = ^TArgint;

  THpic = PicHandle;
  TPpic = PicPtr;

  TFigd = RECORD {figure descriptor}
          n: TN;         {named object which maps the picture}
          ppic: TPpic;   { (very temporary!) pointer to picture itself }
          END;

  TTyset = RECORD fRce: TF; fParBnds: TF; fRpe: TF END;
          {used only as parameter to SetLpd}
  TAlpd = RECORD {logical position descriptor}
          ics:    TB;
          ilpd:   TB;
          fParSt: TF;
          lp:     TLp;
          lpLim:  TLp;
          lpSon:  TLp;
          icsSon: TB;
          tyset:  TTyset;
          rce:    TRce;
          lpFstPar: TLp;
          lpLimPar: TLp;
          rpe:    TRpe;
          arce:   TArce;
          arpe:   TArpe;
          figd:   TFigd;
          END;
  TLpd = ^TAlpd;

  TTycs = (tycsNil, {class types}
          tycsUs,         {unformatted string}
          tycsEd,         {editable document}
          tycsFf,         {formatted file}
          tycsUf,         {unformatted file}
          tycsFm,         {form}
          tycsPg,         {paginated doc}
          tycsRuler,      {screen rulers}
          tycsLst,        {for enumerations}
          tycsFld,        {Field Editor Field}
          tycsMtrx,       {Lisa Calc Matrix}
          tycsFig         {Figure File}
          );

  TCspd = RECORD        { Class procedure descriptor }
           argproc: ARRAY [0..iprocLstCs] OF TProc;
           argimgp: ARRAY [0..iimgpLst] OF TProc;
           END;

  TTyxy = (tyxyScrn, tyxyIP, tyxyPgFract, tyxyMica, tyxyDotMx, tyxyHiDotMx,
           tyxyLDotMx, tyxyLHiDotMx, tyxyNew1, tyxyNew2, tyxyNew3,
           tyxyNew4, tyxyNew5, tyxyNew6, tyxyNew7, tyxyNew8, tyxyNil);

  TCs = RECORD {class; source of text}
          cspd:       TCspd;
          hz:         THz;
          tycs:       TTycs;
          tyxy:       TTyxy;
          ointFst:    TC;
          orecConFst: TC;
          cRef:       TC;
          prprf:      TPrRec;
          END;
  TPcs = ^TCs;
  THcs = ^TPcs;
  TArghcs = ARRAY [0..0] OF THcs;
  TRghcs = ^TArghcs;

  TPglp = RECORD      { page layout parameters }
          icsHdr:  TB;
          icsFtr:  TB;
          yTopHdr: TY;
          dyHdr: TY;
          dyHtBody:   TY;
          dyBotBody:   TY;
          tyxy:    TTyxy;
          END;

  TAmpicsprcs = ARRAY [0..0] OF TL;   {process id of ics}
  TMpicsprcs = ^TAmpicsprcs;

VAR
 {$IFC fDbgOk} fTstCs: TF; {$ENDC}

 tysetRpe: TTyset;
 tysetBnds: TTyset;

 rghcs:  TRghcs;
 mpicsprcs: TMpicsprcs;
 icsMac: TC;

 argalpd: ARRAY [0..clpdDft] OF TAlpd;
 arglpd:  ARRAY [0..clpdDft] OF TLpd;
 lpdStd: TLpd;
 tysetStd: TTyset;
 lldInt: TLld;
 tyxyLst: TTyxy;
 tyxyCs: TTyxy;
 pglpStd: TPglp;

 fNoInvalidate: TF; (* this must be the last VAR, since its address is used
                       in SCRAP.TEXT in an HzInit call as the pBase parameter *)

FUNCTION LpMinP(lp1: TLp; lp2: TLp) : TLp;

FUNCTION LpMaxP(lp1: TLp; lp2: TLp) : TLp;

PROCEDURE PxRgcs;

PROCEDURE PxAchad(VAR achad: TAchad);

PROCEDURE PxCs(ics: TB);

PROCEDURE FreeInt(ics: TB; oint: TC);

FUNCTION OintMark
 (oint: TC; ics: TB; lpFst: TLp; lpLim: TLp; fValid: TF; ointParent: TC) : TC;

FUNCTION IcsCreate(tycs: TTycs; cbCs: TC; hz: THz) : TB;

PROCEDURE FreeIcs(ics: TB);

PROCEDURE PxLpd(lpd: TLpd; VAR achad: TAchad);

PROCEDURE SetLpd(lpd: TLpd; ics: TB; lp: TLp; tyset: TTyset; VAR achad: TAchad);

PROCEDURE SeqLpd(lpd: TLpd; VAR achad: TAchad);

FUNCTION FParStart(ics: TB; lp: TLp) : TF;

PROCEDURE FindLpFixed(ics: TB; lp: TLp; VAR lpFixed: TLp);

PROCEDURE FindLpFstPar(ics: TB; lp: TLp; VAR lpFixed: TLp);

PROCEDURE InitCs(hz: THz);

PROCEDURE PxInt(oint: TC);

PROCEDURE AdjustLps(ics: TB; lpFstInv: TLp; lpLimInv: TLp; dlpAdjust: TLp);

FUNCTION FSelLpBounds(lp: TLp; ics: TB;
                      VAR lpFstSel, lpLimSel, lpFstAtom, lpLimAtom: TLp) : TF;

FUNCTION TrueStdSelLpBounds(lp: TLp; ics: TB;
                      VAR lpFstSel, lpLimSel, lpFstAtom, lpLimAtom: TLp) : TF;

FUNCTION FTextIcs(ics: TB) : TF;

PROCEDURE PurgeIcsPrcs;

IMPLEMENTATION

{$IFC fSymOK}
   {$D+}
{$ELSEC}
   {$D-}
{$ENDC}

CONST cintMore = 5;
      cicsMore = 10;
      icsFree = -1;

TYPE TARgw = ARRAY [0..0] OF TW;
  TRgw = ^TARgw;

  TAWad = RECORD
     rgw: TRgw;
     iwFst: TC;
     iwLIm: TC;
     END;
  TWad = ^TAWad;

VAR cbMoreInts: TC;
    hzCs: THz;
    amptycsfText: ARRAY [TTycs] OF TF;

{$S SUlwwrk}
FUNCTION FTextIcs{ics: TB) : TF};
BEGIN
{$IFC fTraceSU}    LogCall;     {$ENDC}
FTextIcs := amptycsfText[rghcs^[ics]^^.tycs];
END;

{$S SUlwwrk}
FUNCTION LpMinP;
BEGIN
{$IFC fTraceSU}    LogCall;     {$ENDC}
IF lp1 < lp2 THEN LpMinP := lp1 ELSE LpMinP := lp2;
END;

{$S SUlwwrk}
FUNCTION LpMaxP;
BEGIN
{$IFC fTraceSU}    LogCall;     {$ENDC}
IF lp1 < lp2 THEN LpMaxP := lp2 ELSE LpMaxP := lp1;
END;

{$S SUlwwrk}
PROCEDURE AdjustLps;
LABEL   15;
VAR
oint:   TC;
pint: TPint;
pintParent: TPint;
lpFstParentInv: TLp;
BEGIN
{$IFC fTraceSU}    LogCall;     {$ENDC}
IF fTstCs THEN
     WRITELN('AdjustLps(', ics, ', ', lpFstInv, ', ', lpLimInv,
       ', ', dlpAdjust, ')');
oint := rghcs^[ics]^^.ointFst;
WHILE oint <> ointNil  DO
     BEGIN
     pint := POINTER(ORD(lldInt)+oint);
     { special case - empty intervals }
     IF pint^.lpFst = pint^.lpLim THEN
             BEGIN
             IF (lpFstInv = pint^.lpFst)
               OR ((lpFstInv < pint^.lpFst) AND (pint^.lpFst < lpLimInv)) THEN
                     GOTO 15;
             END
     ELSE IF pint^.lpFst >= lpLimInv THEN
             BEGIN
             IF pint^.lpFst > lpFstInv THEN
                  pint^.lpFst := pint^.lpFst+dlpAdjust
             ELSE GOTO 15;
             pint^.lpLim := pint^.lpLim+dlpAdjust;
             END
     ELSE IF lpFstInv >= pint^.lpLim THEN
             BEGIN
             END
     ELSE    BEGIN
15:
             { IF NOT fNoInvalidate THEN } pint^.fValid := FALSE;
             IF pint^.ointParent <> ointNil THEN
                     BEGIN
                     pintParent := POINTER(ORD(lldInt)+pint^.ointParent);

{Slightly risky dangling pint here - seems OK for now}
                     lpFstParentInv := pintParent^.lpFst+
                       LpMaxP(0, lpFstInv-pint^.lpFst);
                     AdjustLps(pintParent^.ics, lpFstParentInv,
                       lpFstParentInv+(lpLimInv-lpFstInv), 0);
                     END;
             END;
     IF fTstCs THEN PxInt(oint);
     oint := pint^.ointNxt;
     END;
END;


{$S SegPxScrap}
PROCEDURE PxAchad;
{$IFC fDbgOk}
LABEL   100;
{$ENDC}
VAR
ich:    TC;
BEGIN
{$IFC fTraceSU}    LogCall;     {$ENDC}
{$IFC fDbgOk}
WRITE('PxAchad - chad.ichFst: ', achad.ichFst, ' chad.ichLim: ', achad.ichLim,
 ', chad.rgch: ', ORD(achad.rgch));
IF achad.rgch = NIL THEN
     WRITELN(', rgch: NIL')
ELSE    BEGIN
     WRITELN;
     FOR ich := achad.ichFst TO achad.ichLim-1 DO
             BEGIN
             CASE ORD(achad.rgch^[ich]) OF
             chTab:  BEGIN WRITE('<Tab>'); GOTO 100; END;
             chCr:   BEGIN WRITE('<Cr>'); GOTO 100; END;
             END;
             {ELSE} WRITE(CHR(achad.rgch^[ich]));
100:            END;
     WRITELN;
     END;
{$ENDC}
END;

{$S SegPxScrap}
PROCEDURE PxInt { (oint: TC) };
VAR
hcs:    THcs;
pint:   TPint;
BEGIN
{$IFC fTraceSU}    LogCall;     {$ENDC}
{$IFC fDbgOk}
WRITE('oint:', oint:4);
pint := POINTER(ORD(lldInt)+oint);
WITH pint^ DO
 BEGIN
 WRITELN('; lp''s: [', lpFst, ' ', lpLim,
       ']; ics:', ics:4, '; fValid: ', ORD(fValid):1, ';');
 WRITELN('                                 ',
        'ointNxt:', ointNxt:4,
        '; -Prv:', ointPrv:4,
        '; -Parent:', ointParent:4);
 END;
{$ENDC}
END;

{$S SegPxScrap}
PROCEDURE CheckIcsInt(oint: TC);
LABEL   11;
VAR
ointT:   TC;
pint:   TPint;
pintT:   TPint;
BEGIN
{$IFC fTraceSU}    LogCall;     {$ENDC}
pint := POINTER(ORD(lldInt)+oint);
IF pint^.ics = icsFree THEN
     ointT := lldInt^.olleFreeFst
ELSE ointT := rghcs^[pint^.ics]^^.ointFst;
WHILE ointT <> ointNil DO
     BEGIN
     IF oint = ointT THEN GOTO 11;
     pintT := POINTER(ORD(lldInt)+ointT);
     ointT := pintT^.ointNxt;
     END;
Break(29);
11:
END;

{$S SegPxScrap}
PROCEDURE CheckInts;
VAR
hcs:    THcs;
oint:   TC;
pint:   TPint;
BEGIN
{$IFC fTraceSU}    LogCall;     {$ENDC}
{ this should be unnecessary now that we have icsFree
oint := lldInt^.olleFreeFst;
WHILE oint <> ointNil DO
     BEGIN
     pint := POINTER(ORD(lldInt)+oint);
     pint^.ics := icsNil;
     oint := pint^.ointNxt;
     END;
}
oint := SIZEOF(TAlld);
WHILE oint < lldInt^.olleLim DO
     BEGIN
     CheckIcsInt(oint);
     oint := oint+SIZEOF(TInt);
     END;
END;

{$S SegPxScrap}
{-----------------------------}
PROCEDURE PxHcs(hcs: THcs; proc: TProc); EXTERNAL;
{-----------------------------}

{$S SegPxScrap}
PROCEDURE PxCs;
VAR
hcs:    THcs;
oint:   TC;
pint:   TPint;
proc:   TProc;
BEGIN
{$IFC fTraceSU}    LogCall;     {$ENDC}
{$IFC fDbgOk}
CheckInts;
hcs := rghcs^[ics];
IF ORD(hcs) <> ORD(hNil) THEN
  WITH hcs^^ DO
     BEGIN
     WRITELN('ics: ', ics:3, ' tycs: ', ORD(tycs):3);
     WRITELN('ointFst: ', ointFst, ', orecConFst: ', orecConFst);
     oint := ointFst;
     WHILE oint <> ointNil DO
             BEGIN
             WRITE('oint:', oint:4);
             pint := POINTER(ORD(lldInt)+oint);
             WITH pint^ DO
                BEGIN
                WRITE('; lp''s: [', lpFst, ' - ', lpLim,
                      ']; fValid: ', ORD(fValid):1, ';');
                IF ointParent <> ointNil
                   THEN WRITELN(' (', ointParent:4, ')')
                   ELSE WRITELN;
                END;
             oint := pint^.ointNxt;
             END;
     IF tycs <> tycsNil THEN
             BEGIN
             proc := hcs^^.cspd.argproc[iprocPxHcs];
             IF proc <> POINTER(procNil) THEN PxHcs(hcs, proc);
             END;
     END;
{$ENDC}
END;


{$S SegPxScrap}
PROCEDURE PxRgcs;
VAR
ics:    TB;
BEGIN
{$IFC fTraceSU}    LogCall;     {$ENDC}
FOR ics := 0 TO icsMac-1 DO PxCs(ics);
END;

{$S SUlwwrk}
PROCEDURE FreeInt;
VAR
pint: TPint;
BEGIN
{$IFC fTraceSU}    LogCall;     {$ENDC}
IF fTstCs THEN WRITELN('FreeInt(, ', oint, ')');
pint := POINTER(ORD(lldInt)+oint);
{ics should be a local variable so we ignore its input value}
{someday we should bite the bullet and change the interface}
ics := pint^.ics;
IF ics <> icsFree THEN
     BEGIN
     pint^.ics := icsFree;
     MoveOlle(lldInt, oint, lldInt^.olleFreeFst, rghcs^[ics]^^.ointFst);
     IF fTstCs THEN PxCs(ics);
     END;
END;

{$S SUlwwrk}
FUNCTION OintAlloc(ics: TB) : TC;
{This function must remain PRIVATE - OintMark is recquired to leave int consistent}
VAR
oint:   TC;
int: TInt;
h:    TH;
ointLimOld: TC;
pint: TPint;
BEGIN
{$IFC fTraceSU}    LogCall;     {$ENDC}
IF fTstCs THEN WRITE('OintAlloc(', ics, ') : ');
IF lldInt^.olleFreeFst = olleNil THEN
     BEGIN
     ointLimOld := lldInt^.olleLim;
     h := @lldInt;
     ChangeSizeH(hzCs, h, ointLimOld+cbMoreInts);
     lldInt^.olleLim := ointLimOld+cbMoreInts;
     InitOlleFree(lldInt, ointLimOld, SIZEOF(int));
     oint := lldInt^.olleFreeFst;
     WHILE oint <> ointNil DO
          BEGIN
          pint := POINTER(ORD(lldInt)+oint);
          pint^.ics := icsFree;
          oint := pint^.ointNxt;
          END;
     END;
oint := lldInt^.olleFreeFst;
IF oint = ointNil THEN Break(24);
MoveOlle(lldInt, oint, rghcs^[ics]^^.ointFst, lldInt^.olleFreeFst);
pint := POINTER(ORD(lldInt)+oint);
pint^.ics := ics;
OintAlloc := oint;
IF fTstCs THEN WRITELN(oint);
END;

{$S SUlwwrk}
FUNCTION OintMark;
VAR pint: TPint;
BEGIN
{$IFC fTraceSU}    LogCall;     {$ENDC}
IF fTstCs THEN WRITELN('OintMark...');
IF oint = ointNil THEN oint := OintAlloc(ics);
pint := POINTER(ORD(lldInt)+oint);
IF pint^.ics <> ics THEN
  BEGIN
  MoveOlle(lldInt, oint, rghcs^[ics]^^.ointFst, rghcs^[pint^.ics]^^.ointFst);
  pint^.ics := ics;
  END;
IF lpFst <> lpNil THEN pint^.lpFst := lpFst;
IF lpLim <> lpNil THEN pint^.lpLim := lpLim;
pint^.ointParent := ointParent;
pint^.fValid := fValid;
OintMark := oint;
IF fTstCs THEN BEGIN WRITELN('...OintMark returning ', oint); PxCs(ics); END;
END;

{-------------------------------------------------}
PROCEDURE CallFree(ics: TB; proc: TProc); EXTERNAL;
{-------------------------------------------------}

{$S SUlwwrk}
{-----------------------------}
PROCEDURE FreeIcs { (ics: TB) };
{-----------------------------}
VAR proc:  TProc;
    hcs: THcs;
    oint: TC;
BEGIN
{$IFC fTraceSU}    LogCall;     {$ENDC}
hcs := rghcs^[ics];
IF hcs = POINTER(ORD(hNil)) THEN EXIT(FreeIcs);
WHILE hcs^^.ointFst <> ointNil DO
     oint := OintMark(hcs^^.ointFst, icsNil, lpNil, lpNil, FALSE, ointNil);
proc := hcs^^.cspd.argproc[iprocFreeIcs];
IF fTstCs THEN WRITELN('FreeIcs - proc: ', ORD(proc));
IF proc <> POINTER(procNil) THEN CallFree(ics, proc);
IF rghcs^[ics] <> POINTER(ORD(hNil)) THEN
     BEGIN
     FreeBk(rghcs^[ics]^^.hz, POINTER(ORD(rghcs^[ics])), tybkStd);
     rghcs^[ics] := POINTER(ORD(hNil));
     END;
mpicsprcs^[ics] := ORD(pNil);
END;


{$S SUcold}
{-----------------------------}
PROCEDURE PurgeIcsPrcs;
{-----------------------------}
LABEL 777;
VAR prcsCur:  TL;
    ics: TB;
    hcs: THcs;
    oint: TC;
    pint: TPint;
BEGIN
{$IFC fTraceSU}    LogCall;     {$ENDC}
prcsCur := My_Id;
FOR ics := 0 TO icsMac - 1 DO
     BEGIN
     hcs := rghcs^[ics];
     IF (ORD(hcs) <> ORD(hNil)) AND (mpicsprcs ^[ics] = prcsCur) THEN
          BEGIN
          WHILE hcs^^.ointFst <> ointNil DO
               BEGIN
               pint := POINTER(ORD(lldInt) + hcs^^.ointFst);
               IF pint^.ics <> ics THEN
                    GOTO 777;
               FreeInt(ics, hcs^^.ointFst);
               END;
777:
          FreeIcs(ics);
          END;
     END;
END;


{$S SUlwwrk}
{--------------------------------------------------------------------}
FUNCTION IcsCreate { (tycs: TTycs; cbCs: TC; cint: TB; hz: THz) : TB };
{--------------------------------------------------------------------}
LABEL   100;
VAR ics:    TB;
 pcs:    TPcs;
 oint:   TC;
 pint:   TPint;
 hcs:    THcs;
 iproc:  TC;
BEGIN
{$IFC fTraceSU}    LogCall;     {$ENDC}
IF fTstCs THEN WRITELN('...In IcsCreate.... rghcs = ', ORD(rghcs));
 { Find the first unused ics. }
FOR ics := 0 TO icsMac - 1 DO
   IF ORD(rghcs^[ics]) = ORD(hNil) THEN GOTO 100;
 { None found: make some more. }
ChangeSizeH(hzCs, @rghcs, (icsMac + cicsMore) * SIZEOF(THcs));
ChangeSizeH(hzCs, @mpicsprcs, (icsMac + cicsMore) * SIZEOF(TL));
FOR ics := icsMac TO icsMac + cicsMore - 1 DO
     BEGIN
     rghcs^[ics] := POINTER(ORD(hNil));
     mpicsprcs^[ics] := ORD(pNil);
     END;
ics := icsMac;
icsMac := icsMac + cicsMore;
100:
hcs := POINTER(ORD(HAllocate(hz, cbCs)));
rghcs^[ics] := hcs; pcs := hcs^;
pcs^.tycs := tycs;
pcs^.tyxy := tyxyNil;
pcs^.ointFst := ointNil;
pcs^.orecConFst := orecNil;
pcs^.hz := hz;
mpicsprcs^[ics] := My_Id;
FOR iproc := 0 TO iprocLstCs DO pcs^.cspd.argproc[iproc] := POINTER(procNil);
FOR iproc := 0 TO iimgpLst   DO pcs^.cspd.argimgp[iproc] := POINTER(procNil);
pcs^.prprf := PrRec68K^;
IcsCreate := ics;
END;

{$S SegPxScrap}
{------------------------------------------------}
PROCEDURE PxLpd { (lpd: TLpd; VAR achad: TAchad) };
{------------------------------------------------}
BEGIN
{$IFC fTraceSU}    LogCall;     {$ENDC}
{$IFC fDbgOk}
WRITELN('PxLpd');
WRITE('   ics:', lpd^.ics:3, ' lp: ', lpd^.lp, '; ilpd:', lpd^.ilpd:2);
WRITELN('   tyset: [ fRce fParBnds fRpe ] = [', ORD(lpd^.tyset.fRce):1,
                 ORD(lpd^.tyset.fParBnds):1, ORD(lpd^.tyset.fRpe):1, ']');
WRITELN( ' icsSon:', lpd^.icsSon:3, '; lpSon: ', lpd^.lpSon);
WRITE('; fParSt: ', ORD(lpd^.fParSt):1, '; lpLim: ', lpd^.lpLim);
WRITELN(' lpFstPar: ', lpd^.lpFstPar, ' lpLimPar: ', lpd^.lpLimPar);
WRITELN('   rce: ', ORD(lpd^.rce), ' rpe: ', ORD(lpd^.rpe));
PxArce(lpd^.rce^); PxArpe(lpd^.rpe^); PxAchad(achad);
{$ENDC}
END;

{$S SUlwwrk}
{---------------------------------------------------------------------}
PROCEDURE CallSeq(lpd: TLpd; VAR achad: TAchad; proc: TProc); EXTERNAL;
{---------------------------------------------------------------------}

{$S SUlwwrk}
{-------------------------------------------------}
PROCEDURE SeqLpd { (lpd: TLpd; VAR achad: TAchad) };
{-------------------------------------------------}
VAR proc:  TProc;
BEGIN
{$IFC fTraceSU}    LogCall;     {$ENDC}
WITH achad DO IF rgch = NIL THEN BEGIN ichFst := 0; ichLim := MAXINT; END;
{ achadSave := achad; }
proc := rghcs^[lpd^.ics]^^.cspd.argproc[iprocSeqLpd];
CallSeq(lpd, achad, proc);
{
IF NOT lpd^.tyset.fRce THEN
  WHILE (lpd^.rce^.fVan) AND
        (achad.ichFst <> achad.ichLim) DO
       BEGIN
       achad := achadSave;
       CallSeq(lpd, achad, proc);
       END;
}
WITH achad, lpd^ DO
  IF (ichFst = ichLim) OR (lpLim <= lp) THEN
       BEGIN
       lpLimPar := lp;
       lpFstPar := lp;
       lpLim := lp;
       ichLim := ichFst;
       END;
IF fTstCs THEN PxLpd(lpd, achad);
END;

{$S SUlwwrk}
{--------------------------------------------------------------------------------}

PROCEDURE SetLpd { (lpd: TLpd; ics: TB; lp: TLp; tyset: TTyset; VAR achad: TAchad)
};
{--------------------------------------------------------------------------------}

BEGIN
{$IFC fTraceSU}    LogCall;     {$ENDC}
IF fTstCs THEN WRITELN('SetLpd(ics: ', ics, ', lp: ', lp, ', fRce: ', tyset.fRce,
 ', fParBnds: ', tyset.fParBnds, ', fRpe: ', tyset.fRpe);
lpd^.ics := ics;
lpd^.tyset := tyset;
lpd^.lpLim := lp;
lpd^.icsSon := icsNil;
SeqLpd(lpd, achad);
END;

PROCEDURE CallFindLpFixed(ics: TB; lp: TLp; VAR lpFixed: TLp; proc: TProc);
EXTERNAL;

{$S SUcold}
{--------------------------------------------}
FUNCTION FParStart { (ics: TB; lp: TLp) : TF };
{--------------------------------------------}
VAR achad:  TAchad;
BEGIN
{$IFC fTraceSU}    LogCall;     {$ENDC}
achad.ichFst := 0; achad.ichLim := MAXINT; achad.rgch := NIL;
SetLpd(lpdStd, ics, lp, tysetStd, achad);
FParStart := (lpdStd^.fParSt) OR (lpdStd^.lp = lpdStd^.lpLim);
END;

{$S SUlwwrk}
PROCEDURE FindLpFixed;
BEGIN
{$IFC fTraceSU}    LogCall;     {$ENDC}
CallFindLpFixed(ics, lp, lpFixed, rghcs^[ics]^^.cspd.argproc[iprocFindLpFixed]);
END;

{$S SUlwwrk}
PROCEDURE FindLpFstPar;
VAR
achad:  TAchad;
BEGIN
{$IFC fTraceSU}    LogCall;     {$ENDC}
IF fTstCs THEN
     BEGIN
     WRITELN('FindLpFstPar(', ics, ', ', lp, ')');
     PxCs(ics);
     END;
achad.ichFst := 0; achad.ichLim := MAXINT; achad.rgch := NIL;
SetLpd(lpdStd, ics, lp, tysetBnds, achad);
lpFixed := lpdStd^.lpFstPar;
IF fTstCs THEN
     WRITELN('lpFixed: ', lpFixed);
END;

FUNCTION CallFSelLpBounds(lp: TLp; ics: TB;
           VAR lpFstSel, lpLimSel, lpFstAtom, lpLimAtom: TLp; proc: TProc) : TF;
EXTERNAL;

{$S SUlwwrk}
FUNCTION FSelLpBounds { (lp: TLp; ics: TB;
           VAR lpFstSel, lpLimSel, lpFstAtom, lpLimAtom: TLp) : TF};
 { sets lpFstSel, lpLimSel to be the lps of "outside" selectable boundaries }
 { around lp. Returns TRUE if lp is selectable. }
 { if lp is selectable lpFstAtom, lpLimAtom are the "inside" selectable boundaries }
 { that is - the selection may not get smaller than [lpFstAtom..lpLimAtom). }
BEGIN
{$IFC fTraceSU}    LogCall;     {$ENDC}
FSelLpBounds := CallFSelLpBounds(lp, ics, lpFstSel, lpLimSel, lpFstAtom, lpLimAtom,
 rghcs^[ics]^^.cspd.argproc[iprocFSelLpBounds]);
END;

{$S SUlwwrk}
FUNCTION TrueStdSelLpBounds { (lp: TLp; ics: TB;
           VAR lpFstSel, lpLimSel, lpFstAtom, lpLimAtom: TLp) : TF};
VAR
achad:  TAchad;
BEGIN
{$IFC fTraceSU}    LogCall;     {$ENDC}
IF fTstCs THEN WRITE('TrueStdSelLpBounds(', lp, ')');
lpFstSel := 0;
lpLimSel := lpMax;
lpFstAtom := lp;
achad.ichFst := 0; achad.ichLim := MAXINT; achad.rgch := NIL;
SetLpd(lpdStd, ics, lp, tysetStd, achad);
WHILE lpdStd^.rce^.fVan AND (lpdStd^.lpLim > lpdStd^.lp) DO
     BEGIN
     achad.rgch := NIL; achad.ichFst := 0; achad.ichLim := 1;
     SeqLpd(lpdStd, achad);
     END;
IF (lpdStd^.rce^.icsFig = icsNil) OR (lpdStd^.lpLim = lpdStd^.lp) THEN
     lpLimAtom := lp
ELSE lpLimAtom := lpdStd^.lp + 1;
TrueStdSelLpBounds := TRUE;
END;

{$S SUinit}
PROCEDURE InitCs (* hz: THz *);
CONST
        vDotsPerInch    = 72;
VAR ilpd:   TC;
    ich:    TC;
    sp1:    STRING[40];
    ichLim: TC;
    rgtbd:  TRgtbd;
    ics:    TB;
    hcs:    THcs;
    rgh:    TRgh;
    tycs:   TTycs;
    oint, ointLim:TC;
    int:    TInt;
    rgchSrc:TRgch;
    p:      TP;
    iproc:  TB;
    pint:   TPint;
BEGIN
{$IFC fTraceSU}    LogCall;     {$ENDC}
FOR tycs := tycsNil TO tycsLst DO amptycsfText[tycs] := TRUE;
amptycsfText[tycsRuler] := FALSE;
amptycsfText[tycsPg] := FALSE;
amptycsfText[tycsFig] := FALSE;

fNoInvalidate := FALSE;
hzCs := hz;
ointLim := SIZEOF(lldInt^)+40*SIZEOF(int);
AllocBk(hzCs, @lldInt, ointLim, tybkStd);
lldInt^.olleLim := ointLim;
lldInt^.olleFreeFst := olleNil;
InitOlleFree(lldInt, SIZEOF(lldInt^), SIZEOF(int));
oint := lldInt^.olleFreeFst;
WHILE oint <> ointNil DO
     BEGIN
     pint := POINTER(ORD(lldInt)+oint);
     pint^.ics := icsFree;
     oint := pint^.ointNxt;
     END;
cbMoreInts := cintMore*SIZEOF(int);
FOR ilpd := 0 TO clpdDft-1 DO
     BEGIN
     arglpd[ilpd] := @argalpd[ilpd];
     arglpd[ilpd]^.ilpd := ilpd;
     END;
lpdStd := arglpd[0];

WITH tysetStd  DO BEGIN fRce :=  TRUE; fRpe :=  TRUE; fParBnds := FALSE; END;
WITH tysetRpe  DO BEGIN fRce := FALSE; fRpe :=  TRUE; fParBnds := FALSE; END;
WITH tysetBnds DO BEGIN fRce := FALSE; fRpe := FALSE; fParBnds :=  TRUE; END;

icsMac := icsLst+1;
AllocBk(hzCs, @rghcs, icsMac*SIZEOF(hcs), tybkStd);
AllocBk(hzCs, @mpicsprcs, icsMac*SIZEOF(TL), tybkStd);
rgh := POINTER(ORD(rghcs));
IF fTstCs THEN WRITELN('.....In InitCS... rgh = ', ORD(rgh));
FOR ics := 0 TO icsLst DO
     BEGIN
     rgh^[ics] := hNil;
     mpicsprcs ^[ics] := ORD(pNil);
     END;
IF IcsCreate(tycsNil, SIZEOF(TCS), hzCs) <> icsNil THEN Break(27);

tyxyCs := tyxyScrn;
tyxyLst := tyxyLDotMx; {tyxyLHiDotMx}

WITH pglpStd DO
  BEGIN
  tyxy := tyxyScrn;
  (*
  yTopHdr := vDotsPerInch div 2; {was 33: didn't look right-now .5 inches}
  dyHdr := vDotsPerInch div 2; {was 33: didn't look right-now .5 inches}
  dyHtBody := vDotsPerInch * 10; {was 616: didn't look right-now 10 inches}
  dyBotBody := (vDotsPerInch * 21) div 2; {was 594: didn't look right-now 10.5 inches}
  *)
  yTopHdr := vDotsPerInch div 2; {was 33: now .5 inches}
  dyHdr := vDotsPerInch div 2; {was 33: now .5 inches (+yTopHdr)}
  dyHtBody := vDotsPerInch * 9; {was 616: now 10 inches (+yTopHdr+dyHdr)}
  dyBotBody := vDotsPerInch * 9 + vDotsPerInch div 2; {was 594: -now 10.5 inches (+yTopHdr+dyHdr)}

  icsHdr := icsNil;
  icsFtr := icsNil;
  END;
END;


END.



ÿ