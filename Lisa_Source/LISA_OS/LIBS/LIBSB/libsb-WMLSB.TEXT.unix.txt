{ ==========================================================================}
{                                                                           }
{                     Copyright 1983, Apple Computer, Inc.                  }
{                                                                           }
{ ==========================================================================}


{$S SBothrwk}

UNIT WmlSb;

INTRINSIC;

{ bas 1/4/83  -- drag-ahead fix to DragThumb and DragSkewer }
{ bas 11/1/82 -- segmentation }
{ bas 10/1/82 -- DragThumb moves a ghost }
{ bkd 8/30/82 -- Arrow and Paging symbols drawn from WMFont }
{ bas 8/19/82 -- HideSBorder fix }
{ bas 8/09/82 -- HideSBorder }
{ bas 7/29/82 -- dhSbox and dhThumb = 24 (instead of 23) }
{ bas 7/27/82 -- new PaintSb and FixVisIcons }
{ bas 7/02/82 -- iconPagA and iconPagB }
{ bas 4/12/82 -- fix DragThumb bug }
{ bas 4/02/82 -- Change CdThumbFromC and CThumbFromCd from REAL to LONGINT }
{ bas 3/29/82 -- EraseSb & EraseSbar, changed arrow from pointing in to pointing
                 out, version 4.0 }
{ edited by bas 3/8/82  rgSymArw, version 3.1}


INTERFACE

USES
    {$U libsm/UnitStd   } UnitStd,
    {$U libsm/UnitHz    } UnitHz,
    {$U libqd/Storage   } Storage,
    {$U libqd/QuickDraw } QuickDraw,
    {$U libfm/FontMgr   } FontMgr,
    {$U libsb/WmlStd    } WmlStd,
{$IFC FWMLOS}
    {$U libos/Syscall   } Syscall,
{$ELSEC}
    {$U obj:OsStub    } Syscall,
{$ENDC}
    {$U libwm/Events    } Events;



CONST
    dhSbox   = 24;      dvSbox   = 16;
    dhThumb  = 24;      dvThumb  = 16;
    dhSkewer =  6;      dvSkewer =  4;

TYPE
    TIcon  = (iconSkewer, iconArwA, iconPagA, iconGryA,
              iconThumb, iconGryB, iconPagB, iconArwB);
    TSicon = SET OF TIcon;

    THSb = ^TPSb;
    TPSb = ^TSb;

     { scroll box }
    TSb = RECORD
            r:         TR;
            tyvh:      TTyvh;
            sicon:     TSicon;  { set of icons sb may display }
            siconVis:  TSicon;  { set of icons currently visible }
            cThumb:    TC;      { in thousandths }
            refCon:    TL;      { for application's usage }
            fHead:     TF;
            hsbPrv:    THSb;
            hsbNxt:    THSb;
          END;

    TSbList = RECORD
                hz:      THz;     { zone to allocate from }
                hsbFst:  THSb;    { first sbox on list }
              END;

     { scroll box icon fence posts -- posts[icon] is coordinate of end of icon }
    TPosts = ARRAY[iconSkewer..iconArwB] OF TCd;

VAR
    hsbNil:    THSb;
    dptSbox:   TPt;
    dptThumb:  TPt;
    dptSkewer: TPt;

    TyvhNOT: ARRAY [TTyvh] OF TTyvh;

    fTstSb: TF;



PROCEDURE InitWmlSb;
PROCEDURE InitSbList    (VAR sbList: TSbList; hz: THz);
FUNCTION  SbCreate      (VAR sbList: TSbList; hsbBefore: THSb; tyvhSb: TTyvh;
                         ptTopLeft: TPt; cdBotRt: TCd): THSb;
PROCEDURE KillSb        (VAR sbList: TSbList; hsbKill: THSb);
PROCEDURE GetSbRect     (hsb: THSb; VAR r: TR);
PROCEDURE SetSbRect     (hsb: THSb; r: TR);
FUNCTION  RefconSb      (hsb: THSb): TL;
PROCEDURE SetSbRefcon   (hsb: THSb; refcon: TL);
FUNCTION  HsbFirst      (VAR sbList: TSbList): THSb;
FUNCTION  HsbNext       (hsb: THSb): THSb;
FUNCTION  HsbPrev       (hsb: THSb): THSb;
FUNCTION  TyvhOfSb      (hsb: THSb): TTyvh;
PROCEDURE SetSbIcons    (hsb: THSb; siconNew: TSicon);
PROCEDURE GetSbIcons    (hsb: THSb; VAR sicon: TSicon);
PROCEDURE GetVisIcons   (hsb: THSb; VAR siconVis: TSicon);
FUNCTION  CThumbPos     (hsb: THSb): TC;
PROCEDURE SetThumb      (hsb: THSb; cNewThumb: TC);
PROCEDURE MoveSb        (hsb: THSb; ptNewTL: TPt);
PROCEDURE AdjSbCorner   (hsb: THSb; dcdAdjust: TCd; fTopLeft: TF);
PROCEDURE PaintSb       (hsb: THSb);
PROCEDURE EraseSb       (hsb: THSb);
PROCEDURE PaintArw      (hsb: THSb; iconWhichArw: TIcon; fBlack: TF);
FUNCTION  HsbFromPt     (VAR sbList: TSbList; pt: TPt): THSb;
FUNCTION  FSbHit        (VAR sbList: TSbList; pt: TPt; VAR hsbHit: THSb;
                         VAR iconHit: TIcon): TF;
PROCEDURE FlushRects    (VAR rH, rV: TR);
PROCEDURE MkPosts       (hsb: THSb; VAR posts: TPosts);

PROCEDURE SplitSb       (VAR sbList: TSbList; hsbOld: THSb; VAR hsbNew: THSb;
                         cdCut: TCd);
PROCEDURE AdjSplitBetween (hsbBefore, hsbAfter: THSb; dcdAdjust: TCd);
FUNCTION  HsbNextOnSbar (hsb: THSb): THSb;
FUNCTION  HsbPrevOnSbar (hsb: THSb): THSb;
FUNCTION  HsbSbarHead   (hsb: THSb): THSb;
FUNCTION  HsbSbarTail   (hsb: THSb): THSb;
FUNCTION  FSbarHead     (hsb: THSb): TF;
PROCEDURE SlideSbar     (hsb: THSb; dcdAdjust: TCd);
PROCEDURE PaintSbar     (hsb: THSb);
PROCEDURE EraseSbar     (hsb: THSb);
PROCEDURE SetupMvThumb  (hsb: THSb);
PROCEDURE MoveThumb     (cNewThumb: TC);
PROCEDURE DragThumb     (hsb: THSb; ptMouse: TPt; VAR cThumbUp: TC);
PROCEDURE FixRLimits    (hsb: THSb; VAR rLimits: TR);
PROCEDURE DragSkewer    (hsbDwn: THSb; ptMouse: TPt; rLimits: TR;
                         VAR ptSkewerUp: TPt);
PROCEDURE HideSBorder   (r: TR);

{$IFC WmlDebug }
PROCEDURE PxSbList     (VAR sbList: TSbList);
PROCEDURE PxSicon      (sicon: TSicon);
PROCEDURE PxSb         (hsb: THSb);
PROCEDURE PxSbar       (hsb: THSb);
{$ENDC }



IMPLEMENTATION

{$IFC WmlDebug }   {$R+} {$ELSEC } {$R-} {$ENDC }

{$IFC WmlSymbols } {$D+} {$ELSEC } {$D-} {$ENDC }

{ segmentation strategy: initialization in SegWmlInit, thumb updating stuff
      in SegWmlHot, rest in SegWml. }

CONST
    symDownArw  = 'F'; { arrow pointing down }
    symRightArw = 'J'; { ... right }
    symUpArw    = 'D'; { ... up }
    symLeftArw  = 'H'; { ... left }

    symDownPag  = '0'; { page pointing down }
    symRghtPag  = 'j'; { ... right }
    symUpPag    = '2'; { ... up }
    symLeftPag  = 'l'; { ... left }

    { the successor character of all the above is the filled-in "interior" symbol }


VAR
     { icons for arrows and page flip symbols come from a font now.
       rgSymArw maps [(icon=iconArwA), tyvh] to the char for the icon,
       rgSymPag similarly for page flip icons. }
    rgSymArw: ARRAY [TF, TTyvh] OF Char;
    rgSymPag: ARRAY [TF, TTyvh] OF Char;

     { global vars for thumb dragging }
    hsbMv: THSb; cdMinThm, cdMaxThm: TCd;
    rMvThumb: TR; tyvhMv: TTyvh; cdThumbMv: TCd;


{$IFC WmlDebug }

PROCEDURE PxR (sp: TSp; VAR r: TR);
BEGIN
{$IFC fTraceSB}    LogCall;     {$ENDC}
    WITH r DO WRITELN(sp, '[', left:5, top:5, '] [', right:5, bottom:5, ']');
END;


PROCEDURE PxSicon {(sicon: TSicon)};
BEGIN
{$IFC fTraceSB}    LogCall;     {$ENDC}
    WRITE('[');
    IF iconSkewer IN sicon THEN WRITE(' Skr');
    IF iconArwA   IN sicon THEN WRITE(' ArwA');
    IF iconPagA   IN sicon THEN WRITE(' PagA');
    IF iconThumb  IN sicon THEN WRITE(' Thmb');
    IF iconPagB   IN sicon THEN WRITE(' PagB');
    IF iconArwB   IN sicon THEN WRITE(' ArwB');
    WRITE(' ]');
END; { PxSicon }


PROCEDURE PxSb { (hsb: THSb) };
BEGIN
{$IFC fTraceSB}    LogCall;     {$ENDC}
    WITH hsb^^ DO BEGIN
        WRITE('Scroll box @ ', ORD(hsb));
        PxR(':  r: ', r);
        WRITELN('  refcon: ', refcon, ' Horiz? ', tyvh=tyvhH, ', fHead? ', fHead);
        WRITE('  Icons: '); PxSicon(sicon); WRITELN;
        WRITE('  Vis icons: '); PxSicon(siconVis); WRITELN;
        WRITELN('  cThumb: ', cThumb, ', hsbPrv: ', ORD(hsbPrv), ', hsbNxt: ',
                ORD(hsbNxt));
    END;
END; { PxSb }


PROCEDURE PxSbList { (VAR sbList: TSbList) };
VAR hsb: THSb;
BEGIN
{$IFC fTraceSB}    LogCall;     {$ENDC}
    WRITELN;
    WRITELN ('SbList @ ', ORD(@sbList));
    hsb := sbList.hsbFst;
    WHILE hsb <> hsbNil DO BEGIN
        PxSb(hsb); hsb := hsb^^.hsbNxt;
    END;
END;


PROCEDURE PxSbar { (hsb: THSb) };
BEGIN
{$IFC fTraceSB}    LogCall;     {$ENDC}
    hsb := HsbSbarHead(hsb);
    WRITE('[');
    WHILE hsb <> hsbNil DO BEGIN
        WRITE(ORD(hsb)); hsb := HsbNextOnSbar(hsb);
    END;
    WRITELN(']');
END;


PROCEDURE PxPosts (VAR posts: TPosts);
VAR icon: TIcon;
BEGIN
{$IFC fTraceSB}    LogCall;     {$ENDC}
    WRITELN('Posts [Skr ArwA PagA GryA Thm GryB PagB ArwB]: ');
    WRITE('  [');
    FOR icon:=iconSkewer TO iconArwB DO WRITE(posts[icon]:5);
    WRITELN(' ]');
END;

{$ENDC }

{$S SBopen2}
PROCEDURE InitSbList { (VAR sbList: TSbList; hz: THz) } ;
BEGIN
{$IFC fTraceSB}    LogCall;     {$ENDC}
    sbList.hz := hz; sbList.hsbFst := hsbNil;
END;


{$S SBopen2}
FUNCTION SbCreate { (VAR sbList: TSbList; hsbBefore: THSb; tyvhSb: TTyvh;
                   ptTopLeft: TPt; cdBotRt: TCd): THSb } ;
VAR hsbAfter, hsbAlloc: THSb;  tyvhOther: TTyvh;
BEGIN
{$IFC fTraceSB}    LogCall;     {$ENDC}
    hsbAlloc := POINTER(ORD(HAllocate(sbList.hz, SIZEOF(TSb))));

    WITH hsbAlloc^^ DO BEGIN
        tyvh := tyvhSb;
        tyvhOther := TyvhNOT[tyvh];

        r.topLeft := ptTopLeft;
        r.botRight.vh[tyvh] := cdBotRt;
        r.botRight.vh[tyvhOther] := r.topLeft.vh[tyvhOther] + dptSbox.vh[tyvhOther];

        sicon := [];  siconVis := [];
        cThumb := 0;
        fHead := TRUE;
        refcon := 0;
    END;

     { link it in }
    IF hsbBefore = hsbNil THEN BEGIN
         { link as new head }
        hsbAfter := sbList.hsbFst;
        sbList.hsbFst := hsbAlloc;
    END ELSE BEGIN
        hsbAfter := hsbBefore^^.hsbNxt;
        hsbBefore^^.hsbNxt := hsbAlloc;
    END;
    hsbAlloc^^.hsbNxt := hsbAfter;
    hsbAlloc^^.hsbPrv := hsbBefore;
    IF hsbAfter <> hsbNil THEN hsbAfter^^.hsbPrv := hsbAlloc;

    SbCreate := hsbAlloc;
END; { SbCreate }


{$S SBcold}
PROCEDURE KillSb { (VAR sbList: TSbList; hsbKill: THSb) } ;
VAR hsbFwd, hsbBak: THSb; hsbHead, hsbTail: THSb;
BEGIN
{$IFC fTraceSB}    LogCall;     {$ENDC}
    IF hsbKill = hsbNil THEN EXIT(KillSb);
{$IFC WmlDebug }
    IF fTstSb THEN BEGIN WRITE('Before Kill (', ORD(hsbKill), ') : sbar: ');
        hsbHead := HsbSbarHead(hsbKill);
        hsbTail := HsbSbarTail(hsbKill);
        PxSbar(hsbKill);
    END;
{$ENDC }

    hsbFwd := hsbKill^^.hsbNxt;
    hsbBak := hsbKill^^.hsbPrv;
    IF hsbBak = hsbNil THEN
         { => hsbKill = sbList.hsbFst }
        sbList.hsbFst := hsbFwd
    ELSE hsbBak^^.hsbNxt := hsbFwd;
    IF hsbFwd <> hsbNil THEN hsbFwd^^.hsbPrv := hsbBak;
    IF hsbKill^^.fHead THEN IF hsbFwd <> hsbNil THEN hsbFwd^^.fHead := TRUE;

    FreeBk(sbList.hz, POINTER(ORD(hsbKill)), tybkStd);
{$IFC WmlDebug }
    IF fTstSb THEN BEGIN
        WRITE('After Kill, sbar: ');
        IF hsbHead <> hsbKill THEN PxSbar(hsbHead)
        ELSE IF hsbTail <> hsbKill THEN PxSbar(hsbTail);
    END;
{$ENDC }
END; { KillSb }


{$S SBscrol}
PROCEDURE GetSbRect { (hsb: THSb; VAR r: TR) } ;
BEGIN
{$IFC fTraceSB}    LogCall;     {$ENDC}
    r := hsb^^.r;
END;


{$S SBscrol}
PROCEDURE SetSbRect { (hsb: THSb; r: TR) } ;
BEGIN
{$IFC fTraceSB}    LogCall;     {$ENDC}
    hsb^^.r := r;
END;


{$S SBcold}
FUNCTION RefconSb { (hsb: THSb): TL } ;
BEGIN
{$IFC fTraceSB}    LogCall;     {$ENDC}
    RefconSb := hsb^^.refcon;
END;


{$S SBcold}
PROCEDURE SetSbRefcon { (hsb: THSb; refcon: TL) } ;
BEGIN
{$IFC fTraceSB}    LogCall;     {$ENDC}
    hsb^^.refcon := refcon;
END;


{$S SBothrwk}
FUNCTION HsbFirst { (VAR sbList: TSbList): THSb } ;
BEGIN
{$IFC fTraceSB}    LogCall;     {$ENDC}
    HsbFirst := sbList.hsbFst;
END;


{$S SBothrwk}
FUNCTION HsbNext { (hsb: THSb): THSb } ;
BEGIN
{$IFC fTraceSB}    LogCall;     {$ENDC}
    HsbNext := hsb^^.hsbNxt;
END;


{$S SBcold}
FUNCTION HsbPrev { (hsb: THSb): THSb } ;
BEGIN
{$IFC fTraceSB}    LogCall;     {$ENDC}
    HsbPrev := hsb^^.hsbPrv;
END;


{$S SBothrwk}
FUNCTION TyvhOfSb { (hsb: THSb): TTyvh } ;
BEGIN
{$IFC fTraceSB}    LogCall;     {$ENDC}
    TyvhOfSb := hsb^^.tyvh;
END;


{$S SBothrwk}
FUNCTION CThumbPos { (hsb: THSb): TC } ;
BEGIN
{$IFC fTraceSB}    LogCall;     {$ENDC}
    CThumbPos := hsb^^.cThumb;
END;



{$S SBscrol}
FUNCTION CClip (cLow, c, cHi: TC): TC;
BEGIN
{$IFC fTraceSB}    LogCall;     {$ENDC}
    IF      c < cLow THEN CClip := cLow
    ELSE IF c > cHi  THEN CClip := cHi
    ELSE                  CClip := c;
END;


{$S SBscrol}
PROCEDURE SetThumb { (hsb: THSb; cNewThumb: TC) } ;
BEGIN
{$IFC fTraceSB}    LogCall;     {$ENDC}
    hsb^^.cThumb := CClip(0, cNewThumb, 1000);
END;



{$S SBact}
PROCEDURE SetSbIcons { (hsb: THSb; siconNew: TSicon) } ;
BEGIN
{$IFC fTraceSB}    LogCall;     {$ENDC}
    hsb^^.sicon := siconNew;
END;


{$S SBcold}
PROCEDURE GetSbIcons { (hsb: THSb; VAR sicon: TSicon) } ;
BEGIN
{$IFC fTraceSB}    LogCall;     {$ENDC}
    sicon := hsb^^.sicon;
END;


{$S SBcold}
PROCEDURE GetVisIcons { (hsb: THSb; VAR siconVis: TSicon) } ;
BEGIN
{$IFC fTraceSB}    LogCall;     {$ENDC}
    siconVis := hsb^^.siconVis;
END;


{$S SBmovrs}
PROCEDURE MoveSb { (hsb: THSb; ptNewTL: TPt) } ;
VAR dpt: TPt;
BEGIN
{$IFC fTraceSB}    LogCall;     {$ENDC}
    WITH hsb^^ DO BEGIN
        dpt := ptNewTL;
        SubPt(r.topLeft, dpt);
        r.topLeft := ptNewTL;
        AddPt(dpt, r.botRight);
    END;
END;


{$S SBalert}
PROCEDURE AdjSbCorner { (hsb: THSb; dcdAdjust: TCd; fTopLeft: TF) } ;
VAR pcd: ^TCd;
BEGIN
{$IFC fTraceSB}    LogCall;     {$ENDC}
    WITH hsb^^ DO BEGIN
        IF fTopLeft THEN
            pcd := @r.topLeft.vh[tyvh]
        ELSE
            pcd := @r.botRight.vh[tyvh];
        pcd^ := pcd^ + dcdAdjust;
    END;
END;


{$S SBscrol}
FUNCTION CThumbFromCd (cdThumb: TCd; cdStart, cdEnd: TCd): TC;
 { Determine thumb 1000th in interval cdStart..cdEnd based on cdThumb, which
   is the coordinate of the beginning of the thumb. }
VAR lT: LONGINT;
BEGIN
{$IFC fTraceSB}    LogCall;     {$ENDC}
    lT := cdThumb - cdStart;
    CThumbFromCd := (1000 * lT) DIV (cdEnd - cdStart);
END;



{$S SBscrol}
FUNCTION CdThumbFromC (cThumb: TC; cdStart, cdEnd: TCd): TCd;
 { Determine thumb coordinate in interval cdStart..cdEnd based on
   cThumb, which is in thousandths (0..1000).  Width (height) of thumb
   is already figured in cdEnd }
VAR lT: LONGINT; cT: TC;
BEGIN
{$IFC fTraceSB}    LogCall;     {$ENDC}
    lT := cThumb;  lT := ((cdEnd-cdStart) * lT) DIV 1000; cT := lT;
    CdThumbFromC := cdStart + cT;
END;


{$S SBscrol}
PROCEDURE RThumb (hsb: THSb; cdBegThm: TCd; VAR rT: TR);
 { sets rect for hsb's thumb box, which starts at cdBegThm along hsb's axis;
   if thumb is not visible, then rT has 0 area. }
BEGIN
{$IFC fTraceSB}    LogCall;     {$ENDC}
    WITH hsb^^ DO BEGIN
        rT := r; InsetRect(rT, 1, 1);
        rT.topLeft.vh[tyvh]  := cdBegThm;
        IF iconThumb IN siconVis THEN
            rT.botRight.vh[tyvh] := cdBegThm + dptThumb.vh[tyvh]
        ELSE
            rT.botRight.vh[tyvh] := cdBegThm;
    END;
END; { RThumb }



{$S SBscrol}
PROCEDURE FixVisIcons (hsb: THSb);
  { Determine the new visible icons based on hsb^^.sicon }
VAR dcdWH, dcdSbox: TCd; rgdcd: ARRAY[0..3] OF TCd; icon: TIcon; i: TC;
BEGIN
{$IFC fTraceSB}    LogCall;     {$ENDC}
 { First skewer, then both arrows, then thumb, finally both page flips }
    WITH hsb^^ DO BEGIN
        dcdSbox := dptSbox.vh[tyvh];
        FOR i := 0 TO 3 DO rgdcd[i] := 0;
        siconVis := sicon;              { assume all ok }
        FOR icon := iconSkewer TO iconArwB DO IF icon IN siconVis THEN
            CASE icon OF
              iconPagA,
              iconPagB:   rgdcd[0] := rgdcd[0] + dcdSbox;
              iconThumb:  rgdcd[1] := rgdcd[1] + dptThumb.vh[tyvh];
              iconArwA,
              iconArwB:   rgdcd[2] := rgdcd[2] + dcdSbox;
              iconSkewer: rgdcd[3] := rgdcd[3] + dptSkewer.vh[tyvh];
            END;
        dcdWH := r.botRight.vh[tyvh]-r.topLeft.vh[tyvh] -
                    (rgdcd[0]+rgdcd[1]+rgdcd[2]+rgdcd[3]);

        i := 0;
        WHILE (dcdWH < 0) AND (i <= 3) DO BEGIN
            dcdWH := dcdWH + rgdcd[i];
            CASE i OF
              0:  siconVis := siconVis - [iconPagA, iconPagB];
              1:  siconVis := siconVis - [iconThumb];
              2:  siconVis := siconVis - [iconArwA, iconArwB];
              3:  siconVis := siconVis - [iconSkewer];
            END;
            i := i+1;
        END;
    END;
END; { FixVisIcons }


{$S SBscrol}
PROCEDURE DrawArrow(symArrow: Char; ptTL: TPt; fBlack: TF);
VAR rT: TR;
BEGIN
{$IFC fTraceSB}    LogCall;     {$ENDC}
    SetRect(rT, ptTL.h, ptTL.v, ptTL.h + dhSbox, ptTL.v + dvSbox);
    EraseRect(rT);

    MoveTo (rT.Left, rT.Top);
    DrawChar (symArrow);
    IF fBlack THEN BEGIN
        MoveTo (rT.Left, rT.Top);
        DrawChar (Succ (symArrow));
    END;
END;



{$S SBscrol}
PROCEDURE MkPosts { (hsb: THSb; VAR posts: TPosts) } ;
VAR cdLeft, cdRight, dcdSbox, dcdThumb: TCd;
BEGIN
{$IFC fTraceSB}    LogCall;     {$ENDC}
    WITH hsb^^ DO BEGIN
        dcdSbox := dptSbox.vh[tyvh];

        cdLeft := r.topLeft.vh[tyvh];
        IF iconSkewer IN siconVis THEN cdLeft := cdLeft + dptSkewer.vh[tyvh];
        posts[iconSkewer] := cdLeft;
        IF iconArwA IN siconVis THEN cdLeft := cdLeft + dcdSbox;
        posts[iconArwA] := cdLeft;
        IF iconPagA IN siconVis THEN cdLeft := cdLeft + dcdSbox;
        posts[iconPagA] := cdLeft;

        cdRight := r.botRight.vh[tyvh];
        posts[iconArwB] := cdRight;
        IF iconArwB IN siconVis THEN cdRight := cdRight - dcdSbox;
        posts[iconPagB] := cdRight;
        IF iconPagB IN siconVis THEN cdRight := cdRight - dcdSbox;

        posts[iconGryB] := cdRight;
        IF iconThumb IN siconVis THEN BEGIN
            dcdThumb := dptThumb.vh[tyvh];
            cdLeft := CdThumbFromC(cThumb, cdLeft, cdRight - dcdThumb);
            cdRight := cdLeft + dcdThumb;
        END ELSE
            cdRight := cdLeft;
        posts[iconThumb] := cdRight;
        posts[iconGryA] := cdLeft;
    END;
END; { MkPosts }



{$S SBscrol}
PROCEDURE PaintSb { (hsb: THSb) };
VAR rT: TR; ptTL: TPt; tyvhOther: TTyvh; posts: TPosts; icon: TIcon;
BEGIN
{$IFC fTraceSB}    LogCall;     {$ENDC}
PenSave; PenNormal;
WmlTxtSave;
WITH hsb^^ DO BEGIN
    FillRect(r, ltgray); FrameRect(r);
    FixVisIcons(hsb);

    tyvhOther := TyvhNOT[tyvh];
     { set front edge in ortho direction (e.g. top edge for horiz sbox) }
    ptTL.vh[tyvhOther] := r.topLeft.vh[tyvhOther];

    MkPosts(hsb, posts);
    FOR icon := iconSkewer TO iconArwB DO IF icon IN siconVis THEN
        CASE icon OF
            iconSkewer:
                BEGIN
                rT := r;  rT.botRight.vh[tyvh] := posts[iconSkewer];
                FillRect(rT, black);
                END;
            iconArwA, iconArwB:
                BEGIN
                ptTL.vh[tyvh] := posts[PRED(icon)];
                DrawArrow(rgSymArw[icon=iconArwA, tyvh], ptTL, FALSE { not hilited });
                END;
            iconPagA, iconPagB:
                BEGIN
                ptTL.vh[tyvh] := posts[PRED(icon)];
                DrawArrow(rgSymPag[icon=iconPagA, tyvh], ptTL, FALSE { not hilited });
                END;
            iconThumb:
                BEGIN
                RThumb(hsb, posts[iconGryA], rT);  EraseRect(rT); FrameRect(rT);
                END;
            END;
    END; { with }
WmlTxtRestore;
PenRestore;
END; { PaintSb }


{$S SBscrol}
PROCEDURE PaintArw  { (hsb: THSb; iconWhichArw: TIcon; fBlack: TF) } ;
VAR ptTL: TPt; posts: TPosts;
BEGIN
{$IFC fTraceSB}    LogCall;     {$ENDC}
    PenSave; PenNormal;
    WmlTxtSave;
    IF (iconWhichArw IN [iconArwA, iconArwB, iconPagA, iconPagB]) AND
       (iconWhichArw IN hsb^^.siconVis) THEN BEGIN
        ptTL := hsb^^.r.topLeft;
        MkPosts(hsb, posts);
        ptTL.vh[hsb^^.tyvh] := posts[PRED(iconWhichArw)];
        IF iconWhichArw in [iconArwA, iconArwB] THEN
            DrawArrow(rgSymArw[iconWhichArw=iconArwA, hsb^^.tyvh], ptTL, fBlack)
        ELSE
            DrawArrow(rgSymPag[iconWhichArw=iconPagA, hsb^^.tyvh], ptTL, fBlack);
    END;
    WmlTxtRestore;
    PenRestore;
END; { PaintArw }


{$S SBcold}
PROCEDURE SplitSb { (VAR sbList: TSbList; hsbOld: THSb; VAR hsbNew: THSb;
                     cdCut: TCd) } ;
VAR tyvh: TTyvh; hsbFwd: THSb;
BEGIN
{$IFC fTraceSB}    LogCall;     {$ENDC}
    IF hsbOld = hsbNil THEN EXIT(SplitSb);
    {$IFC WmlDebug }
    IF fTstSb THEN BEGIN WRITE('Before Split, sbar: '); PxSbar(hsbOld); END;
    {$ENDC }

    hsbNew := POINTER(ORD(HAllocate(sbList.hz, SIZEOF(TSb))));
    hsbNew^^ := hsbOld^^;
    tyvh := hsbNew^^.tyvh;
    hsbNew^^.r.topLeft.vh[tyvh] := cdCut;
    hsbNew^^.siconVis := [];
    hsbNew^^.hsbPrv := hsbOld;
    hsbNew^^.fHead := FALSE;
    hsbNew^^.refcon := 0;

    hsbOld^^.r.botRight.vh[tyvh] := cdCut;
    hsbOld^^.hsbNxt := hsbNew;

    hsbFwd := hsbNew^^.hsbNxt;
    IF hsbFwd <> hsbNil THEN hsbFwd^^.hsbPrv := hsbNew;
    {$IFC WmlDebug }
    IF fTstSb THEN BEGIN WRITE('After Split:'); PxSbar(hsbOld); END;
    {$ENDC }
END; { SplitSb }


{$S SBcold}
PROCEDURE AdjSplitBetween { (hsbBefore, hsbAfter: THSb; dcdAdjust: TCd) } ;
BEGIN
{$IFC fTraceSB}    LogCall;     {$ENDC}
    AdjSbCorner(hsbBefore, dcdAdjust, FALSE { botRight });
    AdjSbCorner(hsbAfter,  dcdAdjust, TRUE  { topLeft  });
END; { AdjSplitBetween }


{$S SBalert}
FUNCTION HsbNextOnSbar { (hsb: THSb): THSb } ;
VAR hsbFwd: THSb;
BEGIN
{$IFC fTraceSB}    LogCall;     {$ENDC}
    hsbFwd := hsb^^.hsbNxt;
    IF hsbFwd <> hsbNil THEN IF hsbFwd^^.fHead THEN hsbFwd := hsbNil;
    HsbNextOnSbar := hsbFwd;
END;


{$S SBcold}
FUNCTION HsbPrevOnSbar { (hsb: THSb): THSb } ;
VAR hsbBak: THSb;
BEGIN
{$IFC fTraceSB}    LogCall;     {$ENDC}
    IF hsb^^.fHead THEN hsbBak := hsbNil ELSE hsbBak := hsb^^.hsbPrv;
    HsbPrevOnSbar := hsbBak;
END;


{$S SBalert}
FUNCTION HsbSbarHead { (hsb: THSb): THSb } ;
LABEL 11;
BEGIN
{$IFC fTraceSB}    LogCall;     {$ENDC}
    WHILE hsb <> hsbNil DO BEGIN
        IF hsb^^.fHead THEN GOTO 11;
        hsb := hsb^^.hsbPrv;
    END;
11: HsbSbarHead := hsb;
END;


{$S SBcold}
FUNCTION HsbSbarTail { (hsb: THSb): THSb } ;
VAR hsbPrv: THSb;
BEGIN
{$IFC fTraceSB}    LogCall;     {$ENDC}
    hsbPrv := hsb;
    WHILE hsb <> hsbNil DO BEGIN
        hsbPrv := hsb; hsb := HsbNextOnSbar(hsb);
    END;
    HsbSbarTail := hsbPrv;
END;


{$S SBmovrs}
FUNCTION FSbarHead { (hsb: THSb): TF } ;
BEGIN
{$IFC fTraceSB}    LogCall;     {$ENDC}
    FSbarHead := hsb^^.fHead;
END;


{$S SBcold}
PROCEDURE SlideSbar { (hsb: THSb; dcdAdjust: TCd) } ;
VAR ptT: TPt; tyvhOther: TTyvh;
BEGIN
{$IFC fTraceSB}    LogCall;     {$ENDC}
    hsb := HsbSbarHead(hsb);
    tyvhOther := TyvhNOT[hsb^^.tyvh];
    WHILE (hsb <> hsbNil) DO BEGIN
        ptT := hsb^^.r.topLeft;
        ptT.vh[tyvhOther] := ptT.vh[tyvhOther] + dcdAdjust;
        MoveSb(hsb, ptT);
        hsb := HsbNextOnSbar(hsb);
    END;
END;


{$S SBact}
PROCEDURE PaintSbar { (hsb: THSb) } ;
BEGIN
{$IFC fTraceSB}    LogCall;     {$ENDC}
    hsb := HsbSbarHead(hsb);
    WHILE (hsb <> hsbNil) DO BEGIN
        PaintSb(hsb); hsb := HsbNextOnSbar(hsb);
    END;
END;


{$S SBcold}
PROCEDURE EraseSb { (hsb: THSb) } ;
BEGIN
{$IFC fTraceSB}    LogCall;     {$ENDC}
    EraseRect(hsb^^.r);
END;


{$S SBcold}
PROCEDURE EraseSbar { (hsb: THSb) } ;
BEGIN
{$IFC fTraceSB}    LogCall;     {$ENDC}
    hsb := HsbSbarHead(hsb);
    WHILE (hsb <> hsbNil) DO BEGIN
        EraseSb(hsb); hsb := HsbNextOnSbar(hsb);
    END;
END;


{$S SBscrol}
PROCEDURE FlushRects { (VAR rH, rV: TR) } ;
BEGIN
{$IFC fTraceSB}    LogCall;     {$ENDC}
    WITH thePort^.portRect DO BEGIN
        SetRect(rH, left-1, bottom+1-dvSbox, right+2-dhSbox, bottom+1);
        SetRect(rV, right+1-dhSbox, top-1, right+1, bottom+2-dvSbox);
    END;
END;


{$S SBothrwk}
FUNCTION HsbFromPt { (VAR sbList: TSbList; pt: TPt): THSb } ;
LABEL 11;
VAR hsbHit: THSb;
BEGIN
{$IFC fTraceSB}    LogCall;     {$ENDC}
    hsbHit := sbList.hsbFst;
    WHILE hsbHit <> hsbNil DO BEGIN
         IF PtInRect(pt, hsbHit^^.r) THEN GOTO 11;
         hsbHit := hsbHit^^.hsbNxt;
    END;
11: HsbFromPt := hsbHit;
END;



{$S SBothrwk}
FUNCTION FSbHit { (VAR sbList: THSb; pt: TPt; VAR hsbHit: THSb;
                   VAR iconHit: TIcon): TF } ;
VAR cd: TCd; posts: TPosts;
BEGIN
{$IFC fTraceSB}    LogCall;     {$ENDC}
    hsbHit := HsbFromPt(sbList, pt);
    FSbHit := hsbHit <> hsbNil;
    IF hsbHit = hsbNil THEN EXIT(FSbHit);
     { we're in a scroll box. check posts }
    MkPosts(hsbHit, posts);
    cd := pt.vh[hsbHit^^.tyvh];
    iconHit := iconSkewer;
    WHILE cd >=  posts[iconHit] DO iconHit := SUCC(iconHit);
END;


{$S SBscrol}
PROCEDURE SetupMvThumb { (hsb: THSb) } ;
VAR posts: TPosts;
BEGIN
{$IFC fTraceSB}    LogCall;     {$ENDC}
    PenSave; PenNormal;
    hsbMv := hsb;
    tyvhMv := hsb^^.tyvh;
    MkPosts(hsb, posts);
    cdMinThm  := posts[PRED(iconGryA)];
    IF iconThumb IN hsb^^.siconVis
        THEN cdMaxThm := posts[iconGryB] - dptThumb.vh[tyvhMv]
        ELSE cdMaxThm := cdMinThm;
    cdThumbMv := posts[iconGryA];
    RThumb(hsb, cdThumbMv, rMvThumb);
END;


{$S SBscrol}
PROCEDURE MoveThumb { (cNewThumb: TC) } ;
VAR rT: TR; cdNew: TCd;
BEGIN
{$IFC fTraceSB}    LogCall;     {$ENDC}
    SetThumb(hsbMv, cNewThumb);
    rT := rMvThumb;
    cdNew := CdThumbFromC(cNewThumb, cdMinThm, cdMaxThm);
    RThumb(hsbMv, cdNew, rMvThumb);
    IF rT.topLeft.vh[tyvhMv] <> rMvThumb.topLeft.vh[tyvhMv] THEN BEGIN
        FillRect(rT, ltgray);                     { erase old thumb }
        EraseRect(rMvThumb); FrameRect(rMvThumb); { paint new thumb }
    END;
END;



{$S SBscrol}
PROCEDURE DragThumb { (hsb: THSb; ptMouse: TPt; VAR cThumbUp: TC) } ;
VAR cdThumbOld: TCd; rThumbNew: TR; event: EventRecord;
    cdMouse, cdMinMouse, cdMaxMouse: TCd;

{$S SBscrol}
    PROCEDURE DoDragThumb;
    VAR cdMouseOld: TCd;
    BEGIN
{$IFC fTraceSB}    LogCall;     {$ENDC}
        cdMouseOld := cdMouse;
        cdMouse := CClip(cdMinMouse, ptMouse.vh[tyvhMv], cdMaxMouse);
        IF cdMouse <> cdMouseOld THEN BEGIN
            FrameRect(rThumbNew);     { hide old ghost }
            cdThumbMv := cdThumbMv + (cdMouse - cdMouseOld);
            RThumb(hsbMv, cdThumbMv, rThumbNew);
            FrameRect(rThumbNew);     { draw new ghost }
        END;
    END;
{$S SBscrol}
BEGIN
{$IFC fTraceSB}    LogCall;     {$ENDC}
    cThumbUp := hsb^^.cThumb;   { initialize at original position }
    SetupMvThumb(hsb);
    cdThumbOld := cdThumbMv;
    rThumbNew := rMvThumb;
    cdMouse := ptMouse.vh[tyvhMv];
    cdMinMouse := cdMouse - cdThumbOld + cdMinThm;
    cdMaxMouse := cdMouse - cdThumbOld + cdMaxThm;

    PenPat(ltGray); PenMode(notPatXor);
    FrameRect(rThumbNew);           { ghost initial }
    WHILE StillDown DO BEGIN
        GetMouse(ptMouse);
        DoDragThumb;
    END;
    IF PeekEvent(event) AND (event.what = buttonUp) THEN BEGIN
        ptMouse := event.where;
        DoDragThumb;
    END;
    FrameRect(rThumbNew);           { unghost final }
    IF cdThumbMv <> cdThumbOld THEN BEGIN
         cThumbUp := CThumbFromCd(cdThumbMv, cdMinThm, cdMaxThm);
         PenNormal;
         MoveThumb(cThumbUp);
    END;
    PenRestore;
END; { DragThumb }


{$S SBcold}
PROCEDURE FixRLimits { (hsb: THSb; VAR rLimits: TR) } ;
VAR tyvh: TTyvh; hsbBeg, hsbEnd: THSb;
BEGIN
{$IFC fTraceSB}    LogCall;     {$ENDC}
    tyvh := hsb^^.tyvh;

    rLimits.topLeft.vh[TyvhNOT[tyvh]] := 0;  { ! }
    IF FSbarHead(hsb) THEN BEGIN
        hsbBeg := hsb;  hsbEnd := HsbSbarTail(hsb);
    END ELSE BEGIN
        hsbBeg := hsb^^.hsbPrv;  hsbEnd := hsb;
    END;
    rLimits.topLeft.vh[tyvh] := hsbBeg^^.r.topLeft.vh[tyvh];
    rLimits.botRight.vh[tyvh] := hsbEnd^^.r.botRight.vh[tyvh];
END;  { FixRLimits }


{$S SBcold}
PROCEDURE DragSkewer { (hsbDwn: THSb; ptMouse: TPt; rLimits: TR;
                        VAR ptSkewerUp: TPt) } ;
VAR tyvhSb, tyvhOther: TTyvh; event: EventRecord;
    cdSkewer, cdLineFrom, cdLineTo: TCd;
    cdMouse, cdMinMouse, cdMaxMouse: TCd;

{$S SBcold}
    PROCEDURE XorSkewer;
    BEGIN
{$IFC fTraceSB}    LogCall;     {$ENDC}
        IF tyvhSb = tyvhH THEN BEGIN
            MoveTo(cdSkewer, cdLineFrom);
            LineTo(cdSkewer, cdLineTo);
        END ELSE BEGIN
            MoveTo(cdLineFrom, cdSkewer);
            LineTo(cdLineTo,   cdSkewer);
        END;
    END;

{$S SBcold}
    PROCEDURE DoDragSkewer;
    VAR cdMouseOld: TCd;
    BEGIN
{$IFC fTraceSB}    LogCall;     {$ENDC}
        cdMouseOld := cdMouse;
        cdMouse := CClip(cdMinMouse, ptMouse.vh[tyvhSb], cdMaxMouse);
        IF cdMouse <> cdMouseOld THEN BEGIN
            XorSkewer;                          { hide old }
            cdSkewer := cdSkewer + (cdMouse - cdMouseOld);
            XorSkewer;                          { draw new }
        END;
    END;
{$S SBcold}
BEGIN
{$IFC fTraceSB}    LogCall;     {$ENDC}
    PenSave; PenPat(gray); PenMode(14); PenSize(1,1);
    WITH hsbDwn^^ DO BEGIN
        tyvhSb := tyvh;  tyvhOther := TyvhNOT[tyvhSb];
        cdSkewer := r.topLeft.vh[tyvh];
        cdLineFrom := r.botRight.vh[tyvhOther];
        ptSkewerUp.vh[tyvhOther] := r.topLeft.vh[tyvhOther];
    END;
    WITH rLimits DO BEGIN
        cdLineTo := topLeft.vh[tyvhOther]-1;
        cdMinMouse := topLeft.vh[tyvhSb];
        cdMaxMouse := botRight.vh[tyvhSb];
    END;
    cdMouse := ptMouse.vh[tyvhSb];
    cdMinMouse := cdMouse - cdSkewer + cdMinMouse;
    cdMaxMouse := cdMouse - cdSkewer + cdMaxMouse;
    XorSkewer;                              { hide initial }
    WHILE StillDown DO BEGIN
        GetMouse(ptMouse);
        DoDragSkewer;
    END;
    IF PeekEvent(event) AND (event.what = buttonUp) THEN BEGIN
        ptMouse := event.where;
        DoDragSkewer;
    END;
    XorSkewer;
    ptSkewerUp.vh[tyvhSb] := cdSkewer;
    PenRestore;
END; { DragSkewer }


{$S SBact}
PROCEDURE HideSBorder {(r: TR) };
 { r should be thePort's portRect or something like it }
VAR rT: TR;
BEGIN
{$IFC fTraceSB}    LogCall;     {$ENDC}
    rT := r;
    r.left := r.right-dhSbox+2;
    FillRect(r, white);
    SetPt(r.topLeft, rT.left, r.bottom-dvSbox+2);
    FillRect(r, white);
    SetRect(rT, rT.left-1, rT.top-1, rT.right-dhSbox+2, r.top);
    PenSave; PenNormal;
    FrameRect(rT);
    PenRestore;
END;



{$S SBopen1}
PROCEDURE InitWmlSb;
BEGIN
{$IFC fTraceSB}    LogCall;     {$ENDC}
    fTstSb := FALSE;
    hsbNil := NIL;
    dptSbox.h   := dhSbox;          dptSbox.v   := dvSbox;
    dptThumb.h  := dhThumb;         dptThumb.v  := dvThumb;
    dptSkewer.h := dhSkewer;        dptSkewer.v := dvSkewer;

    TyvhNOT[tyvhH] := tyvhV; TyvhNOT[tyvhV] := tyvhH;

    rgSymArw[TRUE,  tyvhV] := symUpArw;       { TRUE  => iconArwA }
    rgSymArw[TRUE,  tyvhH] := symLeftArw;
    rgSymArw[FALSE, tyvhV] := symDownArw;     { FALSE => iconArwB }
    rgSymArw[FALSE, tyvhH] := symRightArw;

    rgSymPag[TRUE,  tyvhV] := symUpPag;       { TRUE  => iconPagA }
    rgSymPag[TRUE,  tyvhH] := symLeftPag;
    rgSymPag[FALSE, tyvhV] := symDownPag;     { FALSE => iconPagB }
    rgSymPag[FALSE, tyvhH] := symRghtPag;

END;

END.

ÿ