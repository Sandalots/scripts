{FRLisa:=.i
YQ
Cl:fmgrutil

lisa:fmgrutil.i[*]

Glisa:fmgrutil
obj:FmgrUtil
{{{}

{<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>}
{$S FmgrUtil}
UNIT FmgrUtil;
{$SETC fmOS := TRUE}

{ CopyRight 1983, Apple Computer, Inc. }

INTRINSIC;


INTERFACE

USES
   {$U libsm/UnitStd} UnitStd,
   {$U libsm/UnitHz} UnitHz,
   {$U libos/SysCall} SysCall,
   {$U libqd/QuickDraw} QuickDraw,
   {$U libfm/FontMgr} FontMgr,
   {$U libqd/Storage} Storage;
{$SETC fmdebug := fdbgok}
{$SETC fmSymbols := fsymok}

{<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>}

FUNCTION GetIcon
   (    fam: Tfam;
        cc: TCc;
    VAR rSrc: Rect;
    VAR bmSrc: BitMap;
    VAR cError: TC)
   : TF;

PROCEDURE DrawIcon
   (    fam: Tfam;
        rDst: Rect;
        cc: TCc;
        cmode: TC;
    VAR cError: TC);

PROCEDURE GetPattern
   (    fam: Tfam;
        ccPat: TCc;
    VAR pat: Pattern;
    VAR cError: TC);

PROCEDURE GetCursor
   (    fam: Tfam;
        ccData,
        ccMask: TCc;
    VAR curse: Cursor;
    VAR cError: TC);


{<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>}

IMPLEMENTATION

{$IFC fmSymbols}
   {$D+}
{$ELSEC}
   {$D-}
{$ENDC}
{$IFC fmDebug}
   {$R+}
{$ELSEC}
   {$R-}
{$ENDC}

TYPE
   TLb = 0..255;

   TFormat = PACKED RECORD
      CASE BOOLEAN OF
         TRUE: (cformat: TC);
         FALSE: (version: TLb;
                 fontKind: TLb);
   END;

   TKshdr = RECORD
      format: TC;      {format of KS font i.e. fixed, variable, or special     }
      chFst: TC;       {char code of first char in KS font                     }
      chLst: TC;       {char code of last char in KS font                      }
      widMax: TC;      {maximum width (dist.) between origins of adjacent char }
      fBBox: TC;       {negative offset from font bounding box in x direc.     }
      fBBoy: TC;       {negative offsets from font bounding box in y direc.    }
      fBBdx: TC;       {extent of the font bounding box in the x direc.        }
      fBBdy: TC;       {extent of the font bounding box in the y direc.        }
      length: TC;      {number of integer words in the strike body             }
      ascent: TC;      {acent of char's in font                                }
      descent: TC;     {descent of char's in font                              }
      leading: TC;     {the amount of white space between lines of text.       }
      raster: TC;      {number of integer words / scanline in the ks bitmap    }
   END; {record TKshdr}
   TPkshdr = ^TKshdr;
   THkshdr = ^TPKshdr;

   TRgw = ARRAY [TCc] OF TC;
   TPrgw = ^TRgw;

{===============================================================================

#######> SplitFont

        PURPOSE: To split a handle to a font into three pointers to the main
                 portions of the font (header, loc table, and offset/width
                 table).

        GLOBALS MODIFIED: None.

===============================================================================}

FUNCTION SplitFont
   (    family: Tfam;
    VAR pkshdr: TPkshdr;
    VAR prgwloc,
        prgwoff: TPrgw;
    VAR cError: TC)
   : TF;

CONST
   hdrSize = 12;

VAR
   lfntid: TLfntid;
   pchsT: TPcharstyle;
   bytesToLoc,
   bytesToOff: TC;

BEGIN {SplitFont}
{$IFC fTraceFM} LogCall; {$ENDC}
   IF fdebug THEN BEGIN
      WRITELN ('SplitFont');
   END; {if}
   SplitFont := TRUE; {assume the best}
   pkshdr := NIL;
   prgwOff := NIL;
   prgwLoc := NIL;
   WITH lfntid DO BEGIN
      fam := family;
      seteface := [];
      dev := devScreen;
   END; {with}
   pchsT := FMSwapFont (lfntid, cError);
   IF pchsT <> NIL THEN BEGIN
      WITH pchsT^ DO BEGIN
         IF hks <> NIL THEN BEGIN
            pkshdr := POINTER (ORD (hks^));
            WITH pkshdr^ DO BEGIN
               bytesToLoc := ((raster * fbbdy) + hdrSize + 1) * 2;
               bytesToOff := (length + 8) * 2;
               prgwloc := POINTER (ORD (pkshdr) + bytesToLoc);
               prgwoff := POINTER (ORD (pkshdr) + bytesToOff);
            END; {with}
         END; {if}
      END; {with}
      IF fdebug THEN BEGIN
         WRITELN ('  pkshdr:  ', ORD (pkshdr));
         WRITELN ('  prgwloc: ', ORD (prgwloc));
         WRITELN ('  prgwoff: ', ORD (prgwoff));
      END; {if}
   END {if}
   ELSE BEGIN
      SplitFont := FALSE;
   END; {else}
END; {SplitFont}

{===============================================================================

#######> GetIcon

        PURPOSE: To map a font-handle/cc pair to a bitmap and rectangle.

        GLOBALS MODIFIED: None.

===============================================================================}

FUNCTION GetIcon
   {(    fam: Tfam;
        cc: TCc;
    VAR rSrc: Rect;
    VAR bmSrc: BitMap;
    VAR cError: TC)
   : TF};

CONST
   bytesToMap = 26;             {The number of bytes from the start of the font}
                                {The header is 13 words long.                  }
VAR
   pkshdr: TPkshdr;
   prgwloc,
   prgwoff: TPrgw;
   i: TCc;

BEGIN {GetIcon}
{$IFC fTraceFM} LogCall; {$ENDC}
   IF fdebug THEN BEGIN
      WRITELN ('GetIcon');
      WRITELN ('  cc: ', cc:2);
   END; {if}
   {First get the index of the start of the location table and offset width  }
   {tables.                                                                  }
   IF SplitFont (fam, pkshdr, prgwloc, prgwoff, cError) THEN BEGIN
      GetIcon := TRUE;
      {Find out if the there is an icon associated with the given cc}
      {if not, give the "missing" icon                              }
      IF (cc < pkshdr^.chFst)
         OR
         (cc > pkshdr^.chLst) THEN BEGIN
         cc := pkshdr^.chLst + 1;
      END {if}

      ELSE IF (prgwoff^ [cc - pkshdr^.chFst] = -1) THEN BEGIN
         cc := pkshdr^.chLst + 1;
      END; {else if}

      {Set up the dest bitmap. (entire font strike)}
      WITH bmSrc, pkshdr^ DO BEGIN
         baseaddr := POINTER (ORD (pkshdr) + bytesToMap);
         rowbytes := raster * 2;
         SetRect (bounds, 0, 0, raster * 16, fbbdy);
      END; {with}

      {Set up the destination rectangle.}
      i := cc - pkshdr^.chFst;
      SetRect (rSrc, prgwloc^ [i],
                     0,
                     prgwloc^ [i + 1],
                     pkshdr^.fbbdy);
   END {if}
   ELSE BEGIN
      GetIcon := FALSE;
   END; {else}
END; {GetIcon}

{===============================================================================

#######> DrawIcon

        PURPOSE: To center the specified icon in the given rectangle.

        GLOBALS MODIFIED: None.

===============================================================================}

PROCEDURE DrawIcon
   {    fam: Tfam;
        rDst: Rect;
        cc: TCc;
        cmode: TC;
    VAR cError: TC};

VAR
   dx,
   dy: TC;
   rSrc,
   rDstT: Rect;
   bmSrc: BitMap;

BEGIN {DrawIcon}
{$IFC fTraceFM} LogCall; {$ENDC}
   IF fdebug THEN BEGIN
      WRITELN ('DrawIcon');
      WRITELN ('rDst: ', rDst.left, rDst.top, rDst.right, rDst.bottom);
      WRITELN ('  cc: ', cc:2, ' cMode: ', cMode:2);
   END; {if}
   IF GetIcon (fam, cc, rSrc, bmSrc, cError)THEN BEGIN
      rDstT := rSrc;
      dx := ((rDst.right - rDst.left) - (rDstT.right - rDstT.left)) DIV 2;
      dx := dx - rDstT.left; {Translate to origin}
      dy := ((rDst.bottom - rDst.top) - (rDstT.bottom - rDstT.top)) DIV 2;
      dy := dy - rDstT.top; {Translate to origin}
      OffsetRect (rDstT, rDst.left + dx, rDst.top + dy);
      IF fdebug THEN BEGIN
         WITH rDstT DO BEGIN
            WRITELN ('rDstT: ', left, top, right, bottom);
         END; {with}
      END; {if}
      CopyBits (bmSrc, thePort^.portBits, rSrc, rDstT, cmode, Nil);
   END; {if}
END; {DrawIcon}

{===============================================================================

#######> GetPattern

        PURPOSE:  To extract a pattern from a font given the handle to the
                  font and the patterns character code.

        GLOBALS MODIFIED: None.

===============================================================================}

PROCEDURE GetPattern
   {    fam: Tfam;
        ccPat: TCc;
    VAR pat: Pattern;
    VAR cError: TC};

CONST
   bMax = 15;
   bpatMax = 7;

TYPE
   TRgb = PACKED ARRAY [0..bMax] OF TCc;
   TPrgb = ^TRgb;

VAR
   ib: 0..bMax;
   rgb: TRgb;
   prgb: TPrgb;
   r1,
   r2: Rect;
   bm,
   bmTmp: BitMap;

BEGIN {GetPattern}
{$IFC fTraceFM} LogCall; {$ENDC}
   IF fdebug THEN BEGIN
      WRITELN ('GetPattern');
      WRITELN ('  ccPat: ', ccPat);
   END; {if}

   IF GetIcon (fam, ccPat, r1, bmTmp, cError) THEN BEGIN
      prgb := @rgb;

      WITH bm DO BEGIN
         baseaddr :=  POINTER (ORD (prgb));
         rowbytes := 2;
         SetRect (bounds, 0, 0, 8, 8);
      END; {with}

      {Clear the destination}
      FOR ib := 0 TO bMax DO BEGIN
         rgb [ib] := 0;
      END; {for}

      r2 := r1;
      OffsetRect (r2, -r2.left, 0);  {Set origin to (0,0)}
      CopyBits (bmTmp, bm, r1, r2, srcCopy, Nil);

      FOR ib := 0 TO bpatMax DO BEGIN
         pat [ib] :=  prgb^ [ib * 2];
      END; {for}
   END; {if}
END; {GetPattern}

{===============================================================================

#######> GetCursor

        PURPOSE: To extract a cursor from a font given the handle to the font
                 and the character codes for the data, and mask.

        GLOBALS MODIFIED: None.

===============================================================================}

PROCEDURE GetCursor
   {    fam: Tfam;
        ccData,
        ccMask: TCc;
    VAR curse: Cursor;
    VAR cError: TC};

CONST
   bMax = 15;

TYPE
   TCheat = RECORD
      CASE BOOLEAN OF
         TRUE: (w: TW);
         FALSE: (bx: TB;
                 by: TB);
   END; {record}

VAR
   ib: 0..bMax;
   dx: TC;
   bm,
   bmTmp: BitMap;
   r1,
   r2: Rect;
   cursorTmp: Bits16;
   cheat: TCheat;
   pkshdr: TPkshdr;
   prgwloc,
   prgwoff: TPrgw;
   pbits16: ^Bits16;

BEGIN {GetCursor}
{$IFC fTraceFM} LogCall; {$ENDC}
   IF fdebug THEN BEGIN
      WRITELN ('GetPattern');
      WRITELN ('  ccData: ', ccData:2, ' ccMask: ', ccMask:2);
   END; {if}
   {Get the cursor data}
   IF GetIcon (fam, ccData, r1, bmTmp, cError) THEN BEGIN
      WITH bm DO BEGIN
         baseaddr := @cursorTmp;
         rowbytes := 2;
         SetRect (bounds, 0, 0, 16, 16);
      END; {with}

      {Save a pointer to the bits for clearing the bitmap before transfers}
      pbits16 := @cursorTmp;
      FOR ib := 0 TO bMax DO BEGIN
         pbits16^ [ib] := 0;
      END; {for}

      r2 := r1;
      OffsetRect (r2, -r2.left, 0); {set origin to (0,0)}
      CopyBits (bmTmp, bm, r1, r2, srcCopy, Nil);
      curse.data := cursorTmp;

      {Get the cursor hot spot}
      IF SplitFont (fam, pkshdr, prgwloc, prgwoff, cError) THEN BEGIN
         WITH curse DO BEGIN
            cheat.w := prgwoff^ [ccData - pkshdr^.chFst];
            hotspot.h := cheat.bx;
            hotspot.v := cheat.by;
         END; {with}
      END; {if}

      IF ccMask <> ccWhiteMask THEN BEGIN

         {Get the cursor mask}
         IF GetIcon (fam, ccMask, r1, bmTmp, cError) THEN BEGIN
            r2 := r1;
            OffsetRect (r2, -r2.left, 0); {set origin to (0,0)}

            {Offset the mask rect as necessary to align with data.}
            WITH curse DO BEGIN
               cheat.w := prgwoff^ [ccMask - pkshdr^.chFst];
               dx := hotspot.h - cheat.bx;
            END; {with}
            OffsetRect (r2, dx, 0);
            CopyBits (bmTmp, bm, r1, r2, srcCopy, Nil);
         END; {if}
      END {if not a white mask}
      ELSE BEGIN

          {set all the bits in cursorTmp to 0}
          FOR ib := 0 TO bMax DO BEGIN
             pbits16^ [ib] := 0;
          END; {for}
      END; {else it was a white mask}

      curse.mask := cursorTmp;
   END; {if}
END; {GetCursor}
END. {Unit FmgrUtil}
ÿ