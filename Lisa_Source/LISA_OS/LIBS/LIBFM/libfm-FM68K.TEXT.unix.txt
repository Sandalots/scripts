; Copyright 1983, Apple Computer Inc.
;-------------------------------------------
;
;  KERNED STRIKE FONT FORMAT OFFSETS:
;
FORMAT          .EQU    0               ;WORD
MINCHAR         .EQU    2               ;WORD
MAXCHAR         .EQU    4               ;WORD
MAXWD           .EQU    6               ;WORD
FBBOX           .EQU    8               ;WORD
FBBOY           .EQU    10              ;WORD
FBBDX           .EQU    12              ;WORD
FBBDY           .EQU    14              ;WORD
LENGTH          .EQU    16              ;WORD
ASCENT          .EQU    18              ;WORD
DESCENT         .EQU    20              ;WORD
XOFFSET         .EQU    22              ;WORD
RASTER          .EQU    24              ;WORD
BITS            .EQU    26              ;byte offset in font for bits

        .PROC   GetWidths,2
;------------------------------------------
;
;  PROCEDURE GetWidths(hFont,hWidths: Handle);
;
;  Given a handle to a kerned-strike format font,
;  fill in a 256 byte width table.
;
;  A6 OFFSETS OF PARAMETERS AFTER LINK:
;
PARAMSIZE       .EQU    8
HFONT           .EQU    PARAMSIZE+8-4           ;HANDLE TO KERNED STRIKE
HWIDTHS         .EQU    HFONT-4                 ;HANDLE TO 256 BYTE TABLE


        LINK    A6,#0                           ;NO LOCAL VARS
        MOVE.L  A2,-(SP)                        ;PRESERVE REG
;
;  GET FONT HANDLE AND DE-REFERENCE IT
;
        MOVE.L  HFONT(A6),A2                    ;GET HANDLE TO FONT
        MOVE.L  (A2),A2                         ;DE-REFERENCE IT
;
;  POINT TO WIDTH TABLE IN FONT
;
        MOVE.L  A2,A0                           ;COPY FONTPTR
        ADD     LENGTH(A2),A0                   ;ADD 2*LENGTH BYTES
        ADD     LENGTH(A2),A0
        ADD     #16,A0                          ;POINT TO KS WIDTHS
;
;  GET WIDTH OF MISSING CHAR
;
        MOVE    MAXCHAR(A2),D0

        ADD     #1,D0
        SUB     MINCHAR(A2),D0
        ADD     D0,D0
        MOVE    0(A0,D0),D2                     ;GET OFFSET AND WIDTHS BYTES
;
;  GET DST HANDLE AND DE-REFERENCE
;
        MOVE.L  HWIDTHS(A6),A1                  ;GET HANDLE TO WIDTHS
        MOVE.L  (A1),A1                         ;DE-REFERENCE IT
;
;  INSTALL MISSING WIDTH UP TO MINCHAR
;
        MOVE    MINCHAR(A2),D1
        BRA.S   MORE1
LOOP1   MOVE.B  D2,(A1)+
MORE1   DBRA    D1,LOOP1

;
;  INSTALL CHARACTER WIDTHS UP TO MAXCHAR
;
        MOVE    MAXCHAR(A2),D1
        SUB     MINCHAR(A2),D1
LOOP2   MOVE    (A0)+,D0                        ;GET OFFSET AND WIDTH
        CMP     #-1,D0                          ;IS THIS ONE MISSING ?
        BNE.S   NOTMISS                         ;NO, CONTINUE
        MOVE    D2,D0                           ;YES, REPLACE WITH MISSING WIDTH
NOTMISS MOVE.B  D0,(A1)+                        ;INSTALL WIDTH INTO TABLE
        DBRA    D1,LOOP2

;
;  INSTALL MISSING WIDTH UP TO 255
;
        MOVE    #255,D1
        SUB     MAXCHAR(A2),D1
        BRA.S   MORE3
LOOP3   MOVE.B  D2,(A1)+                        ;INSTALL WIDTH OF MISSING CHAR
MORE3   DBRA    D1,LOOP3                        ;LOOP TILL TABLE(255) DONE

        MOVE.L  (SP)+,A2                        ;RESTORE REG
        UNLK    A6
        MOVE.L  (SP)+,A0                        ;POP RETURN ADDR
        ADD     #PARAMSIZE,SP                   ;STRIP PARAMS
        JMP     (A0)                            ;AND RETURN
        .ASCII 'GETWIDTH'


;---------------------------------------------------------------
        .PROC   UnCompress,3
;       PROCEDURE UnCompress( maxSize, size: TL; pThing: TP ); EXTERNAL;
;       Uncompress image of size n into maxsize
;---------------------------------------------------------------

        LINK    A6,#0                   ; fake debugger head
        MOVE.L  (A7)+,A6                ;  "      "

        MOVE.L  (A7)+,A0                ; get return address
        MOVE.L  A2,D2                   ; save A2

        MOVE.L  (A7)+,D0                ; get thing pointer
        MOVE.L  (A7)+,A1                ; get size into pointer
        ADD.L   D0,A1                   ; A1:=pThing+size
        MOVE.L  (A7)+,D1                ; get unComp size
        MOVE.L  D1,A2                   ; get unComp into pointer
        ADD.L   D0,A2                   ; A2:=pThing+maxsize


LOOP    MOVE    #$100,D0                ; prime D0 for 8 bit count count
        MOVE.B  -(A1),D0                ; load map byte from old

MLOOP   LSR.W   #1,D0                   ; shift off map bit
        BEQ.S   DONE                    ; byte done when = 0
        BCC.S   BLACK                   ; dispatch on the bit
        CLR.B  -(A2)                    ; store zero in new
        BRA.S   MLOOP                   ; continue for all 8 bits
BLACK   MOVE.B  -(A1),-(A2)             ; store byte in new
        BRA.S   MLOOP                   ; continue for all 8 bits

DONE    SUBQ.L  #8,D1                   ; see if dest pointer = 0
        BGT.S   LOOP

;       BOTH A1 and A2 should point to original pThing if all went well
        MOVE.L  D2,A2                   ; restore A2
        JMP     (A0)

        UNLK    A6                      ; fake debugger tail
        RTS

        .ASCII  'UNCOMPRE'

;---------------------------------------------------------------
        .FUNC   FMCOMPRE,3
;       FUNCTION  FMCompress( pSource, pDest: TP; lSourceSize: TL ): TL; EXTERNAL;
;       Compress image of size lSourceSize and return new size. lSourceSize MUST
;       be rounded up to nearest multiple of 8
;---------------------------------------------------------------

        LINK    A6,#0

        MOVEM.L  D3-D4/A2,-(A7)            ; Save registers on stack

        MOVE.L  8(A6),D1                ; get size D1
        MOVE.L  12(A6),A2               ; get dest pointer
        MOVE.L  16(A6),A1               ; get src pointer

        MOVE.L  A2,D2                   ; save dest pointer for size calculation

LOOP    MOVE    #7,D4                   ; bit counter
BLOOP   MOVE.B  (A1)+,(A2)              ; move byte into destination
        SEQ     D3                      ; set loworder byte in D3 accordingly
        BEQ.S   CONT                    ; check for zero, if so skip store
        ADDQ.L  #1,A2                   ; <>0, bump up dest pointer

CONT    LSL.W   #1,D3                   ; save map bit in high order byte
        DBF     D4,BLOOP                 ; continue for 8 bits

        LSR.W   #8,D3                   ; get high order byte
        MOVE.B  D3,(A2)+                ; save map bits in dest

        SUBQ.L  #8,D1                   ; see if done( 8 bytes at a time )
        BGT     LOOP

; calculate return value
        SUB.L   A2,D2                   ; subtract end ptr from start = -count
        NEG.L   D2                      ; positive count

        MOVEM.L  (A7)+,D3-D4/A2         ; Restore registers from stack
        UNLK    A6
        MOVE.L  (A7)+,A0                ; return address
        ADD.L   #12,A7                  ; fix up the stack(3 l)
        MOVE.L  D2,(A7)                 ; return count
        JMP     (A0)                    ; return

        .ASCII  'FMCOMPRE'

;---------------------------------------------------------------
        .PROC   VXORFont,1
;       PROCEDURE VXORFont( pFont: TP ); EXTERNAL;
;       Compresses the raster portion of a font by XOR's.
;---------------------------------------------------------------

        LINK    A6,#0

        MOVEM.L  D3/A2,-(A7)            ; Save registers on stack

        MOVE.L  8(A6),A0                ; get font pointer

        MOVE.L  A0,A1                   ; get pointer to bits
        ADD.L   #bits,A1                ; offset to bits start

        MOVE    ascent(A0),D3           ; get ascent
        ADD     descent(A0),D3          ; add descent to get height
        BEQ     OUT                     ; escape if = 0

        CLR.L   D0                      ; get raster width
        MOVE    raster(A0),D0
        BEQ     OUT                     ; escape if = 0
        ADD.L   D0,D0                   ; double raster to make it # bytes

        MOVE    D3,D1                   ; calculate # bytes in bitmap
        MULU    D0,D1                   ; raster*height = #bytes in D1

        ADD.L   D1,A1                   ; A1 points just after bits

        MOVE.L  A1,A2                   ; get other pointer
        SUB.L   D0,A2                   ; point to previous scanline

        SUB.L   D0,D1                   ; D1 now = #bytes-raster bytes
        ASR.L   #1,D1                   ; D1 = # words from above

LOOP    MOVE    -(A2),D0                ; get word from previous scanline
        EOR     D0,-(A1)                ; xor into this scanline

        SUBQ.L  #1,D1                   ; continue
        BGT     LOOP

; A2 should = first word of bits and A1 the next scanline


OUT     MOVEM.L  (A7)+,D3/A2            ; Restore registers from stack
        UNLK    A6
        MOVE.L  (A7)+,A0                ; return address
        ADD.L   #4,A7                   ; fix up the stack(1 long)
        JMP     (A0)                    ; return

        .ASCII  'VXORFONT'

;---------------------------------------------------------------
        .PROC   UnVXORFont,1
;       PROCEDURE UnVXORFont( pFont: TP ); EXTERNAL;
;       Uncompresses the raster portion of a font by XOR's.
;---------------------------------------------------------------

        LINK    A6,#0

        MOVEM.L  D3/A2,-(A7)            ; Save registers on stack

        MOVE.L  8(A6),A0                ; get font pointer

        MOVE.L  A0,A1                   ; get pointer to bits
        ADD.L   #bits,A1                ; offset to bits start

        MOVE    ascent(A0),D3           ; get ascent
        ADD     descent(A0),D3          ; add descent to get height
        BEQ     OUT                     ; escape if = 0

        CLR.L   D0                      ; get raster width
        MOVE    raster(A0),D0
        BEQ     OUT                     ; escape if = 0

        SUBQ    #1,D3                   ; height-1;
        MULU    D0,D3                   ; raster*height-1 = #words in D1

        MOVE.L  A1,A2                   ; get other pointer
        ADD     D0,A2                   ; point to second scan line
        ADD     D0,A2                   ; point to second scan line


LOOP    MOVE    (A1)+,D0                ; get word from previous scanline
        EOR     D0,(A2)+                ; xor into this scanline

        SUBQ.L  #1,D3                   ; continue
        BGT     LOOP

; A2 should = first word of bits and A1 the next scanline


OUT     MOVEM.L  (A7)+,D3/A2            ; Restore registers from stack
        UNLK    A6
        MOVE.L  (A7)+,A0                ; return address
        ADD.L   #4,A7                   ; fix up the stack(1 long)
        JMP     (A0)                    ; return

        .ASCII  'UNVXORFO'

;---------------------------------------------------------------
        .PROC   HXORFont,1
;       PROCEDURE HXORFont( pFont: TP ); EXTERNAL;
;       Horizontally ompresses the raster portion of a font by XOR's.
;---------------------------------------------------------------

        LINK    A6,#0

        MOVEM.L  D3/A2,-(A7)            ; Save registers on stack

        MOVE.L  8(A6),A0                ; get font pointer

        MOVE.L  A0,A1                   ; get pointer to bits
        ADD.L   #bits,A1                ; offset to bits start

        MOVE    ascent(A0),D3           ; get ascent
        ADD     descent(A0),D3          ; add descent to get height
        SUBQ    #1,D3                   ; DBF ize it

        MOVE    raster(A0),D0
        SUBQ    #1,D0                   ; DBF ize it

LOOP1   MOVE    D0,D1                   ; D1 = word count across
        CLR     D2                      ; clear x bit
        LSR     #1,D2

LOOP2   MOVE    (A1),D2                 ; get word from scanline
        ROXR    #1,D2                   ; rotate right
        EOR     D2,(A1)+                ; xor back
        DBF     D1,LOOP2                ; continue

        DBF     D3,LOOP1

; A1 should = just after last word of bits


        MOVEM.L  (A7)+,D3/A2            ; Restore registers from stack
        UNLK    A6
        MOVE.L  (A7)+,A0                ; return address
        ADD.L   #4,A7                   ; fix up the stack(1 long)
        JMP     (A0)                    ; return

        .ASCII  'HXORFONT'


;---------------------------------------------------------------
        .FUNC   HashOfId,3
;
;       FUNCTION HashOfId
;               (    id: INTEGER;
;                    hTable: TH;
;                VAR firstIndex: INTEGER)
;       : INTEGER;
;       EXTERNAL;
;       return the hashed value (index) of the given id.
;---------------------------------------------------------------
;
;
id              .EQU    16              ; offset from A6 of id
hTable          .EQU    12              ;    "     "   "  "  hTable
firstIndex      .EQU    8               ;    "     "   "  "  firstIndex
paramSize       .EQU    10
rollMask        .EQU    $7F             ; 128 - 1
empty           .EQU    0               ; key value of unoccupied table entry

                LINK    A6,#0                   ; set up our stack frame
;
; set up registers
;
                MOVE.L  hTable (A6), A0         ; get handle to hash table
                MOVE    id (A6), D2             ; get id
                MOVE    D2, D1                  ; copy id
;
; hashing function equals half-rotated, exclusive-or of swapped halves of name
;
                ROR     #8, D1                  ; swap bytes
                EOR     D2, D1                  ;
                ROR     #2, D1                  ; /2 for the heck of it
                AND     #rollMask, D1           ; D1 (index) is inside the table
;
; search loop: roll is beginning of rollover part - if index overflows
;
roll            MOVE    D1, D0                  ; get base index
                ASL     #1, D0                  ; D0 is times 2 bytes
                MOVE    D1, D3                  ;
                ASL     #4, D3                  ; D3 is times 16 bytes
                ADD     D3, D0                  ; index-D0 = D1 * 18 bytes/entry
;
; lets keep a few statistics about collisions
;
                MOVE.L  firstIndex(A6), A2      ; get the address to firstIndex
                CMP     #0, (A2)                ; have we set the first index
                BNE     continue                ; yes,  continue
                MOVE    D1, (A2)                ; no, return the index
                ADD     #1, (A2)                ; table begins @ 1
;
continue        MOVE.L  (A0), A1                ; get pointer to hash table
loop            CMP     #empty, 0(A1, D0)       ; is entry empty?
                BEQ     goHome                  ; yes, return that index
                MOVE    0(A1, D0), D3           ; does key match id?
                CMP     D3, D2                  ; check key against id
                BEQ     goHome                  ; yes, return that index
                ADD     #1, D1                  ; no, check the next one
                AND     #rollMask, D1           ; make sure we're in-bounds
                BEQ     roll                    ; if rollover, start at beginning
                ADD     #18, D0                 ; otherwise index to next entry
                BRA     loop
;
; assignment of result to function and return
;
goHome          UNLK    A6                      ; restore old frame pointer
                MOVE.L  (SP)+, A0               ; get return address
                ADD     #paramSize, SP          ; clean up stack
                ADD     #1, D1                  ; table begins @ 1
                MOVE    D1, (SP)                ; set up function result
                JMP     (A0)                    ; return

                .ASCII  'HASHOFID'

;---------------------------------------------------------------
        .FUNC   FMROR,2
;       FUNCTION FMROR ( w1: TW ): TW
;---------------------------------------------------------------
                MOVE.L  (A7)+, A0               ; return address
                MOVE    (A7)+, D0               ; w1
                ROR     #3, D0                  ; rotate 3 bits(1 or 2 didnt work)
                MOVE    D0, (A7)                ; store return value
                JMP     (A0)

                .END
ÿ