PROGRAM MakeHeur;
{$E MAKE.ERROR.TEXT}
USES
   {$U libqd/QuickDraw}    QuickDraw,
   {$U libsm/UnitStd} UnitStd;

CONST
   bufMax = 5000;
   heapSize = 10000;

TYPE
   TCc = 0..255;

   TEface = (efaceBold,
             efaceItalic,
             efaceUnderLined,
             efaceOutlined,
             efaceShadow);

   TSeteface = SET OF TEface;

   TFam = TC;

   TDev = -128..127;

   TEwhcl = (ewhcl20,
             ewhcl15,
             ewhcl12,
             ewhcl10,
             ewhclps,
             ewhclmf,
             ewhclmp,
             ewhcllcs);

   TEskcl = (eskclMPPS,
             eskclMPFP,
             eskclMFR,
             eskclMFI,
             eskclApple,
             eskclQume);

   TEdev = (edevScreen,
            edevDotMx,
            edevHDotMx,
            edevTyper,
            edevLaser,
            edev1,
            edev2);

   TLfntid = PACKED RECORD
      fam: TFam;
      seteface: Style;
      CASE BOOLEAN OF
         TRUE: (dev: TDev);

         FALSE: (fOffline: TF;

                 CASE edev: TEdev OF
                    edevScreen,
                    edevLaser: ();
                    edevDotMx,
                    edevHDotMx: (fHiRes: TF;
                                fPortrait: TF);
                    edevTyper: (fSpoke: TF;
                                ewhcl: TEwhcl);
                    edev1,      {Added 2 general devices for additional printers }
                    edev2:  ()
                );
   END; {TLfntid}

   TWid = 0..255;
   TMpccwid = PACKED ARRAY [TCc] OF TWid;
   TPmpccwid = ^TMpccwid;
   THmpccwid = ^TPmpccwid;

   Tstr30 = STRING [30];

   TWordFile = FILE OF INTEGER;

   THeurTable = ARRAY [0..bufMax] OF TB;

VAR
   str30T: TStr30;
   outFile: FILE;
   inFile: TEXT;
   c,
   cch,
   cerr,
   crec,
   cTemp,
   cFaceRecs: TC;

   eface: TEface;
   seteface: TSeteface;
   setedev: SET OF TEdev;
   ewhcl: TEwhcl;
   eskcl: TEskcl;
   pb: ^TB;
   recMax: TB;
   srcFile,
   dstFile: STRING [30];

   irgb: INTEGER;
   heurTable: THeurTable;

   fdebug: TF;

{===============================================================================


#######> WriteFile

        COMMENT: To the modified heuristic table out to a file.

        GLOBALS: None.


===============================================================================}
PROCEDURE WriteFile;
VAR
   blocksTransferred,
   cblocks: INTEGER;

BEGIN {WriteFile}
   cblocks := (irgb + 511) DIV 512;
   IF fdebug THEN BEGIN
      WRITELN;
      WRITELN ('Number of bytes in table = ', irgb:1);
      WRITELN ('Number of blocks to be written = ', cblocks:1);
   END;
   blocksTransferred := BLOCKWRITE (outFile, heurTable, cblocks);
   IF fdebug THEN BEGIN
      WRITELN (blocksTransferred:1, ' blocks transferred.');
   END;
   CLOSE (outFile, LOCK);
END; {WriteFile}

{===============================================================================

#######> Int

        COMMENT: To convert a string number into an integer.

        GLOBALS: None.

===============================================================================}

FUNCTION Int
   (   str30: TStr30)
   : TC;

CONST
   max = 32767;
   min = -32768;

VAR
   dlet,
   ilet: TC;
   c: LONGINT;

BEGIN {Int}
   dlet := ORD ('0');
   ilet := 1;
   c := 0;
   WHILE (str30 [ilet] IN ['0'..'9'])
         AND
         (ilet <= LENGTH (str30)) DO BEGIN
      c := c * 10 + (ORD (str30 [ilet]) - dlet);
      ilet := ilet + 1;
   END; {while}

   IF (c < max)
      AND
      (c > min) THEN BEGIN
      Int := c;
   END; {if}
END; {Int}

{<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>}

BEGIN {MakeHeur}
   fdebug := TRUE;
   IF fdebug THEN BEGIN
      WRITELN ('MakeHeur');
   END; {if}
   WRITELN ('What file do you want converted?');
   READLN (srcFile);
   WRITELN ('What file do you want generated?');
   READLN (dstFile);
   {$I-} RESET (infile, srcFile);
         cerr := IORESULT; {$I+}
   IF cerr <> 0 THEN BEGIN
      IF fdebug THEN BEGIN
         WRITELN ('FATAL Error #', cerr:1, ' on file ', srcFile);
      END; {if}
      HALT;
   END; {if}
   {$I-} REWRITE (outFile, dstFile);
         cerr := IORESULT; {$I+}
   IF cerr <> 0 THEN BEGIN
      IF fdebug THEN BEGIN
         WRITELN ('FATAL Error #', cerr:1, ' on file ', dstFile);
      END; {if}
      HALT;
   END; {if}

   irgb := 0;
   {read the number of heurRec's in this file}
   READLN (infile, str30T);
   recMax := Int (str30T);
   IF fdebug THEN BEGIN
      WRITELN ('recMax: ', recMax:3);
   END; {if}
   heurTable [irgb] := recMax;
   irgb := irgb + 1;

   FOR crec := 1 TO recMax DO BEGIN
      IF NOT EOF (infile) THEN BEGIN
         {Fill in successive entrys in the record}
         READLN (infile, str30T);  {Read in which family}
         heurTable [irgb] := Int (str30T);
         irgb := irgb + 1;
         IF fdebug THEN BEGIN
            WRITELN (' Entry#', crec:2, '[', heurTable [irgb - 1]:1);
         END;

         IF fdebug THEN BEGIN
            WRITE ('           [');
         END;
         FOR ewhcl := ewhcl20 TO ewhcllcs DO BEGIN   {Read in the print wheel sub. table}
            READLN (infile, cTemp);
            heurTable [irgb] := cTemp;
            irgb := irgb + 1;
            IF fdebug THEN BEGIN
               WRITE (heurTable [irgb - 1]:3);
            END;
         END; {for}
         IF fdebug THEN BEGIN
            WRITELN (']');
         END;

         READLN (infile, str30T); {Find out how many faceRecs there will be}
         cFaceRecs := Int (str30T);
         heurTable [irgb] := cFaceRecs;
         irgb := irgb + 1;

         IF fdebug THEN BEGIN
            WRITELN ('           [');
         END;
         FOR c := 1 TO cFaceRecs DO BEGIN
            IF fdebug THEN BEGIN
               WRITE ('             [{');
            END;
            seteface := []; {Get the set of allowable faces}
            READLN (infile, str30T);
            IF POS ('B', str30T) <> 0 THEN BEGIN
               seteface := seteface + [efaceBold];
            END;
            IF POS ('I', str30T) <> 0 THEN BEGIN
               seteface := seteface + [efaceItalic];
            END;
            IF POS ('U', str30T) <> 0 THEN BEGIN
               seteface := seteface + [efaceUnderLined];
            END;
            IF POS ('O', str30T) <> 0 THEN BEGIN
               seteface := seteface + [efaceOutlined];
            END;
            IF POS ('S', str30T) <> 0 THEN BEGIN
               seteface := seteface + [efaceShadow];
            END;
            pb := @seteface;
            heurTable [irgb] := pb^;
            irgb := irgb + 1;
            IF fdebug THEN BEGIN
               WRITE (heurTable [irgb - 1]:1, '} ');
            END;

            {Get the values of faces}
            FOR eface := efaceBold TO efaceShadow DO BEGIN
               READLN (infile, str30T);  {Get the values of faces}
               heurTable [irgb] := Int (str30T);
               irgb := irgb + 1;
               IF fdebug THEN BEGIN
                  WRITE (heurTable [irgb - 1]:1, ' ');
               END;
            END; {for}

            READLN (infile, str30T);  {Get the extra width}
            heurTable [irgb] := Int (str30T);
            irgb := irgb + 1;
            IF fdebug THEN BEGIN
               WRITELN (heurTable [irgb - 1]:1, ']');
            END;

            IF fdebug THEN BEGIN
               WRITE ('             [');
            END;
            setedev := [];  {Get the set of allowable devices}
            READLN (infile, str30T);
            IF POS ('S', str30T) <> 0 THEN BEGIN
               setedev := setedev + [edevScreen];
            END;
            IF POS ('T', str30T) <> 0 THEN BEGIN
               setedev := setedev + [edevTyper];
            END;
            IF POS ('D', str30T) <> 0 THEN BEGIN
               setedev := setedev + [edevDotMx];
            END;
            IF POS ('H', str30T) <> 0 THEN BEGIN
               setedev := setedev + [edevHDotMx];
            END;
            IF POS ('L', str30T) <> 0 THEN BEGIN
               setedev := setedev + [edevLaser];
            END;
            IF POS ('C', str30T) <> 0 THEN BEGIN
               setedev := setedev + [edev1];
            END;
            IF POS ('X', str30T) <> 0 THEN BEGIN
               setedev := setedev + [edev2];
            END;
            pb := @setedev;
            heurTable [irgb] := pb^;
            irgb := irgb + 1;
            IF fdebug THEN BEGIN
               WRITELN (heurTable [irgb - 1]:1, ']');
            END;

            READLN (infile, str30T); {Get the root file name}
            IF fdebug THEN BEGIN
               WRITELN ('             [', str30T, ']');
               IF c <> cFaceRecs THEN BEGIN
                  WRITELN;
               END;
            END;
            FOR cch := 0 TO LENGTH (str30T) DO BEGIN
               heurTable [irgb] := ORD (str30T [cch]);
               irgb := irgb + 1;
            END; {for}
         END; {for # faceRecs}
         IF fdebug THEN BEGIN
            WRITELN ('           ]');
            WRITELN ('         ]');
         END;
         READLN (infile, str30T); {Read an inter-record gap}
         IF fdebug THEN BEGIN
            WRITELN ('< current byte count is: ', irgb:1, ' >');
         END;
      END; {if EOF}
   END; {for # heuristic entries}

   {read the spoke table heuristic entries}
   IF fdebug THEN BEGIN
      WRITELN ('Processing Spoke Table Entrys');
   END;
   IF NOT EOF (infile) THEN BEGIN
      FOR eskcl := eskclMPPS TO eskclQume DO BEGIN
         READLN (infile, str30T);  {Get the spoke table root file name}
         IF fdebug THEN BEGIN
            WRITELN ('Spoke Table Name: "', str30T, '"');
         END;
         IF EOF (inFile) THEN BEGIN
            WRITELN ('EOF on entry #', ORD (eskcl):1);
         END
         ELSE BEGIN
            FOR cch := 0 TO LENGTH (str30T) DO BEGIN
               heurTable [irgb] := ORD (str30T [cch]);
               irgb := irgb + 1;
            END; {for}
         END; {else}
         IF fdebug THEN BEGIN
            WRITELN;
         END;
      END; {for}
   END
   ELSE BEGIN
      WRITELN ('EOF found prior to Spoke Table Entrys.');
   END;

   CLOSE (infile, LOCK);
   IF fdebug THEN BEGIN
      WRITELN ('{heurTable address = ', ORD (heurTable [0]):1, '}');
   END;
   WriteFile;
END. {MakeHeur}

ÿ