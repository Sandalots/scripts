{ Copyright 1983, 1984, Apple Computer Inc. }

(*$S kr_spare *)

  procedure Info_Process (* var errnum : int2;  proc_id : int4;
                            var proc_info : procinfoRec *);

        (*************************************************************)
        (*                                                           *)
        (* Description:                                              *)
        (*   System call procedure to get information about a        *)
        (*   process.                                                *)
        (*                                                           *)
        (* Input Parameters:                                         *)
        (*   proc_id : the global id of the process about which the  *)
        (*             the information is to be returned.            *)
        (*                                                           *)
        (* Output Parameters:                                        *)
        (*   errnum : the error return paramter for this system      *)
        (*            call procedure (see values below).             *)
        (*                                                           *)
        (*   proc_info : the record containing the information       *)
        (*               about the specified process.                *)
        (*                                                           *)
        (* Side Effects:                                             *)
        (*   None.                                                   *)
        (*                                                           *)
        (* Special Conditions of Use:                                *)
        (*   None.                                                   *)
        (*                                                           *)
        (* Error Conditions:                                         *)
        (*   errnum = 0              successfully completed          *)
        (*          = e_noprocess    no such process                 *)
        (*          = e_invprocess   invalid process (system)        *)
        (*                                                           *)
        (*************************************************************)

    var
      pcb_ptr : ptr_PCB;                  { ptr to PCB for the process }
      fatherpcb_ptr : ptr_PCB;            { ptr to PCB of father of the process }

    begin  { Info_Process }
      (*$IFC debug3 *)
      if Trace (pm, 90) then
        writeln ('   Entering Info_Process...');
      (*$ENDC *)

      Get_PName (errnum, proc_id, true, proc_info.progpathname);

      if errnum = 0 then
        begin  { proc_id was for a valid process }
          { make sure it's still around }
          Find_PCB (errnum, Sys_Called, proc_id, pcb_ptr);
          if errnum <> 0 then exit (Info_Process)
          else if (proc_info.progpathname = '') and not Sys_Called then
            begin  { user process is unloading/terminating }
              errnum := e_noprocess;
              exit (Info_Process)
            end;

          { fill in the needed info - already have program name }
          with proc_info do
            begin
              global_id := pcb_ptr^.glob_id;
              fatherpcb_ptr := pointer (pcb_ptr^.fatherptr + b_sysglobal_ptr);
              father_id := fatherpcb_ptr^.glob_id;
              priority := pcb_ptr^.norm_pri;
              if pcb_ptr^.blk_state = [] then
                state := pactive
              else if Stopped (pcb_ptr) then
                state := psuspended
              else
                state := pwaiting;
              if pcb_ptr^.need_mem then
                data_in := false
              else
                data_in := true
            end { with proc_info }
        end { valid proc_id }
    end { Info_Process };


  procedure Suspend_Process (* var errnum : int2;  proc_id : int4;
                               susp_family : boolean *);

        (*************************************************************)
        (*                                                           *)
        (* Description:                                              *)
        (*   System call procedure to suspend a process and          *)
        (*   possibly its descendants.  Each process in the family   *)
        (*   is 'stopped' from execution by either being blocked     *)
        (*   immediately waiting on an 'activate' or, if the         *)
        (*   process is in system code, a 'suspend' soft interrupt   *)
        (*   is sent to it indicating that it should block itself    *)
        (*   when it leaves system code.                             *)
        (*                                                           *)
        (* Input Parameters:                                         *)
        (*   proc_id : the global id of the process to be            *)
        (*             suspended.  This can be the calling process   *)
        (*             itself.                                       *)
        (*                                                           *)
        (*   susp_family : a flag indicating that the descendants    *)
        (*                 of the process should be suspended along  *)
        (*                 the process itself.                       *)
        (*                                                           *)
        (* Output Parameters:                                        *)
        (*   errnum : the error return parameter for this system     *)
        (*            call procedure (see values below).             *)
        (*                                                           *)
        (* Side Effects:                                             *)
        (*   None.                                                   *)
        (*                                                           *)
        (* Special Conditions of Use:                                *)
        (*   None.                                                   *)
        (*                                                           *)
        (* Error Conditions:                                         *)
        (*   errnum = 0              successfully completed          *)
        (*          = e_noprocess    no such process                 *)
        (*          = e_invprocess   invalid process (system)        *)
        (*          = w_suspended    process already suspended       *)
        (*                                                           *)
        (*************************************************************)

    const
      w_suspended = 0;                    { warning: process already suspended }

    var
      pcb_ptr : ptr_PCB;                  { ptr to PCB for the process }
      fam_ptr : ptr_PCB;                  { ptr to 1st PCB in family of process }

    begin
      (*$IFC debug3 *)
      if Trace (pm, 90) then
        writeln ('   Entering Suspend_Process...');
      (*$ENDC *)

      Find_PCB (errnum, Sys_Called, proc_id, pcb_ptr);

      if errnum = 0 then
        begin  { proc_id is for a valid process }
          if Stopped (pcb_ptr) then
            errnum := -(proc_errbase + eb_suspend + w_suspended)
          else
            begin  { process is not already suspended }
              if (pcb_ptr^.sonptr <> 0) and susp_family then
                begin  { suspend family 1st }
                  fam_ptr := pointer(pcb_ptr^.sonptr + b_sysglobal_ptr);
                  Scan_Family (fam_ptr, @Stop_Proc)
                end;

              { now suspend the specified process itself }
              Stop_Proc (pcb_ptr)
            end
        end { valid proc_id }
    end { Suspend_Process };


  procedure Activate_Process (* var errnum : int2;  proc_id : int4
                                act_family : boolean *);

        (*************************************************************)
        (*                                                           *)
        (* Description:                                              *)
        (*   System call procedure to activate a suspended process   *)
        (*   and possibly its descendants.  Each process in the      *)
        (*   family is 'unstopped' by either unblocking it if it is  *)
        (*   in an 'activate' wait or by clearing the pending soft   *)
        (*   interrupt sent to it earlier.                           *)
        (*                                                           *)
        (* Input Parameters:                                         *)
        (*   proc_id : the global id of the process that is to be    *)
        (*             activated.                                    *)
        (*                                                           *)
        (*   act_family : a flag indicating that the descendants of  *)
        (*                the process should be activated along      *)
        (*                with process itself.                       *)
        (*                                                           *)
        (* Output Parameters:                                        *)
        (*   errnum : the error return parameter for this system     *)
        (*            call procedure (see values below).             *)
        (*                                                           *)
        (* Side Effects:                                             *)
        (*   None.                                                   *)
        (*                                                           *)
        (* Special Conditions of Use:                                *)
        (*   None.                                                   *)
        (*                                                           *)
        (* Error Conditions:                                         *)
        (*   errnum = 0              successfully completed          *)
        (*          = e_noprocess    no such process                 *)
        (*          = e_invprocess   invalid process (system)        *)
        (*          = w_notsuspd     process not in suspended state  *)
        (*                                                           *)
        (*************************************************************)

    const
      w_notsuspd = 0;                     { warning: process not suspended }

    var
      pcb_ptr : ptr_PCB;                  { ptr to PCB for the process }
      fam_ptr : ptr_PCB;                  { ptr to 1st PCB in family of process }

    begin
      (*$IFC debug3 *)
      if Trace (pm, 90) then
        writeln ('   Entering Activate_Process...');
      (*$ENDC *)

      Find_PCB (errnum, Sys_Called, proc_id, pcb_ptr);

      if errnum = 0 then
        begin  { proc_id is for a valid process }
          if not Stopped (pcb_ptr) then
            errnum := -(proc_errbase + eb_activate + w_notsuspd)
          else
            begin  { process is, indeed, suspended }
              if (pcb_ptr^.sonptr <> 0) and act_family then
                begin  { handle family 1st }
                  fam_ptr := pointer(pcb_ptr^.sonptr + b_sysglobal_ptr);
                  Scan_Family (fam_ptr, @Unstop_Proc)
                end;

              { now handle process itself }
              Unstop_Proc (pcb_ptr)
            end
        end { valid proc_id }
    end { Activate_Process };


(*$S ProcTerm *)

  procedure Kill_Process (* var errnum : int2;  proc_id : int4 *);

        (*************************************************************)
        (*                                                           *)
        (* Description:                                              *)
        (*   System call procedure to delete a process and all of    *)
        (*   its descendants from the system.  The specified         *)
        (*   process is deleted by sending it a 'die' soft           *)
        (*   interrupt which is handled by the process as soon as    *)
        (*   it is not in system code.                               *)
        (*                                                           *)
        (* Input Parameters:                                         *)
        (*   proc_id : the global id of the process which is to be   *)
        (*             deleted.                                      *)
        (*                                                           *)
        (* Output Parameters:                                        *)
        (*   errnum : the error return parameter for this system     *)
        (*            call procedure (see values below).             *)
        (*                                                           *)
        (* Side Effects:                                             *)
        (*   None.                                                   *)
        (*                                                           *)
        (* Special Conditions of Use:                                *)
        (*   None.                                                   *)
        (*                                                           *)
        (* Error Conditions:                                         *)
        (*   errnum = 0              successfully completed          *)
        (*          = e_noprocess    no such process                 *)
        (*          = e_invprocess   invalid process (system)        *)
        (*          = w_terming      process already terminating     *)
        (*                                                           *)
        (*************************************************************)

    const
      w_terming = 0;                      { warning: process already terminating }

    var
      int_level : intson_type;            { current interrupt level }
      pcb_ptr : ptr_PCB;                  { ptr to PCB for the process }
      cause : term_types;                 { reason for process termination }

    begin
      (*$IFC debug3 *)
      if Trace (pm, 90) then
        writeln ('   Entering Kill_Process...');
      (*$ENDC *)

      Find_PCB (errnum, Sys_Called, proc_id, pcb_ptr);

      if errnum = 0 then
        begin  { proc_id is for a valid process }
          Intsoff (allints, int_level);
          if pcb_ptr^.terming then
            begin  { happened to catch him terminating on his own }
              Intson (int_level);
              errnum := -(proc_errbase + eb_kill + w_terming)
            end
          else
            begin  { must send him a 'die' soft interrupt }
              Intson (int_level);

              if ord(pcb_ptr) <> c_pcb_ptr then
                cause := Killed
              else
                cause := self_Killed;

              AbortProcess (pcb_ptr, true, cause)
            end
        end { valid proc_id }
    end { Kill_Process };

ÿ