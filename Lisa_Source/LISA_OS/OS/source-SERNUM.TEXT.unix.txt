;  SERIAL NUMBER READER
;
;       Copyright 1983, 1984, Apple Computer Inc.

;  Initially written by Ken Schmahl and Susanna Lam.  Modified to be called
;  from Pascal by Dave Offen.  Susanna has more documentation on why this
;  magic seems to work.


;----------------------------------
;  local equates
;----------------------------------
RDLOOPCNT       .EQU    7               ; need to loop 7 times to get 56 words
                                        ; (8 words / read)
IOMMU           .EQU    $FC0000
STATUS          .EQU    $0F801+IOMMU        ; status register address (I/O seg 126)
SNUM            .EQU    $0FE8000        ; location for serial # SN1 & SN2
                                        ; (special I/O seg 127)
VRIRENB         .EQU    $0E01A+IOMMU        ; vertical retrace interrupt bit set
VRIRDIS         .EQU    $0E018+IOMMU        ; vert. retrace intrerupt bit reset
TKILLER         .EQU    172             ; constant for time killing loop
DELAY           .EQU    9               ; delay loop constant
HALFSIZE        .EQU    112             ; half size of scratch array (scratch
                                        ; array is used to store the data read
                                        ; form video display.  Bit 15 of each
                                        ; word is the bit that makes up the
                                        ; SN1 & SN2)
                                        ; The SN1 & SN2 are made up of 28
                                        ; nibbles, i.e. 112 bits.  Each bit is
                                        ; embedded in a word.  Therefore total
                                        ; size of the scratch array is 112 words
                                        ; (224 bytes).


        .PROC   SERNUM

        ;PROCEDURE SERNUM (VAR ERRNUM: INTEGER; VAR SER: LONGINT);
;------------------------------------------------------------------------------------
;    INTERRUPTS MUST BE OFF!!
;
;    SERMAIN ROUTINE VARIABLE USEAGE:
;
;  [xx]=global  (xx)=input  <xx>=output  xx=local or unused
;
;   A0 = scratch (status reg address)   D0 = scratch (time killer constant)
;  <A1>= location in MMU of SN1, SN2    D1 = scratch (vert. retrace bit in SR)
;        (used in READBLOCK)
;  <A2>= pointer to scratch array for   D2 = scratch (vert. retrace interrupt
;        words read back from MMU            timeout counter)
;        (video display data)
;        (used in READBLOCK)
;  <A3>= pointer to nibble buffer       D3 =
;        Nibble buffer is starts from
;        the end of the scratch array
;        each nibble is stored in a
;        byte
;        (used in GETNIBBLES)
;  <A7>= counter to count for blocks    D4 =
;        of data read (8 words per
;        read --- read 7 times to get
;        56 words)
;        (used in READBLOCK)
;  <A5>= ending address of the half     D5 =
;        of the scratch array that is
;        being worked on
;        (used in FINDSYNC, GETNIBBLES)
;  <A6>= beginning address of the      [D6]= step number
;        half of the scratch array
;        that is being worked on
;        (used in FINDSYNC, GETNIBBLES)
;  <A4>= link register                 [D7]= error code
;
;-----------------------------------------------------------------------------------
;
SERMAIN:
        MOVEM.L D4-D7/A3/A5-A6,-(SP)
        LINK    A4,#-256        ; A7 points to 4-byte count followed by 126-wd buff
;  First on the stack is the 4-byte count, then the SCRATCH array that contains the
;  112 words read from video memory.  Then the nibbles of the serial #
;  (extracted from the words read) are stored as 28 bytes (one nibble / byte).

SMAIN1:
        MOVEA.L #SNUM,A1        ; loc in MMU of SN1, SN2
        LEA     4(A7),A2        ; a2 points to scratch array
        MOVE.L  #RDLOOPCNT,(A7) ; need to read 7 times to get 56 words (8 words/read)
SYNC:   MOVEQ   #$11,D6         ; set step # to $11
        MOVEQ   #2,D1           ; vertical retrace is bit 2 in SR
        MOVEA.L #STATUS,A0      ; check status register for vert. retrace
        CLR     D2              ; clear time out counter (just in case vert.
                                ; retrace interrupt does not work)
        CLR     VRIRDIS         ; clear vertical retrace bit
        CLR     VRIRENB         ; set vertical retrace interrupt
@1      BTST    D1,(A0)         ; wait till vertical retrace interrupt occurs
        BEQ.S   GETSN1          ; vert. retrace occurs, go get SN1
        ADDQ    #1,D2           ; waited too long for vert. retrace to occur yet?
        BNE.S   @1              ; no, keep waiting
        MOVEQ   #6,D7           ; signal no vert. retrace interrupt
        RTS                     ; give up!!
        ;
        ;  snyc up with vertical retrace, extract SN1 & SN2
        ;  first --- SN1:
        ;
GETSN1: MOVEM   (A1),D0-D7      ; read 8 words (from video memory)
        MOVEM   D0-D7,(A2)      ; store in scratch array
        ADDQ.L  #8,A2           ; update scratch array pointer
        ADDQ.L  #8,A2
        NOP
        NOP
        MOVEQ   #DELAY-1,D0     ; waste some time
        SUBQ.L  #1,(A7)         ; done all 7 times yet (7 * 8 = 56 words read) ?
@1      DBLE    D0,@1           ; waste time
        BGT.S   GETSN1          ; not done yet, read another block of data
;
        MOVE.L  #RDLOOPCNT,(A7) ; 56 words.  8 words per read)
        ;
        MOVE.W  #TKILLER-1,D0   ; kill time
@2      DBRA    D0,@2
        ;
        ;  read SN2 (another 56 words)
        ;
GETSN2: MOVEM   (A1),D0-D7      ; read 8 words (from video memory)
        MOVEM   D0-D7,(A2)      ; store in scratch array
        ADDQ.L  #8,A2           ; update scratch array pointer
        ADDQ.L  #8,A2
        NOP
        NOP
        MOVEQ   #DELAY-1,D0     ; waste some time
        SUBQ.L  #1,(A7)         ; done all 7 times yet (7 * 8 = 56 words read) ?
@1      DBLE    D0,@1           ; waste time
        BGT.S   GETSN2          ; not done yet, read another block of data
;
        MOVEQ   #$13,D6         ; restore step number (up a bit)
        MOVEQ   #0,D7           ; and error code (no error yet)
        CLR     VRIRDIS         ; no more vertical retrace interrupt (turn it off)
        ;
        ;  at this point A2 points to the end of the scratch array
        ;
        MOVEA.L A2,A3           ; A3 points to beginning address of storage area
                                ; for the nibbles to be extracted from data read
SN1:    LEA     4(A7),A6        ; A6 points to beginning of scratch array
        MOVEA.L A6,A5
        ADDA.L  #HALFSIZE,A5    ; a5 points to end of the first half of the scratch
                                ; array
        MOVEQ   #$14,D6         ; step # is $14 for finding first sync byte
        BSR     FINDSYNC        ; find sync byte in the first half of data read back
        TST     D7              ; sync byte found?
        BNE     SEREND          ; no, exit with error
        ;
        MOVEQ   #$15,D6         ; step # is $15 for getting the first 14 nibbles
        BSR     GETNIBBLES      ; get the first 14 nibbles from bit stream
        ;
        ;   done with SN1.  Extract the second 14 nibbles
        ;
SN2:    MOVEQ   #$16,D6         ; step # is $16 for finding second sync byte
        MOVEA.L A5,A6           ; A6 points at beginning of second half of scratch
                                ; buffer
        ADDA.L  #HALFSIZE,A5    ; A5 points to end of second half of scratch buffer
        BSR     FINDSYNC        ; find sync byte for SN2
        TST     D7              ; ok?
        BNE     SEREND          ; no --- exit with error
        ;
        MOVEQ   #$17,D6         ; step # is $17 for getting second half of serial
                                ; # (14 nibbles)
        BSR     GETNIBBLES      ; extract the second 14 nibbles
        MOVE    #$18,D6         ; step # is $18 for checking checksum
        LEA     4(A7),A0        ; point to scratch array
        BSR     CHKSUM          ; calculate checksums from data read and compare to
                                ; checksum embedded in serial number
        TST     D7              ; ok ?
        BNE.S   SEREND          ; no --- give up!!!
        ;
        ;  serial number read is ok
        ;
        LEA     4(A7),A0        ; get addr of scratch array start
        ADDA.L  #236,A0         ; get starting address of the 8-DIGIT serial #
                                ; (the SCRATCH array is 112 words long; the
                                ;  applenet serial number is 6 words into the
                                ;  serial number which follows the array)

        MOVEQ   #2,D3           ; convert 1st 3 digits from bcd to binary in D0
        MOVEQ   #0,D2
        MOVEQ   #0,D0
@8      MULU    #10,D0
        MOVE.B  (A0)+,D2
        ADD     D2,D0
        DBF     D3,@8
        MOVEQ   #4,D3           ; convert next 5 digits from bcd to binary in D1
        MOVEQ   #0,D1
@9      MULU    #10,D1
        MOVE.B  (A0)+,D2
        ADD     D2,D1
        DBF     D3,@9
        SWAP    D0              ; put binary 24-bit result in D0
        MOVE.W  D1,D0
SEREND  MOVE    D7,D1           ; put error number in D1
        UNLK    A4
        MOVEM.L (SP)+,D4-D7/A3/A5-A6
        MOVEM.L (SP)+,A0-A2     ; ret addr in A0, result addr in A1, errnum addr
                                ; in A2
        MOVE.L  D0,(A1)         ; return resulting binary applenet serial number
        MOVE    D1,(A2)         ; return error number
        JMP     (A0)            ; exit
        ;



;------------------------------------------------------------------------------------
;   FINDSYNC ROUTINE VARIABLE USEAGE:
;
;  [xx]=global  (xx)=input  <xx>=output  xx=local or unused
;
;   A0 =                                D0 = reg used in storing SN bit stream
;                                            to be identified if it is the sync
;                                            byte ($FF)
;   A1 =                                D1 = scratch
;   A2 =                                D2 = # of times the scratch array is
;                                            searched (at most twice)
;   A3 =                                D3 = scratch
;   A4 =                                D4 = flag returned from CYLBUF indicating
;                                            if the circular buffer has been
;                                            visited twice yet
;  (A5)= ending addr in scratch array  [D5]= test count (test to be run 20
;        in which to look for sync           times)
;        byte
;  (A6)= beginning location in scratch [D6]= step number
;        array from which to look for
;        sync byte (upon exit from
;        FINDSYNC, A6 points to word
;        that follows the sync byte)
;   A7 =                               [D7]= error code
;
;   Find the sync byte that is embedded in the video data read (bit 15 of each
;   word).  Half of the data read (vertical / horizontal half) is treated each
;   time.  If the sync byte is found, then A6 points to the word in scratch
;   array that contains the first serial # bit following the sync byte.
;   Otherwise, D7 is set to $4 to indicate error.
;-----------------------------------------------------------------------------------
FINDSYNC:
        MOVEQ   #2,D2           ; look at scratch array at most twice (circular array)
        CLR     D0              ; put data into D0 for identifying if sync byte found
@4      MOVE    (A6)+,D3        ; find zero bit before the sync byte
        LSL     #1,D3
        BCC.S   @2              ; found it!  proceed to get SN1
        BSR     CYLBUF          ; ok to go on?
        TST     D4
        BNE.S   @4              ; yes, look at next buffer element
        BRA.S   SYNCERR         ; no, report error


@2      MOVE    (A6)+,D1
        LSL     #1,D1           ; move desired bit (bit 15) into D0
        ROXL    #1,D0
        ;
        CMP.B   #$FF,D0         ; sync byte found?
        BEQ.S   @1              ; yes, done
        BSR     CYLBUF          ; ok to go on?
        TST     D4
        BNE.S   @2              ; yes, loop at next element
        BRA     SYNCERR

@1      RTS                     ; return after sync byte is found


;
;  CYLBUF is used to determine if the scratch array has been traversed twice.
;  If yes, then the effort to find the sync byte was fruitless.
;  D4 is used as a flag to communicate to the outside world if there is no hope
;  to find the sync byte.
;    D4 = 1 -> ok to continue
;       = 0 -> scratch array traversed twice
;
CYLBUF:
        CMPA.L  A6,A5           ; at end of buffer yet?
        BHI.S   @2              ; no, ok to continue
        SUBQ    #1,D2           ; yes, go back to beginning of buffer?
        BEQ.S   @3              ; cannot find synd byte, ERROR
        SUBA.L  #HALFSIZE,A6    ; reset A6 to beginning of scratch buffer
@2      MOVEQ   #1,D4           ; indicate ok to continue
        RTS

@3      CLR     D4              ; indicate buffer have been looked at twice and
        RTS                     ; it's time to quit
        ;
SYNCERR:
        MOVE    #$4,D7          ; set error code
        RTS

;------------------------------------------------------------------------------;
;   GETNIBBLES ROUTINE VARIABLE USEAGE:
;
;  [xx]=global  (xx)=input  <xx>=output  xx=local or unused
;
;   A0 =                                D0 = temp nibble reg
;   A1 =                                D1 = scratch
;   A2 =                                D2 = # of nibbles to extract (12)
;  (A3)= pointer to nibble buffer       D3 = 4 bits / nibble
;        (each nibble is stored as a
;        byte)
;   A4 =                                D4 =
;  (A5)= points to end of the half of   D5 =
;        the scratch array from which
;        the nibbles are to be
;        extracted
;  (A6)= points to location following  [D6]= step number
;        sync byte in scratch array
;   A7 =                               [D7]= error code
;
;   After the sync byte is found, nibbles are extracted from the words read
;   and stored in the nibble buffer, which follows the scratch buffer in memory.
;   Each nibble is stored in the form of a byte.  Half of the scratch array is
;   treated in this routine to get the vertical/horizontal half of the serial #.
;   The scratch array is treated as a circular buffer because the sync byte
;   may be read in over the physical boundary of the buffer.
;-----------------------------------------------------------------------------------

GETNIBBLES:
        ;
        ;   12 more nibbles to get
        ;
        MOVE    #12,D2
@3      MOVEQ   #4,D3           ; 4 bits/nibble
        CLR     D0              ; clear temp reg for nibble
@2      CMPA.L  A6,A5           ; at end of scratch buffer yet?
        BHI.S   @1              ; no, keep going
        SUB     #HALFSIZE,A6    ; yes, restore pointer to beginning of scratch buffer
@1      MOVE    (A6)+,D1        ; get word from buffer
        LSL     #1,D1           ; get bit 15 into extend bit
        ROXL    #1,D0           ; get bit 15 (part of nibble) into temp nibble reg
        SUBQ    #1,D3           ; 4 bits yet?
        BNE.S   @2              ; no, get next bit
        MOVE.B  D0,(A3)+        ; save nibble got into nibble buffer
        SUBQ    #1,D2           ; 12 nibbles got?
        BNE.S   @3              ; no, get next one
        RTS


;------------------------------------------------------------------------------------
;   CHKSUM ROUTINE VARIABLE USEAGE:
;
;  [xx]=global  (xx)=input  <xx>=output  xx=local or unused
;
;   A0 = pointer to nibble buffer       D0 = reg for checksum calculated from
;                                            info embedded in the serial # byte
;                                            stream (checksum in bytes 20,
;                                            21 and 22)
;   A1 =                                D1 = checksum calculated by adding
;                                            nibbles 0 - 19
;   A2 =                                D2 = scratch (nibble count)
;   A3 =                                D3 = scratch
;   A4 =                                D4 =
;   A5 =                                D5 =
;   A6 =                               [D6]= step number
;   A7 =                               [D7]= error code
;
;   In the serial # bit stream, the calculated checksum of the stream is
;   embedded in bits 20 - 22.  To verify that the correct serial # is read,
;   this checksum is compared to that calculated by adding nibbles 0 - 19
;   together.  If the 2 checksums do not match, the error code D7 is set to
;   $5.
;------------------------------------------------------------------------------;
CHKSUM:
        ADDA.L  #HALFSIZE,A0    ; get pointer to nibble buffer
        ADDA.L  #HALFSIZE,A0
        ;
        CLR     D0
        MOVE.B  20(A0),D0       ; most sig BCD nibble of checksum (bit #20)
        MOVE    #100,D2
        MULU    D2,D0
        ;
        CLR     D3
        MOVE.B  21(A0),D3      ; second BCD nibble of checksum (bit 21)
        MOVE    #10,D2
        MULU    D2,D3
        ADD     D3,D0
        ;
        CLR     D3
        MOVE.B  22(A0),D3       ; last BCD checksum digit in bit 22
        ADD     D3,D0           ; got checksum embedded in SN1 & SN2
        ;
        MOVEA.L A0,A1
        MOVE    #20,D2          ; calculate check sum from bytes 0 to 19
        CLR     D1
@1      MOVE.B  (A1)+,D3
        ADD     D3,D1
        SUBQ    #1,D2           ; 20 nibbles added?
        BNE     @1
        ;
        MOVE.B  23(A0),D3       ; this nibble should be zero
        ADD     D3,D1
        CMP     D1,D0           ; checksums match?
        BEQ.S   @2              ; ok
        MOVE    #$5,D7          ; set error
@2      RTS

        .END

ÿ