;       Copyright 1983, 1984, Apple Computer, Inc.
;
;       PRIAMASM:  Priam Driver Assembly Language
;
;       By Wendell Henry 1/83
;
           .PROC       PRIAMROUTINE

           .REF        OKXFERNEXT

           .DEF        PRIAMASM
           ;---------------------------------------------------------------
           ;
           ;
           ;
           ;
           ;
           ;
           ;---------------------------------------------------------------
;
;    (* high level commands                              *)
;    initdev   = 0;         (* initialize drive          *)
;    interrupt = 2;         (* process interrupt         *)
;    read      = 4;         (* read data - retry enabled *)
;    write     = 6;         (* write data - retry enabled*)
;    format    = 8;         (* format disk               *)
;    downdisk  = 10;        (* sequence down drive       *)
;
;
;    Misc Constants
TIMELIMIT  .EQU        300000   ;Gives timeout of approximately 3 seconds
MAXTRIES   .EQU        4        ;Max times a command will be tried
ECC_ER     .EQU        $11      ;ECC error gets mapped to 617
DEVTIMEOUT .EQU        $33      ;Error from drive for device timeout
;
;
;    Response Values
OK         .EQU        0        ;Good response - no errors - command done
WAITINT    .EQU        1        ;Good response - no errors - wait for interrupt
INITERR    .EQU        1840     ;Errors during initialization of drive
CRCERR     .EQU        617      ;CRC (actually ECC) error
FORMATERR  .EQU        618      ;Error formatting device
TIMEOUT    .EQU        666      ;Timeout error
WRITERR    .EQU        1841     ;Error writing to disk
READERR    .EQU        1842     ;Error reading from disk
;
;INTERLEAVE FACTORS
E_FACTOR   .EQU        3                   ;For E Controller
T_FACTOR   .EQU        1                   ;For T Controller
;
;
;DRIVECB OFFSETS
CONFIGADDR .EQU        0                   ;Configinfo entry address
EXT_PTR    .EQU        CONFIGADDR+4        ;Configinfo index
RAW_DATA   .EQU        EXT_PTR+4           ;Ptr to raw data buffer
RAW_HEADER .EQU        RAW_DATA+4          ;Ptr to raw header buffer
X_LENG     .EQU        RAW_HEADER+4        ;Offset from raw_data to data buffer
SECT_LEFT  .EQU        X_LENG+4            ;The total sectors required
V_FLAG     .EQU        SECT_LEFT+2         ;True if all writes should be verified
;
;
;EXT_DRIVE_CB OFFSETS
HWBASE     .EQU        0                   ;Device high select iospace address
HWPARITY   .EQU        HWBASE+4            ;Device address for parity FF
DEVICE     .EQU        HWPARITY+4          ;Device number
COMMAND    .EQU        DEVICE+1            ;High level command being issued
RESPONSE   .EQU        COMMAND+1           ;Response to high level command
INPUT      .EQU        RESPONSE+2          ;Input parms to drive
OUTPUT     .EQU        INPUT+6             ;Output parms from drive
PE         .EQU        OUTPUT+6            ;Parity error flag
STATE      .EQU        PE+1                ;Current state of driver
RETRIES    .EQU        STATE+1             ;# retries this request
TOT_RETRIES .EQU       RETRIES+2           ;Total # retries this drive
ERROR      .EQU        TOT_RETRIES+2       ;Error from sector transfer
NXFERCNT   .EQU        ERROR+2             ;Next block xfer count
CXFERCNT   .EQU        NXFERCNT+2          ;Current block xfer count
CTRLTYPE   .EQU        CXFERCNT+2          ;Controller Type: 2=E; 3=T

PACKETLEN  .EQU        CTRLTYPE+1          ;Number of valid bytes in packet
PACKET     .EQU        PACKETLEN+1         ;Packet command

PID        .EQU        PACKET+16           ;Packet id
PSTATE     .EQU        PID+1               ;Packet state
PTDF       .EQU        PSTATE+1            ;Termination device flag
PPRISTAT   .EQU        PTDF+1              ;Primary termination status
PSECSTAT   .EQU        PPRISTAT+1          ;Secondary termination status
PRES1      .EQU        PSECSTAT+1          ;Unused
POFFSET    .EQU        PRES1+1             ;Packet offset
PLENGTH    .EQU        POFFSET+2           ;Packet length
PSTEPCODE  .EQU        PLENGTH+2           ;Current step operation code
PSTEPNUMB  .EQU        PSTEPCODE+1         ;Current step number
PSECTNUMB  .EQU        PSTEPNUMB+1         ;Total number of records copied
PSDEV      .EQU        PSECTNUMB+4         ;Source device select
PSSTAT     .EQU        PSDEV+1             ;Source device status
PSSUPSTAT  .EQU        PSSTAT+1            ;Supplemental source status
PSADDR     .EQU        PSSUPSTAT+1         ;Source transfer address
PISECTSTEP .EQU        PSADDR+3            ;Number of records input this copy step
PDDEV      .EQU        PISECTSTEP+4        ;Destination device select
PDSTAT     .EQU        PDDEV+1             ;Destination device status
PDSUPSTAT  .EQU        PDSTAT+1            ;Supplemental destination status
PDADDR     .EQU        PDSUPSTAT+1         ;Destination transfer address
POSECTSTEP .EQU        PDADDR+3            ;Number of records output this copy step
RTN_PSTAT  .EQU        POSECTSTEP+4        ;Return address for READ_PSTATUS

PSECTOR    .EQU        INPUT               ;Sector to be transferred
PSTATUS    .EQU        PID                 ;Packet status report
;
;
;INPUT PARAMETER OFFSETS
PARM0      .EQU        INPUT
PARM1      .EQU        INPUT+1
PARM2      .EQU        INPUT+2
PARM3      .EQU        INPUT+3
PARM4      .EQU        INPUT+4
PARM5      .EQU        INPUT+5
;
;
;OUTPUT RESULT OFFSETS
RESULT0    .EQU        OUTPUT
RESULT1    .EQU        OUTPUT+1
RESULT2    .EQU        OUTPUT+2
RESULT3    .EQU        OUTPUT+3
RESULT4    .EQU        OUTPUT+4
RESULT5    .EQU        OUTPUT+5
;
;
;IOSPACE REGISTER OFFSETS
STATUS     .EQU        0                 ;Interface Status Register
CMDREG     .EQU        0                 ;Command Register
DATA       .EQU        2                 ;Read/Write Disk Data Register
P0         .EQU        4                 ;Parameter 0
P1         .EQU        6
P2         .EQU        8
P3         .EQU       10
P4         .EQU       12
P5         .EQU       14                 ;Parameter 5
R0         .EQU        4                 ;Result 0 - Transaction Status Register
R1         .EQU        6
R2         .EQU        8
R3         .EQU       10
R4         .EQU       12
R5         .EQU       14                 ;Result 5
;
;
;INTERFACE STATUS REGISTER BITS
CMD_REJECT .EQU        7                 ;Command reject
CMD_DONE   .EQU        6                 ;Command Completion Request
CMD_SDONE  .EQU        5                 ;Special Command Completion
BTR_INT    .EQU        4                 ;Block transfer - interrupt
ISR_BUSY   .EQU        3                 ;Interface Busy
DTREQ      .EQU        2                 ;Data Transfer Request
RW_REQ     .EQU        1                 ;Read/Write Request
DBUS_ENA   .EQU        0                 ;Data Bus Enabled
;
;
;INTERFACE CONTROL BITS
N          .EQU     $200                 ;No device cycle
I          .EQU     $100                 ;Interrupt enable
B          .EQU      $80                 ;Byte mode
W          .EQU      $40                 ;Waiting
P          .EQU      $20                 ;Priam Parity cheking circuitry enabled
BW         .EQU      $C0                 ;Byte mode/waiting
BI         .EQU     $180                 ;Byte mode/interrupt enable
BWI        .EQU     $1C0                 ;Byte mode/waiting/interrupt enable
;
;
;          Driver states
IDLING     .EQU        0        ;Driver idle - no command in progress
READING    .EQU        2        ;Driver reading device - waiting for interrupt
WRITING    .EQU        4        ;Driver writing to device - waiting for interrupt
INITING    .EQU        6        ;Driver initializing device - waiting for int
FORMATTING .EQU        8        ;Driver formatting device - waiting for int
ISSUE1     .EQU       10        ;Driver formatting T controller - transfer packet
ISSUE2     .EQU       12        ;Driver formatting T Controller - end packet
ISSUE3     .EQU       14        ;Driver formatting T controller - read status
ISSUE4     .EQU       16        ;Driver formatting T controller - format complete
;
;
;
;
ACKDT      ;Acknowledge Data Transfer
           ;Note: wait is always in byte mode device interrupts disabled
           MOVE.L      HWBASE(A3),A2
           ADDA        #BW,A2
           MOVE.B      #1,CMDREG(A2)            ;Acknowledge transfer of data
           MOVE.L      #TIMELIMIT,D0            ;Timeout value
@1         BTST        #BTR_INT,STATUS(A2)      ;Wait for int flag to drop out
           BEQ.S       @10                      ;Skip if gone
           SUBQ.L      #1,D0                    ;Loop till gone or timeout
           BGT.S       @1
           MOVE        #TIMEOUT,D0              ;Timeout return
           RTS
@10        ADDA        #I,A2                    ;setup for next interrupt
           BTST        #BTR_INT,STATUS(A2)      ;
           MOVEQ       #0,D0                    ;OK return
           RTS
;
;
WAITRF     ;Wait for register file not busy
           ;Note: wait is always in byte mode device interrupts disabled
           MOVE.L      #TIMELIMIT,D0            ;Timeout value
@1         BTST        #ISR_BUSY,STATUS(A2)     ;Test Register file busy
           BEQ.S       @10                      ;Skip if free
           SUBQ.L      #1,D0                    ;Loop till free or timeout
           BGT.S       @1
           MOVE        #TIMEOUT,D0              ;Timeout return
           RTS
@10        MOVEQ       #0,D0                    ;OK return
           RTS
;
;
;

ACKCC     ;Acknowledge command completion
           ;Note: wait is always in byte mode device interrupts disabled
           ;      after command ack device is left with interrupts disabled
           ;Output= Result parms from command
           ;      = Low word of D0 contains error completion type
           ;D0 Destroyed
           MOVE.L      #TIMELIMIT,D0            ;Timeout value
@1         BTST        #CMD_DONE,STATUS(A2)     ;Test command complete
           BNE.S       @10                      ;Skip if done
           SUBQ.L      #1,D0                    ;Loop till free or timeout
           BGT.S       @1
           MOVE        #TIMEOUT,D0              ;Timeout return
           RTS
@10        MOVE.B      R0(A2),RESULT0(A3)       ;Transaction Status
           MOVE.B      R1(A2),RESULT1(A3)       ;Other output parameters
           MOVE.B      R2(A2),RESULT2(A3)
           MOVE.B      R3(A2),RESULT3(A3)
           MOVE.B      R4(A2),RESULT4(A3)
           MOVE.B      R5(A2),RESULT5(A3)
           CLR.B       CMDREG(A2)               ;Send Command Acknowledge
           MOVE.L      #TIMELIMIT,D0            ;Timeout value
@2         BTST        #CMD_DONE,STATUS(A2)     ;Test Command complete
           BEQ.S       @20                      ;Skip if done
           SUBQ.L      #1,D0                    ;Loop till free or timeout
           BGT.S       @2
           MOVE        #TIMEOUT,D0              ;Timeout return
           RTS
@20        MOVE.B      RESULT0(A3),D0           ;Get transaction status
           ANDI.W      #$30,D0                  ;Extract error completion type
           RTS
;
;
PRIAMASM
           MOVEM.L     D2-D5/A2-A4,-(SP)        ;Save Pascal Registers
           MOVE.L      32(SP),A4                ;Drive control block in A4
           MOVE.L      EXT_PTR(A4),A3           ;Drivecb extension in A3
           MOVEA.L     HWBASE(A3),A2            ;IOspace address in A2 with
           ADDA        #BW,A2                   ; byte mode - waiting
           MOVE.B      COMMAND(A3),D0           ;Get command
           EXT.W       D0
           MOVE.W      CMD_TABLE(D0),D0
           JMP         CMD_TABLE(D0)            ;Go process command
;
CMD_TABLE
           .WORD       INITDEV-CMD_TABLE        ;Initialize disk
           .WORD       INTERRUPT-CMD_TABLE      ;Process interrupt
           .WORD       READ-CMD_TABLE           ;Read
           .WORD       WRITE-CMD_TABLE          ;Write
           .WORD       FORMAT-CMD_TABLE         ;Format disk
           .WORD       DOWNDEV-CMD_TABLE        ;Sequence down drive
;
RETURN     MOVEM.L     (SP)+,D2-D5/A2-A4        ;Restore Registers
           MOVE.L      (SP)+,A0                 ;Return Address
           ADDQ        #4,SP                    ;Pop input parameter
           JMP         (A0)                     ;Return
;
;
;
INTERRUPT  ; INTERRUPT FROM DISK
           MOVE.B      STATE(A3),D0             ;Get driver state
           EXT.W       D0
           MOVE.W      STATE_TABLE(D0),D0
           JMP         STATE_TABLE(D0)          ;Go process interrupt
;
;
STATE_TABLE
           .WORD       IDLE-STATE_TABLE         ;Unexpected interrupt
           .WORD       READ_INT-STATE_TABLE     ;Interrupt during read
           .WORD       WRITE_INT-STATE_TABLE    ;Interrupt during write
           .WORD       INIT_INT-STATE_TABLE     ;Interrupt during initialize
           .WORD       FORMAT_INT-STATE_TABLE   ;Interrupt during format
           .WORD       ISSUE1_INT-STATE_TABLE   ;Interrupt after issue packet
           .WORD       ISSUE2_INT-STATE_TABLE   ;Interrupt after packet complete
           .WORD       ISSUE3_INT-STATE_TABLE   ;Interrupt to read packet status
           .WORD       ISSUE4_INT-STATE_TABLE   ;Interrupt after read status
;
;
;
IDLE       ; Interrupt while in idle state

           CLR.W       RESPONSE(A3)             ;Return error response
           MOVE.B      #IDLING,STATE(A3)        ;No command in progress
           JMP         RETURN                   ;For now ignore interrupt
;
;
;
INITDEV    ; INITIALIZE DEVICE
           BSR         WAITRF                   ;Wait for free register file
           TST.W       D0
           BNE         ERRINITDEV               ;Error exit
           CLR.B       P0(A2)                   ;Device # Smart E,unused Smart T
           MOVE.B      #9,CMDREG(A2)            ;Read Mode
           BSR         ACKCC                    ;Wait to Command Acknowledge
           TST.W       D0
           BNE         ERRINITDEV               ;Error exit
           MOVE.B      RESULT3(A3),CTRLTYPE(A3) ;Save controller type
           BSR         WAITRF                   ;Wait for free register file
           TST.W       D0
           BNE         ERRINITDEV               ;Error exit
           MOVE.B      DEVICE(A3),P0(A2)        ;Read drive parameters
           MOVE        SR,D0                    ;See if we're booting (pri=7) or not
           ANDI        #$0700,D0
           CMPI        #$0700,D0
           BEQ.S       BOOTING                  ;Skip if booting
           MOVE.L      HWBASE(A3),A2
           ADDA        #BI,A2
           MOVE.B      #$85,CMDREG(A2)          ;Issue command
           MOVE.W      #WAITINT,RESPONSE(A3)    ;Wait for Sequence up interrupt
           MOVE.B      #INITING,STATE(A3)       ;Set driver state
           JMP         RETURN

BOOTING    MOVE.B      #$85,CMDREG(A2)          ;Issue command & fall thru
INIT_INT   ;Interrupt while initializing device
           BSR         ACKCC                    ;Wait to command acknowledge
           TST.B       D0                       ;Test error completion type
           BEQ         OKINITDEV                ;Good response
ERRINITDEV MOVE.W      #INITERR,D0              ;Unable to init device
OKINITDEV  MOVE.W      D0,RESPONSE(A3)          ;Return error response
           MOVE.B      #IDLING,STATE(A3)        ;No command in progress
           JMP         RETURN                   ;Exit INITDEV
;
;
;
DOWNDEV    ; Sequence down disk
           BSR         WAITRF                   ;Wait for free register file
           TST.W       D0
           BNE         @1                       ;Error exit - don't care
           MOVE.B      DEVICE(A3),P0(A2)        ;Sequence device down
           MOVE.B      #$81,CMDREG(A2)          ;Issue command
           BSR         ACKCC                    ;Wait to command acknowledge
@1         CLR.W       RESPONSE(A3)             ;Return ok response
           MOVE.B      #IDLING,STATE(A3)        ;No command in progress
           JMP         RETURN                   ;Exit DOWNDEV
;
;
;
FORMAT     ; FORMAT DISK
           BSR         WAITRF                   ;Wait for free register file
           TST.W       D0
           BNE         ERRFORMAT                ;Error exit
           MOVE.B      DEVICE(A3),P0(A2)        ;Set Device
           CMPI.B      #2,CTRLTYPE(A3)          ;Type of Controller?
           BEQ         SMARTE_FMT               ;Skip if formatting Smart E
           ;Smart T Controller
           MOVE.B      #12,PACKETLEN(A3)        ;Packet length
           MOVE.B      #2,PACKET(A3)            ;Command
           MOVE.B      DEVICE(A3),PACKET+1(A3)  ;Device select
           MOVE.B      #$00,PACKET+2(A3)        ;FBD/media
                                                ;write nulls in every sector
           MOVE.B      #0,PACKET+3(A3)          ;fill byte
           MOVE.W      #536,PACKET+4(A3)        ;Sector size
           MOVE.B      #0,PACKET+6(A3)          ;DMD/#spares each track
           MOVE.B      #10,PACKET+7(A3)         ;# alt cyl
           MOVE.B      #0,PACKET+8(A3)          ;Cyl interleave factor
           MOVE.B      #1,PACKET+9(A3)          ;Head interleave factor
           MOVE.B      #1,PACKET+10(A3)         ;Sector interleave factor
           MOVE.B      #0,PACKET+11(A3)         ;Sector interleave table length
           ;Issue transfer packet command
           CLR.W       RETRIES(A3)
           CLR.W       ERROR(A3)
           MOVE.B      #0,P0(A2)                ;Packet ID
           MOVE.B      #0,P2(A2)
           MOVE.B      PACKETLEN(A3),P3(A2)     ;Packet length
           MOVE.L      HWBASE(A3),A2
           ADDA        #BI,A2
           MOVE.B      #$B0,CMDREG(A2)          ;Issue Transfer Packet command
           MOVE.W      #WAITINT,RESPONSE(A3)    ;Wait for interrupt
           MOVE.B      #ISSUE1,STATE(A3)
           BRA         RETURN
;
;
ISSUE1_INT ;Interrupt to transfer packet
           BTST        #DTREQ,STATUS(A2)        ;Waiting for data?
           BEQ         ISSUE4_INT               ;Skip if no
           LEA         PACKET(A3),A0            ;Packet address
           MOVE.B      PACKETLEN(A3),D0         ;Packet length
           EXT.W       D0
           SUBQ        #1,D0                    ;Length - 1
@10        MOVE.B      (A0)+,DATA(A2)           ;Write packet
           DBF         D0,@10
           BSR         ACKDT                    ;Acknowledge transfer of packet
           MOVE.W      #WAITINT,RESPONSE(A3)    ;Wait for interrupt
           MOVE.B      #ISSUE2,STATE(A3)
           BRA         RETURN
;
;
ISSUE2_INT ;Interrupt after the packet has been completed
           BSR         ACKCC                    ;Acknowledge command complete
           ;Read packet status
           BSR         WAITRF                   ;Wait for free register file
           MOVE.B      #0,P0(A2)                ;Packet ID
           MOVE.L      HWBASE(A3),A2
           ADDA        #BI,A2
           MOVE.B      #$B8,CMDREG(A2)          ;Issue Read Packet Status command
           MOVE.W      #WAITINT,RESPONSE(A3)    ;Wait for interrupt
           MOVE.B      #ISSUE3,STATE(A3)
           BRA         RETURN
;
;
ISSUE3_INT ; Interrupt when status can be read
           BTST        #DTREQ,STATUS(A2)        ;Waiting for data?
           BEQ         ISSUE4_INT               ;Skip if no
           LEA         PSTATUS(A3),A0
           MOVE.W      #35,D0                   ;Status = 36 bytes
@10        MOVE.B      DATA(A2),(A0)+           ;Read packet status
           DBF         D0,@10
           BSR         ACKDT                    ;Acknowledge transfer of status
           MOVE.W      #WAITINT,RESPONSE(A3)    ;Wait for interrupt
           MOVE.B      #ISSUE4,STATE(A3)
           BRA         RETURN
;
;
ISSUE4_INT ;Interrupt after read packet status
           BSR         ACKCC                    ;Acknowledge command complete
           ;Check state of packet
           CMPI.B      #$0D,PSTATE(A3)          ;Packet completed successfully?
           BNE.S       ERRFORMAT
           TST.B       PPRISTAT(A3)             ;Primary status = 0?
           BNE.S       ERRFORMAT
           MOVE.W      #0,D0
           BRA         OKFORMAT

           ;Smart E Controller
SMARTE_FMT MOVE.B      #E_FACTOR,P3(A2)         ;Interleave for E controller
           MOVE.L      HWBASE(A3),A2
           ADDA        #BI,A2
           MOVE.B      #$A8,CMDREG(A2)          ;Issue command
           MOVE.W      #WAITINT,RESPONSE(A3)    ;Wait for FORMAT interrupt
           MOVE.B      #FORMATTING,STATE(A3)    ;Set driver state
           JMP         RETURN
FORMAT_INT ;Interrupt while formatting device
           BSR         ACKCC                    ;Wait to command acknowledge
           TST.B       D0                       ;Test error completion type
           BEQ         OKFORMAT                 ;Good response
ERRFORMAT  MOVE.W      #FORMATERR,D0            ;Unable to format device
OKFORMAT   MOVE.W      D0,RESPONSE(A3)          ;Return error response
           MOVE.B      #IDLING,STATE(A3)        ;No command in progress
           JMP         RETURN                   ;Exit FORMAT
;
;
;  Setup controller for data transfer
XFER_SETUP MOVE.B      DEVICE(A3),P0(A2)        ;Device
           MOVE.B      PSECTOR+1(A3),P1(A2)     ;Sector - byte 2
           MOVE.B      PSECTOR+2(A3),P2(A2)     ;Sector - byte 1
           MOVE.B      PSECTOR+3(A3),P3(A2)     ;Sector - byte 0
           MOVE.W      NXFERCNT(A3),D0
           CMPI.W      #127,D0                  ;Max sector count of 127
           BGT         @1
           MOVE.B      D0,P4(A2)                ;Sector count
           MOVE.W      D0,CXFERCNT(A3)          ;Current sector count
           CLR.W       NXFERCNT(A3)             ;Sectors left
           RTS
@1         MOVE.B      #127,P4(A2)              ;Transfer done in chunks
           MOVE.W      #127,CXFERCNT(A3)        ;Current sector count
           SUBI.W      #127,NXFERCNT(A3)        ;Sectors left
           RTS
;
;
;          Read sector - parity is checked
;          Destroys registers D0,D1,D5,A0,A1
;
READ_SECT  MOVE.L      RAW_DATA(A4),A0          ;Data Buffer address
           MOVE.L      RAW_HEADER(A4),A1        ;Header Buffer address
           ADD.L       X_LENG(A4),A0            ;Adjust data buffer address
           MOVE.L      A0,D1
@1         BTST        #DTREQ,STATUS(A2)        ;Wait for DTREQ before reading data
           BEQ         @1
           MOVE.L      A2,D5                    ;Save A2
           MOVE.L      HWBASE(A3),A2
           ADDA        #DATA,A2                 ;Add Data Register
           ADDA        #P,A2                    ;Add Parity Check Enable
           TST.W       (A2)                     ;Start read of first word
           TST.W       D0
           BEQ         @21                      ;Do not read header
           MOVEQ       #1,D0                    ;Read header in 2 passes (24 B)
@2         MOVE.W      (A2),(A1)+
           NOP
           MOVE.W      (A2),(A1)+
           NOP
           MOVE.W      (A2),(A1)+
           NOP
           MOVE.W      (A2),(A1)+
           NOP
           MOVE.W      (A2),(A1)+
           NOP
           MOVE.W      (A2),(A1)+
           DBF         D0,@2
@21        BTST        #0,D1                    ;Odd data address?
           BNE         @10                      ;Skip if yes
           MOVEQ       #62,D0                   ;Read data in 63 passes (504 B)
@3         MOVE.W      (A2),(A0)+
           NOP
           MOVE.W      (A2),(A0)+
           NOP
           MOVE.W      (A2),(A0)+
           NOP
           MOVE.W      (A2),(A0)+
           DBF         D0,@3
           MOVE.W      (A2),(A0)+               ; now read 8 bytes - 6 normally
           NOP
           MOVE.W      (A2),(A0)+
           NOP
           MOVE.W      (A2),(A0)+
           NOP
           MOVE.W      N(A2),(A0)+              ; last 2 avoiding a device cycle
@5         MOVE.L      HWPARITY(A3),A2          ;Location of parity FF
           MOVE.B      (A2),D0                  ;Get parity byte
           ANDI.B      #$80,D0                  ;Extract parity FF
           OR.B        D0,PE(A3)                ;Save
           MOVE.L      D5,A2                    ;Restore A2
           CMPI.B      #2,CTRLTYPE(A3)          ;Type E Controller?
           BEQ         @6
           MOVE.B      #1,CMDREG(A2)            ;Acknowledge block transfer
@6         RTS
           ;
           ;Read to odd address
@10        MOVE        #254,D0                  ;Read data in 255 passes (512bytes)
@11        MOVE.W      (A2),D1
           MOVE.W      D1,-(SP)
           MOVE.B      (SP)+,(A0)+
           MOVE.B      D1,(A0)+
           DBF         D0,@11
           MOVE.W      N(A2),D1                 ;Last 2 avoiding a device cycle
           MOVE.W      D1,-(SP)
           MOVE.B      (SP)+,(A0)+
           MOVE.B      D1,(A0)+
           BRA.S       @5
;
;
;
READ       ; Start read from disk
           CLR.W       RETRIES(A3)              ;Clear retry count
           CLR.W       ERROR(A3)                ;Clear error
READ_AGAIN ; Restart read after timeout
           MOVE.W      SECT_LEFT(A4),NXFERCNT(A3);Get # blocks to be transferred
READ_MORE  ; Read next multi-block chunk of sectors
           BSR         WAITRF                   ;Wait for free register file
           BSR         XFER_SETUP               ;Setup Parameters and data ptrs
           CLR.B       PE(A3)                   ;Clear Parity Error Flag
           MOVE.L      HWBASE(A3),A2
           ADDA        #BI,A2                   ;Set byte mode/Interrupts enabled
           MOVE.B      #$53,CMDREG(A2)          ;Issue read
READ_WAIT  MOVE.W      #WAITINT,RESPONSE(A3)    ;Wait for DTREQ interrupt
           MOVE.B      #READING,STATE(A3)       ;Set driver state
           JMP         RETURN
;
;
READ_INT   ;Interrupt while reading
           BTST        #CMD_DONE,STATUS(A2)     ;Command Complete?
           BNE         READ_DONE
           TST.W       CXFERCNT(A3)             ;Block to xfer?
           BEQ         READ_DONE
           SUBQ.W      #1,CXFERCNT(A3)          ;Decrement block count left
           TST.W       ERROR(A3)                ;Previous error?
           BNE         READ_FLUSH               ;Flush sector from controller
           MOVEQ       #1,D0                    ;Read header and data
           BSR         READ_SECT                ;Continue read after interrupt
           ADDQ.L      #1,PSECTOR(A3)           ;Sector for next transfer
           MOVE.L      A2,D5                    ;Save A2
           CLR.W       -(SP)
           MOVE.L      A4,-(SP)
           JSR         OKXFERNEXT               ;error:=OKXFERNEXT(drivecb_ptr)
           MOVE.W      (SP)+,ERROR(A3);
           MOVE.L      D5,A2                    ;Restore A2
           ADDA        #I,A2                    ;Setup for next interrupt
           MOVE.B      STATUS(A2),D0
           BRA         READ_WAIT                ;Wait for interrupt
;
;
;          After error flush the controller with dummy read to complete
;          the current request
READ_FLUSH
@1         BTST        #DTREQ,STATUS(A2)        ;Wait for DTREQ before reading data
           BEQ         @1
           MOVE.L      HWBASE(A3),A1
           ADDA        #DATA,A1                 ;Add Data Register
           ADDA        #P,A1                    ;Add Parity Check Enable
           TST.W       (A1)                     ;Start read of first word
           MOVEQ       #5,D0                    ;Read header in 6 passes (24 B)
@2         MOVE.W      (A1),D1
           NOP
           MOVE.W      (A1),D1
           DBF         D0,@2
           MOVEQ       #126,D0                   ;Read data in 127 passes (508 B)
@3         MOVE.W      (A1),D1
           NOP
           MOVE.W      (A1),D1
           DBF         D0,@3
           MOVE.W      (A1),D1                  ; 2 more normally
           NOP
           MOVE.W      N(A1),D1                 ; last 2 avoiding a device cycle
           CMPI.B      #2,CTRLTYPE(A3)          ;Type E Controller?
           BEQ         @5
           MOVE.B      #1,CMDREG(A2)            ;Acknowledge block transfer
@5         ADDA        #I,A2                    ;Setup for next interrupt
           MOVE.B      STATUS(A2),D0
           BRA         READ_WAIT                ;Wait for interrupt
;
READ_DONE  BSR         ACKCC                    ;Acknowledge cmd complete
           TST.B       D0                       ;Test error completion type
           BNE         @10                      ;Jump if error
           TST.W       NXFERCNT(A3)             ;More blocks to transfer?
           BNE         READ_MORE
@1         MOVE.W      D0,RESPONSE(A3)          ;Return error response
           MOVE.B      #IDLING,STATE(A3)        ;No command in progress
           JMP         RETURN                   ;Exit READ
@10        MOVE.W      #CRCERR,D0
           CMPI.B      #ECC_ER,RESULT0(A3)      ;ECC err requiring scavenger write?
           BEQ.S       @1                       ;Yes
           MOVE.W      #READERR,D0
           CMPI.B      #DEVTIMEOUT,RESULT0(A3)  ;Timeout Error?
           BNE         @1
           TST.W       SECT_LEFT(A4)            ;Sectors still to xfer?
           BEQ         @1
           TST.W       ERROR(A3)                ;Previous Error?
           BNE         @1
           BRA         READ_AGAIN
;
;
;
;
;          Write a sector
;          Registers destroyed D0,D1,D5,A0,A1
;
WRITE_SECT MOVE.L      RAW_DATA(A4),A0          ;Data Buffer address
           MOVE.L      RAW_HEADER(A4),A1        ;Header Buffer address
           ADD.L       X_LENG(A4),A0            ;Adjust data buffer address
           MOVE.L      A0,D1
@1         BTST        #DTREQ,STATUS(A2)        ;Wait for DTREQ before writing data
           BEQ         @1
           MOVE.L      A2,D5                    ;Save A2
           MOVE.L      HWBASE(A3),A2
           TST.W       D0
           BEQ         @21                      ;Do not read header
           MOVEQ       #5,D0                    ;write header in 6 passes (24 b)
@2         MOVE.W      (A1)+,DATA(A2)
           NOP
           MOVE.W      (A1)+,DATA(A2)
           DBF         D0,@2
@21        BTST        #0,D1                    ;Transfer from odd address
           BNE         @10                      ;Skip if yes
           MOVEQ       #127,D0                  ;Write data in 128 passes (512 b)
@3         MOVE.W      (A0)+,DATA(A2)
           NOP
           MOVE.W      (A0)+,DATA(A2)
           DBF         D0,@3
@5         MOVE.L      D5,A2                    ;Restore A2
           CMPI.B      #2,CTRLTYPE(A3)          ;Type E Controller?
           BEQ         @6
           MOVE.B      #1,CMDREG(A2)            ;Acknowledge block transfer
@6         RTS
           ;
           ;Write from odd address
@10        MOVE        #255,D0                  ;Write data in 256 passes (512b)
@11        MOVE.B      (A0)+,-(SP)
           MOVE.W      (SP)+,D1
           MOVE.B      (A0)+,D1
           MOVE.W      D1,DATA(A2)
           DBF         D0,@11
           BRA.S       @5
;
;
;
WRITE      ; Start write to disk
           CLR.W       RETRIES(A3)              ;Clear retry count
           CLR.W       ERROR(A3)                ;Clear error
WRITE_AGAIN ;Restart write after timeout
           MOVE.W      SECT_LEFT(A4),NXFERCNT(A3);Get # blocks to be transferred
WRITE_MORE  ;Write next multi-block chunk of sectors
           BSR         WAITRF                   ;Wait for free register file
           BSR         XFER_SETUP               ;Setup Parameters and data Ptrs
           MOVE.L      HWBASE(A3),A2
           ADDA        #BI,A2                   ;Set byte mode/interrupts enabled
           MOVE.B      #$52,CMDREG(A2)          ;Issue write
WRITE_WAIT MOVE.W      #WAITINT,RESPONSE(A3)    ;Wait for DTREQ interrupt
           MOVE.B      #WRITING,STATE(A3)       ;Set driver state
           JMP         RETURN
;
;
WRITE_INT  ;Interrupt while writing
           BTST        #CMD_DONE,STATUS(A2)     ;Command Complete?
           BNE         WRITE_DONE
           TST.W       CXFERCNT(A3)             ;Block to xfer?
           BEQ         WRITE_DONE
           SUBQ.W      #1,CXFERCNT(A3)          ;Decrement block count left
           TST.W       ERROR(A3)                ;Previous error?
           BNE         WRITE_FLUSH              ;If yes flush sector to controller
           MOVEQ       #1,D0                    ;Write header and data
           BSR         WRITE_SECT               ;Continue write after interrupt
           ADDQ.L      #1,PSECTOR(A3)           ;Sector for next transfer
           MOVE.L      A2,D5                    ;Save A2
           CLR.W       -(SP)
           MOVE.L      A4,-(SP)
           JSR         OKXFERNEXT               ;error:=OKXFERNEXT(drivecb_ptr)
           MOVE.W      (SP)+,ERROR(A3);
           MOVE.L      D5,A2                    ;Restore A2
           ADDA        #I,A2                    ;Setup for next interrupt
           MOVE.B      STATUS(A2),D0
           BRA         WRITE_WAIT               ;Wait for interrupt
;
;          After error flush the controller with dummy sectors to complete
;          the request in progress
WRITE_FLUSH
@1         BTST        #DTREQ,STATUS(A2)        ;Wait for DTREQ before writing data
           BEQ         @1
           MOVE.L      HWBASE(A3),A1
           MOVEQ       #5,D0                    ;write header in 6 passes (24 b)
@2         MOVE.W      D0,DATA(A1)
           NOP
           MOVE.W      D0,DATA(A1)
           DBF         D0,@2
           MOVEQ       #127,D0                   ;Write data in 128 passes (512 b)
@3         MOVE.W      D0,DATA(A1)
           NOP
           MOVE.W      D0,DATA(A1)
           DBF         D0,@3
           CMPI.B      #2,CTRLTYPE(A3)          ;Type E Controller?
           BEQ         @5
           MOVE.B      #1,CMDREG(A2)            ;Acknowledge block transfer
@5         ADDA        #I,A2                    ;Setup for next interrupt
           MOVE.B      STATUS(A2),D0
           BRA         WRITE_WAIT               ;Wait for interrupt
;
WRITE_DONE BSR         ACKCC                    ;Acknowledge cmd complete
           TST.B       D0                       ;Test error completion type
           BNE         @10                      ;Jump if error
           TST.W       NXFERCNT(A3)             ;More blocks to transfer?
           BNE         WRITE_MORE
@1         MOVE.W      D0,RESPONSE(A3)          ;Return error response
           MOVE.B      #IDLING,STATE(A3)        ;No command in progress
           JMP         RETURN                   ;Exit WRITE
@10        MOVE.W      #WRITERR,D0
           CMPI.B      #DEVTIMEOUT,RESULT0(A3)  ;Timeout Error?
           BNE         @1
           TST.W       SECT_LEFT(A4)            ;Sectors still to xfer?
           BEQ         @1
           TST.W       ERROR(A3)                ;Previous Error?
           BNE         @1
           BRA         WRITE_AGAIN

;
;



           .END



ÿ