;       Copyright 1983, 1984, Apple Computer, Inc.
;
;       PRIAMASM:  Priam Driver Assembly Language
;
;       By Wendell Henry 1/83
;
           .INCLUDE    SOURCE/PASCALDEFS.TEXT

           .PROC       PRIAMROUTINE

           .DEF        ARCHIVEASM
           ;---------------------------------------------------------------
           ;
           ;
           ;
           ;
           ;
           ;
           ;---------------------------------------------------------------
;
;    (* high level commands                              *)
;    interrupt = 0;         (* process interrupt         *)
;    readTape  = 2;         (* read tape                 *)
;    writeTape = 4;         (* write tape                *)
;    TControl  = 6;         (* control tape              *)
;
;
;    Misc Constants
TIMELIMIT  .EQU        50000    ;Gives timeout of approximately .5 seconds
;
;
;    Response Values
OK         .EQU        0        ;Good response - no errors - command done
WAITINT    .EQU        1        ;Good response - no errors - wait for interrupt
TIMEOUT    .EQU        666      ;Timeout error
TREADERR   .EQU        1841     ;Error reading from tape
TWRITERR   .EQU        1842     ;Error writing to tape
TCONERR    .EQU        1843     ;Error controling tape
RESUMABLE  .EQU       -1840     ;Packet ended in resumable state
PACKETERR  .EQU        1844     ;Packet ended in non resumable state
PACKET2ERR .EQU        1845     ;A packet command had an error
;
;
;DRIVECB OFFSETS
CONFIGADDR .EQU        0                   ;Configinfo entry address
EXT_PTR    .EQU        CONFIGADDR+4        ;Configinfo index
RAW_DATA   .EQU        EXT_PTR+4           ;Ptr to raw data buffer
X_LENG     .EQU        RAW_DATA+4          ;Offset from raw_data to data buffer
SECT_LEFT  .EQU        X_LENG+4            ;The total sectors required
OKXFERNEXT .EQU        SECT_LEFT+2         ;Function entry pt to verify
                                           ;last sector transferred and
                                           ;setup for next sector
V_FLAG     .EQU        OKXFERNEXT+4        ;True if all writes should be verified
;
;
;EXT_DRIVE_CB OFFSETS
HWBASE     .EQU        0                   ;Device high select iospace address
HWPARITY   .EQU        HWBASE+4            ;Device address for parity FF
DEVICE     .EQU        HWPARITY+4          ;Device number
COMMAND    .EQU        DEVICE+1            ;High level command being issued
RESPONSE   .EQU        COMMAND+1           ;Response to high level command
INPUT      .EQU        RESPONSE+2          ;Input parms to drive
OUTPUT     .EQU        INPUT+6             ;Output parms from drive
PE         .EQU        OUTPUT+6            ;Parity error flag
STATE      .EQU        PE+1                ;Current state of driver
RETRIES    .EQU        STATE+1             ;# retries this request
TOT_RETRIES .EQU       RETRIES+2           ;Total # retries this drive
ERROR      .EQU        TOT_RETRIES+2       ;Error from sector transfer
NXFERCNT   .EQU        ERROR+2             ;Next block xfer count
CXFERCNT   .EQU        NXFERCNT+2          ;Current block xfer count
CTRLTYPE   .EQU        CXFERCNT+2          ;Controller Type: 2=E; 3=T

PACKETLEN  .EQU        CTRLTYPE+1          ;Number of valid bytes in packet
PACKET     .EQU        PACKETLEN+1         ;Packet command

PID        .EQU        PACKET+16           ;Packet id
PSTATE     .EQU        PID+1               ;Packet state
PTDF       .EQU        PSTATE+1            ;Termination device flag
PPRISTAT   .EQU        PTDF+1              ;Primary termination status
PSECSTAT   .EQU        PPRISTAT+1          ;Secondary termination status
PRES1      .EQU        PSECSTAT+1          ;Unused
POFFSET    .EQU        PRES1+1             ;Packet offset
PLENGTH    .EQU        POFFSET+2           ;Packet length
PSTEPCODE  .EQU        PLENGTH+2           ;Current step operation code
PSTEPNUMB  .EQU        PSTEPCODE+1         ;Current step number
PSECTNUMB  .EQU        PSTEPNUMB+1         ;Total number of records copied
PSDEV      .EQU        PSECTNUMB+4         ;Source device select
PSSTAT     .EQU        PSDEV+1             ;Source device status
PSSUPSTAT  .EQU        PSSTAT+1            ;Supplemental source status
PSADDR     .EQU        PSSUPSTAT+1         ;Source transfer address
PISECTSTEP .EQU        PSADDR+3            ;Number of records input this copy step
PDDEV      .EQU        PISECTSTEP+4        ;Destination device select
PDSTAT     .EQU        PDDEV+1             ;Destination device status
PDSUPSTAT  .EQU        PDSTAT+1            ;Supplemental destination status
PDADDR     .EQU        PDSUPSTAT+1         ;Destination transfer address
POSECTSTEP .EQU        PDADDR+3            ;Number of records output this copy step
RTN_PSTAT  .EQU        POSECTSTEP+4        ;Return address for READ_PSTATUS

PSECTOR    .EQU        INPUT               ;Sector to be transferred
PSTATUS    .EQU        PID                 ;Packet status report
;
;
;INPUT PARAMETER OFFSETS
PARM0      .EQU        INPUT
PARM1      .EQU        INPUT+1
PARM2      .EQU        INPUT+2
PARM3      .EQU        INPUT+3
PARM4      .EQU        INPUT+4
PARM5      .EQU        INPUT+5
;
;
;OUTPUT RESULT OFFSETS
RESULT0    .EQU        OUTPUT
RESULT1    .EQU        OUTPUT+1
RESULT2    .EQU        OUTPUT+2
RESULT3    .EQU        OUTPUT+3
RESULT4    .EQU        OUTPUT+4
RESULT5    .EQU        OUTPUT+5
;
;
;IOSPACE REGISTER OFFSETS
STATUS     .EQU        0                 ;Interface Status Register
CMDREG     .EQU        0                 ;Command Register
DATA       .EQU        2                 ;Read/Write Disk Data Register
P0         .EQU        4                 ;Parameter 0
P1         .EQU        6
P2         .EQU        8
P3         .EQU       10
P4         .EQU       12
P5         .EQU       14                 ;Parameter 5
R0         .EQU        4                 ;Result 0 - Transaction Status Register
R1         .EQU        6
R2         .EQU        8
R3         .EQU       10
R4         .EQU       12
R5         .EQU       14                 ;Result 5
;
;
;INTERFACE STATUS REGISTER BITS
CMD_REJECT .EQU        7                 ;Command reject
CMD_DONE   .EQU        6                 ;Command Completion Request
CMD_SDONE  .EQU        5                 ;Special Command Completion
BTR_INT    .EQU        4                 ;Block transfer - interrupt
ISR_BUSY   .EQU        3                 ;Interface Busy
DTREQ      .EQU        2                 ;Data Transfer Request
RW_REQ     .EQU        1                 ;Read/Write Request
DBUS_ENA   .EQU        0                 ;Data Bus Enabled
;
;
;INTERFACE CONTROL BITS
N          .EQU     $200                 ;No device cycle
I          .EQU     $100                 ;Interrupt enable
B          .EQU      $80                 ;Byte mode
W          .EQU      $40                 ;Waiting
P          .EQU      $20                 ;Priam Parity cheking circuitry enabled
BW         .EQU      $C0                 ;Byte mode/waiting
BI         .EQU     $180                 ;Byte mode/interrupt enable
;
;
;          Driver states
IDLING     .EQU        0        ;Driver idle - no command in progress
TREADING   .EQU        2        ;Driver reading tape - waiting for interrupt
TWRITING   .EQU        4        ;Driver writing tape - waiting for interrupt
TCONT      .EQU        6        ;Driver controlling tape - waiting for interrupt
ISSUE      .EQU        8        ;Driver xfering packet - waiting to write packet
RESUME     .EQU       10        ;Driver resuming packet - waiting for packet done
RPSTAT     .EQU       12        ;Driver reading packet status - waiting to read
RPSTAT2    .EQU       14        ;Driver reading packet status- waiting for cmd done
ABORT      .EQU       16        ;Driver aborting packet - waiting for cmd done
;
;
;
;
ACKDT      ;Acknowledge Data Transfer
           ;Note: wait is always in byte mode device interrupts disabled
           MOVE.L      HWBASE(A3),A2
           ADDA        #BW,A2
           MOVE.B      #1,CMDREG(A2)            ;Acknowledge transfer of data
           MOVE.L      #TIMELIMIT,D0            ;Timeout value
@1         BTST        #BTR_INT,STATUS(A2)      ;Wait for int flag to drop out
           BEQ.S       @10                      ;Skip if gone
           SUBQ.L      #1,D0                    ;Loop till gone or timeout
           BGT.S       @1
           MOVE        #TIMEOUT,D0              ;Timeout return
           RTS
@10        ADDA        #I,A2                    ;setup for next interrupt
           BTST        #BTR_INT,STATUS(A2)      ;
           MOVEQ       #0,D0                    ;OK return
           RTS
;
;
WAITRF     ;Wait for register file not busy
           ;Note: wait is always in byte mode device interrupts disabled
           MOVE.L      #TIMELIMIT,D0            ;Timeout value
@1         BTST        #ISR_BUSY,STATUS(A2)     ;Test Register file busy
           BEQ.S       @10                      ;Skip if free
           SUBQ.L      #1,D0                    ;Loop till free or timeout
           BGT.S       @1
           MOVE        #TIMEOUT,D0              ;Timeout return
           RTS
@10        MOVEQ       #0,D0                    ;OK return
           RTS
;
;
;

ACKCC     ;Acknowledge command completion
           ;Note: wait is always in byte mode device interrupts disabled
           ;      after command ack device is left with interrupts disabled
           ;Output= Result parms from command
           ;      = Low word of D0 contains error completion type
           ;D0 Destroyed
           MOVE.L      #TIMELIMIT,D0            ;Timeout value
@1         BTST        #CMD_DONE,STATUS(A2)     ;Test command complete
           BNE.S       @10                      ;Skip if done
           SUBQ.L      #1,D0                    ;Loop till free or timeout
           BGT.S       @1
           MOVE        #TIMEOUT,D0              ;Timeout return
           RTS
@10        MOVE.B      R0(A2),RESULT0(A3)       ;Transaction Status
           MOVE.B      R1(A2),RESULT1(A3)       ;Other output parameters
           MOVE.B      R2(A2),RESULT2(A3)
           MOVE.B      R3(A2),RESULT3(A3)
           MOVE.B      R4(A2),RESULT4(A3)
           MOVE.B      R5(A2),RESULT5(A3)
           CLR.B       CMDREG(A2)               ;Send Command Acknowledge
           MOVE.L      #TIMELIMIT,D0            ;Timeout value
@2         BTST        #CMD_DONE,STATUS(A2)     ;Test Command complete
           BEQ.S       @20                      ;Skip if done
           SUBQ.L      #1,D0                    ;Loop till free or timeout
           BGT.S       @2
           MOVE        #TIMEOUT,D0              ;Timeout return
           RTS
@20        MOVE.B      RESULT0(A3),D0           ;Get transaction status
           ANDI.W      #$30,D0                  ;Extract error completion type
           RTS
;
;
ARCHIVEASM
           MOVEM.L     D2-D5/A2-A4,-(SP)        ;Save Pascal Registers
           MOVE.L      32(SP),A4                ;Drive control block in A4
           MOVE.L      EXT_PTR(A4),A3           ;Drivecb extension in A3
           MOVEA.L     HWBASE(A3),A2            ;IOspace address in A2 with
           ADDA        #BW,A2                   ; byte mode - waiting
           MOVE.B      COMMAND(A3),D0           ;Get command
           EXT.W       D0
           MOVE.W      CMD_TABLE(D0),D0
           JMP         CMD_TABLE(D0)            ;Go process command
;
CMD_TABLE
           .WORD       INTERRUPT-CMD_TABLE      ;Process interrupt
           .WORD       READTAPE-CMD_TABLE       ;Read tape
           .WORD       WRITETAPE-CMD_TABLE      ;Write tape
           .WORD       TCONTROL-CMD_TABLE       ;Control tape
;
RETURN     MOVEM.L     (SP)+,D2-D5/A2-A4        ;Restore Registers
           MOVE.L      (SP)+,A0                 ;Return Address
           ADDQ        #4,SP                    ;Pop input parameter
           JMP         (A0)                     ;Return
;
;
;
INTERRUPT  ; INTERRUPT FROM DISK
           MOVE.B      STATE(A3),D0             ;Get driver state
           EXT.W       D0
           MOVE.W      STATE_TABLE(D0),D0
           JMP         STATE_TABLE(D0)          ;Go process interrupt
;
;
STATE_TABLE
           .WORD       IDLE-STATE_TABLE         ;Unexpected interrupt
           .WORD       TREAD_INT-STATE_TABLE    ;Interrupt during tape read
           .WORD       TWRITE_INT-STATE_TABLE   ;Interrupt during tape write
           .WORD       TCONT_INT-STATE_TABLE    ;Interrupt during tape control
           .WORD       ISSUE_INT-STATE_TABLE    ;Interrupt during Packet issue
           .WORD       RESUME_INT-STATE_TABLE   ;Interrupt during Resume Packet
           .WORD       RPSTAT_INT-STATE_TABLE   ;Interrupt during Read Packet Status
           .WORD       RPSTAT2_INT-STATE_TABLE  ;Interrupt during Read Packet Status
           .WORD       ABORT_INT-STATE_TABLE    ;Interrupt during Abort Packet
;
;
;
IDLE       ; Interrupt while in idle state

           CLR.W       RESPONSE(A3)             ;Return error response
           MOVE.B      #IDLING,STATE(A3)        ;No command in progress
           JMP         RETURN                   ;For now ignore interrupt
;
;
;  Setup controller for data transfer
XFER_SETUP MOVE.B      DEVICE(A3),P0(A2)        ;Device
           MOVE.B      PSECTOR+1(A3),P1(A2)     ;Sector - byte 2
           MOVE.B      PSECTOR+2(A3),P2(A2)     ;Sector - byte 1
           MOVE.B      PSECTOR+3(A3),P3(A2)     ;Sector - byte 0
           MOVE.W      NXFERCNT(A3),D0
           CMPI.W      #127,D0                  ;Max sector count of 127
           BGT         @1
           MOVE.B      D0,P4(A2)                ;Sector count
           MOVE.W      D0,CXFERCNT(A3)          ;Current sector count
           CLR.W       NXFERCNT(A3)             ;Sectors left
           RTS
@1         MOVE.B      #127,P4(A2)              ;Transfer done in chunks
           MOVE.W      #127,CXFERCNT(A3)        ;Current sector count
           SUBI.W      #127,NXFERCNT(A3)        ;Sectors left
           RTS
;
;
;          Read sector - parity is checked
;            Destroys registers D0,A0,A1
;
READ_SECT  MOVE.L      RAW_DATA(A4),A0          ;Data Buffer address
           ADD.L       X_LENG(A4),A0            ;Adjust data buffer address
@1         BTST        #DTREQ,STATUS(A2)        ;Wait for DTREQ before reading data
           BEQ         @1
           MOVE.L      HWBASE(A3),A1
           ADDA        #DATA,A1                 ;Add Data Register
           ADDA        #P,A1                    ;Add Parity Check Enable
           TST.W       (A1)                     ;Start read of first word
           MOVEQ       #62,D0                   ;Read data in 63 passes (504 B)
@3         MOVE.W      (A1),(A0)+
           NOP
           MOVE.W      (A1),(A0)+
           NOP
           MOVE.W      (A1),(A0)+
           NOP
           MOVE.W      (A1),(A0)+
           DBF         D0,@3
           MOVE.W      (A1),(A0)+               ; now read 8 bytes - 6 normally
           NOP
           MOVE.W      (A1),(A0)+
           NOP
           MOVE.W      (A1),(A0)+
           NOP
           MOVE.W      N(A1),(A0)+              ; last 2 avoiding a device cycle
           MOVE.L      HWPARITY(A3),A1          ;Location of parity FF
           MOVE.B      (A1),D0                  ;Get parity byte
           ANDI.B      #$80,D0                  ;Extract parity FF
           OR.B        D0,PE(A3)                ;Save
           MOVE.B      #1,CMDREG(A2)            ;Acknowledge block transfer
           RTS
;
;
;
;
;          Write a sector
;            Registers destroyed D0,A0,A1
;
WRITE_SECT MOVE.L      RAW_DATA(A4),A0          ;Data Buffer address
           ADD.L       X_LENG(A4),A0            ;Adjust data buffer address
@1         BTST        #DTREQ,STATUS(A2)        ;Wait for DTREQ before writing data
           BEQ         @1
           MOVE.L      HWBASE(A3),A1
           MOVEQ       #127,D0                   ;Write data in 128 passes (512 b)
@3         MOVE.W      (A0)+,DATA(A1)
           NOP
           MOVE.W      (A0)+,DATA(A1)
           DBF         D0,@3
           MOVE.B      #1,CMDREG(A2)            ;Acknowledge block transfer
           RTS
;
;
;
READTAPE   ;Start read of tape unit
           CLR.W       RETRIES(A3)
           CLR.W       ERROR(A3)
           MOVE.W      SECT_LEFT(A4),NXFERCNT(A3);Number of sectors to transfer
TREAD_MORE BSR         WAITRF                   ;Wait for free register file
           BSR         XFER_SETUP               ;Setup parameters and data ptrs
           CLR.B       PE(A3)                   ;Clear parity error flag
           MOVE.L      HWBASE(A3),A2
           ADDA        #BI,A2                   ;Byte mode/interrupts enabled
           MOVE.B      #$53,CMDREG(A2)          ;Issue read
TREAD_WAIT MOVE.W      #WAITINT,RESPONSE(A3)    ;Wait for interrupt
           MOVE.B      #TREADING,STATE(A3)      ;Driver state
           JMP         RETURN
;
;
TREAD_INT  ;Interrupt while reading tape
           BTST        #CMD_DONE,STATUS(A2)     ;Command complete?
           BNE         TREAD_DONE
           TST.W       CXFERCNT(A3)             ;Block to transfer?
           BEQ         TREAD_DONE
           BSR         READ_SECT                ;Continue read after interrupt
           SUBQ.W      #1,SECT_LEFT(A4)         ;Decrement block count left
           SUBQ.W      #1,CXFERCNT(A3)          ;Decrement current block count left
           ADDI.L      #512,X_LENG(A4)          ;Next data block address offset
           ADDA        #I,A2                    ;Setup for next interrupt
           MOVE.B      STATUS(A2),D0
           BRA         TREAD_WAIT               ;Wait for next interrupt
;
;
TREAD_DONE ;Finish the request
           BSR         ACKCC                    ;Acknowledge command complete
           TST.B       D0                       ;Test completion type
           BNE         @10
           TST.W       NXFERCNT(A3)             ;More blocks to transfer?
           BNE         TREAD_MORE
@1         MOVE.W      D0,RESPONSE(A3)          ;Return error response
           MOVE.B      #IDLING,STATE(A3)        ;Driver state
           JMP         RETURN
@10        MOVE.W      #TREADERR,D0             ;Tape read error
           BRA         @1
;
;
;
WRITETAPE  ;Start write of tape unit
           CLR.W       RETRIES(A3)
           CLR.W       ERROR(A3)
           MOVE.W      SECT_LEFT(A4),NXFERCNT(A3);Number of sectors to transfer
TWRITE_MORE
           BSR         WAITRF                   ;Wait for free register file
           BSR         XFER_SETUP               ;Setup parameters and data ptrs
           MOVE.L      HWBASE(A3),A2
           ADDA        #BI,A2                   ;Byte mode/interrupts enabled
           MOVE.B      #$52,CMDREG(A2)          ;Issue write
TWRITE_WAIT
           MOVE.W      #WAITINT,RESPONSE(A3)    ;Wait for interrupt
           MOVE.B      #TWRITING,STATE(A3)      ;Driver state
           JMP         RETURN
;
;
TWRITE_INT ;Interrupt while writing tape
           BTST        #CMD_DONE,STATUS(A2)     ;Command complete?
           BNE         TWRITE_DONE
           TST.W       CXFERCNT(A3)             ;Block to transfer?
           BEQ         TWRITE_DONE
           BSR         WRITE_SECT               ;Continue write after interrupt
           SUBQ.W      #1,SECT_LEFT(A4)         ;Decrement block count left
           SUBQ.W      #1,CXFERCNT(A3)          ;Decrement current block count left
           ADDI.L      #512,X_LENG(A4)          ;Next data block address offset
           ADDA        #I,A2                    ;Setup for next interrupt
           MOVE.B      STATUS(A2),D0
           BRA         TWRITE_WAIT              ;Wait for next interrupt
;
;
TWRITE_DONE ;Finish the request
           BSR         ACKCC                    ;Acknowledge command complete
           TST.B       D0                       ;Test completion type
           BNE         @10
           TST.W       NXFERCNT(A3)             ;More blocks to transfer?
           BNE         TWRITE_MORE
@1         MOVE.W      D0,RESPONSE(A3)          ;Return error response
           MOVE.B      #IDLING,STATE(A3)        ;Driver state
           JMP         RETURN
@10        MOVE.W      #TWRITERR,D0             ;Tape write error
           BRA         @1
;
;
DCODE_TABLE ;Command codes for each DControl
           .BYTE       0                        ;Unused
           .BYTE       $6A                      ;Rewind Tape
           .BYTE       $6F                      ;Erase tape
           .BYTE       $C1                      ;Retension tape
           .BYTE       $C0                      ;Advance file marks
           .BYTE       $62                      ;Write file mark
           .BYTE       $63                      ;Read file mark
           .BYTE       $06                      ;Read drive status
;
;
TCONTROL   ;Tape control operations
           CLR.W       RETRIES(A3)
           CLR.W       ERROR(A3)
           MOVE.B      PARM0(A3),D1             ;Get dcode
           EXT.W       D1
           CMPI.W      #8,D1                    ;Packet oriented operation?
           BGE         T_PACKET
           BSR         WAITRF                   ;Wait for free register file
           MOVE.B      DEVICE(A3),P0(A2)        ;Device number
           CMPI.W      #4,D1                    ;Dcode = skip file marks?
           BNE         @1
           MOVE.B      PARM1(A3),P4(A2)         ;File mark count
@1         MOVE.L      HWBASE(A3),A2
           ADDA        #BI,A2                   ;Byte mode/interrupts enabled
           MOVE.B      DCODE_TABLE(D1),CMDREG(A2) ;Issue command
           MOVE.W      #WAITINT,RESPONSE(A3)    ;Wait for interrupt
           MOVE.B      #TCONT,STATE(A3)         ;Driver state
           JMP         RETURN
;
;
TCONT_INT  ;Interrupt while controlling tape
           BSR         ACKCC                    ;Acknowledge command complete
           TST.B       D0                       ;Test completion type
           BNE         @10
           CMPI.B      #5,PARM0(A3)             ;Dcode = Write file marks?
           BNE.S       @1
           SUBQ.B      #1,PARM1(A3)             ;Decrement # file marks to write
           BNE         TCONTROL                 ;Issue command again if more to go
@1         MOVE.W      D0,RESPONSE(A3)          ;Return error response
           MOVE.B      #IDLING,STATE(A3)        ;Driver state
           JMP         RETURN
@10        MOVE.W      #TCONERR,D0              ;Tape control error
           BRA         @1

T_PACKET   ; Packet oriented operations
           SUBQ        #8,D1                    ;Copy to tape?
           BNE.S       @1
           MOVE.B      #1,PACKET(A3)
           MOVE.B      #0,PACKET+1(A3)
           ;counting device and error flags are already filled in
           MOVE.B      #$40,PACKET+3(A3)
           MOVE.W      #0,PACKET+4(A3)
           ;source device is already filled in
           MOVE.B      #0,PACKET+7(A3)
           MOVE.W      #0,PACKET+8(A3)
           MOVE.W      #0,PACKET+10(A3)
           ;destination device is already filled in
           MOVE.B      #0,PACKET+13(A3)
           MOVE.W      #0,PACKET+14(A3)
           MOVE.B      #16,PACKETLEN(A3)
           BRA         I_PACKET                 ;Go issue packet
@1         SUBQ        #1,D1
           BEQ         R_PACKET                 ;Resume Copy
           SUBQ        #1,D1
           BEQ         A_PACKET                 ;Abort Copy
           SUBQ        #1,D1
           BEQ         I_PACKET                 ;Issue Packet
           SUBQ        #1,D1
           BEQ         R_PACKET                 ;Resume Packet
           BRA         A_PACKET                 ;Abort Packet

I_PACKET   ;Issue transfer packet command
           CLR.W       RETRIES(A3)
           CLR.W       ERROR(A3)
           BSR         WAITRF                   ;Wait for free register file
           MOVE.B      #0,P0(A2)                ;Packet ID
           MOVE.B      #0,P2(A2)
           MOVE.B      PACKETLEN(A3),P3(A2)     ;Packet length
           MOVE.L      HWBASE(A3),A2
           ADDA        #BI,A2
           MOVE.B      #$B0,CMDREG(A2)          ;Issue Transfer Packet command
           MOVE.W      #WAITINT,RESPONSE(A3)    ;Wait for interrupt
           MOVE.B      #ISSUE,STATE(A3)
           BRA         RETURN
;
;
ISSUE_INT  ;Interrupt to transfer packet
           BTST        #DTREQ,STATUS(A2)        ;Waiting for data?
           BEQ         RESUME_INT
           LEA         PACKET(A3),A0            ;Packet address
           MOVE.B      PACKETLEN(A3),D0         ;Packet length
           EXT.W       D0
           SUBQ        #1,D0                    ;Length - 1
@10        MOVE.B      (A0)+,DATA(A2)           ;Write packet
           DBF         D0,@10
           BSR         ACKDT                    ;Acknowledge transfer of packet
           MOVE.W      #WAITINT,RESPONSE(A3)    ;Wait for interrupt
           MOVE.B      #RESUME,STATE(A3)
           BRA         RETURN
;
;

READ_PSTATUS ;Read packet status
             ;Exits with transaction status in D0
             ;
           BSR         WAITRF                   ;Wait for free register file
           MOVE.B      #0,P0(A2)                ;Packet ID
           MOVE.L      HWBASE(A3),A2
           ADDA        #BI,A2
           MOVE.B      #$B8,CMDREG(A2)          ;Issue Read Packet Status command
           MOVE.W      #WAITINT,RESPONSE(A3)    ;Wait for interrupt
           MOVE.B      #RPSTAT,STATE(A3)
           BRA         RETURN
;
;
RPSTAT_INT ; Interrupt when status can be read
           BTST        #CMD_DONE,STATUS(A2)     ;Command completed?
           BNE         RPSTAT2_INT
@1         BTST        #DTREQ,STATUS(A2)        ;Waiting for data?
           BEQ.S       @1
           LEA         PSTATUS(A3),A0
           MOVE.W      #35,D0
@10        MOVE.B      DATA(A2),(A0)+           ;Read packet status
           DBF         D0,@10
           BSR         ACKDT                    ;Acknowledge transfer of packet
           MOVE.W      #WAITINT,RESPONSE(A3)    ;Wait for interrupt
           MOVE.B      #RPSTAT2,STATE(A3)
           BRA         RETURN
;
;
RPSTAT2_INT ;Interrupt after read packet status
           BSR         ACKCC                    ;Acknowledge command complete
           MOVE.L      RTN_PSTATUS(A3),A0       ;Get return address
           JMP         (A0)                     ;Return


R_PACKET   ;Resume packet operation
           BSR         WAITRF                   ;Wait for free register file
           MOVE.B      #0,P0(A2)                ;Packet ID
           MOVE.B      #$FF,P1(A2)              ;Resume address
           MOVE.B      #$FF,P2(A2)
           MOVE.B      #$FF,P3(A2)
           MOVE.L      HWBASE(A3),A2
           ADDA        #BI,A2
           MOVE.B      #$B1,CMDREG(A2)          ;Issue Resume Packet command
           MOVE.W      #WAITINT,RESPONSE(A3)    ;Wait for interrupt
           MOVE.B      #RESUME,STATE(A3)
           BRA         RETURN
;
;
RESUME_INT ;Interrupt after a packet has been resumed
           BSR         ACKCC                    ;Acknowledge command complete
           CMPI.B      #$08,RESULT0(A3)         ;Packet terminated?
           BEQ.S       @1
           CMPI.B      #$28,RESULT0(A3)         ;Packet terminated?
           BNE         RESUME_ERR
@1         LEA         @2,A0                    ;Get return address
           MOVE.L      A0,RTN_PSTAT(A3)         ;Save it
           BRA         READ_PSTATUS             ;Read packet status
@2         TST.B       D0
           BNE         RESUME_ERR
           ;Check state of packet
           CMPI.B      #$0D,PSTATE(A3)          ;Packet completed?
           BNE.S       R_EXIT2
           MOVE.W      #0,D0
R_EXIT     MOVE.W      D0,RESPONSE(A3)          ;Return error response
           MOVE.B      #IDLING,STATE(A3)
           BRA         RETURN
R_EXIT2    CMPI.B      #$03,PSTATE(A3)          ;Packet resumable?
           BNE.S       @10
           MOVE.W      #RESUMABLE,D0            ;Packet waiting to be resumed
           BRA.S       R_EXIT
@10        MOVE.W      #PACKETERR,D0            ;Packet terminated with error
           BRA.S       R_EXIT
RESUME_ERR MOVE.W      #PACKET2ERR,D0           ;A packet command had an error
           BRA.S       R_EXIT


A_PACKET   ;Abort packet
           BSR         WAITRF                   ;Wait for free register file
           MOVE.B      #0,P0(A2)               ;Packet ID
           MOVE.L      HWBASE(A3),A2
           ADDA        #BI,A2
           MOVE.B      #$BF,CMDREG(A2)          ;Issue Abort Packet command
           MOVE.W      #WAITINT,RESPONSE(A3)    ;Wait for interrupt
           MOVE.B      #ABORT,STATE(A3)
           BRA         RETURN
;
;
ABORT_INT ;Interrupt after a packet has been aborted
           BSR         ACKCC                    ;Acknowledge command complete
           TST.B       D0
           BNE         @10
           LEA         @1,A0                    ;Get return address
           MOVE.L      A0,RTN_PSTAT(A3)         ;Save it
           BRA         READ_PSTATUS             ;Read packet status
@1         TST.B       D0
           BNE         @10
@5         MOVE.W      D0,RESPONSE(A3)          ;Return error response
           MOVE.B      #IDLING,STATE(A3)
           BRA         RETURN
@10        MOVE.W      #PACKET2ERR,D0
           BRA.S       @5

           .END



ÿ