;       Copyright 1983, 1984, Apple Computer, Inc.
;
;       PRIAMASM:  Priam Driver Assembly Language
;
;       By Wendell Henry 1/83
;
           .INCLUDE    SOURCE/PASCALDEFS.TEXT

;    Misc constants
TIMELIMIT  .EQU         500000   ;Gives timeout after about 5 seconds
;
;    (* high level commands                              *)
;    initcont  = 0;         (* initialize controller     *)
;
;
;    Response Values
OK         .EQU        0        ;Good response - no errors - command done
WAITINT    .EQU        1        ;Good response - no errors - wait for interrupt
TIMEOUT    .EQU        666      ;Error = timeout
INITERR    .EQU        1840     ;Errors during initialization of controller
;
;
;INTERLEAVE FACTORS
E_FACTOR   .EQU        3                   ;For E Controller
T_FACTOR   .EQU        1                   ;For T Controller
;
;
;DRIVECB OFFSETS
CONFIGADDR .EQU        0                   ;Configinfo entry address
EXT_PTR    .EQU        CONFIGADDR+4        ;Extension to driveCB
DEVPTR0    .EQU        EXT_PTR+4           ;attached device[0]
DEVPTR1    .EQU        DEVPTR0+4
DEVPTR2    .EQU        DEVPTR1+4
DEVPTR3    .EQU        DEVPTR2+4           ;attached device[3]

;
;
;EXT_DRIVE_CB OFFSETS
HWBASE     .EQU        0                   ;Device high select iospace address
HWPARITY   .EQU        HWBASE+4            ;Device address for parity FF
DEVICE     .EQU        HWPARITY+4          ;Device number
COMMAND    .EQU        DEVICE+1            ;High level command being issued
RESPONSE   .EQU        COMMAND+1           ;Response to high level command
INPUT      .EQU        RESPONSE+2          ;Input parms to drive
OUTPUT     .EQU        INPUT+6             ;Output parms from drive
PE         .EQU        OUTPUT+6            ;Parity error flag
STATE      .EQU        PE+1                ;Current state of driver
RETRIES    .EQU        STATE+1             ;# retries this request
TOT_RETRIES .EQU       RETRIES+2           ;Total # retries this drive
ERROR      .EQU        TOT_RETRIES+2       ;Error from sector transfer
NXFERCNT   .EQU        ERROR+2             ;Next block xfer count
CXFERCNT   .EQU        NXFERCNT+2          ;Current block xfer count
CTRLTYPE   .EQU        CXFERCNT+2          ;Controller Type: 2=E; 3=T

;
;
;INPUT PARAMETER OFFSETS
PARM0      .EQU        INPUT
PARM1      .EQU        INPUT+1
PARM2      .EQU        INPUT+2
PARM3      .EQU        INPUT+3
PARM4      .EQU        INPUT+4
PARM5      .EQU        INPUT+5
;
;
;OUTPUT RESULT OFFSETS
RESULT0    .EQU        OUTPUT
RESULT1    .EQU        OUTPUT+1
RESULT2    .EQU        OUTPUT+2
RESULT3    .EQU        OUTPUT+3
RESULT4    .EQU        OUTPUT+4
RESULT5    .EQU        OUTPUT+5
;
;
;IOSPACE REGISTER OFFSETS
STATUS     .EQU        0                 ;Interface Status Register
CMDREG     .EQU        0                 ;Command Register
DATA       .EQU        2                 ;Read/Write Disk Data Register
P0         .EQU        4                 ;Parameter 0
P1         .EQU        6
P2         .EQU        8
P3         .EQU       10
P4         .EQU       12
P5         .EQU       14                 ;Parameter 5
R0         .EQU        4                 ;Result 0 - Transaction Status Register
R1         .EQU        6
R2         .EQU        8
R3         .EQU       10
R4         .EQU       12
R5         .EQU       14                 ;Result 5
;
;
;INTERFACE STATUS REGISTER BITS
CMD_REJECT .EQU        7                 ;Command reject
CMD_DONE   .EQU        6                 ;Command Completion Request
CMD_SDONE  .EQU        5                 ;Special Command Completion
ISR_RES    .EQU        4                 ;Reserved
ISR_BUSY   .EQU        3                 ;Interface Busy
DTREQ      .EQU        2                 ;Data Transfer Request
RW_REQ     .EQU        1                 ;Read/Write Request
DBUS_ENA   .EQU        0                 ;Data Bus Enabled
;
;
;INTERFACE CONTROL BITS
N          .EQU     $200                 ;No device cycle
I          .EQU     $100                 ;Interrupt enable
B          .EQU      $80                 ;Byte mode
W          .EQU      $40                 ;Waiting
P          .EQU      $20                 ;Priam Parity cheking circuitry enabled
BW         .EQU      $C0                 ;Byte mode/waiting
BI         .EQU     $180                 ;Byte mode/interrupt enable
;
;
;          Driver states
IDLING     .EQU        0        ;Driver idle - no command in progress
;
;
           ;----------------------------------------------------------------------
           ;PCARD DRIVER ENTRY POINT
           ;
           ;function DRIVER(parameters: param_ptr): integer;
           ;
           ;NOTE: if the function code being passed to DRIVER is NOT "Dinterrupt"
           ;      then the pascal routine PCARDDRIVER will be executed to handle it.
           ;      Dinterrupt is handled locally.

           .PROC   DRIVER

           .REF    PCARDDRIVER

CONFIG     .EQU        0
F_CODE     .EQU        4

PARMPTR    .EQU        4

           MOVE.L      PARMPTR(SP),A0
           TST.W       F_CODE(A0)               ;Function code = Dinterrupt?
           BEQ.S       @10                      ;Skip if yes
           ;Not an interrupt function call - pass the request to the Pascal
           ;portion of the driver
           JMP         PCARDDRIVER
           ;Dinterrupt function
@10        MOVE.L      CONFIG(A0),A1            ;Get config ptr
           MOVE.L      CB_ADDR(A1),A1           ;DriveCB
           LEA         DEVPTR3(A1),A1           ;Address of Devptrs[3]
           MOVEQ       #3,D0
@1         MOVE.L      (A1),A0                  ;Get configptr
           MOVE.L      (A1),D1
           BEQ.S       @2                       ;Skip if nil
           MOVE.L      CB_ADDR(A0),A0           ;DriveCB
           MOVE.L      EXT_PTR(A0),A0           ;Ext_driveCB
           CMPI.W      #WAITINT,RESPONSE(A0)    ;Response = wait for interrupt?
           BNE.S       @2                       ;Skip if no
           ;Device is waiting for an interrupt
           ;Call its driver
           MOVE.L      (A1),A1                  ;Get configptr
           MOVE.L      PARMPTR(SP),A0
           MOVE.L      A1,(A0)                  ;Switch configptr in parameters
           MOVE.L      (A1),A1
           JMP         (A1)                     ;Jump to driver
           ;
           ;look at next device in list
@2         SUBA.W      #4,A1
           DBF         D0,@1                    ;Go check next device
           ;Did not find any device waiting for interrupt
           ;Send 0 to try and clear interrupt
           MOVE.L      PARMPTR(SP),A0           ;Parameter pointer
           MOVE.L      CB_ADDR(A0),A0           ;DriveCB
           MOVE.L      EXT_PTR(A0),A0           ;Ext_driveCB
           MOVE.L      HWBASE(A0),A0            ;IOspace address
           ADDA        #BW,A0                   ;Byte mode, waiting
           CLR.B       CMDREG(A0)               ;Acknowledge interrupt
           MOVE.L      (SP)+,A0                 ;PC
           ADDA.W      #4,SP                    ;Pop parmptr
           CLR.W       (SP)                     ;return:=0
           JMP         (A0)                     ;exit
           ;


           .PROC       PCARDASM
           ;---------------------------------------------------------------
           ;
           ;
           ;
           ;
           ;
           ;
           ;---------------------------------------------------------------
           MOVEM.L     D2-D5/A2-A4,-(SP)        ;Save Pascal Registers
           MOVE.L      32(SP),A4                ;Drive control block in A4
           MOVE.L      EXT_PTR(A4),A3           ;Drivecb extension in A3
           MOVEA.L     HWBASE(A3),A2            ;IOspace address in A2 with
           ADDA        #BW,A2                   ; byte mode - waiting
           MOVE.B      COMMAND(A3),D0           ;Get command
           EXT.W       D0
           MOVE.W      CMD_TABLE(D0),D0
           JMP         CMD_TABLE(D0)            ;Go process command
;
CMD_TABLE
           .WORD       INITCONT-CMD_TABLE       ;Initialize Controller
;
RETURN     MOVEM.L     (SP)+,D2-D5/A2-A4        ;Restore Registers
           MOVE.L      (SP)+,A0                 ;Return Address
           ADDQ        #4,SP                    ;Pop input parameter
           JMP         (A0)                     ;Return
;
;
WAITRF     ;Wait for register file not busy
           ;Note: wait is always in byte mode device interrupts disabled
           MOVE.L      #TIMELIMIT,D0            ;Timeout value
@1         BTST        #ISR_BUSY,STATUS(A2)     ;Test Register file busy
           BEQ.S       @10                      ;Skip if free
           SUBQ.L      #1,D0                    ;Loop till free or timeout
           BGT.S       @1
           MOVE        #TIMEOUT,D0              ;Timeout return
           RTS
@10        MOVEQ       #0,D0                    ;OK return
           RTS
;
;
;

ACKCC     ;Acknowledge command completion
           ;Note: wait is always in byte mode device interrupts disabled
           ;      after command ack device is left with interrupts disabled
           ;Output= Result parms from command
           ;      = Low word of D0 contains error completion type
           ;D0 Destroyed
           MOVE.L      #TIMELIMIT,D0            ;Timeout value
@1         BTST        #CMD_DONE,STATUS(A2)     ;Test command complete
           BNE.S       @10                      ;Skip if done
           SUBQ.L      #1,D0                    ;Loop till free or timeout
           BGT.S       @1
           MOVE        #TIMEOUT,D0              ;Timeout return
           RTS
@10        MOVE.B      R0(A2),RESULT0(A3)       ;Transaction Status
           MOVE.B      R1(A2),RESULT1(A3)       ;Other output parameters
           MOVE.B      R2(A2),RESULT2(A3)
           MOVE.B      R3(A2),RESULT3(A3)
           MOVE.B      R4(A2),RESULT4(A3)
           MOVE.B      R5(A2),RESULT5(A3)
           CLR.B       CMDREG(A2)               ;Send Command Acknowledge
           MOVE.L      #TIMELIMIT,D0            ;Timeout value
@2         BTST        #CMD_DONE,STATUS(A2)     ;Test Command complete
           BEQ.S       @20                      ;Skip if done
           SUBQ.L      #1,D0                    ;Loop till free or timeout
           BGT.S       @2
           MOVE        #TIMEOUT,D0              ;Timeout return
           RTS
@20        MOVE.B      RESULT0(A3),D0           ;Get transaction status
           ANDI.W      #$30,D0                  ;Extract error completion type
           RTS
;
;
;
INTERRUPT  ; INTERRUPT FROM DISK
           MOVE.B      STATE(A3),D0             ;Get driver state
           EXT.W       D0
           MOVE.W      STATE_TABLE(D0),D0
           JMP         STATE_TABLE(D0)          ;Go process interrupt
;
;
STATE_TABLE
           .WORD       IDLE-STATE_TABLE         ;Unexpected interrupt
;
;
;
IDLE       ; Interrupt while in idle state

           CLR.W       RESPONSE(A3)             ;Return error response
           MOVE.B      #IDLING,STATE(A3)        ;No command in progress
           JMP         RETURN                   ;For now ignore interrupt
;
;
;
INITCONT   ; INITIALIZE CONTROLLER
           BTST        #CMD_DONE,STATUS(A2)     ;Test command complete
           BEQ         @1                       ;Must be false
           BSR         WAITRF                   ;Wait for free register file
           TST.W       D0
           BNE         ERREXIT
           CLR.B       CMDREG(A2)               ;Command ack power up
           MOVE.L      #TIMELIMIT,D0            ;Timeout value
@10        BTST        #CMD_DONE,STATUS(A2)     ;Test Command complete
           BEQ.S       @1                       ;Skip if done
           SUBQ.L      #1,D0                    ;Loop till free or timeout
           BGT.S       @10
           MOVE        #TIMEOUT,D0              ;Timeout return
           BRA         ERREXIT
@1         BSR         WAITRF                   ;Wait for free register file
           TST.W       D0
           BNE         ERREXIT
           MOVE.B      #7,CMDREG(A2)            ;Issue software reset command
           BSR         ACKCC                    ;Command acknowledge
           CMPI.W      #$10,D0                  ;error code = init complete?
           BEQ.S       @11                      ;skip if yes
           BRA         ERREXIT
@11        BSR         WAITRF                   ;Wait for free register file
           TST.W       D0
           BNE         ERREXIT
           CLR.B       P0(A2)                   ;Device # Smart E,unused Smart T
           MOVE.B      #9,CMDREG(A2)            ;Read Mode
           BSR         ACKCC                    ;Wait to Command Acknowledge
           TST.W       D0
           BNE         ERREXIT
           MOVE.B      RESULT3(A3),CTRLTYPE(A3) ;Save controller type
           BSR         WAITRF                   ;Wait for free register file
           TST.W       D0
           BNE         ERREXIT
           CLR.B       P0(A2)                   ;Device # Smart E,unused Smart T
           CMPI.B      #2,RESULT3(A3)           ;Type of controller?
           BNE         @5
                                                ;Smart E Controller
           MOVE.B      #$48,P1(A2)              ;Log. Sector Mode/Disable Buffering
           CLR.B       P2(A2)
           MOVE.B      #8,CMDREG(A2)            ;Set Mode
           BSR         ACKCC                    ;Wait to Command Acknowledge
           TST.W       D0
           BNE         ERREXIT
           BRA         @6
@5                                              ;Smart T Controller
           MOVE.B      #$40,P1(A2)              ;Log. Sector Mode
           CLR.B       P2(A2)
           MOVE.B      #8,CMDREG(A2)            ;Set Mode
           BSR         ACKCC                    ;Wait to Command Acknowledge
           TST.W       D0
           BNE         ERREXIT
           BSR         WAITRF                   ;Wait for free register file
           TST.W       D0
           BNE         ERREXIT
           MOVE.B      #$40,P0(A2)
           MOVE.B      #1,P1(A2)                ;Parm select 1
           MOVE.B      #7,P2(A2)                ;Disable High performance mcode/
                                                ;Block Transfer Interrupt/
                                                ;Block Transfer Timer Disable
           MOVE.B      #$C,CMDREG(A2)           ;Specify Parameters
           BSR         ACKCC                    ;Wait to Command Acknowledge
           TST.W       D0
           BNE         ERREXIT
           BSR         WAITRF                   ;Wait for free register file
           TST.W       D0
           BNE         ERREXIT
           MOVE.B      #$40,P0(A2)
           MOVE.B      #0,P1(A2)                ;Parm select 0
           MOVE.B      #3,P2(A2)                ;Command Complete Interrupt/
                                                ;Enable Parity
           MOVE.B      #$C,CMDREG(A2)           ;Specify Parameters
           BSR         ACKCC                    ;Wait to Command Acknowledge
           TST.W       D0
           BNE         ERREXIT
@6         CLR.W       D0                       ;Error return
ERREXIT    MOVE.W      D0,RESPONSE(A3)          ;Return error response
           MOVE.B      #IDLING,STATE(A3)        ;No command in progress
           JMP         RETURN                   ;Exit INITCONT

           .END



ÿ