(* Copyright 1983, 1984, Apple Computer, Inc. *)

(* MMPRIM2 *)

procedure INIT_MMLIST(* c_mmlist: ptr_mmlist; max_entries: int2 *);

  var
    i: int2;

  begin
    with c_mmlist^ do
      begin
        chain.fwd_link:=ord(@chain.fwd_link) - b_sysglobal_ptr;
        chain.bkwd_link:=chain.fwd_link;
        size:=0;
        maxsize:=max_entries;
        for i:=1 to max_entries do ptr[i]:=ord(nil);
      end;
  end; (* INIT_MMLIST *)


procedure ADDTO_MMLIST(c_mmlist: ptr_mmlist; entry: absptr);

  var
    f_mmlist: ptr_mmlist;

  function IN_MMLIST: boolean;

    label 10;

    var
      found: boolean;
      i: int2;

    begin
      f_mmlist:=c_mmlist;
      found:=false;
      repeat
        with f_mmlist^ do
          begin
            for i:=1 to maxsize do if ptr[i] = entry then
                                     begin
                                       found:=true;
                                       goto 10;
                                     end;
            f_mmlist:=pointer(chain.fwd_link + b_sysglobal_ptr);
          end; (* with *)
      until f_mmlist = c_mmlist;

10:   (* quick exit when entry found *)
      in_mmlist:=found;
    end; (* IN_MMLIST *)

  procedure EXTEND_MMLIST;

    var
      new_mmlist: ptr_mmlist;
      ordaddr: absptr;

    begin
      if not GETSPACE(SIZEOF(mmlist2),b_sysglobal_ptr,ordaddr)
        then SYSTEM_ERROR(10595)
        else
         begin
           new_mmlist:=pointer(ordaddr);
           INIT_MMLIST(new_mmlist,max_mmlist_size);
           ENQUEUE(new_mmlist^.chain,c_mmlist^.chain,b_sysglobal_ptr);
         end;
     end;(* EXTEND_MMLIST *)

  procedure ADD_ENTRY;

    label 20;

    var
      i: int2;

    begin
      while true do
        begin
          f_mmlist:=c_mmlist;
          repeat
            with f_mmlist^ do
              begin
                if size < maxsize then
                  begin (* space available for entry *)
                    for i:=1 to maxsize do if ptr[i] = ord(nil) then
                                             begin
                                               ptr[i]:=entry;
                                               size:=size+1;
                                               goto 20;
                                             end;
                  end;
                f_mmlist:=pointer(chain.fwd_link + b_sysglobal_ptr);
              end; (* with *)
          until f_mmlist = c_mmlist;

          (* did not find a place for the entry - extend the list *)
          EXTEND_MMLIST;
        end; (*while *)

20: (* QUICK EXIT WHEN ENTRY ADDED *)
      end;(* ADD_ENTRY *)

  begin
    if not IN_MMLIST then ADD_ENTRY;
  end; (* ADDTO_MMLIST *)


procedure DEL_MMLIST(c_mmlist: ptr_mmlist; entry: absptr);

  label 10;

  var
    f_mmlist: ptr_mmlist;
    i: int2;

  begin
    f_mmlist:=c_mmlist;
    repeat
      with f_mmlist^ do
        begin
          for i:=1 to maxsize do if ptr[i] = entry then
                                   begin
                                     ptr[i]:=ord(nil);
                                     size:=size-1;
                                     goto 10;
                                   end;
          f_mmlist:=pointer(chain.fwd_link + b_sysglobal_ptr);
        end; (*with*)
     until f_mmlist = c_mmlist;
10: (* quick exit for removal *)
  end;(*DEL_MMLIST *)


procedure NEXT_IN_MMLIST(var c_mmlist: ptr_mmlist; var i: int2);

  label 10;

  var
    s_mmlist: ptr_mmlist;
    j: int2;

  begin
    s_mmlist:=c_mmlist; (* stopper *)
    repeat
      with c_mmlist^ do
        begin
          if size > 0
            then for j:=i to maxsize do if ptr[j] <> ord(nil) then goto 10;
          c_mmlist:=pointer(chain.fwd_link + b_sysglobal_ptr);(*next list*)
          i:=1;
        end;(*with*)
    until c_mmlist = s_mmlist;

    (* no entry found *)
    j:=0;

10: (*QUICK EXIT WHEN NEXT ENTRY FOUND *)
    i:=j;

  end;(*NEXT_IN_MMLIST*)


procedure ADDTO_SRB(* c_sdb_ptr: sdb_ptr; c_pcb: absptr *);

  (*******************************************)
  (*                                         *)
  (* Removes an entry from the srb list.     *)
  (* This function should never be done on a *)
  (* code segment.                           *)
  (*******************************************)
  var
    c_mmlist: ptr_mmlist;

  begin
    c_mmlist:=pointer(c_sdb_ptr^.srbRP+b_sysglobal_ptr);
    ADDTO_MMLIST(c_mmlist,c_pcb);
  end; (* ADDTO_SRB *)


procedure DEL_SRB(* c_sdb_ptr: sdb_ptr; c_pcb: absptr *);

  (*******************************************)
  (*                                         *)
  (* Removes an entry from the srb list.     *)
  (* This function should never be done on a *)
  (* code segment.                           *)
  (*******************************************)
  var
    c_mmlist: ptr_mmlist;

  begin
    c_mmlist:=pointer(c_sdb_ptr^.srbRP+b_sysglobal_ptr);
    DEL_MMLIST(c_mmlist,c_pcb);
  end; (* DEL_SRB *)


procedure CLEAR_SRB(* c_sdb_ptr: sdb_ptr *);

  (****************************************)
  (*                                      *)
  (* The need_mem flag will be set for all*)
  (* processes chained to this sdb via the*)
  (* srb_list.                            *)
  (*                                      *)
  (****************************************)

  var
    c_pcb: ptr_pcb;
    c_mmlist: ptr_mmlist;
    i: int2;

  begin
    if c_sdb_ptr^.sdbtype <> code
      then if c_sdb_ptr^.srbRP <> 0 then
             begin
               c_mmlist:=pointer(c_sdb_ptr^.srbRP+b_sysglobal_ptr);
               i:=1;
               repeat
                 NEXT_IN_MMLIST(c_mmlist,i);
                 if i <> 0 then
                   begin (* an entry was found *)
                     with c_mmlist^ do
                       begin
                         c_pcb:=pointer(ptr[i]);
                         c_pcb^.need_mem := true;
                         (* clear the entry from the mmlist *)
                         ptr[i]:=ord(nil);
                         size:=size-1;
                       end;
                   end;
               until i=0;
             end;
  end; (* CLEAR_SRB *)


procedure ABORT_VIA_SRB(* c_sdb_ptr: sdb_ptr; reason: term_types *);

  (****************************************)
  (*                                      *)
  (* Abort all processes chained to this  *)
  (* sdb via the srb_list.                *)
  (*                                      *)
  (****************************************)

  var
    f_mmlist, c_mmlist: ptr_mmlist;
    f, i: int2;

  begin
    f_mmlist:=nil;
    if c_sdb_ptr^.sdbtype <> code
      then if c_sdb_ptr^.srbRP <> 0 then
             begin
               c_mmlist:=pointer(c_sdb_ptr^.srbRP+b_sysglobal_ptr);
               f:=0;
               i:=1;
               repeat
                 NEXT_IN_MMLIST(c_mmlist,i);
                 if (c_mmlist=f_mmlist) and
                    (f=i) then i:=0;(*we are back at the beginning *)
                 if i <> 0 then
                   begin (*entry found*)
                     if f_mmlist = nil then
                       begin (* first time an entry found *)
                         f_mmlist:=c_mmlist;
                         f:=i;
                       end;
                     ABORTPROCESS(pointer(c_mmlist^.ptr[i]),true,reason);
                     i:=i+1;
                   end;
               until i=0;
             end;
  end; (* ABORT_VIA_SRB *)



  procedure Prepare_Process (* pcb_ptr : ptr_PCB;  var status : launch_status *);

       (**********************************************************)
       (*                                                        *)
       (* Description:                                           *)
       (*   Prepare the specified process for execution by       *)
       (*   ensuring that any memory scheduling required by the  *)
       (*   process has been completed.                          *)
       (*                                                        *)
       (* Input Parameters:                                      *)
       (*   pcb_ptr : ptr to the PCB of the process whose        *)
       (*             memory state is to be checked.             *)
       (*                                                        *)
       (* Output Parameters:                                     *)
       (*   status : indicates what action can be taken by the   *)
       (*            Scheduler in trying to schedule the         *)
       (*            specified process for execution.            *)
       (*                                                        *)
       (* Side Effects:                                          *)
       (*   Unblocks the memory manager process if memory        *)
       (*   scheduling must be done for the specified process.   *)
       (*                                                        *)
       (* Special Conditions of Use:                             *)
       (*   This procedure is intended to be used only by the    *)
       (*   Scheduler.  It executes on the Supervisor's stack.   *)
       (*                                                        *)
       (* Error Conditions:                                      *)
       (*   None.                                                *)
       (*                                                        *)
       (**********************************************************)

  var
    memSched_reqd : boolean;              { true if memory scheduling required }
    slocSdb_ptr: sdb_ptr;                 { ptr to sdb for syslocal of process }
    sysloc_ptr : slocal_ptr;              { ptr to syslocal of process }
    prep_mrbt_ptr : mrbt_ptr;             { ptr to MRBT of process }
    segSdb_ptr : sdb_ptr;                 { ptr to sdb for various segments }
    segNum : int2;                        { loop index for checking data space }
    domNum : domainRange;                 { loop index for checking domains }
    c_mmrb_ptr : mmrb_ptr;                { ptr to MMRB }

  begin
    memSched_reqd := false;

    { map process's syslocal segment 1st }
    slocSdb_ptr := pointer(pcb_ptr^.slocal_sdbRP + b_sysglobal_ptr);

    if not slocSdb_ptr^.sdbstate.memoryF or (slocSdb_ptr^.newlength <> 0) then
      memSched_reqd := true
    else
      begin  { process's syslocal is in memory }
        if slocSdb_ptr^.sdbstate.overlayF then
          Save_OC (slocSdb_ptr, ord(pcb_ptr))
        else
          Addto_SRB (slocSdb_ptr, ord(pcb_ptr));
        Map_Syslocal (pcb_ptr);
        sysloc_ptr := pointer(b_syslocal_ptr);
        prep_mrbt_ptr := pointer(sysloc_ptr^.mrbt_addr);

        if sysloc_ptr^.csreq_addr <> ord(nil) then
          begin{ faulted for a segment }
            memSched_reqd := true;
          end
        else
          begin  { check data space }
            segNum := minsysldsnmmu;

            repeat
              if prep_mrbt_ptr^[segNum].sdbRP <> 0 then
                begin  { a segment is mapped }
                  segSdb_ptr := pointer(prep_mrbt_ptr^[segNum].sdbRP +
                                          b_sysglobal_ptr);
                  with segSdb_ptr^ do
                    if sdbtype <> code then
                      if not sdbstate.memoryF or (newlength <> 0) then
                        { the segment is absent or a size change is required }
                        memSched_reqd := true
                      else if sdbstate.overlayF then
                        Save_OC (segSdb_ptr, ord(pcb_ptr))
                      else
                        Addto_SRB (segSdb_ptr, ord(pcb_ptr));
                end { segment is mapped };

              segNum := segNum + 1
            until memSched_reqd or (segNum > shrsegmmu);

            if grow_sysglobal then memSched_reqd := true   { due to nonPreempt }
          end { check data space }
      end { syslocal in memory };

    if not memSched_reqd then
      begin  { process can be launched }
        pcb_ptr^.need_mem := false;
        status := ok_launch;

        { make sure data segments of process are mapped correctly  }
        { in all domains owned by the process                      }
        for domNum := 0 to maxDomain do
          if DCT[domNum].owner = pcb_ptr^.glob_id then
            Map_Space (ldsn1mmu, shrsegmmu, domNum)
      end
    else
      begin  { process requires memory scheduling }
        c_mmrb_ptr := pointer(mmrb_addr);
        with c_mmrb_ptr^ do
          if not memmgr_busyF then
            begin  { memory manager is free }

              { unblock memory manager process }
              Signal_sem (memmgr_sem);
              memmgr_busyF:=true;

              req_pcb_ptr := pcb_ptr;
              status := mm_launch
            end
          else if req_pcb_ptr = pcb_ptr then
            status := busy_thisPCB
          else
            status := busy_otherPCB
      end { process requires memory }
  end { Prepare_Process };



procedure Exp_SegDiskSize (c_sdb_ptr : sdb_ptr);  external;


function EXPAND_POOLSPACE(* size: int2; b_area: absptr): boolean *);

       (**********************************************************)
       (*                                                        *)
       (* Description:                                           *)
       (*    The free pool whose base is at b_area will be       *)
       (*    expanded by size + 2 memory pages.  This procedure  *)
       (*    should only be called by GETSPACE.                  *)
       (*                                                        *)
       (* Input Parameters:                                      *)
       (*    b_area - the base of the free pool to be expanded.  *)
       (*    size   - the number of bytes of free space needed.  *)
       (*                                                        *)
       (* Output : Return true if expansion valid: false if not  *)
       (*                                                        *)
       (**********************************************************)

  var
    c_mmu: int2;
    c_pcb: ptr_pcb;
    c_sdb: sdb_ptr;
    c_syslocal_ptr: slocal_ptr;
    c_mrbt: mrbt_ptr;
    c_mmrb: mmrb_ptr;
    int_level: intson_type;
    sl, growOK: boolean;

  procedure GROW_SPACE;
    (* perform the actual expansion of the free space pool *)

    var
      offset1: int4;
      base: absptr;
      poolsize: int2;
      poollimit: int2;                                                    (*OS159*)

    begin
      with c_sdb^ do
        begin
          base:=MMU_BASE(c_mmu);
          offset1:=(b_area - base) div mempgsize;
          poolsize:=memsize-offset1+size;(*new pool size in pages*)
          if sl then poollimit:=64  (*syslocal limited to 32k bytes *)    (*OS159*)
                                    (*due to relptr*)                     (*OS159*)
                else poollimit:=128;(*other pools limited to 64k bytes*)  (*OS159*)
          if poolsize < poollimit then                                    (*OS159*)
            begin (* room to grow *)
              growOK:=true;
              if poolsize+2 < poollimit then size:=size+2;(* expand by 2*)(*OS159*)
                                                          (* extra pages*)(*OS159*)
              (* mark segment to be expanded and enter scheduler so *)
              (* memory manager can run.                            *)
              poolsize:=memsize + size;                                   (*WH02*)
              newlength:=poolsize;                                        (*WH02*)
              base:=base+ord4(memsize)*hmempgsize*2;
              (* notify all referencing processes of size change *)
              CLEAR_SRB(c_sdb);
              ENTER_SCHEDULER;

              (* the segment has been expanded *)
              if memsize >= poolsize then
                begin  { expansion occurred successfully }
                  NEWSPACE(base,mempgsize*size,b_area);                   (*WH02*)
                  if (length > disclength) and (sdbtype = data) then
                    Exp_SegDiskSize (c_sdb)
                end { expansion occurred }
            end;
        end;(*with*)
    end;(*GROW_SPACE*)

  begin
    growOK:=false;
    size:=(size + mempgsize - 1) div mempgsize; (* convert to pages *)
    c_pcb:=pointer(c_pcb_ptr);
    c_mmu:=b_area div maxmmusize;
    if c_mmu = syslocmmu then
      begin (* expanding pool in syslocal *)
        c_sdb:=pointer(c_pcb^.slocal_sdbRP + b_sysglobal_ptr);
        sl:=true;
        GROW_SPACE;
      end
     else
      begin
        sl:=false;
        if c_mmu = sysglobmmu then
          begin (* expanding pool in sysglobal *)
            (* signal memory manager to expand sysglobal space *)
            (* if the request has not already been made.       *)

            growOK:=true;
            c_mmrb:=pointer(mmrb_addr);
            with c_mmrb^ do
              begin
                INTSOFF(allints,int_level);
                if not grow_sysglobal then
                  begin (* need to make request *)
                    grow_sysglobal:=true;
                    INTSON(int_level);
                    inc_sysglobal:=size;
                    if not memmgr_busyF then
                      begin
                        SIGNAL_SEM(memmgr_sem);
                        memmgr_busyF:=true;
                      end;
                  end
                 else
                  begin (* request already made *)
                    INTSON(int_level);
                  end;
              end;(*with*)
          end
         else
          begin (* expanding pool in other area *)
            if (c_mmu < fMRdataMMU) or (c_mmu > lMRdataMMU) then
              begin (*cannot expand special memory resident data segs*)
                c_syslocal_ptr:=pointer(b_syslocal_ptr);
                c_mrbt:=pointer(c_syslocal_ptr^.mrbt_addr);
                if c_mrbt^[c_mmu].sdbRP <> 0 then
                  begin (* a segment is mapped *)
                    c_sdb:=pointer(c_mrbt^[c_mmu].sdbRP + b_sysglobal_ptr);
                    if (c_sdb^.sdbtype = data) then GROW_SPACE;
                  end;
              end;
          end;
      end;
    expand_poolspace:=growOK;
  end;(*EXPAND_POOLSPACE*)


procedure ADJ_IO_CNT(* inc_io_cntF: boolean; c_sdb: absptr *);

       (**********************************************************)
       (*                                                        *)
       (* Description:                                           *)
       (*    Increment/decrement the number of i/o requests for  *)
       (*    the segment.  If the io_count = 0 then a check for  *)
       (*    a waiting pcb will be made.                         *)
       (*                                                        *)
       (* Input Parameters:                                      *)
       (*    inc_io_cntF - true if the count is to be incremented*)
       (*    c_sdb       - the segment to be adjusted.           *)
       (*                                                        *)
       (**********************************************************)

var
  int_level: intson_type;
  c_sdb_ptr: sdb_ptr;
  inc: int2;

begin
  c_sdb_ptr:=pointer(c_sdb);
  if inc_io_cntF then inc:=1
                 else inc:=-1;
  INTSOFF(allints,int_level);
  c_sdb_ptr^.io_count:=c_sdb_ptr^.io_count+inc;
  INTSON(int_level);
end;(* ADJ_IO_CNT *)


procedure CVT_BUFF_ADDR(* var errnum: int2; mem_writeF: boolean; buffaddr: absptr;
                          byte_cnt: int4; var ordsdb: absptr; var offset: int4 *);

       (**********************************************************)
       (*                                                        *)
       (* Description:                                           *)
       (*    Convert the buffaddr into a seg relative offset and *)
       (*    seg ptr.  In addition, verify that the i/o operation*)
       (*    is valid for the specified segment.                 *)
       (*                                                        *)
       (* Input Parameters:                                      *)
       (*    mem_writeF - true if write memory(read i/o device)  *)
       (*    buffaddr - the address to be transformed into seg   *)
       (*               relative offset and seg ptr              *)
       (*    byte_cnt - the number of bytes to transfer          *)
       (*                                                        *)
       (* Output Parameters:                                     *)
       (*    errnum   - the success of the operation             *)
       (*    ordsdb   - ptr to the segment enclosing buffaddr    *)
       (*    offset   - the seg relative offset within c_sdb^ of *)
       (*               buffaddr                                 *)
       (*                                                        *)
       (* Error Conditions:                                      *)
       (*    errnum = 0 Successful transformation (access valid) *)
       (*           = e_badaddr (The i/o access is invalid)      *)
       (*                                                        *)
       (**********************************************************)

var
  c_syslocal_ptr: slocal_ptr;
  c_mrbt: mrbt_ptr;
  c_mmu: int2;
  c_sdb: sdb_ptr;

begin
  errnum:=e_mmbase+e_badaddr;
  c_syslocal_ptr:=pointer(b_syslocal_ptr);
  c_mmu:=buffaddr div maxmmusize; (* get the mmu which maps this buffer *)
  if c_mmu < minsysldsnmmu
    then c_mrbt:=pointer(s_mrbt_addr) (* buffer is in system space *)
    else c_mrbt:=pointer(c_syslocal_ptr^.mrbt_addr); (* buffer is in user space *)

  (* find the mmu which maps the base of the segment  *)
  while (c_mrbt^[c_mmu].sdbRP = 0) and (c_mmu >= 0) do c_mmu:=c_mmu-1;
  if c_mmu >= 0 then
    begin (* found the sdb that should map bufaddr *)
      with c_mrbt^[c_mmu] do
        begin
          if (access <> mmuabsent) (* accessible segment *)
            then if not( mem_writeF and
                       ( access = mmureadonly)) then (* correct access *)
                   begin (* check if in limits of segment *)
                     c_sdb:=pointer(sdbRP + b_sysglobal_ptr);
                     ordsdb:=ord(c_sdb);
                     offset:=buffaddr - ord4(c_mmu) * maxmmusize;
                     if access = mmustack
                       then offset:=offset + ord4(c_sdb^.memsize)*hmempgsize*2
                                           - maxmmusize;
                     if (offset+byte_cnt) <= (ord4(c_sdb^.memsize)*hmempgsize*2)
                       then errnum:=0; (* valid access *)
                   end;
        end;(*with*)
    end;
end; (* CVT_BUFF_ADDR *)


procedure ADJ_DATA_LOCKCOUNT(increment: int2);

       (**********************************************************)
       (*                                                        *)
       (* Description:                                           *)
       (*    The lockcount of the currently bound data segments  *)
       (*    will be adjusted by the value of increment.         *)
       (*                                                        *)
       (* Special Conditions of Use                              *)
       (*    It is assumed that no binding or unbinding of data  *)
       (*    segments will occur between locking and unlocking of*)
       (*    the segments.                                       *)
       (*                                                        *)
       (**********************************************************)

   var
     c_syslocal: slocal_ptr;
     c_mrbt: mrbt_ptr;
     c_sdb: sdb_ptr;
     c_pcb: ptr_pcb;
     seg_numb: int2;

   begin
     (* lock/unlock syslocal segment *)
     c_pcb:=pointer(c_pcb_ptr);
     if c_pcb <> nil then
       begin
         c_sdb:=pointer(c_pcb^.slocal_sdbRP+b_sysglobal_ptr);
         with c_sdb^ do lockcount:=lockcount+increment;

         (* lock/unlock user's data segments *)
         c_syslocal:=pointer(b_syslocal_ptr);
         c_mrbt:=pointer(c_syslocal^.mrbt_addr);
         for seg_numb:=minsysldsnmmu to shrsegmmu do
           begin
             with c_mrbt^[seg_numb] do
               begin
                 if sdbRP <> 0 then
                   begin (* a segment is bound *)
                     c_sdb:=pointer(sdbRP+b_sysglobal_ptr);
                     with c_sdb^ do
                       if sdbtype <> code then lockcount:=lockcount+increment;
                   end;
               end;
           end;(*for*)
       end;
   end;(*ADJ_DATA_LOCKCOUNT*)


procedure UNLOCKDATA;

       (**********************************************************)
       (*                                                        *)
       (* Description:                                           *)
       (*    The currently bound data segments will be unlocked  *)
       (*    from memory.                                        *)
       (*                                                        *)
       (* Special Conditions of Use                              *)
       (*    It is assumed that no unbinding of data segs has    *)
       (*    occurred since the data segments were locked.       *)
       (*                                                        *)
       (**********************************************************)

   begin
     ADJ_DATA_LOCKCOUNT(-1);
   end;

procedure LOCKDATA;

       (**********************************************************)
       (*                                                        *)
       (* Description:                                           *)
       (*    The currently bound data segments will be locked    *)
       (*    into memory.  These segments cannot be swapped until*)
       (*    they are unlocked.                                  *)
       (*                                                        *)
       (* Special Conditions of Use                              *)
       (*    It is assumed that no unbinding of data segs will   *)
       (*    occur before the data segments are unlocked.        *)
       (*                                                        *)
       (**********************************************************)

   begin
     ADJ_DATA_LOCKCOUNT(1);
   end;



function MMBUSY(*: boolean*);

  (***************************)
  (* Returns true if memory  *)
  (* Manager is busy         *)
  (***************************)

  var
    c_mmrb: mmrb_ptr;

  begin
    c_mmrb:=pointer(mmrb_addr);
    mmbusy:=c_mmrb^.memmgr_busyF;
  end; (*mmbusy*)


procedure SET_MMBUSY(* busy: boolean *);

  (****************************)
  (* Will set the state of the*)
  (* memmgr_busyF to "busy"   *)
  (****************************)

  var
    c_mmrb: mmrb_ptr;

  begin
    c_mmrb:=pointer(mmrb_addr);
    c_mmrb^.memmgr_busyF:=busy;
  end; (*set_mmbusy*)


procedure S_SWAP_SPACE(* var space_swapable, space_locked: int4 *);

   (***************************************************)
   (*  Will accumulate the amount of system swap space*)
   (*  which is currently swapable and the amount that*)
   (*  is currently memory resident.                  *)
   (***************************************************)

   var
     c_mmrb: mmrb_ptr;
     c_sdb, l_sdb: sdb_ptr;
     swapable, locked: int2;

   begin
     c_mmrb:=pointer(mmrb_addr);
     c_sdb:=pointer(ord(c_mmrb^.head_sdb.memchain.fwd_link));
     l_sdb:=@c_mmrb^.tail_sdb;
     swapable:=0;
     locked:=0;

     while c_sdb <> l_sdb do
       begin
         with c_sdb^ do
           begin
             if (sdbtype = free) or sdbstate.releaseF
               then swapable:=swapable + memsize
               else if sdbtype = code
                      then if lockcount = 0
                             then swapable:=swapable + memsize
                             else locked:=locked + memsize
                      else if (lockcount = 0) and
                              (disclength >= length)
                             then swapable:=swapable + memsize
                             else locked:=locked + memsize;
             c_sdb:=pointer(ord(memchain.fwd_link));
           end;(*with*)
       end;(*while*)
     space_swapable:=ord4(swapable)*hmempgsize*2;
     space_locked:=ord4(locked)*hmempgsize*2;
   end;(*S_SWAP_SPACE*)


procedure PROCS_DATA_NEEDS(* var data_swapable, data_locked: int4 *);

   (**************************************************)
   (*  Will accumulate the total data space required *)
   (*  by the current process.  This includes all    *)
   (*  bound data segments, stack, shared segment,   *)
   (*  and syslocal data segment.  The data space is *)
   (*  accumulated as swapable and locked data needs.*)
   (**************************************************)

   var
     c_syslocal_ptr: slocal_ptr;
     c_mrbt: mrbt_ptr;
     c_pcb: ptr_pcb;
     c_sdb: sdb_ptr;
     i: int2;

   begin
     data_swapable:=0;
     data_locked:=0;
     c_syslocal_ptr:=pointer(b_syslocal_ptr);
     c_mrbt:=pointer(c_syslocal_ptr^.mrbt_addr);
     c_pcb:=pointer(c_pcb_ptr);

     (* space for syslocal data segment *)
     c_sdb:=pointer(c_pcb^.slocal_sdbRP + b_sysglobal_ptr);
     with c_sdb^ do
       begin
         if (lockcount=0) and (disclength >= length)
           then data_swapable:=data_swapable+length
           else data_locked:=data_locked+length;
       end;

     (* accumulate needs for bound data segments, stack, and shared seg *)
     for i:=minsysldsnmmu to shrsegmmu do
       begin
         if c_mrbt^[i].sdbRP <> 0 then
           begin (* seg defined by mrbt *)
             c_sdb:=pointer(c_mrbt^[i].sdbRP + b_sysglobal_ptr);
             with c_sdb^ do
               begin
                 if sdbtype <> code
                   then if (lockcount=0) and (disclength >= length)
                          then data_swapable:=data_swapable+length
                          else data_locked:=data_locked+length;
               end;
           end;
       end;(*for*)
   end;(*PROCS_DATA_NEEDS*)


function DATA_LOCKOUT(* c_sdb: sdb_ptr; boundseg: boolean): boolean *);

   (*************************************************)
   (* Return true if the current data segment needs *)
   (* of the current process + c_sdb would          *)
   (* reduce the amount of swap space for code segs *)
   (* to less than 64KB.                            *)
   (*************************************************)

   var
     sys_swapable, sys_locked, pdata_swapable, pdata_locked: int4;
     memoryRes: boolean;
     len: int4;

   begin
     if init_in_progress
       then data_lockout:=false (* don't worry about it during startup *)
       else
         begin
           S_SWAP_SPACE(sys_swapable,sys_locked);
           PROCS_DATA_NEEDS(pdata_swapable,pdata_locked);
           with c_sdb^ do
             begin
               if (sdbstate.memoryF) and
                  ((lockcount > 0) or
                   ((sdbtype <> code) and (length > disclength)))
                 then memoryRes:=true
                 else memoryRes:=false;
               if boundseg or memoryRes
                 then sys_swapable:=sys_swapable+length;
               if newlength <> 0
                 then len:=ord4(newlength)*hmempgsize*2
                 else len:=length;
             end;
           data_lockout:=
           (sys_swapable - pdata_swapable - len) < 65536;
         end;
   end;(*DATA_LOCKOUT*)


procedure MAKE_MRDATA(* var errnum: integer; size: longint; var addr: absptr *);

   (*************************************************)
   (* Make a memory resident data segment of size   *)
   (* bytes.  The segment will be mapped only in    *)
   (* domain 0.  Its max size is 128kb.             *)
   (* Error returns:                                *)
   (*   = 0 - OK                                    *)
   (*   = 553 - no mmu available                    *)
   (*************************************************)

   var
     c_mmrb: mmrb_ptr;
     i: integer;
     d_addr: addrdisc;
     c_sdb: sdb_ptr;
     c_mrbt: mrbt_ptr;

   begin
     errnum:=e_mmbase+e_noMRdata;
     c_mmrb:=pointer(mmrb_addr);
     i:=fMRdataMMU;
     repeat
       with c_mmrb^ do
         if mrdata[i] = nil then
           begin
             errnum:=0;
             size:=((size+mempgsize-1) div mempgsize);
             d_addr.fid:=0;
             d_addr.offset:=0;
             (*Build memory resident sdb*)
             BLD_SEG(data,0,size,d_addr,0,0,c_sdb);
             mrdata[i]:=c_sdb;
             (*Force seg into memory*)
             while not c_sdb^.sdbstate.memoryF do CHECK_DS(c_sdb);
             (*Clear this process from segment SRB*)
             DEL_SRB(c_sdb,c_pcb_ptr);
             (*setup system mrbt to point to the segment*)
             c_mrbt:=pointer(s_mrbt_addr);
             with c_mrbt^[i] do
               begin
                 access:=mmuReadWrite;
                 sdbRP:=ord(c_sdb)-b_sysglobal_ptr;
               end;
             (*map segment into Domain 0*)
             MAP_SEGMENT(c_sdb,i,0,mmuReadWrite);
             addr:=ord4(i)*maxmmusize;
             i:=lMRdataMMU; (*force end to repeat loop*)
           end;
       i:=i+1;
     until i > lMRdataMMU;
   end;(*MAKE_MRDATA*)


procedure REL_MRDATA(* var errnum: integer; addr: absptr*);

   (*************************************************)
   (* Release the memory resident data segment which*)
   (* corresponds with the logical address 'addr'.  *)
   (* Error returns:                                *)
   (*   = 0 - OK                                    *)
   (*   = 553 - addr is invalid                     *)
   (*************************************************)

   var
     c_mmrb: mmrb_ptr;
     i: integer;
     c_sdb: sdb_ptr;
     c_mrbt: mrbt_ptr;

   begin
     errnum:=e_mmbase+e_nomrdata;
     i:=addr div maxmmusize;
     c_mmrb:=pointer(mmrb_addr);
     with c_mmrb^ do
       if (i >= fMRdataMMU) and (i <= lMRdataMMU) then
         begin
           c_sdb:=mrdata[i];
           mrdata[i]:=nil;
           (*clear system mrbt *)
           c_mrbt:=pointer(s_mrbt_addr);
           with c_mrbt^[i] do
             begin
               access:=mmuAbsent;
               sdbRP:=0;
             end;
           if c_sdb <> nil then
             begin
               REL_SEG(c_sdb);
               errnum:=0;
             end;
         end;
   end;(*REL_MRDATA*)


ÿ