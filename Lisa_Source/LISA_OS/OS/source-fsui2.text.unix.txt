
(*********************************************************************************)
(*                                                                               *)
(*  (c) copyright 1983, 1984  Apple Computer Inc.                                *)
(*                                                                               *)
(*********************************************************************************)

{
Table of Contents of this module:

  1.  FLUSH
  2.  LOOKUP
  3.  INFO
  4.  ALLOCATE
  5.  TRUNCATE
  6.  COMPACT
- 7.  FLUSH_PIPE
  8.  RENAME_ENTRY

Note:  - indicates not callable from applications environment.

}
(*$S fs2 *)
procedure FLUSH (*var ecode:error; refnum:integer*);

label 1;

const parmbytes = 6;    (* calculated by hand for each procedure *)
      perrbase = 47;

var refdbaddr : absptr;
    refdbptr : fref_ptr;
    h : hentry;

begin


(*$IFC DEBUG2*)
  if trace (FS, 70) then
  begin
    writeln ('FLUSH:  request to flush buffers for refnum ', refnum);
  end;
(*$ENDC*)

  ecode := 0;

  (* A refnum of -1 results in a file system flush *)

  if refnum <> -1 then
  begin
    chk_refnum (ecode, refnum, frefdb, refdbaddr);
    if ecode <> 0 then
    begin
      ecode := E4_REFNUM_INVALID;
      goto 1;
    end;

    refdbptr := pointer (refdbaddr);   (* make a pointer to the fs_refdb *)

    with refdbptr^ do
    if configinfo[refdb_devnum]^.blockstructured then with fcbptr^ do
    begin
      wait_sem (semgp, []);        (* protect hint entry modification *)

      DTA := timestamp;   (* update time accessed *)
      if time_dirty then
      begin
        (* let process mgmt see if this is a program file requiring unloading *)
        (* only do this if the file has been modified since the last flush *)
        Free_LPs(fcbptr, -1);

        DTM := DTA;  (* and time modified if appropriate *)
        time_dirty := FALSE;
      end;

      (* read and update the hint entry *)

      hentry_io (ecode, curdev, sent.hintaddr, h, readop);
      if ecode <= 0 then
      begin
        h.DTC := DTC;    (* date/time created                      *)
        h.DTA := DTA;    (* date/time last accessed                *)
        h.DTM := DTM;    (* date/time last modified                *)
        h.DTB := DTB;    (* date/time last backup copy was made    *)
        h.DTS := DTS;    (* date/time last scavenge                *)
                  (* write hint entry *)
        hentry_io (ecode, curdev, sent.hintaddr, h, writeop);
                  (* write the small file map for new files *)
        if not old_volume(curdev) then
          fmap_io(ecode, curdev, sent.hintaddr, 0, @fmap, writeop);
      end;
      slist_io (ecode, curdev, sfilenum, writeop, sent);
      signal_sem (semgp);
    end;
  end;

  flushfs (ecode);

1:

  {if (ecode > 0) and (ecode < 100) then
                            ecode := EXTERRBASE + UIERRBASE + PERRBASE + ecode;}


(*$IFC DEBUG2*)
  if trace (FS, 70) then
  begin
    writeln ('FLUSH:  exiting with error code of ',ecode);
  end;
(*$ENDC*)

end;   (* of procedure FLUSH *)





  {$S fs2}
  procedure  FCB_Report (     ptrFCB  : SFCB_ptr;
                          var InfoRec : FS_Info );

  begin
      with ptrFCB^, InfoRec do
      begin
        name := fname;
        {dir_path}
        machine_id := mid;
        {fs_overhead}
        result_scavenge := ptrFCB^.result_scavenge;
        passwd_present := (length(ptrFCB^.password) <> 0);
        size := sent.filesize;
        psize := phys_size;
        {lpsize}
        ftype := ptrFCB^.ftype;
        {etype}
        DTC := ptrFCB^.DTC;
        DTA := ptrFCB^.DTA;
        DTM := ptrFCB^.DTM;
        DTB := ptrFCB^.DTB;
        DTS := ptrFCB^.DTS;
        {refnum}
        {fmark}
        {acmode}
        nreaders := ptrFCB^.nreaders;
        nwriters := ptrFCB^.nwriters;
        nusers := ptrFCB^.nusers;
        fuid := fileuid;
        user_type := ptrFCB^.user_type;
        user_subtype := ptrFCB^.user_subtype;
        system_type := ptrFCB^.system_type;
        {eof}
        safety_on := sswitch;
        kswitch := ptrFCB^.kswitch;
        private := private_in_use;
        locked := file_locked;
        protected := sfcb_protected;
        master_file := sfcb_master;
        file_scavenged := scav_flag;
        file_closed_by_OS := OS_close_flag;
        file_left_open := file_still_open;
        file_portion := ptrFCB^.file_portion;
        build_info := ptrFCB^.build_info;
        fs_overhead := fsOverhead;
      end{with};
  end{proc FCB_Report};




  {$S fs2}
  procedure  Dir_Report ( var DirRec  : ObjRec;
                          var InfoRec : Q_Info );

  var
       parID : NodeIdent;

  begin
      ClearMem(ord(@InfoRec), sizeof(InfoRec));
      with DirRec, InfoRec do
      begin
        DecompKey(@key, @name, parID);
        etype := DirRec.eType;
        case eType of
          fileentry:
          begin
            DTC := fileDTC;
            DTM := fileDTM;
            size := DirRec.size;
            psize := physSize;
            fs_overhead := fsOvrhd;
            master := flags[fMaster];
            protected := flags[fProtected];
            safety := flags[fSafety];
            left_open := flags[fCrashed];
            scavenged := flags[fScavenged];
            closed_by_OS := flags[fClosed];
          end;
          direntry:
          begin
            DTC := dirDTC;
            DTM := dirDTC;
          end;
        end{case};
      end{with};
  end{proc Dir_Report};




  {$S fs2}
  procedure  Config_Report ( ptrConfig   : ptrdevrec;
                             var InfoRec : FS_Info );

  var
       locl_ecode : integer;
       ptrExt     : ^ext_diskconfig;

  begin
      with ptrConfig^, InfoRec do
      begin
        name := devname;
        dir_path := concat('-', devname);
        slot_no := ptrConfig^.slot_no + 1;
        iochannel := ptrConfig^.iochannel + 1;
        devicenumb := device_no + 1;
        devt := ptrConfig^.devt;
        if devt = diskdev then
          UP(locl_ecode, ptrConfig, nil);
        blockstructured := ptrConfig^.blockstructured;
        if blockstructured then
        begin
          ptrExt := pointer(ext_addr);
          with ptrExt^ do
          begin
            vol_size := num_bloks;
            fs_size := vol_size - fs_strt_blok;
            InfoRec.removable := ptrExt^.removable;
          end{with};
          ejectable := ptrExt^.ejectable;
        end{if};
      end{with};
  end{proc Config_Report};





  {$S fs2}
  procedure  DCB_Report (     ptrDCB  : DCBptr;
                          var InfoRec : FS_Info );

  var
       ptrMDDF : MDDFptr;

  begin
      with ptrDCB^, InfoRec do
      begin
        name := ptrDCB^.name;
        opencount := ptrDCB^.opencount;
        privatedev := private;
        remote := remotedev;
        lockeddev := locked;
        mount_pending := ptrDCB^.mount_pending;
        unmount_pending := ptrDCB^.unmount_pending;
        vol_left_mounted := left_mounted;
      end{with};

      ptrMDDF := ptrDCB^.MDDFdata;
      if ptrMDDF <> pointer(BADPTR1) then
      begin
        with ptrMDDF^, InfoRec do
        begin
          volname := ptrMDDF^.volname;
          password := ptrMDDF^.password;
          passwd_present := (length(password) <> 0);
          fsversion := ptrMDDF^.fsversion;
          volid := ptrMDDF^.volid;
          volnum := ptrMDDF^.volnum;
          vol_size := geography.lastblock + 1;
          fs_size := geography.lastfspage + 1;
          blocksize := ptrMDDF^.blocksize;
          datasize := ptrMDDF^.datasize;
          clustersize := cluster_size;
          filecount := ptrMDDF^.filecount;
          freecount := ptrMDDF^.freecount;
          DTVC := DT_created;
          DTCC := DT_copy_created;
          DTVB := DT_copied;
          DTVS := DT_scavenged;
          machine_id := init_machine_id;
          overmount_stamp := ptrMDDF^.overmount_stamp;
          master_copy_id := master_machine_id;
          copy_thread := ptrMDDF^.copy_thread;
          backup_volid := ptrMDDF^.backup_volid;
          vol_sequence := vol_seq_no;
          label_size := flabel_size;
          fs_overhead := ptrMDDF^.fs_overhead;
          result_scavenge := ptrMDDF^.result_scavenge;
          boot_code := ptrMDDF^.boot_code;
          boot_environ := ptrMDDF^.boot_environ;
          scavenge_flag := vol_scavenged;
          copy_flag := tbt_copied;
        end{with};
      end{if};
  end{proc DCB_Report};





  {$S fs2}
  procedure  Hentry_Report ( var HintEntry : hentry;
                             var InfoRec   : FS_Info );

  begin
      with HintEntry, InfoRec do
      begin
        name := HintEntry.name;
        ftype := HintEntry.ftype;
        machine_id := HintEntry.machine_id;
        protected := HintEntry.protected;
        master_file := HintEntry.master;
        passwd_present := (length(HintEntry.password) <> 0);
        DTC := HintEntry.DTC;
        DTA := HintEntry.DTA;
        DTM := HintEntry.DTM;
        DTB := HintEntry.DTB;
        DTS := HintEntry.DTS;
        fuid := HintEntry.unique_ID;
        user_type := HintEntry.user_type;
        user_subtype := HintEntry.user_subtype;
        system_type := HintEntry.system_type;
        result_scavenge := HintEntry.result_scavenge;
        safety_on := HintEntry.safety_on;
        file_scavenged := HintEntry.scavenged;
        file_closed_by_OS := closed_by_OS;
        file_left_open := file_open;
        file_portion := HintEntry.file_portion;
        build_info := HintEntry.build_info;
        fs_overhead := fsOverhead;
      end{with};
  end{proc Hentry_Report};





  {$S fs2 }
  procedure GLookup ( var ecode   : error;
                      var path    : pathname;
                          quick   : boolean;
                      var InfoRec : FS_Info );

  label 1, 2;

  var
       device  : integer;
       volPath : pathname;
       ptrDCB  : DCBptr;
       DirRec  : ObjRec;
       C       : centry;
       parID   : NodeIdent;
       nodeID  : NodeIdent;
       ptrFCB  : SFCB_ptr;
       S       : s_entry;
       H       : hentry;
       i : integer;
       ch : char;
       locl_ecode : integer;

  begin

  {$IFC DEBUG2}
    if trace(FS, 70) then
      writeln('LOOKUP  ', path);
  {$ENDC}

      ecode := 0;

      DecompPath(ecode, path, device, nodeID, volPath);
      if ecode = E_NOT_MOUNTED then ecode := 0;
      if ecode > 0 then
      begin
        ecode := E_DEV_RANGE;
        goto 1;
      end;
      ptrDCB := mounttable[device];

      (* Initialize the FS_Info record before filling-in pertinent fields *)
      ClearMem(ord(@InfoRec), sizeof(InfoRec));

      if ((length(volPath) = 0) and (nodeID = ROOT_NID))
         or (not configinfo[device]^.blockstructured) then
      begin
        (************************)
        (*  Device Information  *)
        (************************)
        InfoRec.otype := device_t;

        (* Extract basic attributes from the configuration table *)
        Config_Report(configinfo[device], InfoRec);

        InfoRec.mounted := ptrDCB <> pointer(BADPTR1);
        if InfoRec.mounted then
        begin
          (********************************)
          (*  Mounted Device Information  *)
          (********************************)

          (* Extract attributes from the Device Control Block and MDDF *)
          DCB_Report(ptrDCB, InfoRec);
        end{if};
      end
      else
      begin
        (************************)
        (*  Object Information  *)
        (************************)
        if ptrDCB = pointer(BADPTR1) then
        begin
          ecode := E_NOT_MOUNTED;
          goto 1;
        end{if};
        InfoRec.otype := object_t;

        wait_sem(ptrDCB^.volSem, []);
        ReadDir(device, nodeID, volPath, DirRec, c, ecode);
        if ecode > 0 then
        begin     (* no such name *)
          ecode := E3_NOT_FOUND;
          goto 2;
        end{if};

        InfoRec.etype := c.cetype;

        if C.cetype = direntry then
        begin
          with InfoRec, DirRec do
          begin
            (**********************************)
            (*  Directory Object Information  *)
            (**********************************)
            DTC := dirDTC;
            DTM := dirDTC;
            DecompKey(@DirRec.key, @name, parID);
            GetDirPath(ptrDCB, parID, dir_path, locl_ecode);
            goto 2;  (* exit early *)
          end{with};
        end{if};

        sopencheck(ecode, device, C.sfile, ptrFCB);
        if ecode > 0 then goto 2;

        with InfoRec do
        begin
          lpsize := ptrDCB^.MDDFdata^.datasize;
          if ptrFCB <> pointer(BADPTR2) then
          begin
            (**********************************)
            (*  Open File Object Information  *)
            (**********************************)
            FCB_Report(ptrFCB, InfoRec);
            GetDirPath(ptrDCB, ptrFCB^.parID, dir_path, locl_ecode);
          end
          else
          begin
            (************************************)
            (*  Closed File Object Information  *)
            (************************************)

            if quick and (not flat_catalog(device)) then
            begin
              with DirRec, InfoRec do
              begin
                DecompKey(@DirRec.key, @name, parID);
                DTC := fileDTC;
                DTM := fileDTM;
                size := DirRec.size;
                psize := physSize;
                fs_overhead := fsOvrhd;
                master_file := flags[fMaster];
                protected := flags[fProtected];
                safety_on := flags[fSafety];
                file_left_open := flags[fCrashed];
                file_scavenged := flags[fScavenged];
                file_closed_by_OS := flags[fClosed];
              end{with};
              goto 2;  (* exit early *)
            end{if};

            slist_io(ecode, device, C.sfile, readop, S);
            if ecode > 0 then goto 2;
            if S.hintaddr <= 0 then
            begin
              ecode := E_BADSENTRY;
              goto 2;
            end;

            hentry_io(ecode, device, S.hintaddr, H, readop);
            if ecode > 0 then goto 2;
            if length(H.name) > max_ename then
            begin
              ecode := E_BADENAME;
              goto 2;
            end;

            if H.version < cur_file_version then
            begin
              UpgradeFile(device, S.hintaddr);
              (* Now reread the hint entry *)
              hentry_io (ecode, device, S.hintaddr, H, readop);
            end;

            Hentry_Report(H, InfoRec);
            size := S.filesize;
            psize := get_psize(ecode, device, C.sfile);
            GetDirPath(ptrDCB, H.parentID, dir_path, locl_ecode);
          end{if};

          refnum := -1;
          acmode := [];
        end{with};

  2:
        signal_sem (ptrDCB^.volSem);
      end{if};

  1:
  {$IFC DEBUG2}
    if trace(FS, 70) then
      writeln('LOOKUP  exit error = ',ecode);
  {$ENDC}

  end{proc GLookup};




  {$S fs2 }
  procedure Lookup (* var ecode   : error;
                      var path    : pathname;
                      var attributes : FS_Info *);
  begin
      GLookup(ecode, path, FALSE, attributes);
  end{proc Lookup};








  {$S fs2 }
  procedure Info (* var ecode   : error;
                        refnum  : integer;
                    var RefInfo : fs_info *);

  label 1;

  var
       addrRefDB : absptr;
       ptrRefDB  : fref_ptr;
       ptrDCB    : DCBptr;
       locl_ecode : integer;

  begin
  {$IFC DEBUG2}
      if trace(FS, 70) then
        writeln ('INFO  refnum = ', refnum);
  {$ENDC}

      ecode := 0;

      chk_refnum(ecode, refnum, frefdb, addrRefDB);
      if ecode > 0 then
      begin
        ecode := E5_REFNUM_INVALID;
        goto 1;
      end{if};

      ptrRefDB := pointer(addrRefDB);

      ClearMem(ord(@RefInfo), sizeof(RefInfo));
      with ptrRefDB^, RefInfo do
      begin
        otype := object_t;
        ptrDCB := mounttable[refdb_devnum];
        if configinfo[refdb_devnum]^.blockstructured then
        begin
          (*****************************)
          (*  Block-structured Device  *)
          (*****************************)
          FCB_Report(fcbptr, RefInfo);
          refnum := ptrRefDB^.refnum;
          fmark := ptrRefDB^.fmark;
          eof := ptrRefDB^.eof;
          acmode := ptrRefDB^.acmode;
          lpsize := ptrDCB^.MDDFdata^.datasize;
          GetDirPath(ptrDCB, fcbptr^.parID, dir_path, locl_ecode);
          etype := emptyentry;  (* undefined *)
        end
        else
        begin
          (***********************)
          (*  Sequential Device  *)
          (***********************)
          name := ptrDCB^.name;
          ftype := userfile;
          nusers := ptrDCB^.opencount;
          private := ptrDCB^.private;
          locked := ptrDCB^.locked;
        end{if};
      end{with};

  1:
  {$IFC DEBUG2}
      if trace(FS, 70) then
        writeln ('INFO  exiting error = ',ecode);
  {$ENDC}

  end{proc Info};






  {$S fs2 }
  procedure  Quick_Lookup (* var ecode   : error;
                             var path    : pathname;
                             var InfoRec : Q_Info *);

  var
       attributes : FS_Info;

  begin
      GLookup(ecode, path, TRUE, attributes);
      with attributes, InfoRec do
      begin
        if otype <> object_t then
        begin
          ecode := E_VOL_OP;
          exit(Quick_Lookup);
        end{if};
        name := attributes.name;
        eType := attributes.etype;
        DTC := attributes.DTC;
        DTM := attributes.DTM;
        size := attributes.size;
        psize := attributes.psize;
        fs_overhead := attributes.fs_overhead;
        master := master_file;
        protected := attributes.protected;
        safety := safety_on;
        left_open := file_left_open;
        scavenged := file_scavenged;
        closed_by_OS := file_closed_by_OS;
        level := 0;
        nreaders := attributes.nreaders;
        nwriters := attributes.nwriters;
      end{with};
  end{proc Quick_Lookup};






(*$S fs2 *)
(*
 *  performance
 *  1.  Uses new abspage procedure
 *  2.  Passes file map ptr to appendpages
 *)
procedure ALLOCATE (*var ecode : error;
                       refnum : integer;
                   contiguous : boolean;
                        count : longint;
                   var actual : longint*);


label 1, 2;

const parmbytes = 16;    (* calculated by hand for each procedure *)
      perrbase = 56;
      partial_operation_warning = -1146;

var refdbaddr : absptr;
    refdbptr : fref_ptr;
    sfile, pgdatasize, device : integer;
    appendto, allsize, actualcount, firstnew{, firstofall}, rptr, wptr,
                peof_relpage, leof_relpage, emptypages : longint;
    {firsttime : boolean;}
    tempecode : error;
    ptrFmap : flmap_ptr;
    psize : longint;

begin


(*$IFC DEBUG2*)
  if trace (FS, 70) then
  begin
    writeln ('ALLOCATE:  request to allocate ',count,' pages for refnum ', refnum);
  end;
(*$ENDC*)

  ecode := 0;
  actual := 0;   (* initially *)

  chk_refnum (ecode, refnum, frefdb, refdbaddr);
  if ecode <> 0 then
  begin
    ecode := E6_REFNUM_INVALID;
    goto 1;
  end;

  if count = 0 then
  begin
    ecode := E_ZERO_COUNT;
    goto 1;
  end;

  refdbptr := pointer (refdbaddr);   (* make a pointer to the fs_refdb *)

  if not configinfo [refdbptr^.refdb_devnum]^.blockstructured then
  begin
    ecode := E1_NOT_BLOCK_DEV;
    goto 1;
  end;

  wait_sem (refdbptr^.fcbptr^.semgp, []);
                                (* to protect from interruption by another writer *)

  with refdbptr^.fcbptr^ do
  begin
    if ftype = pipe then
    begin
      ecode := E_NOT_IMPLEMENTED;  (* pipes are no longer supported *)
      goto 2;
    end;

    sfile := sfilenum;    (* sfilenumber for this file *)
    device := curdev;     (* device on which file resides *)
    pgdatasize := mounttable[device]^.MDDFdata^.datasize;
    if old_volume(curdev) then
    begin
      ptrFmap := NIL;
{$IFC BLK512}
      Pg(phys_size, psize);
{$ELSEC}
      psize := phys_size div pgdatasize;
{$ENDC}
    end
    else
    begin
      ptrFmap := @fmap;
      psize := fmap.size;
    end;
  end;

  if count > 0 then
  begin
    (* try to alloc new pages near the file *)
    with refdbptr^.fcbptr^ do
    begin
      if phys_size > 0 then
        appendto := abspage(ecode, psize-1, refdbptr^.fcbptr)
      else
        appendto := 0;
    end;

    allsize := count;         (* amount of work left to do *)


    repeat
           (* append a contiguous chunk, which need not be an exact fit *)
      with refdbptr^.fcbptr^ do
        {appendpages (appendto, device, allsize, false, firstnew, actualcount, sfile,
                     sent.version, ptrFmap, ecode);}
        appendpages (appendto, device, allsize, false, firstnew, actualcount, sfile,
                     @sent, ptrFmap, ecode);

      actual := actual + actualcount;            (* work done so far *)
      allsize := allsize - actualcount;          (* work left to do *)
    until (allsize=0) or (contiguous) or (ecode<>0) or (actualcount=0);
           (* got it all, or only wanted biggest contiguous available or error *)
  end
  else       (* request to deallocate some space between leof and peof *)
  begin

    peof_relpage := psize - 1;
                         (* last relpage; -1 => no space allocated *)
    if ecode <> 0 then goto 2;    (* in case we couldn't map to a good page *)
    if refdbptr^.fcbptr^.sent.filesize > 0 then
{$IFC BLK512}
      Pg(refdbptr^.fcbptr^.sent.filesize-1, leof_relpage)
{$ELSEC}
      leof_relpage := (refdbptr^.fcbptr^.sent.filesize - 1) div pgdatasize
{$ENDC}
    else
      leof_relpage := -1;
    emptypages := peof_relpage - leof_relpage;
    allsize := -count;  (* positive number of pages to de-allocate *)
    if allsize > emptypages then
    begin
      allsize := emptypages;
    end;

    actual := -allsize;   (* negative means deallocation *)

    peof_relpage := peof_relpage - allsize; (* relpage where peof should now be *)
    {if allsize > 0 then releasepages (ecode, device, sfile, peof_relpage+1,
                                      ptrFmap);}
    if allsize > 0 then releasepages(ecode, device, sfile, peof_relpage+1,
                                      ptrFmap, @refdbptr^.fcbptr^.sent);
    if ecode > 0 then goto 2;
  end;

  with refdbptr^.fcbptr^ do
  begin
    if old_volume(curdev) then
      phys_size := get_psize(tempecode, curdev, sfilenum)
    else
      phys_size := fmap.size * pgdatasize;
  end;

  if flush_level <= allocFlush then flushfs(tempecode);

2:
  signal_sem (refdbptr^.fcbptr^.semgp);

  if ecode = 0 then             (* no errors encountered, check for warning *)
  begin
    if actual <> count then ecode := partial_operation_warning;
  end;

1:
  {if (ecode > 0) and (ecode < 100) then
                            ecode := EXTERRBASE + UIERRBASE + PERRBASE + ecode;}


(*$IFC DEBUG2*)
  if trace (FS, 70) then
  begin
    writeln ('ALLOCATE:  allocated ',actual,' pages; ecode of ',ecode);
  end;
(*$ENDC*)

end;   (* of procedure ALLOCATE *)










(*$S fs2 *)
procedure TRUNCATE (*var ecode : error; refnum : integer*);


label 1, 2;

const parmbytes =  6;    (* calculated by hand for each procedure *)
perrbase = 60;

var refdbaddr : absptr;
    refdbptr : fref_ptr;
    lsize, page : longint;
    extra, pgdatasize, sfile, device : integer;
    tempecode : error;

begin


(*$IFC DEBUG2*)
  if trace (FS, 70) then
  begin
    writeln ('TRUNCATE:  request to truncate for refnum ', refnum);
  end;
(*$ENDC*)

  ecode := 0;

  chk_refnum (ecode, refnum, frefdb, refdbaddr);
  if ecode <> 0 then
  begin
    ecode := E7_REFNUM_INVALID;
    goto 1;
  end;

  refdbptr := pointer (refdbaddr);   (* make a pointer to the fs_refdb *)

  if not configinfo [refdbptr^.refdb_devnum]^.blockstructured then
  begin
    ecode := E2_NOT_BLOCK_DEV;
    goto 1;
  end;

  wait_sem (refdbptr^.fcbptr^.semgp, []);
                              (* to prevent read_data or write_data from coming in *)
  if refdbptr^.fcbptr^.ftype = pipe then
  begin
    ecode := E_NOT_IMPLEMENTED;  (* pipes are no longer supported *)
    goto 2;
  end;

  device := refdbptr^.fcbptr^.curdev;
  sfile := refdbptr^.fcbptr^.sfilenum;
  pgdatasize := mounttable [device]^.MDDFdata^.datasize;

  lsize := refdbptr^.fmark;
  refdbptr^.fcbptr^.sent.filesize := lsize;
  slist_io (ecode, device, sfile, writeop, refdbptr^.fcbptr^.sent); (* update s_entry *)

  (* need to reset dataused info in each page label *)

{$IFC BLK512}
  PgOffset(lsize, page, extra);
{$ELSEC}
  page := lsize div pgdatasize;
  extra := lsize mod pgdatasize;
{$ENDC}
  if (extra = 0) and (page > 0) then
  begin
    page := page - 1;
    extra := pgdatasize;
  end;

  page := abspage(ecode, page, refdbptr^.fcbptr);
  if ecode = 0 then
  begin
    clear_dataused (ecode, device, page, extra);
                                (* page is last abspage containing logical *)
           (* data, extra is number of bytes on that page logically in use *)

  end
  else ecode := E_FILE_NULL;   (* no allocated space in file, nothing to truncate *)

  if flush_level <= allocFlush then flushfs(tempecode);

2:
  signal_sem (refdbptr^.fcbptr^.semgp);


1:


  {if (ecode > 0) and (ecode < 100) then
                             ecode := EXTERRBASE + UIERRBASE + PERRBASE + ecode;}


(*$IFC DEBUG2*)
  if trace (FS, 70) then
  begin
    writeln ('TRUNCATE:  exiting with error code of ',ecode);
  end;
(*$ENDC*)

end;   (* of procedure TRUNCATE *)










{$S fs2 }
procedure COMPACT (* var ecode : error;
                         refnum : integer *);

label 1;

var
    refdbaddr : absptr;
    refdbptr  : fref_ptr;
    page,
    lsize,
    lastpage  : longint;
    extra,
    pgdatasize,
    device    : integer;
    tempecode : error;
    ptrFmap   : flmap_ptr;
    mustSignal : boolean;

begin

{$IFC DEBUG2}
  if trace (FS, 70) then
    writeln ('COMPACT  refnum = ', refnum);
{$ENDC}

  ecode := 0;
  mustSignal := FALSE;

  chk_refnum(ecode, refnum, frefdb, refdbaddr);
  if ecode > 0 then
  begin
    ecode := E8_REFNUM_INVALID;
    goto 1;
  end{if};

  refdbptr := pointer(refdbaddr);   (* make a pointer to the fs_refdb *)

  if not configinfo[refdbptr^.refdb_devnum]^.blockstructured then
  begin
    ecode := E3_NOT_BLOCK_DEV;
    goto 1;
  end{if};

  wait_sem(refdbptr^.fcbptr^.semgp, []);     (* to protect from interruption by another writer *)
  mustSignal := TRUE;

  device := refdbptr^.fcbptr^.curdev;
  pgdatasize := mounttable[device]^.MDDFdata^.datasize;

  with refdbptr^.fcbptr^ do
  begin
    if ftype = pipe then
    begin
      ecode := E_NOT_IMPLEMENTED;  (* pipes are no longer supported *)
      goto 1;
    end{if};

    lsize := sent.filesize;
    if phys_size <= pgdatasize then goto 1;  (* no work to do *)  (* bug #195 *)

{$IFC BLK512}
    PgOffset(lsize, page, extra);
{$ELSEC}
    page := lsize div pgdatasize;
    extra := lsize mod pgdatasize;
{$ENDC}

    if (extra = 0) and (page > 0) then
    begin
      page := page - 1;
      extra := pgdatasize;
    end{if};

    if old_volume(curdev) then
      ptrFmap := NIL
    else
      ptrFmap := @fmap;
    releasepages(ecode, device, sfilenum, page+1, ptrFmap, @sent);
    if old_volume(curdev) then
      phys_size := get_psize(ecode, curdev, sfilenum)
    else
      phys_size := fmap.size * mounttable[curdev]^.MDDFdata^.datasize;
  end{with};

1:
  if mustSignal then
  begin
    if flush_level <= allocFlush then flushfs(tempecode);
    signal_sem(refdbptr^.fcbptr^.semgp);
  end{if};

{$IFC DEBUG2}
  if trace (FS, 70) then
    writeln ('COMPACT  exit error = ',ecode);
{$ENDC}

end{proc Compact};








{*************************************************************************
 This procedure is not needed, since TRUNCATE can do the job !
 procedure FLUSH_PIPE (*var ecode:error; refnum:integer*);

label 1;

const perrbase = 67;

var refdbaddr : absptr;
    refdbptr : fref_ptr;
    pgdatasize : integer;
    absbyte, relpage : longint;

begin


(*$IFC DEBUG2*)
  if trace (FS, 70) then
  begin
    writeln ('FLUSH_PIPE:  request to flush pipe for refnum ', refnum);
  end;
(*$ENDC*)

  ecode := 0;

  chk_refnum (ecode, refnum, frefdb, refdbaddr);
  if ecode <> 0 then
  begin
    ecode := 1;
    goto 1;
  end;

  refdbptr := pointer (refdbaddr);   (* make a pointer to the fs_refdb *)

  if not configinfo [refdbptr^.refdb_devnum]^.blockstructured then
  begin
    ecode := 3;
    goto 1;
  end;

  if (refdbptr^.fcbptr^.ftype <> pipe) or (not (dread in refdbptr^.acmode)) then
  begin
    ecode := 2;      (* not a reader of this pipe, not allowed to flush *)
    goto 1;
  end;

  (* set reader's curpage, curoffset in fcb to write pointer *)

  with refdbptr^.fcbptr^ do
  begin
    curpage := wrcurpage;
    curoffset := wrcuroffset;
  end;



1:


  if (ecode > 0) and (ecode < 100) then
                                  ecode := EXTERRBASE + UIERRBASE + PERRBASE + ecode;


(*$IFC DEBUG2*)
  if trace (FS, 70) then
  begin
    writeln ('FLUSH_PIPE:  exiting with error code of ',ecode);
  end;
(*$ENDC*)

end;   (* of procedure FLUSH_PIPE *)
*************************************************************************}





(*$S fs3 *)
procedure Grename_Entry ( var ecode   : error;
                          var path    : pathname;
                          var newname : e_name;
                              passwd  : e_name );

label 1, 2;

var devnum, saveo1, saveo2 : integer;
    name : e_name;
    s : s_entry;
    c, cjunk : centry;
    h : hentry;
    actual, savep1, savep2 : longint;
    fcbptr : sfcb_ptr;
    tempecode : error;
    wcode : integer;
    junk : integer;
    volPath : pathname;
    mustDelete : boolean;
    oldKey : RecKey;
    DirRec : ObjRec;
    id : integer;
    dirObject : boolean;
    size : integer;
    nullName : e_name;
    ptrEntry : entry_ptr;
    ptrBuf : buffer_ptr;
    found : boolean;
    parID : NodeIdent;

begin

(*$IFC DEBUG2*)
  if trace (FS, 70) then
  begin
    writeln ('RENAME_ENTRY:  request to rename ',path,' to ',newname);
  end;
(*$ENDC*)

  ecode := 0;
  wcode := 0;
  mustDelete := FALSE;

  if (pos ('-',newname) <> 0) then  (* '-' not allowed in an entry name *)
  begin
    ecode := E2_NAME_INVALID;
    goto 1;
  end;

  DecompPath(ecode, path, devnum, parID, volPath);

  if ecode > 0 then
  begin
    ecode := E4_PATH_INVALID;
  end
  else
  begin
    if ((length(volPath) = 0) and (parID = ROOT_NID)) or
       (not configinfo [devnum]^.blockstructured) then
    begin                   (* valid devnum, so something is mounted *)
      mounttable [devnum]^.name := newname;
      if configinfo [devnum]^.blockstructured then (* volume name? *)
      with mounttable [devnum]^ do
      begin
        wait_sem (semMDDF, []);        (* to prevent this from interfering flushfs *)
        MDDFdata^.volname := newname; (* yes, change it *)
        MDDFdirty := true;
        signal_sem (semMDDF);
      end;
    end
    else
    begin
      wait_sem (mounttable [devnum]^.volSem, []);

      ReadDir(devnum, parID, volPath, DirRec, c, ecode);
      if ecode > 0 then
      begin
        ecode := E4_NOT_FOUND; (* could not find the named entry, or catptr was not a cat *)
        goto 2;
      end;

      if flat_catalog(devnum) then
      begin
        savep1 := rootptr [devnum]^.curpage;  (* points to entry we want to rename *)
        saveo1 := rootptr [devnum]^.curoffset;

        (* see if new name already exists *)

        lookup_by_ename(ecode, newname, rootptr[devnum], cjunk);
        if (ecode = 0) then
        begin
          ecode := E2_ENTRY_EXISTS; (* new name already exists, or catptr was not a cat *)
          goto 2;
        end
        else if (ecode <> E_NOT_FOUND) OR (cjunk.cetype <> emptyentry) then
        begin  (* error other than entry not found, or no free slots *)
          goto 2;
        end;

        savep2 := rootptr [devnum]^.curpage;  (* points to entry we want to rename *)
        saveo2:= rootptr [devnum]^.curoffset;
        dirObject := FALSE;
      end
      else
      begin
        moveleft(DirRec.key, oldKey, KEY_SIZE);
        DecompKey(@DirRec.key, @name, id);
        MakeKey(id, @newname, @DirRec.key);
        case DirRec.eType of
          fileentry: size := sizeof(ObjectRec);
          direntry:  size := sizeof(DirectRec);
        end{case};
        Insert(mounttable[devnum], @DirRec, size, NIL, ecode);
        if ecode > 0 then
        begin
          ecode := E2_ENTRY_EXISTS;
          goto 2;
        end;
        mustDelete := TRUE;
        dirObject := (DirRec.eType = direntry);
      end;

      (* update the hint part of the file *)

      if not dirObject then
      begin
        slist_io (ecode, devnum, c.sfile, readop, s); (* get s-entry to find hints *)
        if ecode > 0 then goto 2;
        if s.hintaddr <= 0 then
        begin
          ecode := E_BADSENTRY;
          goto 2;
        end;

        hentry_io (ecode, devnum, s.hintaddr, h, readop); (* get hints *)
        if ecode > 0 then goto 2;
        if length(h.name) > max_ename then
        begin
          ecode := E_BADENAME;
          goto 2;
        end;

        if h.version < cur_file_version then
        begin
          UpgradeFile(devnum, s.hintaddr);
          (* Now reread the hint entry *)
          hentry_io (ecode, devnum, s.hintaddr, h, readop);
        end;

        (* Check password protection *)
        CheckPasswd(ecode, passwd, h);
        if ecode > 0 then goto 2;
        wcode := ecode;
      end;

      if flat_catalog(devnum) then
      begin
                          (* entry not found, but no other errors *)
        c.name := newname;      (* change the name in the catalog entry *)

             (* write out the new catalog entry *)
        fileio (ecode, rootptr [devnum], ord(@c), sizeof (centry), actual,
                       savep2, saveo2, writeop);
        if ecode > 0 then goto 2;

        cjunk.name := '';    (* clear entry with an empty name *)
        cjunk.sfile := 0;    (* and an empty sfile number, etc.*)
        cjunk.cetype := removed;
        cjunk.readpage := 0;
        cjunk.readoffset := 0;
        cjunk.writepage := 0;
        cjunk.writeoffset := 0;
               (* write cleared catalog entry to the old entry *)
        fileio (ecode, rootptr [devnum], ord(@cjunk), sizeof(centry), actual,
                                                          savep1, saveo1, writeop);
      end
      else
      begin
        moveleft(oldKey, DirRec.key, KEY_SIZE);
        Ddelete(mounttable[devnum], @DirRec, NIL, junk, ecode);
      end;

      if not dirObject then
      begin
        h.name := newname;
        hentry_io (ecode, devnum, s.hintaddr, h, writeop); (* write hints *)

        sopencheck (ecode, devnum, c.sfile, fcbptr);
        if ecode > 0 then goto 2;

        if fcbptr <> pointer (badptr2) then
          fcbptr^.fname := newname;
      end
      else
      begin
        (* Directory object *)
        nullName := '';
        MakeKey(DirRec.id, @nullName, @oldKey);
        E_Search(mounttable[devnum], @oldKey, ptrBuf, ptrEntry, found, ecode);
        if (ecode <= 0) and found then
        begin
          ptrEntry^.myName := newname;
          ptrBuf^.dirty := TRUE;
          FreeBuf(ptrBuf);
        end{if};
      end{if};

      (* Made it!  Commit *)
      mustDelete := FALSE;

      if flush_level <= makeFlush then flushfs(tempecode);

2:
      if mustDelete then
        Ddelete(mounttable[devnum], @DirRec, NIL, junk, tempecode);
      signal_sem (mounttable [devnum]^.volSem);
    end;
  end;

1:
  if ecode = 0 then ecode := wcode;

(*$IFC DEBUG2*)
  if trace (FS, 70) then
  begin
    writeln ('RENAME_ENTRY:  exiting with error code of ',ecode);
  end;
(*$ENDC*)

end;   (* of procedure RENAME_ENTRY *)




{$S fs3}
procedure  Rename_Entry (* var ecode   : error;
                           var path    : pathname;
                           var newname : e_name *);

begin
    Grename_Entry(ecode, path, newname, '' (* no password *));
end{proc Rename_Entry};




{$S fs3}
procedure  Rename_Secure (* var ecode   : integer;
                            var path    : Pathname;
                            var newName : e_name;
                            var passwd  : e_name *);

begin
    Grename_Entry(ecode, path, newName, passwd);
end{proc Rename_Secure};





ÿ