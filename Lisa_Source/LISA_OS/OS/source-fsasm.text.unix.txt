;
;         File System support routines
;
;        (c) copyright 1983, 1984  Apple Computer Inc.                                      *)


;************************************************************************
;  PgOffset -  convert a byte offset into an ordered pair (page #, offset)
;
;  INTERFACE
;       procedure  PgOffset (     absOffset : longint;
;                             var page      : longint;
;                             var offset    : integer );
;
;  ARGUMENTS PASSED
;    absOffset :  absolute byte offset
;
;  ARGUMENTS RETURNED
;    page      :  page number (nbytes DIV 512)
;    offset    :  offset within page (nbytes MOD 512)
;
;  - - - NOTES - - -
;  This procedure assumes that a page contains 512 bytes!  Currently, all
;  Lisa block-structured devices have a block size of 512 bytes.
;
;************************************************************************

               .PROC     PgOffset
               .DEF      Pg

               MOVEA.L   (A7)+,A0       ; pop return PC
PgEntry
               MOVEA.L   (A7)+,A2       ; pop addr of remainder return
               MOVEA.L   (A7)+,A1       ; pop addr of result return
               MOVE.L    (A7)+,D1       ; pop dividend
               MOVE.W    D1,D2          ; save low word for remainder

               MOVEQ     #9,D0          ; shift count
               LSR.L     D0,D1          ; div 512
               AND.W     low9,D2        ; mod 512

               MOVE.L    D1,(A1)        ; store result return
               MOVE.W    D2,(A2)        ; store remainder return

               JMP       (A0)           ; return

low9           .WORD     $01FF



;************************************************************************
;  Pg -  convert a byte offset into a relative page number
;
;  INTERFACE
;       procedure  Pg (     absOffset : longint;
;                       var page      : longint );
;
;  ARGUMENTS PASSED
;    absOffset :  absolute byte offset
;
;  ARGUMENTS RETURNED
;    page      :  page number (nbytes DIV 512)
;
;************************************************************************

Pg
               MOVEA.L   (A7)+,A0       ; pop return pc
               SUBQ.L    #4,A7          ; make cell on stack
               MOVE.L    A7,(A7)        ; fake remainder addr
               BRA       PgEntry




;************************************************************************
;  ClearMem -  initialize a section of memory to zero
;
;  INTERFACE
;       procedure  ClearMem ( address : longint;
;                             nbytes  : longint );
;
;  ARGUMENTS PASSED
;    address :  address of the memory area to be cleared
;
;  ARGUMENTS RETURNED
;    nbytes  :  number of bytes to clear
;
;************************************************************************

               .PROC     ClearMem

               MOVEA.L   (A7)+,A0       ; pop return pc
               MOVE.L    (A7)+,D0       ; number of bytes
               MOVE.L    (A7)+,D1       ; address
               MOVE.L    D1,A1
               BLE.S     ClrExit        ; byte count <= 0

               BTST      #0,D1          ; test for odd address
               BEQ.S     @1             ; even address
               SUBQ.W    #1,D0          ; decrement the byte count
               CLR.B     (A1)+          ; clear the initial byte

@1             MOVE.W    D0,D1          ; save byte count
               LSR.W     #2,D0          ; number of longints to clear
               BEQ       ClrWord        ; branch if none
               SUBQ.W    #1,D0          ; adjust loop index
ClrLong
               CLR.L     (A1)+          ; clear 4 bytes
               DBF       D0,ClrLong
ClrWord
               BTST      #1,D1          ; odd word to clear
               BEQ.S     ClrByte        ; branch if none
               CLR.W     (A1)+
ClrByte
               BTST      #0,D1          ; odd byte to clear
               BEQ.S     ClrExit        ; branch if none
               CLR.B     (A1)+
ClrExit
               JMP       (A0)           ; return



               .SEG      'fs7'
               .PROC     MovrOffsets,12
               .DEF      MovlOffsets

ptrSrc         .EQU      20
ptrDst         .EQU      16
count          .EQU      14
delta          .EQU      12
parmsz         .EQU      12


               MOVE.L    D2,-(A7)       ; save D2
               BSR       GetParm        ; extract parameters from stack

               CMPM.W    (A0)+,(A1)+    ; preincrement A0, A1 for loop
MovrInc
               MOVE.W    -(A0),D2       ; *ptrDst-- = *ptrSrc-- + delta
               ADD.W     D1,D2
               MOVE.W    D2,-(A1)
               DBF       D0,MovrInc

MovrExit
               MOVE.L    (A7)+,D2       ; restore D2
               MOVE.L    (A7)+,A0       ; pop return pc
               MOVEQ     #parmsz,D0
               ADDA.L    D0,A7          ; pop stack frame
               JMP       (A0)           ; return

GetParm
               MOVE.W    delta(A7),D1   ; extract parameters from stack
               MOVE.W    count(A7),D0
               MOVE.L    ptrDst(A7),A1
               MOVE.L    ptrSrc(A7),A0
               RTS



MovlOffsets
               MOVE.L    D2,-(A7)       ; save D2
               BSR       GetParm        ; extract parameters from stack
MovlIncr
               MOVE.W    (A0)+,D2       ; *ptrDst++ = *ptrSrc++ + delta
               ADD.W     D1,D2
               MOVE.W    D2,(A1)+
               DBF       D0,MovlIncr

               BRA.S     MovrExit       ; return




               .SEG      'fs7'
               .PROC     MoveLt,10

               MOVE.L    (A7)+,D1       ; pop return pc
               MOVE.W    (A7)+,D0       ; pop nbytes
               MOVE.L    (A7)+,A1       ; pop ptrDst
               MOVE.L    (A7)+,A0       ; pop ptrSrc
               MOVE.L    D1,-(A7)       ; push return pc
               MOVE.W    D0,D1          ; save byte count
               BLE.S     MovlExit       ; byte count <= 0

               LSR.W     #2,D0          ; number of longints to move
               BEQ.S     MovlWord       ; branch in none
               SUBQ.W    #1,D0          ; adjust loop index
MovlLoop
               MOVE.L    (A0)+,(A1)+    ; move longints
               DBF       D0,MovlLoop
MovlWord
               BTST      #1,D1          ; odd word to move
               BEQ.S     MovlByte       ; branch if none
               MOVE.W    (A0)+,(A1)+
MovlByte
               BTST      #0,D1          ; odd byte to move
               BEQ.S     MovlExit       ; branch if none
               MOVE.B    (A0)+,(A1)+
MovlExit
               RTS                      ; return


               .SEG      'fs7'
               .PROC     MoveRt,10

               MOVE.L    (A7)+,D1       ; pop return pc
               MOVE.W    (A7)+,D0       ; pop nbytes
               MOVE.L    (A7)+,A1       ; pop ptrDst
               MOVE.L    (A7)+,A0       ; pop ptrSrc
               MOVE.L    D1,-(A7)       ; push return pc
               TST.W     D0
               BLE.S     MovrExit       ; byte count <= 0

               ADDA.W    D0,A0          ; high-address of source block
               ADDA.W    D0,A1          ; high-address of dest block
               LSR.W     #1,D0          ; odd byte to move
               BCC.S     MovrWord       ; branch if none
               MOVE.B    -(A0),-(A1)
MovrWord
               LSR.W     #1,D0          ; odd word to move
               BCC.S     MovrLong       ; branch if none
               MOVE.W    -(A0),-(A1)
MovrLong
               SUBQ.W    #1,D0          ; adjust loop index
               BLT.S     MovrExit       ; no longints to move
MovrLoop
               MOVE.L    -(A0),-(A1)    ; move longints
               DBF       D0,MovrLoop
MovrExit
               RTS                      ; return




;    procedure  SearchNode ( ptrRec     : rec_ptr;    { array of records }
;                            ptrO       : offset_ptr; { array of offsets }
;                            nentries   : integer;    { entry count }
;                            ptrKey     : rec_ptr;    { search key }
;                            keySize    : integer;    { bytes per key }
;                        var index      : integer;    { record index }
;                        var result     : boolean );  { found or not }


               .SEG      'fs7'
               .PROC     SearchNode

RegSaveArea    .EQU      32
ArgBase        .EQU      RegSaveArea+4
result         .EQU      ArgBase
index          .EQU      ArgBase+4
keySize        .EQU      ArgBase+8
ptrKey         .EQU      ArgBase+10
nentries       .EQU      ArgBase+14
ptrO           .EQU      Argbase+16
ptrRec         .EQU      ArgBase+20
parmsz         .EQU      24

               MOVEM.L   D2-D6/A2-A4,-(A7)   ; save registers
               MOVE.L    A7,A2               ; A2 is parameter ptr
               MOVE.L    ptrKey(A2),A3       ; ptrKey
               MOVE.L    ptrO(A2),A4         ; ptrO
               MOVE.L    ptrRec(A2),D6       ; ptrRec
               MOVE.W    keySize(A2),D5      ; keySize

               CLR.W     D2                  ; lower bnd := 0
               MOVE.W    nentries(A2),D3     ; upper bnd := nentries - 1
               SUBQ.W    #1,D3
               CMP.W     D3,D2               ; while lower bnd <= upper bnd
               BGT.S     SrchExit
SrchLoop
               MOVE.W    D2,D4               ; index := (upper+lower) / 2
               ADD.W     D3,D4
               LSR.W     #1,D4

               MOVE.W    D4,D0               ; A1 <- @ptrRec^[ptrO^[-index]]
               ADD.W     D0,D0
               NEG.W     D0
               MOVE.W    0(A4,D0),D0
               MOVE.L    D6,A1
               ADDA.W    D0,A1
               MOVE.L    A3,A0               ; ptrKey
Compare
               MOVE.W    D5,D0               ; loop index is key length
               SWAP      D5                  ; move key size out of the way
               SUBQ.W    #4,D0               ; adjust loop index to skip PID
               MOVE.W    #1,D1               ; parent ID is 2 bytes
               CMPM.B    (A1)+,(A0)+         ; skip length byte
CmpPID         CMPM.B    (A1)+,(A0)+         ; compare bytes of parent ID
               DBNE      D1,CmpPID
               BHI.S     IsGT
               BEQ.S     CmpLoop
               BRA.S     IsLT
                                             ; parent ID's are the same
CmpLoop
               MOVE.B    (A0)+,D1            ; load next byte of trial key
               BSR       UpShift             ; map to upper case
               MOVE.B    D1,D5
               MOVE.B    (A1)+,D1            ; load next byte of search key
               BSR       UpShift             ; map to upper case
               CMP.B     D1,D5               ; compare the characters
               DBNE      D0,CmpLoop          ; keep looping if they're equal
               BEQ.S     IsEQ
               BHI.S     IsGT
IsLT
               SWAP      D5                  ; reinstate key size
               MOVE.W    D4,D3               ; upper := index - 1
               SUBQ.W    #1,D3
               CMP.W     D3,D2               ; upper <= lower ?
               BLE.S     SrchLoop
               BRA.S     SrchExit
IsEQ
               MOVEQ     #1,D0               ; result := FOUND
               BRA.S     SrchRet
IsGT
               SWAP      D5                  ; reinstate key size
               MOVE.W    D4,D2               ; lower := index + 1
               ADDQ.W    #1,D2
               CMP.W     D3,D2               ; lower <= upper ?
               BLE.S     SrchLoop
SrchExit
               MOVE.W    D2,D4               ; index := lower
               CLR.W     D0                  ; result := NOT_FOUND
SrchRet
               MOVE.L    index(A2),A0        ; store index and result
               MOVE.W    D4,(A0)
               MOVE.L    result(A2),A0
               MOVE.B    D0,(A0)

               MOVEM.L   (A7)+,D2-D6/A2-A4   ; restore registers
               MOVE.L    (A7)+,A0            ; pop return pc
               MOVEQ     #parmsz,D0          ; pop stack frame
               ADDA.L    D0,A7
               JMP       (A0)                ; return
UpShift
               CMP.B     #97,D1              ; compare char to 'a'
               BLT       UpRet
               CMP.B     #122,D1             ; compare char to 'z'
               BGT       UpRet
               BCLR      #5,D1               ; shift char to upper case
UpRet          RTS                           ; return




;    procedure  MakeKey ( parentID  : integer;
;                         ptrName   : ename_ptr;
;                         ptrKey    : key_ptr );

               .SEG      'fs7'
               .PROC     MakeKey

               MOVEA.L   (A7)+,A0            ; pop return pc
               MOVEA.L   (A7)+,A1            ; pop addr of key
               MOVEA.L   (A7)+,A2            ; pop entry name

               MOVE.B    #36,(A1)+           ; stuff key length
               MOVE.B    (A7),(A1)+          ; stuff parent ID (high)
               MOVE.W    (A7)+,D0            ; extract whole parent ID
               MOVE.B    D0,(A1)+            ; stuff parent ID (low)
               MOVE.B    (A2)+,D0            ; extract entry name length
               EXT.W     D0
               MOVE.W    #32,D1              ; max entry name length

               SUB.W     D0,D1               ; max length - entry length
               BRA.S     @2                  ; decrement before testing
@1             MOVE.B    (A2)+,(A1)+         ; copy next byte from name to key
@2             DBF       D0,@1
               BRA.S     @4                  ; decrement before testing
@3             CLR.B     (A1)+               ; clear next byte in key (null pad)
@4             DBF       D1,@3
               CLR.B     (A1)+               ; always null name-terminator in key

               JMP       (A0)                ; return



;    procedure  DecompKey (     ptrKey    : key_ptr;
;                               ptrName   : ename_ptr;
;                           var parentID  : integer );

               .SEG      'fs7'
               .PROC     DecompKey

               MOVEA.L   (A7)+,A0            ; pop return pc
               MOVEA.L   (A7)+,A2            ; pop addr of parentID
               MOVE.L    (A7)+,D2            ; pop addr of entry name
               MOVEA.L   (A7)+,A1            ; pop addr of key

               ADDQ.L    #1,A1               ; skip key length byte
               MOVE.B    (A1)+,(A2)+         ; stuff parent ID (high)
               MOVE.B    (A1)+,(A2)          ; stuff parent ID (low)
               MOVE.L    D2,A2               ; address of entry name
               ADDQ.L    #1,A2               ; skip entry name length byte
               CLR.W     D0                  ; clear entry name length count
               BRA.S     @2                  ; avoid bumping count for null byte
@1             ADDQ.W    #1,D0               ; accumulate entry name length
@2             MOVE.B    (A1)+,(A2)+         ; copy char from key to entry name
               BNE       @1                  ; look for null terminator
               MOVE.L    D2,A2
               MOVE.B    D0,(A2)             ; stuff entry name length

               JMP       (A0)                ; return



;         Scavenger support routines
;
;        (c) copyright 1983  Apple Computer Inc.


;************************************************************************
;  ItoA -  convert integer (2-byte) to ASCII string format
;
;  INTERFACE
;         procedure  ItoA (     number : integer;
;                               width  : integer;
;                           var nstr   : GenString );
;
;  ARGUMENTS PASSED
;    number :  integer to convert
;    width  :  width desired for the resultant string (zero padded)
;
;  ARGUMENTS RETURNED
;    nstr   :  ASCII string representation of number
;
;************************************************************************

               .SEG      'ni_scAvenger'
               .PROC     ItoA

               MOVEA.L   (A7)+,A0       ; pop return pc
               MOVEA.L   (A7)+,A1       ; pop addr of string
               MOVE.W    (A7)+,D1       ; pop width of string
               MOVE.W    (A7)+,D2       ; pop number to convert
               EXT.L     D2

               MOVE.B    D1,(A1)+       ; set string length to width passed
               EXT.L     D1             ; cast width as longint
               ADDA.L    D1,A1          ; move ptr to end of string
               MOVEQ     #10,D0         ; base 10

@1             DIVS      D0,D2          ; number div 10
               SWAP      D2             ; remainder to low word of register
               ADDI.W    #48,D2         ; convert digit to ascii
               MOVE.B    D2,-(A1)       ; stuff into string
               SWAP      D2             ; restore number for next div
               EXT.L     D2

               SUBQ.L    #1,D1          ; decrement width
               BNE       @1

               JMP       (A0)           ; return



;************************************************************************
;  AtoI -  convert ASCII string to integer (2-byte) format
;
;  INTERFACE
;         procedure  AtoI ( var string : GenString;
;                           var number : integer );
;
;************************************************************************

;              .SEG      'ni_scAvenger'
;              .PROC     AtoI

;              MOVEA.L   (A7)+,A0       ; pop return pc
;              MOVEA.L   (A7)+,A2       ; pop addr of number
;              MOVEA.L   (A7)+,A1       ; pop addr of string

;              CLR.L     (A2)           ; sum := 0
;              CLR.W     D0
;              MOVE.B    (A1)+,D2       ; string length
;              EXT.W     D2             ; convert to integer

;@1            MOVE.B    (A1)+,D0       ; next byte from string
;              SUBI.W    #48,D0         ; convert ASCII to integer byte
;              MOVEQ     #10,D1         ; base 10
;              MULS      (A2),D1        ; sum * 10
;              ADD.W     D0,D1          ; (sum * 10) + digit
;              MOVE.W    D1,(A2)        ; save sum
;              SUBQ.W    #1,D2          ; decrement length
;              BNE       @1

;              JMP       (A0)           ; return




;************************************************************************
;  ScvFile -  Build {Snnnnn} file name
;
;  INTERFACE
;         procedure  ScvFile (     idNum : integer;
;                              var name  : E_Name );
;
;************************************************************************

;              .SEG      'ni_scAvenger'
;              .PROC     ScvFile
;              .REF      ItoA

;              MOVEA.L   (A7)+,A0       ; pop return pc
;              MOVEA.L   (A7)+,A1       ; pop addr of string
;              MOVE.W    (A7)+,D1       ; pop id number

;              ADDA.L    #2,A1          ; skip first two chars
;              MOVE.L    A0,-(A7)       ; save return pc on stack
;              MOVE.W    D1,-(A7)       ; push id number arg
;              MOVE.W    #5,-(A7)       ; push width arg
;              MOVE.L    A1,-(A7)       ; push string addr arg
;              JSR       ITOA           ; convert id number to string

;              MOVEA.L   (A7)+,A0       ; pop saved return pc
;              MOVE.B    #83,-(A1)      ; concat 'S'
;              MOVE.B    #123,-(A1)     ; concat '{'
;              MOVE.B    #8,-(A1)       ; insert string length
;              ADDA.L    #8,A1          ; point to end of string
;              MOVE.B    #125,(A1)      ; concat '}'

;              JMP       (A0)           ; return




;************************************************************************
;  ScvDir -  Build Directorynnnnn file name
;
;  INTERFACE
;         procedure  ScvDir (     idNum : integer;
;                             var name  : GenString );
;
;************************************************************************

               .SEG      'ni_scAvenger'
               .PROC     ScvDir
               .REF      ItoA

               MOVEA.L   (A7)+,A0       ; pop return pc
               MOVEA.L   (A7)+,A1       ; pop addr of string
               MOVE.W    (A7)+,D1       ; pop id number

               ADDA.L    #9,A1          ; skip first nine chars
               MOVE.L    A0,-(A7)       ; save return pc on stack
               MOVE.W    D1,-(A7)       ; push id number arg
               MOVE.W    #5,-(A7)       ; push width arg
               MOVE.L    A1,-(A7)       ; push string addr arg
               JSR       ITOA           ; convert id number to string

               MOVEA.L   (A7)+,A0       ; pop saved return pc
               MOVE.L    #$746F7279,-(A1)    ; concat 'tory'
               MOVE.L    #$69726563,-(A1)    ; concat 'irec'
               MOVE.W    #$0E44,-(A1)   ; concat 'D' and string length

               JMP       (A0)           ; return




;************************************************************************
;  Pair -  predicate to determine if two page labels form a valid pair
;
;  INTERFACE
;         function  Pair ( var LeftLabel  : Pagelabel;
;                          var RightLabel : PageLabel ) : boolean;
;
;************************************************************************

               .SEG      'ni_scAvenger'
               .PROC     Pair

version        .EQU      0
fileid         .EQU      4
dataused       .EQU      6
abspage        .EQU      8
relpage        .EQU      12
fwdlink        .EQU      16
bkwlink        .EQU      20

               MOVEA.L   (A7)+,A0            ; pop return pc
               MOVEA.L   (A7)+,A2            ; pop addr of Right
               MOVEA.L   (A7)+,A1            ; pop addr of Left

               MOVE.W    fileid(A1),D0       ; Left.fileid
               CMP.W     fileid(A2),D0       ; Left.fileid = Right.fileid ?
               BNE       SORRY

               TST.W     D0                  ; is Left a free page ?
               BEQ       Match
               BLT       @1                  ; continue if Left is leader page
               CMP.W     #3,D0               ; is Left a primitive s-file page ?
               BLE       Match
@1
               MOVE.W    version(A1),D0      ; Left.version
               CMP.W     version(A2),D0      ; Left.version = Right.version ?
               BNE       Sorry

               MOVE.W    dataused(A1),D0     ; Left.dataused
               CMP.W     dataused(A2),D0     ; Left.dataused = Right.dataused ?
               BEQ       CHKSPECIAL
               BLT       Sorry
                                             ; invariant: Left.du > Right.du
               CMP.W     #512,D0             ; Left.dataused = 512 ?
               BNE       Sorry
                                             ; invariant: Left.du = 512
               TST.W     dataused(A2)        ; Right.dataused = 0 ?
               BEQ       Sorry
               BRA.S     Continue
ChkSpecial                                   ; invariant: Left.du = Right.du
               OR.W      #512,D0             ; Left.dataused = 512 or
               CMP.W     #512,D0             ; Left.dataused = 0 ?
               BNE       Sorry
Continue
               MOVE.L    fwdlink(A1),D0      ; Left.fwdlink
               CMP.L     abspage(A2),D0      ; Left.fwdlink = Right.abspage ?
               BNE       Sorry

               MOVE.L    abspage(A1),D0      ; Left.abspage
               CMP.L     bkwlink(A2),D0      ; Right.bkwdlink = Left.abspage ?
               BNE       Sorry

               ADDQ.L    #1,D0
               CMP.L     abspage(A2),D0      ; Left.abspage+1 = Right.abspage ?
               BNE       Sorry

               MOVE.L    relpage(A1),D0      ; Left.relpage
               ADDQ.L    #1,D0
               CMP.L     relpage(A2),D0      ; Left.relpage+1 = Right.relpage ?
               BNE       Sorry
Match
               MOVE.B    #1,(A7)             ; return TRUE
               JMP       (A0)
Sorry
               MOVE.B    #0,(A7)             ; return FALSE
               JMP       (A0)



;************************************************************************
;  NextCell -  Return next cell in a list of cells
;
;  INTERFACE
;       procedure  NextCell (     ptrList : cell_ptr;
;                             var ptrCell : cell_ptr );
;
;************************************************************************

               .SEG      'ni_scAvenger'
               .PROC     NextCell

               MOVEA.L   (A7)+,A0            ; pop return pc
               MOVEA.L   (A7)+,A1            ; pop addr of ptrCell
               MOVEA.L   (A7)+,A2            ; pop ptrList
               CMP.L     #-1,(A1)            ; ptrCell = BAD ?
               BEQ       EOL                 ; yes, exit
               CMP.L     (A1),A2             ; ptrCell = ptrList ?
               BEQ       EOL                 ; yes, end of list
               MOVEA.L   (A1),A2             ; ptrCell := ptrCell^.link
               MOVE.L    (A2),(A1)
               JMP       (A0)                ; return
EOL
               MOVE.L    #-1,(A1)            ; ptrCell := BAD
               JMP       (A0)                ; return

;************************************************************************
;  PutFreeLen -  Assign longint to 3-byte integer length field
;
;  INTERFACE
;       procedure  PutFreeLen ( value   : longint;
;                               ptrCell : cell_ptr );
;
;************************************************************************

               .SEG       'ni_scAvenger'
               .PROC      PutFreeLen
               .DEF       PutFsize

PUTFSIZE
               MOVEA.L   (A7)+,A0            ; pop return pc
               MOVEA.L   (A7)+,A1            ; pop cell address
               ADDA.L    #8,A1               ; offset to length field
               MOVE.B    (A1),D0             ; save high byte of target
               MOVE.L    (A7)+,(A1)          ; move whole longint
               MOVE.B    D0,(A1)             ; restore high byte of target
               JMP       (A0)                ; return


;************************************************************************
;  GetFreeLen -  Return longint extracted from 3-byte integer length field
;
;  INTERFACE
;       function  GetFreeLen ( ptrCell : cell_ptr ) : longint;
;
;************************************************************************

               .SEG       'ni_scAvenger'
               .PROC      GetFreeLen
               .DEF       GetFsize

GETFSIZE
               MOVEA.L   (A7)+,A0            ; pop return pc
               MOVEA.L   (A7)+,A1            ; pop cell address
               MOVE.L    8(A1),(A7)          ; return longint
               CLR.B     (A7)                ; clear high byte of return
               JMP       (A0)                ; return


               .END
ÿ