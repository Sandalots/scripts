
(*********************************************************************************)
(*                                                                               *)
(*  (c) copyright 1983, 1984  Apple Computer Inc.                                *)
(*                                                                               *)
(*********************************************************************************)


  {$S fs4 }
  procedure Fs_Utilities (* var ecode : error;
                            var Parms : ut_parmt *);


    function getdevnum ( var ecode   : error;
                         var devName : e_name ) : integer;

    var
         device : integer;
         path   : pathname;
         ptrSysloc : slocal_ptr;

    begin
        ecode := 0;

        if length(devName) = 0 then
        begin
          (* A null name indicates the working device *)
          ptrSysloc := pointer(b_syslocal_ptr);
          device := ptrSysloc^.working_dev;
        end
        else
          device := map_dev_name(devName);

        if device < firstdev then
          ecode := E_DEV_RANGE;   (* dev name does not map to a valid dev number *)

        getdevnum := device;
    end{func getdevnum};



    procedure newv (* idev_name, pages, newvolname, newpassword, max_sfiles *);

    label 1;

    var device : integer;

    begin
      device := getdevnum (ecode, parms.idev_name);
      if ecode <> 0 then
      begin
        goto 1;
      end;
      if not configinfo [device]^.blockstructured then
      begin
        ecode := E_NOT_BLOCK_DEV;   (* device is not block structured *)
      end
      else
      begin
        if mounttable [device] <> pointer (badptr1) then
        begin
          ecode := E_DEV_MOUNTED; (* volume already mounted *)
        end
        else
        begin
          newvolume (ecode, device, parms.pages, parms.max_sfiles,
                     parms.newvolname, parms.newpassword);
        end;
      end;

    1:

    end;   (* of procedure newv *)





    procedure zapvol (* dev_name *);

    label  1;

    var
         devnum : integer;
         ptrDCB : DCBptr;
         locl_ecode : integer;

    begin
        devnum := getdevnum(ecode, Parms.dev_name);
        if ecode > 0 then goto 1;

        if not configinfo[devnum]^.blockstructured then
          ecode := E_NOT_BLOCK_DEV
        else
        begin
          ptrDCB := mounttable[devnum];
          if ptrDCB = pointer(badptr1) then
            ecode := E_NO_DEV_MOUNTED
          else with ptrDCB^.MDDFdata^ do
          begin
            fsversion := -fsversion;     (* zaps or unzaps the volume *)
            ptrDCB^.MDDFdirty := true;
            flushfs(locl_ecode);
          end{if};
        end;

    1:  (* exit *)
    end{proc zapvol};




    procedure format_twiggy (* dev_name *);

    label  1;

    var
         devnum    : integer;
         ptrConfig : ptrdevrec;

    begin
        devnum := getdevnum(ecode, Parms.dev_name);
        if ecode > 0 then goto 1;

        if mounttable[devnum] <> pointer (badptr1) then
          ecode := E_DEV_MOUNTED  (* volume already mounted *)
        else
        begin
          (* Load the configurable driver *)
          ptrConfig := configinfo[devnum];
          if ptrConfig^.devt <> non_io then
            UP(ecode, ptrConfig, nil);
          if ecode > 0 then goto 1;

          with ptrConfig^ do
          begin
            if (devname = '#14#1') or
               (devname = '#14#2') or
               (devname = '#13') then
              pdev_control(ecode, devnum, dskformat)
            else
              ecode := E_NOT_TWIGGY;
          end{with};
        end{if};

    1:  (* exit *)
    end{proc format_twiggy};






    procedure eject_twiggy (* dev_name *);

    label 1;

    var devnum : integer;
        extconfig_ptr: ^ext_diskconfig;
        ptrConfig : ptrdevrec;

    begin
      devnum := getdevnum(ecode, Parms.dev_name);
      if ecode > 0 then goto 1;

      ptrConfig := configinfo[devnum];
      if ptrConfig^.devt <> non_io then
        UP(ecode, ptrConfig, nil);
      if ecode > 0 then goto 1;

      extconfig_ptr := pointer(ptrConfig^.ext_addr);
      if extconfig_ptr^.ejectable then
      begin
        if mounttable[devnum] <> pointer (badptr1) then
          ecode := E_DEV_MOUNTED (* volume already mounted *)
        else
          pdev_control(ecode, devnum, dskunclamp);
      end
      else
        ecode := E_NOT_TWIGGY;

    1:  (* exit *)
    end{proc eject_twiggy};







    procedure block_mount (* mon_unitnum, twig_unitnum (1 = UPPER, 2 = LOWER) *);

    const os_size = 1684;
          mon_size = 1702;

    var
         drive : integer;
         p_extconfig: ^ext_diskconfig;

    begin
      with Parms do
      begin
        if (configinfo [twig_unitnum]^.devname = '#14#1') or
           (configinfo [twig_unitnum]^.devname = '#14#2') then
        begin
          if mounttable [twig_unitnum] = pointer (badptr1) then
          begin                       (* not OS mounted, so proceed *)
            (* a block structured, unmounted Twiggy, proceed *)
            (*$IFC OS15*)
            p_extconfig:=pointer(configinfo[twig_unitnum]^.ext_addr);
            (*$ENDC*)
            if mon_unitnum = 0 then  (* configure as OS Twiggy *)
            begin
              (*$IFC OS15*)
              p_extconfig^.num_bloks := os_size;
              (*$ELSEC*)
              configinfo [twig_unitnum]^.num_bloks := os_size;
              (*$ENDC*)
            end
            else if mon_unitnum = 1 then  (* configure as Monitor Twiggy *)
            begin
              (*$IFC OS15*)
              p_extconfig^.num_bloks := mon_size;
              (*$ELSEC*)
              configinfo [twig_unitnum]^.num_bloks := mon_size;
              (*$ENDC*)
            end
            else
            begin (* error, bad function *)
              ecode := E_FUNCTION;
            end;
          end
          else
          begin
            ecode := E_DEV_MOUNTED;
          end;
        end
        else
        begin
          ecode := E_NOT_TWIGGY;
        end;
      end;
    end;  (* of procedure block_mount *)






    procedure ubd (* level : integer *);

    label 1;
    const ubd_sg_need = 2840;   (* 770*2 for sysglobal space for 2 mounts during ubd )
                                   + 170*2 for 2 sfcb's for open of 2 files
                                   + 1000 for some margin for misc *)
                                (* global refnum is not allowed during ubd *)
          ubd_sl_need = 500;    (* at least 26*2 for 2 refdb's + some margin for misc *)

    var pcb_ptr : ptr_pcb;
        temp_addr, temp_nonPreempt : absptr;
        i : integer;
        no_space : boolean;

    begin
      ecode := 0;
      if mounttable [bootdev] = pointer (badptr1) then
      begin
        ecode := E_BOOT_NOT_MOUNT;           (* boot disk was not mounted *)
        goto 1;
      end;
      if mounttable [bootdev]^.status <> fsmounted then
      begin
        ecode := E_BOOT_NOT_MOUNT;           (* boot disk was not mounted *)
        goto 1;
      end;

      if boot_dcbptr <> badptr1 then
      begin
        ecode := E_BOOT_MOUNT;               (* boot disk already unmounted *)
        goto 1;
      end;

      pcb_ptr := pointer (c_pcb_ptr);

                                  (* so that no other calls can be made to this disk *)
      LOCKOS (ecode);                (* lock OS code we are going to need later *)
                   (* This call could cause MM to load code segs in - always return 0 *)

        (* sysglobal and syslocal can not be expanded after ubd, so make sure it has
           enough space now, and make MM to expand them if needed. *)
      if not getspace (ubd_sl_need, b_syslocal_ptr, temp_addr) then
        begin
          ecode := 1139;          (* not enough sysglobal space left to do the work *)
          goto 1;
        end;
      relspace (temp_addr, b_syslocal_ptr);         (* do not need it now *)

      { temporarily clear non-preemptive scheduling conditions }
      temp_nonPreempt := nonPreempt;
      nonPreempt := ord(nil);

      (* Reserve a chunk of SysGlobal space *)
      if SuperGetspace(ubd_sg_need, temp_addr) then
        relspace (temp_addr, b_sysglobal_ptr)        (* do not need it now *)
      else
      begin
        nonPreempt := temp_nonPreempt;  (* restore sched conditions before exiting *)
        ecode := 1139;          (* not enough sysglobal space left to do the work *)
        goto 1;
      end;

    {  But there is no way to know before hand whether there is enough physical memory
       to expand or not, and return to user. If MM can not find enough memory,
       system error !!!!!

      if ecode <> 0 then
        begin
          ecode := 1139;          (* not enough sysglobal space left to do the work *)
          goto 1;
        end;
      end;
     }

      while MMBUSY do
        begin  { spin wait until MemMgr is finished }
          nonPreempt := ord(nil);
          Enter_Scheduler
        end;

      nonPreempt := temp_nonPreempt;

      {SET_MMBUSY (true);}      (* MM has just finished swapping, all the data segs
                                 on disk are as much consistent as you can get, so
                                 do not let MM run beyond this point. *)
        (* Before we turn off scheduling, all the semaphores must be released, so that
           the Filer will not be blocked on semaphores after disabling schduler.
           So now is the last chance to do this. *)
        (* But if the following wait_sem's cause page faults from some other process,
           MM will not do any more swapping. That is fine, the other process will just
           have to wait after we reset MMBUSY again. *)
      wait_sem (mounttable [bootdev]^.volSem, []); (* wait for pathname intfcs to finish *)
      signal_sem (mounttable [bootdev]^.volSem);
      wait_sem (mounttable [bootdev]^.semio, []);    (* wait for open_sfile to complete *)
      signal_sem (mounttable [bootdev]^.semio);
      wait_sem (mounttable [bootdev]^.semmap, []);     (* wait for flush to complete *)
      signal_sem (mounttable [bootdev]^.semmap);
      wait_sem (mounttable [bootdev]^.semMDDF, []);    (* wait for flush to complete *)
      signal_sem (mounttable [bootdev]^.semMDDF);

      vmbusy := true;       (* to make other processes calling vm not wait on semvm's
                               and semfreeslot once they complete vm by the following
                               wait_sem's, since filer needs them to do i/o *)

      wait_sem (semfreeslot, []);      (* then wait for vm to complete freeslot usage*)
      for i := 1 to bufferpages do
      begin
        wait_sem (semvm [i], []);      (* wait for vm to complete *)
        signal_sem (semvm [i]);        (* must wait for all slots  *)
      end;
      signal_sem (semfreeslot);

      vmbusy := false;      (* so that filer can use vm without having to wait on
                               semvmbusy. *)

      ubd_pcb_ptr := c_pcb_ptr;  (* this is the process unmounting the boot device *)
      SCHEDDISABLE;   (* turn off scheduler so nobody else will do any I/O accesses! *)
                      (* especially no more i/o to the boot disk allowed while this
                         process is blocked in flushfs. *)
      getid (mounttable [bootdev]^.MDDFdata^.overmount_stamp);
                  (* set up UID (timestamp) and states so remount will be verifiable *)
                            (* record in MDDF in both memory and disk copy *)

      mounttable [bootdev]^.MDDFdata^.mountinfo := temp_unmounted;   (* record state *)
      mounttable [bootdev]^.MDDFdirty := true;

      flushfs (ecode);    (* flush file system to get as much consistent as possible *)
      mounttable [bootdev]^.status := temp_unmounted;
      if ecode <> 0 then
      begin
        mounttable [bootdev]^.status := fsmounted;
        ubd_pcb_ptr := ord(nil);
        SCHEDENABLE;                (* undo all these to return error *)
        RESET_VMBUSY;               (* wake up other processes waiting on semvmbusy *)
        UNLOCKSEGS (ecode);         (* no error return *)
        {SET_MMBUSY (false);}
        goto 1;
      end;

      io_disinit (ecode, bootdev);        (* clear all references in VM for boot dev *)
      if ecode <> 0 then
      begin
        mounttable [bootdev]^.status := fsmounted;
        ubd_pcb_ptr := ord(nil);
        SCHEDENABLE;                (* undo all these to return error *)
        RESET_VMBUSY;               (* wake up other processes waiting on semvmbusy *)
        UNLOCKSEGS (ecode);
        {SET_MMBUSY (false);}
        goto 1;
      end;

                 (* unhook DCB from mounttable and put in sysglobal for temp unmount *)
      boot_dcbptr := ord (mounttable [bootdev]);
      boot_rootptr := ord (rootptr [bootdev]);
      mounttable [bootdev] := pointer (badptr1);
      rootptr [bootdev] := pointer (badptr2);

    1:

    end;   (* of procedure ubd *)










    procedure rbd;

    label 1;
    var temp_addr : absptr;
        tempecode : error;
        ubd_MDDF : ^MDDFdb;
        ubd_dcbptr : dcbptr;                (* boot disk dcb pointer during ubd *)

    begin
      ecode := 0;
                                            (* see if boot device is available *)
      if boot_dcbptr = badptr1 then
      begin
        ecode := E2_BOOT_MOUNT;             (* boot disk was not unmounted *)
        goto 1;
      end;
      if mounttable [bootdev] <> pointer (badptr1) then
      begin
          ecode := E_VOL_MOUNTED;               (* boot disk was unmounted, but some
                                          other disk is still mounted on the boot dev*)
          goto 1;
      end;

                                            (* get space for MDDF *)
      if not getspace(sizeof(MDDFdb), b_sysglobal_ptr, temp_addr) then
      begin
        ecode := E_MDDF_NOSPACE;    (* no space for MDDF, space should have been
                                       reserved, this should not happen *)
        goto 1;
      end;

      ubd_MDDF := pointer (temp_addr);

                           (* check timestamps and states to see if a valid remount *)
      ubd_dcbptr := pointer (boot_dcbptr);
      with ubd_dcbptr^ do
      begin
        mounttable [bootdev] := ubd_dcbptr;

        mddf_io(ecode, bootdev, temp_addr, READOP);
        if ecode > 0 then
        begin                       (* I/O error, don't bring up *)
          relspace (temp_addr, b_sysglobal_ptr);
          mounttable[bootdev] := pointer(badptr1);
          goto 1;
        end;
        if (MDDFdata^.overmount_stamp.a <> ubd_MDDF^.overmount_stamp.a)
          or (MDDFdata^.overmount_stamp.b <> ubd_MDDF^.overmount_stamp.b)
          or (MDDFdata^.mountinfo <> ubd_MDDF^.mountinfo) then
        begin                               (* not remounting same boot disk *)
          ecode := E_BT_REMOUNT;
          relspace (temp_addr, b_sysglobal_ptr);
          mounttable[bootdev] := pointer(badptr1);
          goto 1;
        end;

        relspace (ord (MDDFdata), b_sysglobal_ptr);
                                            (* release the old memory copy of MDDF *)
        MDDFdata := pointer (temp_addr);               (* use the new MDDF  *)
                                            (* restore bit map too *)

        Make_MRData(ecode, MDDFdata^.bitmap_bytes, temp_addr);
        if ecode > 0 then
        begin         (* no space for the bitmap, space should have been reserved,
                         this should not happen *)
          ecode := E2_BITMAP_NOSPACE;
          goto 1;
        end;

                    (* hook up sysglobal-reserved DCB back onto mounttable *)
        (* read allocation bit map into memory *)
        bitmap_io (ecode, bootdev, readop, temp_addr);
        if (ecode > 0) then
        begin             (* I/O error reading in bit map *)
          Rel_MRData(tempecode, temp_addr);
          mounttable [bootdev] := pointer (badptr1);       (* back to ubd state *)
          goto 1;
        end;

        Rel_MRData(tempecode, ord(allocmap));
                                    (* release the old memory copy of bit map *)
        allocmap := pointer (temp_addr);    (* use the new bit map *)
      end;  (* with ubd_dcbptr *)

                    (* make sure volume goes on same device as originally mounted on *)
      rootptr [bootdev] := pointer (boot_rootptr);
      boot_dcbptr := badptr1;
      boot_rootptr := badptr2;

      mounttable [bootdev]^.MDDFdata^.mountinfo := unmounted;  (* restore normal state *)
      mounttable [bootdev]^.MDDFdata^.overmount_stamp.a := 0;
      mounttable [bootdev]^.MDDFdata^.overmount_stamp.b := 0;
      mounttable [bootdev]^.MDDFdirty := true;

      mounttable [bootdev]^.status := fsmounted;    (* back to mounted state *)

      SCHEDENABLE;                (* put the execution environment to normal *)
      RESET_VMBUSY;               (* wake up other processes waiting on semvmbusy *)
      UNLOCKSEGS (ecode);         (* no error return *)
      {SET_MMBUSY (false);}

      ubd_pcb_ptr := ord(NIL);   (* process no longer has boot device unmounted *)

    1:

    end;   (* of procedure rbd *)





    procedure tbt_copy (* from_dev, to_dev, buffaddr, buffsize *);

    label 1, 2;

    var sdevice, ddevice, cycles, remainder, i, buf_pages : integer;
        tempecode : error;
        bnum, srcblocks, actual : longint;
        MDDFdptr : ^MDDFdb;
        (*$IFC OS15*)
        p_sextconfig, p_dextconfig: ^ext_diskconfig;
        (*$ENDC*)

    begin
      sdevice := getdevnum (ecode, parms.from_dev);   (* source device *)
      if ecode <> 0 then
      begin
        goto 1;
      end;

      ddevice := getdevnum (ecode, parms.to_dev);     (* destination device *)
      if ecode <> 0 then
      begin
        goto 1;
      end;

      if (not configinfo [sdevice]^.blockstructured) or
         (not configinfo [ddevice]^.blockstructured) then
      begin
        ecode := E_NOT_BLOCK_DEV;   (* device is not block structured *)
      end
      else
      begin
        if (mounttable [sdevice] <> pointer (badptr1)) or
           (mounttable [ddevice] <> pointer (badptr1)) then
        begin
          ecode := E_DEV_MOUNTED; (* volume already mounted *)
        end
        else
        begin
          (* These mounts are critical because they UP the devices *)
          def_mount (ecode, sdevice);  (* keeps others from mounting the device *)
          if ecode > 0 then goto 1;
          def_mount (ecode, ddevice);  (* keeps others from mounting the device *)
          if ecode > 0 then
          begin
            def_unmount (tempecode, sdevice);
            goto 1;
          end;
          (*$IFC OS15*)
          p_sextconfig:=pointer(configinfo [sdevice]^.ext_addr);
          p_dextconfig:=pointer(configinfo [ddevice]^.ext_addr);
          srcblocks := p_sextconfig^.num_bloks;
          if srcblocks > p_dextconfig^.num_bloks then
          (*$ELSEC*)
          srcblocks := configinfo [sdevice]^.num_bloks;
          if srcblocks > configinfo [ddevice]^.num_bloks then
          (*$ENDC*)
          begin
            ecode := E_VOL_TOO_SMALL;
            goto 1;
          end;
          buf_pages := parms.buffsize div mounttable [sdevice]^.MDDFdata^.blocksize;
               (* copies headers too *)
          if buf_pages < 1 then
          begin
            ecode := E_BUF_TOO_SMALL;
            goto 2;
          end;
          cycles := srcblocks div buf_pages;
          remainder := srcblocks mod buf_pages;
          bnum := 0;
          for i := 1 to cycles do
          begin
            lisaio (ecode, sdevice, parms.buffaddr, bnum, buf_pages, actual,
                                                     raw_io, nil, readop);
            lisaio (ecode, ddevice, parms.buffaddr, bnum, buf_pages, actual,
                                                    raw_io, nil, writeop);
            bnum := bnum + buf_pages;
          end;
          if remainder > 0 then
          begin
            lisaio (ecode, sdevice, parms.buffaddr, bnum, remainder, actual,
                                                     raw_io, nil, readop);
            lisaio (ecode, ddevice, parms.buffaddr, bnum, remainder, actual,
                                                    raw_io, nil, writeop);
          end;

          (*$IFC OS15*)
          bnum := p_sextconfig^.fs_strt_blok; (* MDDF disk address *)
          (*$ELSEC*)
          bnum := configinfo [sdevice]^.fs_strt_blok; (* MDDF disk address *)
          (*$ENDC*)
          MDDFdptr := pointer (parms.buffaddr + sizeof (pagelabel));

                      (* read old MDDF, set DT_copied *)

          lisaio (ecode, sdevice, parms.buffaddr,
                                  bnum, 1, actual, raw_io, nil, readop);
          MDDFdptr^.DT_copied := timestamp;
               (* copy thread is bumped on both source and dest *)
          MDDFdptr^.copy_thread := MDDFdptr^.copy_thread + 1;
          (* write it to source device *)
          lisaio (ecode, sdevice, parms.buffaddr, bnum, 1, actual,
                                                 raw_io, nil, writeop);

          (* reset DT_copied, set DT_copy_created, copy_thread, mountinfo,    *)
          (* overmount_stamp, tbt_copied, backup_volid                                      *)

          with MDDFdptr^ do
          begin
            DT_copy_created := DT_copied;
            zero_time (DT_copied);
            backup_volid := volid;  (* id of volume this volume backs-up *)
            getid(volid);           (* backup volume gets a new volid    *)
            mountinfo := unmounted;
            overmount_stamp.a := 0;
            overmount_stamp.b := 0;
            tbt_copied := true;
          end;

          (* write it to destination device *)
          lisaio (ecode, ddevice, parms.buffaddr, bnum, 1, actual,
                                                 raw_io, nil, writeop);
    2:

          def_unmount (tempecode, sdevice);
          def_unmount (tempecode, ddevice);
        end;
      end;

    1:

    end;   (* of procedure tbt_copy *)





    procedure wrbt (* dev_name *);

    label 1;

    var devnum : integer;
        progname : pathname;

    begin
      devnum := getdevnum (ecode, parms.dev_name);
      if ecode <> 0 then
      begin
        goto 1;
      end;
      if mounttable [devnum] <> pointer (badptr1) then
      begin
        ecode := E_DEV_MOUNTED; (* volume already mounted *)
        goto 1;
      end;

      (*$IFC OS15*)
      if configinfo[devnum]^.devt <> non_io then
        UP(ecode,configinfo[devnum],nil);
      if ecode > 0 then goto 1;
      (*$ENDC*)

      GET_BT_FILE(devnum, progname);
      PWBT (ecode, progname, devnum);

    1:

    end;   (* of procedure wrbt *)




    procedure  trans_psw;

    var
         SetParms : sh_parmt;
         device   : integer;
         snum     : integer;
         S        : s_entry;
         H        : hentry;

    begin
        if parms.op = PSWWRITE then
        begin
          SetParms.command := c_passwd;
          moveleft(parms.password, SetParms.newPassword, sizeof(SetParms.newPassword));
          Set_Hentry(ecode, parms.path, pointer(badptr2), SetParms);
        end
        else
        begin  (* op = PSWREAD *)
          FetchHentry(ecode, parms.path, device, snum, S, H);
          if ecode <= 0 then
          begin
            parms.password := H.password;
            signal_sem(mounttable[device]^.volSem);
          end
          else
            parms.password := '';
        end{if};
    end{proc trans_psw};




    procedure  general_purpose;

    var
         device         : integer;
         ptrDevName     : ^e_name;
         pbyte1, pbyte2 : ^byte;
         save_byte      : byte;
         ptrConfig      : ptrdevrec;

    begin
         with Parms do
         begin
           case funct of

                1: begin
                     (*  Peek  *)
                     pbyte1 := pointer(parm1);  (* domain 0 address *)
                     pbyte2 := pointer(ord(@parm2) + 3);  (* low-order byte of parm2 *)
                     pbyte2^ := pbyte1^;
                   end;
                2: begin
                     (*  Poke  *)
                     pbyte1 := pointer(parm1);  (* domain 0 address *)
                     pbyte2 := pointer(ord(@parm2) + 3);  (* low-order byte of parm2 *)
                     save_byte := pbyte1^;
                     pbyte1^ := pbyte2^;    (* assigns low-order byte of parm2 *)
                     pbyte2^ := save_byte;  (* return byte formerly at poke location *)
                   end;
                3: begin
                     (*  Convert device name to device number  *)
                     ptrDevName := @parm_array;
                     parm1 := map_dev_name(ptrDevName^);
                   end;
                4: begin
                     (*  Make device driver permanently loaded  *)
                     ptrDevName := @parm_array;
                     (* map device name to device number *)
                     device := getdevnum(ecode, ptrDevName^);
                     if ecode <= 0 then
                     begin
                       ptrConfig := configinfo[device];
                       with ptrConfig^ do
                       begin
                         (* return the former state of the permanent flag *)
                         parm2 := ord(permanent);
                         (* set the permanent flag to the new state *)
                         permanent := (parm1 <> 0);
                       end{with};
                     end{if};
                   end;
                otherwise
                   ecode := E_NOT_IMPLEMENTED;   (* function not implemented *)
           end{case};
         end{with};
    end{proc general_purpose};





  (******************)
  (*  FS_Utilities  *)
  (******************)

  begin
    flushfs(ecode);

    case Parms.command of
                   no_op:
                     (* No operation *);
      {'I','i', 1} initvol:
                     begin
                     (* Write a new file system volume *)
                     Size_Stack(ecode, 13312);
                     newv;
                     end;
      {'Z','z', 2} zap:
                     (* Make a file system volume unmountable *)
                     zapvol;
      {'D','d', 3} dumpdata:
                     (* VolCheck utility code *)
                     {$IFC DEBUG2}
                     DumpSys(parms.ddev_name, parms.pagenum)
                     {$ENDC}  ;
      {'T','t', 4} setfstrace:
                     (* Set the file system trace-fence location *)
                     {$IFC DEBUG1}
                     set_trace_fence(FS, parms.level)
                     {$ENDC}  ;
      {'F','f', 5} fsscavenge:
                     (* Scavenge a file system volume *)
                     begin
                        Size_Stack(ecode, 9216);
                        Scavenge(ecode, parms.sdev_name, parms.soptions);
                     end;
      {         6} writeBT:
                     (* Write boot blocks on a disk *)
                     wrbt;
      {         7} format:
                     (* Format a disk volume *)
                     format_twiggy;
      {         8} verify:
                     (* Unimplemented verify volume feature *)
                     ;
      {        9} eject:
                     (* Eject media *)
                     eject_twiggy;
      {        10} flushbuffers:
                     (* Flush file system buffers; side-effect of FS_Utilities *)
                     ;
      {        11} boot_unmount:
                     (* Temporarily unmount the boot volume *)
                     ubd;
      {        12} boot_remount:
                     (* Remount the temporarily-unmounted boot volume *)
                     rbd;
      {        13} copy_volume:
                     (* Copy one disk volume to another track-by-track *)
                     tbt_copy;
      {        14} mount_BD:
                     (* Mount a foreign Twiggy diskette volume *)
                     block_mount;
      {        15} xfer_passwd:
                     (* Transfer the encrypted password from one file to another *)
                     trans_psw;
      {        16} gp:
                     (* Extensible utility call *)
                     general_purpose;

                   otherwise  ecode := E_FUNCTION;
    end{case};

  end{proc FS_Utilities};










  {$S fs1 }
  procedure FS_Setup (* sl_ptr : slocal_ptr *);

  var
       ecode    : integer;
       nullPath : pathname;

  begin     (* called when a process is set up to run *)
    with sl_ptr^ do
    begin
      head_fs_refdb.fwd_link := ord(@sl_ptr^.head_fs_refdb) - ord(sl_ptr);
      head_fs_refdb.bkwd_link := sl_ptr^.head_fs_refdb.fwd_link;
      working_dev := bootdev;    (* default working device *)
      scanDevice := bootdev;    (* default reset catalog values *)
      scanIndex := 0;
      gne_recptr := badptr2;    (* init to bad value *)
      netState := 0;  (* absptr to network state record *)
      scanID := ROOT_NID;
      topol := 0;
      workDirID := ROOT_NID;
      scanPage := -1;
      scanMode := dirscan;
      ptrScanStack := pointer(BAD);
    end{with};
  end{proc FS_Setup};






  {$S fs1 }
  procedure FS_Cleanup;

  var
      refdbptr : fref_ptr;
      cptr, headaddr : absptr;
      c_syslocal_ptr : slocal_ptr;
      ecode : error;
{$IFC DEBUG2}
      ptrPCB : ptr_PCB;
{$ENDC}

  begin
{$IFC DEBUG2}
      if trace(FS, 90) then
      begin
        ptrPCB := pointer(c_pcb_ptr);
        writeln('FS_CLEANUP  process = ',ptrPCB^.glob_id);
      end;
{$ENDC}

    c_syslocal_ptr := pointer (b_syslocal_ptr);    (* current syslocal pointer *)
    if c_syslocal_ptr^.gne_recptr <> badptr2 then  (* get rid of gne rec space *)
      relspace (c_syslocal_ptr^.gne_recptr, b_syslocal_ptr); (* do not need it now *)

    cptr := c_syslocal_ptr^.head_fs_refdb.fwd_link + b_syslocal_ptr;
                                                              (* absptr of next refdb *)
    headaddr := ord (@c_syslocal_ptr^.head_fs_refdb);(* absptr to head of refdb chain *)

    while cptr <> headaddr do
    begin
      refdbptr := pointer (cptr);                  (* get pointer to a refdb *)
      if configinfo [refdbptr^.refdb_devnum]^.blockstructured then
        refdbptr^.fcbptr^.OS_c_request := true;  (* OS-originated close *)
      show_refnum(ecode, refdbptr^.refnum);    (* precaution: unprotect the refnum *)
      close_object (ecode, refdbptr^.refnum);  (* close open objects, if any *)
      cptr := c_syslocal_ptr^.head_fs_refdb.fwd_link + b_syslocal_ptr;
    end{while};
  end{proc FS_Cleanup};
ÿ