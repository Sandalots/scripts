UNIT GLOBALDA; (* operating system global data *)

  { Copyright 1983, 1984, Apple Computer Inc. }

INTERFACE

USES
  (*$U- *)   { for the rest of the OS }
  (*$Uobject/driverdefs.obj*)
    driverdefs,
  (*$U object/hwint.obj*)
    hwint;

(*$IFC not debug2 *)
(*$R-*)
(*$ENDC *)


(*$X- *)   { Disallow automatic stack expansion in system code }



CONST
  (* COMMON SYSTEM ERROR CODES *)
  e_badrefnum = 1999;  (* invalid refnum *)
  e_badparmaddr = 1998;(* invalid parameter address *)

  e_badfreepool = 11901; (* call to getspace/relspace or freepool itself bad *)


  (********************************************************************************
  (**
  (** The delineated values are also defined in PASCALDEFS assembly language routine.
  (**
  (*********************************************************************************)


  (**) subc_max=  150;          (* maximum system call subcode *)

       maxDomain = 3;           (* maximum domain number *)
       sentDomain = 4;          (* sentinel (non-existent) domain # for DCT def *)

       beg_glorefnum = 1;       (* first valid global refnum value     *)
       max_glorefnum = 10000;   (* max global refnum value             *)
       beg_locrefnum = 10001;   (* first local refnum value            *)
       max_locrefnum = 32767;   (* max local refnum value              *)
       defblocksize = 536;      (* number of bytes in a disk block     *)
       bufferpages = 21;        (* number of block slots in memory     *)
       max_pathname = 255;      (* maximum length of a pathname string *)
       max_vmerr = 14;          (* bound of list of FS buffer write errs *)



TYPE
  sysareaT = (sysglob,sysloc);  (* system area types*)

  pathname = string[max_pathname];  (* pathname for object names *)
  ptr_pathname = ^pathname;

  domainRange = 0..maxDomain;   (* possible domains in system *)

  seg_bitmap = packed array[0..127] of boolean;

  NodeIdent = integer;  (* File Sys directory Unique ID *)

  NodeScanType = ( dirscan, treescan );

  (* record key *)
  RecKey = string[35];
  key_ptr = ^RecKey;

  dirmrk_ptr = ^DirMarker;
  DirMarker = record
                link    : dirmrk_ptr;
                page    : longint;
                index   : integer;
                id      : NodeIdent;
                saveKey : RecKey;
                topol   : longint;
              end;

(*********************************)
(*                               *)
(* REFNUM RECORD                 *)
(*                               *)
(*********************************)

  refnum_rec = record
                 hd_refnum_list: linkage;   (* list of refnum entries that*)
                                            (* have been allocated        *)
                 next_refnum: int2;         (* the next refnum value available*)
                                            (* for allocation                 *)
               end;

  ptr_refnum_rec = ^refnum_rec;


(*********************************)
(*                               *)
(* DEFINITION OF SYSLOCAL RECORD *)
(*                               *)
(*********************************)

{ Definitions preceeded by (**) cannot be changed nor can their location }
{ within the record be changed without corresponding changes to PASCALDEFS }
(**) env_area = record  (* environment save area *)
(**)         PC: int4;    (* program counter *)
(**)         SR: int2;    (* status register *)
(**)         D0: int4;    (* data registers 0 - 7 *)
(**)         D1: int4;
(**)         D2: int4;
(**)         D3: int4;
(**)         D4: int4;
(**)         D5: int4;
(**)         D6: int4;
(**)         D7: int4;
(**)         A0: int4;    (* Address  registers 0 - 7 *)
(**)         A1: int4;
(**)         A2: int4;
(**)         A3: int4;
(**)         A4: int4;
(**)         A5: int4;
(**)         A6: int4;
(**)         A7: int4;
(**)  end;

slocal_ptr = ^syslocal;             (* Ptr to syslocal record *)

syslocal = record (* SYSLOCAL DATA AREA *)
(**)         sl_free_pool_addr: absptr; (* pointer to free space pool *)
(**)         size_slocal: int2;         (* size of segment *)

(**)         env_save_area: env_area; (* process environment area *)
(**)         SCB : record  (* System Call Block *)
(**)                 A5value  : int4;         (* user's A5 setting *)
(**)                 PCvalue  : int4;         (* user's return PC *)
(**)                 domvalue : domainRange   (* user's domain on sys call entry *)
(**)               end;
(************************************************************************)

             mrbt_addr: absptr;  (* pointer to mrbt record in syslocal *)
             lbt_addr: absptr;   (* ptr to ldsn binding table *)
             csreq_addr: absptr; (* ptr to sdb which has been faulted for *)
             csreq_type: int2;   (* The type segment fault; 0- code; 1- data *)

             hd_ds_refdb_list: linkage; (* list of refdb entries that *)
                                        (* define data segments       *)

             loc_refnum: refnum_rec;    (* process local refnums *)

             head_ecb: linkage;         (* exception control block list head *)
             last_ecb_ptr: absptr;      (* last occuring exception *)
             divz_ecb_rp: relptr;       (* divide by zero *)
             ovfw_ecb_rp: relptr;       (* overflow       *)
             oob_ecb_rp: relptr;        (* out of bounds  *)
             abort_ecb_rp: relptr;      (* abort          *)
             nmik_ecb_rp : relptr;      (* NMI key        *)
             adr_return_excep: longint; (* address of return_excep procedure in oslib *)

             head_ec_refdb : linkage;   (* list of event channel refdbs *)
             head_lec_refdb : absptr;   (* list of local event channel refdbs *)

             commchan : int2;           (* refnum of comm channel w/ father *)
             termE_ptr : absptr;        (* ptr to user's SYS_SON_TERM event *)
             termE_cause : int4;        (* cause of termination to send w/ event *)
             maxStk_pages : int2;       (* maximum allowable size of stack *)

             head_fs_refdb : linkage;   (* list of file system refdbs *)

             hd_seglock_list: linkage;  (* queue of all segslocked by this process*)

             smrbt_addr: absptr;        (* pointer to secondary mrbt entries *)

             netState: absptr;          (* pointer for networking structures *)

             working_dev : integer;     (* process working directory (device) *)
             workDirID   : integer;  (* ID of working directory node *)
             scanMode    : NodeScanType;
             scanID      : integer;  (* ID of directory being scanned *)
             scanDevice  : integer;  (* current device for catalog enumeration*)
             scanPage    : longint;  (* current enumeration page *)
             scanIndex   : integer;  (* current enumeration offset *)
             scanKey     : RecKey;   (* key located at (scanPage, scanIndex) *)
             scanLevel   : integer;  (* current scan directory's level in tree *)
             ptrScanStack : dirmrk_ptr;
             gne_recptr  : absptr;   (* get_next_entry record structure ptr *)
             topol       : longint;  (* snapshot of tree topology counter *)

             preLoaded: boolean;        (* true if process has been pre-loaded *)
             recordFaults: boolean;     (* true if MemMgr should record faults *)
             initSegMap: seg_bitmap;    (* code segment bit map for fetch-ahead *)

         end; (* of syslocal *)



refdbType = (dsrefdb,frefdb,ecrefdb);
refdbHead = record
              refdb_list: linkage;  (* linkage for refdb entries *)
              kind: refdbType;      (* type of refdb entry *)
            end;

DCTentry = record  (* Domain Control Table entry *)
(**)         owner : int4;       (* global id of process currently in domain *)
             refcounter : int2   (* shift counter for LRU replacement *)
           end;

(**********************************)
(*                                *)
(* SYSTEM CALL TABLE (sc_table)   *)
(*                                *)
(**********************************)

(**) sc_table = record
(**)              sc_adr: array[1..subc_max] of ^longint;
(**)              sc_par_no: array[1..subc_max] of int1;
(**)            end;


(****************************************************************************)

  UID = record  (*unique id*)
     a,b: longint
  end;

(**) rec_param_mem = record
(**)                   alarm_ref: integer;
(**)                   parm_mem: array[1..32] of int2
(**)                 end;

  pmemStatus = (PMg_SSg, PMg_SSb, PMb_SSg, PMb_SSb);   { parm mem status values }



(*************************************)
(*                                   *)
(* Types used for parameter checking *)
(*                                   *)
(*************************************)
pchk = record
         addr: absptr;
         readwrite: boolean;
       end;
pchkrange = 1..8;
parmcheck = record
              numcheck: int2;
              parms: array[pchkrange] of pchk;
            end;

(*$IFC debug1 *)
meas_Table = record  { internal measurements table }
               scHist : array[1..subc_max] of int4;   { 600 bytes }
               misc   : array[1..50] of int4          { 200 bytes }
             end;
(*$ENDC *)

(*  Record of FS buffer write error *)
 VmErrRec = record
              device : int2;  (* device/page location of write error *)
              page   : int4;
              sfile  : int2;  (* s-file containing the page *)
              error  : int2;  (* error code encountered on write *)
            end;

(* bit map utility types: *)
 bitmap   = packed array [0..255] of boolean;
 bitmapptr = ^bitmap;

(* low-level file system I/O mode *)
 PrimIOMode = ( synch, asynch );

(* file system levels of buffer flushing *)
              (* most flushing *)                   (* least flushing *)
 FlushLevel = ( highFlush, allocFlush, makeFlush, closeFlush, lowFlush );




VAR
  (***************************************)
  (*                                     *)
  (* DEFINITION OF SYSGLOBAL CELLS       *)
  (*                                     *)
  (***************************************)

  (*******************************************************************************
  (**
  (**  The displacements of these variables within sysglobal are defined as constant
  (**  displacements in the assembly language routine PASCALDEFS.  Any changes
  (**  to the definition of these variables must also be reflected in PASCALDEFS.
  (**
  (**
  (**) sg_free_pool_addr: absptr;   (* pointer to free space pool *)
  (**) size_sglobal: int2;          (* size of data segment *)
  (**)
  (**) port_cb_ptrs: rec_port_cb;   (* port control block for drivers *)
  (**) sysA5: int4;                 (* the system A5 register      *)
  (**) c_pcb_ptr: absptr;           (* Ptr to pcb of current process *)
  (**) configinfo: configtype;      (* table of configured devices*)

  (*  Configurable drivers rely on the placement of the above VARs !! *)

  (**) sct_ptr: absptr;             (* pointer to system call table *)
  (**) b_syslocal_ptr:  absptr;     (* pointer to syslocal base *)
  (**) Invoke_sched: boolean;       (* true if processor scheduling is needed *)
  (**) param_mem: rec_param_mem;    (* resident version of parameter memory *)
  (**) DCT: array[0..sentDomain] of DCTentry;   (* Domain Control Table *)
  (**) smt_addr: absptr;            (* ptr to smt *)

  (**) pe_loc1: int2;               (* original bad address from parity error*)
  (**) pe_loc2: int4;               (* full addr of loc being tested for pe *)
  (**) pe_phase: int1;              (* current phase of parity error recovery*)
  (**) pe_access:  int1;            (* type of access at time of pe *)
  (**) membase: int4;               (* physical address of memory base *)
  (**) memleng: int4;               (* byte length of physical memory*)

  (**) SchdAddr: absptr;            (* address of Scheduler code *)

  (**) stataddr: absptr;            (* address of statistics record *)
  (**)
  (**) sctab: sc_table;             (* system call table *)

  (**) lb_loaded : boolean;         (* true if Lisabug code has been loaded *)

  (**) lb_enabled : boolean;        (* true if Lisabug code has been enabled *)

                                    (* LRU for domain replacement *)
  (**) domainLRU : array[1..sentDomain] of domainRange;

  (**) domSMTBase : absptr;         (* ptr to base entry of SMT for current dom *)

  (**) bkwd_BlockQ : absptr;        (* backward ptr for Blocked queue *)
  (**) fwd_BlockQ : absptr;         (* forward ptr for Blocked queue *)
  (**) bkwd_ReadyQ : absptr;        (* backward ptr for Ready queue *)
  (**) fwd_ReadyQ : absptr;         (* forward ptr for Ready queue *)
  (**) mmrb_addr: absptr;           (* ptr to memory manager request record*)

  (*$IFC debug1 *)
  (**) measTable : meas_Table;      (* OS internal measurement counters, etc *)
  (*$ENDC *)

(*******************************************************************************)

  s_mrbt_addr: absptr;             (* ptr to mrbt for O.S. *)
  b_sysglobal_ptr: absptr;         (* pointer to sysglobal base *)

  sysg_free: int2;  (* number of integers in free pool in sysglobal *)
  sysl_free: int2;  (* number of integers in free pool in syslocal *)


  next_gprocid: int4;          (* global process id # to allocate next *)
  root_pcb_ptr : absptr;       (* ptr to root process in hierarchy *)
  mm_pcb_ptr: absptr;          (* ptr to memory manager process PCB *)

  sdb_sysglobal: absptr;       (* address of sdb for sysglobal data *)
  sdb_realmem: absptr;         (* address of sdb for real memory - used by memmgr*)

  lcb_addr: relptr;            (* relptr to loader control block *)

  glob_pcb_list : absptr;      (* ptr to global process list head *)

  sched_attn : absptr;         (* ptr to PCB of process for special scheduling *)

  nonPreempt : absptr;         (* Current 'active' non-preemptive process *)

  bootdev : integer;           (* device number of initial FS boot *)

  init_in_progress: boolean;   (* true during startup's initialization of system *)

  grow_sysglobal: boolean;     (* true if sysglobal needs expansion *)

  c_domain: integer;           (* index of current domain *)

  glo_refnum: refnum_rec;      (* global refnums *)

  serial_no : longint;         (* serial number of this machine *)

  mainmem : absptr;            (* address of system buffer memory area *)

  boot_dcbptr : absptr;        (* saved DCB ptr when boot volume temp unmounted *)

  boot_rootptr: absptr;        (* saved root ptr when boot volume temp unmounted*)

  no_syscodesegs: int2;        (* the number of os code segments             *)

  debugProcess : boolean;      (* true if process creation should set init BP *)

  anyBPs : boolean;            (* true if any break points exist *)

  pmem_state : pmemStatus;     (* current status of parameter memory *)

  osVers_num : int2;           (* this version of the OS - integer form *)

  unpacktabaddr: absptr;       (* address of table to unpack code segments *)

  IUDsdb_ptr : absptr;         (* ptr to sdb for Intrinsic Unit Directory *)

  going_down : integer;        (*If zero, means no previous system_error, but if   *)
                               (*  non-zero, is system_error errnum from first call *)

  io_retries : int2;           (* count of retries made by Lisaio *)

                               (* List of FS buffer write errors *)
  VmErrList  : array[0..max_vmerr] of VmErrRec;

  pVmErr     : int2;           (* index into list of FS flush errors *)

  EC_list : absptr;            (* ptr to local event channel ECCB list *)

  flush_level : FlushLevel;    (* level of file system buffer flushing *)

  fs_mode : PrimIOMode;        (* file system I/O mode (synchronous or asynch) *)

  rootReq_chan : int2;         (* event chan refnum for Root functions *)

  enterShutdown : boolean;     (* set when FS_SHUTDOWN is entered *)

  cpumodel, iomodel : integer; (* lisa, pepsi, or whopper *)

  ubd_pcb_ptr: absptr;         (* process that has done ubd *)

  unblk_alarm: integer;        (* alarm number for use by unblk_req *)
  unblk_tail: reqptr_type;     (* tail of circular request list for unblk_req*)

  (****************************************)
  (*                                      *)
  (* END OF SYSGLOBAL CELL DEFINITION     *)
  (*                                      *)
  (****************************************)


  function GDVERSION: integer;

  procedure ENQUEUE(var newlink,leftlink: linkage; b_sysarea: absptr);

  procedure DEQUEUE(var link: linkage; b_sysarea: absptr);

  function GET_REFNUM(ordrefdbptr: absptr; global: boolean): int2;

  procedure FREE_REFNUM(refnum: int2);

  procedure CHK_REFNUM(var errnum: int2; refnum: int2; c_type: refdbType;
                        var ord_refdb_ptr: int4 );

  procedure SHOW_REFNUM(var errnum: int2; refnum: int2);

  procedure HIDE_REFNUM(var errnum: int2; refnum: int2);

  procedure INIT_FREEPOOL (fp_ptr: absptr; fp_size: int2);

  procedure RELSPACE(ordaddr: absptr; b_area: absptr);

  function GETSPACE(amount: int2; b_area: absptr;
                                       var ordaddr: absptr): boolean;

  procedure NEWSPACE(newaddr: absptr; deltasize: int2; b_area: absptr);

  function SYS_CALLED : boolean;

  procedure SYSTEM_ERROR(errnum: int2);

  procedure POOL_INIT(mb_sysglob, l_sysglob, mb_sgheap, l_sgheap,
                        mb_syslocal, l_syslocal: absptr);

  procedure GETID (var unid: uid);

  procedure INITUID;

  procedure Calc_Cksum (pm_adr : absptr;  var cksum : int2);

  procedure INTSOFF(int_val: intsoff_type; var old_val: intson_type);

  procedure INTSON(old_val: intson_type);

  procedure Mover (source, tartget : absptr;  amount : int4);
    { procedure to move (large) amounts of data in memory }

  function VALID_ADDR(var errnum: int2; pchkarr: parmcheck): boolean;

  procedure MACSBUG;

  function TRACE (part:osportion; level:integer) : boolean;

  procedure SET_TRACE_FENCE (part:osportion; fencelevel:integer);

  procedure Init_BP (DCT_addr, anyBPs_addr : absptr);

  procedure Create_BP (proc_id, BP_addr : int4;  var error : int2);

  procedure Set_BP (proc_id : int4);

  procedure Clr_BP (proc_id : int4);

  procedure ClearBPs (proc_id : int4);

  procedure FILL_SPAN(base: bitmapptr;
                      fill_start,
                      fill_length: longint;
                      fill_value: boolean);

  function COUNT_SPAN(base: bitmapptr;
                      count_start,
                      count_leng: longint;
                      count_value: boolean) : longint;

  function FIND_SPAN(base: bitmapptr;
                     reqstart,
                     reqsize,
                     freestart,
                     lastcell: longint;
                 var maxstart: longint;
                     wanted: boolean) : longint;

 (*$IFC DEBUG1*)
  procedure RECORDIT(p1,p2: int1; p3: int2; p4,p5: int4);
 (*$ENDC*)


IMPLEMENTATION

const
  version = 120;       (* version number of this unit *)

  tracebase = 532;   (* abs location $214 where trace fence vars are stored *)
  maxmmusize = 131072; (* max size of segment mapped by mmu *)

  (* constants defining free pool arrays *)
  poolEnthdsize = 2; (* size of allocated pool entry header in bytes *)
  minfreesize = 3;   (* minimum size of a free entry in words*)
  stopper = 0;       (* value used to indicate end of free chain *)

TYPE
(****************************************)
(*                                      *)
(* SYSTEM AREA FREE SPACE POOL(FREEPOOL)*)
(*                                      *)
(****************************************)

hdr_pool_ptr = ^hdr_freepool;
hdr_freepool = record
                  pool_size: int2; (* integer size of pool array *)
                  firstfree: int4; (* offset into pool for first free *)
                  freecount: int2; (* cumulative amount of free space *)
                end;

ent_pool_ptr = ^ ent_freepool;
ent_freepool = record
                  size: int2;      (* size of free pool entry > 0 = free *)
                                   (*                         < 0 = allocated *)
                  next: int4;      (* link to next free entry - not present in*)
                                   (* allocated entry                         *)
               end;

(**********************************)
(*                                *)
(* DUMMY REFDB DEFINITION         *)
(*                                *)
(**********************************)

refdb_ptr = ^refdb;
refdb = record
          header: refdbHead;
        end;

(*********************************)
(*                               *)
(* REFNUM CONTROL BLOCK          *)
(*                               *)
(*********************************)

refncb_ptr = ^refncb;
refncb = record
           refnum_list: linkage; (* linkage for chain of entries *)
           refn: int2;           (* the refnum defined by entry *)
           hide: boolean;        (* if true refnum will look invalid *)
           refdb_addr: refdb_ptr;(* the address of the refdb    *)
                                 (* entry associated with this  *)
                                 (* refnum entry                *)
        end;


VAR
  prev_unid: uid; (*part of previous unique id*)


(*$S kr_prims *)

  procedure INTSOFF(* int_val: intsoff_type; var old_val: intson_type *); external;

  procedure INTSON(* old_val: intson_type *); external;

  procedure Mover (* source, tartget : absptr;  amount : int4 *);  external;
    { procedure to move (large) amounts of data in memory }

  function EXP_POOLSPACE(size: int2; b_area: absptr): boolean; external;

  procedure Calc_Cksum;  external;

  function GDVERSION;
  (* return the version number of this unit *)
  begin
    gdversion:=version;
  end; (* GDVERSION *)

  function VALID_ADDR(* var errnum:int2; pchkarr:parmcheck): boolean *); external;

  function traceaddr (part:osportion) : absptr;
  begin
    traceaddr := tracebase + (ord (part) * sizeof (int2));
  end;

  function TRACE (*part:osportion; level:integer) : boolean*);

  var fence : ^int2;
      range :  int2;
      f     :  int2;

  begin
    fence := pointer (traceaddr (part));
    if fence^ <= 100 then trace := level >= fence^
    else
    begin
      range := (fence^ div 100) - 1;
      f := fence^ mod 100;
      trace := (level >= f - range) AND (level <= f);
    end;
  end;


  procedure SET_TRACE_FENCE (*part:osportion; fencelevel:integer*);

  var fence : ^int2;

  begin
    fence := pointer (traceaddr (part));
    fence^ := fencelevel;
  end;


  (*$IFC DEBUG1*)
  procedure LOG(var errnum: integer; ptr_arr: absptr); external;

  procedure RECORDIT(* p1,p2: int1; p3: int2; p4,p5: int4 *);
    (*Record information in the logging buffers*)
    type
      logrec = record
                 lp1: int1;
                 lp2: int1;
                 lp3: int2;
                 lp4: int4;
                 lp5: int4;
               end;

    var
      lrec: logrec;
      errnum: int2;

    begin
      if TRACE(trecord,98) then
        begin
          with lrec do
            begin
              lp1:=p1;
              lp2:=p2;
              lp3:=p3;
              lp4:=p4;
              lp5:=p5;
            end;
          LOG(errnum,ord(@lrec));
        end;
    end;(*recordit*)
  (*$ENDC*)

  procedure Init_BP (* DCT_addr, anyBPs_addr : absptr *);  external;

  procedure Create_BP (* proc_id, BP_addr : int4;  var error : int2 *);  external;

  procedure Set_BP (* proc_id : int4 *);  external;

  procedure Clr_BP (* proc_id : int4 *);  external;

  procedure ClearBPs (* proc_id : int4 *);  external;


  procedure MACSBUG;   external;
    (* procedure to get into low level Debugger *)

  procedure DISP_ERROR(errnum : integer); external;
    (* procedure to display error and lodge control in the boot prom*)

  procedure FS_EMERGENCY; external;{actually, FORWARD declaration}
    (* attempts to cleanup fs prior to shutdown *)

  procedure SETMMU(index:integer; {Another forward declaration}
                   domain:integer;
                   base:absptr;
                   length:longint; permits:integer); external;

  function VERSN_PROM: integer; external;



  procedure SYSTEM_ERROR(* errnum: int2 *);
    (* handle those errors we don't know what to do with *)
  const
     mmureadwrite = 7; {With apologies to mmprims, where this constant is }
                       {  also defined.                                   }
  var
     lptr : ^longint;
     ch : char;
     i, adjust_index, err : integer;
  begin
  (*$IFC DEBUG*)
    if lb_loaded then
       begin
          writeln;writeln;writeln;
          writeln('   *** SYSTEM ERROR ',errnum,' ***');
          MACSBUG;
       end
    else  {DUPLICATES CODE FROM THE elsec part, BELOW}
       begin {lisabug not even loaded}
         if (going_down=0)  then {Only do FS_Shutdown ONCE.}
         begin
           going_down := errnum;       {save REAL errnum as re-entered flag contents}
           if not init_in_progress then
             FS_EMERGENCY;               {System errors from this CALL could cause }
                                         {   system to re-enter system_error again.}
         end; {of only shutdown ONCE}

       {Restore low-core so boot prom won't be inconvenienced: }
         adjust_index := membase div maxmmusize;  {dont map beyond 2nd mem card}
         for i := 0 to 15-adjust_index do
            SETMMU(i            {mmu number},
                   0            {return context},
                   i*maxmmusize {mmu base},
                   maxmmusize   {mmu length},
                   mmureadwrite){permited access};

         if (VERSN_PROM<=$202) then
            begin
               writeln;writeln;writeln;
               writeln('   *** SYSTEM ERROR ',errnum,' ***');
               writeln;
               write('Your boot prom is too old');
               writeln(' to both display the system error icon and reboot.');
               writeln;
               writeln('Press space when you''re ready to reboot.');
               if not lb_loaded then
                  SETSCREENKEYBD (AltScrn); {get readln access to alternate screen}
               ch := 'X';
               while (ch<>' ') do
                  read(ch);

             {double bus error time:}
               lptr := pointer (12);     (* points to address error vector *)
               lptr^ := -1;              (* zaps address error vector *)
               lptr := pointer (-1);     (* points to something that will gen address error *)
               lptr^ := -1;              (* results in double address error, reseting machine *)
            end; {of printing system error}

         DISP_ERROR(going_down)         {leave software-land for prom kingdom}
       end;

  (*$ELSEC*)

        if (going_down=0) then {Only do FS_Shutdown ONCE.}
         begin
           going_down := errnum;       {save REAL errnum as re-entered flag contents}
           if not init_in_progress then
             FS_EMERGENCY;               {System errors from this CALL could cause }
                                         {   system to re-enter system_error again.}
         end; {of only shutdown ONCE}

       {Restore low-core so boot prom won't be inconvenienced: }
         adjust_index := membase div maxmmusize;  {dont map beyond 2nd mem card}
         for i := 0 to 15-adjust_index do
            SETMMU(i            {mmu number},
                   0            {return context},
                   i*maxmmusize {mmu base},
                   maxmmusize   {mmu length},
                   mmureadwrite){permited access};


         if (VERSN_PROM<=$202) then
            begin
               writeln;writeln;writeln;
               writeln('   *** SYSTEM ERROR ',errnum,' ***');
               writeln;
               write('Your boot prom is too old');
               writeln(' to both display the system error icon and reboot.');
               writeln;
               writeln('Press space when you''re ready to reboot.');
               if not lb_loaded then
                  SETSCREENKEYBD (AltScrn); {get readln access to alternate screen}
               ch := 'X';
               while (ch<>' ') do
                  read(ch);

             {double bus error time:}
               lptr := pointer (12);     (* points to address error vector *)
               lptr^ := -1;              (* zaps address error vector *)
               lptr := pointer (-1);     (* points to something that will gen address error *)
               lptr^ := -1;              (* results in double address error, reseting machine *)
            end; {of printing system error}

         DISP_ERROR(going_down)         {leave software-land for prom kingdom}

  (*$ENDC*)

  end;(* SYSTEM_ERROR *)



  procedure ENQUEUE(* var newlink,leftlink: linkage; b_sysarea: absptr *);
       (**********************************************************)
       (*                                                        *)
       (* Description:                                           *)
       (*    Enqueues newlink into a chain headed by leftlink.   *)
       (*    The linkage values are maintained as relptr's to the*)
       (*    linkage record.                                     *)
       (*                                                        *)
       (* Input Parameters:                                      *)
       (*    Newlink is the linkage record of the control block  *)
       (*    to be added to the chain.                           *)
       (*    Leftlink is the linkage record of the control block *)
       (*    in the chain after which the new control block is   *)
       (*    to be chained.                                      *)
       (*    b_sysarea is the address ordinal of the data area   *)
       (*    base.  It identifies which system area contains the *)
       (*    chain (i.e. sysglobal,syslocal, or proclocal).      *)
       (*                                                        *)
       (* Output Parameters:                                     *)
       (*                                                        *)
       (* Side Effects:                                          *)
       (*                                                        *)
       (* Special Conditions of Use:                             *)
       (*    Enqueue assumes the linkage chain is initialized as *)
       (*    a circular list.                                    *)
       (*    Enqueue assumes the caller has exclusive access to  *)
       (*    to the chain being modified.                        *)
       (*                                                        *)
       (* Error Conditions:                                      *)
       (*                                                        *)
       (**********************************************************)

var
  rightlink: link_ptr;
  newRP: relptr;

begin
  newlink.fwd_link:=leftlink.fwd_link;
  rightlink:=pointer(leftlink.fwd_link+b_sysarea);
  newlink.bkwd_link:=rightlink^.bkwd_link;
  newRP:=ord(@newlink) - b_sysarea;
  leftlink.fwd_link:=newRP;
  rightlink^.bkwd_link:=newRP;
end; (* ENQUEUE *)


procedure DEQUEUE(* var link: linkage; b_sysarea: absptr *);

       (**********************************************************)
       (*                                                        *)
       (* Description:                                           *)
       (*    Dequeues the link from its chain.                   *)
       (*                                                        *)
       (* Input Parameters:                                      *)
       (*    Link is the linkage for the chain from which the    *)
       (*    control block is to be removed.                     *)
       (*    b_sysarea is the address ordinal of the data area   *)
       (*    base.  It defines which system area contains the    *)
       (*    chain (i.e. sysglobal,syslocal or proclocal )       *)
       (*                                                        *)
       (* Output Parameters:                                     *)
       (*                                                        *)
       (* Side Effects:                                          *)
       (*                                                        *)
       (* Special Conditions of Use:                             *)
       (*    Dequeue assumes the linkage records are in a        *)
       (*    circular list.                                      *)
       (*    Dequeue assumes the caller has exclusive access to  *)
       (*    the chain being modified.                           *)
       (*                                                        *)
       (* Error Conditions:                                      *)
       (*                                                        *)
       (**********************************************************)

var
  leftlink: link_ptr;
  rightlink: link_ptr;

begin
  leftlink:=pointer(link.bkwd_link + b_sysarea);
  rightlink:=pointer(link.fwd_link + b_sysarea);
  leftlink^.fwd_link:=link.fwd_link;
  rightlink^.bkwd_link:=link.bkwd_link;
end;



procedure FILL_SPAN(*base: bitmapptr;
                     fill_start,
                     fill_length: longint;
                     fill_value: boolean*);
      (************************************)
      (*
      (* Description:
      (*    Fills a range of cells of a (packed) bit array with a value.
      (*
      (* Input Parameters:
      (*    Base - Address of the bit array.  The bit array must be declared to
      (*             index from 0, but the maximum isn't important here.
      (*    Fill_start - Index of first cell to fill.
      (*    Fill_length - Number of cells to fill.
      (*    Fill_value - Value to fill into the array
      (*
      (************************************)
   external;



function COUNT_SPAN(*base: bitmapptr;
                     count_start,
                     count_leng: longint;
                     count_value: boolean) : longint*);
      (************************************)
      (*
      (* Description:
      (*    Counts the number of contiguous cells of a (packed) bit array with
      (*    a given value, from a SINGLE position, up to a maximum/desired number.
      (*
      (* Input Parameters:
      (*    Base - Address of the bit array.  The bit array must be declared to
      (*             index from 0, but the maximum isn't important here.
      (*    Count_start - Index of cell where count starts.
      (*    Count_length - Maximum number of cells to examine.
      (*    Count_value - Value of cells that qualify for counting.
      (*
      (* Output Parameter:
      (*    Count_span - returns the actual number found at that position.
      (*
      (************************************)
   external;



function FIND_SPAN(*base: bitmapptr;
                    reqstart,
                    reqsize,
                    freestart,
                    lastcell: longint;
                var maxstart: longint;
                    wanted: boolean) : longint*);
      (************************************)
      (*
      (* Description:
      (*    Searches for a contiguous range of cells of a (packed) bit array
      (*    until either enough are found or the whole list is searched.  If
      (*    the request can't be satisfied then the largest block is returned.
      (*    If a search, beginning in the middle of the array, encounters the
      (*    end of the array (wraparound), then the search continues from the
      (*    front.
      (*
      (* Input Parameters:
      (*    Base - Address of the bit array.  The bit array must be declared to
      (*             index from 0, but the maximum isn't important here.
      (*    Reqstart - Index of cell where search starts.
      (*    Reqsize - Maximum number of cells sought.
      (*    Freestart - Index of the front of the array, ie, where to resume
      (*             searching when wraparound happens.
      (*    Lastcell - Index of the end of the array.
      (*    Wanted - Value of the desired cells.
      (*
      (* Output Parameters:
      (*    Maxstart - Returns the index of the largest/desired region.
      (*    Find_span - Returns the actual number found there.
      (*
      (* Implementation Notes:
      (*    Two 'spanners' of cells exist, one spanning free the other taken
      (*    cells.  Earch has less work to do because the other PROMISES to
      (*    detect having searched the whole list, or wrapping back around
      (*    from the end of the array.
      (*
      (************************************)
var
   done : boolean;
   span, trial, maxsize, how_much: longint;
begin
   if (reqstart<freestart) or (reqstart>lastcell) or (reqsize<=0) then
      begin
         find_span := 0;
         EXIT(find_span);
      end;
   maxsize := 0;
   maxstart := -1;  {Encourage range check failure if caller uses this before }
                    {  checking the size allocated.}
   trial := reqstart;
   done := false;
   repeat
      if (trial<reqstart) then how_much := reqstart - trial
      else how_much := lastcell - trial + 1;
      if (reqsize<how_much) then how_much := reqsize;
      span := COUNT_SPAN(base, trial, how_much, wanted);
      if (span>maxsize) then
         begin
            maxsize := span;
            maxstart := trial;
         end;
      trial := trial + span;
      if (trial=lastcell+1) {at end of array} then
         trial := freestart;
    {Done if whole list processed or found what we needed:}
      if span>0 then      {note: this test guards against falsely terminating on}
         done := (trial=reqstart)           { the FIRST pass.}
                    or (maxsize=reqsize);
      if not done then {get across as many unwanted cells as possible}
         begin
            if (trial<reqstart) then how_much := reqstart - trial
            else how_much := lastcell - trial + 1;
            trial := trial + COUNT_SPAN(base, trial, how_much, not wanted);
            if (trial=lastcell+1) {at end of array} then
               trial := freestart;
          {Done if whole list processed:}
            done := (trial=reqstart)
         end;
   until done;

   find_span := maxsize;
end; {of find_span}


(*$I source/sysg1.text*)

end.

ÿ