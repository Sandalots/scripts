

(*$R-*)

UNIT MMPRIMITIVES;  (* PRIMITIVES FOR MEMORY MANAGER *)

(* Copyright 1983, 1984, Apple Computer, Inc. *)

INTERFACE

USES
(*$U object/DRIVERDEFS.OBJ*)
     DRIVERDEFS,
(*$U object/HWINT.OBJ*)
     hwint,
(*$U object/SYSGLOBAL.OBJ*)
     GLOBALDATA,
(*$U object/PROCPRIMS.OBJ*)
     PROC_PRIMS,
(*$U object/ASYNCTR.OBJ*)
     ASYNCTR;


CONST
  {                                                           }
  {  NOTE: the definitions indicated by (**) are also defined }
  {  in PASCALDEFS.  Any changes must be reflected there also.}
  {                                                           }
  {                                                           }
  {  NOTE: the definitions indicated by (*#*) are also define }
  {  in MMASM.  Any changes must be reflected there also.     }
  {                                                           }
  (*#*)e_mmbase = 500;   (* error base for memory manager *)
  e_noresources= 1;  (* no space in sysglobal or syslocal *)
  e_badpc     = 3;  (* bad pc value                  *)

  (*#*)e_nofreeze  = 10; (* unable to freeze segment      *)
  e_badaddr   = 11; (* invalid buffer address for io access *)

  e_stktoobig = 30;    (* expansion of stack will exceed limits *)
  e_dataoverflow = 31; (* expansion of sstack would cause a data lockout *)
  e_nostkdisk = 32;    (* insufficient disk space for stack expansion *)

  e_nomem     = 50; (* unable to get physical memory - internal use only *)
  e_unpackerr = 51; (* unable to unpack a packed code segment *)
  e_ioerr     = 52; (* I/O error on file system read or write of segment *)
  e_nomrdata  = 53; (* mmu for memory resident data seg is invalid or unavailable*)

  k_1MB     = 1048576; (* 1mb *)
  k_1MBm32k = 1015808; (* 1mb minus 32k *)
  k_allmem  = 1048064; (* 1mb minus 512 *)
  k_pg1MB   = 2048;    (* 1mb in pages *)
  k_pg1MBm32k=1984;    (* 1mb minus 32k in pages *)
  k_pgallmem= 2047;    (* 1mb minus 512 in pages *)
  maxmmusize= 131072;  (* max bytes mapped by mmu*)
  mempgsize = 512;     (* memory allocation block size (bytes) *)
  hmempgsize= 256;     (* memory allocation half block size (bytes) *)
  maxpgmmu  = 256;     (* max number of pages mapped by an mmu register *)

  min_ldsn = -2;    (* minimum ldsn number *)
  max_ldsn = 16;    (* maximum ldsn number *)
  minmmu = 1;       (* first mmu index (in mrbt), ignoring 'unmapped' group *)
  maxmmu = 124;     (* maximum dynamic mmu number *)
  numbmmus= 128;    (* number of mmu's per domain *)

  monvarmmu = 0;    (* mmu mapping monitor variables *)
  lisabugmmu = 16;  (* mmu used to map lisabug and it's data *)
  kernelmmu = 17;   (* first resident os code segment *)
  codespacemmu = 1; (* first user code segment *)
  lsyscodemmu=59;   (* last mmu used for system code segment*)
  fMRdataMMU=60;    (* first mmu used for system memory resident data areas*)
  lMRdataMMU=82;    (* last mmu used for system memory resident data areas*)
  DrvrCodeMMU=83;   (* mmu used for mapping dynamic device driver code*)
  logDrvrCode=$A60000; (*logical address of start of dynamic driverr code space*)
(**) mmucodemmu=84; (* mmu used to re-map other mmu segments *)
(**) realmemmmu = 85;  (* start of (16) mmus defining real memory *)
(**) logrealmem = $AA0000; (* logical address of start of realmemmmu *)
(**) superstkmmu=101;(* mmu used to map the supervisor's stack *)
(**) sysglobmmu=102;(* mmu used to map sysglobal data segment *)
  syslocmmu = 103;  (* mmu used to map syslocal *)
  minsysldsnmmu=104;(* mmu used to map 1st system ldsn (-2) *)
  maxsysldsnmmu=106;(* mmu used to map last system ldsn (0) *)
    lusercodemmu=106; (* last mmu used for user code segment*)
(**) ldsn1mmu = 107;(* the mmu to be assigned to ldsn 1 *)
  ldsn2mmu = 108;   (* the mmu to be assigned to ldsn 2 *)
  ldsn7mmu = 113;   (* the mmu to be assigned to ldsn 7 *)
(**) stackmmu = 123;(* mmu used to map process's stack, including jump table *)
(**) shrsegmmu =124;(* mmu used to map global data for shared Intrinsic Units *)
  screenmmu = 125;  (* mmu used to map the system's screen *)

  (* NOTE: iospacemmu is really defined in DRIVERDEFS - it is here for reference only*)
  { iospacemmu = 126; (* mmu used to map the system I/O space *) }

(**) prommmu = 127;    (* mmu reserved to access the prom *)

      (* these are mmu segment accessibility possibilities *)
(**)  mmureadonly = $5;
      mmustack =    $6; (* read/write *)
      mmureadwrite= $7;
      mmuio =       $8;
(**)  mmuabsent =   $C;

  max_mmlist_size = 16;        (* max number of entries in a mmlist            *)

  oset_memchain = 0;           (* offset from memchain to head of sdb record *)
  oset_freechain= 14;          (* offset from freechain to head of sdb record *)

  (*$IFC DEBUG1*)
  cntCsin = 1;                 (* measurement counters in measTable.misc[cnt..] *)
  cntDsin = 2;
  cntCsout= 3;
  cntDsout= 4;
  cntCmoc = 5;
  cntDmoc = 6;
  cntCsoc = 7;
  cntDsoc = 8;
  cntCmov = 9;
  cntDmov =10;
  cntPageMov = 11;                                                            (*P*)
  (*$ENDC*)


TYPE
  (********************************)
  (*                              *)
  (* 32 bit linkage record        *)
  (*                              *)
  (********************************)
  ptr_p_linkage = ^p_linkage;
  p_linkage = record
                fwd_link: ptr_p_linkage;
                bkwd_link: ptr_p_linkage;
              end;

  (***********************************)
  (*                                 *)
  (* MEMORY MANAGER LISTS for segment*)
  (* references and working sets     *)
  (***********************************)
  ptr_mmlist = ^mmlist2;
  ptr_1mmlist= ^mmlist1;

  mmlist1 = record                       (* mmlist of one primary element *)
              chain: linkage;            (* linkage to next mmlist entry*)
              size: int1;                (* number of elements in this entry *)
              maxsize: int1;             (* max number of elements in this entry *)
              ptr: array[1..1] of absptr;(* array of elements           *)
              sdscb_addr: absptr;        (* ptr to sdscb for shared data seg *)
            end;

  mmlist2 = record                       (* mmlist with max_mmlist_size elements *)
              chain: linkage;            (* linkage to next mmlist entry         *)
              size: int1;                (* number of elements in this entry     *)
              maxsize: int1;             (* max number of elements in this entry *)
              ptr: array[1..max_mmlist_size] of absptr;(* array of elements      *)
            end;

  (*********************************)
  (*                               *)
  (* SEGMENT DESCRIPTOR BLOCK (SDB)*)
  (*                               *)
  (*********************************)

  (*???? NOTE: IF ANYTHING CHANGES BETWEEN THE HEAD OF THE SDB RECORD AND *)
  (*????       THE FREECHAIN FIELD THEN THE CORRESPONDING OFFSET CONSTANTS*)
  (*????       MUST BE CHANGED!!!!!                                       *)

  (*#*)addrdisc = record               (* format of segment disc address *)
  (*#*)             fid: int2;
  (*#*)             offset: int4;
  (*#*)          end;

  (*#*)Tsdbtype = (free,code,data,stack,slocal,header,DCode);

  (*#*)segstates = packed record  (* states of a segment *)
  (*#*)              memoryF: boolean;   (* seg is in memory *)
  (*#*)              motionF: boolean;   (* seg is being moved by memory mgr *)
  (*#*)              deleteF: boolean;   (* seg is to be deleted when open count = 0 *)
  (*#*)              pcb_wF:  boolean;   (* process is waiting for iocount or *)
  (*#*)                                  (* freeze count = 0                  *)
  (*#*)              swaperrF:boolean;   (* true if seg is unable to be swapped in *)
  (*#*)              overlayF:boolean;   (* seg is an overlay candidate *)
  (*#*)              releaseF:boolean;   (* true if seg to be released by memory mgr*)
  (*#*)              res1: boolean;      (* reserved for future expansion *)
  (*#*)            end;

  (*NOTE!!!! - The codesdb and sdb must agree to fields and their types*)
  (*Bld_seg will get space for a codesdb if the segment is of type code*)
  (* otherwise it will getspace for an sdb.  The memory manager, however*)
  (* does not make this distinction.  All references are to an sdb.    *)
  (* This saves on system free pool space since code segs do not use   *)
  (* all of the fields.                                                *)

  (*#*)codesdb = record
  (*#*)        memchain: p_linkage;           (* linkage for allocated regions *)
  (*#*)        memaddr: int2;                 (* block addr of this region     *)
  (*#*)        memsize: int2;                 (* block size of this region     *)
  (*#*)        lockcount: int1;               (* number of locks performed on seg*)
  (*#*)        case sdbtype: Tsdbtype of      (* type of region                *)
  (*#*)          free: (freechain: p_linkage);(* secondary linkage for free region *)
  (*#*)          code:      (sdbstate: segstates; (* states of allocated seg*)
  (*#*)                      length: int4;    (* byte length of segment        *)
  (*#*)                      packedLen: int4; (* packed length of seg on disk *)
  (*#*)                      discaddr: addrdisc; (* address of segment on disc *)
  (*#*)                      numbopen: int1;) (* number of processes with segment open *)
  (*#*)      end;

  (*#*)sdb = record
  (*#*)        memchain: p_linkage;           (* linkage for allocated regions *)
  (*#*)        memaddr: int2;                 (* block addr of this region     *)
  (*#*)        memsize: int2;                 (* block size of this region     *)
  (*#*)        lockcount: int1;               (* number of locks performed on seg*)
  (*#*)        case sdbtype: Tsdbtype of      (* type of region                *)
  (*#*)          free: (freechain: p_linkage);(* secondary linkage for free region *)
  (*#*)          code,
  (*#*)          data,
  (*#*)          stack,
  (*#*)          slocal,
  (*#*)          header:    (sdbstate: segstates; (* states of allocated seg*)
  (*#*)                      length: int4;    (* byte length of segment        *)
  (*#*)                      packedLen: int4; (* packed length of seg on disk *)
  (*#*)                      discaddr: addrdisc; (* address of segment on disc *)
  (*#*)                      numbopen: int1;  (* number of processes with segment open *)
  (*#*)                      newlength: int2; (* new block size of data segment*)
  (*#*)                      disclength: int4;(* byte size of disc space allocated *)
  (*#*)                      io_count: int1;  (* number of i/o requests pending on seg *)
  (*#*)                      freeze_count: int1;(* number of freeze's on segment *)
  (*#*)                      srbRP: relptr;)  (* list of pcb's referencing seg *)
  (*#*)      end;

  sdb_ptr = ^sdb;


(*************************************)
(*                                   *)
(* Map Register Binding Table (MRBT) *)
(*                                   *)
(*************************************)

mrbt_Ptr = ^mrbt;

mrbtEnt = record (* mrbt entry *)
            access: int1;  (* the access allowed for this segment *)
            state: int1;   (* state flags for this segment *)
            sdbRP: relptr; (* segment bound to this map register *)
          end; (* of mrbtEnt *)

mrbt = array[minmmu..maxmmu] of mrbtEnt;

(*************************************)
(* Secondary                         *)
(* Map Register Binding Table (SMRBT)*)
(*                                   *)
(*************************************)

smrbt_Ptr = ^smrbt;

smrbt = array[minsysldsnmmu..maxsysldsnmmu] of mrbtEnt;

(*************************************)
(*                                   *)
(* Segment Map Table (SMT)           *)
(*                                   *)
(*************************************)

smt_ptr = ^smt;

smtEnt = packed record
           origin: int2;  (* origin of the bound segment *)
           access: int1;  (* allowable access to the segment *)
           limit: 0..255; (* size of the bound segment *)
         end;

smt = array[0..511] of smtEnt;


(*************************************)
(*                                   *)
(* Ldsn Binding Table (LBT)          *)
(*                                   *)
(*************************************)

lbt_ptr = ^lbt;

lbt = array[min_ldsn..max_ldsn] of relptr;


(*****************************************)
(*                                       *)
(* MEMORY MANAGER RESERVATION BLOCK(MMRB)*)
(*                                       *)
(*****************************************)

mmrb_ptr = ^mmrb;
mmrb = record
(*#*)     hd_qioreq_list: linkage;(* head of list of queued i/o requests*)
(*#*)                             (* held pending segment lock          *)
(*#*)     seg_wait_sem: semaphore;(* semaphore used to wait for segment *)
(*#*)                             (* io_count or lock_count = 0         *)
          memmgr_sem: semaphore;  (* semaphore used to synchronize memory *)
                                  (* manager process                      *)
          memmgr_busyF: boolean;  (* true if memory manager process is busy*)
          clr_mmbusy: boolean;    (* true if memmgr_busyF should be cleared*)
          numbRelSegs: int2;      (* count of segs to be released by memmgr*)
          req_pcb_ptr: ptr_pcb;   (* ptr to pcb needing memory manager *)
          hd_sdscb_list: linkage; (* head of list of shared data segment *)
                                  (* control blocks                      *)
          sds_sem: semaphore;     (* semaphore used to ensure exclusion *)
                                  (* during data segment creation, open,*)
                                  (* close, or kill                     *)
          head_sdb: codesdb;      (* sdb for head of allocated and free chains *)
          tail_sdb: codesdb;      (* sdb for tail of allocated and free chains *)
          DriverCode_sdb: sdb;    (* sdb for configurable driver code *)

          avail_space: int2;      (* amount of available free pages *)

          clock_ptr: sdb_ptr;     (* last sdb visited by replacement algorithm *)
          sys_swap_space: int4;   (* size of system swap area in bytes *)

          inc_sysglobal: int2;    (* minimum increment of sysglobal *)

          mrdata: array[fMRdataMMU..lMRdataMMU] of sdb_ptr; (* mmus available for*)
                                  (* memory resident data segments  *)

          resReqBlks: absptr;     (* ptr to reserved space for req blks *)
          DC_srblist: mmlist1;    (* dummy srb list for DriverCode_sdb *)
       end;



(***************************************)
(*                                     *)
(* QUEUED I/O REQUEST (QIOREQ)         *)
(*                                     *)
(***************************************)

(*#*)qioreq = record
(*#*)           qioreq_list: linkage;       (* chain of i/o requests pending seg lock *)
(*#*)           sdb_addr: sdb_ptr;          (* seg needed by the i/o request          *)
(*#*)           pioreq: absptr;             (* ptr to the pending i/o request         *)
(*#*)         end;




function EXPAND_POOLSPACE(size: int2; b_area: absptr): boolean;

procedure MAP_SYSLOCAL(c_pcb: ptr_pcb);

procedure MAP_DATASPACE;

procedure MAP_DOMAIN(domain: domainRange);

procedure MAP_SEGMENT(c_sdb: sdb_ptr; c_mmu: int2; domain: domainRange;
                                                   c_access: int2);

procedure REMAP_SEGMENT(c_sdb: sdb_ptr; old_memaddr, old_memsize: int2);

procedure SET_DOMAIN(c_domain: domainRange);

function LDSN_TO_MMU(ldsn: int2): int2;

function MMU_BASE(mmu: int2): absptr;

procedure Prepare_Process (pcb_ptr: ptr_PCB;  var status : launch_status);

procedure FREEZE_SEG(var errnum: int2; c_sdb: absptr; offset: int4;
                         ioreq_addr: absptr; var buffaddr: absptr );

procedure UNFREEZE_SEG(c_sdb: absptr);

procedure ADJ_IO_CNT(inc_io_count: boolean; c_sdb: absptr);

procedure CVT_BUFF_ADDR(var errnum: int2; mem_writeF: boolean; buffaddr: absptr;
                            byte_cnt: int4; var ordsdb: absptr; var offset: int4);

function MMPRIMVERSION: integer;

procedure ADDTO_SRB(c_sdb_ptr: sdb_ptr; c_pcb: absptr);

procedure DEL_SRB(c_sdb_ptr: sdb_ptr; c_pcb: absptr);

procedure CLEAR_SRB(c_sdb_ptr: sdb_ptr);

procedure ABORT_VIA_SRB(c_sdb_ptr: sdb_ptr; reason: term_types);

procedure INIT_MMLIST(c_mmlist: ptr_mmlist; max_entries: int2);

procedure LOCKDATA;

procedure UNLOCKDATA;

function MMBUSY: boolean;

procedure SET_MMBUSY(busy: boolean);

procedure S_SWAP_SPACE(var space_swapable, space_locked: int4);

procedure PROCS_DATA_NEEDS(var data_swapable, data_locked: int4);

function DATA_LOCKOUT(c_sdb:sdb_ptr; boundseg: boolean): boolean;

procedure MAKE_MRDATA(var errnum: integer; size: longint; var addr: absptr);

procedure REL_MRDATA(var errnum: integer; addr: absptr);

IMPLEMENTATION

CONST
  version = 56;       (* version number of this unit *)

TYPE
  ptr_proc = ^longint;

  procedure SET_DOMAIN; external;

  procedure PROG_MMU(index: int2; tar_domain: int2; mmucount: int2;
                     rtn_domain: int2); external;

  function LOGGING: boolean; external;

  procedure LOG(var errnum: integer; ptr_arr: absptr); external;

  procedure SAVE_OC(c_sdb_ptr: sdb_ptr; ordpcb: absptr); external;

  procedure BLD_SEG(kind: Tsdbtype; csize: int4; newsize: int2; disca: addrdisc;
             discspace: int4; pkedLen: int4; var c_sdb_ptr: sdb_ptr); external;

  procedure REL_SEG(c_sdb_ptr: sdb_ptr); external;

  procedure CHECK_DS(c_sdb_ptr: sdb_ptr); external;

  procedure FREEZE_SEG(* var errnum: int2; c_sdb: absptr; offset: int4;
                         ioreq_addr: absptr; var buffaddr: absptr *); external;

  procedure UNFREEZE_SEG(* c_sdb: absptr *); external;

 (*$S kr_mmgr *)


  function MMPRIMVERSION;
  (* return version number of this unit *)
  begin
    mmprimversion:=version;
  end;


procedure MAP_SYSLOCAL(* c_pcb: ptr_pcb *);

       (**********************************************************)
       (*                                                        *)
       (* Description:                                           *)
       (*    The syslocal segment used by the process c_pcb will *)
       (*    be mapped into "syslocmmu" of domain 0.             *)
       (*                                                        *)
       (* Side Effects:                                          *)
       (*    The hardware will be left with domain 0 active.     *)
       (*                                                        *)
       (**********************************************************)

var
  c_sysl_sdb: sdb_ptr;
  c_smt: smt_ptr;
  c_mrbt: mrbt_ptr;

begin
  c_sysl_sdb:=pointer(c_pcb^.slocal_sdbRP + b_sysglobal_ptr);
  c_smt:=pointer(smt_addr);

  (* update syslocal entry of s_mrbt *)
  c_mrbt:=pointer(s_mrbt_addr);
  with c_mrbt^[syslocmmu] do
    begin
      access:=mmureadwrite;
      sdbRP:=c_pcb^.slocal_sdbRP;
    end;

  (* construct smt entry in domain 0 for syslocmmu *)
  with c_smt^[syslocmmu] do
    begin
      origin:=c_sysl_sdb^.memaddr;
      access:=mmureadwrite;
      limit:=c_sysl_sdb^.memsize;
    end;

  (* force hardware to map the syslocal segment - leave with domain 0 active *)
  PROG_MMU(syslocmmu,0,1,0);
end; (* MAP_SYSLOCAL *)


procedure MAP_SPACE(firstmmu, lastmmu: int2; domain: domainRange);

       (**********************************************************)
       (*                                                        *)
       (* Description:                                           *)
       (*    The segments used by the current process (c_pcb_ptr *)
       (*    in sysglobal) will be mapped into "domain".  Only   *)
       (*    segments from firstmmu to lastmmu will be mapped.   *)
       (*    Segments which are overlay candidates will not be   *)
       (*    mapped.                                             *)
       (*                                                        *)
       (* Side Effects:                                          *)
       (*    The hardware will be left with "domain" active.     *)
       (*                                                        *)
       (**********************************************************)
var
  c_syslocal: slocal_ptr;
  c_mrbt: mrbt_ptr;
  c_smt: smt_ptr;
  c_sdb: sdb_ptr;
  i, j, no_mmu_m1, left, position_in_seg, domainbase: int2;
  l_domain: int2;

begin
  c_syslocal:=pointer(b_syslocal_ptr);
  c_mrbt:=pointer(c_syslocal^.mrbt_addr);
  c_smt:=pointer(smt_addr);
  l_domain:=domain; (* get into integer form *)

  (* construct smt entries *)
  domainbase:=domain*numbmmus;
  for i:= lastmmu downto firstmmu do
    begin
      with c_smt^[domainbase+i] do
        begin
          origin:=0;
          limit:=0;
          access:=mmuabsent;
        end;

      if c_mrbt^[i].sdbRP <> 0 then
        begin (* a segment is mapped *)
          c_sdb:=pointer(c_mrbt^[i].sdbRP + b_sysglobal_ptr);
          with c_sdb^ do
            begin
              if sdbstate.memoryF and
                 not sdbstate.overlayF then
                begin (* segment is present and not overlay candidate *)
                  if not ((sdbtype = code) and c_syslocal^.recordFaults) then
                    begin
                      (* compute number of mmu minus 1 required by seg *)
                      no_mmu_m1:=(memsize-1) div maxpgmmu;

                      (* change smt entries for segment *)
                      for j:=0 to no_mmu_m1 do
                        begin
                          with c_smt^[domainbase + i + j] do
                            begin
                              access:=c_mrbt^[i].access;
                              position_in_seg:=j*maxpgmmu;
                              origin:=memaddr + position_in_seg;
                              if sdbtype = code then
                                begin
                                  limit:=memsize+1; (* map extra block due to compiler's *)
                                                    (* string manipulations              *)
                                end
                               else
                                begin
                                  left:=memsize - position_in_seg;
                                  if left > maxpgmmu
                                    then limit:=maxpgmmu
                                    else limit:=left;
                                end;
                            end;(*with*)
                        end;(*for*)
                    end;
                end;
            end;(*with*)
        end;
    end;(*for*)

  (* force the hardware to map the segments and return with domain 0 active *)
  PROG_MMU(firstmmu,l_domain,lastmmu-firstmmu+1,l_domain);
end;(*MAP_SPACE*)


procedure MAP_DATASPACE;

       (**********************************************************)
       (*                                                        *)
       (* Description:                                           *)
       (*    The data segments used by the current process       *)
       (*    (c_pcb_ptr in sysglobal) will be mapped into        *)
       (*    domain 0.  These data segments are ldsn1mmu to      *)
       (*    stackmmu.                                           *)
       (*                                                        *)
       (* Side Effects:                                          *)
       (*    The hardware will be left with domain 0 active.     *)
       (*                                                        *)
       (**********************************************************)

begin
  (*$IFC DEBUG3*)
  if TRACE(mm,17) then writeln('map_dataspace for pcb= ',c_pcb_ptr);
  (*$ENDC*)

  MAP_SPACE(minsysldsnmmu,shrsegmmu,0);
end; (* MAP_DATASPACE *)


procedure MAP_DOMAIN(* domain: domainRange *);

       (**********************************************************)
       (*                                                        *)
       (* Description:                                           *)
       (*    All segments of the current process (c_pcb_ptr of   *)
       (*    sysglobal) will be mapped into "domain".            *)
       (*                                                        *)
       (* Input Parameters:                                      *)
       (*    domain - the domain to be used for mapping the      *)
       (*             process.                                   *)
       (*                                                        *)
       (* Side Effects:                                          *)
       (*    The hardware will be left with "domain" active.     *)
       (*                                                        *)
       (**********************************************************)

begin
  (*$IFC DEBUG3*)
  if TRACE(mm,17) then writeln('map_domain for pcb= ',c_pcb_ptr);
  (*$ENDC*)

  MAP_SPACE(minmmu,maxmmu,domain);
end; (* MAP_DOMAIN *)


procedure MAP_SEGMENT(* c_sdb: sdb_ptr; c_mmu: int2; domain: domainRange;
                                                     c_access: int2 *);

       (**********************************************************)
       (*                                                        *)
       (* Description:                                           *)
       (*    The smt entry for the segment c_sdb will be built.  *)
       (*    "C_mmu" within "domain" will be made to map the     *)
       (*    segment with the specified access capabilities.     *)
       (*                                                        *)
       (* Input Parameters:                                      *)
       (*    c_sdb - the segment to be mapped.                   *)
       (*    c_mmu - the mmu register to use for the mapping     *)
       (*    domain - the domain to be used for mapping the      *)
       (*             segment.                                   *)
       (*    c_access - the access capabilities to the segment   *)
       (*                                                        *)
       (* Side Effects:                                          *)
       (*    The hardware will be left with the domain of the    *)
       (*    current process active.                             *)
       (*                                                        *)
       (**********************************************************)
var
  c_pcb: ptr_pcb;
  c_smt: smt_ptr;
  i, no_mmu_m1, position_in_seg, left, index: int2;
  l_domain, m_domain: int2;

begin
  index:=domain*numbmmus + c_mmu; (* index into smt *)

  (* compute number of mmu minus 1 that will be affected *)
  if c_sdb = nil
    then no_mmu_m1:=0
    else no_mmu_m1:=(c_sdb^.memsize - 1) div maxpgmmu;

  c_smt:=pointer(smt_addr);
  c_pcb:=pointer(c_pcb_ptr);
  l_domain:=domain; (* get domain values into integer form *)
  m_domain:=c_pcb^.domain;

  for i:=0 to no_mmu_m1 do
    begin (* construct smt entries *)

      (*$IFC DEBUG3*)
      if TRACE(mm,17) then
        begin
          writeln('map_segment - sdb= ',ord(c_sdb),' domain =',l_domain);
          writeln('              mmu= ',c_mmu,' access= ',c_access);
        end;
      (*$ENDC*)

      with c_smt^[index+i] do
        begin
          access:=c_access;
          if c_sdb = nil then
            begin (* no segment to map *)
              origin:=0;
              limit:=0;
            end
           else
            begin (* segment to map *)
              with c_sdb^ do
                begin
                  position_in_seg:=i*maxpgmmu;
                  origin:=memaddr + position_in_seg;
                  if sdbtype = code then
                    begin
                      limit:=memsize+1; (* map extra block due to compiler's *)
                                        (* string manipulations              *)
                    end
                   else
                    begin
                      left:=memsize - position_in_seg;
                      if left > maxpgmmu
                        then limit:=maxpgmmu
                        else limit:=left;
                    end;
                end;(*with*)
            end;
        end;(*with*)
    end;(*for*)

  (*force hardware to map the altered smt entries *)
  PROG_MMU(c_mmu,l_domain,no_mmu_m1 + 1,m_domain);
end; (* MAP_SEGMENT *)

procedure REMAP_SEGMENT(* c_sdb: sdb_ptr; old_memaddr, old_memsize: int2 *);

       (**********************************************************)
       (*                                                        *)
       (* Description:                                           *)
       (*    The smt is scanned for any segment with origin =    *)
       (*    old_memaddr.   The segment origin and length will be*)
       (*    changed to their new values based upon the sdb.  The*)
       (*    access to the segment will remain unchanged.        *)
       (*                                                        *)
       (* Input Parameters:                                      *)
       (*    c_sdb - the segment to be remapped                  *)
       (*    old_memaddr  - the page address of the segment as   *)
       (*                   it is currently mapped.              *)
       (*                                                        *)
       (* Side Effects:                                          *)
       (*    The hardware will be left with the domain of the    *)
       (*    current process active.                             *)
       (*                                                        *)
       (**********************************************************)

var
  c_domain, c_mmu, i, j, no_mmu_m1: int2;
  c_smt: smt_ptr;

begin
  (*$IFC DEBUG3*)
  if TRACE(mm,17) then writeln('remap_segment -sdb= ',ord(c_sdb));
  (*$ENDC*)

  c_smt:=pointer(smt_addr);
  for i:=17 to 511 do (*do not try to remap debug segments in domain 0*)
    begin
      if (i < realmemmmu) or (i >= realmemmmu+16) then
        begin (*remap all but realmemmmu registers *)
          if c_smt^[i].origin = old_memaddr then
            begin (* found smt entry that maps the segment *)
              c_domain:= i div numbmmus;
              c_mmu:= i mod numbmmus;
              if c_sdb^.memaddr = 0 then
                begin (* segment is being unmapped *)
                  (* ensure that all mmu[s] that mapped segment are cleared *)
                  (* compute number of mmu minus 1 required by seg *)
                  no_mmu_m1:= (old_memsize - 1) div maxpgmmu;
                  for j:=0 to no_mmu_m1 do
                    begin
                      MAP_SEGMENT(nil,c_mmu + j,c_domain,mmuabsent);
                    end;
                end
               else
                begin (* segment is being mapped to new location *)
                  MAP_SEGMENT(c_sdb,c_mmu,c_domain,c_smt^[i].access);(*still mapped*)
                end;
            end;
        end;
    end;(*for*)
end; (* REMAP_SEGMENT *)


function MMU_BASE(* mmu: int2): absptr *);

       (**********************************************************)
       (*                                                        *)
       (* Description:                                           *)
       (*    return the base address of the segment defined by   *)
       (*    mmu                                                 *)
       (*                                                        *)
       (* Input Parameters:                                      *)
       (*    mmu  is the value to be transformed                 *)
       (*                                                        *)
       (* Output Parameters:                                     *)
       (*    MMU_BASE = the base of the defined segment          *)
       (*                                                        *)
       (* Side Effects:                                          *)
       (*                                                        *)
       (* Special Conditions of Use:                             *)
       (*                                                        *)
       (* Error Conditions:                                      *)
       (*                                                        *)
       (**********************************************************)

begin
  mmu_base:=ord4(mmu)*maxpgmmu*hmempgsize*2;
end; (* MMU_BASE *)

(*$s dsegs*)     (* force to the segment where it is used *)
function LDSN_TO_MMU(* ldsn: int2): int2 *);

       (**********************************************************)
       (*                                                        *)
       (* Description:                                           *)
       (*    return the mmu register number which corresponds    *)
       (*    with the input ldsn value                           *)
       (*                                                        *)
       (* Input Parameters:                                      *)
       (*    ldsn is the value to be transformed                 *)
       (*                                                        *)
       (* Output Parameters:                                     *)
       (*    LDSN_TO_MMU = the corresponding mmu value           *)
       (*                                                        *)
       (* Side Effects:                                          *)
       (*                                                        *)
       (* Special Conditions of Use:                             *)
       (*                                                        *)
       (* Error Conditions:                                      *)
       (*                                                        *)
       (**********************************************************)

begin
  ldsn_to_mmu:=minsysldsnmmu + ldsn - min_ldsn;
end; (* LDSN_TO_MMU *)

(*$s kr_mmgr*)  (* restore normal memory manager segmentation *)

(*$I source/mmprim2.text*)

end.
ÿ