
UNIT clock;

      (*drivers implemented using the real time clock or interval timer*)


      (*  By Dave Offen *)
      (*  Copyright 1983, 1984, Apple Computer Inc. *)

INTERFACE

  USES
      (*$Uobject/driverdefs.obj*)
        driverdefs,
      (*$Uobject/hwint.obj*)
        hwint,
      (*$Uobject/sysglobal.obj*)
        globalda,
      (*$Uobject/procprims.obj*)
        proc_prims,
      (*$Uobject/mmprim.obj*)
        mmprimitives,
      (*$Uobject/asynctr.obj*)
        asynctr,
      (*$Uobject/genio.obj*)
        genio;


  TYPE

    time_rec = record
                year: integer;  (*when hour or minute <0, year must = 1900*)
                day: 1..366;    (*when hour or minute <0, day must = 1*)
                hour: -23..23;
                minute: -59..59;
                second: 0..59;
                msec: 0..999
               end;

  procedure SET_TIME (var errnum: integer; time: time_rec);

  procedure GET_TIME (var errnum: integer; var time1: time_rec);

  procedure TIMEOUT (var errnum: integer; interval: timestmp_interval;
                     time1: time_rec; var req_ptr: reqptr_type);

  procedure ADD_INTERVAL (var errnum: integer; compute_time_rec: boolean; time1:
                          time_rec; interval: timestmp_interval;
                          var julian: timestmp_interval; var time2: time_rec);

  procedure CLOCK_INIT;

  IMPLEMENTATION

  (*$IFC not debug2*)
  (*$R-*) (*rangecheck off unless debug mode*)
  (*$ENDC*)

  CONST
    errbase = 630;
    syserbase = 10630;


  (*$S krgenio*)

  VAR
    time_cb: record
              ref: integer;
              reqlist: link_ptr;
             end;

  procedure A5SET; external;


  (*$S timemgr*)

  procedure ADD_INTERVAL; (*var errnum: integer; compute_time_ptr: boolean; time1:
                          time_rec; interval: timestmp_interval;
                          var julian: timestmp_interval; var time2: time_rec*)

       (*************************************************************)
       (*                                                           *)
       (* Description:  Add a time_rec to an interval               *)
       (*                                                           *)
       (* Input Parameters:  If compute_time_ptr is true, the result*)
       (*   is converted to time_rec format as well.                *)
       (*                                                           *)
       (* Output Parameters:  Julian is the interval result of the  *)
       (*   addition.  Time2_ptr is the time_rec result, if requested.*)
       (*                                                           *)
       (* Side Effects:  none                                       *)
       (*                                                           *)
       (* Special Conditions of Use:  none                          *)
       (*                                                           *)
       (* Error Conditions:  errbase+0 = result under/overflow      *)
       (*                    (year must be between 1900 and 2030)   *)
       (*                                                           *)
       (*************************************************************)

  VAR
    date: DateArray;
    i: integer;
    j: longint;

  begin
    if (time1.year < 1900) or (time1.year > 2030) then
      errnum := errbase + 0  (*overflow/underflow*)
    else
    begin

      (*convert time1 to an interval*)

      WITH time1 do
      begin
        date.year := year;
        date.day := day;
        date.hour := hour;
        date.minute := minute;
        date.second := second;
      end;
      DATETOTIME (date, j);

      (*add this result to the "interval" passed, and normalize the result*)

      i := time1.msec + interval.msec;
      if i >= 1000 then
      begin (*carry*)
        julian.msec := i - 1000;
        j := j + 1
      end
      else
        julian.msec := i;
      julian.sec := j + interval.sec;
      if (j>=0) and (interval.sec<0) and (julian.sec<0) or
         (j<0) and (interval.sec>=0) and (julian.sec>=0) then
        errnum := errbase + 0  (*overflow/underflow*)
      else
      begin

        (*see if we need to convert the resulting interval back to a time_rec*)

        if compute_time_ptr then
          WITH time2 do
            begin (*convert result back to time pointer format*)
              TIMETODATE (julian.sec, date);
              year := date.year;
              day := date.day;
              hour := date.hour;
              minute := date.minute;
              second := date.second;
              msec := julian.msec;
            end
      end
    end
  end; (*add_interval*)

  procedure GET_TIME; (*var errnum: integer; var time1: time_rec*)

       (*************************************************************)
       (*                                                           *)
       (* Description:  return current greenwich mean time          *)
       (*                                                           *)
       (* Input Parameters:  none                                   *)
       (*                                                           *)
       (* Output Parameters:  current time                          *)
       (*                                                           *)
       (* Side Effects:                                             *)
       (*                                                           *)
       (* Special Conditions of Use:  none                          *)
       (*                                                           *)
       (* Error Conditions:                                         *)
       (*                                                           *)
       (*************************************************************)

  VAR
    date: dateArray;
    (*$IFC debug2*)
    parmarr: parmcheck;
    (*$ENDC*)

  begin

    (*$IFC debug2*)
    WITH parmarr do (* check validity of parameters passed *)
    begin
      numcheck := 1;
      parms[1].addr := ord(@time1);
      parms[1].readwrite := true;
    end;
    if VALID_ADDR (errnum, parmarr) then
    (*$ENDC*)

    begin
      errnum := 0;
      DATETIME (date);
      WITH time1 do     (*#172*)
        if (date.year <= 1980) or (date.day > 366) or (date.day <= 0) or
        (date.hour > 23) or (date.hour < 0) or (date.minute > 59) or
        (date.minute < 0) or (date.second > 59) or (date.second < 0) then
        begin (*Set to default date.  Bug #172 fix*)
          year := 1980; (*#172*)
          day := 1;     (*#172*)
          hour := 0;    (*#172*)
          minute := 0;  (*#172*)
          second := 0;  (*#172*)
          msec := 0;    (*#172*)
          errnum := errbase+9 (*assume dead battery*)
        end             (*#172*)
        else
        begin

          (*$R-*)  (*rangecheck off*)

          year := date.year;
          day := date.day;
          hour := date.hour;
          minute := date.minute;
          second := date.second;
          msec := 0

          (*$IFC debug2*)
          (*$R+*) (*rangecheck back on if debug mode*)
          (*$ENDC*)

        end
    end
  end; (*get_time*)


  procedure SET_TIME (*var errnum: integer; time: time_rec*);

       (*************************************************************)
       (*                                                           *)
       (* Description:  set current greenwich mean time             *)
       (*                                                           *)
       (* Input Parameters:  current time                           *)
       (*                                                           *)
       (* Output Parameters:                                        *)
       (*                                                           *)
       (* Side Effects:                                             *)
       (*                                                           *)
       (* Special Conditions of Use:  none                          *)
       (*                                                           *)
       (* Error Conditions:  errbase+9 = illegal year               *)
       (*                                                           *)
       (*************************************************************)
  VAR
    date: dateArray;
    (*$IFC debug2*)
    parmarr: parmcheck;
    (*$ENDC*)

  begin

    (*$IFC debug2*)
      (* check validity of parameters passed *)
      parmarr.numcheck := 0;
    if VALID_ADDR (errnum, parmarr) then
    (*$ENDC*)

      WITH time do
        if (year > 1995) or (year <= 1980) then
          errnum := errbase + 9
        else
        begin
          errnum := 0;
          date.year := year;
          date.day := day;
          date.hour := hour;
          date.minute := minute;
          if (msec >= 500) and (second < 59) then
            date.second := second + 1
          else
            date.second := second;
          SETDATETIME (date)
        end
  end; (*set_time*)


  procedure TIMEOUT; (* (var errnum: integer; interval: timestmp_interval;
                     time1: time_rec; var req_ptr: reqptr_type*)

       (*************************************************************)
       (*                                                           *)
       (* Description:  create a timeout request and link to the pcb*)
       (*                                                           *)
       (* Input Parameters:  interval, if positive, is time to delay*)
       (*     before timeout.  If < 0, time1 points to timeout  *)
       (*     time.                                                 *)
       (*                                                           *)
       (* Output Parameters:  req_ptr points to the created request *)
       (*     block.                                                *)
       (*                                                           *)
       (* Side Effects:                                             *)
       (*                                                           *)
       (* Special Conditions of Use:  none                          *)
       (*                                                           *)
       (* Error Conditions: errbase+1= interval equals zero.        *)
       (*   errbase+8= request for more than 230 days in future.    *)
       (*   other errors as per ADD_INTERVAL.                       *)
       (*                                                           *)
       (*************************************************************)
  VAR
    d_ptr: absptr;
    ignore: time_rec;
    working_time, temp_time: timestmp_interval;
    actual_time, cur_time: longint;
    leftlink, rightlink: link_ptr;
    prevints: intson_type;
    sort_ptr: reqptr_type;
    continue: boolean;
    err: integer;
    pblk_in_pcb: ptrblk_type;

  begin
    if (interval.sec = 0) and (interval.msec = 0) then
      errnum := errbase + 1
    else
    begin
      errnum := 0;

      (* compute time of request  *)

      if interval.sec < 0 then
      begin (*convert time1 to required format*)
        temp_time.sec := 0;
        temp_time.msec := 0;
        ADD_INTERVAL (errnum, false, time1, temp_time, working_time, ignore);
                      (*errnum to be checked below*)
        cur_time := TIMESTAMP;  (*current time*)
        if working_time.sec < cur_time then
          working_time.sec := -1  (*prevent underflow if before today*)
        else
        begin
          working_time.sec := working_time.sec - cur_time; (*relative # seconds*)
          if (errnum = 0) and (working_time.sec > 2147483) then
            errnum := errbase+8; (*interval timer only holds up to 23 days worth of ms.*)
        end;
      end
      else (*use interval*)
        if interval.sec > 2147483 then
          errnum := errbase+8 (*interval timer only holds up to 23 days worth of ms.*)
        else
          working_time := interval;

      if errnum = 0 then (*not changed by 23 day check or ADD_INTERVAL*)
      begin
        actual_time := working_time.sec*1000 + working_time.msec + TIMER; (*ms count*)
        if not GETSPACE (sizeof(reqblk), b_sysglobal_ptr, d_ptr) then
          errnum := errbase + 2
        else
        begin
          req_ptr := pointer (d_ptr);  (*point to the request*)

          (* initialize the request block & add to pcb chain*)

          LINK_TO_PCB (req_ptr);

          WITH req_ptr^ do
          begin
            pblk_in_pcb:=@blk_in_pcb;
            pblk_in_pcb^ := [ostimer];
            cfigptr := nil;
            req_extent := ord(nil);
            operatn := 99; (*non-io operation*)
            reqspec_info := actual_time;
          end;

          (* find out where request goes on device chain, sorted by time *)

          INTSOFF (clokints, prevints);
          leftlink := time_cb.reqlist;
          rightlink := pointer (leftlink^.fwd_link + b_sysglobal_ptr);
          continue := true;
          while (rightlink <> time_cb.reqlist) and continue do
          begin
            sort_ptr := pointer ( ord (rightlink) - sizeof(rb_headT) );
            if (actual_time - sort_ptr^.reqspec_info) <= 0 then
              continue := false
            else
            begin
              leftlink := rightlink;
              rightlink := pointer (leftlink^.fwd_link + b_sysglobal_ptr)
            end
          end;

          (* add request to device chain *)

          ENQUEUE (req_ptr^.dev_chain, leftlink^, b_sysglobal_ptr);

          (* see if this is at the head of the list *)

          if leftlink = time_cb.reqlist then
            ALARMABSOLUTE (time_cb.ref, actual_time);

          INTSON (prevints)
        end
      end
    end
  end; (*timeout*)

  (*$S krgenio*)

  procedure CLK_Q_MGR;
       (*************************************************************)
       (*                                                           *)
       (* Description:  Interrupt handler routine for timed requests*)
       (*                                                           *)
       (* Input Parameters:  none                                   *)
       (*                                                           *)
       (* Output Parameters:                                        *)
       (*                                                           *)
       (* Side Effects:  dequeues request and signals or unblocks   *)
       (*                                                           *)
       (* Special Conditions of Use:  use only from 10 ms handler   *)
       (*                                                           *)
       (* Error Conditions:                                         *)
       (*                                                           *)
       (*************************************************************)


  VAR
    leftlink: link_ptr;
    ms_now: longint;
    done: boolean;
    cur_ptr: reqptr_type;
    err, sameaddr: integer;

  begin
    A5SET;
    INTSON (clokints); (*lower interrupt priority*)
    WITH time_cb do
    begin
      sameaddr := ord(reqlist) - b_sysglobal_ptr;
      if reqlist^.fwd_link = sameaddr then
        EXIT(CLK_Q_MGR); (*nothing in list to timeout!*)
      ms_now := TIMER;
      done := false;
      repeat
        cur_ptr := CHAIN_FORWARD (reqlist^);
        if reqlist^.fwd_link = sameaddr then
          done := true
        else
          if (cur_ptr^.reqspec_info - ms_now) <= 0 then
          begin (*time_up*)
            DEQUEUE (cur_ptr^.dev_chain, b_sysglobal_ptr);
            UNBLK_REQ (cur_ptr, true)
          end
          else
            done := true
      until done;

      if reqlist^.fwd_link <> sameaddr then (*startup next timeout*)
         ALARMABSOLUTE (ref, cur_ptr^.reqspec_info)
    end
  end; (*clk_q_mgr*)





  (*$S init*)

      procedure CLOCK_INIT;
       (*************************************************************)
       (*                                                           *)
       (* Description:  initialize time control block               *)
       (*                                                           *)
       (* Input Parameters:  none                                   *)
       (*                                                           *)
       (* Output Parameters:  none                                  *)
       (*                                                           *)
       (* Side Effects:                                             *)
       (*                                                           *)
       (* Special Conditions of Use:  call after T_INIT, and before *)
       (*        calling TIMEOUT, TIMED_EVENT or DEFINE_TREQ.       *)
       (*                                                           *)
       (* Error Conditions:                                         *)
       (*                                                           *)
       (*************************************************************)

  VAR
    err, i: integer;
    d_ptr: absptr;
    longtemp: longint;

  begin
    ALARMASSIGN (time_cb.ref, ord(@CLK_Q_MGR));
    if not GETSPACE (sizeof(linkage), b_sysglobal_ptr, d_ptr) then
       SYSTEM_ERROR (syserbase + 7); (*no room for device control block's linkage*)
    time_cb.reqlist := pointer (d_ptr);
    time_cb.reqlist^.fwd_link := d_ptr - b_sysglobal_ptr; (*empty linkage pts to self*)
    time_cb.reqlist^.bkwd_link := time_cb.reqlist^.fwd_link;
  end; (*clock_init*)


  end.

ÿ