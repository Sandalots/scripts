      (*  Copyright 1983, 1984, Apple Computer Inc. *)

{File source/cdchar}

program cdchar; {Configuration tool to make a characteristics file}
(********************************************************)
(*
(* Description:
(*   The program uses readln/writeln to create a user named characteristics file.
(*
(*   (No facility for EDITING an existing characteristics file exists.)
(*
(********************************************************)


uses
   {$U object/syscall.obj }         SysCall;

const
   cdd_version = 5; {current version of the CDD data.}

   ESC = 27;                                { <ESC> character }

  {possible replies from GETYESNO: }
   yes = 1;
   no = -1;

   cdid_max = 131071;

   maxcons = 50;
   is_conn = true;
   is_attach = false;

type

(*$I source/icdd.text*)  {shared declarations}

   prompter = string[64];

var
   peter: ch_info;
   link_list: array[1..maxcons] of typeconnect; {both attachments and connectors}
   link_flag: array[1..maxcons] of boolean;     {whether link entry is an attach}
   next_link,                                   {index into link arrays}
   num_conn, num_attach, ans, i, error, itsrefnum:  integer;
   bigvalue, actual: longint;
   prompt: prompter;
   dest:   pathname;
   done, blkstr, writeit, too_much: boolean;
   drvr_name: e_name;


procedure QUITTER;
 forward;


function GETYESNO(tickle: prompter) : integer;
(*****)
(* Description:
(*    GETYESNO writes a prompt and returns the sort of 1 char response
(*      gotten for the user:
(*         'Y'/'y' : yes;
(*         'N'/'n' : no;
(*****)
var
   diddly: prompter;
   answer: char;
   result: integer;
begin
   repeat
      write(tickle);
      readln(diddly);
      answer := diddly[1];
      if ord(answer) = esc then QUITTER;
      if (answer='Y') or (answer='y') then
         begin
            getyesno := yes;
            EXIT(getyesno);
         end
      else if (answer='N') or (answer='n') then
         begin
            getyesno := no;
            EXIT(getyesno);
         end;
   until {never} false;
end; {of getyesno}


procedure QUITTER;
begin
   if GETYESNO('Want to quit now? ') = yes then EXIT(cdchar);
end; {of quitter}



procedure GET_CONNS(flavor: boolean; var overflow: boolean);
var
   gotnextone: boolean;
begin
   repeat
      if (next_link > maxcons) then
         begin
            writeln;
            writeln('There''s no more room for any more names.');
            overflow := true;
            EXIT(get_conns);
         end;

      gotnextone := false;
      repeat
         write('   Next name [<CR> when done]? ');
         link_list[next_link] := 'X';
         readln(link_list[next_link]);
         if LENGTH(link_list[next_link]) = 0 then
            begin
               overflow := false;
               EXIT(get_conns);
            end
         else
            if (ord(link_list[next_link, 1]) = esc) then QUITTER
            else
               begin
                  if flavor=is_attach then num_attach := num_attach + 1
                                      else num_conn   := num_conn + 1;
                  link_flag[next_link] := flavor;
                  next_link := next_link + 1; {a keeper}
                  gotnextone := true;
               end;
      until gotnextone;

   until false {NEVER};
end; {of get_conns}


procedure PUT_CONNS(flavor: boolean);
var
   next: integer;
begin
   next := 1;
   repeat
      if (link_list[next] <> '') and (link_flag[next] = flavor) then
         writeln('   ',link_list[next]:32);
      next := next + 1;
   until next = maxcons;
end; {of put_connns}



procedure WRITE_CONNS(flavor: boolean);
var i: integer;
begin
   for i := 1 to maxcons do
      if (link_list[i] <> '') and (link_flag[i] = flavor) then
         begin
            WRITE_DATA(error, itsrefnum, ord4(@link_list[i]), sizeof(typeconnect),
                    actual, sequential, 0);
            if (error > 0) or (actual <> sizeof(typeconnect) ) then
               begin
                  write('Error writing out file ',dest);
                  if (error > 0) then writeln('  (error = ',error,')')
                  else writeln;
                  EXIT(cdchar);
               end;
         end;
end; {of write_conns}



   (***************************************************************)
   (*
   (*   Here begins the main program part of cdchar.
   (*
   (***************************************************************)
begin
   writeln;
   writeln;
   writeln('Welcome to the CD Characteristics File Generation Utility,',
                     ' driver format ',cdd_version:2,'.');
   writeln;
   writeln('Use "<clear> <CR>" to terminate Cdchar during non-numeric responses.');
   writeln;
   with peter do
      repeat {until entry is acceptable to the user}

         version := cdd_version;
         repeat
            drvr_name := 'X'; {overwrite escape position}
            writeln;
            write('Driver name? ');
            readln(drvr_name);
            if ord(drvr_name[1])=esc then
               begin
                  QUITTER;
                  drvr_name  := ''; {in case user doesn't quit}
               end;
            if (LENGTH(drvr_name) + 10 { for 'SYSTEM.BT_'} > SIZEOF(e_name) ) then
               begin
                  writeln('   Names can only be 22 characters long.');
                  drvr_name := '';
               end;
         until (LENGTH(drvr_name) > 0);

         repeat
            writeln;
            write('Unique id (32 and above are administered by Apple)? ');
            readln(driver_id);
            if (driver_id <= 0) then
               begin    {ID of ZERO expressly forbidden, NOT an oversight.}
                  writeln('   That''s too low, try a positive number.');
                  driver_id := 0;
               end;
            if driver_id>cdid_max then
               begin
                  writeln('   That''s too high, try a number less than ',
                                (cdid_max+1):1,'.');
                  driver_id := 0;
               end;
         until driver_id>0;

         writeln;
         for i := 1 to 3 do def_cdinfo[i] := 0;
         if (yes = GETYESNO('Any default info for PM''s extension words [Y/N]? ') ) then
            begin
               repeat
                  write('How many default extension words [0 to 3]? ');
                  readln(bigvalue);
               until (bigvalue >= 0) and (bigvalue <= 3);
               size_exten := bigvalue;
               for i := 1 to size_exten do
                  begin
                     repeat
                        write('Value of cell ',i:2,' [0 to 65535]? ');
                        readln(bigvalue);
                        if (bigvalue < 0) or (bigvalue > 65535) then
                           begin
                              writeln('   Bad value, try again.');
                              writeln;
                           end;
                     until (bigvalue >= 0) and (bigvalue <= 65535);
                     def_cdinfo[i] := bigvalue;
                  end;
            end
         else size_exten := 0;

         repeat
            writeln;
            write('Device type [1-sequential, 2-disk, 3-demultiplexor]? ');
            readln(ans);
            done := true;
            case ans of
               1: devicetype := seqdev;
               2: devicetype := diskdev;
               3: devicetype := non_io;
               otherwise done := false;
            end; {of case}
         until (done);
         blkstr := (devicetype = diskdev);

         if blkstr then {blockstructured}
            begin
               writeln;
               bootable := GETYESNO('Is the disk bootable? ') = yes;

               writeln;
               removable := GETYESNO('Removable media? ') = yes;
               if removable then
                  begin
                     writeln;
                     ejectable := GETYESNO('Ejectable media? ') = yes;
                  end
               else ejectable := false;

               repeat
                  writeln;
                  write('Address of boot track blocks? ');
                  readln(start_block); {no range checking}
               until (start_block >= 0);
               if start_block=1 then start_block := 0;

               repeat
                  writeln;
                  write('Address of the first file system block? ');
                  readln(fs_start_block); {no range checking}
               until (fs_start_block >= 0);
            end
         else
            begin {sequential devices: }
               bootable := false;
               removable := false;
               ejectable := false;
               fs_start_block := 0;
               start_block := 0;
            end;

         if (devicetype = non_io) then
            begin
               preload := false;
               permanent := false;
            end
         else if removable then
            begin
               preload := true;
               permanent := true;
            end
         else
            begin {let the user decide}
               writeln;
               preload :=
GETYESNO('Should the driver be preloaded? ') = yes;
               writeln;
               permanent :=
GETYESNO('Should the driver stay resident, once loaded? ') = yes;
            end;

       {get all connectors/attachments: }
         next_link := 1;
         num_attach := 0;
         num_conn := 0;
         for i := 1 to maxcons do
           link_list[i] := '';

         writeln;
         writeln('Defining attachment choices: ');
         GET_CONNS(is_attach, too_much);
         writeln;
         writeln('Defining driver connections: ');
         if not too_much then GET_CONNS(is_conn, too_much);
         numAttach := num_attach;
         numConnectors := num_conn;

     {review for approval: }
         writeln;
         writeln;
         writeln('OK, what I''ve got is: ');
         writeln('   driver name is "',drvr_name,'"');
         writeln('   code file name is "SYSTEM.CD_',drvr_name,'"');
         writeln('   unique id is ',driver_id:1);
         if size_exten > 0 then
            writeln('   ',size_exten:1,' word(s) of extension');
         if size_exten>0 then
            for i := 1 to size_exten do
               begin
                  bigvalue := def_cdinfo[i];
                  if (bigvalue < 0) then bigvalue := bigvalue + 65536; {go unsigned}
                  writeln('     info[',i:2,'] is ',bigvalue:1);
               end;
         case devicetype of
            diskdev: prompt := 'disk';
            seqdev:  prompt := 'sequential';
            non_io:  prompt := 'demultiplexer';
         end; {of cases}
         writeln('   device type is ',prompt);
         if blkstr then
         begin
            if bootable then
               begin
                  writeln('   the disk is bootable');
                  writeln('   boot code file name is "SYSTEM.BT_',drvr_name,'"');
               end;
            if removable then
               begin
                  writeln('   the media is removable');
                  if ejectable then
                     writeln('   the media is ejectable');
               end;
            writeln('   first block is ',start_block:1);
            writeln('   first fs block is ',fs_start_block:1);
         end;
         if preload then
            writeln('   the driver is preloaded');
         if permanent then
            writeln('   once loaded, the driver stays loaded');

         if (num_attach > 0) then
            begin
               writeln;
               writeln('Attaches to:');
               PUT_CONNS(is_attach);
            end;
         if (num_conn > 0) then
            begin
               writeln;
               writeln('Has connections for:');
               PUT_CONNS(is_conn);
               writeln;
            end;

            writeln;
      until GETYESNO('Is all this correct (answer N to start over)? ') = yes;

   repeat  {until we find the destination file name}
      writeit := false;
      writeln;
      writeln;
      write('Char file destination disk (ie, ''-mydisk'') [<CR> FOR WORKING DEVICE]? ');
      dest := 'x';
      readln(dest);
      if (ord(dest[1]) = esc) then QUITTER
      else
         begin
            if (LENGTH(dest) > 0) then
               begin
                  if dest[1] <> '-' then
                     dest := CONCAT('-', dest);
                  dest := CONCAT(dest, '-');
               end;
            dest := CONCAT(dest, 'SYSTEM.CH_', drvr_name);
            MAKE_FILE(error, dest, 0 {label});
            if (error=890 {file already exists}) then
               begin
                  writeln;
                  prompt := CONCAT('File ',dest,' already exists.  Delete [Y/N]? ');
                  writeit := (GETYESNO(prompt) = yes);
               end
            else if (error > 0) then
                    begin
                       writeln;
                       writeln('Error creating file ',dest,'  (error =',error,')');
                    end
            else writeit := true;
         end; {of not quitting}
   until writeit;
   OPEN(error, dest, itsrefnum, [dread, dwrite]);
   if (error > 0) then
      begin
         writeln;
         writeln('Error openning file ',dest,'  (error = ',error,')');
         EXIT(cdchar);
      end;

   WRITE_DATA(error, itsrefnum, ord4(@peter), sizeof(ch_info),
               actual, absolute, 0);
   if (error > 0) or (actual <> sizeof(ch_info) ) then
      begin
         writeln;
         write('Error writing out file ',dest);
         if (error > 0) then writeln('  (error = ',error,')') else writeln;
         EXIT(cdchar);
      end; {of trouble writing out the header}

   WRITE_CONNS(is_attach);
   WRITE_CONNS(is_conn);

   writeln;
   writeln;
   CLOSE_OBJECT(error, itsrefnum);
   if (error > 0) then
        writeln('Error closing ',dest,', error = ',error,'.')
   else writeln('Characteristics info successfully written to ',dest,'.');

end. {cdchar}
ÿ