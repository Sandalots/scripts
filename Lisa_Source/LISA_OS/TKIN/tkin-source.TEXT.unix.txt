PROGRAM Install;  {12-13-83}

{---------------------------------------------------------------------------------}
{         Takes a user defined tool from the workshop                             }
{         and places it in the office system                                     }
{---------------------------------------------------------------------------------}

{**** Modification Log ****}
{original finished : 7-29-83  Steve Scholl}
{modified, made functional : 9-20-83 Dan Smith}
{grudgingly assumed responsibility : 12-13-83 Ken Krugler}
{compiled for latest Pepsi build : 12-13-83 KK}
{labelIO call to apdm/cat changed : 12-13-83 KK}
{cleaned up interface, added conditional duplicate tool replacement : 1-23-84 KK}
{changed LabelIO error return : 1-24-84 KK}
{modified for Spring build : 3-20-84 KK}
{more mods, Uses list re-order : 3-22-84 KK}
{remove Annette's name, fix spelling mistakes, make it more convenient to
   run in EXEC file (allowSameNumber BOOLEAN): 6-01-84 LSR}

{$SETC ForOS := TRUE}
{$SETC ForSpring := TRUE}

USES
   {$U Libhw/HwInt      } HwInt,
   {$U Libos/SysCall    } SysCall,
   {$U Libos/PSysCall   } PSysCall,
   {$U Libsm/UnitStd    } UnitStd,
   {$U Libsm/UnitHz     } UnitHz,
   {$U Libqd/Storage    } Storage,
   {$U Libqd/QuickDraw  } QuickDraw,
   {$U Libfm/FontMgr    } FontMgr,
   {$U Libwm/Events     } Events,
   {$U Libwm/Menus      } Menus,
   {$U AlertMgr         } AlertMgr,
   {$U Libfc/Filercomm  } FilerComm,
   {$U dbenv            } dbenv,
   {$U dbdecl1          } dbdecl1,
   {$U Tkin/Globals     } DmGlobals,
   {$U Tkin/Entry       } DmEntry,     {Tkin/Entry is Apdm/entry w/one line of code removed}
   {$U Tkin/Cat         } DmCat,
   {$U Sulib            } StdUnit;

CONST
   ccClear     = 27;
   copyright   = 'Copyright 1983, 1984 Apple Computer Inc.';
   heapSize    = 2000;
   LabelError  = 10040;       { garbage # for ErrorCheck iff LabelIO call fails }

TYPE
   MesStr   = STRING[40];

VAR
   volHdl   : TentryHdl;
   devHdl   : TentryHdl;
   myHeap   : ARRAY [1..heapSize] OF INTEGER;
   catRec   : TcatRec;
   catRID   : TcatRID;
   objName  : FmaxStr;
   holdName : FmaxStr;
   allowSameNumber : BOOLEAN; {If TRUE, don't ask user if we should overwrite a duplicate tool #}

{**********************************************************************************************}
{Forward procedure declarations}

PROCEDURE AddRecToCat;
   FORWARD;
PROCEDURE Assignment;
   FORWARD;
PROCEDURE ErrorCheck(err : INTEGER; str : MesStr);
   FORWARD;
PROCEDURE PromptUser;
   FORWARD;
PROCEDURE SameName;
   FORWARD;
FUNCTION SameToolID : BOOLEAN;
   FORWARD;
PROCEDURE ToolSize(devHdl: TentryHdl; prefix: E_Name; VAR sizeBlks, created: LONGINT);
   FORWARD;

{**********************************************************************************************}
{**********************************************************************************************}

{----------------------------------------------------------------------------------------------}
{         Dummy procs to satisfy catalog external references                                   }
{----------------------------------------------------------------------------------------------}
FUNCTION ObjectSize(hVol: TentryHdl; objCatRID: TcatRID;
                   devInfoPtr: TfsInfoPtr; excludeUnfiled: BOOLEAN;
                   VAR DTM, DTC: LONGINT) : LONGINT;
BEGIN
END;

{----------------------------------------------------------------------------------------------}
PROCEDURE UpdateAll;

BEGIN
END;

{----------------------------------------------------------------------------------------------}
{         Add tool to catalog                                                                  }
{----------------------------------------------------------------------------------------------}
PROCEDURE AddRecToCat;

VAR
   err  : INTEGER;

BEGIN
   AddCatRec(err,volHdl,catRec,TRUE,catRID);
   ErrorCheck(err,'AddCatRec');
END;

{----------------------------------------------------------------------------------------------}
{         Assign parentID,objKind, toolSize and creation date                                  }
{----------------------------------------------------------------------------------------------}
PROCEDURE Assignment;

VAR
   numStr   : NumberStr;
   prefix   : E_Name;
   sizeBlks : LONGINT;
   created  : LONGINT;

BEGIN
   catRec.objKind  := toolKind;
   IntToStr(catRec.toolID,numStr);
   prefix := CONCAT('{t',numStr,'}');
   ToolSize(devHdl,prefix,sizeBlks,created);
   catRec.objSize := sizeBlks;
   catRec.created := created;
END;

{----------------------------------------------------------------------------------------------}
{         Close and terminate catalog                                                          }
{----------------------------------------------------------------------------------------------}
PROCEDURE Conclude(normal : BOOLEAN);

VAR
   err : INTEGER;

BEGIN
   IF volHdl <> NIL THEN
      BEGIN
      CloseCat(err,volHdl);
      TermCat;
      END;
   IF normal THEN
      BEGIN
      WRITELN;
      IF NOT catRec.props[noDocs] THEN
         DELETE(objName,pos(' P',objName),6);{delete "Paper" from name}
      WRITELN('"',objname,'" has been successfully installed in the Office System');
      WRITELN('and it will appear in the disk window associated with the device, ',
               devHdl^^.nameHdl^^,'.');
      END
   ELSE
      BEGIN
      SUStopExec(err);
      EXIT (Install);
      END;
   WRITELN;
END;

{----------------------------------------------------------------------------------------------}
{         Checks for errors in catalog procedure calls                                         }
{----------------------------------------------------------------------------------------------}
PROCEDURE ErrorCheck;{err : INTEGER, str : MesSTR}

VAR
   EMsg : SUStr;

BEGIN
  IF (err > 0) THEN
     BEGIN
     WRITELN('The call to <',str,'> failed. The error number reported was ',err:1);
     SUErrText('OSErrs.Err',err,@EMsg);
     WRITELN(EMsg);
     Conclude(FALSE);
     END;
END;

{----------------------------------------------------------------------------------------------}
{         Gets yes or no reponse from user                                                     }
{----------------------------------------------------------------------------------------------}
FUNCTION GetYN(default : BOOLEAN): BOOLEAN;

VAR
 answer: STRING[4];

BEGIN
   READLN(answer);
   DownShift(@answer);
   IF LENGTH(answer)=0 THEN
      GetYN := default
   ELSE IF answer[1] = 'y' THEN
      GetYN := TRUE
   ELSE IF answer[1] = 'n' THEN
      GetYN := FALSE
   ELSE IF answer[1] = CHR(ccClear) THEN
      ConClude(FALSE)
   ELSE
      BEGIN
      WRITELN;
      WRITE(' Y or N ? ');
      GetYN := GetYN(default);
      END;
END;

{----------------------------------------------------------------------------------------------}
{         Handles heap overflow problems                                                       }
{----------------------------------------------------------------------------------------------}

FUNCTION HeapFull : INTEGER;

BEGIN
   WRITELN('The heap is full, so stop feeding it.');
   Conclude(FALSE);
END;

{----------------------------------------------------------------------------------------------}
{         Initialize heap, catalog and catalog default values                                  }
{----------------------------------------------------------------------------------------------}

PROCEDURE Initialize;

VAR
   err  : INTEGER;

BEGIN

   {$IFC FlrDebug }
      trcCatalog := FALSE;
      TraceDB(FALSE);
      trcFEntry  := FALSE;
      fakeNoHeap := FALSE;
      testIOerr  := FALSE;
   {$ENDC }

   SUInit;

   WRITELN(' ':25,'TOOL INSTALL -  June 1, 1984');
   WRITELN(' ':21,'Copyright 1983, 1984 Apple Computer Co.');
   WRITELN;
   WRITELN;
   WRITELN('Tool Install allows programs developed on the ');
   WRITELN('Workshop to be used with the Office System. ');
   WRITELN;
   WRITELN;
   WRITELN('         *******  WARNING  *******');
   WRITELN('Each tool must have a unique number, one that is');
   WRITELN('different from all other tool numbers on the same');
   WRITELN('Office System disk.');
   WRITELN;

   WRITELN('Do you want to automatically overwrite an existing tool');
   WRITE(  '      with the same number? ( Y or N ) [NO] >> ');
   allowSameNumber := GetYN(FALSE);
   WRITELN;
   WRITELN;

   InitHeap(@myHeap,@myHeap[heapSize],@HeapFull);

   volHdl := NIL;

   InitFentry(err);
   ErrorCheck(err,'InitEntry');

   devHdl := firstDev;

   InitCat(err);
   ErrorCheck(err,'InitCat');

   catRec := BlankCatRec;{Set default values for catRec}
   catRec.parentID := idDisk;
   holdName := '';

END;

{----------------------------------------------------------------------------------------------}
{         Create icon for tool                                                                 }
{----------------------------------------------------------------------------------------------}

PROCEDURE MakeIcon;

BEGIN
   WRITELN;
   WRITELN('If the tool supports documents (e.g. LisaWrite) create a');
   WRITELN('design to appear on the document icon and store');
   WRITELN('this at character code 0 (zero) in the Font.');
   WRITELN;
   WRITELN('If the tool does not support documents (e.g. Calculator)');
   WRITELN('then have the icon created at character code 1 and its mask at');
   WRITELN('character code 2.');
   WRITELN;
   WRITELN('Then transfer the Font file to the same disk as the tool, ');
   WRITELN('and name it "{T<your tool #>}icon". (e.g. {T50}icon)');
END;

{----------------------------------------------------------------------------------------------}
{         Writes redundant info to tool file label                                             }
{----------------------------------------------------------------------------------------------}

PROCEDURE MakeLabel;

CONST
   writing = TRUE;

VAR
   lablRec : LabelFmt;
   err     : INTEGER;

BEGIN
   WITH lablRec DO
      BEGIN
      version   := labelVersion;
      name      := objName;
      kind      := toolKind;
      toolOnly  := catRec.props[noDocs];
      multiDocs := catRec.props[manyDocs];
      windLoc   := catRec.openRect;
      {$IFC ForSpring}
         split     := nullInt2;              {not split}
         totalSize := 0;                     {ditto}
         parentID  := 1;                     {currently not used}
      {$ENDC}
      END; {of WITH block}

   {LabelIO is writing the info into the label}
   IF (NOT LabelIO(writing,devHdl^^.nameHdl^^,catRec.toolID,0,lablRec)) THEN
      BEGIN
      WRITELN('There appears to be a problem writing to the tool');
      WRITELN('object file label.  The problem may be that the');
      WRITELN('{T',catRec.toolID:1,'}obj file is not on the correct disk.');
      WRITELN;
      ErrorCheck(LabelError,'LabelIO');
      END;

END;

{----------------------------------------------------------------------------------------------}
{         Open catalog and get device name                                                     }
{----------------------------------------------------------------------------------------------}

PROCEDURE OpenCatalog;

VAR
   devName : E_Name;
   err     : INTEGER;

BEGIN
   WRITELN('If your tool is not on the device that you want to install it on,');
   WRITELN('then enter CLEAR to exit the program and move your tool');
   WRITELN('to the proper device. ');
   WRITELN;
   WRITE('Please enter the name of the device your tool is on. [PARAPORT]. >> ');
   READLN(devName);
   WRITELN;
   IF (LENGTH(devName) = 0) THEN
      devName := 'paraport';
   IF (devName[1] = CHR(ccClear)) THEN
      EXIT(Install);

   devHdl^^.nameHdl^^ := devName;

   OpenCat(err,devHdl);
   ErrorCheck(err,'OpenCat');
   volHdl := devHdl^^.volHdl;

END;

{----------------------------------------------------------------------------------------------}
{         Receives tool name, tool ID, and number of documents info from user                  }
{----------------------------------------------------------------------------------------------}

PROCEDURE PromptUser;

VAR
   err            : INTEGER;
   machineNumber  : LongInt;

   FUNCTION GetNum(str : MesStr): LONGINT;

   VAR
      cState   : ConvNState;
      NumStr   : SUStr;
      tempInt  : LONGINT;

   BEGIN
      REPEAT
         WRITE(str);
         READLN(NumStr);
         WRITELN;
      UNTIL LENGTH(NumStr)>0;

      IF NumStr[1] = CHR(ccClear) THEN
         Conclude(FALSE);
      SUStrTOLInt(@NumStr,tempInt,cState);
      GetNum := tempInt;
   END;

BEGIN
   Get_Serial_No(err,machineNumber);
   ErrorCheck(err,'Get_Serial_No');
   WRITELN('Each tool must have a unique number.  One such number');
   WRITELN('is ',machineNumber:1,', which is the serial number of this Lisa.');
   WRITELN('If you are developing multiple tools on the same machine, may I');
   WRITELN('suggest that you multiply this number by 100 and add an offset.');
   REPEAT
      WRITELN;
      catRec.toolID := GetNum('Please enter your tool id number >> ');
      UNTIL SameToolID;

   WRITELN;

   WRITE('Does your tool create documents? ( Y or N ) [YES] >> ');
   catRec.props[noDocs]   := NOT GetYN(TRUE);
   catRec.props[manyDocs] := NOT catRec.props[noDocs];

   WRITELN;

   IF catRec.props[manyDocs] THEN
      BEGIN
      WRITELN('Can your tool handle MORE THAN ONE document at a time? ');
      WRITE('If you don''t know press RETURN. ( Y or N ) [NO] >> ');
      catRec.props[manyDocs] := GetYN(FALSE);
      END;

   WRITELN;

   WRITELN('The stationery opening rectangle is defaulted to');
   WITH catRec.openRect DO
      WRITELN(left:1,', ',top:1,', ',right:1,', ',bottom:1);
   WRITELN;
   WRITE('Do you wish to specify a different one? ( Y or N ) [NO] >> ');
   IF GetYN(FALSE) THEN
      BEGIN
      WITH catRec.openRect DO
         BEGIN
         left := GetNum('left   >> ');
         top := GetNum('top    >> ');
         right := GetNum('right  >> ');
         bottom := GetNum('bottom >> ');
         END;
      END;

   WRITELN;

   WRITE('Please enter the name of your tool. >> ');
   READLN(objName);
   IF objName[1] = CHR(ccClear) THEN
      Conclude(FALSE);
   InsObjName(catRec,objName);
   SameName;

   WRITELN;

END;

{----------------------------------------------------------------------------------------------}
{         Allows user to change name if there is a conflict                                    }
{----------------------------------------------------------------------------------------------}

PROCEDURE SameName;

VAR
   tempName  : FmaxStr;
   catRecPtr : PtrCatRec;

BEGIN
   IF ObjInCat(volHdl,objName,[toolKind],-1,catRecPtr) THEN
      BEGIN
      WRITELN;
      WRITELN('There already exists a tool with that name.');
      WRITELN;
      WRITE('Please enter a new name (a [RETURN] will result in duplicate names). ');
      READLN(tempName);
      IF (tempName[1] = CHR(ccClear)) THEN
         Conclude(FALSE);
      IF (LENGTH(tempName) > 0) THEN
         BEGIN
         objName := tempName;
         InsObjName(catRec,objName);
         SameName;
         END;
      END;
END;

{----------------------------------------------------------------------------------------------}
{         Allows user to replaces old tool if conflicting tool numbers                         }
{----------------------------------------------------------------------------------------------}

FUNCTION SameToolID {* : BOOLEAN *};

VAR
   catRecPtr  : PtrCatRec;
   err        : INTEGER;
   tempCatRec : TCatRec;
   reply      : BOOLEAN;
BEGIN
   SameToolID := FALSE;
   IF ObjInCat(volHdl,'',[toolKind],catRec.toolID,catRecPtr) THEN
      BEGIN
      IF allowSameNumber THEN
         reply := TRUE
      ELSE
         BEGIN
         WRITELN('There is already a tool with the number ',catRec.toolID:1,'.');
         WRITE  ('Do you wish to replace the current tool? ( Y or N ) [NO] >> ');
         reply := GetYN(FALSE);
         END;

      IF reply THEN
         BEGIN
         SameToolID := TRUE;
         catRec.parentID := catRecPtr^.parentID;
         catRec.closedPt := catRecPtr^.closedPt;
         catRec.openRect := catRecPtr^.openRect;
         tempCatRec      := catRecPtr^;
         ExtObjName(tempCatRec,holdName);
         DelCatRec(err,volHdl,PosCurrent);
         ErrorCheck(err,'DelCatRec');
         END;
      END
   ELSE
      SameToolID := TRUE;

END; {of FUNC SameToolID}

{----------------------------------------------------------------------------------------------}
{         Creates stationery pad record                                                        }
{----------------------------------------------------------------------------------------------}

PROCEDURE Stationery;

VAR
   catRecPtr  : PtrCatRec;
   tempCatRec : TCatRec;
   err        : INTEGER;

BEGIN
   IF ObjInCat(volHdl,CONCAT(holdName,' Paper'),[docPad],catRec.toolID,catRecPtr)
   AND NOT catRec.props[noDocs] THEN
      BEGIN
      tempCatRec := catRecPtr^;
      tempCatRec.props[noDocs] := catRec.props[noDocs];
      tempCatRec.props[manyDocs] := catRec.props[manyDocs];
      tempCatRec.openRect := catRec.openRect;
      InsObjName(tempCatRec,CONCAT(objName,' Paper'));
      UpdCatRec(err,volHdl,PosCurrent,tempCatRec);
      ErrorCheck(err,'UpdCatRec');
      END
   ELSE IF NOT catRec.props[noDocs]  THEN
      BEGIN
      catRec.objKind := docPad;
      objName := Concat(objName,' Paper');
      InsObjName(catRec,objName);
      catRec.objSize := 0;
      AddRecToCat;
      END
   ELSE IF ObjInCat(volHdl,CONCAT(holdName,' Paper'),[docPad],catRec.toolID
                    ,catRecPtr) THEN
      BEGIN
      tempCatRec := catRecPtr^;
      DelCatRec(err,volHdl,PosCurrent);
      ErrorCheck(err,'DelCatRec');
      END;
END;

{----------------------------------------------------------------------------------------------}
{         Determines the size of the tool and the creation date                                }
{----------------------------------------------------------------------------------------------}

PROCEDURE ToolSize;{devHdl: TentryHdl; prefix: E_Name;
                    VAR sizeBlks, created: LONGINT}

CONST
   minLongInt = $80000000;

VAR devInfo      : Fs_Info;
    devName      : E_Name;
    devPathname  : Pathname;
    fileBlks     : LONGINT;
    fileInfo     : Fs_Info;
    fileName     : E_Name;
    filePathName : Pathname;
    err          : INTEGER;

BEGIN
   created  := minLongInt;
   sizeBlks := 0;

   { Create the pathname for the device }

   devName := devHdl^^.nameHdl^^;
   devPathname := CONCAT('-',devName);

   { Get the device attributes }

   Lookup (err, devPathname, devInfo);
   ErrorCheck (err,'LookUp');

   { Position at the start of the catalog }

   Reset_Catalog (err, devPathname);
   ErrorCheck (err,'ResetCatalog');

   Get_Next_Entry (err, prefix, fileName);

   { Loop thru the catalog examining all files with "prefix" }

   WITH devInfo DO
      WHILE err = 0 DO
         BEGIN
         filePathName := CONCAT(devPathname,'-',fileName);
         Lookup (err, filePathName, fileInfo);
         ErrorCheck(err,'LookUp');
         IF fileInfo.dtc > created THEN
            created := fileInfo.dtc;
         fileBlks := fileInfo.pSize DIV dataSize;
         IF fileInfo.pSize MOD dataSize <> 0 THEN
            fileBlks := fileBlks + 1;
         sizeBlks := sizeBlks + fileBlks + fileInfo.fs_Overhead;
         Get_Next_Entry (err, prefix, fileName);
         END;

   IF err<> 848 THEN
      ErrorCheck(err,'ToolSize');

   IF created = minLongInt THEN created := TimeStamp;
END;

{----------------------------------------------------------------------------------------------}
{         MAIN BLOCK                                                                           }
{----------------------------------------------------------------------------------------------}

BEGIN
   Initialize;
   OpenCatalog;
   PromptUser;
   Assignment;
   AddRecToCat;
   MakeLabel;
   Stationery;
   MakeIcon;
   Conclude(TRUE{normal});
END.

ÿ