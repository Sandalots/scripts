
;
;   File MOUSE.TEXT
;
;-----------------------------------------------------------------------------
;
;   Hardware Interface for the Mouse
;
;   Written by Rick Meyers
;   Copyright 1983, Apple Computer Inc.
;
;
;
;   The routines below provide an assembly language interface to the mouse.
;   Input parameters are passed in registers, output parameters are returned
;   in registers.  Unless otherwise noted, all registers are preserved.
;
;   Routines labeled with a * require supervisor mode.
;
;   Change Log
;
;   <28Sep83>   LAN    Added routine MOUSE so that users could supply their
;                      own routines to detect mouse movements.
;
;
;-----------------------------------------------------------------------------
;
;   The Mouse
;
;   The mouse is a pointing device used to indicate screen locations.  The
;   X-coordinate can range from 0 to 719, and the Y-coordinate from 0 to 363.
;   The initial mouse location is 0,0.
;
;
;   Routine:    MouseLocation
;   Arguments:    D0 (output)  --  X-coordinate (integer)
;                 D1 (output)  --  Y-coordinate (integer)
;   Function:   Return the current mouse location.
;

MouseLocation   MOVE.L  MousX,D0            ; MousX, MousY (long)
                MOVE.W  D0,D1               ; mouse Y-coordinate (integer)
                SWAP    D0                  ; mouse X-coordinate (integer)
                RTS                         ; return



;-----------------------------------------------------------------------------
;
;   Mouse Update Frequency
;
;   Software knowledge of the mouse location is updated periodically, rather
;   than continuously.  The frequency of these updates can be programmatically
;   controlled.  The time between updates can range from 0 milliseconds
;   (continuous updating) to 28 milliseconds, in intervals of 4 milliseconds.
;   The initial setting is 16 milliseconds.
;
;
;   Routine:    MouseUpdates *
;   Arguments:    D0 (input)   --  milliseconds between updates (integer)
;   Function:   Set the approximate number of milliseconds between mouse
;               location updates.
;

MouseUpdates    MOVEM.L D0-D1,-(SP)         ; save registers
                MOVE.L  COPSCounter,D1      ; COPS transaction counter
                ADD.W   #2,D0               ; round to multiple of 4
                ASR.W   #2,D0               ; (frequency+2)/4
                BPL.S   @1                  ; branch if (...)/4 >= 0
                MOVE.W  #0,D0               ; minimum frequency of 0

@1              CMP.W   #7,D0               ; (...)/4 <= 7 ?
                BLE.S   @2                  ; branch if (...)/4 <= 7
                MOVE.W  #7,D0               ; maximum frequency of 28

@2              OR.W    #$78,D0             ; enable mouse interrupts
                JSR     COPSCMD             ; send command to COPS
                CMP.L   COPSCounter,D1      ; interrupted ?
                MOVEM.L (SP)+,D0-D1         ; restore registers
                BNE.S   MouseUpdates        ; branch if interrupted

                ADD.L   #1,COPSCounter      ; COPS transaction counter
                RTS                         ; return



;-----------------------------------------------------------------------------
;
;   Mouse Scaling
;
;   The relationship between physical mouse movements and logical mouse
;   movements is not necessary a fixed linear mapping.  Three alternatives
;   are available: 1) unscaled, 2) scaled for fine movement and 3) scaled
;   for coarse movement.  Initially mouse movements are unscaled.
;
;   When mouse movement is unscaled, a horizontal mouse movement of x units
;   yields a change in the mouse X-coordinate of x pixels.  Similiarly, a
;   vertical movement of y units yields a change is the mouse Y-coordinate
;   of y pixels.  These rules apply independent of the speed of the mouse
;   movement.
;
;   When mouse movement is scaled, horizontal movements are magnified by 3/2
;   relative to vertical movements.  This is intended to compensate for the
;   2/3 aspect ratio of pixels on the screen.  When scaling is in effect, a
;   distinction is made between fine (small) movements and coarse (large)
;   movements.  Fine movements are slightly reduced, while coarse movements
;   are magnified. For scaled fine movements, a horizontal mouse movement of
;   x units yields a change in the X-coordinate of x pixels, but a vertical
;   movement of y units yields a change of (2/3)*y pixels.  For scaled coarse
;   movements, a horizontal movement a x units yields a change of (3/2)*x
;   pixels, while a vertical movements of y units yields a change of y pixels.
;
;   The distinction between fine movements and coarse movements is determined
;   by the sum of the x and y movements each time the mouse location is
;   updated.  If this sum is at or below the 'threshold', the movement is
;   considered to be a fine movement.  Values of the threshold range from 0
;   (which yields all coarse movements) to 256 (which yields all fine
;   movements).  Given the default mouse updating frequency, a threshold of
;   about 8 (threshold's initial setting) gives a comfortable transition between
;   fine and coarse movements.
;
;
;   Routine:    MouseScaling
;   Argument:     D0 (input)   --  0=no scaling, else=scaling (byte)
;   Function:   Enable or disable the scaling of mouse movements.
;

MouseScaling    MOVE.B  D0,MousScaling      ; 0=disabled, else=enabled (byte)
                RTS                         ; return


;
;   Routine:    MouseThresh
;   Argument:     D0 (input)   --  mouse movement threshold (integer)
;   Function:   Set the threshold between fine and coarse mouse movements.
;

MouseThresh     MOVE.L  D0,-(SP)            ; save registers
                TST.W   D0                  ; threshold >= 0 ?
                BGE.S   @1                  ; branch if threshold >= 0
                MOVE.W  #0,D0               ; minimum threshold of 0

@1              CMP.W   #256,D0             ; threshold <= 256 ?
                BLE.S   @2                  ; branch if threshold <= 256
                MOVE.W  #256,D0             ; maximum threshold of 256

@2              MOVE.W  D0,MousThresh       ; mouse movement threshold
                MOVE.L  (SP)+,D0            ; restore registers
                RTS                         ; return



;-----------------------------------------------------------------------------
;
;   Mouse Odometer
;
;   In order to properly specify, design and test mice, it's important to
;   estimate how far a mouse moves during it's lifetime.  Routine MouseOdometer
;   returns the sum of the X and Y movements of the mouse since boot time.
;   The value returned is in (unscaled) pixels.  There are 180 pixels per inch
;   of mouse movement.
;
;
;   Routine:    MouseOdomener
;   Argument:     D0 (output)  --  mouse odometer in pixels (long)
;   Function:   Return the sum of the X and Y mouse movements since boot time.
;

MouseOdometer   MOVE.L  MousOdometer,D0     ; mouse odometer (long)
                RTS                         ; return


;-----------------------------------------------------------------------------
;
;   User Supplied Detection of Mouse Movements
;
;
;   The user may supply a routine, called "MOUSE," which is called each time a            <28Sep83>
;   mouse movement is detected.  Routine MOUSE is passed the change in the X and          <28Sep83>
;   Y locations of the mouse as signed integers.  The user supplied routine MOUSE         <28Sep83>
;   replaces the built-in routine that provides mouse and cursor movement.                <28Sep83>
;   Therefore, if a user supplied routine is being used (and it doesn't in turn call      <28Sep83>
;   the build-in routine), calls to MouseLocation will return meaningless results.        <28Sep83>
;
;   The user supplied routine MOUSE runs as an interrupt handler.  Interrupts at          <28Sep83>
;   priority levels 1-6 are disabled during its execution.  To guarantee proper           <28Sep83>
;   response to interrupts, MOUSE should complete its processing within about             <28Sep83>
;   1 millisecond.  MOUSE need not preserve registers.                                    <28Sep83>
;
;   Routine:    MOUSEMOVEMENT (user routine)                                              <28Sep83>
;   Arguments:    D0 (input)   --  mouse X-coordinate                                     <28Sep83>
;                 D1 (input)   --  mouse Y-coordinate                                     <28Sep83>
;                 D2 (input)   --  mouse Dx (integer)                                     <28Sep83>
;                 D3 (input)   --  mouse Dy (integer)                                     <28Sep83>
;   Function:   User supplied routine MOUSE is called each time mouse movement            <28Sep83>
;               detected.
;
;
;   Routine:    MouseRoutine                                                              <28Sep83>
;   Arguments:    A0 (output)  --  address of routine MOUSE                               <28Sep83>
;   Function:   Return the address of routine MOUSE, the user supplied routine            <28Sep83>
;               which is called each time mouse movement is detected.                     <28Sep83>
;

MouseRoutine    MOVE.L  MouseMVRoutine,A0   ; address of routine KEY                      <28Sep83>
                RTS                         ; return                                      <28Sep83>


;   Routine:    SetMouseRoutine                                                           <28Sep83>
;   Arguments:    A0 (input)   --  address of routine MOUSE                               <28Sep83>
;   Function:   Specify the address of routine MOUSE, the user supplied routine           <28Sep83>
;               which is called each time mouse movement is detected.                     <28Sep83>
;

SetMouseRoutine MOVE.L  A0,MouseMVRoutine   ; address of routine KEY                      <28Sep83>
                RTS                         ; return                                      <28Sep83>



;-----------------------------------------------------------------------------
;
;   Mouse Movement
;
;   This routine is called by the level 2 interrupt handler when the COPS has
;   reported mouse movement.  Interrupts are disabled when MouseMovement is
;   called.  All registers are preserved.
;
;
;   Register Assignments:
;
;       D0  --  Mouse Dx (integer)
;       D1  --  Mouse Dy (integer)
;

MouseMovement   MOVEM.L D1-D5,-(SP)         ; save registers
                MOVE.B  D0,MousDx           ; save mouse Dx         <28Sep83>
                MOVE.B  D1,MousDy           ; save mouse Dy         <28Sep83>
                MOVE.W  MousX,D0            ; mouse X-coordinate
                MOVE.W  MousY,D1            ; mouse Y-coordinate
                MOVE.B  MousDx,D2           ; mouse Dx (byte)
                EXT.W   D2                  ; mouse Dx (integer)
                MOVE.W  D2,D4               ; mouse Dx
                BGE.S   @1                  ; branch if >= 0
                NEG.W   D4                  ; ABS(mouse Dx)

@1              MOVE.B  MousDy,D3           ; mouse Dy (byte)
                EXT.W   D3                  ; mouse Dy (integer)
                MOVE.W  D3,D5               ; mouse Dy
                BGE.S   @2                  ; branch if >= 0
                NEG.W   D5                  ; ABS(mouse Dy)

@2              ADD.W   D5,D4               ; ABS(Dx) + ABS(Dy)
                EXT.L   D4                  ; mouse movement (long value)
                ADD.L   D4,MousOdometer     ; increment mouse odometer

                TST.B   MousScaling         ; scaling enabled
                BNE.S   Scale               ; branch if enabled

NoScale         ADD.W   D2,D0               ; new X-coordinate
                ADD.W   D3,D1               ; new Y-coordinate
                BRA.S   Bounds              ; continue

Scale           CMP.W   MousThresh,D4       ; fine or coarse movement ?
                BGT.S   Coarse              ; branch if coarse movement

Fine            ADD.W   D2,D0               ; new X-coordinate (scale 1)
                MOVE.W  D3,D2               ; save Dy
                ADD.W   D3,D3               ; Dy*2
                ADD.W   D3,D3               ; Dy*4
                ADD.W   D2,D3               ; Dy*5
                ADDQ    #2,D3               ; (Dy*5)+2
                BLT.S   @3                  ; branch if negative
                ADDQ    #3,D3               ; (Dy*5)+5
@3              ASR.W   #3,D3               ; Dy*(5/8) with rounding
                ADD.W   D3,D1               ; new Y-coordinate (scale 5/8)
                BRA.S   Bounds              ; continue

Coarse          ADD.W   D3,D1               ; new Y-coordinate (scale 1)
                MOVE.W  D2,D3               ; save Dx
                ADD.W   D3,D2               ; Dx*2
                ADD.W   D3,D2               ; Dx*3
                BLT.S   @4                  ; branch if negative
                ADDQ.W  #1,D2               ; (Dx*3)+1
@4              ASR.W   #1,D2               ; Dx*(3/2) with rounding
                ADD.W   D2,D0               ; new X-coordinate (scale 3/2)

Bounds          TST.W   D0                  ; new X-coordinate >= 0
                BGE.S   @5                  ; branch if >= 0
                MOVE.W  #0,D0               ; minimum X of 0

@5              CMP.W   #MaxX,D0            ; new X-coordinate <= 719
                BLE.S   @6                  ; branch if <= 719
                MOVE.W  #MaxX,D0            ; maximum X of 719

@6              TST.W   D1                  ; new Y-coordinate >= 0
                BGE.S   @7                  ; branch if >= 0
                MOVE.W  #0,D1               ; minimum Y of 0

@7              CMP.W   #MaxY,D1            ; new Y-coordinate <= 363
                BLE.S   @8                  ; branch if <= 363
                MOVE.W  #MaxY,D1            ; maximum Y of 363

@8              MOVE.W  D0,MousX            ; update Mouse X-coordinate
                MOVE.W  D1,MousY            ; update Mouse Y-coordinate
                MOVEM.L (SP)+,D1-D5         ; restore registers
                RTS                         ; return




ÿ