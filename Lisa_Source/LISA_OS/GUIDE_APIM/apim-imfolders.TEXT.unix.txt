{******************************************************************************}
{*                                                                            *}
{*              LisaGuide : Copyright 1983, Apple Computer Inc.               *}
{*                                                                            *}
{******************************************************************************}



{$s imfc }
{$r- }

UNIT IMFolders;

INTERFACE

USES {$U HwInt        }  HwInt,
     {$U libos/SysCall      }  SysCall,
     {$U libos/PSysCall     }  PSysCall,
     {$U UnitStd.obj  }  UnitStd,
     {$U UnitHz.obj   }  UnitHz,
     {$U Storage.obj  }  Storage,
     {$U QuickDraw    }  QuickDraw,
     {$U FontMgr.obj  }  FontMgr,
     {$U PmDecl       }  PmDecl,
     {$U PMM          }  PMM,
     {$U FMgrUtil.obj }  FmgrUtil,
     {$U WM.Events    }  Events,
     {$U WM.Folders   }  Folders,
     {$U WM.Menus     }  Menus,
     {$U WMLStd       }  WMLStd,
     {$U WMLSB        }  WMLSb,
     {$U WMLGrow      }  WMLGrow,
     {$U AlertMgr.obj }  AlertMgr,
     {$U dbenv.obj    }  dbenv,
     {$U DBdecl1      }  DBdecl1,
     {$U fedec.obj    }  fedec,
     {$U fld.obj      }  FieldEdit,
     {$U apim/UnitImf.obj  }  UnitImf,
     {$U apim/imcore.obj   }  IMCore,
     {$U apim/immenus.obj  }  IMMenus,
     {$U apim/IMStream.obj }  IMStream,
     {$U apim/IMSEdit.obj  }  IMSEdit;


CONST
  CMaxFldrs = 15;
  CRtMrgn  = 18;
  CLftMrgn = 22;



VAR
    FirstFldr:     THFldrInfo;
    LastFldr:      THFldrInfo;
    FldrCount:     TC;

    DefFldr:       TKeyStr;
    DefFldrIdx:    TC;

FUNCTION  HoldFldrState: THFldrState;
PROCEDURE FreeFldrState(fldrState: THFldrState);
PROCEDURE FreeIMFolder(dFldr: THFldrInfo);
FUNCTION  BuildIMFolder(fname: TKeyStr; fVis, fWindow, fSBars: TF;
                        fport: WindowPtr): THFldrInfo;
FUNCTION  FindFolder(fname: TKeyStr): THFldrInfo;
PROCEDURE FixWindowRects(fldr: THFldrInfo; str: THIMS);
PROCEDURE FreeStrFldr(fldr: THFldrInfo);
FUNCTION  NewFldrStream(fldr: THFldrInfo; skeep: TC; sNoErase: TF): THIMS;
PROCEDURE LinkFldrStream(fldr: THFldrInfo; imsObj: THIMS);
PROCEDURE DrawFldrContents(fldr: THFldrInfo);
PROCEDURE ReDrawFldrContents(fldr: THFldrInfo);
FUNCTION  PortFolder(fport: WindowPtr): THFldrInfo;
PROCEDURE SetFldrSize(IMFldr: THFldrInfo; szH, szV: TC; forStream: THIMS);
PROCEDURE IMHideScroll(IMFldr: THFldrInfo);
PROCEDURE IMShowScroll(IMFldr: THFldrInfo);
PROCEDURE DrawScroll(IMFldr: THFldrInfo);
PROCEDURE GrowFolder(folder: WindowPtr; IMFldr: THFldrInfo; hitPt: Point);
FUNCTION  TstScroll(windPort: WindowPtr; hitPt: Point): INTEGER;
PROCEDURE SetFldrMove(fldr: WindowPtr; fMove: TF);
FUNCTION  CanFldrMove(fldr: WindowPtr): TF;
PROCEDURE SetFldrGrow(fldr: WindowPtr; fGrow: TF);
FUNCTION  CanFldrGrow(fldr: WindowPtr): TF;
PROCEDURE SetFldrClose(fldr: WindowPtr; fClose: TF);
FUNCTION  CanFldrClose(fldr: WindowPtr): TF;
PROCEDURE IMFldrsInit;



IMPLEMENTATION

{$S imflw }
FUNCTION  HoldFldrState;   { : THFldrState; }
VAR fldrState: THFldrState;
BEGIN
  fldrState := POINTER(ORD(HAllocate(StrHeap, SIZEOF(TFldrState))));
  HoldFldrState := fldrState;
END;


{$S imflw }
PROCEDURE FreeFldrState;  { (fldrState: THFldrState); }
BEGIN
  IF fldrState <> NIL
    THEN FreeBk(StrHeap, POINTER(ORD(fldrState)), tybkStd);
END;


{$S imflc }
PROCEDURE FreeIMFolder; { (dFldr: THFldrInfo); }
LABEL 900;
VAR pFldr: THFldrInfo;
BEGIN
  IF dFldr = FirstFldr
    THEN BEGIN
      FirstFldr := dFldr^^.nextFldr;
      pFldr := NIL;
     END
    ELSE BEGIN
      pFldr := FirstFldr;
      WHILE pFldr <> NIL DO
        IF pFldr^^.nextFldr = dFldr
          THEN BEGIN
            pFldr^^.nextFldr := dFldr^^.nextFldr;
            GOTO 900;
           END
          ELSE pFldr := pFldr^^.nextFldr;
      IF fTstFldrs THEN
        WRITELN('FreeIMFolder - cant find delete Fldr:', ORD(dFldr));
     END;
900:
  IF dFldr = LastFldr THEN LastFldr := pFldr;
  DropMenus(@dFldr^^.fldrMenuList);
  FreeFldrState(dFldr^^.fldrState);
  FreeBk(StrHeap, POINTER(ORD(dFldr)), tybkStd);
END;


{$S imflw }
FUNCTION BuildIMFolder; { (fname: TKeyStr; fVis, fWindow, fSBars: TF;
             fport: WindowPtr): THFldrInfo; }
VAR hFldrInfo: THFldrInfo;
    fldrRect: Rect;
    bFldr: WindowPtr;
BEGIN
  fldrCount := fldrCount + 1;
  hFldrInfo := POINTER(ORD(HAllocate(StrHeap, SIZEOF(TFldrInfo))));
  IF FirstFldr = NIL
    THEN BEGIN
      FirstFldr := hFldrInfo;
      LastFldr := FirstFldr;
     END
    ELSE BEGIN
      LastFldr^^.nextFldr := hFldrInfo;
      LastFldr := hFldrInfo;
     END;
  WITH hFldrInfo^^ DO BEGIN
    IF fTstFldrs THEN WRITELN('creating ', fname, ' folder.');
    fldrName := fname;
    nextFldr := NIL;
    fldrId := fldrCount;
    fldrPort := fport;
    WITH fport^.portBits.bounds DO SetPt(fldrDefPos, -left, -top);
    WITH fport^.PortRect DO SetPt(fldrDefSize, right-left, bottom-top);
    fldrDispWindow := NIL;
    fldrMenuList.firstMenu := NIL;
    fldrMenuList.lastMenu := NIL;
    fldrMenuList.menuCount := 0;
    fldrState := NIL;
    fldrVis := fVis;
    fldrActive := FALSE;
    fldrSBars := fSBars;
   END;
  IF fSBars THEN InitSbList(hFldrInfo^^.fldrSBList, StrHeap);
  WITH hFldrInfo^^ DO
    IF fWindow THEN fldrDispWindow := NewIMWindow(fldrPort, hFldrInfo);
  IF fTstFldrs THEN WRITELN('BuildIMFolder returns:', ORD(hFldrInfo));
  BuildIMFolder := hFldrInfo;
END;


{FUNCTION  NewIMObj; { (fName: TKeyStr; fVis, fWindow, fSBars: TF;
{                       clsLoc: Point; fport: WindowPtr;
{                       fKind, fSubKind: ObjectKind): THFldrInfo; }
{  { given the graf port, this builds the IMFolder and the FilerObject
{    around it }
{VAR newFldr: THFldrInfo;
{    newObj: ObjectHandle;
{BEGIN
{  IF fTstFldrs THEN WRITELN('creating ', fname, ' IMobj.');
{  newFldr := BuildIMFolder(fName, fVis, fWindow, fSBars, fport);
{  newObj := IFMakeObj(fName, fKind, fSubKind, deskObject,
{                      clsLoc, newFldr, fPort^.PortRect);
{  newFldr^^.fldrFObj := POINTER(ORD(newObj));
{  IF fVis THEN IFOpenObj(newObj);
{  NewIMObj := newFldr;
{  IFDrawDesk;
{END;
}


{$S imflh }
FUNCTION FindFolder; {(fname: TKeyStr): THFldrInfo; }
LABEL 900;
VAR ffldr: THFldrInfo;
BEGIN
  ffldr := FirstFldr;
    WHILE ffldr <> NIL DO
      BEGIN
        IF ffldr^^.fldrName = fname THEN GOTO 900;
        ffldr := ffldr^^.nextFldr;
      END;
900:
  FindFolder := ffldr;
END;


{$S imflw }
PROCEDURE FixWindowRects;  { (fldr: THFldrInfo; str: THIMS); }
VAR IMWobj: THIMW;
    wr, wdr: Rect;
BEGIN
  IMWobj := fldr^^.fldrDispWindow;
  IF IMWobj = NIL
    THEN BEGIN
       { IF fTstFldrs THEN }
         WRITELN('FixWindowRects folder "', fldr^^.fldrName, '" has no DWindow');
       EXIT(FixWindowRects);
     END;
  wr := fldr^^.fldrPort^.portRect;
  IF fldr^^.fldrSBars THEN
    WITH wr DO SetRect(wr, left, top, right-dhSbox+1, bottom-dvSbox+1);
  WITH IMWobj^^ DO SetRect(wdr,
           wr.left   + wDispRect.left   - wRect.left,
           wr.top    + wDispRect.top    - wRect.top,
           wr.right  + wDispRect.right  - wRect.right,
           wr.bottom + wDispRect.bottom - wRect.bottom);
  SetDWRect(fldr^^.fldrDispWindow, wr);
  SetDWDispRect(fldr^^.fldrDispWindow, wdr);
  IF str <> NIL
    THEN str^^.sLineData.dLWidth := wdr.right - wdr.left - CRtMrgn;
  IF fTstFldrs THEN
    WRITELN('Set width of stream:', ORD(str), ' to:', wdr.right-wdr.left);
END;


{$S imflc }
PROCEDURE FreeStrFldr;  { (fldr: THFldrInfo);  }
BEGIN
  IF fldr <> NIL
    THEN BEGIN
      IF fldr^^.fldrDispWindow <> NIL
        THEN BEGIN
          IF fldr^^.fldrDispWindow^^.wLink <> NIL
            THEN FreeIMStream(fldr^^.fldrDispWindow^^.wLink^^.kStream);
          FreeIMWindow(fldr^^.fldrDispWindow, FALSE);
        END;
      FreeIMFolder(fldr);
     END;
END;


{$S imflw }
FUNCTION   NewFldrStream;
        { (fldr: THFldrInfo; skeep: TC; sNoErase: TF): THIMS;  }
VAR newIMS: THIMS;
    dr: Rect;
BEGIN
  IF fTstFldrs THEN
    WRITELN('NewFldrStream - fldr:', ORD(fldr), ' sKeep:', sKeep,
             ' sNoErase: ', sNoErase);
  IF fldr = NIL THEN EXIT(NewFldrStream);
  WITH fldr^^ DO
    IF fldrDispWindow = NIL
      THEN fldrDispWindow := NewIMWindow(fldrPort, fldr)
      ELSE IF ((fldr = hndLGfldr) OR (fldr = hndScriptFldr))
              THEN BEGIN
                IF fTstFldrs THEN
                  WRITELN('NewFldrStream called for Guide or Script folder: ',
                          ORD(fldr));
                BreakWSLink(fldrDispWindow^^.wLink, FALSE);
               END
              ELSE BreakWSLink(fldrDispWindow^^.wLink, TRUE);
  newIMS := NewIMStream;
  dr := fldr^^.fldrDispWindow^^.wRect;
  IF fldr^^.fldrSBars THEN
    WITH dr DO SetRect(dr, left, top, right - dhSBox, bottom - dvSBox);
  SetDWRect(fldr^^.fldrDispWindow, dr);
  dr.left := dr.left + CLftMrgn;  { default indent for fldrStream }
  SetDWDispRect(fldr^^.fldrDispWindow, dr);
  newIMS^^.sKeepCt := sKeep;
  newIMS^^.sDontErase := sNoErase;
  MakeWSLink(newIMS, fldr^^.fldrDispWindow);
  newIMS^^.sLineData.dLWidth := dr.right - dr.left - CRtMrgn;
  NewFldrStream := newIMS;
END;


{$S imflw }
PROCEDURE LinkFldrStream;  { (fldr: THFldrInfo; imsObj: THIMS); }
VAR dr: Rect;
BEGIN
  IF fldr = NIL THEN EXIT(LinkFldrStream);
  WITH fldr^^ DO
    IF fldr^^.fldrDispWindow = NIL
      THEN fldrDispWindow := NewIMWindow(fldrPort, fldr);
  MakeWSLink(imsObj, fldr^^.fldrDispWindow);
  dr := fldr^^.fldrDispWindow^^.wRect;
  IF fldr^^.fldrSBars THEN
    WITH dr DO SetRect(dr, left, top, right - dhSBox, bottom - dvSbox);
  SetDWRect(fldr^^.fldrDispWindow, dr);
  dr.left := dr.left + CLftMrgn;  { default indent for fldrStream }
  SetDWDispRect(fldr^^.fldrDispWindow, dr);
  IF imsObj <> NIL
    THEN imsObj^^.sLineData.dLWidth := dr.right - dr.left - CRtMrgn;
END;


{$S imflh }
PROCEDURE DrawFldrContents;  { (fldr: THFldrInfo)}
BEGIN
  IF fldr = NIL THEN EXIT(DrawFldrContents);
  SetPort(fldr^^.fldrPort);
  IF fldr^^.fldrDispWindow = NIL
    THEN EXIT(DrawFldrContents);
  WITH fldr^^ DO DrawDWindow(fldrDispWindow^^.wLink);
END;


{$S imflh }
PROCEDURE ReDrawFldrContents;  { (fldr: THFldrInfo); }
BEGIN
  IF fldr = NIL THEN EXIT(ReDrawFldrContents);
  SetPort(fldr^^.fldrPort);
  IF fldr^^.fldrDispWindow <> NIL
    THEN WITH fldr^^ DO
       ReDrawDWRect(fldrDispWindow^^.wLink, fldrPort^.PortRect);
  IF fldr^^.fldrSBars THEN DrawScroll(fldr);
END;


{$S imflh }
FUNCTION PortFolder; { (fport: WindowPtr): THFldrInfo; }
LABEL 900;
VAR ffldr: THFldrInfo;
BEGIN
  ffldr := FirstFldr;
    WHILE ffldr <> NIL DO
      BEGIN
        IF ffldr^^.fldrPort = fport THEN GOTO 900;
        ffldr := ffldr^^.nextFldr;
      END;
900:
  PortFolder := ffldr;
END;


{$S imflw }
PROCEDURE SetFldrSize; {(IMFldr: THFldrInfo; szH, szV: TC; forStream: THIMS);}
VAR rH, rV: Rect;
    fixRgn: RgnHandle;
BEGIN
  IF IMFldr = NIL THEN EXIT(SetFldrSize);
  SetPort(IMFldr^^.fldrPort);
  fixRgn := NewRgn;
  FlushRects(rH, rV);
  WITH rH DO SetRectRgn(fixRgn, left-1, top-1, right, bottom);
  InvalidateRgn(IMFldr^^.fldrPort, fixRgn);
  RectRgn(fixRgn, rV);
  WITH rV DO SetRectRgn(fixRgn, left-1, top-1, right, rH.bottom);
  InvalidateRgn(IMFldr^^.fldrPort, fixRgn);
  FolderSize(IMFldr^^.fldrPort, szH, szV, TRUE);
  WITH IMFldr^^ DO
    IF fldrSBars AND (fldrDispWindow <> NIL)
     THEN WITH fldrDispWindow^^ DO BEGIN
       FlushRects(rH, rV);
       SetSbRect(wHSbar, rH);
       SetSbRect(wVSbar, rV);
      END;
  WITH rH DO SetRectRgn(fixRgn, left-1, top-1, right, bottom);
  InvalidateRgn(IMFldr^^.fldrPort, fixRgn);
  RectRgn(fixRgn, rV);
  WITH rV DO SetRectRgn(fixRgn, left-1, top-1, right, rH.bottom);
  InvalidateRgn(IMFldr^^.fldrPort, fixRgn);
  DisposeRgn(fixRgn);
  FixWindowRects(IMFldr, forStream);
END;


{$S imflx }
PROCEDURE RmvScroll(IMFldr: THFldrInfo; fUpdate: TF);
VAR hbar, vbar: Rect;
    folder: WindowPtr;
    fixRgn: RgnHandle;
BEGIN
  IF IMFldr = NIL
    THEN BEGIN
      IF IMtst THEN WRITELN('RmvScroll of null IMFldr.');
      EXIT(RmvScroll);
     END;
  folder := IMFldr^^.fldrPort;
  IF folder = NIL
    THEN BEGIN
      IF IMtst THEN WRITELN('RmvScroll of null folder.');
      EXIT(RmvScroll);
     END;
  IF IMFldr^^.fldrSBars
    THEN WITH folder^ DO WITH portRect DO
      BEGIN
 IF fTstFldrs THEN WRITELN('Calling setport');
        SetPort(folder);
        ClipRect(portRect);
 IF fTstFldrs THEN WRITELN('Calling pennormal');
        PenNormal;
        FlushRects(hbar, vbar);
        EraseRect(hbar);
        IF fUpdate THEN BEGIN
            fixRgn := NewRgn;
            RectRgn(fixRgn, hbar);
            InvalidateRgn(folder, fixRgn);
           END;
        EraseRect(vbar);
        IF fUpdate THEN BEGIN
            RectRgn(fixRgn, vbar);
            InvalidateRgn(folder, fixRgn);
            DisposeRgn(fixRgn);
           END;
      END;
END;


{$S imflw }
PROCEDURE IMHideScroll; { (IMFldr: THFldrInfo); }
VAR hbar, vbar: Rect;
    folder: WindowPtr;
BEGIN
  IF IMFldr = NIL
    THEN BEGIN
      IF IMtst THEN WRITELN('IMHideScroll of null IMFldr.');
      EXIT(IMHideScroll);
     END;
  folder := IMFldr^^.fldrPort;
  IF folder = NIL
    THEN BEGIN
      IF IMtst THEN WRITELN('IMHideScroll of null folder.');
      EXIT(IMHideScroll);
     END;
  IF (folder <> alertFolder)
    THEN WITH folder^ DO BEGIN
      SetPort(folder);
      ClipRect(portRect);
      HideSBorder(portRect);
     END;
END;


{$S imflw }
PROCEDURE IMShowScroll; { (IMFldr: THFldrInfo); }
{ draw scroll bars then clip down to protect them }
VAR saveFont: TC;
    fErr: TC;
    tempRect: Rect;
    width,height: INTEGER;
    imWindow: THIMW;
    windPort: WindowPtr;
BEGIN
  IF IMFldr = NIL
    THEN BEGIN
      IF IMtst THEN WRITELN('IMShowScroll of null IMFldr.');
      EXIT(IMShowScroll);
     END;
  imWindow := IMFldr^^.fldrDispWindow;
  IF imWindow = NIL
    THEN BEGIN
      IF cDebug THEN WRITELN('IMShowScroll of folder with no IM Window.');
      EXIT(IMShowScroll);
     END;
  windPort := imWindow^^.wPort;
  IF windPort = NIL
    THEN BEGIN
      IF cDebug THEN WRITELN('IMShowScroll of null folder.');
      EXIT(IMShowScroll);
     END;
  SetPort(windPort);
  PenNormal;
IF fTstFldrs THEN WRITELN('IMShowScroll - Calling Cliprect');
  ClipRect(windPort^.portRect);   { open clipping to whole portRect }
  IF IMFldr^^.fldrSBars
    THEN BEGIN
      IF fTstFldrs THEN WRITELN('Painting scroll bars');
      PaintSb(imWindow^^.wHSbar);
      PaintSb(imWindow^^.wVSbar);
      IF CanFldrGrow(windPort) THEN PaintGrow;
     END;
  SetIMWClip(imWindow);
END;


{$S imflw }
PROCEDURE DrawScroll; { (IMFldr: THFldrInfo); }
BEGIN
  IF IMtst THEN WRITELN('Calling DrawScroll');
  IF IMFldr = hndActiveFldr
    THEN IMShowScroll(IMFldr) ELSE IMHideScroll(IMFldr);
END;


{$S imflw }
PROCEDURE GrowFolder; { (folder: WindowPtr; IMFldr: THFldrInfo; hitPt: Point); }
VAR sizePt: Point;
BEGIN
  SetPort(folder);
  IF NOT CanFldrGrow(folder) THEN EXIT(GrowFolder);
  DragFrame(hitPt, TRUE, sizePt);
  SubPt(folder^.portRect.topLeft, sizePt);
  SetFldrSize(IMFldr, sizePt.h, sizePt.v, NIL);
  DrawScroll(IMFldr);
END;


{$S imflh }
FUNCTION TstScroll; { (windPort: WindowPtr; hitPt: Point): INTEGER; }
{ hit test for scroll bars and grow icon, return code }
{ code:  0 = nothing hit,  1 = hit grow icon, 2 = scroll - already done}
VAR growRect: Rect;
    checkRes: INTEGER;
    hFldr: THFldrInfo;
    wind: THIMW;
    hitSb: THSb;
    hitIcon: TIcon;
BEGIN
  checkRes := 0;
  hFldr := PortFolder(windPort);
  SetPort(windPort);
  IF hFldr = NIL
    THEN BEGIN
      WRITELN('Calling tstScroll for non-im port: ', ORD(windPort));
      IF FGrowHit(hitPt) THEN checkRes := 1;
     END
    ELSE BEGIN
      wind := hFldr^^.fldrDispWindow;
      IF wind = NIL
        THEN WRITELN('TstScroll hit IMFldr with no IMWindow')
        ELSE BEGIN
           IF FSbHit(hFldr^^.fldrSBList, hitPt, hitSb, hitIcon)
             THEN BEGIN
               checkRes := 2;
               ScrollWindow(wind^^.wLink, hitSb, hitIcon, hitPt);
              END;
         END;
      IF FGrowHit(hitPt)
         THEN BEGIN
           checkRes := 1;
           GrowFolder(windPort, hFldr, hitPt);
         END;
     END;
  TstScroll := checkRes;
END;


{$S imflw }
PROCEDURE SetFldrMove; { (fldr: WindowPtr; fMove: TF); }
VAR wInfo: WindowInfo;
BEGIN
  IF fldr = NIL THEN EXIT(SetFldrMove);
  GetWindInfo(fldr, wInfo);
  wInfo.okMove := fMove;
  SetWindInfo(fldr, wInfo);
END;

{$S imflh }
FUNCTION  CanFldrMove; { (fldr: WindowPtr): TF; }
VAR wInfo: WindowInfo;
BEGIN
  IF fldr = NIL THEN BEGIN CanFldrMove := TRUE; EXIT(CanFldrMove); END;
  GetWindInfo(fldr, wInfo);
  CanFldrMove := wInfo.okMove;
END;

{$S imflw }
PROCEDURE SetFldrGrow; { (fldr: WindowPtr; fGrow: TF); }
VAR wInfo: WindowInfo;
    gRect: Rect;
BEGIN
  IF fldr = NIL THEN EXIT(SetFldrGrow);
  GetWindInfo(fldr, wInfo);
  wInfo.okGrow := fGrow;
  SetWindInfo(fldr, wInfo);
  IF fldr = activeFolder
    THEN BEGIN
      SetPort(fldr);
      ClipRect(fldr^.PortRect);
      IF fGrow
        THEN PaintGrow
        ELSE BEGIN
          GetGrowRect(gRect);
          InsetRect(gRect, 1, 1);
          EraseRect(gRect);
         END;
     END;
END;

{$S imflh }
FUNCTION  CanFldrGrow; { (fldr: WindowPtr): TF; }
VAR wInfo: WindowInfo;
BEGIN
  IF fldr = NIL THEN BEGIN CanFldrGrow := TRUE; EXIT(CanFldrGrow); END;
  GetWindInfo(fldr, wInfo);
  CanFldrGrow := wInfo.okGrow;
END;

{$S imflw }
PROCEDURE SetFldrClose; { (fldr: WindowPtr; fClose: TF); }
VAR wInfo: WindowInfo;
BEGIN
  IF fldr = NIL THEN EXIT(SetFldrClose);
  GetWindInfo(fldr, wInfo);
  wInfo.okClose := fClose;
  SetWindInfo(fldr, wInfo);
END;


{$S imflh }
FUNCTION  CanFldrClose; { (fldr: WindowPtr): TF; }
VAR wInfo: WindowInfo;
BEGIN
  IF fldr = NIL THEN BEGIN CanFldrClose := TRUE; EXIT(CanFldrClose); END;
  GetWindInfo(fldr, wInfo);
  CanFldrClose := wInfo.okClose;
END;

{$S imflini }
PROCEDURE IMFldrsInit;
BEGIN
  FldrCount := 0;
  FirstFldr := NIL;
  LastFldr := NIL;
END;



END.



ÿ