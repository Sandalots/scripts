{******************************************************************************}
{*                                                                            *}
{*              LisaGuide : Copyright 1983, Apple Computer Inc.               *}
{*                                                                            *}
{******************************************************************************}



{$s imjc }
UNIT IMSim;

INTERFACE

USES {$U HwInt        }  HwInt,
     {$U libos/SysCall      }  SysCall,
     {$U libos/PSysCall     }  PSysCall,
     {$U UnitStd.obj  }  UnitStd,
     {$U UnitHz.obj   }  UnitHz,
     {$U Storage.obj  }  Storage,
     {$U QuickDraw    }  QuickDraw,
     {$U FontMgr.obj  }  FontMgr,
     {$U PmDecl       }  PmDecl,
     {$U PMM          }  PMM,
     {$U FMgrUtil.obj }  FmgrUtil,
     {$U WM.Events    }  Events,
     {$U WM.Folders   }  Folders,
     {$U WM.Menus     }  Menus,
     {$U WMLStd       }  WMLStd,
     {$U WMLSB        }  WMLSb,
     {$U WMLGrow      }  WMLGrow,
     {$U AlertMgr.obj }  AlertMgr,
     {$U dbenv.obj    }  dbenv,
     {$U DBdecl1      }  DBdecl1,
     {$U fedec.obj    }  fedec,
     {$U fld.obj      }  FieldEdit,
     {$U apim/UnitImf.obj  }  UnitImf,
     {$U apim/imcore.obj   }  IMCore,
     {$U apim/IMMenus.obj  }  IMMenus,
     {$U apim/IMStream.obj }  IMStream,
     {$U apim/IMSEdit.obj  }  IMSEdit,
     {$U apim/IMFolders.obj}  IMFolders,
     {$U apim/TFGlobals    }  IFGlobals,  { Common routines & declarations }
     {$U apim/TFEntry      }  IFEntry,
     {$U apim/TCatalog     }  IFCatalog,
     {$U apim/TFiler       }  TFiler,     { TeachingFiler unit }
     {$U apim/IMScript.obj }  IMScript,
     {$U apim/IMPatMat.obj }  IMPatMat,
     {$U apim/IMInterp.obj }  IMInterp;


CONST
  CsimNormal = 1;
  CsimPlayback = 2;
  csimRecord = 3;
  CsimHold = 4;

  CJrnlBlkSize = 512;

  kcI = 35;
  kcCodeUp = 124;
  kcCodeDown = 252;



TYPE

  TJFile = FILE;


VAR
  simFileID: TJFile;
  simFileName: TFileStr;

  simMode:    TB;

  simJMode:   INTEGER;

  simTrapKeys: KeySet;
  simNoTrapKeys: KeySet;
  fCodeKey: BOOLEAN;


PROCEDURE InitSim;
PROCEDURE MakeSimName(VAR fName: TFileStr);
FUNCTION  FileExists(fName: TFileStr): TF;
FUNCTION  OpenPlayback(filStr: TFileStr): TF;
PROCEDURE ClosePlayback;
FUNCTION  OpenRecord(filStr: TFileStr): TF;
PROCEDURE CloseRecord;
PROCEDURE SetSimPrefix(pfx: TFileStr);
FUNCTION  CopyJrnlFile(srcFilStr, destFilStr: TFileStr): TF;



IMPLEMENTATION

VAR

  simOpened: TF;
  simFilePrefix: TFileStr;
  simFileSuffix: TFileStr;

  simHoldMode: TB;

  simStartTime: TL;
  simHoldTime: TL;


{$S imjc }
PROCEDURE SimHold;
VAR curTime: TL;
BEGIN
  IF (simMode = CsimPlayback) OR (simMode = CsimRecord)
    THEN BEGIN
      curTime := TIME;
      simHoldTime := curTime;
      simHoldMode := simMode;
      simMode := CsimHold;
      IF fTstSim THEN
        WRITELN('Sim Holding from time:', curTime, ' simStartTime:', simStartTime);
     END;
END;


{$S imjc }
PROCEDURE SimRelease;
VAR curTime: TL;
BEGIN
  IF simMode = CsimHold
    THEN BEGIN
      curTime := TIME;
      simMode := simHoldMode;
      simStartTime := simStartTime + (curTime - simHoldTime);
      IF fTstSim THEN
        WRITELN('Sim Released at:', curTime, ' simHoldTime:', simHoldTime,
                 'simStartTime:', simStartTime);
      simHoldTime := 0;
      simHoldMode := CsimNormal;
     END;
END;



{$S imjw }
PROCEDURE IMCommand;
VAR gp: GrafPtr;
BEGIN
  { SimHold; }
  IF fTstSim THEN WRITELN('Doing IM Command.');
  GetPort(gp);
  SetPort(activePort);
  doIMCommand;
  SetPort(gp);
  IF fTstSim THEN WRITELN('Back from IM Command.');
  { SimRelease; }
END;


{$S imjw }
PROCEDURE CodeIProc(kc: KeyCode);
BEGIN
  IF fTstSim THEN WRITELN('keyTrap for key:', kc);
  IF kc = kcCodeDown THEN fCodeKey := TRUE;
  IF kc = kcCodeUp THEN fCodeKey := FALSE;
  { IF fCodeKey THEN }  IF kc = kcI THEN IMCommand;
END;


{$S imjini }
PROCEDURE InitSim;
VAR i: TC;
BEGIN

  simMode := CsimNormal;
  simOpened := FALSE;
  simFilePrefix := '';
  simFileSuffix := '.jrnl';

  fCodeKey := FALSE;

  simTrapKeys := [35];    { [kcI, kcCodeUp, kcCodeDown]; }
  simNoTrapKeys := [35];  { [kcCodeUp, kcCodeDown];      }
  trapKeys := simNoTrapKeys;
  keyTrapProc := @CodeIProc;
  trapPrcs := My_ID;
  simJMode := 6;
  IF fTstSim THEN
    WRITELN('keyTrapProc: ', ORD(keyTrapProc),
         ' trapPrcs:', trapPrcs, ' simJMode:', simJMode);

END;


{$S imjw }
PROCEDURE MakeSimName; { (VAR fName: TFileStr); }
BEGIN
  IF fTstSim THEN WRITE('MakeSimName: ', fName);
  IF POS('-', fName) = 0
    THEN fName := CONCAT(simFilePrefix, fName);
  IF POS('.', fName) = 0
    THEN fName := CONCAT(fName, simFileSuffix);
  IF fTstSim THEN WRITELN(' becomes: ', fName);
  IF fTstSim THEN WRITE('prefix: ', simFilePrefix);
  IF fTstSim THEN WRITELN(', suffix: ', simFileSuffix);
END;


{$S imjw }
FUNCTION  FileExists; { (fName: TFileStr): TF; }
VAR fid: TJFile;
    ior: TC;
BEGIN
  IF fTstSim THEN WRITELN('Checking existence of file: ', fName);
  {$I-}
  RESET(fid, fName);
  {$I+}
  ior := IORESULT;
  IF ior = 0
    THEN BEGIN
      CLOSE(fid);
      FileExists := TRUE;
      IF fTstSim THEN WRITELN('File: ', fName, ' exists.');
     END
    ELSE BEGIN
      FileExists := FALSE;
      IF fTstSim THEN WRITELN('File: ', fName, ' does not exist.');
     END;
END;


{$S imjw }
FUNCTION  OpenPlayback; { (filStr: TFileStr): TF; }
BEGIN
  trapKeys := simNoTrapKeys;
  keyTrapProc := @CodeIProc;
  trapPrcs := My_ID;
  MakeSimName(filStr);
  IF fTstSim THEN WRITELN('Starting playback of file: ', filStr);
  IF NOT FileExists(filStr)
    THEN BEGIN
      CmdError('Simulation file does not exist.');
      EXIT(OpenPlayback);
     END;
  keyTrapProc := @CodeIProc;
  trapPrcs := My_ID;
  trapKeys := simTrapKeys;
  IF fTstSIm THEN WRITELN('keyTrapProc: ', ORD(keyTrapProc),
                          ' trapPrcs:', trapPrcs, ' simJMode:', simJMode);
  StartPlayback(filStr, simJMode);
  simStartTime := TIME;
  simOpened := TRUE;
  OpenPlayback := TRUE;
  simMode := CsimPlayback;
  IF fTstSim THEN
    WRITELN('Playback started at time:', simStartTime, ' simMode:', ORD(simMode));
END;


{$S imjw }
PROCEDURE ClosePlayback;
BEGIN
  StopPlayback;
  trapKeys := simNoTrapKeys;
  simMode := CsimNormal;
  IF fTstSim THEN WRITELN('Playback stopped at time:', TIME);
  simStartTime := 0;
  simOpened := FALSE;
END;


{$S imjc }
FUNCTION  OpenRecord; { (filStr: TFilStr): TF; }
VAR recfile: TFileSTr;
    svPort: GrafPtr;
BEGIN
  GetPort(svPort);
  SetPort(deskPort);
  InvertRect(deskPort^.PortRect);
  SetPort(svPort);
  trapKeys := simNoTrapKeys;
  keyTrapProc := @CodeIProc;
  trapPrcs := My_ID;
  MakeSimName(filStr);
  IF fTstSim THEN WRITELN('Starting recording of file: ', filStr);
  simMode := CsimRecord;
  keyTrapProc := @CodeIProc;
  trapPrcs := My_ID;
  trapKeys := simTrapKeys;
  IF fTstSIm THEN WRITELN('keyTrapProc: ', ORD(keyTrapProc),
                          ' trapPrcs:', trapPrcs, ' simJMode:', simJMode);
{ trapKeys := [kcI, kcCodeUp, kcCodeDown];  }
  StartRecord(filStr, simJMode);
  simStartTime := TIME;
  simOpened := TRUE;
  OpenRecord := TRUE;
  IF fTstSim THEN
    WRITELN('Record started at time:', simStartTime, ' simMode=', ORD(simMode));
  SetPort(deskPort);
  InvertRect(deskPort^.PortRect);
  SetPort(svPort);
END;


{$S imjc }
PROCEDURE CloseRecord;
BEGIN
  simMode := CsimNormal;
  StopRecording;
  trapKeys := simNoTrapKeys;
  simStartTime := 0;
  simOpened := FALSE;
  WRITELN('Recording Stopped at time', TIME);
END;


{$S imjini }
PROCEDURE SetSimPrefix; { (pfx: TFileStr); }
VAR dashpos: TC;
BEGIN
  simFilePrefix := pfx;
  dashpos := POS('-', simFilePrefix);
  IF dashpos = 0
    THEN BEGIN
      WRITELN('simFilePrefix has no dash. /', simfilePrefix, '/');
     END;
  IF fTstSim THEN WRITELN('simFilePrefix set to: ', simFilePrefix);
END;


{$S imjw }
FUNCTION OpenJrnlFile(VAR filid: TJFile; VAR filstr: TFileStr; fOld: TF): TF;
VAR JrnlFile: TFileSTr;
    ior: TC;
BEGIN
  IF fTstSim THEN WRITELN('Opening file: ', filstr, ' fOld: ', fOld);
  JrnlFile := filStr;
  MakeSimName(JrnlFile);
  IF fOld
    THEN BEGIN
      {$I-}
      IF fTstSim THEN WRITELN('Resetting file name: ', JrnlFile);
      RESET(filid, JrnlFile);
      ior := IORESULT;
      {$I+}
     END
    ELSE BEGIN
      {$I-}
      IF fTstSim THEN WRITELN('Rewriting file name: ', JrnlFile);
      REWRITE(filid, JrnlFile);
      ior := IORESULT;
      {$I+}
     END;
  IF ior <> 0
    THEN BEGIN
      IF fTstSim THEN WRITELN('Cant open JrnlFile: ', JrnlFile);
      OpenJrnlFile := FALSE;
     END
    ELSE BEGIN
      IF fTstSim THEN WRITELN('JrnlFile ', JrnlFile, ' opened.');
      OpenJrnlFile := TRUE;
      filstr := JrnlFile;
     END;
END;


{$S imjc }
FUNCTION  CopyJrnlFile; { (srcFilStr, destFilStr: TFileStr): TF; }
LABEL 700, 800;
VAR srcFilid,destFilid: TJFile;
    fBuf: ARRAY[0..CJrnlBlkSize] OF TC;
    rBlkCt: TC;
    ior: TC;
BEGIN
  WRITELN('Entering CopyJrnlFile.');
  IF NOT OpenJrnlFile(srcFilid, srcFilStr, TRUE)
    THEN BEGIN
      WRITELN('Cant open source jrnl file in CopyJrnlFile. Copy aborted.');
      CopyJrnlFile := FALSE;
      GOTO 800;
     END;
  IF NOT OpenJrnlFile(destFilid, destFilStr, FALSE)
    THEN BEGIN
      WRITELN('Cant open destination jrnl file in CopyJrnlFile. Copy aborted.');
      CLOSE(srcFilid);
      CopyJrnlFile := FALSE;
      GOTO 800;
     END;
  WHILE NOT EOF(srcFilid) DO
    BEGIN
      rBlkCt := BlockRead(srcFilID, fbuf, 1);
      ior := IORESULT;
      IF rBlkCt = 0 THEN
        BEGIN
          WRITELN('No more blocks to read from input file in CopyFile');
          GOTO 700;
         END;
      IF ior <> 0
        THEN BEGIN
          WRITELN('IORESULT (', ior, ') reading source file. Copy aborted.');
          CopyJrnlFile := FALSE;
          CLOSE(srcFilid);
          CLOSE(destFilid);
          GOTO 800;
         END;
      rBlkCt := BlockRead(srcFilID, fbuf, 1);
      ior := IORESULT;
      IF (ior <> 0) OR (rBlkCt <> 1)
        THEN BEGIN
          WRITELN('IORESULT (', ior, '), writeBlockCount (', rBlkCt:3,
                            ') writing dest file. Copy aborted.');
          CopyJrnlFile := FALSE;
          CLOSE(srcFilid);
          CLOSE(destFilid);
          GOTO 800;
         END;
     END;
700:
  CLOSE(srcFilid);
  CLOSE(destFilid, LOCK);
  CopyJrnlFile := TRUE;
800:
  WRITELN('Exiting CopyJrnlFile.');
END;




END.





ÿ