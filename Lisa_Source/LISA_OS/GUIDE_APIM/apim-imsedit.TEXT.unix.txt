{******************************************************************************}
{*                                                                            *}
{*              LisaGuide : Copyright 1983, Apple Computer Inc.               *}
{*                                                                            *}
{******************************************************************************}



{$s imsec }
{$r-}

UNIT ImsEdit;
INTERFACE

USES {$U UnitStd.obj}  UnitStd,
     {$U UnitHz.obj}   UnitHz,
     {$U Storage.obj}  Storage,
     {$U QuickDraw}    QuickDraw,
     {$U FontMgr.obj}  FontMgr,
     {$U libos/SysCall.obj}  SysCall,
     {$U libos/PSysCall.obj} PSysCall,
     {$U PmDecl     }  PmDecl,
     {$U PMM        }  PMM,
     {$U WM.Events}    Events,
     {$U WM.Folders}   Folders,
     {$U WM.Menus }    Menus,
     {$U WMLStd  }     WMLStd,
     {$U WMLSB   }     WMLSb,
     {$U WMLGrow }     WMLGrow,
     {$U AlertMgr.obj} AlertMgr,
     {$U dbenv.obj}    dbenv,
     {$U fedec.obj}    fedec,
     {$U fld.obj}      FieldEdit,
     {$U Scrap.obj}    Scrap,
     {$U apim/UnitIMF.obj}  UnitIMF,
     {$U apim/imcore.obj}   IMCore,
     {$U apim/immenus.obj}  IMMenus,
     {$U apim/imstream.obj} Imstream;


PROCEDURE ShowScrap;
PROCEDURE HideScrap;
PROCEDURE SetupUndo(fUndoable: TF);
PROCEDURE RecordSelection;
PROCEDURE SelectComplete;
PROCEDURE ClearSelection;
FUNCTION  IMSSelectLine(wsLink: THWSLink; hSelLine: THImsLine;
               mloc, fldPt: Point; VAR nbrHd: Rect; VAR tout: TC): TF;
PROCEDURE IMSDragSelect(wsLink: THWSLink; mloc: Point; VAR nbrHd: Rect);
FUNCTION  SelectAtPos(wsLink: THWSLink; hSelLine: THImsLine;
                      fldPt: Point; selFst, selLim: TC; VAR tout: TC): TF;
FUNCTION  IMSChar(wsLink: THWSLink; ch: CHAR; shifted: TF; VAR tout: TC): TF;
PROCEDURE IMSCut(wsLink: THWSLink; VAR errnum: TC);
PROCEDURE IMSCopy(wsLink: THWSLink; VAR errnum: TC);
PROCEDURE IMSPaste(wsLink: THWSLink; VAR errnum: TC);
PROCEDURE IMSUndo(wsLink: THWSLink; VAR errnum: TC);
FUNCTION  IMSDouble(wsLink: THWSLink;
                     mloc: Point; VAR nbhd: Rect; VAR tout: TC): TF;
FUNCTION  IMSTriple(wsLink: THWSLink; VAR nbhd: Rect; VAR tout: TC): TF;
FUNCTION  IMSButtonUp(wsLink: THWSLink; VAR tout: TC): TF;
PROCEDURE IMSTimeout(wsLink: THWSLink; VAR tout: TC);
PROCEDURE GetSelection(IMSobj: THIMS; VAR sFlag: TF; VAR sLine: THImsLine;
                           VAR sData: txSel);





IMPLEMENTATION

CONST
  CIMLMargin = 10;
  CFldLMargin = 4;
  CIMSelExtra = 10;

  CMaxFmts = 4;

  chbs = 8;
  chret = 13;

PROCEDURE WaitEnded;       EXTERNAL;
PROCEDURE IFCloseObj(obj : THFilerObj; closeSons: BOOLEAN);       EXTERNAL;
PROCEDURE IFOpenObj(obj : THFilerObj);                            EXTERNAL;
FUNCTION  IFFindObj(window: WindowPtr; VAR hObj: THFilerObj): TF; EXTERNAL;


{$s imsew }
PROCEDURE ShowScrap;
VAR scrapObj: THFilerObj;
BEGIN
  IF cDebug THEN BEGIN
    WRITELN('ScrapFolder port:');
    PrintPort(scrapFolder);
   END;
  IF NOT IFFindObj(scrapFolder, scrapObj)
    THEN BEGIN
      WRITELN('Cant find scrap object!!!');
      { IMInitScrap; }
      scrapObj := hScrapFldr^^.fldrFOBj;
     END;
  IF cDebug THEN WRITELN('show scrap - scrap obj: ', ORD(scrapObj));
  IFOpenObj(scrapObj);
END;

{$s imsew }
PROCEDURE HideScrap;
VAR scrapObj: THFilerObj;
BEGIN
  IF NOT IFFindObj(scrapFolder, scrapObj)
    THEN BEGIN
      WRITELN('Cant find scrap object!!!');
      { IMInitScrap; }
      scrapObj := hScrapFldr^^.fldrFObj;
      IF cDebug THEN WRITELN('hide scrap - new scrap obj: ', ORD(scrapObj));
     END
    ELSE BEGIN
     IF cDebug THEN WRITELN('hide scrap - old scrap obj: ', ORD(scrapObj));
     IFCloseObj(scrapObj, TRUE);
    END;
END;


{$s imseh }
PROCEDURE SetupUndo; { (fUndoable: TF); }
BEGIN
  IF undoEnabled
    THEN BEGIN
      IF fUndoable
        THEN BEGIN
          SetAbleItem(EditMInfo, UndoMItem, TRUE);
          IF IMFldTst THEN WRITELN('Undo Edit item is disabled');
         END
        ELSE BEGIN
          SetAbleItem(EditMInfo, UndoMItem, FALSE);
          IF IMFldTst THEN WRITELN('Undo Edit item is disabled');
         END;
     END
    ELSE IF IMFldTst THEN WRITELN('Undo command not available');
END;


{$s imseh }
PROCEDURE RecordSelection;
VAR sFlag: TF;
    sLine: THImsLine;
    sData: txSel;
BEGIN
  IF fRInsertion OR fRSelections
     THEN BEGIN
       GetSelection(ActiveWSLink^^.kStream, sFlag, sLine, sData);
       IF sFlag
         THEN AddSelRec(curEvtBuf, sLine^^.lIdx, LineCCount(sLine),
                        LineChars(sLine), sData, hndActiveFldr, fAdjustSel);
      END;
END;


{$s imseh }
PROCEDURE SelectComplete;
BEGIN
  IF cDebug AND IMtst THEN WRITELN('Selection Complete');
  fInSelect := FALSE;
  fSelection := TRUE;
  IF fWSelections THEN WaitEnded;
  IF fRSelections OR fRInsertions THEN RecordSelection;
END;


{$s imseh }
PROCEDURE ClearSelection;
BEGIN
  IF cDebug AND IMtst THEN WRITELN('ClearSelection.  fSelection: ',
                        fSelection, ' fInSelect: ', fInSelect);
  IF fInSelect THEN SelectComplete;
  fSelection := FALSE;
  fAdjustSel := FALSE;
  selNbrhood := bigNbrhood;
  IF ActiveWSLink <> NIL
    THEN ExitSelection(ActiveWSLink^^.kStream);
  SetupUndo(FALSE);
  IF selTimeout <> -1
    THEN BEGIN
      IF cDebug AND IMtst
        THEN WRITELN('Resetting selTimeout from', selTimeout, ' to -1');
      selTimeout := -1;
    END;
END;


{$s imsew }
FUNCTION IMSSelectLine; { (wsLink: THWSLink; hSelLine: THImsLine;
               mloc, fldPt: Point, VAR nbrHd: Rect; VAR tout: TC): TF; }
VAR lstSelLine: THIMSLine;
    ferr: TC;
BEGIN
  IF IMFldTst THEN
    WRITELN('IMSSelectLine  at point. h:', mLoc.h:5, ' v:', mLoc.v:5,
        ' in line:', ORD(hSelLine), '  at point:', fldPt.h:6, fldPt.v:6, ')');
  SetPort(wsLink^^.kWindow^^.wPort);
  lstSelLine := wsLink^^.kStream^^.sField;
  IF hSelLine <> lstSelLine
    THEN BEGIN
      ExitSelection(wsLink^^.kstream);
      SetupUndo(FALSE);
      IF hSelLine^^.lType = CTxtLine
        THEN hSelLine := TxtToFldLine(wsLink^^.kStream, hSelLine);
     END
    ELSE BEGIN
      UnSelect(wsLink^^.kStream);
     END;
  IF hSelLine^^.lType <> CFldLine
    THEN BEGIN
      IF IMStst THEN
        WRITELN('IMSSelectLine line type not FldLine', ORD(hSelLine));
      IMSSelectLine := FALSE;
      EXIT(IMSSelectLine);
     END;
  hSelLine^^.fld.fSelOffset := fldPt;
  WITH hSelLine^^.fld DO
         OffsetRect(fhField^^.coords, fSelOffset.h, fSelOffset.v);
  hSelLine^^.fld.fActSel := TRUE;
  hSelLine^^.fld.fhFState :=
         POINTER(ORD(HAllocate(StrHeap, SIZEOF(fieldState))));
  WITH hSelLine^^.fld DO InitFState(fhField, fhFState);
  WITH hSelLine^^.fld DO Select(mloc, fhField, fhFState, nbrHd, tout);
  IF IMFldTst THEN WITH hSelLine^^ DO
    BEGIN WRITELN('Making selection in Line: ', ORD(hSelLine), ' idx:',
              lIdx, '  selOffset:', fld.fSelOffset.h:5, fld.fSelOffset.v:5);
    END;
  wsLink^^.kStream^^.sField := hSelLine;
  IMSSelectLine := TRUE;
END;


{$s imsew }
PROCEDURE IMSDragSelect; {(wsLink: THWSLink; mloc: Point; VAR nbrHd: Rect);}
VAR hFldLine: THIMSLine;
BEGIN
  IF ActSel(wsLink^^.kStream) THEN
    BEGIN
      IF IMFldTst
        THEN WRITELN('IMSDragSelect point. h:', mLoc.h, ' v:', mLoc.v);
      SetPort(wsLink^^.kWindow^^.wPort);
      hFldLine := wsLink^^.kStream^^.sField;
      WITH hFldLine^^.fld DO GrowSel(mloc, fhField, fhFState, nbrHd);
    END;
END;


{$s imsew }
FUNCTION SelectAtPos; { (wsLink: THWSLink; hSelLine: THImsLine;
                         fldPt: Point; selFst, selLim: TC; VAR tout: TC): TF; }
VAR lstSelLine: THIMSLine;
    selLen: TC;
    ferr: TC;
BEGIN
  IF IMFldTst THEN
    WRITELN('SelectAtPos  range from:', selFst:5, ' to:', selLim:5,
        ' in line:', ORD(hSelLine), '  at point:', fldPt.h:6, fldPt.v:6, ')');
  SetPort(wsLink^^.kWindow^^.wPort);
  lstSelLine := wsLink^^.kStream^^.sField;
  IF hSelLine <> lstSelLine
    THEN BEGIN
      ExitSelection(wsLink^^.kstream);
      SetupUndo(FALSE);
      IF hSelLine^^.lType = CTxtLine
        THEN hSelLine := TxtToFldLine(wsLink^^.kStream, hSelLine);
     END
    ELSE BEGIN
      UnSelect(wsLink^^.kStream);
     END;
  IF hSelLine^^.lType <> CFldLine
    THEN BEGIN
      IF IMStst THEN
        WRITELN('SelectAtPos line type not FldLine', ORD(hSelLine));
      SelectAtPos := FALSE;
      EXIT(SelectAtPos);
     END;
  selLen := LineCCount(hSelLine);
  IF selLen > selLim THEN selLim := selLen;
  IF selLen > selFst THEN selFst := selLen;
  hSelLine^^.fld.fSelOffset := fldPt;
  WITH hSelLine^^.fld DO
         OffsetRect(fhField^^.coords, fSelOffset.h, fSelOffset.v);
  hSelLine^^.fld.fActSel := TRUE;
  hSelLine^^.fld.fhFState :=
         POINTER(ORD(HAllocate(StrHeap, SIZEOF(fieldState))));
  WITH hSelLine^^.fld DO InitFState(fhField, fhFState);
  WITH hSelLine^^.fld DO ErrorSelect(fhField, fhFState, selFst, selLim, tout);
  IF IMFldTst THEN WITH hSelLine^^ DO
    BEGIN WRITELN('Making selection in Line: ', ORD(hSelLine), ' idx:',
              lIdx, '  selOffset:', fld.fSelOffset.h:5, fld.fSelOffset.v:5);
    END;
  wsLink^^.kStream^^.sField := hSelLine;
  SelectAtPos := TRUE;
END;


{$s imsew }
FUNCTION InsertNewLine(wsLink: THWSLink; hFldLine: THImsLine; VAR tout: TC): TF;
VAR newLine: THImsLine;
    newFldPt: Point;
    fIdx, fIndent: TC;
    oneFont: TF;
    blankstr: STRING[1];
    xrgch: TRgch;
    prtRect: Rect;
BEGIN
  SetPort(wsLink^^.kwindow^^.wPort);
  IF IMFldTst THEN WRITELN('InsertNewLine after:', ORD(hFldLine));
  WITH hFldLine^^.fld DO fIndent := fhField^^.coords.left - fSelOffset.h;
  fIDx := FontIndex(GetFldFont(hFldLine, oneFont));
  IF IMFldTst THEN WRITELN('NewLineindent = ', fIndent);
  WITH hFldLine^^ DO SetPt(newFldPt, fld.fSelOffset.h, fld.fSelOffset.v + lVDsp);
  UnSelect(wsLink^^.kStream);
  hFldLine := FldToTxtLine(wsLink^^.kStream, hFldLine);
  SetupUndo(FALSE);
  blankstr := ' ';
  xrgch := POINTER(ORD(@blankstr) + 1);
  WITH hFldLine^^ DO
    newLine := NewFldLine(wsLink^^.kStream, xrgch, 0, fIdx, lHt, lVDsp, fIndent);
  WindPt(wsLink^^.kWindow, newFldPt);
  InsertAfter(newLine, hFldLine);
  AddLnToPrtRect(wsLink^^.kStream, newLine);
  ShowStreamEnd(wsLink^^.kStream);
  prtRect := wsLink^^.kWindow^^.wDispRect;
  prtRect.top := newFldPt.v;
  ReDrawDWRect(wsLink, prtRect);
  IF IMFldTst THEN WRITELN('new line started at:', newFldPt.h, newFldPt.v);
  IF LnWindowPos(wsLink, newLine, newFldPt)
    THEN BEGIN
      IF IMFldTst THEN WRITELN('new line found at:', newFldPt.h, newFldPt.v);
      PortPt(wsLink^^.kWindow, newFldPt);
      InsertNewLine := SelectAtPos(wsLink, newLine, newFldPt, 0, 0, tout);
     END
    ELSE BEGIN
      IF IMFldTst THEN
         WRITELN('InsertNewLine - Cant find window pos of:', ORD(newLine));
      InsertNewLine := FALSE;
     END;
END;


{$s imsew }
FUNCTION IMSChar; {(wsLink: THWSLink; ch: CHAR; shifted: TF; VAR tout: TC): TF;}
VAR ferr: TC;
    hFldLine: THIMSLine;
    r: Rect;
    stillSelected: TF;
BEGIN
  stillSelected := FALSE;
  IF ActSel(wsLink^^.kStream)
    THEN BEGIN
      stillSelected := TRUE;
      IF IMFldTst THEN WRITELN('IMSChar. ch: ', ch, ' shifted: ', shifted);
      SetPort(wsLink^^.kWindow^^.wPort);
      hFldLine := wsLink^^.kStream^^.sField;
      CASE ORD(ch) OF
        chbs:
          BEGIN
            WITH hFldLine^^.fld DO CaretOn(fhField, fhFState);
            IF shifted
              THEN BEGIN
                WITH hFldLine^^.fld DO Forwardspace(fhField, fhFState, ferr);
                IF IMFldTst OR (ferr <> 0)
                  THEN WRITELN('Forwardspace error = ', ferr);
               END
              ELSE BEGIN
                WITH hFldLine^^.fld DO Backspace(fhField, fhFState, ferr);
                IF IMFldTst OR (ferr <> 0)
                  THEN WRITELN('Backspace error = ', ferr);
               END;
            SetupUndo(TRUE);
            tout := 0;
          END;
        chtab: BEGIN END;
        chret: IF wsLink^^.kStream^^.sCanAddLines
                 THEN BEGIN
                   stillSelected := InsertNewLine(wsLink, hFldLine, tout);
                   SetupUndo(FALSE);
                  END;
        OTHERWISE
          BEGIN
            WITH hFldLine^^.fld DO CaretOn(fhField, fhFState);
            WITH hFldLine^^.fld DO InsCh(ch, fhField, fhFState, ferr);
            r := hFldLine^^.fld.fhField^^.coords;
            r.right := r.right + 20;
            WITH hFldLine^^.fld DO SetFCoords(fhField, fhFState, r);
            IF IMFldTst THEN WITH hFldLine^^.fld DO PxField(fhField, fhFState);
            IF IMFldTst OR (ferr <> 0)
              THEN WRITELN('Char insert error: ', ferr);
            tout := 0;
            SetupUndo(TRUE);
          END;
      END;
     END
    ELSE IF IMFldTst THEN WRITELN('No selection point to insert char.');
  IMSChar := stillSelected;
END;


{$s imsew }
PROCEDURE DrawSelLine(wsLink: THWSLink; hFldLine: THIMSLine);
VAR fldIdx: TC;
    fldPt: Point;
BEGIN
  WITH wsLink^^.kWindow^^ DO
    BEGIN SetPort(wPort); ClipRect(wRect); END;
  IF NOT hFldLine^^.fld.fActSel
    THEN BEGIN
      WRITELN('DrawSelLine - line is not selected!!!', ORD(hFldLine));
      InterpError('Called DrawSelLine for line which is not selected');
      EXIT(DrawSelLine);
     END;
  WITH hFldLine^^.fld DO
    DrawField(fhField, fhFState, TRUE, TRUE)

  {fldIdx := FontIndex(hFldLine^^.fld.fhField^^.fmtInfo^^[0].font);
  {WITH hFldLine^^.fld.fhField^^ DO
  {  SetPt(fldPt, coords.left+drawPad, coords.top + FontBase(fldIdx)+1);
  {WITH hFldLine^^.fld DO DrawFldAt(fhField, fhFState, fldPt, TRUE, TRUE);
  {OffsetRect(hFldLine^^.fld.fhField^^.coords, 0, -1); {ugly hack }
    { try making the field a pixel higher }
END;


{$s imsew }
PROCEDURE IMSCut; {(wsLink: THWSLink, VAR errnum: TC);}
VAR hFldLine: THIMSLine;
    savePort: GrafPtr;
BEGIN
  { ShowScrap; }
  IF ActSel(wsLink^^.kStream)
    THEN BEGIN
      GetPort(savePort);
      SetPort(wsLink^^.kWindow^^.wPort);
      IF IMFldTst THEN WRITELN('IMSCut');
      hFldLine := wsLink^^.kStream^^.sField;
      WITH hFldLine^^.fld DO
{$IFC fOS} CutCopyField(fhField, fhFState, TRUE, TRUE, errnum);
{$ELSEC  } CutCopyField(fhField, fhFState, TRUE, TRUE, @GrowHeap, errnum);
{$ENDC   }
      DrawSelLine(wsLink, hFldLine);
      SetPort(savePort);
     END
    ELSE IF IMFldTst THEN WRITELN('No selection to cut.');
END;

{$s imsew }
PROCEDURE IMSCopy; {(wsLink: THWSLink, VAR errnum: TC);}
VAR hFldLine: THIMSLine;
    savePort: GrafPtr;
BEGIN
  { ShowScrap; }
  IF ActSel(wsLink^^.kStream)
    THEN BEGIN
      GetPort(savePort);
      SetPort(wsLink^^.kWindow^^.wPort);
      IF IMFldTst THEN WRITELN('IMSCut');
      hFldLine := wsLink^^.kStream^^.sField;
      WITH hFldLine^^.fld DO
{$IFC fOS} CutCopyField(fhField, fhFState, FALSE, TRUE, errnum);
{$ELSEC  } CutCopyField(fhField, fhFState, FALSE, TRUE, @GrowHeap, errnum);
{$ENDC   }
      DrawSelLine(wsLink, hFldLine);
      SetPort(savePort);
     END
    ELSE IF IMFldTst THEN WRITELN('No selection to copy.');
END;

{$s imsew }
PROCEDURE IMSPaste; {(wsLink: THWSLink, VAR errnum: TC);}
VAR hFldLine: THIMSLine;
    savePort: GrafPtr;
    fPt: Point;
    asc,desc: TC;
BEGIN
  IF ActSel(wsLink^^.kStream)
    THEN BEGIN
      GetPort(savePort);
      SetPort(wsLink^^.kWindow^^.wPort);
      IF IMFldTst THEN WRITELN('IMSCut');
      hFldLine := wsLink^^.kStream^^.sField;
      IF hFldLine^^.fld.fhField^^.maxFmts > 1
        THEN WITH hFldLine^^.fld DO
          PasteField(fhField, fhFState, errnum, TRUE,100 { ??})
        ELSE WITH hFldLine^^.fld DO
          PasteField(fhField, fhFState, errnum, FALSE, 100 { ??});
      WITH hFldLine^^.fld DO BEGIN  { adjust field rect size bug IM46?? }
        VertCoords(fhField, asc, desc);
        SetPt(fPt, fhField^^.coords.topLeft.h+ fhField^^.drawPad,
                   fhField^^.coords.botRight.v-desc);
        MoveField(fhField, fhFState, fPt);
       END;
      DrawSelLine(wsLink, hFldLine);
      SetPort(savePort);
     END
    ELSE IF IMFldTst THEN WRITELN('No selection to paste into.');
END;

{$s imsew }
PROCEDURE IMSUndo; {(wsLink: THWSLink; VAR errnum: TC);}
VAR hFldLine: THIMSLine;
    savePort: GrafPtr;
BEGIN
  IF ActSel(wsLink^^.kStream)
    THEN BEGIN
      GetPort(savePort);
      SetPort(wsLink^^.kWindow^^.wPort);
      IF IMFldTst THEN WRITELN('IMSCut');
      hFldLine := wsLink^^.kStream^^.sField;
      WITH hFldLine^^.fld DO UndoEdit(fhField, fhFState, errnum);
      DrawSelLine(wsLink, hFldLine);
      SetPort(savePort);
     END
    ELSE IF IMFldTst THEN WRITELN('No selection n which to undo.');
END;

{$s imsew }
FUNCTION IMSDouble;
     {(wsLink: THWSLink; mloc: Point; VAR nbhd: Rect; VAR tout: TC): TF;}
VAR hFldLine: THIMSLine;
    savePort: GrafPtr;
BEGIN
  IF ActSel(wsLink^^.kStream)
    THEN BEGIN
      GetPort(savePort);
      SetPort(wsLink^^.kWindow^^.wPort);
      IF IMFldTst THEN WRITELN('IMSDouble - selecting word');
      hFldLine := wsLink^^.kStream^^.sField;
      WITH hFldLine^^.fld DO SelectWord(mloc, fhField, fhFState, nbhd, tout);
      SetPort(savePort);
      IMSDouble := TRUE;
     END
    ELSE IMSDouble := FALSE;
END;

{$s imsew }
FUNCTION IMSTriple; {(wsLink: THWSLink; VAR nbhd: Rect; VAR tout: TC): TF;}
VAR hFldLine: THIMSLine;
    savePort: GrafPtr;
BEGIN
  IF ActSel(wsLink^^.kStream)
    THEN BEGIN
      GetPort(savePort);
      SetPort(wsLink^^.kWindow^^.wPort);
      IF IMFldTst THEN WRITELN('IMSTriple - selecting whole line');
      hFldLine := wsLink^^.kStream^^.sField;
      WITH hFldLine^^.fld DO ExitField(fhField, fhFState);
      WITH hFldLine^^.fld DO SelectAll(fhField, fhFState, tout);
      SetPort(savePort);
      IMSTriple := TRUE;
     END
    ELSE IMSTriple := FALSE;
END;

{$s imsew }
FUNCTION IMSButtonUp; {(wsLink: THWSLink; VAR tout: TC): TF;}
VAR hFldLine: THIMSLine;
    savePort: GrafPtr;
BEGIN
  IF ActSel(wsLink^^.kStream)
    THEN BEGIN
      GetPort(savePort);
      SetPort(wsLink^^.kWindow^^.wPort);
      IF IMFldTst THEN WRITELN('IMSButtonUp');
      hFldLine := wsLink^^.kStream^^.sField;
      StartBlink(tout);
      SetPort(savePort);
      IMSButtonUp := TRUE;
    END
   ELSE IMSButtonUp := FALSE;
END;

{$s imsew }
PROCEDURE IMSTimeout; {(wsLink: THWSLink; VAR tout: TC);}
VAR hFldLine: THIMSLine;
    savePort: GrafPtr;
BEGIN
  IF ActSel(wsLink^^.kStream)
    THEN BEGIN
      GetPort(savePort);
      SetPort(wsLink^^.kWindow^^.wPort);
      hFldLine := wsLink^^.kStream^^.sField;
      IF NOT hFldLine^^.fld.fhFState^^.valid
        THEN BEGIN
          IF IMFldTst
            THEN WRITELN('IMSTimeout - redrawing and starting caret blink');
          DrawSelLine(wsLink, hFldLine);
          StartBlink(tout);
         END
        ELSE BEGIN
          WITH hFldLine^^.fld DO BlinkCaret(fhField, fhFState, tout);
          IF IMFldTst THEN WRITE('<to>');
         END;
      SetPort(savePort);
     END;
END;

{$s imsew }
PROCEDURE GetSelection; { (IMSobj: THIMS; VAR sFlag: TF; VAR sLine: THImsLine;
                           VAR sData: txSel); }
VAR hFldLine: THIMSLine;
BEGIN
  IF IMFldTst THEN WRITELN('GetSelection');
  sFlag := FALSE;
  sLine := NIL;
  IF ActSel(IMSobj)
    THEN BEGIN
      hFldLine := IMSobj^^.sField;
      sFlag := TRUE;
      sLine := hFldLine;
      sData := hFldLine^^.fld.fhFState^^.select;
     END;
END;


END.

ÿ