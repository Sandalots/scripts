{******************************************************************************}
{*                                                                            *}
{*       LisaGuide - imlib unit : Copyright 1983, Apple Computer Inc.         *}
{*                                                                            *}
{******************************************************************************}


   { this unit contains all the global declarations for the filer }

UNIT IFGlobals;


   {

   $IFC R  ... ENDC surrounds Real-filer code the Tutor doesn't want
   The prefix "Flr" => "TF" (Tutor Filer) in file names & other identifiers

   Conventions used:

      "{"  is used for comments.
      "(*" is used to comment out code that may contain comments.

      "{*** comment"   => more work needed in this area.

      Many routines are named:

          <verb>Object    =>  action is applied to the object & optionally to sons
          <verb>Contents  =>  action is applied to contents (sons) of the object

      All routines are declared in alphabetical order.
   }
{$DECL R}
{$SETC R:=FALSE}


INTERFACE



USES
   {$U UnitStd    } UnitStd,
   {$U UnitHz     } UnitHz,
   {$U Storage    } Storage,
   {$U QuickDraw  } QuickDraw,
   {$U FontMgr    } FontMgr,
   {$U libos/SysCall    } SysCall,
   {$U libos/PSysCall   } PSysCall,
   {$U PmDecl     } PmDecl,
   {$U PMM        } PMM,
   {$U WM.Events  } Events,
   {$U WM.Menus   } Menus,
   {$U AlertMgr   } AlertMgr,
   {$U WmlStd     } WmlStd,
   {$U WmlSb      } WmlSb;


CONST

{$SETC filerDebug := fDbgOK }  { Set to FALSE for release software        }

{$IFC fSymOK}   { system symbol flag init }
  {$D+}
{$ELSEC }
  {$D-}
{$ENDC }


      { tracing flags }

{$IFC filerDebug}
   dbgFiler  = TRUE;   { For user non-switchable debugging output }
{$ELSEC }                      { Set to FALSE for release software        }
   dbgFiler  = FALSE;
{$ENDC }

{$IFC NOT filerDebug }

   trcFiler         = FALSE;
   trcCatalog       = FALSE;
   trcEvents        = FALSE;
   trcFDocCtrl      = FALSE;
   trcFEntry        = FALSE;
   trcFVolCtrl      = FALSE;
   wantCheckHeap    = FALSE;

{$ENDC }


      { LDSN (logical data segment number) assignments }

   flrHeapLDSN    = 1;    { theHeap for filer structures }
   dBLDSN         = 4;    { DB heap & buffers }
   dfCopyLDSN     = 6;    { DocCtrl ds used to copy diskfiles, bound only during copy }


      { screen sizes }

   screenWidth    = 720;
   screenHt       = 364;

      { misc. constants }

   fMaxStrLen     = 63;      { max string size for catalog names }
   bell           = 7;       { ASCII "beep" }
   nullErr        = MAXINT;  { used w/ SysErr to indicate no low level err }

      { logging constants - for creating Filer system.log entries }

   flrEvType      = 100;     { Identifies a Filer generated log entry }

   opOpenDoc      = 1;       { User actions }
   opPutBackDoc   = 2;
   opSuspendDoc   = 11;
   opCopyDoc      = 3;
   opMoveDoc      = 4;
   opPowerDown    = 5;
   opTwigBackup   = 6;
   opProBackup    = 7;
   opDiskBackup   = 8;
   opBootDismount = 9;
   opBootRemount  = 10;
   opOpenContainer= 12;
   opCloseContainer=13;


TYPE

   {  the following types are compensation for typer removed for spring  }
   Tports = (uppertwig, lowertwig, parallel, slot11, slot12, slot13, slot14,
             slot21, slot22, slot23, slot24, slot31, slot32, slot33, slot34,
             seriala, serialb, main_console, alt_console, t_mouse, t_speaker,
             hard_disk, micro_disk, t_extra3);
   Tdevicetype = 0..255 ;
   {  spring types to here  }

      { misc types }

   PtrString     = ^Str255;
   CharArray     =  PACKED ARRAY [1..MAXINT] OF CHAR;
   PtrCharArray  = ^CharArray;
   String80      =  STRING[80];
   NumberStr     =  STRING[11];   { large enough for - max LongInt }


      { catalog types }

   FmaxStr       =  STRING[fMaxStrLen];
   FmaxStrPtr    = ^FmaxStr;
   FmaxStrHdl    = ^FmaxStrPtr;


     { token for catalog records }

   IDType = LONGINT;

   TcatRID =
     RECORD
     fatherId : IdType;   { parent Id.  If given, improves access time }
     uniqueId : IdType;   { the actual identifier.  Unique on a volume }
     END;




VAR

   {$IFC filerDebug }

      { tracing flags }

   wantCheckHeap,
   trcDB,
   trcFiler,
   trcCatalog,
   trcEvents,
   trcFDocCtrl,
   trcFEntry,
   trcFVolCtrl : BOOLEAN;

   {$ENDC }


      { misc. globals }

   curEvent     : EventRecord; { last event received }
   alertRefnum  : INTEGER;     { OS file refnum for the phrase file }
   busy         : BOOLEAN;     {*** partial fix to keep Filer awake while proc terminates ***}
   quitFiler    : BOOLEAN;     { TRUE => exit program, set by menu command }
   flrAlert     : TAlertFile;  { Set by ReadAlerts in Initialize }
   termEventRefnum: INTEGER;   { for sys_son_term event }
   catVolName   : e_name;      { vol name of dummy catalog files }


PROCEDURE ArrayToStr(pArray : PtrCharArray; numChars : INTEGER; pStr : PtrString);
PROCEDURE ClipScrollBars(window: WindowPtr; setClipping: BOOLEAN);
PROCEDURE IntToStr(number : LongInt; VAR strg : NumberStr);
FUNCTION  Max(a,b : INTEGER) : INTEGER;
FUNCTION  Min(a,b : INTEGER) : INTEGER;
PROCEDURE StrToArray(pStr : PtrString; pArray : PtrCharArray; arraySize : INTEGER);
PROCEDURE StrToInt(str : E_Name; start : INTEGER;
                   VAR number : LongInt; VAR numDigits : INTEGER);
PROCEDURE SysErr(err: INTEGER; lowlvlerr: INTEGER);

{#############################################################################}

IMPLEMENTATION
{$S Filer }

{$IFC fDbgOk }
PROCEDURE TDbgOk;
BEGIN
END;
{$ENDC }

{$IFC fSymOk }
PROCEDURE TSymOk;
BEGIN
END;
{$ENDC }

{$IFC filerDebug }
PROCEDURE TFilerDebug;
BEGIN
END;
{$ENDC }



{-----------------------------------------------------------------------------}
{  ArrayToStr - moves chars into string and sets count                        }
{-----------------------------------------------------------------------------}
PROCEDURE ArrayToStr{pArray : PtrCharArray; numChars : INTEGER; pStr : PtrString};

VAR
   i : INTEGER;

BEGIN
FOR i := 1 TO numChars DO
   pStr^[i] := pArray^[i];

{$R-}
pStr^[0] := CHR(numChars);
{$R+}
END;


{-----------------------------------------------------------------------------}
{  ClipScrollBars                                                             }
{-----------------------------------------------------------------------------}
PROCEDURE ClipScrollBars{* window: WindowPtr; setClipping: BOOLEAN *};
{ Sets or clears clipping on scroll bars }
BEGIN
WITH window^ DO
  IF setClipping THEN WITH portRect DO
     SetRectRgn(clipRgn,left,top,right-dhSBox+1,bottom-dvSBox+1)
  ELSE
     RectRgn(clipRgn,portRect);
END;



{-----------------------------------------------------------------------------}
{  IntToStr - converts an integer into a string                               }
{-----------------------------------------------------------------------------}
PROCEDURE IntToStr{ number : LongInt; VAR strg : NumberStr };

VAR
   i,
   numDigits : INTEGER;
   negative  : BOOLEAN;
   bkwdStrg  : NumberStr;

BEGIN
negative := (number < 0);
numDigits := 0;

REPEAT
   numDigits := numDigits + 1;
   bkwdStrg[numDigits] := CHR(ORD('0') + ABS(number MOD 10));
   number := number DIV 10
UNTIL number = 0;

IF negative THEN
   BEGIN
   numDigits := numDigits + 1;
   bkwdStrg[numDigits] := '-';
   END;

FOR i := 1 TO numDigits DO
   strg[i] := bkwdStrg[numDigits-i+1];

{$R-}
strg[0] := CHR(numDigits);
{$R+}
END;

{-----------------------------------------------------------------------------}
{  Max - returns maximum of 2 integers                                        }
{-----------------------------------------------------------------------------}
FUNCTION Max(* a,b: INTEGER): INTEGER *);
BEGIN
  IF a > b THEN Max:=a ELSE Max:=b;
END;

{-----------------------------------------------------------------------------}
{  Min - returns minimum of 2 integers                                        }
{-----------------------------------------------------------------------------}
FUNCTION Min(* a,b : INTEGER) : INTEGER *);

BEGIN
IF a <= b THEN Min := a ELSE Min := b;
END;


{-----------------------------------------------------------------------------}
{  StrToArray - moves string into array                                       }
{-----------------------------------------------------------------------------}
PROCEDURE StrToArray{pStr : PtrString; pArray : PtrCharArray;
                     arraySize : INTEGER};

VAR
   moveCnt,
   i : INTEGER;

BEGIN
moveCnt := MIN(arraySize,LENGTH(pStr^));
FOR i := 1 TO moveCnt DO
   pArray^[i] := pStr^[i];
FOR i := moveCnt+1 TO arraySize DO
   pArray^[i] := ' ';
END;


{-----------------------------------------------------------------------------}
{  StrToInt - converts string to an integer                                   }
{-----------------------------------------------------------------------------}
PROCEDURE StrToInt{str : E_Name; start : INTEGER;
                   VAR number : LongInt; VAR numDigits : INTEGER};
   { converts string begining at 'start' to a LongInt }
VAR
   index : INTEGER;
   endOfNum : BOOLEAN;
BEGIN
IF trcFiler THEN WRITELN('StrToInt  str = "',str,'"  start = ',start:1);
number := 0;
index := start;
endOfNum := FALSE;
WHILE (index <= LENGTH(str)) AND (NOT endOfNum) DO
   BEGIN
   IF str[index] IN ['0'..'9'] THEN
      BEGIN
      number := 10*number + (ORD(str[index]) - ORD('0'));
      index := index + 1;
      END
   ELSE
      endOfNum := TRUE;
   END;
numDigits := index - start;
IF trcFiler THEN WRITELN('   exit StrToInt.  number = ',number:1);
END;


{-----------------------------------------------------------------------------}
{  SysErr - reports error msg and terminates Filer                            }
{-----------------------------------------------------------------------------}

PROCEDURE SysErr{err: INTEGER; lowlvlerr: INTEGER};

{ Called when the Filer has found an unexpected condition. 'err' denotes the
  calling module and a specific call within that module. 'lowlvlerr' is an
  error passed up to the Filer from low-level software e.g. the OS. If desired,
  SysErr can be called to check for lowlvlerr=0 which will cause SysErr to do
  nothing. If there is no low level error, use 'nullErr'.

  The system informs the user of the error via the alert box, waits for the user
  to respond, and then reboots the system. Optionally the user can be given the
  choice of proceeding, i.e., SysErr returns to the caller, instead or a reboot.
  This option is enabled by -err, i.e., negative errs are warnings.

  The error ranges for each module are:

  FGlobals       100-199
  FEntry         200-299
  Catalog        300-399
  Buttons        400-499
  Panels         500-599
  Orphans        600-699
  Choices        700-799
  FDocCtrl       800-899
  FVolCtrl       900-999
  Filer         1000-1099
}

VAR
    response:  FMaxStr;
    tempStr:   NumberStr;
    errStr:    STRING[5];
    lowLvlStr: STRING[5];

BEGIN
IF lowlvlerr < 0 THEN WRITELN ('   warning: ',lowlvlerr:1);
IF lowlvlerr <= 0 THEN EXIT (SysErr);

IntToStr (ABS(err), tempStr);            { Build alert msg params           }
errStr := tempStr;
IF lowlvlerr <> nullErr
THEN BEGIN
  IntToStr (lowlvlerr, tempStr);
  lowlvlstr := CONCAT('/',tempStr);
  END
ELSE lowlvlstr := '';
ParamAlert (errStr, lowlvlstr, '');

IF err < 0
THEN BEGIN
  IF CautionAlert (flrAlert, 201)
  THEN EXIT (SysErr)
  ELSE HALT;
  END
ELSE BEGIN
  IF CautionAlert (flrAlert, 201){*** change to StopAlert(..., 200) for release ***}
  THEN EXIT (SysErr)
  ELSE HALT;
  END
END;


END.

ÿ