{******************************************************************************}
{*                                                                            *}
{*              LisaGuide : Copyright 1983, Apple Computer Inc.               *}
{*                                                                            *}
{******************************************************************************}



{$s immc }
{$r- }

UNIT IMMenus;


INTERFACE

USES {$U HwInt        }  HwInt,
     {$U libos/SysCall  }  SysCall,
     {$U libos/PSysCall }  PSysCall,
     {$U UnitStd.obj  }  UnitStd,
     {$U UnitHz.obj   }  UnitHz,
     {$U Storage.obj  }  Storage,
     {$U QuickDraw    }  QuickDraw,
     {$U FontMgr.obj  }  FontMgr,
     {$U PmDecl       }  PmDecl,
     {$U PMM          }  PMM,
     {$U FMgrUtil.obj }  FmgrUtil,
     {$U WM.Events    }  Events,
     {$U WM.Folders   }  Folders,
     {$U WM.Menus     }  Menus,
     {$U WMLStd       }  WMLStd,
     {$U WMLSB        }  WMLSb,
     {$U WMLGrow      }  WMLGrow,
     {$U AlertMgr.obj }  AlertMgr,
     {$U dbenv.obj    }  dbenv,
     {$U DBdecl1      }  DBdecl1,
     {$U fedec.obj    }  fedec,
     {$U fld.obj      }  FieldEdit,
     {$U apim/UnitImf.obj  }  UnitImf,
     {$U apim/imcore.obj   }  IMCore;


CONST
  CMenuItemHeight = 15;

VAR
  blankStr:      STRING[1];

PROCEDURE DropMenus(pmlist: TPMenus);
PROCEDURE LoadMenuBar(fldr: THFldrInfo);
FUNCTION  NewMenu(newTitle: TMStr; activeFlag, dispFlag: TF;
                    barInf, nameInf, lookupInf: TPMenuInfo): THMenuDinf;
PROCEDURE AddFldrMenu(fldr: THFldrInfo; hMenuInf: THMenuDInf; fldrDFlag: TF);
FUNCTION  GetIDMenu(mID: TC): THMenuDInf;
FUNCTION  PutMenuFldr(mName: TKeyStr; fldr: THFldrInfo): TF;
FUNCTION  SetDspMenu(mName: TKeyStr; dFlag: TF): TF;
FUNCTION  SetActItem(mName, iName: TKeyStr; aFlag: TF): TF;
FUNCTION  SetDspItem(mName, iName: TKeyStr; dFlag: TF): TF;
PROCEDURE IMCheckItem(hMenuInf: THMenuDInf; itm: TC; aFlag: TF);
PROCEDURE SetAbleItem(hMenuInf: THMenuDInf; itm: TC; aFlag: TF);



IMPLEMENTATION

{$S immc }
PROCEDURE DropMenus; { (pmlist: TPMenus); }
VAR mlink, nxtMLink: THMenuLink;
BEGIN
  WITH pmlist^ DO
    BEGIN
      mLink := firstMenu;
      firstmenu := NIL;
      lastmenu := NIL;
      menuCount := 0;
     END;
  WHILE mlink <> NIL DO
    BEGIN
      nxtMLink := mLink^^.nextMenu;
      FreeBk(IMHeap, POINTER(ORD(mLink)), tyBkStd);
      mLink := nxtMLink;
    END;
END;


{$S immw }
PROCEDURE LoadMenuBar; {(fldr: THFldrInfo); }
VAR hMLink: THMenuLink;
    mIdx,iIdx, barPos: TC;
BEGIN
  IF IMMTst THEN WRITELN('LoadMenuBar for folder:', ORD(fldr));
  IF fldr = NIL THEN EXIT(LoadMenuBar);
  SetPort(menuFolder);  {hack to fix menu font bug!!! -gb 11/19/82}
  TextFont(sysText);
  SetPort(fldr^^.fldrPort);  {set the port for the right  window???? }
IF thePort = menuFolder THEN BEGIN
  WRITELN('Changing font of menu folder!!!'); Holdit(sDebug); END;
  TextFont(sysText);
  IF fldr = hndActiveFldr THEN ClearMenuBar;
  hMLink := fldr^^.fldrMenuList.FirstMenu;
  WHILE hMLink <> NIL DO
    BEGIN
      IF hMLink^^.fldrMDispFlag
         THEN BEGIN
            IF IMMTst THEN
              WRITELN('Inserting name menu ', hMLink^^.menuInfo^^.mTitle,
                       ORD(hMLink^^.menuInfo));
            InsertMenu(hMLink^^.menuInfo^^.mNameInfo^, 0);
          END
       ELSE IF hMLink^^.menuInfo^^.mDispFlag
         THEN BEGIN
            IF IMMTst THEN
              WRITELN('Inserting bar menu ', hMLink^^.menuInfo^^.mTitle,
                       ORD(hMLink^^.menuInfo));
            InsertMenu(hMLink^^.menuInfo^^.mBarInfo^, 0);
          END;
      hMLink := hMLink^^.nextMenu;
    END;
  IF fldr = hndActiveFldr THEN DrawMenuBar;
END;


{$S immw }
PROCEDURE AddMList(pmlist: TPMenus; hMenuInf: THMenuDInf; fldrDFlag: TF);
VAR newMLink: THMenuLink;
BEGIN
  newMLink := POINTER(ORD(HAllocate(IMHeap, SIZEOF(TMenuLink))));
  newMLink^^.fldrMDispFlag := fldrDFlag;
  newMLink^^.menuInfo := hMenuInf;
  newMLink^^.nextMenu := NIL;
  WITH pmlist^ DO
    BEGIN
      IF firstMenu = NIL
         THEN firstMenu := newMLink
         ELSE lastMenu^^.nextMenu := newMLink;
      lastMenu := newMLink;
      menuCount := menuCount + 1;
      IF IMMTst THEN WITH newMLink^^.menuInfo^^ DO
        BEGIN
          WRITELN('Adding menu', menuCount:3, '(', mTitle, mNumber, ')',
                              ' to menulist ', ORD(pmlist));
          WRITELN('   menuLink:', ORD(newMLink));
         END;
    END;
END;


{$S immini }
FUNCTION NewMenu; {(newTitle: TMStr; activeFlag, dispFlag: TF;
                    barInf, nameInf, lookupInf: TPMenuInfo): THMenuDinf; }
{ this loads 3 menuinfo's for each menu. storage could be saved by using only
  one menuinfo for any menu which will not be changed by the IM script  }
VAR hMenuInf: THMenuDInf;
BEGIN
  blankStr := '.';  { really a one time init, but this will do fine }
  hMenuInf := POINTER(ORD(HAllocate(IMHeap, SIZEOF(TMenuDInf))));
  WITH hMenuInf^^ DO
    BEGIN
      mBarInfo := barInf;
      mNameInfo := nameInf;
      mLookupInfo := lookupInf;
      mTitle := newTitle; { should get this from mNameInfo }
      mNumber := mBarInfo^.menuId;
      mCItems := mNameInfo^.menuHeight DIV CMenuItemHeight;
      mDispFlag := dispFlag;
      mActiveFlag := activeFlag;
      AddMList(@MasterMenuList, hMenuInf, FALSE);
    END;
  NewMenu := hMenuInf;
END;


{$S immc }
PROCEDURE AddFldrMenu; {(fldr: THFldrInfo; hMenuInf: THMenuDInf; fldrDFlag: TF); }
BEGIN
  IF fldr = NIL
    THEN BEGIN
      IF IMTst OR IMMTst THEN WRITELN('Null folder in AddFldrMenu! Ignored.');
      EXIT(AddFldrMenu);
     END;
  AddMList(@fldr^^.fldrMenuList, hMenuInf, fldrDFlag);
  IF IMMTst THEN WITH hMenuInf^^ DO
    BEGIN
      WRITELN('Adding menu', fldr^^.fldrMenuList.menuCount:3,
              '(', mTitle, mNumber, ')', ' to folder ', fldr^^.fldrName);
      WRITELN('   mActiveFlag ', mActiveFlag, '  mDispFlag: ', mDispFlag);
     END;
END;


{$S immh }
FUNCTION GetIDMenu; { (mID: TC): THMenuDInf; }
LABEL 900;
VAR hMLink: THMenuLink;
BEGIN
  { IF IMMTst THEN WRITELN('GetIDMenu:', mID); }
  hMLink := MasterMenuList.FirstMenu;
  WHILE hMLink <> NIL DO
    BEGIN
      {IF IMMTst THEN
      {  WRITELN('GetIDMenu:', mID, ' trying: ', hMLink^^.menuInfo^^.mTitle);}
      IF hMLink^^.menuInfo^^.mNumber = mID THEN GOTO 900;
      hMLink := hMLink^^.nextMenu;
    END;
900:  GetIDMenu := hMLink^^.menuInfo;
      IF IMMTst THEN IF hMLink <> NIL
        THEN WRITELN('GetIDMenu:', mID, ' returns: ', hMLink^^.menuInfo^^.mTitle)
        ELSE WRITELN('GetIDMenu:', mID, ' cant find menu.');
END;


{$S immh }
FUNCTION GetMenu(mName: TKeyStr): THMenuDInf;
LABEL 900;
VAR hMLink: THMenuLink;
BEGIN
  IF IMMTst THEN WRITELN('GetMenu:', mName);
  hMLink := MasterMenuList.FirstMenu;
  WHILE hMLink <> NIL DO
    BEGIN
      IF hMLink^^.menuInfo^^.mTitle = mName THEN GOTO 900;
      hMLink := hMLink^^.nextMenu;
    END;
900:
  IF hMLink = NIL
    THEN BEGIN
      GetMenu := NIL;
      IF IMMTst THEN WRITELN('GetMenu: ', mName, ' - Menu not found');
     END
    ELSE BEGIN
      GetMenu := hMLink^^.menuInfo;
      IF IMMTst THEN WRITELN('GetMenu returns:', ORD(hMLink^^.menuInfo));
     END;
END;


{$S immc }
FUNCTION PutMenuFldr; {(mName: TKeyStr; fldr: THFldrInfo): TF; }
LABEL 900;
VAR hMenuInf: THMenuDInf;
BEGIN
  IF IMMtst THEN
    WRITELN('PutMenuFldr(', mName, ', ', ORD(fldr), ');');
  hMenuInf := GetMenu(mName);
  IF hMenuInf = NIL
    THEN BEGIN
       PutMenuFldr := FALSE;
       IF IMMtst THEN WRITELN('Menu not found, item cant be put in folder');
       GOTO 900;
     END;
  IF IMMTst THEN WRITELN('Found menu: ', mName, ' handle:', ORD(hMenuInf));
  AddFldrMenu(fldr, hMenuInf, FALSE);
  PutMenuFldr := TRUE;
900:
END;


{$S immh }
FUNCTION GetItemIdx(hMenuInf: THMenuDInf; iName: TKeyStr): TC;
LABEL 900;
VAR iidx: TC;
    istr: Str255;
BEGIN
  IF IMMTst THEN
    WRITELN('GetItemIdx: menu - ', hMenuInf^^.mTitle, ' item - ', iName);
  FOR iidx := 1 TO hMenuInf^^.mCItems DO BEGIN
    GetItem(hMenuInf^^.mLookupInfo^, iidx, @istr);
    IF IMMTst THEN WRITELN('  item idx:', iidx:4, ' "', istr, '"');
    IF LENGTH(istr) > CKeySize
      THEN BEGIN
        istr[0] := CHR(CKeySize);
        IF IMMtst THEN
          WRITELN('item string shortened to "', istr, '"');
       END;
    IF istr = iName THEN GOTO 900;
   END;
  iidx := 0;
900:  GetItemIdx := iidx;
  IF IMMTst THEN WRITELN('GetItemIdx returns:', iidx:4);
END;


{$S immw }
PROCEDURE DisplayItem(hMenuInf: THMenuDInf; itm: TC; aFlag: TF);
VAR istr: TMStr; { ItemStr ? }
BEGIN
  IF aFlag
    THEN BEGIN
      GetItem(hMenuInf^^.mNameInfo^, itm, @istr);
      IF IMMTst
        THEN WRITELN('Displaying menu ', hMenuInf^^.mTitle,
                     ' item:', itm:3, '(', istr, ')');
      SetItem(hMenuInf^^.mBarInfo^, itm, @istr);
      IF IMMtst THEN WRITELN('Item successfully displayed');
     END
    ELSE WITH hMenuInf^^ DO BEGIN
      IF IMMTst THEN WRITELN('Blanking menu ', mTitle, ' item:', itm:3);
      DisableItem(hMenuInf^^.mBarInfo^, itm);
      IF mBarInfo <> mNameInfo
        THEN BEGIN
          SetItem(hMenuInf^^.mBarInfo^, itm, @blankStr);
          IF IMMtst THEN WRITELN('Item successfully blanked');
         END
        ELSE IF IMMTst THEN
         WRITELN('Menu Item not blanked because name not saved in menuName info');
     END;
END;


{$S immh }
PROCEDURE IMCheckItem; { (hMenuInf: THMenuDInf; itm: TC; aFlag: TF); }
BEGIN
  WITH hMenuInf^^ DO BEGIN
    CheckItem(mBarInfo^, itm, aFlag);
    IF mBarInfo <> mNameInfo THEN CheckItem(mNameInfo^, itm, aFlag);
    IF IMMTst THEN
      WRITELN('Checking menu item', mTitle, ' item:', itm:3, ' ', aFlag);
   END;
END;


{$S immw }
PROCEDURE SetAbleItem; { (hMenuInf: THMenuDInf; itm: TC; aFlag: TF); }
BEGIN
  IF aFlag
    THEN BEGIN
      EnableItem(hMenuInf^^.mBarInfo^, itm);
      IF IMMTst
        THEN WRITELN('Enabling menu ', hMenuInf^^.mTitle,
                     ' item:', itm:3, ORD(hMenuInf));
      DisplayItem(hMenuInf, itm, TRUE);
     END
    ELSE BEGIN
      DisableItem(hMenuInf^^.mBarInfo^, itm);
      IF IMMTst
        THEN WRITELN('Disabling menu ', hMenuInf^^.mTitle,
                     ' item:', itm:3, ORD(hMenuInf));
     END;
END;


{$S immw }
FUNCTION SetDspMenu; { (mName: TKeyStr; dFlag: TF): TF; }
LABEL 900;
VAR hMenuInf: THMenuDInf;
BEGIN
  IF IMMtst THEN
    WRITELN('SetDspMenu(', mName, ', ', dFlag, ');');
  hMenuInf := GetMenu(mName);
  IF hMenuInf = NIL
    THEN BEGIN
       SetDspMenu := FALSE;
       IF IMMtst THEN WRITELN('Menu not found, item cant be activated');
       GOTO 900;
     END;
  IF IMMTst THEN WRITELN('Found menu: ', mName, ' handle:', ORD(hMenuInf));
  hMenuInf^^.mDispFlag := dFlag;
  SetDspMenu := TRUE;
  IF IMMTst THEN WRITELN('  mDispFlag set to:', dFlag);
900:
END;



{$S immw }
FUNCTION SetDspItem; { (mName, iName: TKeyStr; dFlag: TF): TF; }
LABEL 900;
VAR hMenuInf: THMenuDInf;
    itm: TC;
BEGIN
  IF IMMtst THEN
    WRITELN('SetDspItem(', mName, ', ', iName, ', ', dFlag, ');');
  hMenuInf := GetMenu(mName);
  IF hMenuInf = NIL
    THEN BEGIN
       SetDspItem := FALSE;
       IF IMMtst THEN WRITELN('Menu not found, item cant be activated');
       GOTO 900;
     END;
  IF IMMTst THEN WRITELN('Found menu: ', mName, ' handle:', ORD(hMenuInf));
  itm := GetItemIdx(hMenuInf, iName);
  IF itm = 0
    THEN BEGIN
      SetDspItem := FALSE;
      IF IMMtst THEN WRITELN('SetDspItem - Item not found, cant be activated');
      GOTO 900;
    END;
  DisplayItem(hMenuInf, itm, dFlag);
  SetDspItem := TRUE;
900:
END;


{$S immw }
FUNCTION SetActItem; { (mName, iName: TKeyStr; aFlag: TF): TF; }
LABEL 900;
VAR hMenuInf: THMenuDInf;
    itm: TC;
BEGIN
  IF IMMtst THEN
    WRITELN('SetDspItem(', mName, ', ', iName, ', ', aFlag, ');');
  hMenuInf := GetMenu(mName);
  IF hMenuInf = NIL
    THEN BEGIN
       SetActItem := FALSE;
       IF IMMtst THEN WRITELN('Menu not found, item cant be activated');
       GOTO 900;
     END;
  IF IMMTst THEN WRITELN('Found menu: ', mName, ' handle:', ORD(hMenuInf));
  itm := GetItemIdx(hMenuInf, iName);
  IF itm = 0
    THEN BEGIN
      SetActItem := FALSE;
      IF IMMtst THEN WRITELN('SetActItem - Item not found, cant be activated');
      GOTO 900;
    END;
  SetAbleItem(hMenuInf, itm, aFlag);
  IF (hMenuInf = EditMInfo) AND (itm = UndoMItem)
     THEN UndoEnabled := aFlag;
     { remembers when undo enabled by name rather than by number. }
  SetActItem := TRUE;
900:
END;


END.



ÿ