
{******************************************************************************}
{*                                                                            *}
{*              LisaGuide : Copyright 1983, Apple Computer Inc.               *}
{*                                                                            *}
{******************************************************************************}


{$S imfc }
UNIT UnitIMF;


{$r-}

INTERFACE

USES {$U UnitStd.obj}   UnitStd,
     {$U UnitHz.obj}    UnitHz,
     {$U Storage.obj}   Storage,
     {$U QuickDraw.obj} QuickDraw,
     {$U FontMgr.obj}   FontMgr,
     {$U libos/SysCall.obj}   SysCall;

{$DECL IMDebug }
{$DECL IMCompile }

{$IFC fDbgOK}   { system debug flag init }
  {$SETC IMDebug := TRUE}
{$ELSEC }
  {$SETC IMDebug := FALSE}
{$ENDC }

{ ******************  REMOVE THIS FOR PRODUCTION BUILDS !!!!! ****************** }
{ ******************  REMOVE THIS FOR PRODUCTION BUILDS !!!!! ****************** }
{ ******************  REMOVE THIS FOR PRODUCTION BUILDS !!!!! ****************** }

{$SETC IMDebug := FALSE} { force imdebug on }

{ ****************** For building test systems with debugging ***************** }
{ ****************** and compilation included when fDbgOk is  ****************** }
{ ****************** turned off.  Removing this will cause    ****************** }
{ ****************** IM build to follow fDbgOk and fSymOk.    ****************** }

{$IFC fSymOK}   { system symbol flag init }
  {$D+}
{$ELSEC }
  {$D-}
{$ENDC }

  {    IFC IMDebug}   { system debug flag init }
  {    SETC IMCompile := TRUE}
  {    ELSEC }
{$SETC IMCompile := FALSE}
  {    ENDC }


CONST IMBlkSz = 512;
      IMBlkCt = 4;
      IMBufSz = 2048; {IMBlkCt * IMBlkSz}
      MaxLFiles = 15;
    {$IFC NOT IMDebug}
      IMFtst = FALSE;
    {$ENDC }


TYPE IMFilStr = STRING[31];
     TFpos = TL;
     TPFpos = ^TFpos;
     TFposRec = RECORD
                  fileidx: TB;
                  hifilepos: TB;
                  filepos: TC;
                 END;
     TPFposRec = ^TFposRec;

     TPFileDir = ^TFileDir;
     TFileDir   = RECORD
                    curFile, nFiles: TB;
                    lfn: ARRAY[1..MaxLFiles] OF IMFilStr;
                  END;

     TPIMF = ^TIMFileState;
     TIMFileState = RECORD
                      fname: String[40];
                      fid: FILE;
                      fopen, feof: TF;
                      bufpos: TL;
                      fblock: TC;
                      fbufLim: TL;
                      lastLinePos, spaceCount: TL;
                      spaceCode: TF;
                      fbuf: ARRAY [0..IMBufSz] OF TB;
                      fdir: TFileDir;
                    END;

{$IFC IMDebug}
VAR
   IMFtst: TF;
{$ENDC }

PROCEDURE RgchToStr(VAR st: Str255; rgch: TRgch; rgl: TC);
PROCEDURE PxFdir(pfd: TPFileDir);
PROCEDURE PxIMF(pimfs: TPIMF);
PROCEDURE PxIMFPos(lfpos: TFPos);
FUNCTION  InitIMFS(pimfs: TPIMF; lfname: Str255): TF;
FUNCTION  IMFrgch(pimfs: TPIMF): TRgch;
FUNCTION  IMFpos(pimfs: TPIMF): TFpos;
FUNCTION  IMFLinePos(pimfs: TPIMF; linePos: TC): TFpos;
FUNCTION  IMFSetPos(pimfs: TPIMF; fp: TFpos): BOOLEAN;
FUNCTION  IMFLineCt(pimfs: TPIMF; VAR rgch: TRgch): TC;
PROCEDURE IMFReadln(pimfs: TPIMF; VAR rst: Str255);
FUNCTION  IMFChar(pimfs: TPIMF): CHAR;

{  IMF Debugging procedures  }
{$IFC IMDebug }
  PROCEDURE PrintIMFPos(lfpos: TFPos);
  PROCEDURE IMFTestInit;
  PROCEDURE NextIMFChar(pfs: TPIMF);
  PROCEDURE NextIMFLine(pfs: TPIMF);
  PROCEDURE ShowIMFPos(pfs: TPIMF);
  PROCEDURE ShowIMFLinePos(pfs: TPIMF; lpos: TC);
  PROCEDURE ShowIMFBufPos(pfs: TPIMF);
  PROCEDURE ListIMFBuf(pfs: TPIMF);
  PROCEDURE ListCurrentIMFBuf(pfs: TPIMF);
  PROCEDURE SetIMFFilePos(pfs: TPIMF);
  PROCEDURE SetIMFPos(pfs: TPIMF);
  PROCEDURE ReSetIMFPos(pfs: TPIMF);
  PROCEDURE SetIMFRPos(pfs: TPIMF);
 {$ENDC }




IMPLEMENTATION

VAR ior: TC;
    fileVol: IMFilStr;
    dirVol: IMFilStr;

  { IMF Debugging variables }
    saveIMFpos: TFpos;

 {$IFC IMDebug }
    wlen, wmax, wct: TC;
    wstr: Str255;
    wbuf: TRgch;
    wbufsp: ARRAY[0..255] OF TB;
    wend: TF;
 {$ENDC }


{$IFC fDbgOk }
PROCEDURE TDbgOk;
BEGIN
END;
{$ENDC }

{$IFC fSymOk }
PROCEDURE TSymOk;
BEGIN
END;
{$ENDC }

{$IFC IMDebug }
PROCEDURE TIMDebug;
BEGIN
END;
{$ENDC }


{$S imfh }
PROCEDURE RgchToStr;  { (VAR st: Str255; rgch: TRgch; rgl: TC);  }
BEGIN
  IF rgl > 255 THEN rgl := 255;
  st[0] := CHR(rgl);
  MoveRgch(POINTER(ORD(@st)+1),rgch,rgl);
END;


{$S imfdbg }
PROCEDURE PxFdir;  { (pfd: TPFileDir); }
VAR i: TC;
BEGIN
  WITH pfd^ DO
    BEGIN
      WRITELN('  File Directory: ', ORD(pfd), 'nFiles:', nFiles:4,
              '  curFile:', curFile:4);
      FOR i := 1 to nFiles DO WRITELN('     File ', i:3, ' - ', lfn[i]);
    END;
END;


{$S imfdbg }
PROCEDURE PxIMF;  { (pimfs: TPIMF); }
BEGIN
  WITH pimfs^ DO
    BEGIN
      WRITELN('IMFileState for file: ', fname);
      WRITELN('  fileid: ', ORD(@fid^), '  fopen:', fopen);
      WRITELN('  bufpos:', bufpos, '  fblock:', fblock, ' fbufLim:', fbufLim);
      WRITELN('  feof:',  feof);
      WRITELN('  lastLinePos:', lastLinePos, '  spaceCount:', spaceCount,
              'spaceCode: ', spaceCode);
    END;
END;


{$S imfdbg }
PROCEDURE PxIMFPos; { (lfpos: TFPos); }
VAR lpfpr: TPFPosRec;
BEGIN
  lpfpr := @lfpos;
  WRITE(lfpos, '(', lpfpr^.fileidx:3, ',');
  lpfpr^.fileidx := 0;
  WRITELN(lfpos:6, ')');
END;



{$S imfw }
PROCEDURE ClearLastLinePos(pimfs: TPIMF);
BEGIN
  WITH pimfs^ DO
    BEGIN
      spaceCount := 0;
      lastLinePos := -1;
      spaceCode := FALSE;
    END;
END;


{$S imfini }
FUNCTION LoadFDir(pld: TPFileDir; lfname: Str255): TF;
LABEL 900;
VAR i, ior: TC;
    lfid: TEXT;
    fn: IMFilStr;
BEGIN
  WITH pld^ DO
    BEGIN
      IF IMFtst THEN WRITELN('LoadFDir(', ORD(pld), ',', lfname,')');
      FOR i := 1 TO maxLFiles DO BEGIN lfn[i] := ''; END;
      ior := -1;
      curFile := 0;
      nFiles := 0;
      IF lfname[1] = '-'
        THEN BEGIN
          fileVol := COPY(lfname,2,LENGTH(lfname)-1);
          fileVol := COPY(fileVol, 1, POS('-',fileVol)-1);
         END;
      dirVol := fileVol;
      IF IMFtst THEN
        WRITELN('fileVol and dirVol set to "', fileVol, '"');

      IF POS('.',lfname) = 0 THEN lfname := CONCAT(lfname, '.text');
      {$I-}
      RESET(lfid, lfname);
      ior := IORESULT;
      {$I+}
      IF IMFtst THEN WRITELN('Opening file directory : ', lfname, '  IOResult:',
                             ior, '  EOF:', EOF(lfid));
      IF ior <> 0 THEN
        BEGIN
          WRITELN('Can''t open file directory:', lfname);
          LoadFDir := FALSE;
          EXIT(LoadFDir);
        END;
      REPEAT
        READLN(lfid, fn);
        IF LENGTH(fn) < 1 THEN GOTO 900;
        nfiles := nfiles + 1;
        lfn[nfiles] := fn;
        IF nfiles > maxLFiles THEN
          BEGIN
            WRITELN('maxLFiles exceeded in file dir: ', lfname,
                     ' only the first ', maxLFiles:3, ' will be used.');
            GOTO 900;
          END;
        UNTIL EOF(lfid);
900:  IF IMFtst THEN PxFdir(pld);
      CLOSE(lfid, LOCK);
      IF nfiles < 1 THEN
        WRITELN('Alert - no file names in file directory');
      LoadFDir := (nfiles > 0);
    END;
END;


{$S imfw }
{ old loadFDir from when there were lessons }
{PROCEDURE LoadFDir(pimfs: TPIMF);
{LABEL 100, 900;
{VAR i, ior: TC;
{    fdfid: TEXT;
{    fn, fdfname: IMFilStr;
{BEGIN
{  WITH pimfs^.fdir DO
{    BEGIN
{      curFile := 0;
{      IF fLoaded THEN GOTO 900;
{      fdfname := pimfs^.ldir.lfn[pimfs^.ldir.curLesson];
{      IF fdfname[1] = '-'
{        THEN BEGIN
{          dirVol := COPY(fdfname,2,LENGTH(fdfname)-1);
{          dirVol := COPY(dirVol, 1, POS('-',dirVol)-1);
{         END
{        ELSE BEGIN
{          fdfname := CONCAT('-', lessonVol, '-', fdfname);
{          dirVol := lessonVol;
{         END;
{      IF IMFtst THEN WRITELN('LoadFDir(', ORD(pimfs), ',', fdfname,')');
{      IF IMFtst THEN WRITELN(' dirVol set to "', dirVol, '"');
{      FOR i := 1 TO maxLFiles DO lfn[i] := '';
{      ior := -1;
{      nFiles := 0;
{ 100: IF POS('.',fdfname) = 0 THEN fdfname := CONCAT(fdfname, '.text');
{      {$I-}
{      RESET(fdfid, fdfname);
{      ior := IORESULT;
{      {$I+}
{      IF IMFtst THEN
{        WRITELN('Opening file directory: ', fdfname, '  IOResult:', ior);
{      IF ior <> 0 THEN
{        BEGIN
{          WRITELN('Invalid file directory - ', fdfname,
{                  ' skipping to next file directory');
{          SetNextLesson(pimfs);
{          GOTO 900;
{        END;
{      REPEAT
{        READLN(fdfid, fn);
{        IF LENGTH(fn) < 1 THEN GOTO 900;
{        nFiles := nFiles + 1;
{        lfn[nFiles] := fn;
{        IF nFiles >= maxLFiles THEN
{          BEGIN
{            WRITELN('maxLFiles exceeded in directory file: ', fdfname,
{                     ' only the first ', maxLFiles:3, ' will be used.');
{            GOTO 900;
{          END;
{        UNTIL EOF(fdfid);
{ 900: IF IMFtst THEN PxFdir(@pimfs^.ldir.lfdir);
{      CLOSE(fdfid, LOCK);
{  END;
{END;  }


{$S imfw }
FUNCTION IMFileOpen(pimfs: TPIMF; fidx: TC): TF;
BEGIN
  WITH pimfs^ DO
    BEGIN
      IF IMFtst THEN WRITELN('Entering IMFileOpen');
      IF fopen THEN
        BEGIN
          IF IMFtst THEN WRITELN('Closing file: ', fname);
          {$I-}
          CLOSE(fid, LOCK);
          ior := IORESULT;
          {$I+}
          IF IMFtst THEN WRITELN('file ', fname, ' closed. IORESULT: ', ior);
          fopen := FALSE;
        END;
      IF fidx < 0 THEN BEGIN IMFileOpen := TRUE; EXIT(IMFileOpen); END;
      fname := fdir.lfn[fidx];
      IF fname[1] <> '-' THEN fname := CONCAT('-', dirVol, '-', fname);
      bufpos := 0;
      {$I-}
      RESET(fid, fname);
      ior := IORESULT;
      {$I+}
      IF IMFtst THEN
        WRITELN('Opening IMfile: ', fname, '  IOResult:', ior, '  EOF: ', EOF(fid));
      IF ior = 0 THEN fopen := TRUE;
      fblock := 0;
      feof := FALSE;
      fbufLim := -1;
      IMFileOpen := fopen;
      IF IMFtst THEN WRITELN('IMFileOpen fopen: ', fopen, '  file:', fname);
    END;
END;


{$S imfc }
PROCEDURE SetNextFile(pimfs: TPIMF);
LABEL 100;
VAR fidx: TC;
    fn: IMFilStr;
    fInvalid: TF;
BEGIN
  IF IMFtst THEN WRITELN('Entering SetNextFile');
100:
  WITH pimfs^.fdir DO
    BEGIN
      fInvalid := TRUE;
      IF curFile = nFiles THEN
          BEGIN
          WRITELN('LisaGuide reached the end of its lessons - halting');
          HALT;
          END;
      curFile := curFile + 1;
      fn := lfn[curFile];
      fidx := curFile;
      fInvalid := NOT IMFileOpen(pimfs, fidx);
      IF IMFtst THEN WRITELN('File opened. fInvalid: ', fInvalid);
      IF fInvalid
        THEN BEGIN
          WRITELN('Cannot open script file: ', fn, ' going to next file.');
          GOTO 100;
         END;
      IF IMFtst THEN WRITELN('Next file set and opened.');
    END;
END;


{$S imfw }
FUNCTION SetFile(pimfs: TPIMF; fidx: TB):BOOLEAN;
VAR fn: IMFilStr;
BEGIN
  WITH pimfs^.fdir DO
    BEGIN
      IF (fidx = curFile)
        THEN SetFile := curFile <> 0
        ELSE
          IF (fidx > nFiles) OR (fidx < 1)
            THEN BEGIN
              WRITELN('SetFile - invalid file number: ', fidx:4);
              SetFile := FALSE;
            END
           ELSE BEGIN
            curFile := fidx;
            IF IMFtst THEN WRITELN('Setting file index to file ', curFile:2,
                                     '  ', lfn[curFile]);
            SetFile := IMFileOpen(pimfs, curFile);
           END;
    END;
END;


{$S imfini }
FUNCTION InitIMFS;  { (pimfs: TPIMF; lfname: Str255): TF; }
VAR i: TC;
BEGIN
  WITH pimfs^ DO
    BEGIN
      IF IMFtst THEN WRITELN('InitIMFS(', ORD(pimfs), ',', lfname, ')');
      fname := '';
      fopen := FALSE;
      bufpos := 0;
      fblock := -1;
      fBufLim := -1;
      feof := FALSE;
      FOR i := 0 TO IMBufSz DO
        fbuf[i] := 0;
      IF NOT LoadFDir(@fdir, lfname)
        THEN BEGIN
          InitIMFS := FALSE;
          WRITELN('Alert - no file directory, cant run LisaGuide');
          EXIT(InitIMFS);
         END;
      InitIMFS := TRUE;
      fdir.curFile := 0;
      SetNextFile(pimfs);
      ClearLastLinePos(pimfs);
      IF IMFtst THEN WRITELN('InitIMFS complete: ', ORD(pimfs));
    END;
END;


{$S imfh }
PROCEDURE IMFGetBlock(pimfs: TPIMF; blkn: TC; bufBlk: TC);
  { blkn is relative block in file, bufBlk is block position in fbuf.  }
VAR blkCt, rBlkCt, blkPos: TC;
    rgch: TRgch;
BEGIN
  WITH pimfs^ DO
    BEGIN
      blkn := blkn + 2;  { adjust for ucsd text file header }
      blkCt := IMBlkCt - bufBlk;
      IF IMFtst THEN WRITELN('Reading ', blkCt:3, ' blocks from file ',
                              fname, ' at block pos ', blkn:6);
      IF blkCt > 0
        THEN
          BEGIN
            rgch := POINTER(ORD(@fbuf) + (IMBlkSz * bufBlk));
            IF IMFtst THEN
               WRITELN('BlockRead(', ORD(@fid^),',',ORD(rgch),',',blkCt,',',blkn,')');
            {$I-}
            rBlkCt := BlockRead(fid, rgch^, blkCt, blkn);
            ior := IORESULT;
            {$I+}
            IF (ior > 0) AND (NOT IMFtst) THEN
               WRITELN('BlockRead(', ORD(@fid^),',',ORD(rgch),',',blkCt,',',blkn,')',
                        ' = ', rBlkCt:3, ' blocks read.');
            IF IMFTst OR (ior > 0) THEN WRITELN('IOResult = ', ior);
            fbufLim := IMBufSz - ((blkCt - rBlkCt) * IMBlkSz);
            feof := EOF(fid);
          END;
      ClearLastLinePos(pimfs);
      IF IMFtst THEN PxIMF(pimfs);
    END;
END;


{$S imfh }
FUNCTION IMFrgch;  { (pimfs: TPIMF): TRgch; }
BEGIN
  IMFrgch := POINTER(ORD(@pimfs^.fbuf) + pimfs^.bufpos);
END;


{$S imfh }
FUNCTION IMFpos;  { (pimfs: TPIMF): TFpos; }
VAR fp: TFpos;
    pfpr: TPFposRec;
    lfblk: TL;
BEGIN
  WITH pimfs^ DO
    BEGIN
      pfpr := @fp;
      lfblk := fblock;
      fp := (lfblk * IMBlkSz) + bufpos;
      pfpr^.fileidx := fdir.curFile;
      IMFpos := fp;
    END;
END;


{$S imfh }
FUNCTION IMFLinePos;  { (pimfs: TPIMF; linePos: TC): TFpos; }
VAR fp: TFpos;
    pfpr: TPFposRec;
    lpos: TL;
    lfblk: TL;
BEGIN
  WITH pimfs^ DO
    BEGIN
      IF lastLinePos >= 0
        THEN BEGIN
          pfpr := @fp;
          lfblk := fblock;
          lpos := (lfblk * IMBlkSz) + lastLinePos;
          IF spaceCode THEN lpos := lpos + 2;
          IF linePos > spaceCount THEN lpos := lpos + linePos - spaceCount;
          fp := lpos;
          pfpr^.fileidx := fdir.curFile;
          IF IMFtst THEN WRITELN('Got IMF Line Pos.');
          IMFLinePos := fp;
         END
        ELSE BEGIN
          IMFLinePos := IMFPos(pimfs);
         {$IFC IMDebug}
          WRITELN('IMFLinePos - Cant get IMFLinePos - using current pos.');
         {$ENDC}
         END;
    END;
END;


{$S imfh }
FUNCTION SetBufPos(pimfs: TPIMF; fp: TL): BOOLEAN;
VAR blkn: TL;
    bufp: TL;
    bfpr: TPFposRec;
BEGIN
  bfpr := @fp;
  bfpr^.fileidx := 0;
  WITH pimfs^ DO BEGIN
    blkn := fp DIV IMBlkSz;
    bufp := fp MOD IMBlkSz;
    feof := FALSE;
    IF (fBufLim >= 0) AND (blkn >= fblock) AND (blkn < fblock+IMBlkCt)
      THEN BEGIN
        bufpos := bufp + (IMBlkSz * (blkn-fblock));
        SetBufPos := TRUE;
        IF IMFtst THEN WRITELN('SetBufPos - reset bufpos to:', bufpos);
       END
      ELSE BEGIN
        IMFGetBlock(pimfs, blkn, 0);
        SetBufPos := bufp < fbufLim;
        bufpos := bufp;
        fblock := blkn;
       END;
    IF IMFtst THEN PxIMF(pimfs);
  END;
END;


{$S imfh }
FUNCTION IMFSetBuf(pimfs: TPIMF): TF;
LABEL 100;
VAR src: TP;
BEGIN
  WITH pimfs^ DO
    BEGIN
 100: IF fbufLim < 0 THEN IMFGetBlock(pimfs, 0, 0);
      WHILE fbuf[bufpos] = 0 DO bufpos := bufpos + 1;
      IF (bufpos > (IMBufSz-IMBlkSz)) AND (NOT feof)
        THEN BEGIN
          src := POINTER(ORD(@fbuf)+ IMBlkSz);
          MOVELEFT(src^, fbuf, IMBufSz-IMBlkSz);
          fblock := fblock + 1;
          bufpos := bufpos - IMBlkSz;
          IMFGetBlock(pimfs, fblock + IMBlkCt - 1, IMBlkCt - 1);
         END;
      IF bufpos >= fbufLim THEN
        BEGIN
          IF IMFtst THEN BEGIN
                WRITELN('IMFSetBuf - reached end of IM file, getting Next file.');
                PxIMF(pimfs);
              END;
          SetNextFile(pimfs);
          GOTO 100;
        END;
      IMFSetBuf := bufpos < fbufLim;
    END;
END;


{$S imfw }
FUNCTION IMFSetPos;  { (pimfs: TPIMF; fp: TFpos): BOOLEAN; }
VAR pfpr, spfpr, pbpr: TPFposRec;
    sfp: TFpos;
    bufPos: TFPos;
BEGIN
  IMFSetPos := FALSE;
  IF pimfs = NIL THEN EXIT(IMFSetPos);
  IF fp = -1 THEN BEGIN
    IF IMFtst THEN WRITELN('IMFSetPos - closing script file');
    IMFSetPos := IMFileOpen(pimfs, -1);
    EXIT(IMFSetPos);
   END;
  pfpr := @fp;
  bufPos := fp;
  pbpr := @bufPos;
  pbpr^.fileidx := 0;
  sfp := IMFpos(pimfs);
  spfpr := @sfp;
  IF SetFile(pimfs, pfpr^.fileidx) AND
     SetBufPos(pimfs, bufpos)
    THEN IMFSetPos := TRUE
    ELSE IF SetFile(pimfs, spfpr^.fileidx) AND
            SetBufPos(pimfs, spfpr^.filepos)
           THEN BEGIN
             WRITELN('Cannot jump to:', fp, ' returned to: ', sfp)
            END
           ELSE BEGIN
             WRITELN('Cant jump to:', fp, ' cant return to:', sfp);
             BREAK(804);
            END;
END;


{$S imfh }
PROCEDURE IMFSkipZips(pimfs: TPIMF);
VAR boo: TF;
BEGIN
  WITH pimfs^ DO
    WHILE fbuf[bufpos] = 0 DO
      BEGIN bufpos := bufpos+ 1;
            IF bufpos = fbufLim
              THEN boo := IMFSetBuf(pimfs);
      END;
END;


{$S imfh }
FUNCTION IMFLineCt;  { (pimfs: TPIMF; VAR rgch: TRgch): TC; }
VAR rgchar: TRgChar;
    lim, crpos: TC;
BEGIN
  WITH pimfs^ DO
    BEGIN
      IF IMFSetBuf(pimfs)
        THEN
          BEGIN
            rgch := POINTER(ORD(@fbuf)+bufpos);
            rgchar := POINTER(ORD(rgch));
            IF fbuf[bufpos] = 0 THEN IMFSkipZips(pimfs);
            lim := fbufLim - bufpos;
            crpos := SCANEQ(lim, CHR(chCr), rgchar^);
            IF (crpos = lim) AND feof { in case theres no CR at end of file }
              THEN BEGIN
                 rgch := POINTER(ORD(rgch) + 1);
                 crpos := SCANEQ(lim, CHR(0), rgchar^);
                END;
            IF crpos = lim THEN { could break if file ended with no CRs or zeros }
              BEGIN WRITELN('Cant find eol in ', fname);
                    PxIMF(pimfs);
                    BREAK(777);
                    crpos := 1;
               END;
            lastLinePos := bufPos;
            spaceCount := 0;
            spaceCode := FALSE;
            bufpos := bufpos + crpos + 1;
          END
        ELSE crpos := 0;
      IMFLineCt := crpos;
    END;
END;



{$S imfh }
PROCEDURE IMFReadln;  { (pimfs: TPIMF; VAR rst: Str255); }
VAR llen, i, spaces: TC;
    rgch, drgch: TRgch;
BEGIN
  WITH pimfs^ DO
    BEGIN
      llen := IMFLineCt(pimfs, rgch);
      IF rgch^[0] = 16
        THEN BEGIN spaces := rgch^[1] - 32;
               spaceCount := spaces;
               spaceCode := TRUE;
             END
        ELSE BEGIN spaces := -1; spaceCode := FALSE; spaceCount := 0; END;
      IF spaces > 0 THEN
        BEGIN
          drgch := POINTER(ORD(@rst)+1);
          IF llen > (255 - spaces) THEN llen := (255 - spaces) ;
          rst[0] := CHR(llen + spaces - 2);
          FOR i := 0 TO spaces-1 DO drgch^[i] := 32;
          MoveRgch(@drgch^[spaces],@rgch^[2],llen-2);
        END
       ELSE IF spaces = 0
              THEN RgchToStr(rst,@rgch^[2], llen-2)
              ELSE RgchToStr(rst, rgch, llen);
    END;
END;



{$S imfw }
FUNCTION IMFChar;  { (pimfs: TPIMF): CHAR; }
BEGIN
  WITH pimfs^ DO
    BEGIN
      IF (bufpos >= fbufLim) OR IMFSetBuf(pimfs)
        THEN BEGIN
           IF fbuf[bufpos] = 16 THEN bufpos := bufpos + 2;
           IMFChar := CHR(fbuf[bufpos]);
           bufpos := bufpos + 1;
          END
        ELSE BEGIN
               WRITELN('Ran out of script!!!');
               IMFChar := CHR(chSp);
               BREAK(999);
             END;
    END;
END;



{  IMF Debugging stuff  - compiled out when deubgging is off  }
{$IFC IMDebug }
{$S imfdbg }
FUNCTION KbdInt: INTEGER;
LABEL 100;
VAR i, ct,ior: INTEGER;
BEGIN
  i := 0;
  ct := 0;
100: WRITE('Enter Number: ');
  READLN(i);
  ior := IORESULT;
  IF ior = 14 THEN GOTO 100;
  ct := ct + 1;
  IF ior <> 0 THEN WRITELN('IOResult Reading number: ', ior, ' i = ', i);
  IF (ior > 0) AND (ct < 5) THEN GOTO 100;
  KbdInt := i;
END;
{$ENDC }


{$IFC IMDebug }
{$S imfdbg }
FUNCTION KbdByte: TB;
LABEL 100;
VAR ct,ior: INTEGER;
    i: TB;
BEGIN
  i := 0;
  ct := 0;
100: WRITE('Enter Number (-127..127): ');
  READLN(i);
  ior := IORESULT;
  IF ior = 14 THEN GOTO 100;
  ct := ct + 1;
  IF ior <> 0 THEN WRITELN('IOResult Reading number: ', ior, ' i = ', i);
  IF (ior > 0) AND (ct < 5) THEN GOTO 100;
  KbdByte := i;
END;
{$ENDC }

{$IFC IMDebug }
{$S imfdbg }
FUNCTION KbdLInt: TL;
LABEL 100;
VAR ct,ior: INTEGER;
    i: TL;
BEGIN
  i := 0;
  ct := 0;
100: WRITE('Enter Number: (long integer)');
  READLN(i);
  ior := IORESULT;
  IF ior = 14 THEN GOTO 100;
  ct := ct + 1;
  IF ior <> 0 THEN WRITELN('IOResult Reading number: ', ior, ' i = ', i);
  IF (ior > 0) AND (ct < 5) THEN GOTO 100;
  KbdLInt := i;
END;
{$ENDC }


{$IFC IMDebug }
{$S imfdbg }
PROCEDURE PrintIMFPos; { (lfpos: TFPos); }
VAR lpfpr: TPFPosRec;
BEGIN
  lpfpr := @lfpos;
  WRITE(lfpos, '(', lpfpr^.fileidx:3, ',');
  lpfpr^.fileidx := 0;
  WRITELN(lfpos:6, ')');
END;
{$ENDC }

{$IFC IMDebug }
{$S imfini }
PROCEDURE IMFTestInit;
VAR spfpr: TPFPosRec;
BEGIN
  spfpr := @saveIMFpos;
  spfpr^.fileidx := 1;
  spfpr^.hifilepos := 0;
  spfpr^.filepos := 0;
  wbuf := @wbufsp;
  wmax := 255;
  wend := FALSE;
  wct := 0;
END;
{$ENDC }



{$IFC IMDebug }
{$S imfdbg }
PROCEDURE NextIMFChar; { (pfs: TFpos); }
VAR fch: CHAR;
BEGIN
  fch := IMFChar(pfs);
  WRITE(CHR(8), CHR(8),fch);
  IF IMFtst THEN WRITE('<', ORD(fch):3, '>');
END;
{$ENDC }

{$IFC IMDebug }
{$S imfdbg }
PROCEDURE NextIMFLine; { (pfs: TPIMF); }
VAR lstr: Str255;
    i: INTEGER;
    fp: TFpos;
    pfpr: TPFposRec;
BEGIN
  fp := IMFpos(pfs);
  pfpr := @fp;
  WRITE(' NextIMFLine at ');
  PrintIMFPos(fp);
  IMFReadLn(pfs, lstr);
  FOR i := 1 TO LENGTH(lstr) DO WRITE(lstr[i]);
  WRITELN;
  IF IMFtst THEN FOR i := 1 TO LENGTH(lstr) DO
    BEGIN WRITE(ORD(lstr[i]):3, ','); IF (i MOD 20) = 0 THEN WRITELN; END;
  IF IMFtst THEN   WRITELN;
END;
{$ENDC }

{$IFC IMDebug }
{$S imfdbg }
PROCEDURE ShowIMFPos; { (pfs: TPIMF); }
VAR lfpos: TFPos;
BEGIN
  lfpos := IMFpos(pfs);
  WRITE(' IMF File poss = ');
  PrintIMFPos(lfpos);
  saveIMFpos := lfpos;
END;
{$ENDC }

{$IFC IMDebug }
{$S imfdbg }
PROCEDURE ShowIMFLinePos; { (pfs: TPIMF; lpos: TC); }
VAR lfpos: TFPos;
BEGIN
  lfpos := IMFLinePos(pfs, lpos);
  WRITE(' IMF File poss = ');
  PrintIMFPos(lfpos);
  saveIMFpos := lfpos;
END;
{$ENDC }

{$IFC IMDebug }
{$S imfdbg }
PROCEDURE ShowIMFBufPos; { (pfs: TPIMF); }
VAR i, ppos: TC;
BEGIN
  WRITELN('Current IMF Bufpos:', pfs^.bufpos);
END;
{$ENDC }

{$IFC IMDebug }
{$S imfdbg }
PROCEDURE ListIMFBuf; { (pfs: TPIMF); }
VAR i, ppos: TC;
BEGIN
  WRITE('list at bufpos:');
  ppos := KbdInt;
  WRITELN;
  FOR i := ppos TO ppos + 50 DO WRITE(CHR(pfs^.fbuf[i]));
  WRITELN;
  IF  IMFtst THEN FOR i := ppos TO ppos + 50 DO
   BEGIN WRITE(pfs^.fbuf[i]:3, ','); IF ((i - ppos) MOD 20) = 0 THEN WRITELN; END;
  IF IMFtst THEN WRITELN;
END;
{$ENDC }

{$IFC IMDebug }
{$S imfdbg }
PROCEDURE ListCurrentIMFBuf; { (pfs: TPIMF); }
VAR i, ppos: TC;
BEGIN
  ppos := pfs^.bufpos;
  WRITELN;
  FOR i := ppos TO ppos + 50 DO WRITE(CHR(pfs^.fbuf[i]));
  WRITELN;
  IF  IMFtst THEN FOR i := ppos TO ppos + 50 DO
   BEGIN WRITE(pfs^.fbuf[i]:3, ','); IF ((i - ppos) MOD 20) = 0 THEN WRITELN; END;
  IF IMFtst THEN WRITELN;
END;
{$ENDC }

{$IFC IMDebug }
{$S imfdbg }
PROCEDURE SetIMFFilePos; { (pfs: TPIMF); }
VAR lfpos: TFPos;
    lpfpr: TPFPosRec;
    fIMFStuff: TF;
BEGIN
  lfpos := IMFpos(pfs);
  lpfpr := @lfpos;
  WRITE('Set file pos (for current file) to: '); lpfpr^.filepos := KbdInt;
  fIMFStuff := IMFSetPos(pfs, lfpos);
  IF fIMFStuff
    THEN BEGIN WRITE('IMF moved to pos '); PrintIMFPos(lfpos); END
    ELSE BEGIN
           WRITE('Cant set IMF pos to '); PrintIMFPos(lfpos);
           lfpos :=IMFpos(pfs);
           WRITE('IMF remains at '); PrintIMFPos(lfpos);
         END;
END;
{$ENDC }

{$IFC IMDebug }
{$S imfdbg }
PROCEDURE SetIMFPos; { (pfs: TPIMF); }
VAR lfpos: TFPos;
    fIMFStuff: TF;
BEGIN
  WRITE('Set IMF global (fileidx, filepos) to:');
  lfpos := KbdLInt;
  fIMFStuff := IMFSetPos(pfs, lfpos);
  IF fIMFStuff
    THEN BEGIN WRITE('IMF moved to pos '); PrintIMFPos(saveIMFpos); END
    ELSE BEGIN
           WRITE('Cant set IMF pos to '); PrintIMFPos(lfpos);
           lfpos := IMFpos(pfs);
           WRITE('IMF remains at '); PrintIMFPos(lfpos);
         END;
END;
{$ENDC }

{$IFC IMDebug }
{$S imfdbg }
PROCEDURE ReSetIMFPos; { (pfs: TPIMF); }
VAR lfpos: TFPos;
    fIMFStuff: TF;
BEGIN
  fIMFStuff := IMFSetPos(pfs, saveIMFpos);
  IF fIMFStuff
    THEN BEGIN WRITE('returned to pos '); PrintIMFPos(saveIMFpos); END
    ELSE BEGIN lfpos := IMFpos(pfs);
           WRITE('Cant reset IMF pos. IMF now at '); PrintIMFPos(lfpos); END;
END;
{$ENDC }



{$IFC IMDebug }
{$S imfdbg }
PROCEDURE SetIMFRPos; { (pfs: TPIMF); }
VAR lfpos: TFPos;
    lpfpr: TPFPosRec;
    fIMFStuff: TF;
BEGIN
  lpfpr := @lfpos;
  WRITE('Set file index to: '); lpfpr^.fileidx := KbdByte;
  WRITE('Set hi file pos to: '); lpfpr^.hifilepos := KbdByte;
  WRITE('Set file pos to: '); lpfpr^.filepos := KbdInt;
  fIMFStuff := IMFSetPos(pfs, lfpos);
  IF fIMFStuff
    THEN BEGIN WRITE('IMF moved to pos '); PrintIMFPos(saveIMFpos); END
    ELSE BEGIN
           WRITE('Cant set IMF pos to '); PrintIMFPos(lfpos);
           lfpos := IMFpos(pfs);
           WRITE('IMF remains at '); PrintIMFPos(lfpos);
         END;
END;
{$ENDC }

END.
ÿ