{******************************************************************************}
{*                                                                            *}
{*       LisaGuide - imlib unit : Copyright 1983, Apple Computer Inc.         *}
{*                                                                            *}
{******************************************************************************}

UNIT IFSetTime;

{  This unit is used to set the lisa's clock.  This is done by the user via the
   menu and dialog box.  }

(* ===== Conditional compilation variable (R) =====
         if TRUE, then compile for real FILER;
                  else compile for pseudo-FILER, which
                       runs under LisaTutor.
*)
{$DECL R}
{$SETC R:=FALSE}

INTERFACE

USES {$U HwInt      } HwInt,
     {$U libos/SysCall    } SysCall,
     {$U libos/PSysCall   } PSysCall,
     {$U UnitStd    } UnitStd,
     {$U UnitHz     } UnitHz,
     {$U Storage    } Storage,
     {$U QuickDraw  } QuickDraw,
     {$U FontMgr    } FontMgr,
     {$U PmDecl     } PmDecl,
     {$U PMM        } PMM,
     {$U WM.Events  } Events,
     {$U WM.Menus   } Menus,
     {$U WM.Windows } Windows,
     {$U WM.Folders } Folders,
     {$U AlertMgr   } AlertMgr,
     {$U apim/TFGlobals  } IFGlobals;


TYPE
      String10 = String[10];


PROCEDURE InitSetTime;
{  Call this proc once after the machine is booted. }


PROCEDURE JulianToMoDay (julian: INTEGER; leapYear: BOOLEAN; VAR mo, day: INTEGER);

{  This proc converts a julian day into month and day }


{$IFC R}
PROCEDURE SetTimeDialog;

{  This proc puts up the dialog box used to set the lisa's internal clock.  It assumes
   local time.  }
{$ENDC}


PROCEDURE TimeToStr(theTime: LONGINT; VAR dateStr,timeStr: String10);

{  This proc takes an OS format of time and converts it to two strings, date and time. }


IMPLEMENTATION

{$S SetTime }


CONST

   doneButn    = 1;      { Button number in the alert phrase file              }
   timeFont =    p10Tile;{ Font used in the dialog box                         }
   cursorOnTime = 60;
   cursorOffTime = 40;

   msg1 =       'The machine''s time and month/day/year are:'; { Form constant }

   editLength =  17;     { length of date/time string, starting with 0         }

   hourOffset =  0;      { Field character position offsets into display string}
   minOffset =   3;
   ampmOffset =  6;
   moOffset =    10;
   dayOffset =   13;
   yearOffset =  16;

   endJa = 31;           { Julian conversion aids, cumulative days             }
   endFe = 59;
   endMr = 90;
   endAp = 120;
   endMy = 151;
   endJn = 181;
   endJl = 212;
   endAu = 243;
   endSe = 273;
   endOc = 304;
   endNo = 334;
   endDe = 365;

   bsCode =      8;      { Decimal codes for non-printing characters           }
   ntrCode =     3;
   tabCode =     9;
   rtnCode =     13;

   cln =        ':';
   slh =        '/';

TYPE

    TcharKind =  (ampmChar, blankOrNumChar, numChar, oneOrLess, twoOrLess,
                  twoToNine, sixToNine, fourToNine, advChar);
    Tmonth =     (Ja,Fe,Mr,Ap,My,Jn,Jl,Au,Se,Oc,No,De);
    TwhereDown = (inField, inNoMansLand, notDown);
    Twhere     = (hourBox, minBox, ampmBox, moBox, dayBox, yearBox, noBox);

VAR

      { State Information }

    abortDialog:BOOLEAN;   { User canceled dialog                                  }
    arrowCrs:   BOOLEAN;   { Cursor location: FALSE=over field, TRUE=anywhere else }
    cursorBlink:INTEGER;   { }
    donePushed: BOOLEAN;   { User has pushed the done button                       }
    downIn:     TwhereDown;{ Area where mouse mouse went down, or button is up     }
    editField:  Twhere;    { Current edit field                                    }
    edited:     BOOLEAN;   { The date and/or time was altered                      }
    editTime:   PACKED ARRAY [0..editLength] OF CHAR; { Displayed date/time, with edits   }
    fieldPos:   INTEGER;   { Which char of field gets next character               }

      { Form (dialog box) Layout }

    editCol:    INTEGER;    { Screen location of displayed date and time           }
    editRow:    INTEGER;

    donePt:     Point;      { Location of fields and buttons on the screen         }
    editRect,
    hourRect,
    minRect,
    ampmRect,
    moRect,
    dayRect,
    yearRect:   Rect;

      { Font size info }

    numWidth:   INTEGER;    { Width of '0' in pixels                               }
    rowHt:      INTEGER;    { Font height in pixels                                }
    sysAscent:  INTEGER;    { char ascent of font in pixels                        }
    sysDescent: INTEGER;    { Char descent of font in pixels                       }

      { Edit Support }

    julianDays: ARRAY [0..12] OF INTEGER; { Cumulative days by month               }
    leapYear:   BOOLEAN;    { TRUE if year MOD 4 = 0                               }

    bs,                     { Non-printing ascii characters                        }
    ntr,
    rtn,
    tab:        CHAR;

      { Development Support }

    LCcross:    Cursor;     { Cursor definition, remove when moving to WmlCrs      }

{$IFC R}
{--------------------------------------------------------------------------}
{  GetTime                                                                 }
{--------------------------------------------------------------------------}

PROCEDURE GetTime;

CONST slash = '/';
VAR i:        INTEGER;
    mo:       INTEGER;
    day:      INTEGER;
    osErr:    INTEGER;
    strVal:   NumberStr;
    curTime:  Time_Rec;

  PROCEDURE InsertStr (offset: INTEGER);
  VAR i:    INTEGER;
      lgth: INTEGER;
  BEGIN
  IF trcFEntry THEN WRITELN ('InsertStr: ',strVal,', offset ',offset:1);

  lgth := LENGTH(strVal);
  FOR i := 1 TO lgth
  DO editTime[offset+1+i-lgth] := strVal[i];

  IF trcFEntry THEN WRITELN ('   exit InsertStr');
  END;


BEGIN
IF trcFEntry THEN WRITELN ('GetTime');

FOR i := 0 TO editLength
DO editTime[i] := ' ';
editTime[minOffset-1] := ':';
editTime[dayOffset-1] := slash;
editTime[yearOffset-1] := slash;

IF trcFEntry THEN WRITELN ('   getting the time');
Get_Time (osErr, curTime);
IF osErr = 0
THEN Convert_Time (osErr, curTime, curTime, FALSE{gmt to local})
ELSE WITH curTime DO BEGIN     { Invalid time, set a default }
  year := 1983;
  day := 1;
  hour := 0;
  minute := 0;
  END;
leapYear := ((curTime.year MOD 4) = 0);
IF trcFEntry THEN WRITELN ('   yeapYear is ',leapYear);
IF trcFEntry THEN WRITELN ('   set am/pm');
IF curTime.hour > 11
THEN BEGIN
  strVal := 'pm';
  IF curTime.hour > 12 THEN curTime.hour := curTime.hour - 12;
  END
ELSE strVal := 'am';
InsertStr (ampmOffset);

IF trcFEntry THEN WRITELN ('   set hour');
IntToStr (curTime.hour, strVal);
InsertStr (hourOffset);

IF trcFEntry THEN WRITELN ('   set minute');
IntToStr (curTime.minute, strVal);
InsertStr (minOffset);
IF editTime[minOffset] = ' ' THEN editTime[minOffset] := '0';

JulianToMoDay (curTime.day, leapYear, mo, day);
IF trcFEntry THEN WRITELN ('   set month');
IntToStr (mo, strVal);
InsertStr (moOffset);

IF trcFEntry THEN WRITELN ('   set day');
IntToStr (day, strVal);
InsertStr (dayOffset);
IF editTime[dayOffset] = ' ' THEN editTime[dayOffset] := '0';

IF trcFEntry THEN WRITELN ('   set year');
IntToStr (curTime.year, strVal);
DELETE (strVal, 1, LENGTH(strVal)-2);
InsertStr (yearOffset);
IF editTime[yearOffset] = ' ' THEN editTime[yearOffset] := '0';

END;
{$ENDC}


{$IFC R}
{--------------------------------------------------------------------------}
{  ShowForm                                                                }
{--------------------------------------------------------------------------}

PROCEDURE ShowForm;
VAR
    curTime:    Time_Rec;
    err:        INTEGER;
    i:          INTEGER;
    textStart:  INTEGER;
    timeString: STRING[30];
    hCharPos,
    vCharPos:   INTEGER;

BEGIN
If trcFEntry THEN WRITELN ('ShowForm');

  { Init state variables }

downIn := notDown;
arrowCrs := TRUE;
editField := noBox;
fieldPos := 0;

  { Draw dialog box }

DialogHeight (editRect.bottom+rowHt, FALSE); { Put up dialog box               }

TextFont (sysText);
MoveTo (20, rowHt);                          { Put up standard message         }
DrawString (msg1);

PushButn (doneButn, TRUE); {*** Alert Mgr workaround ***}
PushButn (doneButn, FALSE); {*** Alert Mgr workaround ***}
ShowButn (doneButn, donePt.h, donePt.v, TRUE{dark border});

GetTime;
TextFont (timeFont);
hCharPos := editRow*rowHt;                   { Put up date and time            }
vCharPos := editRect.left;
FOR i := 0 TO editLength
DO BEGIN
  MoveTo (vCharPos, hCharPos);
  DrawChar (editTime[i]);
  vCharPos := vCharPos + numWidth;
  END;

FlushInput (NIL);                            { Flush any pending user inputs   }

IF trcFEntry THEN WRITELN ('   exit ShowForm');
END;
{$ENDC}

{$IFC R}
{--------------------------------------------------------------------------}
{  RemoveForm                                                              }
{--------------------------------------------------------------------------}

PROCEDURE RemoveForm;
BEGIN
IF trcFEntry THEN WRITELN ('RemoveForm');

dialogHeight (0, FALSE);

IF trcFEntry THEN WRITELN ('   exit RemoveForm');
END;
{$ENDC}

{$IFC R}
{--------------------------------------------------------------------------}
{ WhichMonth                                                               }
{--------------------------------------------------------------------------}

FUNCTION WhichMonth : Tmonth;
VAR mo:  Tmonth;
BEGIN
IF trcFEntry THEN WRITELN ('WhichMonth');

CASE editTime[moOffset] OF
 ' ',
 '0': CASE editTime[moOffset+1] OF
       '1': mo := Ja;
       '2': mo := Fe;
       '3': mo := Mr;
       '4': mo := Ap;
       '5': mo := My;
       '6': mo := Jn;
       '7': mo := Jl;
       '8': mo := Au;
       '9': mo := Se;
        END;
 '1': CASE editTime[moOffset+1] OF
       '0': mo := Oc;
       '1': mo := No;
       '2': mo := De;
        END;
  END;
WhichMonth := mo;
IF trcFEntry THEN WRITELN (' exit WhichMonth: ',ORD(mo):1);
END;
{$ENDC}

{$IFC R}
{--------------------------------------------------------------------------}
{  FieldOffset                                                             }
{--------------------------------------------------------------------------}

FUNCTION FieldOffset (field: Twhere) : INTEGER;
BEGIN
IF trcFEntry THEN WRITELN ('FieldOffset: field = ',ORD(field):1);

CASE field OF
  hourBox: FieldOffset := hourOffset;
  minBox:  FieldOffset := minOffset;
  ampmBox: FieldOffset := ampmOffset;
  moBox:   FieldOffset := moOffset;
  dayBox:  FieldOffset := dayOffset;
  yearBox: FieldOffset := yearOffset;
  END;

IF trcFEntry THEN WRITELN ('   exit FieldOffset');
END;
{$ENDC}

{$IFC R}
{--------------------------------------------------------------------------}
{  EditChar                                                                }
{--------------------------------------------------------------------------}

PROCEDURE EditChar (pos: INTEGER; newChar: CHAR);
VAR
    left:     INTEGER;
    tempRect: Rect;

BEGIN
IF trcFEntry THEN WRITELN ('EditChar: pos = ',pos:1,', newChar = ',newChar);

left := (editCol+pos)*numWidth;
SetRect (tempRect, left, editRect.top, left+numWidth, editRect.bottom);
MoveTo (left, editRow*rowHt);
FillRect (tempRect, white);
DrawChar (newChar);
editTime[pos] := newChar;

IF trcFEntry THEN WRITELN ('   exit EditChar');
END;
{$ENDC}

{$IFC R}
{--------------------------------------------------------------------------}
{  InvertField                                                             }
{--------------------------------------------------------------------------}

PROCEDURE InvertField (field: Twhere);
VAR
    theRect:  Rect;

BEGIN
IF trcFEntry THEN WRITELN ('InvertField: ',ORD(field):1);

CASE field OF
  hourBox:  theRect := hourRect;
  minBox:   theRect := minRect;
  ampmBox:  theRect := ampmRect;
  moBox:    theRect := moRect;
  dayBox:   theRect := dayRect;
  yearBox:  theRect := yearRect;
  OTHERWISE EXIT (InvertField);
  END;

InvertRect (theRect);

IF trcFEntry THEN WRITELN ('   exit InvertField');
END;
{$ENDC}

{$IFC R}
{------------------------------------------------------------------------------}
{  PtInField                                                                   }
{------------------------------------------------------------------------------}

FUNCTION PtInField (where: Point) : Twhere;

BEGIN

IF PtInRect (where, hourRect)       THEN PtInField := hourBox
ELSE IF PtInRect (where, minRect)   THEN PtInField := minBox
ELSE IF PtInRect (where, ampmRect)  THEN PtInField := ampmBox
ELSE IF PtInRect (where, moRect)    THEN PtInField := moBox
ELSE IF PtInRect (where, dayRect)   THEN PtInField := dayBox
ELSE IF PtInRect (where, yearRect)  THEN PtInField := yearBox
                                    ELSE PtInField := noBox;
END;
{$ENDC}

{$IFC R}
{--------------------------------------------------------------------------}
{  NextField                                                               }
{--------------------------------------------------------------------------}

PROCEDURE NextField (key: CHAR);
LABEL 99;
VAR
    next:  Twhere;
    right: BOOLEAN;

BEGIN
IF trcFEntry THEN WRITELN ('NextField: key = ',ORD(key):1);

IF downIn = inField THEN GOTO 99;                { Mouse selecting field   }

IF key = bs THEN right := FALSE                  { Which direction to go   }
ELSE right := TRUE;

IF (editField <> noBox) AND (fieldPos = 1)
THEN InvertField (editField);                    { De-hilite cur field     }

IF key = rtn
THEN next := hourBox                             { Move to first field     }
ELSE CASE editField OF                           { Move to adjacent field  }
       hourBox: IF right = TRUE THEN next := minBox
                                ELSE next := yearBox;
       minBox:  IF right = TRUE THEN next := ampmBox
                                ELSE next := hourBox;
       ampmBox: IF right = TRUE THEN next := moBox
                                ELSE next := minBox;
       moBox:   IF right = TRUE THEN next := dayBox
                                ELSE next := ampmBox;
       dayBox:  IF right = TRUE THEN next := yearBox
                                ELSE next := moBox;
       yearBox: IF right = TRUE THEN next := hourBox
                                ELSE next := dayBox;
       noBox:   next := hourBox;
       END;
InvertField (next);                              { Hilite the new field    }

editField := next;                               { Set state variables     }
fieldPos := 1;

99:
IF trcFEntry THEN WRITELN ('   exit NextField: ',ORD(editField):1);
END;
{$ENDC}

{$IFC R}
{--------------------------------------------------------------------------}
{  RingBell                                                                }
{--------------------------------------------------------------------------}

PROCEDURE RingBell;
BEGIN
WRITE (CHR(bell));
END;
{$ENDC}


{$IFC R}
{--------------------------------------------------------------------------}
{  ResetTime                                                               }
{--------------------------------------------------------------------------}

PROCEDURE ResetTime (err: INTEGER);
VAR days:     INTEGER;
    mo:       INTEGER;
    newTime:  Time_Rec;

  FUNCTION StrToInt (offset, lgth: INTEGER) : INTEGER;
  VAR i:     INTEGER;
      next:  CHAR;
      value: INTEGER;

  BEGIN
  IF trcFEntry THEN WRITELN ('StrToInt: offset ',offset:1,', lgth ',lgth:1);
  value := 0;
  FOR i := offset TO offset+lgth-1
  DO BEGIN
     next := editTime[i];
     IF next IN ['0'..'9']
     THEN value := value * 10 + (ORD(next)-ORD('0'));
     END;
  StrToInt := value;
  IF trcFEntry THEN WRITELN ('   exit StrToInt: ',value:1);
  END;

BEGIN
IF trcFEntry THEN WRITELN ('ResetTime');

newTime.mSec := 0;
newTime.second := 0;
newTime.hour := StrToInt (hourOffset, 2);
IF (editTime[ampmOffset] = 'p') AND (newTime.hour < 12)
THEN newTime.hour := newTime.hour + 12;
IF trcFEntry THEN WRITELN ('   hour is ',newTime.hour:1);

newTime.minute := StrToInt (minOffset, 2);
IF trcFEntry THEN WRITELN ('   minute is ',newTime.minute:1);

newTime.year := 1900 + StrToInt (yearOffset, 2);
IF trcFEntry THEN WRITELN ('   year is ',newTime.year:1);

mo := StrToInt (moOffset, 2);
IF trcFEntry THEN WRITELN ('   month is ',mo:1);
newTime.day := julianDays[mo-1] + StrToInt (dayOffset, 2);
IF leapYear AND (newTime.day > endFe+1) THEN newTime.day := newTime.day + 1;
IF trcFEntry THEN WRITELN ('   day is ',newTime.day:1);

Convert_Time (err, newTime, newTime, TRUE{to GMT});
Set_Time (err, newTime);
SysErr (1000, err);
Get_Time (err, newTime);
SysErr (1000, err);

IF trcFEntry THEN WRITELN ('   exit ResetTime: err =', err:1);
END;
{$ENDC}


{$IFC R}
{--------------------------------------------------------------------------}
{  ValidChar                                                               }
{--------------------------------------------------------------------------}

FUNCTION ValidChar (testChar: CHAR; kind: TcharKind) : BOOLEAN;

VAR ok:   BOOLEAN;

BEGIN
IF trcFEntry THEN WRITELN ('ValidChar: ',testChar);

ok := FALSE;

CASE kind OF
  advChar:     IF testChar IN [tab,rtn,bs,ntr,' ',cln,slh]
                                                THEN ok := TRUE;
  ampmChar:    IF testChar IN ['a','A','p','P'] THEN ok := TRUE;
  numChar:     IF testChar IN ['0'..'9']        THEN ok := TRUE;
  blankOrNumChar: IF testChar IN [' ','0'..'9'] THEN ok := TRUE;
  oneOrLess:   IF testChar IN [' ','0','1']     THEN ok := TRUE;
  twoOrLess:   IF testChar IN [' ','0'..'2']    THEN ok := TRUE;
  twoToNine:   IF testChar IN ['2'..'9']        THEN ok := TRUE;
  fourToNine:  IF testChar IN ['4'..'9']        THEN ok := TRUE;
  sixToNine:   IF testChar IN ['6'..'9']        THEN ok := TRUE;
  END;

IF ok THEN edited := TRUE;
ValidChar := ok;

IF trcFEntry THEN WRITELN ('   exit ValidChar: ',ok);
END;
{$ENDC}

{$IFC R}
{--------------------------------------------------------------------------}
{  EditFirstChar                                                           }
{--------------------------------------------------------------------------}

FUNCTION EditFirstChar (offset: INTEGER; char1: CHAR) : BOOLEAN;

BEGIN
IF trcFEntry THEN WRITELN ('EditFirstChar: offset = ',offset:1,', char1 = ',ORD(char1):1);

EditFirstChar := TRUE;

IF char1 IN [tab, rtn, ntr, bs, cln, slh]
THEN BEGIN                               { Skip field w/o editing          }
  NextField (char1);
  EXIT (EditFirstChar);
  END;

IF NOT ValidChar (char1, blankOrNumChar)
THEN BEGIN                               { Invalid char, don't alter field }
  EditFirstChar := FALSE;
  EXIT (EditFirstChar);
  END;

EditChar (offset, ' ');                  { Clear Field...                  }
EditChar (offset+1, char1);              { ... and set low-order char      }
fieldPos := 2;

IF trcFEntry THEN WRITELN ('   exit EditFirstChar');
END;
{$ENDC}

{$IFC R}
{--------------------------------------------------------------------------}
{  EditSecondChar                                                          }
{--------------------------------------------------------------------------}

FUNCTION EditSecondChar (offset: INTEGER; char2: CHAR) : BOOLEAN;
LABEL 88, 99;
VAR  func:  BOOLEAN;
BEGIN
IF trcFEntry THEN WRITELN ('EditSecondChar: offset = ',offset:1,', char2 = ',ORD(char2):1);

func := TRUE;

IF ValidChar (char2, advChar) OR (char2 = '0')
THEN BEGIN                               { Complete field, check for 0 value}
  IF editTime[offset+1] IN [' ','0']
  THEN BEGIN                             { Field value is 0                 }
    IF editField IN [hourBox, minBox, yearBox]
    THEN EditChar (offset+1, '0')        { Low-order digit can't be ' '     }
    ELSE BEGIN                           { But some fields must be non-zero }
      func := FALSE;
      GOTO 99;
      END;
    END
  ELSE IF Char2 = '0' THEN GOTO 88;      { Non-zero value w/ low-order 0    }
  IF editField IN [minBox,dayBox,yearBox]{ Some high-order digits can't be ' '}
  THEN EditChar (offset, '0');
  NextField (char2);                     { Finished in field, go to next one}
  GOTO 99;
  END;

IF NOT ValidChar (char2, blankOrNumChar)
THEN BEGIN                               { Invalid char, exit w/o altering  }
  func := FALSE;
  GOTO 99;
  END;

88:
IF (editField IN [minBox,dayBox,yearBox]){ Set high-order digit             }
AND (editTime[offset+1] = ' ')
THEN EditChar (offset, '0')              { Some high-order digits can't be ' '}
ELSE EditChar (offset, editTime[offset+1]);
EditChar (offset+1, char2);              { Set low-order digit              }
fieldPos := 3;
(*NextField (tab);*)                     { Finished with this field         }

99:
EditSecondChar := func;

IF trcFEntry THEN WRITELN ('   exit EditSecondChar:',func);
END;
{$ENDC}

{$IFC R}
{--------------------------------------------------------------------------}
{  EditHour                                                                }
{--------------------------------------------------------------------------}

FUNCTION EditHour (newChar: CHAR) : BOOLEAN;
VAR
    func:  BOOLEAN;

BEGIN
IF trcFEntry THEN WRITELN ('EditHour');

func := FALSE;
CASE fieldPos OF
  1: BEGIN
     func := EditFirstChar (hourOffset, newChar);
   (*IF func AND ValidChar (newChar, twoToNine)  { If 2-9,  complt field}
     THEN func := EditSecondChar (hourOffset, ' ');*)
     END;
  2: BEGIN
     IF ((editTime[hourOffset+1] IN [' ','0'])       { hour is 0-9        }
          AND ValidChar (newChar, numChar))
     OR ((editTime[hourOffset+1] = '1')              { hour is 10-12      }
          AND ValidChar (newChar, twoOrLess))
     OR ValidChar (newChar, advChar)
     THEN func := EditSecondChar (hourOffset, newChar)
     ELSE func := FALSE;
     END;
  END;

EditHour := func;

IF trcFEntry THEN WRITELN ('   exit EditHour');
END;
{$ENDC}

{$IFC R}
{--------------------------------------------------------------------------}
{  EditMin                                                                 }
{--------------------------------------------------------------------------}

FUNCTION EditMin (newChar: CHAR) : BOOLEAN;
VAR
    func:  BOOLEAN;

BEGIN
IF trcFEntry THEN WRITELN ('EditMin');

func := FALSE;
CASE fieldPos OF
  1: BEGIN
     func := EditFirstChar (minOffset, newChar);
     IF ValidChar (newChar, sixToNine) THEN EditChar (minOffset, '0');
     END;
  2: BEGIN
     IF (NOT ValidChar (editTime[minOffset+1], sixToNine))
     OR ValidChar (newChar, advChar)
     THEN func := EditSecondChar (minOffset, newChar)
     ELSE func := FALSE;
     END;
  END;

EditMin := func;

IF trcFEntry THEN WRITELN ('   exit EditMin');
END;
{$ENDC}

{$IFC R}
{--------------------------------------------------------------------------}
{  EditAmPm                                                                }
{--------------------------------------------------------------------------}

FUNCTION EditAmPm (newChar: CHAR) : BOOLEAN;
VAR
    func:  BOOLEAN;

BEGIN
IF trcFEntry THEN WRITELN ('EditAmPm');

func := TRUE;
IF ValidChar (newChar, advChar)
THEN NextField (newChar)
ELSE BEGIN
  func := ValidChar (newChar, ampmChar);
  IF func
  THEN BEGIN
    InvertField (ampmBox);
    IF newChar IN ['a','A']
    THEN EditChar (ampmOffset, 'a')
    ELSE EditChar (ampmOffset, 'p');
    fieldPos := 2;
  (*NextField (tab);*)
    END;
  END;

EditAmPm := func;

IF trcFEntry THEN WRITELN ('   exit EditAmPm');
END;
{$ENDC}

{$IFC R}
{--------------------------------------------------------------------------}
{  EditMonth                                                               }
{--------------------------------------------------------------------------}

FUNCTION EditMonth (newChar: CHAR) : BOOLEAN;
VAR
    func:  BOOLEAN;

  {--------------------------------------------------------------------------}

  FUNCTION DayInRange : BOOLEAN;
  LABEL 88;

  VAR func:  BOOLEAN;
      mo:    Tmonth;

  BEGIN
  IF trcFEntry THEN WRITELN ('DayInRange');

  func := TRUE;
  mo := WhichMonth;

  CASE editTime[dayOffset] OF        { Check 10's digit                      }
   ' ',
   '0',
   '1': EXIT (DayInRange);           { In range                              }
   '2': IF mo <> Fe THEN EXIT (DayInRange);
   '3': IF mo = Fe THEN GOTO 88;
    OTHERWISE GOTO 88;               { bulletproofing, should never get here }
    END;

  CASE editTime[dayOffse+1] OF       { Check 1's digit                       }
   '1': IF NOT (mo IN [Ap,Jn,Se,No]) THEN EXIT (DayInRange);
   '9': IF (mo <> Fe) OR leapYear OR (editTime[dayOffset] <> '2')
           THEN EXIT (DayInRange);
    OTHERWISE EXIT (DayInRange);
    END;

  88:
  EditChar (dayOffset, '0');        { Out of range, reset to '1' }
  EditChar (dayOffset+1, '1');
  func := FALSE;

  IF trcFEntry THEN WRITELN ('DayInRange:',func);
  END;

{--------------------------------------------------------------------------}

BEGIN
IF trcFEntry THEN WRITELN ('EditMonth');

func := FALSE;
CASE fieldPos OF
  1: BEGIN
     func := EditFirstChar (moOffset, newChar);
     IF func THEN IF NOT DayInRange THEN;
     END;
  2: BEGIN
     IF ((editTime[moOffset+1] IN [' ','0'])       { month is 0-9        }
          AND ValidChar (newChar, numChar))
     OR ((editTime[moOffset+1] = '1')              { month is 10-12      }
          AND ValidChar (newChar, twoOrLess))
     OR ValidChar (newChar, advChar)
     THEN BEGIN
       func := EditSecondChar (moOffset, newChar);
       IF func THEN IF DayInRange THEN ;
       END
     ELSE func := FALSE;
     END;
  END;

EditMonth := func;

IF trcFEntry THEN WRITELN ('   exit EditMonth');
END;
{$ENDC}

{$IFC R}
{--------------------------------------------------------------------------}
{  EditDay                                                                 }
{--------------------------------------------------------------------------}

FUNCTION EditDay (newChar: CHAR) : BOOLEAN;
VAR
    curMonth:  Tmonth;
    func:      BOOLEAN;

BEGIN
IF trcFEntry THEN WRITELN ('EditDay');

func := FALSE;
curMonth := WhichMonth;
CASE fieldPos OF
  1: BEGIN
     func := EditFirstChar (dayOffset, newChar);
     IF ((curMonth = Fe) AND (newChar > '2')) OR ValidChar (newChar, fourToNine)
     THEN EditChar (dayOffset, '0');                  { Complete field   }
     END;

  2: BEGIN
     IF newChar <> ' '
     THEN CASE curMonth OF
       Fe: IF ValidChar (newChar, advChar)
           OR ValidChar (editTime[dayOffset+1], oneOrLess)
           OR ((editTime[dayOffset+1] = '2') AND ((newChar < '9') OR leapYear))
           THEN func := EditSecondChar (dayOffset, newChar)
           ELSE func := FALSE;

       Ap, Jn, Se, No:
           IF ValidChar (newChar, advChar)
           OR ValidChar (editTime[dayOffset+1], twoOrLess)
           OR ((editTime[dayOffset+1] = '3') AND (newChar IN [' ','0']))
           THEN func := EditSecondChar (dayOffset, newChar)
           ELSE func := FALSE;

       OTHERWISE
           IF ValidChar (newChar, advChar)
           OR ValidChar (editTime[dayOffset+1], twoOrLess)
           OR ((editTime[dayOffset+1] = '3') AND ValidChar (newChar, oneOrLess))
           THEN func := EditSecondChar (dayOffset, newChar)
           ELSE func := FALSE;
       END
     ELSE func := EditSecondChar (dayOffset, newChar);
     END;
  END;

EditDay := func;

IF trcFEntry THEN WRITELN ('   exit EditDay');
END;
{$ENDC}

{$IFC R}
{--------------------------------------------------------------------------}
{  EditYear                                                                }
{--------------------------------------------------------------------------}

FUNCTION EditYear (newChar: CHAR) : BOOLEAN;

BEGIN
IF trcFEntry THEN WRITELN ('EditYear');

EditYear := FALSE;
CASE fieldPos OF
  1: BEGIN
     IF (newChar = '8') OR (newChar = '9')
     THEN EditYear := EditFirstChar (yearOffset, newChar);
     END;
  2: BEGIN
     IF (editTime[yearOffset+1] <> '9')
     OR ((newChar >= '0') OR (newChar <= '5'))
     THEN EditYear := EditSecondChar (yearOffset, newChar);
     END;
  {*** verify feb 29 and leapyear ***}
  END;

IF trcFEntry THEN WRITELN ('   exit EditYear');
END;
{$ENDC}

{$IFC R}
{--------------------------------------------------------------------------}
{  ProcessKey                                                              }
{--------------------------------------------------------------------------}

FUNCTION ProcessKey (newChar: CHAR) : BOOLEAN;
BEGIN
IF trcFEntry THEN WRITELN ('ProcessKey: ',ORD(newChar):1,
                           ', fieldPos = ',fieldPos:1);
ProcessKey := TRUE;

IF fieldPos = 3
THEN IF ValidChar(newChar, advChar)
     THEN NextField (newChar)
     ELSE ProcessKey := FALSE

ELSE CASE editField OF
       hourBox: ProcessKey := EditHour (newChar);
       minBox:  ProcessKey := EditMin (newChar);
       ampmBox: ProcessKey := EditAmPm (newChar);
       moBox:   ProcessKey := EditMonth (newChar);
       dayBox:  ProcessKey := EditDay (newChar);
       yearBox: ProcessKey := EditYear (newChar);
       noBox:   IF ValidChar (newChar, advChar)
                THEN NextField (newChar)
                ELSE ProcessKey := FALSE;
       END;

IF trcFEntry THEN WRITELN('   exit ProcessKey');
END;
{$ENDC}

{$IFC R}
{--------------------------------------------------------------------------}
{  SetEditField                                                            }
{--------------------------------------------------------------------------}

FUNCTION SetEditField (where: Point) : Twhere;
VAR
    thisField:  Twhere;

BEGIN
IF trcFEntry THEN WRITELN ('SetEditField');

SetEditField := editField;               { For error exit                  }

thisField := PtInField (where);          { Which field to select           }
SetEditField := thisField;
IF thisField = noBox
THEN EXIT (SetEditField)
ELSE downIn := inField;

CASE fieldPos OF
  1: IF editField <> ThisField           { De-hilite current field         }
     THEN InvertField (editField)
     ELSE EXIT (SetEditField);           { Prevents flickering             }
  2: IF NOT ProcessKey (' ')
     THEN BEGIN                          { Can't complete field            }
       RingBell;
       EXIT (SetEditField);
       END;
  END;
InvertField (thisField);                 { Hilite the new field            }
editField := thisField;                  { Set field state variables       }
fieldPos := 1;

IF trcFEntry THEN WRITELN ('   exit SetEditField');
END;
{$ENDC}

{$IFC R}
{------------------------------------------------------------------------------}
{  ProcessEvent                                                                }
{------------------------------------------------------------------------------}

PROCEDURE ProcessEvent (curEvent: EventRecord);
VAR whichButn: TButn;
BEGIN
IF trcFEntry THEN WRITELN('ProcessEvent: what = ',ORD(curEvent.what):1);

CASE curEvent.what OF

   buttonDown :
      BEGIN
      IF trcFEntry THEN WRITELN('   button down');
      IF curEvent.who = dialogFolder
      THEN BEGIN
        IF ButnPushed (doneButn, doneButn, whichButn, curEvent.where)
        THEN BEGIN
          IF trcFEntry THEN WRITELN('   done button pushed');
          donePushed := TRUE;
          END
        ELSE IF SetEditField (curEvent.where) <> noBox
             THEN downIn := inField
             ELSE downIn := inNoMansLand;
        END
      ELSE BEGIN
        IF trcFEntry THEN WRITELN('   button down out of dialog box');
        abortDialog := TRUE;  { Button down outside of box cancels dialog }
        END;
      END;

   buttonUp :
      BEGIN
      IF trcFEntry THEN WRITELN('   button up');
      downIn := notDown;
      END;

   folderMoved:  IF trcFEntry THEN WRITELN('   folder moved');

   folderUpdate:
      BEGIN
      IF trcFEntry THEN WRITELN('   folderUpdate');
      BeginUpdate (curEvent.who);
      EndUpdate (curEvent.who);
      END;

   keyDown :
      BEGIN
      IF trcFEntry THEN WRITELN('   key down');
      IF downIn = notDown
      THEN IF NOT ProcessKey (curEvent.ascii)
           THEN RingBell                       { Invalid key         }
           ELSE
      ELSE RingBell;                           { Mouse button is down}
      END;

   END;

IF trcFEntry THEN WRITELN ('   exit ProcessEvent');
END;
{$ENDC}

{$IFC R}
{------------------------------------------------------------------------------}
{  TrackCursor                                                                 }
{------------------------------------------------------------------------------}

PROCEDURE TrackCursor;
VAR
    mousePt:   Point;
    newField:  Twhere;

BEGIN
GetMouse (mousePt);

IF (PtInField(mousePt)<>noBox) = arrowCrs
                                      { If mouse over a field and arrowCrs...  }
                                      { ...OR not over a field and not arrowCrs}
THEN BEGIN                            { Change cursor                          }
  IF arrowCrs
  THEN SetCursor (LCcross)
  ELSE SetCursor (arrow);
  arrowCrs := NOT arrowCrs;
  END;

CASE downIn OF                        { Adjust field and button hiliting       }

  inField: newField := SetEditField(mousePt); { Use mouse to select edit field }

  OTHERWISE ;                         { Ignore if button up or down elsewhere  }
  END;

END;
{$ENDC}


{**************************************************************************}
{*                         External Procedures                            *}
{**************************************************************************}


{$S FInit }

{--------------------------------------------------------------------------}
{  InitSetTime                                                             }
{--------------------------------------------------------------------------}

PROCEDURE InitSetTime;
VAR
    doneBtnRow: INTEGER;
    doneBtnCol: INTEGER;
    fontInfo:  FontInfo;
    fmErr:     INTEGER;
    lFontID:   TLFntID;
    savePort:  GrafPtr;

BEGIN
IF trcFEntry THEN WRITELN ('InitSetTime');

{$IFC R}
  { Init Form layout }

doneBtnRow := 3;
doneBtnCol := 55;
editCol := 21;
editRow := doneBtnRow;

WITH lFontID DO BEGIN
  fam := timeFont;
  setEFace := [];
  dev := devScreen;
  END;

IF NOT FMFontMetrics (lFontID, fontInfo, fmErr)
THEN BEGIN
  WRITELN ('Unable to get p10Tile font metrics');
  HALT;
  END;
IF fmErr <> 0 THEN WRITELN ('FontMetrics error: ',fmErr:1);

WITH fontInfo DO BEGIN
  rowHt := ascent + descent + leading + 4;
  sysAscent := ascent;
  sysDescent := descent;
  END;

GetPort (savePort);
SetPort (dialogFolder);
TextFont (timeFont);
numWidth := CharWidth('0');
setPort (savePort);

  { Init edit support }

SetRect (editRect, editCol*numWidth, (editRow-1)*rowHt+sysDescent+2,
         (editCol+1)*numWidth, editRow*rowHt+sysDescent+2);
WITH editRect
DO BEGIN
  SetRect (hourRect, left+(hourOffset*numWidth), top,
           left+((hourOffset+2)*numWidth), bottom);
  SetRect (minRect, left+(minOffset*numWidth), top,
           left+((minOffset+2)*numWidth), bottom);
  SetRect (ampmRect, left+(ampmOffset*numWidth), top,
           left+((ampmOffset+3)*numWidth), bottom);
  SetRect (moRect, left+(moOffset*numWidth), top,
           left+((moOffset+2)*numWidth), bottom);
  SetRect (dayRect, left+(dayOffset*numWidth), top,
           left+((dayOffset+2)*numWidth), bottom);
  SetRect (yearRect, left+(yearOffset*numWidth), top,
           left+((yearOffset+2)*numWidth), bottom);
  donePt.v := doneBtnRow*rowHt-sysAscent-4;
  donePt.h := doneBtnCol*numWidth;
  END;

bs := CHR (bsCode);
ntr := CHR (ntrCode);
rtn := CHR (rtnCode);
tab := CHR (tabCode);
{$ENDC}

julianDays[0] := 0;
julianDays[1] := endJa;
julianDays[2] := endFe;
julianDays[3] := endMr;
julianDays[4] := endAp;
julianDays[5] := endMy;
julianDays[6] := endJn;
julianDays[7] := endJl;
julianDays[8] := endAu;
julianDays[9] := endSe;
julianDays[10] := endOc;
julianDays[11] := endNo;
julianDays[12] := endDe;

{$IFC R}
  { Init development aids }

StuffHex(@LCcross.Mask,
          '0000000003C003C003C003C0FFFFFFFFFFFF03C003C003C003C0000000000000');
StuffHex(@LCcross.Data,
          '0000000003C0024002400240FC3F8001FC3F02400240024003C0000000000000');
LCcross.hotSpot.h := 8;
LCcross.hotSpot.v := 8;
{$ENDC}

IF trcFEntry THEN WRITELN ('   exit InitSetTime');
END;


{$S SetTime }

{--------------------------------------------------------------------------}
{  JulianToMoDay                                                           }
{--------------------------------------------------------------------------}

PROCEDURE JulianToMoDay {julian: INTEGER; leapYear: BOOLEAN; VAR mo, day: INTEGER};
VAR i:  INTEGER;
BEGIN
IF trcFEntry THEN WRITELN ('JulianToMoDay: ',julian:1);

i := 12;
IF leapYear AND (julian > endFe) THEN julian := julian - 1; { keep in month }
WHILE julianDays[i] >= julian DO i := i - 1;
mo := i+1;
day := julian - julianDays[i];
IF leapYear AND (julian >= endFe) THEN day := day + 1; { adjust for above sub}

IF trcFEntry THEN WRITELN ('   exit JulianToMoDay: mo is ',mo:1,', day is ',day:1);
END;


{$IFC R}
{------------------------------------------------------------------------------}
{  SetTimeDialog                                                               }
{------------------------------------------------------------------------------}

PROCEDURE SetTimeDialog;
VAR curEvent:  EventRecord;
    err:       INTEGER;
    savePort:  GrafPtr;

BEGIN
IF trcFEntry THEN WRITELN ('SetTimeDialog');

abortDialog := FALSE;
donePushed := FALSE;
edited := FALSE;

GetPort (savePort);
SetPort (dialogFolder);
ShowForm;

REPEAT
   IF PeekEvent (curEvent)
   THEN BEGIN
     IF trcFEntry THEN WRITELN ('   event ',curEvent.what:1);
     IF curEvent.what IN [buttonDown, buttonUp, keyDown, folderMoved, folderUpdate]
     THEN BEGIN
       IF (curEvent.what = buttonDown) AND (curEvent.who <> dialogFolder)
       THEN abortDialog := TRUE           { Leave this event in the queue and quit }
       ELSE BEGIN
         GetEvent(curEvent);
         ProcessEvent(curEvent);
         END;
       END
     ELSE abortDialog := TRUE;
     END;
   IF NOT donePushed OR abortDialog THEN TrackCursor;
UNTIL donePushed OR abortDialog;
IF trcFEntry THEN WRITELN('   donePushed is',donePushed,', abortDialog is',abortDialog);

IF donePushed AND edited THEN ResetTime (err);
HideButn (doneButn);
DialogHeight (0, FALSE);
SetPort (savePort);

IF trcFEntry THEN WRITELN ('   exit SetTimeDialog');
END;
{$ENDC}


{--------------------------------------------------------------------------}
{  TimeToStr                                                               }
{--------------------------------------------------------------------------}

PROCEDURE TimeToStr {theTime: LONGINT; VAR dateStr,timeStr: String10};

VAR date:     DateArray;
    day:      INTEGER;
    dayStr:   NumberStr;
    hourStr:  NumberStr;
    leapYear: BOOLEAN;
    minuteStr:NumberStr;
    month:    INTEGER;
    monthStr: NumberStr;
    yearStr:  NumberStr;

BEGIN
IF trcFEntry THEN WRITELN ('TimeToStr');

TimeToDate(theTime,date);

leapYear := (date.year MOD 4) = 0;
JulianToMoDay(date.day,leapYear,month,day);

IntToStr(month,monthStr);
IntToStr(day,dayStr);
IntToStr(date.year-1900,yearStr);
dateStr := CONCAT(monthStr,'/',dayStr,'/',yearStr);

IntToStr(date.hour,hourStr);
IntToStr(date.minute,minuteStr);
timeStr := CONCAT(hourStr,':',minuteStr);

IF trcFEntry THEN WRITELN ('   exit TimeToStr');
END;



END.
ÿ