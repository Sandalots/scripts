(* ********************************************************************* *)

(* CopyRight 1983,1984 Apple Computer Inc. *)
(* Ge Utility Routines *)

(* ********************************************************************* *)

UNIT Utl;
    {$SetC utlSampcalls := FALSE}
(* Segment UTL *)
INTERFACE
USES
    (*$U UNITSTD *)    UnitStd,
    (*$U UNITHZ*)      UnitHz,
    (*$U STORAGE*)     Storage,
    (*$U QUICKDRAW*)   QuickDraw,
    (*$U FONTMGR*)     FontMgr,
    (*$U LibOS/Syscall *)    Syscall,
    {$U LibPm/PmDecL}      PmDecL,
    {$U PrStdInfo}     PrStdInfo,
    {$U PrPublic}      PrPublic,

    {$IFC utlSampcalls}
    {$U apld/tracecalls.obj}  tracecalls,
    {$ENDC}

    (*$U ApLd/HDR*)    Hdr;
    function  GESqrt(x: real): real;
    {function  EqualPt(srcPt, dstPt: Point): boolean;  NOW IN QUICKDRAW}
    procedure IntToStr(i: integer; VAR S: String6);
    procedure GetDistStr(dist: Real; VAR distStr: String80);
    function  RMin(x,y: Real): Real;

    PROCEDURE WldToScrn(wld2D: Point2D; VAR scrnPt: Point);
    PROCEDURE ScrnToWld(scrnPt: Point; VAR wld2D: Point2D);

    PROCEDURE SetPt2D(VAR p2D: Point2D; x,y: REAL);
    PROCEDURE AddPt2D(src2D: Point2D; VAR dst2D: Point2D);
    PROCEDURE SubPt2D(src2D: Point2D; VAR dst2D: Point2D);

    PROCEDURE SetRect2D(VAR r2D: Rect2D; lft,top,rt,bot: REAL);
    PROCEDURE R2DFmP2D(p1,p2: Point2D; VAR dst2D: Rect2D);
    PROCEDURE R2DToRect(r2D: Rect2D; VAR r: Rect);
    PROCEDURE RectToR2D(r: Rect; VAR r2D: Rect2D);
    PROCEDURE RectCtr2D(r: Rect; VAR ctr2D: Point2D);

    PROCEDURE AngleToPt(r: Rect; ang: Integer; VAR pt: Point);
    procedure GetAngles(aStart,aAngle: Integer; VAR a1,a2: Integer);

    procedure GridPoint(VAR pt: Point);
    procedure GridP2D(VAR p2D: Point2D);

    procedure GetPenRect(pnSiz: Integer; VAR r: Rect);
    procedure GetDrwRect(r2d: Rect2D; pnSiz: Integer; VAR r: Rect);

    procedure RotateBox(VAR r: Rect; ctr2D: Point2D; dir: Integer);
    procedure RotateR2D(VAR r2D: Rect2D; ctr2D: Point2D; dir: Integer );
    procedure Rotate2D(VAR p2D: Point2D; ctr2D: Point2D; dir: Integer );

    (* for zoom: *)
    procedure GEMapPt    (Var pt: Point);
    procedure GEMapRect  (Var r: Rect);
    procedure GEUnMapPt  (Var pt: Point);
    procedure GEUnMapRect(Var r: Rect);
    procedure GEClipRect (r: Rect);

(* ********************************************************************* *)

IMPLEMENTATION


 (********************************************************************************)
(*$S Core2*)
    function  GESqrt(* x: real): real *);
    (* Based on Newton's method, only need 4 place accuracy. *)
    VAR Aprx: real;
        i: integer;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        GESqrt := 0;
        if x = 1 then
            GESqrt := 1
        else if x > 0 then begin
            Aprx := x/2;
            for i := 1 to 20 do
                Aprx := (x/(Aprx*Aprx) + 1) * Aprx/2;
            GESqrt := Aprx;
        end else if x=0 then
            GESqrt := 0;
    end; (* GESqrt *)


(********************************************************************************)
(*$S Core2*)
    procedure IntToStr(* i: integer; VAR S: String6 *);
    VAR NegFlag: boolean;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        NegFlag := (i < 0);
        i := abs(i);
        S := '';
        repeat
            S := concat(' ',S);
            S[1] := chr(48 + i mod 10);
            i := i div 10;
        until i = 0;
        if NegFlag then
            S := concat('-',S);
    end; (* IntToStr *)

(* ********************************************************************* *)
(*$S Core2*)
    procedure GetDistStr(* dist: Real; VAR distStr: String80 *);
    VAR s: String6;
        frac: Real;
        idist,ifrac: Integer;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        frac  := dist-Trunc(dist);
        idist := Abs(Round(dist-frac));
        ifrac := Abs(Round(100*frac));
        if ifrac>=100 then begin
            ifrac := 0;
            idist := idist+1;
        end;
        IntToStr(idist,s);
        distStr := s;
        IntToStr(ifrac,s);
        if s='0' then
            s := '00'
        else if ifrac<10 then
            s := Concat('0',s);
        distStr := Concat(distStr,'.',s);
    end; (* GetDistStr *)

(* ********************************************************************* *)
(*$S Ring2*)
    function  RMin(* x,y: Real): Real *);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        RMin := x;
        if y<x then
            RMin := y;
    end; (*  *)

(* ********************************************************************* *)
(*$S Core2*)
    FUNCTION  RndToInt(r: Real): Integer;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
      if r>32767 then
          r := 32767
      else if r<-32768 then
          r := -32768;
      RndToInt := Round(r);
    end;

    PROCEDURE WldToScrn(* wld2D: Point2D; VAR scrnPt: Point *);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
      scrnPt.h := RndToInt(wld2D.x);
      scrnPt.v := RndToInt(wld2D.y);
    END;

    PROCEDURE ScrnToWld(* scrnPt: Point; VAR wld2D: Point2D *);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
      wld2D.x := scrnPt.h;
      wld2D.y := scrnPt.v;
    END;

    PROCEDURE SetPt2D(* VAR p2D: Point2D; x,y: REAL *);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
      p2D.x := x;
      p2D.y := y;
    END;

    PROCEDURE AddPt2D(* src2D: Point2D; VAR dst2D: Point2D *);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
      dst2D.x := dst2D.x + src2D.x;
      dst2D.y := dst2D.y + src2D.y;
    END;

    PROCEDURE SubPt2D(* src2D: Point2D; VAR dst2D: Point2D *);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        dst2D.x := dst2D.x-src2D.x;
        dst2D.y := dst2D.y-src2D.y;
    END;

    PROCEDURE SetRect2D(* VAR r2D: Rect2D; lft,top,rt,bot: REAL *);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
      with r2D do begin
          xL := lft;
          yT := top;
          xR := rt;
          yB := bot;
      end;
    END;

    PROCEDURE R2DFmP2D(* p1,p2: Point2D; dst2D: Rect2D *);

        function  MinReal(r1,r2: Real): Real;
        begin
        {$IFC sampcalls} ALogCall; {$ENDC}
            if r1 < r2 then
                MinReal := r1
            else
                MinReal := r2;
        end; (* MinReal *)

        function  MaxReal(r1,r2: Real): Real;
        begin
        {$IFC sampcalls} ALogCall; {$ENDC}
            if r1 > r2 then
                MaxReal := r1
            else
                MaxReal := r2;
        end; (* MaxReal *)

    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        SetRect2D(dst2D,MinReal(p1.x,p2.x),MinReal(p1.y,p2.y),
                        MaxReal(p1.x,p2.x),MaxReal(p1.y,p2.y));
    END; (* DragR2D *)

    PROCEDURE R2dToRect(* r2D: Rect2D; VAR r: Rect *);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        WldToScrn(r2D.topLeft,r.topLeft);
        WldToScrn(r2D.botRight,r.botRight);
    END; (* R2dToRect *)

    procedure RectToR2D(* r: Rect; VAR r2D: Rect2D *);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        r2D.xL := r.left;
        r2D.yT := r.top;
        r2D.xR := r.right;
        r2D.yB := r.bottom;
    end; (* RectToR2D *)

(*$S Ring2*)
    PROCEDURE RectCtr2D(* r: Rect; VAR ctr2D: Point2D *);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        with r do
        SetPt2D(ctr2D,(left+right)/2.0,(top+bottom)/2.0);
    end;

function SlopeFromAngle(angle: Integer): LongInt; EXTERNAL;

(* ********************************************************************* *)
(*$S Core2*)
    procedure AngleToPt(* r: Rect; ang: Integer; VAR pt: Point *);
    const twoPi = 6.2832;
    VAR x,y,px,py,rxSqrd,rySqrd,aspect,m,rAngle: Real;
        radius: Point2D;
        ctrPt:  Point;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        ang := ang MOD 360;
        if ang<0 then
            ang := 360+ang;
        SetPt(ctrPt,(r.right+r.left) div 2,(r.top+r.bottom) div 2);
        if ang=0 then
            SetPt(pt,ctrPt.h,r.top)
        else if ang=90 then
            SetPt(pt,r.right,ctrPt.v)
        else if ang=180 then
            SetPt(pt,ctrPt.h,r.bottom)
        else if ang=270 then
            SetPt(pt,r.left,ctrPt.v)
        else begin
            SetPt2D(radius,(r.right-r.left)/2.0,(r.bottom-r.top)/2.0);
            rxSqrd := radius.x*radius.x;
            rySqrd := radius.y*radius.y;
            aspect := radius.y/radius.x;

            m := aspect*65536/SlopeFromAngle(ang);
            px := GESqrt((rxSqrd*rySqrd)/(m*m*rxSqrd+rySqrd));
            if ang>180 then
                px := -px;
            py := m*px;
            pt.h := RndToInt(px);
            pt.v := RndToInt(py);
            AddPt(ctrPt,pt);
        end;
    end; (* AngleToPt *)

(* ********************************************************************* *)

    procedure GetAngles(* aStart,aAngle: Integer; VAR a1,a2: Integer *);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        (* returns sorted angles greater than zero *)
        if aAngle<0 then begin
            aStart := aStart+aAngle;
            aAngle := -aAngle;
        end;
        aStart := aStart MOD 360;
        if aStart<0 then
            aStart := 360+aStart;
        a1 := aStart;
        if aAngle>360 then
            aAngle := 360;
        a2 := aStart+aAngle;
    end; (* GetAngles *)

(* ********************************************************************* *)
(*$S Ring2*)
    procedure GridPoint(* VAR pt: Point *);
    VAR this2D: Point2D;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        ScrnToWld(pt,this2D);
        GridP2D(this2D);
        WldToScrn(this2D,pt);
    end; (* GridPoint *)

(* ********************************************************************* *)
(*$S Core2*)
    procedure GridP2D(* VAR p2D: Point2D *);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        p2D.x := Round(p2D.x/curDoc.atr.grdSz2D.x)*curDoc.atr.grdSz2D.x;
        p2D.y := Round(p2D.y/curDoc.atr.grdSz2D.y)*curDoc.atr.grdSz2D.y;
    end; (* GridP2D *)

(* ********************************************************************* *)

    procedure GetPenRect(* pnSiz: Integer; VAR r: Rect *);
    var thisSz: Point;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        thisSz := penSizes[pnSiz];
        if curDoc.atr.zoomOn<>0 then
            ScalePt(thisSz,zoomSrcR,zoomDstR);
        with thisSz do begin
            (* get pen offset *)
            r.left   := r.left  -(h Div 2);
            r.top    := r.top   -(v Div 2);
            r.right  := r.right +(h+1) Div 2;
            r.bottom := r.bottom+(v+1) Div 2;
        end;
    end; (* GetPenRect *)

(* ********************************************************************* *)

    procedure GetDrwRect(* r2d: Rect2D; pnSiz: Integer; VAR r: Rect *);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        R2DToRect(r2D,r);
        GEMapRect(r);
        GetPenRect(pnSiz,r);
    end; (* GetDrwRect *)

(* ********************************************************************* *)

    procedure Rotate2D(* VAR p2D: Point2D; ctr2D: Point2D; dir: Integer *);
    var scaleX,scaleY: Real;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        (* direction can be 1..7 *)
        with p2D do begin
            x := x-ctr2D.x;
            y := y-ctr2D.y;
            scaleX := x*aspRatio;
            scaleY := y/aspRatio;
            if dir=1 then begin                 {  90 degrees clockwise }
                y := scaleX;
                x := -scaleY;
            end else if dir=2 then begin        { 180 degrees clockwise }
                x := -x;
                y := -y;
            end else if dir=3 then begin        { 270 degrees clockwise }
                y := -scaleX;
                x := scaleY;
            end else if dir=4 then begin        { Flip Horiz }
               {y := y;}
                x := -x;
            end else if dir=5 then begin        { Flip Vert }
                y := -y;
               {x := x;}
            end else if dir=6 then begin        { Rotate 90 and Flip Horiz }
                y := scaleX;
                x := scaleY;
            end else if dir=7 then begin        { Rotate 90 and Flip Vert }
                y := -scaleX;
                x := -scaleY;
            end;
            x := x+ctr2D.x;
            y := y+ctr2D.y;
        end;
    end;

(* ********************************************************************* *)

    procedure RotateR2D(* VAR r2D: Rect2D; ctr2D: Point2D; dir: Integer *);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        (* direction can be 1..7 *)
        Rotate2D(r2D.topLeft,ctr2D,dir);
        Rotate2D(r2D.botRight,ctr2D,dir);
        R2DFmP2D(r2D.topLeft,r2D.botRight,r2D);
    end;

(* ********************************************************************* *)

    procedure RotateBox(* VAR r: Rect; ctr2D: Point2D; dir: Integer *);
    VAR r2D: Rect2D;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        (* direction can be 1..7 *)
        RectToR2D(r,r2D);
        RotateR2D(r2D,ctr2D,dir);
        R2DToRect(r2D,r);
    end;

    (* for zoom: *)
    (*
    procedure GEScalePt(Var pt: Point);
    begin
        if curDoc.atr.zoomOn<>0 then ScalePt(pt,zoomSrcR,zoomDstR);
    end;
    *)

    procedure GEMapPt(* Var pt: Point *);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        {
        SubPt(src.topLeft,pt);
        ScalePt(pt,src,dst);
        AddPt(dst.topLeft,pt);
        }
        if curDoc.atr.zoomOn<>0 then MapPt(pt,zoomSrcR,zoomDstR);
    end;

    procedure GEMapRect(* Var r: Rect *);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        if curDoc.atr.zoomOn<>0 then MapRect(r,zoomSrcR,zoomDstR);
    end;

    procedure GEUnMapPt(* Var pt: Point *);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        {
        SubPt(src.topLeft,pt);
        ScalePt(pt,src,dst);
        AddPt(dst.topLeft,pt);
        }
        if curDoc.atr.zoomOn<>0 then MapPt(pt,zoomDstR,zoomSrcR);
    end;

    procedure GEUnMapRect(* Var r: Rect *);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        if curDoc.atr.zoomOn<>0 then MapRect(r,zoomDstR,zoomSrcR);
    end;

    procedure GEClipRect(* r: Rect *);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        GEMapRect(r);
        ClipRect(r);
    end;

end. (* unit *)


ÿ