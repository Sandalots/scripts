(* ********************************************************************* *)

(* CopyRight 1983, Apple Computer Inc. *)
(* GE Unit INT Include File - not needing units to be swapped in *)

(* ********************************************************************* *)
(*$S Init*)

    (* Init all GE Patterns, bitmaps, cursors, text, etc. not requiring  *)
    (* that any GE units be swapped in, but relying on sys initialized.  *)
    (* ***************************************************************** *)

    procedure BigLetter(letter: Char;Var BigLetPattern: Pattern);
    begin
        Case letter of
            'A': StuffHex(@BigLetPattern,'040A11111F111100');
            'B': StuffHex(@BigLetPattern,'1E11111E11111E00');
            'C': StuffHex(@BigLetPattern,'0E11101010110E00');
            'D': StuffHex(@BigLetPattern,'1E11111111111E00');
            'E': StuffHex(@BigLetPattern,'1F10101C10101F00');
            'F': StuffHex(@BigLetPattern,'1F10101C10101000');
            'G': StuffHex(@BigLetPattern,'0F10101013110F00');
            'H': StuffHex(@BigLetPattern,'1111111F11111100');
            'I': StuffHex(@BigLetPattern,'0E04040404040E00');
            'J': StuffHex(@BigLetPattern,'0101010101090600');
            'K': StuffHex(@BigLetPattern,'1112141814121100');
            'L': StuffHex(@BigLetPattern,'1010101010101F00');
            'M': StuffHex(@BigLetPattern,'111B151511111100');
            'N': StuffHex(@BigLetPattern,'1111191513111100');
            'O': StuffHex(@BigLetPattern,'0E11111111110E00');
            'P': StuffHex(@BigLetPattern,'1E11111E10101000');
            'Q': StuffHex(@BigLetPattern,'0E11111115120D00');
            'R': StuffHex(@BigLetPattern,'1E11111E14121100');
            'S': StuffHex(@BigLetPattern,'0E11100E01110E00');
            'T': StuffHex(@BigLetPattern,'1F04040404040400');
            'U': StuffHex(@BigLetPattern,'1111111111110E00');
            'V': StuffHex(@BigLetPattern,'11111111110A0400');
            'W': StuffHex(@BigLetPattern,'11111115151B1100');
            'X': StuffHex(@BigLetPattern,'11110A040A111100');
            'Y': StuffHex(@BigLetPattern,'11110A0404040400');
            'Z': StuffHex(@BigLetPattern,'1F01020408101F00');
        end;{Case}
    end;

    (* ***************************************************************** *)


    procedure LitLetter(letter:Char; Var letterArray:TLetterArray);
    Var letterString: Str255;
        i: Integer;
    begin
        Case letter of
            'A': letterString := '25575550';
            'B': letterString := '65565560';
            'C': letterString := '25444520';
            'D': letterString := '65555560';
            'E': letterString := '74464470';
            'F': letterString := '74464440';
            'G': letterString := '25475520';
            'H': letterString := '55575550';
            'I': letterString := '72222270';
            'J': letterString := '31111520';
            'K': letterString := '55565550';
            'L': letterString := '44444470';
            'M': letterString := '57555550';
            'N': letterString := '57777750';
            'O': letterString := '25555520';
            'P': letterString := '65564440';
            'Q': letterString := '25555730';
            'R': letterString := '65565550';
            'S': letterString := '25421520';
            'T': letterString := '72222220';
            'U': letterString := '55555570';
            'V': letterString := '55555220';
            'W': letterString := '55555750';
            'X': letterString := '55525550';
            'Y': letterString := '55522220';
            'Z': letterString := '71124470';
            'a': letterString := '02517570';
            'b': letterString := '04447570';
            'c': letterString := '02544520';
            'd': letterString := '01117570';
            'e': letterString := '02574520';
            'f': letterString := '02546440';
            'g': letterString := '00757152';
            'h': letterString := '04447550';
            'i': letterString := '02022220';
            'j': letterString := '00101152';
            'k': letterString := '04445650';
            'l': letterString := '06222270';
            'm': letterString := '00575550';
            'n': letterString := '00577750';
            'o': letterString := '00255520';
            'p': letterString := '00075744';
            'q': letterString := '00075711';
            'r': letterString := '00654440';
            's': letterString := '00342160';
            't': letterString := '04464520';
            'u': letterString := '00555570';
            'v': letterString := '00555220';
            'w': letterString := '00555750';
            'x': letterString := '00552550';
            'y': letterString := '00552220';
            'z': letterString := '00712470';
        end;
        For i := 8 downto 1 do
            Insert('000',letterString,i);
        StuffHex(@letterArray,letterString);
    end;{LitLetter}

    (* ***************************************************************** *)

    procedure GetLetPattern(colorString: Str2; Var initPattern: Pattern);

    Type
        TShortLetArray = Array[1..4] of Integer;
        Transition = record
                   case boolean of
                       TRUE: (pat:Pattern);
                       FALSE:(let:TShortLetArray);
                   end;
    Var firstLetter,secondLetter,compositeLetter:TLetterArray;
        compactLetter: Transition;
        i: Integer;
    begin
        If Length(colorString)=1 then BigLetter(colorString[1],initPattern)
        else begin
            LitLetter(colorString[1],firstLetter);
            LitLetter(colorString[2],secondLetter);
            For i := 1 to 8 do
                compositeLetter[i] := 16*firstLetter[i] + secondLetter[i];
            For i := 1 to 4 do
                compactLetter.let[i] := 256*compositeLetter[2*i-1] +
                                            compositeLetter[2*i];
            initPattern := compactLetter.pat;
        end;
    end;{GetLetPattern}

    (* ***************************************************************** *)

    procedure InitColor;
    Var i: Integer;
    begin
        initPat[iClrWhite] := white;
        initPat[iClrBlack] := black;
        For i := 3 to 8 do
            GetLetPattern(initLet[i],initPat[i]);
    end;{InitColor}

    (* ***************************************************************** *)

    procedure ShowAlphabet(colorString: Str2; ch: Char);
    Var i: Integer;
        dummy: Str2;
    begin
        For i := 1 to 26 do begin
            colorString[1] := ch;
            GetLetPattern(colorString,initPat[3]);
            ReshowSel;
            ch := Succ(ch);
            ReadLn(dummy);
        end;
    end;{ShowAlphabet}

    (* ***************************************************************** *)

    procedure TestColorLet;
    begin
        ShowAlphabet('A','A');
        ShowAlphabet('AB','A');
        ShowAlphabet('AB','a');

    end;

    (* ***************************************************************** *)

    procedure InitGE1;
    var txtFile: Text;
        pName:   pathName;
        pInfo:   ProcInfoRec;
        errNum:  Integer;
        vNum:    Integer;

        {XXX timing vars}
        tLast:    LongInt;
        tStart:   LongInt;

        feString:  Str255;

    (* ***************************************************************** *)

        procedure ReadGEMenus;
        var i: Integer;
        begin
        {$IFC sampcalls} ALogCall; {$ENDC}
            for i := iMnuFirst to iMnuLast do
                ReadMenu(txtFile,geMenus[i]);
        end; (* ReadGEMenus *)

    (* ***************************************************************** *)

        procedure ReadGeCkForm;
        var i,formSize: Integer;
            ckBox: TCkBoxHnd;
        begin
        {$IFC sampcalls} ALogCall; {$ENDC}
            For i := iClrFirst to iClrLast do
                ReadLn(txtFile,initLet[i]);
            (* read in all the main dBox ckForms *)
            (* make sure dialogFldr font is set so stringWidth calcs in
               reading ckBoxes is accurate *)
            SetPort(dialogFolder);
            TextFont(tile12);
            TextFace([]);
            (*$R-*)
            (* dwgSize ckForm *)
            dwgCkForm := Pointer(Ord(HAllocate(geHHz,SizeOf(TCkFormInfo))));
            ReadCkBox(txtFile,ckBox);
            with ckBox^^ do begin
                DrawProc   := @ShowCkBtn;
                HitTstFunc := @CkBtnHit;
            end;
            dwgCkForm^^.ckBoxLst[0] := ckBox;

            (* rnd ckForm *)
            formSize := SizeOf(TCkFormInfo)+3*SizeOf(TCkBoxPtr);
            geRndForm := Pointer(Ord(HAllocate(geHHz,formSize)));
            for i := 0 to 3 do begin
                ReadCkBox(txtFile,ckBox);
                geRndForm^^.ckBoxLst[i] := ckBox;
            end;
            with geRndForm^^.ckBoxLst[0]^^ do begin
                DrawProc   := @ShowCkBtn;
                HitTstFunc := @CkBtnHit;
            end;

            (* ruler ckForm *)
            formSize := SizeOf(TCkFormInfo)+iRlrLast*SizeOf(TCkBoxPtr);
            geRlrForm := Pointer(Ord(HAllocate(geHHz,formSize)));
            for i := 0 to iRlrLast do begin
                ReadCkBox(txtFile,ckBox);
                geRlrForm^^.ckBoxLst[i] := ckBox;
            end;
            with geRlrForm^^.ckBoxLst[0]^^ do begin
                DrawProc   := @ShowCkBtn;
                HitTstFunc := @CkBtnHit;
            end;

            (* initialize all main forms *)
            (* dwgSz ckForm *)
            dwgCkForm^^.numCkBox := 0;
            (* rnd ckForm *)
            with geRndForm^^ do begin
                rndICk := ckBoxLst[1];
                rndMCk := ckBoxLst[2];
                ckBoxLst[2] := ckBoxLst[3];
                numCkBox := 2;
            end;
            CkFormHeight(geRndForm);
            (* ruler ckForm *)
            with geRlrForm^^ do begin
                rlrISpcCk := ckBoxLst[iRlrSpc];
                rlrMSpcCk := ckBoxLst[iRlrMtr];
                ckBoxLst[iRlrAtr]^^.hasOnOff := TRUE;
                ckBoxLst[iRlrKnd]^^.hasOnOff := FALSE;
                ckBoxLst[iRlrLck]^^.hasOnOff := FALSE;
                ckBoxLst[iRlrSpc]^^.hasOnOff := FALSE;
                ckBoxLst[iRlrInc]^^.hasOnOff := FALSE;
                ckBoxLst[iRlrDvs]^^.hasOnOff := FALSE;
                ckBoxLst[iRlrMtr]^^.hasOnOff := FALSE;
                numCkBox := iRlrLast-1;
            end;
            CkFormHeight(geRlrForm);
            (*$IfC rangeOn *)
            (*$R+*)
            (*$EndC *)
        end; (* ReadGeCkForm *)

    (* ***************************************************************** *)

        procedure SetGESBoxes;
        var pt: Point;
            iconSet: TSIcon;
        begin
        {$IFC sampcalls} ALogCall; {$ENDC}
            (* GE Fldr *)
            InitSBList(sbList,theHeap);
            SetPt(pt,0,0);
            hSBox := SBCreate(sbList,NIL,tyvhH,pt,200);
            vSBox := SBCreate(sbList,NIL,tyvhV,pt,200);
            iconSet := [iconArwA,iconArwB,iconPagA,iconPagB,iconThumb];
            SetSBIcons(hSBox,iconSet);
            SetSBIcons(vSBox,iconSet);
            SetThumb(hSBox,0);
            SetThumb(vSBox,0);

            (* scrap *)
            InitSBList(sbListScrp,theHeap);
            SetPt(pt,0,0);
            hSBoxScrp := SBCreate(sbListScrp,NIL,tyvhH,pt,200);
            vSBoxScrp := SBCreate(sbListScrp,NIL,tyvhV,pt,200);
            iconSet := [];
            SetSBIcons(hSBoxScrp,iconSet);
            SetSBIcons(vSBoxScrp,iconSet);
        end; (* SetGESBoxes *)


    (* ***************************************************************** *)

        procedure SetGEPats;
        var i,errNum: Integer;
        begin
        {$IFC sampcalls} ALogCall; {$ENDC}
            fillPats[1] := white;
            penPats[1]  := black;  {Whenever clear is actually drawn
                                    it should be black}
            fillPats[2] := white;
            penPats[2]  := white;
            for i := 3 to 36 do begin
                GetPattern(sysPat,i,penPats[i],errNum);
                if traceOn then
                    if errNum<>0 then
                        Error('SetGEpenPats',errNum,NONFATAL);
                GetPattern(sysPat,i,fillPats[i],errNum);
                if traceOn then
                    if errNum<>0 then
                        Error('SetGEfillPats',errNum,NONFATAL);
            end;
            InitColor;
        end; (* SetGEPats *)

    (* ***************************************************************** *)

        procedure SetGESizes;
        var i: Integer;
        begin
        {$IFC sampcalls} ALogCall; {$ENDC}
            (* Dwg Sizes *)
            (* Rnd Diameter Sizes *)
            SetPt(rndSizes[iUnitInch,0],0, 0);     (* 0" *)
            SetPt(rndSizes[iUnitInch,1],12, 8);    (* 1/8" *)
            SetPt(rndSizes[iUnitInch,2],24,16);    (* 1/4" *)
            SetPt(rndSizes[iUnitInch,3],36,24);    (* 1/3" *)
            SetPt(rndSizes[iUnitInch,4],48,32);    (* 1/2" *)
            SetPt(rndSizes[iUnitInch,5],72,48);    (* 1/1" *)
            SetPt(rndSizes[iUnitInch,0],0, 0);     (* 0" *)
            SetPt(rndSizes[iUnitMtrc,1],15,10);    (* 1/3cm *)
            SetPt(rndSizes[iUnitMtrc,2],37,25);    (* 1/1cm *)
            SetPt(rndSizes[iUnitMtrc,3],45,30);    (* 1/1cm *)
            SetPt(rndSizes[iUnitMtrc,4],60,40);    (* 1/1cm *)
            SetPt(rndSizes[iUnitMtrc,5],76,50);    (* 1/1cm *)

            (* Pen Sizes *)
            SetPt(penSizes[0],0,0);
            SetPt(penSizes[1],1,1);
            SetPt(penSizes[2],3,2);
            SetPt(penSizes[3],6,4);
            SetPt(penSizes[4],9,6);

            (* Ruler dialog mapping *)
            for i := 1 to 10 do
                with rlrInfo[i] do begin
                    spc := 0.5*i;
                    inc := i;
                    dvs := i;
                end;
            rlrInfo[7] := rlrInfo[8];
            rlrInfo[8] := rlrInfo[10];
            with rlrInfo[9] do begin
                inc := 12;
                dvs := 12;
            end;
            with rlrInfo[10] do begin
                inc := 25;
                dvs := 16;
            end;
            aspRatio := cScreenRatio;
        end; (* SetGESizes *)

    (* ***************************************************************** *)

        procedure SetGEDoc;
        var i: Integer;
            dfltString: Str255;
        begin
        {$IFC sampcalls} ALogCall; {$ENDC}
            (* set defaults *)
            with stdDoc do begin
                fldr    := NIL;
                fName   := NIL;
                dwgChgd := FALSE;
                hdrChgd := FALSE;

                (* document proto settings *)
                proto := 0;
                align := cAlnCtr;
                fid.fam := 4;
                fid.seteface := [];
                rndR  := 2;
                arrw[iObjRLin] := 0;
                arrw[iObjSLin] := 0;
                pnPat := iPenBlack;
                pnSiz := 1;
                pnClr := iClrBlack;
                for i := iObjText to iObjOval do
                    fill[i] := iFillWhite;
                for i := iObjArc to iObjPoly do
                    fill[i] := iFillClear;
                for i := iProtoFirst to iObjPoly do
                    fillClr[i] := iClrWhite;
                showPats := TRUE;  {Show Patterns}
                assFlClr := TRUE;  {assign Fill Color}

                (* document attributes *)
                with atr do begin
                    msrOn    := FALSE;
                    rlrOn    := FALSE;
                    grdOn    := TRUE;
                    tckOn    := TRUE;
                    zoomOn   := 0;
                    pgBOn    := TRUE;

                    (* see what the default stationary should be *)
                    GEGetAlert(11,dfltString);
                    if dfltString='CM' then
                        rlrUnit := iUnitMtrc
                    else
                        rlrUnit := iUnitInch;

                    rlrKind  := iRlrStd;

                    (* ruler *)
                    with rlrFmts[iUnitInch,iRlrStd] do begin
                        iSpc := 2;
                        iInc := 1;
                        iDvs := 7;
                    end;
                    with rlrFmts[iUnitMtrc,iRlrStd] do begin
                        iSpc := 1;
                        iInc := 1;
                        iDvs := 5;
                    end;
                    with rlrFmts[iUnitInch,iRlrCustom] do begin
                        iSpc := 2;
                        iInc := 1;
                        iDvs := 10;
                    end;
                    with rlrFmts[iUnitMtrc,iRlrCustom] do begin
                        iSpc := 1;
                        iInc := 1;
                        iDvs := 8;
                    end;

                    rlrLock[iUnitInch,iRlrStd]    := TRUE;
                    rlrLock[iUnitMtrc,iRlrStd]    := TRUE;
                    rlrLock[iUnitInch,iRlrCustom] := FALSE;
                    rlrLock[iUnitMtrc,iRlrCustom] := FALSE;

                    SetPt(rlrZero[iUnitInch,iRlrStd   ],0,0);
                    SetPt(rlrZero[iUnitInch,iRlrCustom],0,0);
                    SetPt(rlrZero[iUnitMtrc,iRlrStd   ],0,0);
                    SetPt(rlrZero[iUnitMtrc,iRlrCustom],0,0);

                    SetPt(dwgSz,768,640);    (* 8x10 *)
                    pageSz := dwgSz;         (* 8x10 *)
                    SetPt(scrollPt,0,0);
                    grdSz2D.x := xInchDots/8 (* rlrInfo[iDvs].dvs *);
                    grdSz2D.y := yInchDots/8 (* rlrInfo[iDvs].spc *);
                    tckSz2D.x := xInchDots;
                    tckSz2D.y := yInchDots;
                end;

                (* init heaps separately for each doc *)
                hHz   := NIL;
                pHz   := NIL;

                (* document objects, selection *)
                InitODB(oLst);
                sLst := NIL;

                (* edit state *)
                edtObj   := NIL;
                edtState := iObjNil;
                edtStart := FALSE;

                (* text state *)
                txtObj := NIL;
                fldSet := FALSE;
                fState.lpFst :=  0;
                fState.lpFst :=  0;
                fState.tySel :=  0;
                fState.tyHL  :=  0;
            end;
            curDoc := stdDoc;
        end; (* SetGEDoc *)
(* ********************************************************************* *)

    procedure FailedAlert;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        StopAlert(geAlerts,2);
        Halt;
    end; (* FailedAlert *)

(* ********************************************************************* *)

    procedure FailedInit;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        Error('Failed Alert Init',alertError,FATAL);
    end; (* FailedAlert *)

    (* ***************************************************************** *)

    begin
        {$IFC sampcalls} ALogCall; {$ENDC} (* InitGE1 *)
        {XXX timing}
        if traceInit then begin
            tStart := Time;
            tLast  := Time;
            WrtTime('InitGE1',tLast);
        end;

        (* tracing flags *)
        (*$IfC traceOn *)
        traceTrk   := FALSE;
        traceUop   := FALSE;
        traceFld   := FALSE;
        TxtFldTrace(FALSE);
        (*$EndC *)

        (* read in all text menus, dialog and messages *)

        (* get the name of the file *)
        Info_Process(errNum,My_ID,pInfo);
        pName := Copy(pInfo.progPathName,1,Pos('}',pInfo.progPathName));
        InitMP(geHHz, pName, @FailedAlert, 36, @geMenus[iMnuFirst], geAlerts,
               errNum);
        if alertError>0 then
            FailedInit;
        if rangeOn then
            if alertError<0 then begin
                {BeepAlert(2);}
                Error('Warning from AlertMgr',alertError,NONFATAL);
            end;
        {pName := ConCat(pName,menuSuffix);}
        {Reset(txtFile,pName);}

        if IOResult<>0 then
            Error(Concat('Can''t find "',pName,'"'),0,fatal);
        (* check version of menus *)
        {Readln(txtFile,vNum);
        if vNum<>sVrsNum then
            Error('GE Menu file is the wrong version.',vNum,NONFATAL);
        if traceInit then WrtTime('    Open menuFile',tLast);


        ReadGEMenus;
        if traceInit then WrtTime('    GEMenus',tLast);  }
        pName := ConCat(pName,exMenuSuffix);
        Reset(txtFile,pName);
        ReadGECkForm;
        Close(txtFile);
        if traceInit then WrtTime('    ReadCkForm',tLast);

        (* field editor *)
        (*$IfC fldDebug*) SetFldTest(FALSE); (*$EndC*)
        GEGetAlert(10,feString);
        FEInit(theHeap,@feString,initErr);
        if initErr<>0 then Error('FEInit',initErr,FATAL);
        if traceInit then WrtTime('    InitFld...',tLast);

        InitObj(iObjNil, @trackObj); {Makes sure colors are initialized}
        printing:=False;    (* Set Printing to false to turn off color
                                   for screen *)
        (* init all global menus, scroll boxes, cursors, bitmaps, etc. *)
        SetGESBoxes;
        if traceInit then WrtTime('    SetBoxes',tLast);

        SetGEPats;
        if traceInit then WrtTime('    SetPats ',tLast);

        SetGESizes;
        if traceInit then WrtTime('    SetSizes',tLast);

        SetGEDoc;
        if traceInit then WrtTime('    SetDoc  ',tLast);

        {Initialzations for the scrap}
        scpClrFl := FALSE;
        scpShPat := TRUE;

        SetRect(nilRect,0,0,0,0);
        if traceInit then WrtTime('Total InitGE1',tStart);
    end; (* InitGE1 *)


ÿ