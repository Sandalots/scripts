(* ********************************************************************* *)

(* CopyRight 1983,1984 Apple Computer Inc. *)
(* LisaDraw Dialog Box Manager *)

(* ********************************************************************* *)

UNIT DMgr;      (* Segment DBX,INT *)

INTERFACE
USES
    (*$U UNITSTD *)   UnitStd,      (* interface dependency *)
    (*$U UNITHZ*)     UnitHz,
    (*$U STORAGE*)    Storage,
    (*$U QUICKDRAW*)  QuickDraw,
    (*$U FONTMGR*)    FontMgr,

    (*$U libos/syscall.obj*)    Syscall,      (* implementation units *)
    {$U LibPm/PmDecL}      PmDecL,
    {$U PrStdInfo}     PrStdInfo,
    {$U PrPublic}      PrPublic,

    (*$U WM.EVENTS*)  Events,
    (*$U WM.FOLDERS*) Folders,
    (*$U WM.MENUS*)   Menus,
    (*$U Obj:WmlStd*) WmlStd,
    (*$U WMLCRS*)     WMLCrs,
    (*$U ApLd/HDR*)      Hdr,
    {$IFC sampcalls}
    {$U apld/tracecalls.obj}  tracecalls,
    {$ENDC}
    (*$U ALERTMGR*)   AlertMgr;

const
    maxCkItm   = 10;           (* max number of items in a ckBox         *)
    traceDMgr  = FALSE;
    maxItmSize = 80;            (* max length of item string            *)
type
    TStrHnd    = ^TStrPtr;
    TStrPtr    = ^Str255;

    TDBoxInfo  =
        record
            isOpen:      Boolean;       (* is Dialog Box open?           *)
            inData:      QDPtr;         (* pointer to proper input data  *)
            outData:     QDPtr;         (* pointer to proper output data *)
            ProcessProc: ProcPtr;       (* procedure to process events   *)
            TrkMseProc:  ProcPtr;       (* procedure to track cursor     *)
            RestoreProc: ProcPtr;       (* procedure to restore dialog   *)
            EndProc:     ProcPtr;       (* procedure to end dialog       *)
        end;

    TCkItmHnd   = ^TCkItmPtr;
    TCkItmPtr   = ^TCkItmInfo;
    TCkItmLst   = Array[0..maxCkItm] of TCkItmHnd;
    TCkItmInfo  =
        record
            font:       Integer;        (* font of ckBox item           *)
            iTitle:     TStrHnd;        (* name of ckBox item           *)
            iLoc:       Point;          (* location of start of iTitle  *)
            hitBox:     Rect;           (* actual hit box is bigger     *)
            itmBox:     Rect;           (* box that gets colored in     *)
        end;

    TCkBoxHnd  = ^TCkBoxPtr;
    TCkBoxPtr  = ^TCkBoxInfo;
    TCkBoxLst  = Array[0..0] of TCkBoxHnd;
    TCkBoxInfo =
        record
            ckBoxId:    TB;             (* logical index of ckBox       *)
            numCkItm:   TB;             (* number of items              *)
            curOnOff:   TB;             (* which on/off box is checked  *)
            curCkItm:   TB;             (* which item is checked        *)
            hasOnOff:   Boolean;        (* are items 1 and 2 special?   *)
            ckItmLst:   TCkItmLst;      (* ckItem list may be empty     *)
            DrawProc:   ProcPtr;        (* procedure to draw ckBox      *)
            HitTstFunc: ProcPtr;        (* function to hit test ckBox   *)
        end;

    TCkFormHnd  = ^TCkFormPtr;
    TCkFormPtr  = ^TCkFormInfo;
    TCkFormInfo =
        record
            numCkBox: TB;               (* number of ckBoxes in list    *)
            vCkForm:  Integer;          (* height of dialog box         *)
            ckBoxLst: TCkBoxLst;        (* list of ckBoxPtrs            *)
        end;

var
    (* the dialog Box *)
    theDBox:    TDBoxInfo;

    (* ckBox dialog *)
    procedure ReadCkBox    (var ckFile: Text; var info: TCkBoxHnd);
    procedure CkFormHeight (info: TCkFormHnd);
    procedure ShowCkBtn    (info: TCkBoxHnd);
    procedure ShowCkBox    (info: TCkBoxHnd);
    procedure ShowCkForm   (info: TCkFormHnd);
    function  CkBtnHit     (info: TCkBoxHnd; pt: Point;
                            var ckItm: Integer): Boolean;
    function  CkBoxHit     (info: TCkBoxHnd; pt: Point;
                            var ckItm: Integer): Boolean;
    procedure CkFormSelect (info: TCkFormHnd; var ckBox,ckItm: Integer);
    procedure GetCkHit     (thisEvent: EventRec);
    procedure TrkCkBoxMse  (msePt: Point);
    procedure RestoreCkForm;

    (* miscellaneous *)
    procedure SetDBox      (inD,outD: QdPtr; prcP,trkP,rstP,endP: ProcPtr);
    procedure ClearDBox;
    procedure RestoreDBox;

    (* calling procs *)
    procedure CallPrcsProc (thisEvent: EventRec; ProcessProc: ProcPtr);
    procedure CallTrkProc  (msePt: Point; TrkProc: ProcPtr);
    procedure CallRstProc  (RestoreProc: ProcPtr);
    procedure CallEndProc  (doItFlag: Boolean; EndProc: ProcPtr);
    procedure CallShowCkBox(info: TCkBoxHnd; ShowProc: ProcPtr);
    function  CallHitTst   (info: TCkBoxHnd; pt: Point;
                            var ckItm: Integer; HitFunc: ProcPtr): Boolean;


(* ********************************************************************* *)

IMPLEMENTATION


(* ********************************************************************* *)

TYPE
    TItmStr = String[maxItmSize];

(* ********************************************************************* *)

(* Dialog Box GuideLines:

   The only kinds of events that can be gotten in the dialog folder are
   keyDown, buttonDown, buttonUp, folderUpdate and nilEvent. Ignore
   folderUpdate, since the only way to get it is thru a call to
   DialogHeight, and it has a flag allowing you to specify if you want
   it.  We never do.
*)

(* ********************************************************************* *)

(* ckBox reading *)


(* ********************************************************************* *)
(*$S Init*)
    procedure SetCkItm(var itm: TCkItmHnd; c,r,f: Integer; title: TItmStr);
    var dh,dv: Integer;
        thisTitle: TStrHnd;

        ascent:     Integer;
        descent:    Integer;
        lSpace:     Integer;
        mSpace:     Integer;        (* width of the character "m"            *)
        lSpTop:     Integer;        (* dist fm line top to ascender top      *)
        offset:     Integer;        (* baseLine := offset+row*lineSp         *)
        (* ckBox info *)
        itmBoxSize: Rect;           (* 2/3 ascent *)

        errNum:Integer;
        fInfo: FontInfo;
        fid:   TLFntId;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        SetPort(dialogFolder);
        TextFont(tile12);
        TextFace([]);
        fid.fam := tile12;
        fid.seteface := [];
        fid.dev := devScreen;
        (* get font info used in reading ckForms *)
        if not FmFontMetrics(fid,fInfo,errNum) then begin
            {
            writeln('DMgr Error -- SetDFont can''t find font');
            Halt;
            }
        end else begin
            ascent  := fInfo.ascent;
            descent := fInfo.descent;
            lSpace  := fInfo.ascent+fInfo.descent+fInfo.leading;
            mSpace  := CharWidth('m');
            lSpTop  := Round((lSpace-ascent-descent)/2);
            offset  := lSpTop+ascent-Round(lSpace/2);
            (* ckBox vars *)
            SetRect(itmBoxSize,0,0,(3*ascent) div 2,ascent);
        end;

        itm       := Pointer(Ord(HAllocate(theHeap,SizeOf(TCkItmInfo))));
        thisTitle := Pointer(Ord(HAllocate(theHeap,Length(title)+1)));
        with itm^^ do begin
            (* set font of ckItm *)
            font := f;
            (* set name of ckItm *)
            iTitle := thisTitle;
            MoveLeft(title,iTitle^^,Length(title)+1);
            (* set starting point for text *)
            dh := c*mSpace;
            dv := r*lSpace+offset;
            SetPt(iLoc,dh+2*mSpace,dv);
            (* set itm ckBox *)
            itmBox := itmBoxSize;
            OffSetRect(itmBox,dh,dv-itmBoxSize.bottom);
            (* set hitBox *)
            SetRect(hitBox,0,0,StringWidth(title)+{BBB 4/27/83}2*mSpace,lSpace);
            OffsetRect(hitBox,dh{-(mSpace div 2) BBB 4/27/83},dv-ascent-lSpTop);
        end;
    end; (* SetCkItm *)

(* ********************************************************************* *)

    procedure ScanCkBox(lineBuf: Str255; var c,r,f: Integer; var s: TItmStr);
    var bufIndx: Integer;
        digits: Set of Char;

    (* ***************************************************************** *)

        function  GetInt: Integer;
        var value: Integer;
        begin
            value := 0;
            while (bufIndx<=Length(lineBuf)) and
            not (lineBuf[bufIndx] in digits) do
                bufIndx := bufIndx+1;
            while (bufIndx<=Length(lineBuf)) and
            (lineBuf[bufIndx] in digits) do begin
                value := Ord(lineBuf[bufIndx])-Ord('0')+10*value;
                bufIndx := bufIndx+1;
            end;
            GetInt := value;
        end; (* GetInt *)

    (* ***************************************************************** *)

        procedure GetString(var thisStr: TItmStr);
        var byteCnt: Integer;
            src: TStrPtr;
        begin
            thisStr := '';
            while (bufIndx<=Length(lineBuf)) and (lineBuf[bufIndx]<=' ') do
                bufIndx := bufIndx+1;
            byteCnt := Length(lineBuf)-bufIndx+2;
            if byteCnt>maxItmSize then
                byteCnt := maxItmSize;
            src := Pointer(Ord(@lineBuf)+bufIndx-1);
            MoveLeft(src^,thisStr,byteCnt);
            (*$R-*)
            thisStr[0] := Chr(byteCnt-1);
            (*$R+*)
        end; (* GetString *)

    (* ***************************************************************** *)

    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        if lineBuf<>'' then begin
            digits := ['0'..'9'];
            bufIndx := 1;
            c := GetInt;
            r := GetInt;
            f := GetInt;
            GetString(s);
        end;
    end; (* ScanCkBox *)

(* ********************************************************************* *)

    procedure ReadCkItm(var f: Text; info: TCkBoxHnd; var noMore: Boolean);
    var title: TItmStr;
        c,r,font: Integer;
        lineBuf: Str255;
        thisItm: TCkItmHnd;  (* temp handle to pass to SetCkItm *)
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        (* see if standard CkBox or user defined *)
        Readln(f,lineBuf);
        if lineBuf='' then
            noMore := TRUE
        else if info^^.numCkItm=maxCkItm then
            noMore := TRUE
        else begin
            with info^^ do begin
                noMore := FALSE;
                numCkItm := numCkItm+1;
                ScanCkBox(lineBuf,c,r,font,title);
            end;
            SetCkItm(thisItm,c,r,font,title);
            with info^^ do
                ckItmLst[numCkItm] := thisItm;
        end;
    end; (* ReadCkItm *)

(* ********************************************************************* *)

    procedure ReadCkBox(* var ckFile: Text; var info: TCkBoxHnd *);
    var noMore: Boolean;
        i: Integer;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        SetPort(dialogFolder);
        info := Pointer(Ord(HAllocate(theHeap,SizeOf(TCkBoxInfo))));
        with info^^ do begin
            (* init ckBox *)
            numCkItm   := -1;
            curOnOff   := -1;
            curCkItm   := -1;
            hasOnOff   := FALSE;
            DrawProc   := nil;
            HitTstFunc := nil;
            for i := 0 to maxCkItm do
                ckItmLst[i] := nil;
        end;

        (* read in ckBox info *)
        ReadLn(ckFile,info^^.ckBoxId);
        repeat
            ReadCkItm(ckFile,info,noMore);
        until noMore;

        with info^^ do begin
            (* see if standard ckBox *)
            if numCkItm>0 then with ckItmLst[0]^^ do begin
                (* get ckBox hitBox size *)
                for i := 1 to numCkItm do
                    UnionRect(hitBox,ckItmLst[i]^^.hitBox,hitBox);
                (* set standard Draw and Choose procs *)
                DrawProc   := @ShowCkBox;
                HitTstFunc := @CkBoxHit;
            end;
        end;
    end; (* ReadCkBox *)

(* ********************************************************************* *)

(* ckBox display and tracking routines *)


(* ********************************************************************* *)
(*$S PageLayout*)
    procedure CkFormHeight(* info: TCkFormHnd *);
    var thisHeight,height: Integer;
        i,j,bh,bv: Integer;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        height := 0;
        with info^^ do begin
            for i := 0 to numCkBox do begin
                (*$R-*)
                thisHeight := ckBoxLst[i]^^.ckItmLst[0]^^.hitBox.bottom;
                (*$R+*)
                if thisHeight>height then
                    height := thisHeight;
            end;
            bh := CalcButWidth(0,1,bv);
            (* use MaxInt *)
            bh := height+8 { Round(lSpace/2) };
            bv := 2*bv+30;
            vCkForm := CMax(bh,bv);
        end;
    end; (* CkFormHeight *)

(* ********************************************************************* *)
(*$S PageLayout*)
    procedure ShowCkItm(info: TCkItmHnd; showBox,boxChecked: Boolean);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        if info<>nil then with info^^ do begin
            TextFont(font);
            with iLoc do
            MoveTo(h,v);
            DrawString(iTitle^^);
            if showBox then begin
                if boxChecked then
                    FillRect(itmBox,black)
                else
                    FrameRect(itmBox);
            end;
        end;
    end; (* ShowCkItm *)

(* ********************************************************************* *)
(*$S PageLayout*)
    procedure ShowCkBtn(* info: TCkBoxHnd *);
    var i,bh,bv: Integer;
        boxChecked: Boolean;
        vLoc: Array[0..1] of Integer;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        if info=nil then
            writeln('DMgrError--ShowCkBtn info is nil.')
        else with info^^ do begin
            (* show the button title *)
            ShowCkItm(ckItmLst[0],FALSE,FALSE);

            (* show two buttons *)
            bh := CalcButWidth(0,1,bv);
            with dialogFolder^.portRect do begin
                vLoc[0] := top+10;
                vLoc[1] := bottom-bv-10;
                for i := 0 to 1 do begin
                    HideButn(i);
                    PushButn(i,FALSE);
                    ShowButn(i,right-20-bh,vLoc[i],FALSE);
                end;
            end;
        end;
    end; (* ShowCkBtn *)

(* ********************************************************************* *)
(*$S PageLayout*)
    procedure ShowCkBox(* info: TCkBoxHnd *);
    var i: Integer;
        boxChecked: Boolean;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        if info=nil then
            writeln('DMgrError--ShowCkBox info is nil.')
        else with info^^ do
            for i := 0 to numCkItm do begin
                boxChecked := (i=curOnOff) or (i=curCkItm);
                ShowCkItm(ckItmLst[i],i<>0,boxChecked);
            end;
    end; (* ShowCkBox *)

(* ********************************************************************* *)
(*$S PageLayout*)
    procedure ShowCkForm(* info: TCkFormHnd *);
    var i: Integer;
        thisPort: GrafPtr;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        if traceDMgr then
            writeln('ShowCkForm');

        {OpenDBox}
        GetPort(thisPort);
        if thisPort=dialogFolder then begin
            DialogHeight(info^^.vCkForm,FALSE);
            theDBox.isOpen := TRUE;
        end;
        FillRect(thisPort^.portRect,white);

        with info^^ do begin
            for i := 0 to numCkBox do
                (*$R-*)
                CallShowCkBox(ckBoxLst[i],ckBoxLst[i]^^.DrawProc);
                (*$R+*)
        end;
    end; (* ShowCkForm *)

(* ********************************************************************* *)
(*$S PageLayout*)
    function  CkBtnHit(* info: TCkBoxHnd; pt: Point;
                        var ckItm: Integer): Boolean *);
    var pushed: TButn;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        CkBtnHit := ButnPushed(0,1,pushed,pt);
        ckItm := pushed+1;
    end; (* CkBtnHit *)

(* ********************************************************************* *)

    function  CkBoxHit(* info: TCkBoxHnd; pt: Point;
                        var ckItm: Integer): Boolean *);
    var hit: Boolean;
        i: Integer;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        ckItm := 0;
        hit := PtInRect(pt,info^^.ckItmLst[0]^^.hitBox);
        if hit then with info^^ do begin
            for i := 1 to numCkItm do
                if PtInRect(pt,ckItmLst[i]^^.hitBox) then begin
                    ckItm := i;
                    i := numCkItm;
                end;
        end;
        CkBoxHit :=  hit;
    end; (* CkBoxHit *)

(* ********************************************************************* *)
(*$S PageLayout*)
    procedure CkFormSelect(* info: TCkFormHnd; var ckBox,ckItm: Integer *);
    var msePt: Point;
        oldCkItm,i: Integer;
        thisRect: Rect;
        thisEvent: EventRecord;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        (* assumes port is already set *)
        if traceDMgr then
            writeln('CkFormSelect');

        repeat
            (* do nothing *)
        until not StillDown;
        GetMouse(msePt);
        (* get rid of buttonUp event *)
        if PeekEvent(thisEvent) then begin
            if thisEvent.what=buttonUp then begin
                GetEvent(thisEvent);
                msePt := thisEvent.where;
            end;
        end;
        ckBox := 0;
        with info^^ do
        for i := 0 to numCkBox do begin
            (*$R-*)
            if CallHitTst(ckBoxLst[i],msePt,
                          ckItm,ckBoxLst[i]^^.HitTstFunc) then
            with ckBoxLst[i]^^ do begin
                ckBox := ckBoxId;
                i := numCkBox;
                if ckBox>0 then
                    if ckItm>0 then begin
                        (* figure out which item was checked *)
                        if hasOnOff and (ckItm<=2) then begin
                            oldCkItm := curOnOff;
                            curOnOff := ckItm;
                        end else begin
                            oldCkItm := curCkItm;
                            curCkItm := ckItm;
                        end;
                        (* clear old item *)
                        if oldCkItm>0 then begin
                            thisRect := ckItmLst[oldCkItm]^^.itmBox;
                            InSetRect(thisRect,1,1);
                            FillRect(thisRect,white);
                        end;
                        (* fill new item *)
                        FillRect(ckItmLst[ckItm]^^.itmBox,black);
                    end;
            end;
            (*$R+*)
        end;

        if traceDMgr then
            writeln('    ',ckBox:2,',',ckItm:2);
    end; (* CkFormSelect *)

(* ********************************************************************* *)
(*$S PageLayout*)
    procedure GetCkHit(* thisEvent: EventRec *);
    var info: TCkFormHnd;
        ckBox,ckItm: Integer;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        if traceDMgr then
            writeln('GetCkHit(',thisEvent.what:2,')');

        if thisEvent.what=buttonDown then with theDBox do begin
            info := Pointer(Ord(inData));
            CkFormSelect(info,ckBox,ckItm);
            if (ckBox=0) and (ckItm<>0)  then
                CallEndProc(ckItm=2,endProc)
        end;
    end; (* GetCkHit *)

(* ********************************************************************* *)
(*$S PageLayout*)
    procedure TrkCkBoxMse(* msePt: Point *);
    var info:     TCkFormHnd;
        i,j,ckItm,dBoxv: Integer;
        inCkItm:  Boolean;
        mseMoved: Boolean;
        nHood:    Rect;
        thisPort: GrafPtr;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        (* assumes port is already set *)
        (* msePt is in dialog coords *)
        GetPort(thisPort);
        dBoxv := thisPort^.portRect.bottom;
        info := Pointer(Ord(theDBox.inData));
        with info^^ do
        repeat
            GetMouse(msePt);
            inCkItm := FALSE;
            for i := 0 to numCkBox do
                (*$R-*)
                if CkBoxHit(ckBoxLst[i],msePt,ckItm) then begin
                    nHood := ckBoxLst[i]^^.ckItmLst[ckItm]^^.hitBox;
                    i := numCkBox;
                    inCkItm := ckItm<>0;
                end;
                (*$R+*)
            if inCkItm then begin
                SetStdCursor(iCrsCheck);
                repeat
                    GetMouse(msePt);
                    mseMoved := not PtInRect(msePt,nHood);
                until EventAvail or mseMoved;
                if mseMoved then
                    SetStdCursor(iCrsInactive);
            end;
        until EventAvail or (msePt.v>dBoxv);
    end; (* TrkCkBoxMse *)

(* ********************************************************************* *)
(*$S PageLayout*)
    procedure RestoreCkForm;
    var info: TCkFormHnd;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        if traceDMgr then
            writeln('RestoreCkForm');

        info := Pointer(Ord(theDBox.inData));
        ShowCkForm(info);
    end; (* RestoreCkForm *)

(* ********************************************************************* *)

(* miscellaneous *)

(* ********************************************************************* *)
(*$S PageLayout*)
    procedure SetDBox(* inD,outD: QdPtr; prcP,trkP,rstP,endP: ProcPtr *);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        with theDBox do begin
            inData      := inD;
            outData     := outD;
            processProc := prcP;
            trkMseProc  := trkP;
            restoreProc := rstP;
            endProc     := endP;
        end;
    end; (* SetDBox *)

(* ********************************************************************* *)
(*$S PageLayout*)
    procedure ClearDBox;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        if traceDMgr then
            writeln('ClearDBox.');

        with theDBox do begin
            if isOpen then
                DialogHeight(0,False);
            {
            else
                writeln('DMgrError: ClearDBox--theDBox not open.');
            }
            isOpen := FALSE;
        end;
    end; (* ClearDBox *)

(* ********************************************************************* *)
(*$S PageLayout*)
    procedure RestoreDBox;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        if traceDMgr then
            writeln('RestoreDBox.');

        with theDBox do
        if isOpen then begin
            SetPort(dialogFolder);
            CallRstProc(restoreProc);
        end;
    end; (* RestoreDBox *)

(* ********************************************************************* *)

    procedure CallPrcsProc (* thisEvent: EventRec; ProcessProc: ProcPtr *);
        external;

    procedure CallTrkProc  (* msePt: Point; TrkProc: ProcPtr *);
        external;

    procedure CallRstProc  (* RestoreProc: ProcPtr *);
        external;

    procedure CallEndProc  (* doItFlag: Boolean; EndProc: ProcPtr *);
        external;

    procedure CallShowCkBox(* info: TCkBoxHnd; ShowProc: ProcPtr *);
        external;

    function  CallHitTst   (* info: TCkBoxHnd; pt: Point;
                              var ckItm: Integer; HitFunc: ProcPtr): Boolean *);
        external;

END. (* unit *)


ÿ