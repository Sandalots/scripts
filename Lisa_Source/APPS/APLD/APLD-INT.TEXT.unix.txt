(* ********************************************************************* *)

(* CopyRight 1983,1984 Apple Computer Inc. *)
(* GE Initialization Routines *)

(* ********************************************************************* *)

Unit Int;

(* Segment IN1 *)
INTERFACE
USES
    (*$U UNITSTD*)     UnitStd,    (* implementation units  *)
    (*$U UNITHZ*)      UnitHz,
    (*$U STORAGE*)     Storage,
    (*$U QUICKDRAW*)   QuickDraw,
    (*$U FONTMGR*)     FontMgr,
    (*$U FMGRUTIL*)    FMgrUtil,
    (*$U libos/syscall.obj*)     SysCall,
    (*$U WM.EVENTS*)   Events,
    (*$U WM.FOLDERS*)  Folders,
    (*$U FILERCOMM*)   FilerComm,
    (*$U WM.MENUS*)    Menus,
    (*$U ALERTMGR*)    AlertMgr,
    (*$U WMLSTD*)      WMLStd,
    (*$U WMLSB*)       WMLSB,
    (*$U WMLCRS*)      WMLCrs,
    (*$U DBENV*)       DbEnv,
    (*$U FEDEC*)       FEDec,
    (*$U FLD*)         FieldEdit,
    (*$U FPUNIT*)      FPLib,
    {$U LibPm/PmDecL}      PmDecL,
    {$U PrStdInfo}     PrStdInfo,
    {$U PrPublic}      PrPublic,
    (*$U ApLd/HDR*)      Hdr,
    {$IFC sampcalls}
    {$U apld/tracecalls.obj}  tracecalls,
    {$ENDC}
    (*$U ApLd/DMGR*)        DMgr,
    (*$U SCRAP*)       Scrap,
    (*$U ApLd/CMD*)      Cmd,
    (*$U ApLd/DAT*)      Dat,
    (*$U ApLd/DBX*)      Dbx,
    (*$U ApLd/MNU*)      Mnu,
    (*$U ApLd/SEL*)      Sel,
    (*$U ApLd/UOP*)      UOp,
    (*$U ApLd/DRW*)      Drw,
    (*$U ApLd/FIO*)      Fio,
    (*$U ApLd/PRT*)      Prt,
    (*$U ApLd/SCR*)      Scr,
    (*$U ApLd/TRK*)      Trk,
    (*$U ApLd/TXT*)      Txt,
    (*$U ApLd/UTL*)      Utl;

VAR
    inCrash:    Boolean;

    procedure TestColorLet;  {Take out when done}
    procedure InitSys;
    procedure InitGE1;
    procedure InitGE2;
    function  GrowDocHz(hz: THz; cbNeed: TC): TC;
    procedure NewGEHz(pName: PathName;
                      mSize,dSize: LongInt;
                      ldsn: Integer;
                      proc: ProcPtr;
                      VAR hz: THz;
                      VAR ref: Integer;
                      VAR errNum: Integer);

(* ********************************************************************* *)

IMPLEMENTATION


Type
    TLetterArray = Array[1..8] of Integer;
    Str2 = String[2];


(* ********************************************************************* *)
(*$S Ring*)
    function  GrowTheHz(hz: THz; cbNeed: TC): TC;
    var errNum: Integer;
        newMemSize,newDiskSize,deltaSize: LongInt;
        tWhy: FReason;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        deltaSize := cbNeed;
        (* round up to nearest K only if will not exceed integer limit *)
        if deltaSize<31744 then
            deltaSize := ((deltaSize DIV 1024)+1)*1024;
        Size_DataSeg(errNum,geHHzRef,
                     deltaSize,newMemSize,
                     deltaSize,newDiskSize);
        if traceOn then
            writeln('geHHz expanded by ',deltaSize:6,
                    ', to ',newMemSize:6,',',newDiskSize:6,
                    ', on request of ',cbNeed:10,
                    ', errNum=', errNum:2);

        (* check for errors *)
        if errNum>0 then begin
            {
            curDoc.hzErr := iStopGEHz;
            }
            if errNum in [309,311,312] then begin
                (* out of disk space, see if we can proceed with memory only *)
                Size_DataSeg(errNum,geHHzRef,
                             deltaSize,newMemSize,
                             0,newDiskSize);
            end;
            if errNum>0 then begin
                (* XXX suspend the document *)
                if errNum in [304,306,315] then
                    tWhy := noMemory
                else
                    tWhy := internalError;
                TellFiler(errNum,docClosd,tWhy,curDoc.fldr);
                if traceOn then
                    Error('Expanding Hz',errNum,FATAL)
                else
                    HALT;
            end;
        end;
        if errNum<=0 then
            GrowTheHz := deltaSize;
        (* warn user if we are about to run out of memory *)
        {
        if newMemSize>256000 then
            curDoc.hzErr := iStopGEHz;
        }
    end; (* GrowTheHz *)

(* ********************************************************************* *)
(*$S Ring*)
    function  GrowDocHz(* hz: THz; cbNeed: TC): TC *);
    var errNum, heapRefNum: Integer;
        newMemSize,newDiskSize,deltaSize: LongInt;
        tWhy: FReason;
        s: String80;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        heapRefNum := HzRefNum(hz);
        deltaSize := cbNeed;
        (* round up to nearest K only if will not exceed integer limit *)
        if deltaSize<31744 then
            deltaSize := ((deltaSize DIV 1024)+1)*1024;
        Size_DataSeg(errNum,heapRefNum,
                     deltaSize,newMemSize,
                     deltaSize,newDiskSize);
        if traceOn then begin
            if hz=curDoc.pHz then
                s := 'GEDocPHz'
            else if hz=curDoc.hHz then
                s := 'GEDocHHz';
            writeln(s,' expanded by ',deltaSize:6,
                    ', to ',newMemSize:6,',',newDiskSize:6,
                    ', on request of ',cbNeed:10,
                    ', errNum=', errNum:2);
        end;

        (* warn user if we are about to run out of memory *)
        {
        if newMemSize>cHzMax then
            curDoc.hzErr := iStopDocHz;
        }
        (* check for errors *)
        if errNum>0 then begin
            GrowDocHz := 0;
            (* XXX suspend the document *)
            if errNum in [309,311,312] then
                tWhy := noDiskSpace
            else if errNum in [304,306,315] then
                tWhy := noMemory
            else
                tWhy := internalError;
            TellFiler(errNum,docClosd,tWhy,curDoc.fldr);
            if traceOn then
                Error('Expanding Hz',errNum,FATAL)
            else
                HALT;
        end else
            GrowDocHz := deltaSize;
    end; (* GrowDocHz *)

(* ********************************************************************* *)
(*$S IO*)
    procedure NewGEHz(* pName: PathName;
                        mSize: LongInt;
                        dSize: LongInt;
                        ldsn:  Integer;
                        proc:  ProcPtr;
                        VAR hz:  THz;
                        VAR ref: Integer;
                        VAR errNum: Integer *);
    var hFirst: LongInt;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        (* make datasegs and init heap zones for new doc *)
        Make_DataSeg(errNum,pName,mSize,dSize,ref,hFirst,ldsn,ds_private);
        (* errors: no disk space, data lock out *)
        if errNum<=0 then
            hz := HzInit(Pointer(hFirst),Pointer(hFirst+mSize),
                          NIL,500,0,proc,NIL,NIL,NIL);
        if traceFio then
            Writeln('    NewGEHz(',pName,',',mSize:2,',',dSize:2,',',
                                   ldsn:2,')=',Ord(hz):8,',',ref:2,',',errNum:2);
    end; (* NewGEHz *)

(* ********************************************************************* *)
(*$S Init*)

    procedure GEGrafErr(errNum: Integer);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        if rangeOn then
            if errNum>0 then
                Error('GEGrafErr',errNum,FATAL);
    end;

(* ********************************************************************* *)

    procedure CrashSuspend; EXTERNAL;

    procedure LastRites(envPtr: P_env_blk; exDataPtr: P_ex_data);
    var numBlks,errNum: Integer;
        tWhy: FReason;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        ImDying;
        if traceOn then begin
            writeln;
            writeln('LisaDraw LastRites...');
            writeln('    thePort=',Ord(thePort):8,', deskPort=',Ord(deskPort):8);
            CkGEHz('LastRites');
        end;
        if not quitFlag then begin
            if traceOn then
                writeln('    LisaDraw Abnormal death...');
            (* try to save all the open docs *)
            CrashSuspend;
        end;
        if traceOn then
            writeln('    LisaDraw Halting...');
    end; (* LastRites *)

(* ********************************************************************* *)

    procedure InitSys;
    const
        hMSize  =  $2000;   (*  8K *)     (* typical theHeap size *)
        hDSize  = $4000;   (* 16K *)     (* worst case size *)
        geHLdsn = 1;
    var geHHzPath: PathName;
        tempRect:  Rect;
        exName:    T_Ex_Name;

        copyRight: String80;
        status:   Integer;

        {XXX timing vars}
        tLast:    LongInt;
        tStart:   LongInt;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        copyRight := 'CopyRight 1983, Apple Computer Inc.';
        if traceOn then begin
            geHHz  := NIL;
            tStart := Time;
            tLast  := Time;
            if traceInit then WrtTime('InitSys...',tLast);
        end;

        (* system initialization *)
        {Sched_Class(status,TRUE);       out for spring release}
        inCrash    := FALSE;
        inInit     := TRUE;
        initErr    := 0;

        InitGraf(@thePort);
        if traceInit then WrtTime('    InitGraf...',tLast);

        (* window manager *)
        OpenWM;
        if traceInit then WrtTime('    InitWM...',tLast);

        (* declare exception handler *)
        exName := 'SYS_TERMINATE';
        Declare_Excep_Hdl(initErr,exName,@LastRites);
        if initErr>0 then Error('Declare_Excep_Hdl',initErr,FATAL);
        if traceInit then WrtTime('    ExcepH...',tLast);

        (* Floating Pt Lib *)
        InitFpLib;
        if traceInit then WrtTime('    InitFP...',tLast);

        (* Make DataSegs and Init heap zones *)
        (*$IFC fDbgOk*)
        fTstStd   := FALSE;
        fTstHz    := FALSE;
        (*$EndC*)
        geHHzPath := 'GEHHZ';
        NewGEHz(geHHzPath,hMSize,hDSize,geHLdsn,@GrowTheHz,geHHz,geHHzRef,initErr);
        if initErr>0 then Error('NewGEHz',initErr,FATAL);
        theHeap := geHHz;
        if traceInit then WrtTime('    InitHeap...',tLast);

        (* scroll bars, scrap, geCrs *)
        InitWMLSB;
        InitWMLCrs(initErr);
        if initErr>0 then Error('InitWMLCrs',initErr,FATAL);
        if traceInit then WrtTime('    InitSBox...',tLast);
        OpenScrap(initErr);
        if initErr>0 then Error('OpenScrap',initErr,FATAL);
        if traceInit then WrtTime('    OpenScrap...',tLast);

        (* init alertmgr before read in alerts *)
        InitDBX1;

        (* field editor *) {Now in InitGE1 after alert_menus read in}

        if traceInit then WrtTime('Exit InitSys',tStart);
    end; (* InitSys *)

    (*$I ApLd/IN1.text*)
    (*$I ApLd/IN2.text*)

end. (* unit *)

ÿ