;-------------------------------------------------------------------------------

; CopyRight 1983, Apple Computer Inc.
; File APLD/68K.TEXT
; Assembly language routines for LisaDraw
; By Mark Cutter

;-------------------------------------------------------------------------------

; Dialog Box Manager Call Procs

;-------------------------------------------------------------------------------

        .PROC   DBoxProcCallers

        .DEF    CallPrcsProc
;   Procedure CallProcessProc(thisEvent: EventRec; processProc: ProcPtr);

        .DEF    CallTrkProc
;   Procedure CallTrkMseProc(processProc: ProcPtr);

        .DEF    CallRstProc
;   Procedure CallRestoreProc(processProc: ProcPtr);

        .DEF    CallEndProc
;   Procedure CallEndProc(doItFlag: Boolean; processProc: ProcPtr);

        .DEF    CallShowCkBox
;   Procedure CallShowCkBox(info: TCkBoxPtr; ShowProc: ProcPtr);

        .DEF    CallHitTst
;   Function  CallHitTst(info: TCkBoxPtr; pt: Point; var ckItm: Integer;
;                        HitFunc: ProcPtr): Boolean;

;-------------------------------------------------------------------------------

CallPrcsProc:
CallTrkProc:
CallRstProc:
CallEndProc:
CallShowCkBox:
CallHitTst:

        MOVE.L  4(SP),A0                        ; Load address of proc to be called
        MOVE.L  (SP)+,(SP)                      ; Put return address on stack
        JMP     (A0)                            ; Jump to proc

;-------------------------------------------------------------------------------

; Buffer Rotation Routines

;-------------------------------------------------------------------------------
;
;   procedure ZeroBuf(dst: LongInt; byteCnt: Integer);
;
        .SEG '        '
        .PROC   ZeroBuf

        MOVE.L  (SP)+,A0                        ; POP RETURN ADDR
        MOVE    (SP)+,D0                        ; GET BYTE COUNT
        MOVE.L  (SP)+,A1                        ; GET BUFPTR
        ASR     #2,D0                           ; Get Long Count
        SUBQ    #1,D0                           ; OFFSET FOR FIRST PASS

CLRLONG CLR.L   (A1)+                           ; CLEAR A LONG
        DBRA    D0,CLRLONG                      ; LOOP LONG COUNT

        JMP     (A0)                            ; AND RETURN



;-------------------------------------------------------------------------------
;
;   function MapRotate(oldAmount,newAmount: Integer): Integer;
;
;   rotData: array[0..7,1..5] of Byte
;   MapRotate := rotData[oldAmount,newAmount]

        .SEG '        '
        .FUNC   MAPROTATE

        MOVE.L  (SP)+,A0                        ; POP RETURN
        MOVE.W  (SP)+,D0                        ; POP NEWAMOUNT
        MOVE.W  (SP)+,D1                        ; POP OLDAMOUNT
        SUBQ    #1,D0                           ; ARRAY STARTS AT 1
        LEA     ROTDATA,A1                      ; POINT TO ROTATION DATA
        MULS    #5,D1                           ; GET ROW OFFSET
        ADD     D0,D1                           ; GET BYTE OFFSET
        ADD.W   D1,A1                           ; GET ADDRESS OF RESULT
        CLR     D0                              ; CLEAR D0 FOR RESULT
        MOVE.B  (A1),D0                         ; GET MAPPING
        MOVE    D0,(SP)                         ; PUT ON STACK

        JMP     (A0)                            ; AND RETURN

ROTDATA .BYTE   1,2,3,4,5
        .BYTE   2,3,0,6,7
        .BYTE   3,0,1,5,4
        .BYTE   0,1,2,7,6
        .BYTE   7,5,6,0,2
        .BYTE   6,4,7,2,0
        .BYTE   4,7,5,1,3
        .BYTE   5,6,4,3,1




;-------------------------------------------------------------------------------
;
;  procedure RotateBuf(src,dst: LongInt;
;                      VAR rowBytes,height: Integer;
;                      amount: Integer);
;
;  Assume that rowBytes and height are multiples of 16
;
        .SEG '        '
        .PROC   RotateBuf

PARAMSIZE       .EQU    18                      ; TOTAL BYTES OF PARAMS
SRC             .EQU    PARAMSIZE+8-4           ; LONG, VAR
DST             .EQU    SRC-4                   ; LONG, VAR
AROWBYTES       .EQU    DST-4                   ; WORD, VAR
AHEIGHT         .EQU    AROWBYTES-4             ; WORD, VAR
AMOUNT          .EQU    AHEIGHT-2               ; WORD

NUMBBANDS       .EQU    -2
ROWBYTES        .EQU    NUMBBANDS-2
WIDTH           .EQU    ROWBYTES-2
MASK            .EQU    WIDTH-4
SHIFT           .EQU    MASK-2
VARSIZE         .EQU    SHIFT

        LINK    A6,#VARSIZE                     ; ALLOCATE STACK FRAME
        MOVEM.L D3-D7/A2-A4,-(SP)               ; save registers

        MOVE.L  AHEIGHT(A6),A0                  ; GET ADDRESS OF HEIGHT
        MOVE.W  (A0),D3                         ; GET HEIGHT
        BEQ     DONE

        ;---------------------------------------  COMMON SETUP

        MOVE.L  AROWBYTES(A6),A0                ; GET ADDRESS OF ROWBYTES
        MOVE.W  (A0),D7                         ; GET ROWBYTES
        MOVE.W  D7,ROWBYTES(A6)                 ; SAVE A COPY IN LOCAL VAR
        MOVE.W  D7,D0                           ; CALC ROWWORDS
        ASR     #1,D0                           ;       = ROWBYTES DIV 2
        MOVE    D0,A3                           ; PUT ROWWORDS IN A3
        ASL     #3,D7                           ; CALC WIDTH
        MOVE.W  D7,WIDTH(A6)                    ;       = ROWBYTES * 8
        MOVE.W  D3,D4                           ; GET HEIGHT
        ADD     #15,D4                          ; CALC NUMWBANDS
        ASR     #4,D4                           ;       = (HEIGHT+15) DIV 16
        MOVE    D4,D6                           ; CALC NUMBBANDS
        ASL     #1,D6                           ;       = NUMWBANDS * 2
        MOVE    D6,NUMBBANDS(A6)                ; STORE IT AWAY
        MULS    D6,D7                           ; CALC BUFSIZE = NUMBBANDS * WIDTH
        MOVEQ   #2,D5                           ; CALC DSTOSET = 2
        SUBQ    #1,D4                           ; CALC BANDCNT = NUMWBANDS - 1

        MOVE.L  SRC(A6),A0                      ; GET SRC
        MOVE.L  DST(A6),A1                      ; GET DST
        MOVE    AMOUNT(A6),A2                   ; GET AMOUNT
        MOVE.L  #$8000,MASK(A6)                 ; SET UP MASK
        MOVE    #1,SHIFT(A6)                    ; MASK LSR FLAG

; D7    = BUFSIZE
; D6    = NUMBBANDS
; D5    = DSTOSET
; D4    = BANDCNT
; D3    = WORDCNT
; D2    = BITCNT
; D1    = MASK
; D0    = SRCWORD

; A0    = SRC
; A1    = DST
; A2    = AMOUNT
; A3    = ROWWORDS

        CMP     #1,A2                           ; GO TO PROPER SET UP
        BEQ.S   ROT1
        CMP.W   #2,A2
        BEQ     ROT2
        CMP.W   #3,A2
        BEQ.S   ROT3
        CMP.W   #4,A2
        BEQ     ROT4
        CMP.W   #5,A2
        BEQ     ROT5
        CMP.W   #6,A2
        BEQ     ROT6
        CMP.W   #7,A2
        BEQ     ROT7
        BRA     DONE                            ; IF AMOUNT NOT IN 1..7 THEN EXIT

        ; --------------------------------------- ROTATION AMOUNT = 1,3,6,7

ROT7                                            ; SET UP FOR FIRST LOOP
        SUBQ    #2,A1                           ;       DST = DST -2
        NEG.W   D6                              ; ROW OFFSET = -NUMBBANDS
        NEG.W   D5                              ; BAND OFFSET = -2
        BRA.S   MASK1                           ; SET UP MASK

ROT6    ADD.W   D7,A1                           ; SET UP DST FOR FIRST LOOP
                                                ;       DST = DST + BUFSIZE
        NEG.W   D7                              ; COLUMN OFFSET = -BUFSIZE
        BRA.S   NXTBAND

ROT3    SUB.W   D6,A1                           ; SET UP FOR FIRST LOOP
                                                ;       DST = DST - NUMBBANDS
        NEG.W   D6                              ; COLUMN DSTOFFSET = -NUMBBANDS
        BRA.S   NXTBAND

ROT1    ADD.W   D6,A1                           ; SET UP DST FOR FIRST LOOP
        SUBQ    #2,A1                           ;       DST = DST + NUMBBANDS - 2 + BUFSIZE
        ADD.W   D7,A1
        NEG.W   D7                              ; COLUMN OFFSET = -BUFSIZE
        NEG.W   D5                              ; DSTOFFSET = -2

MASK1   MOVE.L  #$0001,MASK(A6)                 ; SET UP MASK
        CLR     SHIFT(A6)                       ; MASK LSL FLAG

NXTBAND MOVE.L  MASK(A6),D1                     ; GET MASK
NXTROW  ADD     D7,A1                           ; DST = DST + BUFSIZE
        MOVE    A3,D3                           ; INIT WORD COUNT
        SUBQ    #1,D3                           ; OFFSET BY FIRST PASS FOR LOOP CNT
NXTWORD MOVE    (A0)+,D0                        ; GET A WORD OF SRC
        MOVEQ   #15,D2                          ; INIT BIT COUNT
NXTBIT  ADD     D0,D0                           ; GET A BIT OF SRC
        BCC.S   WHITE1
        OR      D1,(A1)                         ; SET A BIT OF DST
WHITE1  ADD     D6,A1                           ; BUMP DST ONE ROW BY NUMBBANDS
        DBRA    D2,NXTBIT                       ; LOOP 16 BITS
        DBRA    D3,NXTWORD                      ; LOOP ROWWORDS

        TST.W   SHIFT(A6)                       ; WHICH WAY TO SHIFT MASK?
        BEQ.S   LSHFT
        LSR     #1,D1                           ; SHIFT MASK BIT RIGHT
        BRA.S   TSTMASK
LSHFT   LSL     #1,D1                           ; SHIFT MASK BIT LEFT

TSTMASK BNE.S   NXTROW                          ; CONTINUE IF NOT ZERO
        ADD     D5,A1                           ; DST = DST + DSTOFFSET
        DBRA    D4,NXTBAND                      ; LOOP ALL BANDS

; HANDLE OUTPUT PARAMETERS

        MOVE.L  AHEIGHT(A6),A0                  ; GET ADDRESS OF HEIGHT
        MOVE    WIDTH(A6),(A0)                  ; PUT WIDTH IN IT
        MOVE.L  AROWBYTES(A6),A0                ; GET ADDRESS OF ROWBYTES
        MOVE    NUMBBANDS(A6),(A0)              ; PUT NUMBBANDS IN IT
        BRA     DONE                            ; EXIT

        ; --------------------------------------- ROTATION AMOUNT = 2: ROTATE 180 DEGREES
        ; --------------------------------------- ROTATION AMOUNT = 4: FLIP HORIZ

; D7    = BUFSIZE
; D5    = DSTOSET
; D4    = DSTWORD
; D3    = ROWCNT
; D2    = WORDCNT
; D1    = MASK
; D0    = SRCWORD

; A0    = SRC
; A1    = DST
; A2    = AMOUNT
; A3    = ROWWORDS

ROT4    MOVE.W  ROWBYTES(A6),D5                 ; GET ROWBYTES
        SUB     D5,A1                           ; SET UP DST = DST - ROWBYTES
        ASL     #1,D5                           ; GET ROWOSET = 2 * ROWBYTES
        BRA.S   START

ROT2    ADD.W   D7,A1                           ; SET UP DST = DST + BUFSIZE
        CLR.W   D5                              ; ROWOSET = 0
START   SUBQ    #1,D3                           ; OFFSET ROWCNT FOR FIRST PASS

NXTROW2 MOVE.W  A3,D2                           ; GET WORDCNT = ROWWORDS
        SUBQ    #1,D2                           ; OFFSET BY FIRST PASS FOR LOOP CNT
        ADD.W   D5,A1                           ; BUMP & POINT DST BY ROWOSET
NXTWRD2 MOVEQ   #1,D1                           ; SET UP MASK
        MOVE.W  (A0)+,D0                        ; GET SRCWORD
        CLR.L   D4                              ; CLEAR DSTWORD
NXTBIT2 ADD     D0,D0                           ; GET HI BIT OF SRCWORD
        BCC.S   WHITE2                          ; SKIP IF NOT SET
        OR.W    D1,D4                           ; SET A BIT IN DST
WHITE2  LSL.W   #1,D1                           ; SHIFT THE MASK LEFT
        BNE     NXTBIT2                         ; LOOP BITCNT
        MOVE.W  D4,-(A1)                        ; SET DSTWORD
        DBRA    D2,NXTWRD2                      ; LOOP WORDCNT
        DBRA    D3,NXTROW2                      ; LOOP ROWCNT = HEIGHT
        BRA.S   DONE


        ; --------------------------------------- ROTATION AMOUNT = 5, FLIP VERT

; D7    = BUFSIZE
; D3    = ROWCNT
; D2    = WORDCNT
; D1    = MASK
; D0    = ROWBYTES

; A0    = SRC
; A1    = DST
; A2    = AMOUNT
; A3    = ROWWORDS

ROT5    MOVE.W  ROWBYTES(A6),D0                 ; GET ROWBYTES
        ADD.W   D7,A1                           ; SET UP DST
        ADD.W   D0,A1                           ;       = DST + BUFSIZE + ROWBYTES
        ASL     #1,D0                           ; GET DSTOSET = 2 ROWS
        SUBQ    #1,D3                           ; OFFSET ROWCOUNT FOR FIRST PASS

NXTROW5 MOVE.W  A3,D2                           ; GET WORDCNT = ROWWORDS
        SUBQ    #1,D2                           ; OFFSET BY FIRST PASS FOR LOOP CNT
        SUB.W   D0,A1                           ; BUMP & POINT AT BEG OF DST ROW
NXTWRD5 MOVE.W  (A0)+,(A1)+                     ; SET DSTWORD = SRCWORD
        DBRA    D2,NXTWRD5                      ; LOOP WORDCNT = ROWWORDS
        DBRA    D3,NXTROW5                      ; LOOP ROWCNT = HEIGHT

        ;---------------------------------------- COMMON END

DONE    MOVEM.L (SP)+,D3-D7/A2-A4               ; RESTORE REGS
        UNLK    A6                              ; RELEASE STACK FRAME
        MOVE.L  (SP)+,A0                        ; POP RETURN ADDR
        ADD     #PARAMSIZE,SP                   ; STRIP PARAMS
        JMP     (A0)                            ; AND RETURN


        .END

ÿ