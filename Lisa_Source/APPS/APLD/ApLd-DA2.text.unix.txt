(* ********************************************************************* *)

(* CopyRight 1983, Apple Computer Inc. *)
(* GE Unit Dat Include File: Curve Generation Routines *)
(*$S CRV*)


    (* ***************************************************************** *)

    procedure GetKnots(Var cLst:TCLstHnd);
    const
        aMin = 50;          (* minimum triangle area *)
        aMax = 200;         (* two * maximum triangle area *)
    var i: Integer;
        p1,p2,lastNode: Point;
        aTot,a1,a2,a3: Integer;
        newCLst: TCLstHnd;
        done,noMorePts: Boolean;


(* ********************************************************************* *)

    procedure PutPoint(pt:Point);
    Var pt2D: Point2D;
    begin
        With newCLst^^ do begin
            crdCnt := crdCnt + 1;
            ScrnToWld(pt,pt2D);
            {$R-}
            c2DLst[crdCnt] := pt2D;
            {$IfC rangeOn} {$R+} {$EndC}
        end;
    end;{PutPoint}

    (* ************************************************************* *)

        procedure GetNxtP2;
        begin
    {$IFC sampcalls} ALogCall; {$ENDC}
            With cLst^^ do begin
                if i<=crdCnt then begin
                    GetNode(cLst,i,p2);
                    i := i+1;
                end;
                noMorePts := (i>crdCnt);
            end;
        end;

    (* ************************************************************* *)

    begin
    {$IFC sampcalls} ALogCall; {$ENDC}
        if traceCmd then begin
            DumpCLst(cLst);
            writeln('    GetKnots, begPt=',begPt.h:2,',',begPt.v:2);
        end;
        (* reduces number of crdPts based on local curvature *)
        With cLst^^ do begin
            If crdCnt>2 then begin
                OpenCLst(HzFromH(Pointer(Ord(cLst))),newCLst,maxCrdCnt,
                         iObjPoly);

                {Initialize newCLst index}
                newCLst^^.crdCnt := -1;


                WldToScrn(c2DLst[0]),begPt);
                p1 := begPt;
                p2 := begPt;
                PutPoint(p1);
                i  := 1;
                GetNxtP2;
                a2   := 0;
                a3   := 0;
                repeat
                    aTot := 0;
                    lastNode := p1;
                    repeat
                        p1 := p2;
                        GetNxtP2;

                        (* get new area *)
                        a1 := a2;
                        a2 := a3;
                        a3 := ((p1.h-lastNode.h)*(p2.v-lastNode.v)-
                               (p2.h-lastNode.h)*(p1.v-lastNode.v)) ;
                        aTot := aTot+a3;

                        (* check total area *)
                        done := (ABS(aTot)>=aMax);

                        (* check localized curvature *)
                        if NOT done then
                            done := (ABS(a3)>=ABS(a1+a2))
                                AND (ABS(aTot)>=aMin);


                        if traceCmd then begin
                            write('        ', (i-2):2,a1:6,a2:6,a3:6,aTot:6,
                                  lastNode.h:6,',',lastNode.v:3,
                                  p1.h:6,',',p1.v:3,p2.h:6,',',p2.v:3);
                            if NOT done then writeln;
                        end;
                    until done OR noMorePts;
                    if traceCmd then
                        writeln(' PutNode(',(p1.h-lastNode.h):2,
                                        ',',(p1.v-lastNode.v):2,')');
                    PutPoint(p1);
                until noMorePts;
                PutPoint(p2);
                CompleteCLst(cLst, newCLst);

                {Switch to the new list}
                FreeCLst(cLst);
                cLst := newCLst;
                CloseCLst(cLst);  {Frees up extra memory}
            end;
    end; (* GetKnots *)

    (* ***************************************************************** *)

        PROCEDURE CalcVert;
        (* given input splinePts splinePts[i], calc vertex polygon Vert[i] *)
        const
            epsilon=1;
        VAR i,delta,maxdelta: INTEGER;

        (* ************************************************************* *)

            procedure GetEnd;
            begin
        {$IFC sampcalls} ALogCall; {$ENDC}
                IF closed THEN BEGIN
                    hVert^^[nPoints+1]:=hVert^^[1];
                    vVert^^[nPoints+1]:=vVert^^[1];
                    hVert^^[0]:=hVert^^[nPoints];
                    vVert^^[0]:=vVert^^[nPoints];
                END ELSE BEGIN
                    hVert^^[0]:=hVert^^[1];
                    vVert^^[0]:=vVert^^[1];
                    hVert^^[nPoints+1]:=hVert^^[nPoints];
                    vVert^^[nPoints+1]:=vVert^^[nPoints];
                END;
            end; (* GetEnd; *)

        (* ************************************************************* *)

        begin
        {$IFC sampcalls} ALogCall; {$ENDC} (* CalcVert *)
            (* init vert array *)
            FOR i:=1 TO nPoints DO begin
                hVert^^[i]:=splinePts^^[i].h;
                vVert^^[i]:=splinePts^^[i].v;
            end;
            GetEnd;

            (* compute inversion *)
            (* iterate till converges to within epsilon *)
            REPEAT
                maxdelta := 0;
                FOR i := 1 TO nPoints DO BEGIN
                    delta := (hVert^^[i-1] + hVert^^[i+1]) DIV 2;
                    delta := (splinePts^^[i].h - delta) DIV 2;
                    delta := delta + splinePts^^[i].h - hVert^^[i];
                    IF ABS(delta) > maxdelta THEN
                        maxdelta := ABS(delta);
                    hVert^^[i] := hVert^^[i] + delta;

                    delta := (vVert^^[i-1] + vVert^^[i+1]) DIV 2;
                    delta := (splinePts^^[i].v - delta) DIV 2;
                    delta := delta + splinePts^^[i].v - vVert^^[i];
                    IF ABS(delta) > maxdelta THEN
                        maxdelta := ABS(delta);
                    vVert^^[i] := vVert^^[i] + delta;

                    GetEnd;
                END;
            UNTIL maxdelta <= epsilon;

            (* convert to data type fixed *)
            FOR i:=0 TO nPoints+1 DO begin
                hVert^^[i]:=65536*hVert^^[i];
                vVert^^[i]:=65536*vVert^^[i];
            end;
        END; (* CalcVert *)

    (* ***************************************************************** *)

        PROCEDURE MakeSpline;
        VAR i: INTEGER;
            lastPt: Point;

        (* ************************************************************* *)

            PROCEDURE SplineSeg(v1,v2,v3,v4: Integer);
            VAR step: INTEGER;
                d1,d2,d3,d4: Fixed;
                h,v,dh,dv: Integer;
            begin
        {$IFC sampcalls} ALogCall; {$ENDC}
                FOR step := 0 TO nSteps DO BEGIN
                    (* get spline coords *)
                    d1 := FixMul(hVert^^[v1],E0[step]);
                    d2 := FixMul(hVert^^[v2],E1[step]);
                    d3 := FixMul(hVert^^[v3],E1[nSteps-step]);
                    d4 := FixMul(hVert^^[v4],E0[nSteps-step]);
                    h  := FixRound(d1+d2+d3+d4);
                    d1 := FixMul(vVert^^[v1],E0[step]);
                    d2 := FixMul(vVert^^[v2],E1[step]);
                    d3 := FixMul(vVert^^[v3],E1[nSteps-step]);
                    d4 := FixMul(vVert^^[v4],E0[nSteps-step]);
                    v  := FixRound(d1+d2+d3+d4);
                    (* check bounding box *)
                    with bBox do begin
                        if left>h then
                            left   := h
                        else if right<h then
                            right  := h;
                        if top>v then
                            top    := v
                        else if bottom<v then
                            bottom := v;
                    end;
                    (* set CLst *)
                    if step<>0 then begin
                        dh := h-lastPt.h;
                        dv := v-lastPt.v;
                        if (ABS(dh)>=hKnob) OR (ABS(dv)>=vKnob) then begin
                            PutNode(fHndCLst,dh,dv);
                            SetPt(lastPt,h,v);
                        end;
                    end;
                END;
            END; (* SplineSeg *)

        (* ************************************************************* *)

        begin
        {$IFC sampcalls} ALogCall; {$ENDC}
            lastPt := begPt;
            FOR i := 0 TO nPoints-2 DO
                SplineSeg(i,i+1,i+2,i+3);
            IF closed THEN
                SplineSeg(nPoints-1,nPoints,1,2)
        END; (* MakeSpline *)

    (* ***************************************************************** *)

    begin
        {$IFC sampcalls} ALogCall; {$ENDC} (* GetSpline *)
        if traceCmd then
            writeln('GetSpline(',Ord(cLst):10,')');

        (* takes an iObjFHnd OR iObjPoly cLst
           returns a smoothed iObjFHnd cLst *)

        (* reduce the cLst to control points for iObjFHnd *)
        (* use actual cLst data for iObjPoly *)
        thisCLst := cLst^^;
        WldToScrn(thisCLst.beg2D,begPt);
        if thisCLst.crdKind=iObjFHnd then begin
            GetKnots;
            thisCLst := cLst^^;
        end;

        (* vars for spline computation *)
        OpenCLst(HzFromH(Pointer(Ord(cLst))),
                 fHndCLst,maxSplineCnt,iObjFHnd);
        fHndCLst^^.beg2D  := thisCLst.beg2D;
        fHndCLst^^.cClose := thisCLst.cClose;
        nPoints := thisCLst.crdCnt+1;
        closed  := thisCLst.cClose=iCLstUsr;

        if traceOn then t1 := ElapsTime;

        (* init bBox *)
        bBox.topLeft  := begPt;
        bBox.botRight := begPt;

        (* allocate intermediate storage *)
        splinePts := Pointer(Ord(HAllocate(geHHz,(nPoints+2)*SizeOf(Point))));
        hVert     := Pointer(Ord(HAllocate(geHHz,(nPoints+2)*SizeOf(LongInt))));
        vVert     := Pointer(Ord(HAllocate(geHHz,(nPoints+2)*SizeOf(LongInt))));

        (* get Points array *)
        splinePts^^[1]  := begPt;
        crdPt := begPt;
        for i := 1 to thisCLst.crdCnt do begin
            GetNode(cLst,i,crdPt);
            splinePts^^[i+1] := crdPt;
        end;

        if traceOn then t2 := ElapsTime;

        (* get Vertices array *)
        CalcVertices;

        if traceOn then t3 := ElapsTime;

        (* get Splines *)
        MakeSpline;

        (* de-allocate intermediate storage *)
        FreeH(geHHz,Pointer(Ord(splinePts)));
        FreeH(geHHz,Pointer(Ord(hVert)));
        FreeH(geHHz,Pointer(Ord(vVert)));

        CloseCLst(fHndCLst);
        FreeCLst(cLst);
        cLst := fHndCLst;

        if traceOn then t4 := ElapsTime;

        if traceTrk then begin
            tot := t4-t1;
            writeln('    copy nodes: ',(t2-t1):8,',',
                                     Round(100*(t2-t1)/tot):4,'%');
            writeln('    calc verts: ',(t3-t2):8,',',
                                     Round(100*(t3-t2)/tot):4,'%');
            writeln('    calc spline:',(t4-t3):8,',',
                                     Round(100*(t4-t3)/tot):4,'%');
            writeln('    total time: ',tot:8);
        end;
    end; (* GetSpline *)

    (*$IfC rangeOn *) (*$R+*) (*$EndC*)
(* ********************************************************************* *)

    procedure SmoothObj(* obj: TObjPtr *);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        if traceCmd then with obj^.oDat do
            writeln('SmoothObj(',Ord(obj):10,')',Ord(cLst):10,',',cLst^^.crdCnt:2);

        with obj^,oDat do
        if locked=0 then
            if (kind=iObjFHnd) OR (kind=iObjPoly) then begin
                GetSpline(cLst,bBox);
                (* finish bBox calc *)
                AddPt(penSizes[inf.pnSiz],bBox.botRight);
                (* change iObjPoly to iObjFHnd *)
                obj^.oDat.kind := iObjFHnd;
            end;
    end; (* SmoothObj *)

(* ********************************************************************* *)

    procedure UnSmthObj(* obj: TObjPtr *);
    const
        cSmthFactor = 2;
    var thisCLst: TCLstHnd;
        i,j: Integer;
        crdPt: Point;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        if traceCmd then with obj^.oDat do
            writeln('UnSmthObj(',Ord(obj):10,')',Ord(cLst):10,',',cLst^^.crdCnt:2);

        with obj^,oDat do
        if locked=0 then
            if (kind=iObjFHnd) then begin
                thisCLst := cLst;

                OpenCLst(HzFromH(Pointer(Ord(cLst))),
                         cLst,(thisCLst^^.crdCnt DIV cSmthFactor)+1,kind);
                cLst^^.beg2D  := thisCLst^^.beg2D;
                cLst^^.cClose := thisCLst^^.cClose;
                SetPt(crdPt,0,0);
                j := 1;
                (* throw away 1/cSmthFactor of the total points *)
                for i := 1 to thisCLst^^.crdCnt do begin
                    GetNode(thisCLst,i,crdPt);
                    if j=cSmthFactor then begin
                        j := 1;
                        PutNode(cLst,crdPt.h,crdPt.v);
                        SetPt(crdPt,0,0);
                    end else
                        j := j+1;
                end;
                if j>1 then
                    PutNode(cLst,crdPt.h,crdPt.v);

                CloseCLst(cLst);
                FreeCLst(thisCLst);
                GetBBox(obj);
            end;
    end; (* UnSmthObj *)

(* ********************************************************************* *)
    (*$S IN2*)

    procedure InitCrv;
    var step: Integer;
        t,tsqu,tcube: real;
        t1,t2,t3,t4: Boolean;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        if traceCmd then
            Writeln('InitDat');

        (* init spline weights *)
        FOR step := 0 TO nsteps DO BEGIN
            t        := step / nsteps;
            tsqu     := t * t;
            tcube    := t * tsqu;
            E0[step] := Round(65536*(-tcube/6 + tsqu/2 - t/2 + 1/6));
            E1[step] := Round(65536*(tcube/2 - tsqu + 2/3));
        END;
    end; (* InitCrv *)




ÿ