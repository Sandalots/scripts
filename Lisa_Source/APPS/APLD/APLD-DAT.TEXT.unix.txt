(* ********************************************************************* *)

(* CopyRight 1983,1984 Apple Computer Inc. *)
(* GE Data Base Access Routines *)

(* ********************************************************************* *)

UNIT Dat;

(* Segment UTL *)
INTERFACE
USES
    (*$U UNITSTD *)    UnitStd,    (* interface dependency  *)
    (*$U UNITHZ*)      UnitHz,
    (*$U STORAGE*)     Storage,
    (*$U QUICKDRAW *)  QuickDraw,
    (*$U FONTMGR*)     FontMgr,
    (*$U LibOS/Syscall *)    Syscall,
    {$U LibPm/PmDecL}      PmDecL,
    {$U PrStdInfo}     PrStdInfo,
    {$U PrPublic}      PrPublic,
    (*$U apld/HDR*)    Hdr,
    {$IFC sampcalls}
    {$U apld/tracecalls.obj}  tracecalls,
    {$ENDC}
                                   (* used only for ElapsTime *)
    (*$U WM.EVENTS*)   Events,

    (*$U GRAFUTIL*)    GrafUtil,   (* implementation unit   *)
    (*$U ApLd/TXT*)      Txt,
    (*$U ApLd/UTL*)      Utl;

CONST
    (* directions for scrolling, lineUp *)
    iDirLft     = 0;
    iDirCtH     = 1;
    iDirRgt     = 2;
    iDirTop     = 3;
    iDirCtV     = 4;
    iDirBot     = 5;

    (* curve constants *)
    nsteps      = 10;           (* number of steps per spline   *)
    maxC2DCnt   = 100;
    maxCrdCnt   = 100;
    maxSplineCnt= 1000;

TYPE
    TStatRec   =
        record
            (* counts *)
            numObj,numArc,numCrd,numC2D,numTxt,numFmt,numGrp: LongInt;
            (* total size in bytes *)
            sizObj,sizArc,sizCrd,sizC2D,sizTxt,sizFmt,sizGrp: LongInt;
            (* size in bytes of largest *)
            maxCrd,maxC2D,maxTxt,maxFmt,maxGrp: LongInt;
        end;

    (* Statistics *)
    function  SizeOfCLst(cLst: TCLstHnd): Integer;
    function  SizeOfTxt(obj: TObjPtr): Integer;
    function  SizeOfObj(obj: TObjPtr): LongInt;
    Function  CntOfObj (obj: TObjPtr): Integer;
    procedure GetOLstStats(oLst: TObjPtr; var thisStat: TStatRec);
    procedure GetSLstStats(sLst: TSelHnd; var thisStat: TStatRec);
    procedure GetStatSiz  (thisStat: TStatRec; VAR hSiz,pSiz: LongInt);

    (* Heap Expansion and checking *)
    function  HzRefNum(hz: THz): integer;
    procedure ExpandHz(hz: THz; howMuch: LongInt; VAR errNum: Integer);
    procedure ExpandMem(hSiz,pSiz: LongInt; VAR errNum: Integer);

    (* Allocation and De-Allocation *)
    procedure FreeText(var tHnd: TH);
    function  NewText (tLen: Integer): TH;
    procedure FreeFmts(var fHnd: TH);
    function  NewFmts (fLen: Integer): TH;
    function  NewArc: TArcHnd;
    procedure FreeArc (var arc: TArcHnd);
    procedure InitObj (oKind: Integer; obj: TObjPtr);
    function  NewObj  (oKind: Integer): TObjPtr;
    procedure FreeObj (var obj: TObjPtr);

    (* CrdLst Management *)
    procedure UpdateCLst(srcCLst,dstCLst: TCLstHnd;delta:Point2D;
                          vrtLst:THndVrtLst);
    procedure FreeCLst (var cLst: TCLstHnd);
    procedure OpenCLst (hz: THz; var cLst: TCLstHnd;
                        maxCnt: Integer; kind: Integer);
    procedure CloseCLst(cLst: TCLstHnd);
    procedure PutPNode(cLst: TCLstHnd; pt2D: Point2D);
    procedure PutNode  (cLst: TCLstHnd; dx,dy: Real);
    procedure SetNode  (cLst: TCLstHnd; node: Integer; dx,dy: Real);
    procedure GetNode  (cLst: TCLstHnd; node: Integer; var crdPt: Point);
    procedure GetNode2D(cLst: TCLstHnd; node: Integer; var crd2D: Point2D);
    procedure DupCLst  (src:  TCLstHnd; var dst: TCLstHnd);

    (* Object Data Base Management *)
    procedure InitOLst (var lst:  TObjLst);
    procedure InitODB  (var oLst: TODBLst);
    procedure AddToOBeg(var oLst: TODBLst; obj: TObjPtr);
    procedure AddToOEnd(var oLst: TODBLst; obj: TObjPtr);
    procedure SubFmOLst(var oLst: TODBLst; obj: TObjPtr);
    procedure DltAll   (var oLst: TODBLst);
    procedure DupObj   (srcObj:   TObjPtr; var dstObj: TObjPtr);

    (* Object Transformations *)
    procedure SetInf( VAR inf: TObjInf; kind,size,pPat,fPat,pDat:Integer;
        frgdClr, bkgdClr: TPByte );
    procedure SetObjRnd (obj: TObjPtr; rnd: Integer);
    procedure SetObjInf( obj: TObjPtr; size,pPat,fPat,pDat: Integer;
        frgdClr, bkgdClr: TPByte );
    procedure SetObjFmt (obj: TObjPtr; face,font: Integer);
    procedure SetObjCase(obj: TObjPtr; nCase: Integer);
    procedure EditObj   (obj: TObjPtr; start,angle: Integer; r1,r2,r3,r4: Real);
    procedure GridObj   (obj: TObjPtr);
    procedure AlignObj  (obj: TObjPtr; dir: Integer; xA,yA: Real);
    procedure RotateObj (obj: TObjPtr; ctr2D: Point2D; amount: Integer);
    procedure ScaleObj  (obj: TObjPtr; ctr2D: Point2D; hScl,vScl: Real);
    procedure OffsetObj (obj: TObjPtr; dx,dy: real);
    procedure GetBBox   (obj: TObjPtr);
    procedure GetGBox   (obj: TObjPtr; VAR gBox: Rect);

    (* knobs *)
    procedure GetDKnobRect(pt: Point; VAR r: Rect);
    procedure GetObjKnobs(obj: TObjPtr; var knobs: TKnobs);

    (* Miscellaneous *)
    procedure GetArrwRect(endPt: Point; pnSiz: Integer; Var aRect: Rect);
    procedure CkBBox(pt: Point; pnSiz: Integer; var bBox: Rect);
    procedure InitDat;

    (* Curves *)
    { procedure SmoothObj(obj: TObjPtr);
    procedure UnSmthObj(obj: TObjPtr);
    procedure InitCrv; }

    (* Tracing *)
    procedure WrtRect(r: Rect);
    procedure WLnRect(s: String80; r: Rect);
    procedure WlnRgn (s: String80; r: RgnHandle);
    procedure WrtObj(obj: TObjPtr; indentLevel: Integer);
    procedure DumpCLst(cLst: TCLstHnd);
    procedure DumpDB(dumpLst: Boolean);
    procedure WrtDwgStats(thisStat: TStatRec);
    procedure DumpDoc(doc: TDocHnd);
    procedure DumpDocLst(s: String80);
    procedure WlnPic(s: String80; thisPic: PicHandle);
    procedure WrtTime(s: String80; VAR t: LongInt);
    procedure CkGEHz(s: String80);
    procedure DumpCurCLst;
    procedure GEDumpSel(sel: TSelHnd);


(* ********************************************************************* *)

IMPLEMENTATION


(* ********************************************************************* *)

VAR
    (* arrow sizes for bBox calc *)
    arrwSz:     Array[1..4] of Point;
    (* arrow arc rects for arrwRect calc *)
    arrwRects:  Array[1..4] of Rect;

    (* spline weight tables *)
    E0,E1:      ARRAY[0..nSteps]    of LongInt;

    (* in SEL *)
    function  GetSelObj(sel: TSelHnd; sIndx: Integer): TObjPtr; EXTERNAL;

(* ********************************************************************* *)

(* Statistics *)

(* ********************************************************************* *)
(*$S Core*)
    Function  SizeOfCLst(* cLst: TCLstHnd): Integer *);
    var thisCLst: TCLstRec;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        thisCLst := cLst^^;
        if thisCLst.crdKind=iObjFHnd then
            SizeOfCLst := (thisCLst.crdCnt+2)*SizeOf(TCrdRec)+SizeOf(TCLstRec)
        else
            SizeOfCLst := (thisCLst.crdCnt+2)*SizeOf(Point2D)+SizeOf(TCLstRec);
    end; (* SizeOfCrdLst *)

(* ********************************************************************* *)
(*$S Core*)
    Function  SizeOfTxt(* obj: TObjPtr): Integer *);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        with obj^.oDat.inf do
        SizeOfTxt := tLen+fLen*runSize;
    end; (* SizeOfTxt *)

(* ********************************************************************* *)
(*$S Core*)
    Function  SizeOfObj(* obj: TObjPtr): LongInt *);
    var size: LongInt;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        size := SizeOf(TObjRec);
        with obj^.oDat do
        case kind of
            iObjText:  size := size+SizeOfTxt(obj);
            iObjArc:   size := size+SizeOf(TArcRec);
            iObjFHnd,
            iObjPoly,
            iObjSPoly:  size := size+SizeOfCLst(cLst);
            iObjGroup: size := size+grp.lSiz;
        end; (* case *)
        SizeOfObj := size;
    end; (* SizeOfObj *)

(* ********************************************************************* *)
(*$S Core*)
    Function  CntOfObj(* obj: TObjPtr): Integer *);
    var cnt: Integer;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        cnt := 1;
        with obj^.oDat do
        if kind=iObjGroup then
            cnt := grp.lTot+1;
        CntOfObj := cnt;
    end; (* CntOfObj *)

(* ********************************************************************* *)

(* obj stats *)

(* ********************************************************************* *)
(*$S Ring*)
    procedure SetStat(VAR siz,max: LongInt; thisSize: LongInt);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        siz := siz+thisSize;
        if thisSize>max then
            max := thisSize;
    end; (* SetStat *)

(* ********************************************************************* *)
(*$S Ring*)
    procedure CntObj(obj: TObjPtr; VAR thisStat: TStatRec); FORWARD;
    procedure CntOLst(lst: TObjPtr; VAR thisStat: TStatRec);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        while lst<>NIL do begin
            CntObj(lst,thisStat);
            lst := lst^.fLink;
        end;
    end; (* CntOLst *)

(* ********************************************************************* *)
(*$S Ring*)
    procedure CntObj(* obj: TObjPtr; VAR thisStat: TStatRec *);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        with thisStat do begin
            numObj := numObj+1;
            with obj^,oDat do
            if kind=iObjArc then begin
                numArc := numArc+1;
            end else if kind=iObjFHnd then begin
                numCrd := numCrd+1;
                SetStat(sizCrd,maxCrd,SizeOfCLst(cLst));
            end else if (kind=iObjPoly) OR (kind=iObjSPoly)then begin
                numC2D := numC2D+1;
                SetStat(sizC2D,maxC2D,SizeOfCLst(cLst));
            end else if kind=iObjGroup then begin
                numGrp := numGrp+1;
                SetStat(sizGrp,maxGrp,grp.lSiz);
                CntOLst(grp.lBeg,thisStat);
            end else if kind=iObjText then with inf do begin
                numTxt := numTxt+1;
                numFmt := numFmt+1;
                SetStat(sizTxt,maxTxt,tLen);
                SetStat(sizFmt,maxFmt,fLen);
            end;
        end;
    end; (* CntObj *)

(* ********************************************************************* *)
(*$S Ring*)
    procedure InitStat(VAR thisStat: TStatRec);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        with thisStat do begin
            numObj := 0;
            numArc := 0;
            numCrd := 0;
            numC2D := 0;
            numTxt := 0;
            numFmt := 0;
            numGrp := 0;
            sizObj := 0;
            sizArc := 0;
            sizCrd := 0;
            sizC2D := 0;
            sizTxt := 0;
            sizFmt := 0;
            sizGrp := 0;
            maxCrd := 0;
            maxC2D := 0;
            maxTxt := 0;
            maxFmt := 0;
            maxGrp := 0;
        end;
    end; (* InitStat *)

(* ********************************************************************* *)
(*$S Ring*)
    procedure CalcStatSize(VAR thisStat: TStatRec);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        with thisStat do begin
            sizObj := numObj*SizeOf(TObjRec);
            sizArc := numArc*SizeOf(TArcRec);
            sizFmt := sizFmt*runSize;
        end;
    end; (* CalcStatSize *)

(* ********************************************************************* *)
(*$S Ring*)
    procedure GetOLstStats(* oLst: TObjPtr; VAR thisStat: TStatRec *);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        InitStat(thisStat);
        CntOLst(oLst,thisStat);
        CalcStatSize(thisStat);
    end; (* GetOLstStats *)

(* ********************************************************************* *)
(*$S Ring*)
    procedure GetSLstStats(* sLst: TSelHnd; VAR thisStat: TStatRec *);
    var i: Integer;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        InitStat(thisStat);
        for i := 1 to sLst^^.sCnt do
            CntObj(GetSelObj(sLst,i),thisStat);
        CalcStatSize(thisStat);
    end; (* GetSLstStats *)

(* ********************************************************************* *)
(*$S Ring*)
    procedure GetStatSiz(* thisStat: TStatRec; VAR hSiz,pSiz: LongInt *);
    const
       pHzOverhead = 2;
       hHzOverhead = 4;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        with thisStat do begin
            pSiz := sizObj+numObj*pHzOverhead;
            hSiz := sizArc+sizCrd+sizC2D+sizTxt+sizFmt+
                    (numArc+numCrd+numC2D+numTxt+numFmt)*hHzOverhead;
        end;
    end; (* GetStatSiz *)

(* ********************************************************************* *)

(* Allocation and De-Allocation *)

(* ********************************************************************* *)
(*$S Reshape*)
    function HzRefNum(* hz: THz): integer *);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        if hz=curDoc.pHz then
            HzRefNum := curDoc.pRef
        else if hz=curDoc.hHz then
            HzRefNum := curDoc.hRef
        else
            HzRefNum := geHHzRef;
    end; (* HzRefNum *)

(* ********************************************************************* *)
(*$S Core*)
    procedure ExpandHz(* hz: THz; howMuch: LongInt; VAR errNum: Integer *);
    var newMemSize,newDiskSize: LongInt;
        s: String80;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        errNum := 0;
        if hz^.cbFree<howMuch then begin
            howMuch := howMuch-hz^.cbFree;

            (* round up to nearest K *)
            howMuch := ((howMuch DIV 1024)+1)*1024;
            Size_DataSeg(errNum,HzRefNum(hz),
                         howMuch,newMemSize,
                         howMuch,newDiskSize);

            if traceOn then
                writeln('ExpandHz by ',howMuch:8,', to ',newMemSize:10,', err=',errNum);

            (* check for errors *)
            if errNum<=0 then begin
                errNum := 0;
                EnlargeHz(hz,howMuch);
            end else if errNum in [309,311,312] then
                (* not enough room on disk, notify user *)
                errNum := iStopDisk
            else if errNum in [304,306,315] then
                (* not enough room in memory, notify user *)
                errNum := iStopMem
            else
                (* unknown failure is FATAL *)
                Error('ExpandHz',errNum,FATAL);
        end;
    end; (* ExpandHz *)

(* ********************************************************************* *)
(*$S Core*)
    procedure ExpandMem(* hSiz,pSiz: LongInt; VAR errNum: Integer *);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        ExpandHz(curDoc.hHz,hSiz+512,errNum);
        if errNum<=0 then
            ExpandHz(curDoc.pHz,pSiz+512,errNum);
    end; (* ExpandMem *)

(* ********************************************************************* *)
(*$S Core*)
    procedure FreeText(* var tHnd: TH *);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        FreeH(HzFromH(tHnd),Pointer(Ord(tHnd)));
        tHnd := NIL;
    end; (* FreeText *)

(* ********************************************************************* *)
(*$S Core*)
    function  NewText(* tLen: Integer): TH *);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        NewText := HAllocate(curDoc.hHz,tLen);
    end; (* NewText *)

(* ********************************************************************* *)
(*$S Core*)
    procedure FreeFmts(* var fHnd: TH *);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        FreeH(HzFromH(fHnd),Pointer(Ord(fHnd)));
        fHnd := NIL;
    end; (* FreeFmts *)

(* ********************************************************************* *)
(*$S Core*)
    function  NewFmts(* fLen: Integer): TH *);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        NewFmts := HAllocate(curDoc.hHz,fLen*runSize);
    end; (* NewFmts *)

(* ********************************************************************* *)
(*$S Core*)
    function  NewArc(* : TArcHnd *);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        NewArc := Pointer(Ord(HAllocate(curDoc.hHz,SizeOf(TArcRec))));
    end; (* NewArc *)

(* ********************************************************************* *)
(*$S Core*)
    procedure FreeArc(* var arc: TArcHnd *);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        FreeH(HzFromH(Pointer(Ord(arc))),Pointer(Ord(arc)));
    end; (* FreeArc *)

(* ********************************************************************* *)
(*$S Core*)
    procedure InitObj(* oKind: Integer; obj: TObjPtr *);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        with obj^,oDat do begin
            fLink  := NIL;
            bLink  := NIL;
            cSel   := iSelNil;
            pSel   := iSelNil;
            kind   := oKind;
            locked := 0;
            with inf do begin
                fgClr := iClrBlack;
                bgClr := iClrWhite;
                pnSiz := 1;                     (* 1x1   *)
                pnPat := iPenBlack;
                flPat := iFillClear;
                dat   := 0;
            end;
        end;
    end; (* InitObj *)

(* ********************************************************************* *)
(*$S Core*)
    function  NewObj(* oKind: Integer): TObjPtr *);
    var thisObj: TObjPtr;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        thisObj := Pointer(Ord(PAllocate(curDoc.pHz,SizeOf(TObjRec))));
        if rangeOn then
            if thisObj=Pointer(Ord(pNIL)) then
                Error('NewObj: PAllocate returned NIL!!!',0,FATAL);
        InitObj(oKind,thisObj);
        NewObj := thisObj;
    end; (* NewObj *)

(* ********************************************************************* *)
(*$S Core*)
    procedure FreeObj(* var obj: TObjPtr *);
    var gObj,nObj: TObjPtr;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        if obj <> NIL then
            with obj^,oDat do begin
                (* free auxiliary data *)
                if obj=curDoc.txtObj then
                    CloseCFld;
                if (kind=iObjFHnd) OR (kind=iObjPoly) OR (kind=iObjSPoly) then
                    FreeCLst(cLst)
                else if kind=iObjArc then
                    FreeArc(arc)
                else if kind=iObjText then begin
                    FreeH(HzFromH(tHnd),tHnd);
                    FreeH(HzFromH(fHnd),fHnd);
                end else if kind=iObjGroup then begin
                    gObj := grp.lBeg;
                    while gObj<>NIL do begin
                        nObj := gObj^.fLink;
                        FreeObj(gObj);
                        gObj := nObj;
                    end;
                end;
                (* free the obj *)
                FreeP(curDoc.pHz,Pointer(Ord(obj)));
                obj := NIL;
            end;
    end; (* FreeObj *)

(* ********************************************************************* *)

(* CrdLst Management *)

(* ********************************************************************* *)
(*$S Core*)
    procedure FreeCLst(* var cLst: TCLstHnd *);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        if traceCmd then
            writeln('FreeCLst(',Ord(cLst):8,')');

        FreeH(HzFromH(Pointer(Ord(cLst))),Pointer(Ord(cLst)));
        cLst := NIL;
    end; (* FreeCLst *)

(* ********************************************************************* *)
(*$S Ring*)
    procedure OpenCLst(* hz: THz; var cLst: TCLstHnd;
                         maxCnt: Integer; kind: Integer *);
    var size: Integer;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        if kind=iObjFHnd then
            size := SizeOf(TCrdRec)
        else
            size := SizeOf(Point2D);
        size := SizeOf(TCLstRec)+(maxCnt+2)*size;
        If traceHeap then CkGEHz('OpenCLst before');
        cLst := Pointer(Ord(HAllocate(hz,size)));
        If traceHeap then CkGEHz('OpenCLst after');
        with cLst^^ do begin
            crdKind := kind;
            crdCnt := 0;
            crdMax := maxCnt;
            cClose := iCLstNil;
        end;
    end; (* OpenCLst *)

(* ********************************************************************* *)
(*$S Ring*)
    procedure CloseClst(* cLst: TCLstHnd *);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        if traceCmd then
            writeln('CloseCLst(',Ord(cLst):8,'), cnt=',cLst^^.crdCnt:2);

        ChangeSizeH(HzFromH(Pointer(Ord(cLst))),
                    Pointer(Ord(cLst)),SizeOfCLst(cLst));
        with cLst^^ do
            crdMax := crdCnt;
    end; (* CloseCLst *)

(* ********************************************************************* *)
(*$S Ring*)
    procedure SetNode(* cLst: TCLstHnd; node: Integer; dx,dy: Real *);
    VAR dh,dv: Integer;

    (* ***************************************************************** *)

        procedure Check(VAR i: Integer);
        begin
        {$IFC sampcalls} ALogCall; {$ENDC}
            if i>127 then
                i := 127
            else if i<-127 then
                i := -127;
        end;

    (* ***************************************************************** *)

    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        with cLst^^ do
        if node=0 then begin
            SetPt2D(beg2D,dx,dy);
            If crdKind=iObjPoly then
                c2DLst[0] := beg2D;
        end else if (node>0) AND (node<=crdMax) then begin
            (*$R-*)
            if crdKind=iObjFHnd then begin
                dh := Round(dx);
                dv := Round(dy);
                Check(dh);
                Check(dv);
                crdLst[node].dh := dh;
                crdLst[node].dv := dv;
            end else begin
                c2DLst[node].x := c2DLst[node-1].x + dx;
                c2DLst[node].y := c2DLst[node-1].y + dy;
            end;
            (*$IfC rangeOn *)
            (*$R+*)
            (*$EndC *)
        end;
    end; (* SetNode *)

(* ********************************************************************* *)
(*$S PIC*)
    procedure PutPNode(*cLst: TCLstHnd; pt2D: Point2D *);
    begin  {As is apparent, this does not apply to the first point.}
        With cLst^^ do begin
            crdCnt := crdCnt + 1;
            {$R-}
            c2DLst[crdCnt] := pt2D;
            {$IfC rangeOn} {$R+} {$EndC}
        end;
    end;{PutPNode}

(* ********************************************************************* *)
(*$S BasicOps*)
    procedure PutNode(* cLst: TCLstHnd; dx,dy: Real *);
    VAR i,dh,dv,dMax,dNum: Integer;
        tx,ty: Real;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        with cLst^^ do
        if (crdCnt>=0) and (crdCnt<maxCrdCnt) then begin
            if crdKind=iObjFHnd then begin
                dh := Round(dx);
                dv := Round(dy);
                dMax := CMax(ABS(dh),ABS(dv));
                if dMax>127 then begin
                    dNum := dMax DIV 127;
                    tx := dx/(dNum+1);
                    ty := dy/(dNum+1);
                    for i := 0 to dNum-1 do
                        PutNode(cLst,tx,ty);
                    dx := Round(dx-(dNum*Round(tx)));
                    dy := Round(dy-(dNum*Round(ty)));
                end;
            end;
            crdCnt := crdCnt+1;
            SetNode(cLst,crdCnt,dx,dy);
        end;
    end; (* PutNode *)

(* ********************************************************************* *)
(*$S Core*)
    procedure GetNode(* cLst: TCLstHnd; node: Integer; var crdPt: Point *);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        with cLst^^ do
        if node=0 then
            WldToScrn(beg2D,crdPt)
        else begin
            (*$R-*)
            if crdKind=iObjFHnd then begin
                crdPt.h := crdPt.h+crdLst[node].dh;
                crdPt.v := crdPt.v+crdLst[node].dv;
            end else begin
                crdPt.h := crdPt.h+Round(c2DLst[node].x - c2DLst[node-1].x);
                crdPt.v := crdPt.v+Round(c2DLst[node].y - c2DLst[node-1].y);
            end;
            (*$IfC rangeOn *)
            (*$R+*)
            (*$EndC *)
        end;
    end; (* GetNode *)

(* ********************************************************************* *)
(*$S Ring*)
    procedure GetNode2D(* cLst: TCLstHnd; node: Integer; var crd2D: Point2D *);
    begin   {This is used now only for freehand}
        {$IFC sampcalls} ALogCall; {$ENDC}
        with cLst^^ do
        if node=0 then
            crd2D := beg2D
        else begin
            (*$R-*)
            crd2D.x := crd2D.x+crdLst[node].dh;
            crd2D.y := crd2D.y+crdLst[node].dv;
            (*$IfC rangeOn *)
            (*$R+*)
            (*$EndC *)
        end;
    end; (* GetNode2D *)

(* ********************************************************************* *)
(*$S Core*)
    procedure DupCLst(* src: TCLstHnd; var dst: TCLstHnd *);
    var i: integer;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        dst := Pointer(Ord(HAllocate(curDoc.hHz,SizeOfCLst(src))));
        If traceHeap then CkGEHz('From DupCLst');
        dst^^ := src^^;
        with src^^ do begin
            {
            dst^^.beg2D   := beg2D;
            dst^^.crdCnt  := crdCnt;
            dst^^.crdMax  := crdMax;
            dst^^.cClose  := cClose;
            dst^^.crdKind := crdKind;
            }
            (*$R-*)
            If crdKind=iObjFHnd then
                For i := 0 to crdCnt do
                    dst^^.crdLst[i] := crdLst[i]
            else
                For i := 0 to crdCnt+2 do
                    dst^^.c2DLst[i] := c2DLst[i];
            (*$IfC rangeOn *)
            (*$R+*)
            (*$EndC *)
        end;
    end; (* DupCLst *)

(* ********************************************************************* *)

(* Object Data Base Management *)

(* ********************************************************************* *)
(*$S Core*)
    procedure InitOLst(* var lst: TObjLst *);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        with lst do begin
            lBeg := NIL;
            lEnd := NIL;
            lCnt := 0;
            lTot := 0;
            lSiz := 0;
        end;
    end;

(* ********************************************************************* *)
(*$S Core*)
    procedure InitODB(* var oLst: TODBLst *);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        with oLst do begin
            InitOLst(lst);
            tBeg := NIL;
            oEnd := NIL;
            lCur := NIL;
        end;
    end;

(* ********************************************************************* *)
(*$S Arrange*)
    procedure AddToOBeg(* var oLst: TODBLst; obj: TObjPtr *);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        if traceCmd then
            writeln('AddToOBeg.');

        (* check for trfoObj *)
        if obj=trfoObj then
            obj := trfoRef;
        with oLst,lst,obj^ do begin
            if oDat.kind=iObjText then begin
                fLink := tBeg;
                bLink := oEnd;
                if lEnd=NIL then begin
                    lBeg := obj;
                    lEnd := obj;
                end;
                if tBeg=NIL then
                    lEnd := obj
                else
                    tBeg^.bLink := obj;
                tBeg := obj;
                if oEnd=NIL then
                    lBeg := obj
                else
                    oEnd^.fLink := obj;
            end else begin
                fLink := lBeg;
                bLink := NIL;
                if lEnd=NIL then
                    lEnd := obj
                else
                    lBeg^.bLink := obj;
                lBeg := obj;
                if oEnd=NIL then
                    oEnd := obj
            end;
            lCnt := lCnt+1;
            lTot := lTot+CntOfObj(obj);
            lSiz := lSiz+SizeOfObj(obj);
        end;
    end; (* AddToOBeg *)

(* ********************************************************************* *)
(*$S Core*)
    procedure AddToOEnd(* var oLst: TODBLst; obj: TObjPtr *);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        if traceCmd then
            writeln('AddToOEnd.');

        (* check for trfoObj *)
        if obj=trfoObj then
            obj := trfoRef;
        with oLst,lst,obj^ do begin
            if oDat.kind=iObjText then begin
                fLink := NIL;
                bLink := lEnd;
                if lEnd=NIL then
                    lBeg := obj
                else
                    lEnd^.fLink := obj;
                lEnd := obj;
                if tBeg=NIL then
                    tBeg := obj;
            end else begin
                fLink := tBeg;
                bLink := oEnd;
                if oEnd=NIL then
                    lBeg := obj
                else
                    oEnd^.fLink := obj;
                oEnd := obj;
                if tBeg=NIL then
                    lEnd := obj
                else
                    tBeg^.bLink := obj;
            end;
            lCnt := lCnt+1;
            lTot := lTot+CntOfObj(obj);
            lSiz := lSiz+SizeOfObj(obj);
        end;
    end; (* AddToOEnd *)

(* ********************************************************************* *)
(*$S Ring *)
    procedure SubFmOLst(* var oLst: TODBLst; obj: TObjPtr *);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        if traceCmd then
            writeln('SubFmOLst.');
        if rangeOn then
            if obj=NIL then
                Error('SubFmOLst -- obj is NIL.',0,FATAL);

        (* check for trfoObj *)
        if obj=trfoObj then
            obj := trfoRef;
        with oLst,lst,obj^ do begin
            if tBeg=Obj then
                tBeg := obj^.fLink
            else if oEnd=obj then
                oEnd := obj^.bLink;
            if (lBeg=obj) and (lEnd=obj) then begin
                lEnd := NIL;
                lBeg := NIL;
            end else if lBeg=obj then begin
                fLink^.bLink := NIL;
                lBeg := fLink;
            end else if lEnd=obj then begin
                bLink^.fLink := NIL;
                lEnd := blink;
            end else begin
                fLink^.bLink := bLink;
                bLink^.fLink := fLink;
            end;
            lCnt := lCnt-1;
            lTot := lTot-CntOfObj(obj);
            lSiz := lSiz-SizeOfObj(obj);
        end;
    end; (* SubFmOLst *)

(* ********************************************************************* *)
(*$S IO*)
    procedure DltAll(* var oLst: TODBLst *);
    var obj: TObjPtr;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        if traceCmd then
            writeln('DltAll.');

        with oLst.lst do
        while lBeg <> NIL do begin
            obj := lBeg;
            lBeg := lBeg^.fLink;
            FreeObj(obj);
        end;
        InitODB(oLst);
    end; (* DltAll *)

(* ********************************************************************* *)
(*$S Core*)
    procedure DupObj(* srcObj: TObjPtr; var dstObj: TObjPtr *);
    var srcGrp,dstGrp: TObjPtr;
        fmtSize: Integer;
        thisDB:  TODBLst;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        if traceCmd then
            writeln('DupObj.');

        dstObj := NewObj(srcObj^.oDat.kind);
        dstObj^ := srcObj^;
        dstObj^.fLink := NIL;
        dstObj^.bLink := NIL;
        with srcObj^,oDat do
        if kind=iObjArc then begin
            dstObj^.oDat.arc := NewArc;
            dstObj^.oDat.arc^^ := arc^^;
        end else if kind=iObjText then begin
            (* copy curValue and fmtInfo *)
            dstObj^.oDat.tHnd := NewText(inf.tLen);
            MoveLeft(tHnd^^,dstObj^.oDat.tHnd^^,inf.tLen);
            fmtSize := inf.fLen*runSize;
            dstObj^.oDat.fHnd := NewFmts(inf.fLen);
            MoveLeft(fHnd^^,dstObj^.oDat.fHnd^^,fmtSize);
        end else if (kind=iObjfHnd) or (kind=iObjPoly) OR (kind=iObjSPoly) then
            DupCLst(cLst,dstObj^.oDat.cLst)
        else if kind=iObjGroup then begin
            srcGrp := srcObj^.oDat.grp.lBeg;
            InitODB(thisDB);
            while srcGrp<>NIL do begin
                DupObj(srcGrp,dstGrp);
                AddToOEnd(thisDB,dstGrp);
                srcGrp := srcGrp^.fLink;
            end;
            dstObj^.oDat.grp := thisDB.lst;
        end;
    end; (* DupObj *)

(*$I ApLd/DA1.text *)
{(*$I ApLd/DA2.text *)}
(*$I ApLd/DA3.text *)

end. (* unit *)

ÿ