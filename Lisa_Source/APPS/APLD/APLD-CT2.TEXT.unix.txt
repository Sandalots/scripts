(* ********************************************************************* *)

(* CopyRight 1983, Apple Computer Inc. *)
(* GE Unit CTR Include File - Event Handling Routines *)

(* ********************************************************************* *)

(* Menu Event Control *)
(*$S CTR*)

(* ********************************************************************* *)
(*$S IO*)
    procedure ScrapMenuEvent(thisEvent: EventRec);
    var menu,item: Integer;
        s,n: Str255;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        if thisEvent.what=buttonDown then begin
            (* close "..." *)
            GetItem(geMenus[iMnuExtra],7,@s);
            GetFldrTitle(scrapFolder,n);
            s := Concat(s,' "',n,'"');
            SetItem(geMenus[iMnuScrap],2,@s);

            (* set the port back to menuFolder *)
            SetPort(menuFolder);

            MenuSelect(thisEvent.where,menu,item);
            if item<>0 then
                case item of
                    1: DoFilingCmd(cmdClosAll); (* Set Aside Everything *)
                    2: DoFilingCmd(cmdClose);   (* Set Aside "" *)
                    4: PrBgdCmd;                (* Printing in Progress... *)
                end; (* case *)
            HiLiteMenu(0);
        end;
    end; {ScrapMenuEvent}

(* ********************************************************************* *)
(*$S Core2*)
    procedure GEMenuCmd(menu,item: Integer);
    var thisStat: TStatRec;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        if traceCmd then with curDoc.fldr^.portRect do
            writeln('GEMenuCmd(',menu:2,',',item:2,')',left:2,',',top:2);

        numClk := 1;               {restart click count}
        ckMultiClk := FALSE;
        SetWrkOrigin;
        case menu of
            iMnuFile,iMnuScrap: (* File/Print *)
                case item of
                    1: DoFilingCmd(cmdClosAll); (* Set Aside Everything *)
                    2: DoFilingCmd(cmdClose);   (* Set Aside "" *)
                    3: (* Do Nothing *);        (* ------------------- *)
                    4: PutAwayCmd;              (* Save & Put Away *)
                    5: SaveContCmd;             (* Save & Continue *)
                    6: RevertCmd;               (* Revert to previous version *)
                    7: (* Do Nothing *);        (* ------------------- *)
                    8: PrInsCmd(TRUE);          {  Print As Is           }
                    9: PrPrfCmd;                (* Format for Printer... *)
                   10: PrInsCmd(FALSE);         {  Print...              }
                   11: PrBgdCmd;                (* Printing in Progress... *)
                end; (* case *)
            iMnuEdit: (* Edit menu *)
                begin
                    case item of
                        1: UndoCmd;               (* Undo Last    *)
                        2: (* Do Nothing *);
                        3: CutCmd;                (* Cut *)
                        4: CopyCmd;               (* Copy *)
                        5: PasteCmd(1);           (* Paste (on Top) *)
                        6: (* Do Nothing *);
                        7: ClearCmd;              (* Clear *)
                        8: DupCmd;                (* Duplicate *)
                        9: SelAllCmd;             (* Select All *)
                       10: (* Do Nothing *);
                 11,12,13: TxtCaseCmd(item-10);
                       14: (* Do Nothing *);
                       15: ReshapeCmd;            (* Reshape *)
                    16,17: SmoothCmd(item-15);    (* Smooth,UnSmooth *)
                       18: { Do Nothing };
                       19: SetUpRndCmd(curDoc.rndR,@RndCmd);  (* Corner Radius... *)
                    end;(* case *)
                ShowClBx;                         {Show color box}
                end;
            iMnuType: (* TypeStyle menu *)
                case item of
           1,2,3,4,5,6,8,9: TxtFmtCmd(item,iFmtFont);

    11,12,13,14,15,16,17,18: TxtFmtCmd(iFmtFont,item-8);{I have the
                                                           fontSize array coded
                                                           from 3 to 10}
                end; (* case *)
            iMnuLayout: (* Page Layout menu *)
                case item of
                 1,2,3: RlrOnOffCmd(item);     (* Ruler *)
                     4: (* Do Nothing *);
                   5,6: SizeCmd(item-4);       (* Size *)
                     7: (* Do Nothing *);
                8,9,10: PaperCmd(item-7);      (* Plain,Graph,Grid *)
                    11: (* Do Nothing *);
              12,13,14: ZoomCmd(item-11);      (* Normal Size, Reduce To Fit, Reduce 70% *)
                    15: (* Do Nothing *);
                 16,17: PageBrkCmd(item-15);   (* Preview *)
                    18: (* Do Nothing *);
                    19: SetUpRlrCmd(@RlrCmd);  (* Ruler ... *)
                    20: SetUpDwgCmd(@DwgCmd);  (* Drawing Size...*)
                end; (* case *)
            iMnuArrange: (* Arrangement menu *)
                case item of
                     1: TopCmd;                (* Bring to Top *)
                     2: BottomCmd;             (* Send to Bottom *)
                   3,4: PasteCmd(item-1);      {Paste in Front, Paste Behind}
                     5: (* Do Nothing *);
                     6: GroupCmd;              (* Group Objects *)
                     7: UnGroupCmd;            (* Ungroup Objects *)
                  8,9: LockCmd(item-7);       (* Lock,UnLock *)
                    10: (* Do Nothing *);
     11,12,13,14,15,16: LineUpCmd(item-11);    (* Align Objects *)
                    17: GridObjCmd;            (* Align to Grid *)
                    18: {Do Nothing };
           19,20,21,22: RotateCmd(item-18);    (* Rotate Left, Rotate Right, Flip Horiz, Flip Vert *)
                end; (* case *)
            iMnuFill:
                FillStyleCmd(item);             (* FillStyle menu *)
            iMnuLines:
                LineStyleCmd(item);             (* LineStyle menu *)
            iMnuPen:
                PenStyleCmd(item);              (* PenStyle menu *)
            iMnuColor:
                begin
                    ColorCmd(item);                 {Color Menu}
                    ShowClBx;                       {Show color box}
                end;
            iMnuDBug: (* Trace menu *)
                (*$IfC traceOn *)
                case item of
                     1:  begin
                             SetTraceCmd(pxlDist, 'Pixel Distance Flag');
                             ShowCurPen(curDoc.proto);
                         end;
                     2: (* All Off *)
                         begin
                             traceCmd   := FALSE;
                             traceEvt   := FALSE;
                             traceFio   := FALSE;
                             traceFld   := FALSE;
                             traceTrk   := FALSE;
                             traceUop   := FALSE;
                             traceHeap  := FALSE;
                             TxtFldTrace(FALSE);
                             Writeln('Trace Flags off');

                             writeln('    Record Sizes: ');
                             writeln('        Obj:   ',SizeOf(TObjRec):2);
                             writeln('        ODat:  ',SizeOf(TObjDat):2);
                             writeln('        OInf:  ',SizeOf(TObjInf):2);
                             writeln('        Crd:   ',SizeOf(TCrdRec):2);
                             writeln('        Sel:   ',SizeOf(TSelLst):2);
                             writeln('        GEAtr: ',SizeOf(TGEAtrRec):2);
                             writeln('        DocInf:',SizeOf(TDocInfo):2);
                         end;
                     3: SetTraceCmd(traceEvt, 'TraceEvt');
                     4: SetTraceCmd(traceCmd, 'TraceCmd');
                     5: SetTraceCmd(traceTrk, 'TraceTrk');
                     6: begin
                             SetTraceCmd(traceFld, 'TraceFld');
                             TxtFldTrace(traceFld);
                        end;
                     7: begin
                             SetTraceCmd(traceFio, 'TraceFio');
                             Writeln('FileName="',curDoc.fName^^,'"');
                        end;
                     8: begin
                             SetTraceCmd(geHHz^.fScramble,'Heap Scramble');
                             curDoc.hHz^.fScramble := geHHz^.fScramble;
                             curDoc.pHz^.fScramble := geHHz^.fScramble;
                             writeln('Compacts: ',geHHz^.cCompact:4,',',
                                                 curDoc.hHz^.cCompact:4,',',
                                                 curDoc.pHz^.cCompact:4);
                        end;
                     9: SetTraceCmd(traceUop,'TraceUop');
                    10: begin
                             SetTraceCmd(traceHeap, 'TraceHeap');
                             GetOLstStats(curDoc.oLst.lst.lBeg,thisStat);
                             WrtDwgStats(thisStat);
                             CkGEHz('HeapStats');
                        end;
                    11: DumpDB(FALSE);
                    12: DumpDB(TRUE);
                    13: begin
                            GEDumpSel(curDoc.sLst);
                            GEDumpSel(lastOp.pSLst);
                        end;
                    14: DumpCurFld;
                    15: DumpCurCLst;
                    16: DumpLastOp;
                    17: DumpGEBox;
                    18: DumpDocLst('');
                    {$IFC sampcalls}
                    19: begin
                            AResetTrace(TRUE);
                            AStartCallLog;

                            {SetTimeState;
                            StartRecording('JOURNAL.DATA',5);}

                        end;
                    {$ENDC}

                    20: begin
                            TestColorLet;
                            {SetTimeState;
                            StartPlayback('JOURNAL.DATA',5);}
                        end;
                    {$IFC sampcalls}
                    21: begin
                            AStopCallLog;
                            AListCalls;
                            {StopRecording;
                            StopPlayback;}
                        end;
                    {$ENDC}
                end; (* case *)
                (*$EndC *)
        end; (* case *)
    end; (* GEMenuCmd *)

(* ********************************************************************* *)
(*$S Core2*)
    procedure GEMenuEvent(thisEvent: EventRec);
    var menu,item: Integer;
        s,n: Str255;
        i,f,count: Integer;
        mFlag,plain,sumPlain: Boolean;
        (* to determine type ckmarks *)
        eface: StyleItem;
        thisSel: TSelPtr;
        thisObj: TObjPtr;
        thisFntid,sumFntid: TFntId;
        ckMark: Array[1..21] of Integer;
        haveFHnd,havePoly,haveSPoly,haveObj,
        fHndNotLocked,polyNotLocked,sPolyNotLocked,notLocked: Boolean;
        frgdClr, bkgdClr: TPByte;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        if traceCmd then
            writeln('GEMenuEvent(',thisEvent.what,')');

        (* handle only buttonDown events *)
        if thisEvent.what=buttonDown then begin
            (* set the port to curDoc.fldr so don't muck up menu text *)
            SetWrkOrigin;

            (* reset curTPat on menu events *)
            curTPat := curDoc.fill[iObjText];

            (* don't allow autoDrw if pull down a menu *)
            ResetAutoDrw;

            (* == File/Print == *)
            (* close "..." *)
            GetItem(geMenus[iMnuExtra],7,@s);
            GetFldrTitle(activeFolder,n);
            s := Concat(s,' "',n,'"');
            SetItem(geMenus[iMnuFile],2,@s);

            (* == Edit == *)
            (* reshape *)
            mFlag := FALSE;
            if curDoc.sLst^^.sState in [iObjArc,iObjFHnd,iObjPoly,iObjSPoly] then
                mFlag := NOT ObjLocked(CurSelObj(1));
            if mFlag then begin
                i := curDoc.sLst^^.sState-4;
                EnableItem(geMenus[iMnuEdit],15);
            end else begin
                i := 2;
                DisableItem(geMenus[iMnuEdit],15);
            end;
            GetItem(geMenus[iMnuExtra],i,@s);
            SetItem(geMenus[iMnuEdit],15,@s);
            (* smooth *)
            haveFHnd := ObjInSLst(iObjFHnd,fHndNotLocked);
            haveSPoly:= ObjInSLst(iObjSPoly,sPolyNotLocked);
            havePoly := ObjInSLst(iObjPoly,polyNotLocked);
            haveFHnd := haveFHnd AND fHndNotLocked;
            havePoly := havePoly AND polyNotLocked;
            haveSPoly := haveSPoly AND sPolyNotLocked;
            if haveFHnd or havePoly then begin
                EnableItem(geMenus[iMnuEdit],16);
            end else begin
                DisableItem(geMenus[iMnuEdit],16);
            end;
            If haveFHnd OR haveSPoly then begin
                EnableItem(geMenus[iMnuEdit],17);
            end else begin
                DisableItem(geMenus[iMnuEdit],17);
            end;

            {Duplicate and Delete Handles}
            mFlag := FALSE;
            If (curDoc.edtState=iObjPoly) OR (curDoc.edtState=iObjSPoly) then begin
                If curDoc.edtObj^.cSel=iSelEdt then begin
                    mFlag := TRUE;
                    GetItem(geMenus[iMnuExtra],8,@s);
                    SetItem(geMenus[iMnuEdit],7,@s);
                    GetItem(geMenus[iMnuExtra],9,@s);
                    SetItem(geMenus[iMnuEdit],8,@s);
                end;
            end;
            If NOT mFlag then begin
                GetItem(geMenus[iMnuExtra],10,@s);
                SetItem(geMenus[iMnuEdit],7,@s);
                GetItem(geMenus[iMnuExtra],11,@s);
                SetItem(geMenus[iMnuEdit],8,@s);
            end;

            (* corner radius *)
            haveObj := ObjInSLst(iObjRndR,notLocked);
            if NOT (haveObj AND notLocked) then
                haveObj := ObjInSLst(iObjRect,notLocked);
            if (haveObj AND notLocked)
            or ((curDoc.sLst^^.sState=iSStPrt) AND (curDoc.proto=iObjRndR)) then
                EnableItem(geMenus[iMnuEdit],19)
            else
                DisableItem(geMenus[iMnuEdit],19);

            (* == TypeStyle == *)
            for i := 1 to 21 do
                ckMark[i] := noMark;
            (* check selection for text with all same fonts, etc. *)
            sumPlain := FALSE;  {Not really being used now 2/14/84}
            sumFntid.fam := -1;
            sumFntid.seteface := [];
            if ((curDoc.sLst^^.sState=iSStPrt) and (curDoc.proto=iObjText)) then
                GetTxtFid(@protoObj[iObjText],sumPlain,sumFntid)
            else if curDoc.sLst^^.sState<>iObjNil then begin
                count := curDoc.sLst^^.sCnt;
                for i := 1 to count do begin
                    thisObj := CurSelObj(i);
                    If thisObj^.oDat.kind=iObjText then begin
                        If lastOp.op in [iOpTxt,iOpFmt] then
                            thisObj := CkObjTrfo(thisObj);
                        GetTxtFid(thisObj,plain,thisFntid);
                        sumFntid := thisFntid;
                        i := count;
                    end;
                end;
            end;
            (* check the menu items *)
            with sumFntid do begin
                For eface := bold to shadow do
                    if eface in seteface then
                        ckMark[mapFaceM[eface]] := checkMark;
                If fam>0 then begin
                    f := fontSize[fam];
                    If f>0 then
                        ckMark[f+8] := checkMark;
                    f := fontStyle[fam];
                    If f=iFontModern then begin
                        ckMark[8] := checkMark;
                        curStyle := iFontModern;
                    end else begin
                        ckMark[9] := checkMark;
                        curStyle := iFontClassic;
                    end;
                end;
            end;
            for i := 1 to 21 do
                MarkItem(geMenus[iMnuType],i,ckMark[i]);


            (* == Page Layout == *)
            for i := 1 to 20 do
                ckMark[i] := noMark;
            with curDoc.atr do begin
                (* rulers *)
                If zoomOn=0 then begin
                    EnableItem(geMenus[iMnuLayout],1);
                    EnableItem(geMenus[iMnuLayout],2);
                    EnableItem(geMenus[iMnuLayout],3);
                end else begin
                    DisableItem(geMenus[iMnuLayout],1);
                    DisableItem(geMenus[iMnuLayout],2);
                    DisableItem(geMenus[iMnuLayout],3);
                end;
                if rlrOn then
                    ckMark[rlrKind] := checkMark
                else
                    ckMark[3] := checkMark;
                (* size *)
                ckMark[5+ORD(NOT msrOn)] := checkMark;
                (* paper *)
                ckMark[8+ORD(grdOn)+ORD(tckOn)] := checkMark;
                (* zoom *)
                ckMark[12+zoomOn] := checkMark;
                (* page break *)
                ckMark[16+ORD(NOT pgBOn)] := checkMark;
            end;
            for i := 1 to 20 do
                MarkItem(geMenus[iMnuLayout],i,ckMark[i]);

            (* == Arrangement == *)
            (* Align to Auto-Grid *)
            if curDoc.atr.grdOn then
                EnableItem(geMenus[iMnuArrange],17)
            else
                DisableItem(geMenus[iMnuArrange],17);

            (* == Color == *)
            For i := 1 to 14 do
                ckMark[i] := noMark;
            GetSlClr(curDoc.sLst, frgdClr, bkgdClr);
            If curDoc.assFlClr then begin   {Assign Fill color}
                ckMark[1] := checkMark;
                If bkgdClr <> iClrMix then ckMark[3 + bkgdClr] := checkMark;
            end else begin                      {Assign Line/Text color}
                ckMark[2] := checkMark;
                If frgdClr <> iClrMix then ckMark[3 + frgdClr] := checkMark;
            end;
            If curDoc.showPats then ckMark[13] := checkMark {Show Patterns}
            else ckMark[14] := checkMark;                   {Show Color by Initials}
            For i := 1 to 14 do
                MarkItem(geMenus[iMnuColor],i,ckMark[i]);


            (*$IfC traceOn *)
            (* Set DebugMenu *)
            for i := 1 to 10 do
                ckMark[i] := noMark;
            if pxlDist then
                ckMark[1] := checkMark;
            if not (traceCmd or traceEvt or traceFio or
                    traceFld or traceTrk or traceDMgr or
                    traceUop or traceHeap) then
                ckMark[2] := checkMark;
            if traceEvt then
                ckMark[3] := checkMark;
            if traceCmd then
                ckMark[4] := checkMark;
            if traceTrk then
                ckMark[5] := checkMark;
            if traceFld then
                ckMark[6] := checkMark;
            if traceFio then
                ckMark[7] := checkMark;
            if traceDMgr then
                ckMark[8] := checkMark;
            if traceUop then
                ckMark[9] := checkMark;
            if traceHeap then
                ckMark[10] := checkMark;
            for i := 1 to 10 do
                MarkItem(geMenus[iMnuDBug],i,ckMark[i]);
            (*$EndC*)

            (* set the port back to menuFolder *)
            SetPort(menuFolder);

            MenuSelect(thisEvent.where,menu,item);
            if item<>0 then
                GEMenuCmd(menu,item);
            HiLiteMenu(0);
        end;
    end; (* GEMenuEvent *)

(* ********************************************************************* *)
(*$S BasicOps*)
    procedure CmdKeyEvent(thisEvent: EventRec);
    var menu,item: Integer;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        if traceCmd then
            writeln('CmdKeyEvent(',thisEvent.ascii,')');

        (*$IfC traceOn *)
        if thisEvent.ascii='1' then begin
            if lastMenu=iMnuDBug then begin
                DeleteMenu(iMnuDBug);
                lastMenu := iMnuColor;
            end else begin
                InsertMenu(geMenus[iMnuDBug],0);
                lastMenu := iMnuDBug;
            end;
            DrawMenuBar;
        end else begin
        (*$EndC *)
            MenuKey(thisEvent.ascii,menu,item);
            HiLiteMenu(menu);
            if item<>0 then
                GEMenuCmd(menu,item);
            HiLiteMenu(0);
        (*$IfC traceOn *)
        end;
        (*$EndC *)
    end;  (* CmdKeyEvent *)

(* ********************************************************************* *)

(* folder activation *)

(* ********************************************************************* *)
(*$S IO*)
    procedure SBarUpdate(thisEvent: EventRec);
    var hRect,vRect: Rect;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        ClipRect(thisEvent.who^.portRect);
        HideSBorder(thisEvent.who^.portRect);
    end; (* SBarUpdate *)

(* ********************************************************************* *)

(* Scrap Folder Event Control *)

(* ********************************************************************* *)
(*$S IO*)
    procedure ScrapEvent(thisEvent: EventRec);
    Const
        docPLdsn    = 3;
        pSize   = $1000;   (* 4K *)
    Var
        hz: THz;
        ref:  Integer;
        errNum: Integer;
        tempPHzFlag: Boolean;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        if traceCmd then with thisEvent do
            writeln('ScrapEvent(',what:1,')');

        with thisEvent do
        CASE what OF
            buttonDown:
                if FGrowHit(where) then
                    GrowCtrl(who,where);
            folderActivate:
                begin
                    sActive := TRUE;
                    TakeControl(thisEvent,FALSE,FALSE);
                    (* install menus and DrawMenuBar *)
                    InsertMenu(geMenus[iMnuScrap],0);
                    DrawMenuBar;
                    (* draw scroll bars *)
                    DrwScrpSBars(scrapFolder^.portRect);
                    SetStdCursor(iCrsInactive);
                end;
            folderDeactivate:
                begin
                    sActive := FALSE;
                    SBarUpdate(thisEvent);
                    GiveControl(thisEvent);
                end;
            folderUpdate:
                begin
                    tempPHzFlag := FALSE;  {Initialize}
                    errNum := 0;
                    If curDoc.pHz=NIL then begin {If necessary create curDoc.pHz}
                        tempPHzFlag := TRUE;
                        NewGEHz('gePHz',pSize,pSize,docPLdsn,@GrowDocHz,hz,ref,errNum);
                        If errNum=0 then begin
                            curDoc.pHz := hz;
                            curDoc.pRef := ref;
                            curDoc.fldr := who;
                        end;
                    end;
                    If errNum=0 then begin
                        BeginUpdate(who);
                        DrwScrpFldr;
                        EndUpdate(who);
                        If tempPHzFlag then begin {If temporary pHz created then restore
                                                   to previous state.}
                            Close_DataSeg(errNum,ref);
                            curDoc.pHz := NIL;
                            curDoc.pRef := 0;
                            curDoc.fldr := NIL;
                        end;
                    end;
                    If traceHeap then
                         WriteLn('ScrapEvent folderUpdate errNum = ',errNum);
                end;
            filerEvent:
                GEFilerEvent(thisEvent)
        end; (* case *)
    end; (* ScrapEvent *)

(* ********************************************************************* *)

(* GE Folder Event Control *)

(* ********************************************************************* *)
(*$S Core2*)
    procedure GEFldrEvent(thisEvent: EventRec);
    const
        BSP =  8;
        CLR = 27;
    var i: Integer;
        (* scroll vars *)
        sBoxHit: THSB;
        iconHit: TIcon;
        (* update var *)
        curFldr: GrafPtr;
        fWhy:    FReason;
        fMsg:    FReply;
        fldSet:  Boolean;
        dName:   PathName;
        okFlag:  Boolean;
        clkFlag: Boolean;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        with thisEvent do begin
            if traceCmd then begin
                with curDoc.fldr^.portRect do
                writeln('GEFldrEvent(',what:1,'), origin:',left:4,',',top:4);
                WLnRect('    clipBBox:',curDoc.fldr^.clipRgn^^.rgnBBox);
            end;

            (* make sure port is set up properly *)
            SetWrkOrigin;
            GEClipRect(dwgLBox);
            (* reset curPenFlag *)
            if what<>buttonUp then
                curPenFlag := FALSE;
        end;

        if NOT SkipEvent(thisEvent) then with thisEvent do
            CASE what OF
                buttonDown:
                    begin
                        clkFlag := TRUE;
                        mseMoved := FALSE;
                        (* ordered in anticipated frequency of use *)
                        if GrowHit(where) then
                            GrowCtrl(who,where)
                        else if PtInRect(where,wrkBox) then begin
                            SnglClkCtrl(thisEvent);
                            clkFlag := FALSE;         {click flag used only at the
                                                       end of this case}
                        end else if PtInRect(where,protoBox) then
                            ProtoCtrl(where)
                        else if PtInRect(where,penBox) then
                            CurPenCtrl
                        else if FSBHit(sbList,where,sBoxHit,iconHit) then
                            SBoxCtrl(where,sBoxHit,iconHit)
                        else if RlrHit(where) then
                            RlrCtrl(where)
                        else if rangeOn then
                            Error('event.where is not defined.',0,NONFATAL);
                        If NOT (curDoc.proto=iObjText) then ShowClBx;
                        If clkFlag then begin
                            numClk := 1;
                            ckMultiClk := FALSE;
                        end;
                    end;
                buttonUp:
                    begin
                        (* set up multi-click checks *)
                        if mseMoved then begin
                            ckMultiClk := FALSE;
                            numClk     := 1;
                        end;
                        upTime := when;
                    end;
                keyDown:
                    begin
                        numClk := 1;               {restart click count}
                        ckMultiClk := FALSE;
                        if ORD(ascii)=CLR then begin
                            ClearCmd;
                            ShowClBx;
                        end else if appleKey and (ORD(ascii)<>BSP) then begin
                            CmdKeyEvent(thisEvent);
                            ShowClBx;
                        end else begin
                            If curDoc.atr.zoomOn<>0 then
                                GEAlert(iStopZText)
                            else begin
                                If not curDoc.fldSet then begin
                                    okFlag := TRUE;
                                    if curDoc.sLst^^.sState<>iObjText then
                                        oKFlag := ExpMiscMem(iCItmCh,1,40);
                                    if okFlag then
                                        TxtSelCmd(curDoc.align,FALSE);
                                    ShowClBx;
                                end;
                                If curDoc.fldSet then
                                    TxtKeyCtrl(ascii,shiftKey,appleKey);
                            end;
                        end;
                    end;
                folderActivate:
                    begin
                        TakeControl(thisEvent,FALSE,FALSE);
                        DoActivateStuff(thisEvent);

                        if traceFio then
                            DumpDocLst('    End Activate...');

                    end;
                folderDeactivate:
                    begin
                        if theDBox.isOpen then
                            GEEndDBox;

                        if traceFio then
                            DumpDocLst('    Start deactivate');

                        ResetPoly;
                        ResetAutoDrw;
                        CommitLastOp;
                        UpdateDocLst;
                        HideDist;
                        fActive := FALSE;
                        SBarUpdate(thisEvent);
                        GEClipRect(dwgLBox);
                        SetGEBox(FALSE);
                        (* unhilite the current selection *)
                        HiliteSel(FALSE);
                        ShowProto(curDoc.proto,FALSE);
                        (* draw a picture of the folder *)
                        WmOpenPicture(curDoc.fldr);
                        DrwGEFldr;
                        ShowClBx;
                        WmClosePicture;
                        (* remember the fld edit state, if any *)
                        GetTxtState(curDoc.fState);
                        (* update the scrap *)
                        TxtTerminate;

                        if traceFio then
                            DumpDocLst('    End deactivate');

                        GiveControl(thisEvent);
                    end;
                folderUpdate:
                    begin
                        ShowRlrHrs(wrkPt,Off);
                        (* remember curFldr *)
                        curFldr := curDoc.fldr;
                        (* bind doc *)
                        BindGEDoc(who);
                        (* turn on the caret so don't get it confused *)
                        TxtCaretOn;
                        (* update *)
                        BeginUpdate(who);
                        DrwGEFldr;
                        ShowClBx;        {repaint color box}
                        EndUpdate(who);
                        (* blink the caret again *)
                        TxtBlinkCaret;
                        (* restore curFldr *)
                        BindGEDoc(curFldr);
                    end;
            end; (* case *)
    end; (* GEFldrEvent *)

(* ********************************************************************* *)

(* Event Dispatching *)

(* ********************************************************************* *)
(*$S DeBug*)

    procedure WrtEvent(thisEvent: EventRec);
    var sWhat,sWho: String80;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        (*$IfC traceOn *)
        sWhat := 'Unknown';
        sWho  := 'Unknown folder';
        with thisEvent do begin
            case what of
                nilEvent:         sWhat := 'Nil';
                buttonDown:       sWhat := 'ButtonDown';
                buttonUp:         sWhat := 'ButtonUp';
                keyDown:          sWhat := 'KeyDown';
                folderActivate:   sWhat := 'Activate';
                folderDeActivate: sWhat := 'DeActivate';
                folderUpdate:     sWhat := 'Update';
                folderMoved:      sWhat := 'Moved';
                filerEvent:       sWhat := 'Filer';
                abortEvent:       sWhat := 'Abort';
                diedEvent:        sWhat := 'Died';
            end; (* case *)
            if who=curDoc.fldr then
                sWho := 'curDoc.fldr'
            else if who=scrapFolder then
                sWho := 'scrapFldr'
            else if who=filerFolder then
                sWho := 'filerFldr'
            else if who=menuFolder then
                sWho := 'menuFldr'
            else if who=dialogFolder then
                sWho := 'dialogFldr';

            with where do
                writeln(sWho,' ',sWhat,' Event ',ORD(who):10,';',
                        what:1,';',h:2,',',v:2);
        end;
        (*$EndC *);
    end; (* WrtEvent *)

(* ********************************************************************* *)
(*$S Core2*)

    function  InDocLst(who: GrafPtr): Boolean;
    var i,cnt: Integer;
        inLst: Boolean;
        lFldr: GrafPtr;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        if traceCmd then
            Writeln('    InDocLst(',ORD(who):8,')');
        inLst := FALSE;
        cnt := docLst^^.cnt;
        for i := 1 to cnt do begin
            (*$R-*)
            lFldr := docLst^^.lst[i]^^.fldr;
            (*$IfC rangeOn*) (*$R+*) (*$EndC*)
            if traceCmd then
                writeln('        ',i:2,'=',ORD(lFldr):8);
            if who=lFldr then begin
                inLst := TRUE;
                i := cnt;
            end;
        end;
        InDocLst := inLst;
    end; (* InDocLst *)

(* ********************************************************************* *)
(*$S Core2*)
    procedure ProcessTheEvent(thisEvent: EventRec);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        (* check the heap integrity *)
        if traceEvt or traceHeap then
            WrtEvent(thisEvent);
        if traceHeap then
            CkGEHz('ProcessTheEvent');

        with thisEvent do begin
            SetPort(who);
            if what=keyDown then begin
                if (who=curDoc.fldr) or (who=dialogFolder) then begin
                    (* set proper port and call keyProc *)
                    SetPort(keyPort);
                    CallPrcsProc(thisEvent,keyProc);
                end;
            end else if what=filerEvent then
                GEFilerEvent(thisEvent)
            else if InDocLst(who) then
                GEFldrEvent(thisEvent)
            else if who=dialogFolder then begin
                if traceEvt then with dialogFolder^.portRect do
                    writeln('DialogEvent, portOrigin=',left:4,top:4);

                if theDBox.isOpen then
                    if what=folderUpdate then begin
                        BeginUpdate(who);
                        if theDBox.isOpen then
                            CallRstProc(theDBox.RestoreProc);
                        EndUpdate(who);
                    end else if (what>=folderActivate) AND (what<=folderMoved) then
                        GEFldrEvent(thisEvent)
                    else
                        CallPrcsProc(thisEvent,theDBox.processProc);
            end else if who=menuFolder then begin
                if fActive then
                    GEMenuEvent(thisEvent)
                else if sActive then
                    ScrapMenuEvent(thisEvent);
            end else if who=scrapFolder then
                ScrapEvent(thisEvent)
            else if rangeOn then
                if who<>alertFolder then begin
                    WrtEvent(thisEvent);
                    Error('Unknown owner of event!!!',0,NONFATAL);
                end;
        end;
    end; (* ProcessTheEvent *)

(* ********************************************************************* *)
(*$S Core2*)
    procedure MainProg;
    var theEvent:   EventRecord;
        i: Integer;

        {XXX timing}
        tStart: LongInt;
        tLast:  LongInt;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        {XXX timing}
        tStart := Time;
        tLast  := Time;
        if traceInit then WrtTime('MainProg',tLast);

        fldrProc := @GEFldrEvent;
        keyProc  := fldrProc;
        InitCtr;

        (*$IfC traceOn*)
        if traceInit then WrtTime('MainLoop Start',tLast);
        traceInit := FALSE;
        (*$EndC*)

        REPEAT
            WHILE NOT (ImActive or quitFlag) DO begin
                GetEvent(theEvent);
                ProcessTheEvent(theEvent);
            end;
            WHILE ImActive and not quitFlag DO begin
                if fActive and (docsOpen>0) then
                    SetFldrOrigin;
                if EventAvail then begin
                    GetEvent(theEvent);
                    ProcessTheEvent(theEvent);
                end ELSE begin
                    CkTimeOut;
                    LetOthersRun;
                end;
                (* UpdateCursor after processEvent to make TrkPoly work *)
                (* properly after update event *)
                UpdateCursor;
            end;
        until quitFlag;
        (* turn off the journal *)
        {StopRecording;
        StopPlayback;}
    end; (* MainProg *)


ÿ