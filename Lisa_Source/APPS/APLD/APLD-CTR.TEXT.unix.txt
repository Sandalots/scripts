(* ********************************************************************* *)

(* CopyRight 1983,1984 Apple Computer Inc. *)
(* GE Event Handlers *)

(* ********************************************************************* *)

Unit Ctr;

(* Segment CTR *)
INTERFACE
USES
    (*$U UNITSTD*)     UnitStd,     (* implementation unit   *)
    (*$U UNITHZ*)      UnitHz,
    (*$U STORAGE*)     Storage,
    (*$U QUICKDRAW*)   QuickDraw,
    (*$U FONTMGR*)     FontMgr,
    (*$U libos/syscall.obj*)     SysCall,
    {$U LibOS/psyscall }         PSysCall,
    (*$U WM.EVENTS*)   Events,
    (*$U WM.MENUS*)    Menus,
    (*$U WM.FOLDERS*)  Folders,
    (*$U WMLSTD*)      WMLStd,
    (*$U WMLSB*)       WMLSB,
    (*$U WMLCRS*)      WMLCrs,
    (*$U WMLGROW*)     WMLGrow,
    (*$U SCRAP*)       Scrap,
    (*$U FILERCOMM*)   FilerComm,
    (*$U ALERTMGR*)    AlertMgr,

    {$U LibPm/PmDecL}      PmDecL,
    {$U PrStdInfo}     PrStdInfo,
    {$U PrPublic}      PrPublic,
    (*$U ApLd/HDR*)      Hdr,
    {$IFC sampcalls}
    {$U apld/tracecalls.obj}  tracecalls,
    {$ENDC}
    (*$U ApLd/DMGR*)     DMgr,
    (*$U ApLd/DBX*)      Dbx,
    (*$U ApLd/DAT*)      Dat,
    (*$U ApLd/SEL*)      Sel,
    (*$U ApLd/UOP*)      UOp,
    (*$U ApLd/DRW*)      Drw,
    (*$U ApLd/FIO*)      Fio,
    (*$U ApLd/MNU*)      Mnu,
    (*$U ApLd/SCP*)      Scp,
    (*$U ApLd/SCR*)      Scr,
    (*$U ApLd/TRK*)      Trk,
    (*$U ApLd/CMD*)      Cmd,
    (*$U ApLd/PRT*)      Prt,
    (*$U ApLd/INT*)      Int,
    (*$U ApLd/TXT*)      Txt,
    (*$U ApLd/UTL*)      Utl;


    procedure MainProg;
    procedure CrashSuspend;

(* ********************************************************************* *)

IMPLEMENTATION


(* ********************************************************************* *)
Const
    iStopZText = 352;  {Others in hdr and dbx}
var
    lastProto:  Integer;
    lastMenu:   Integer;

    (* filer vars *)
    docsOpen:   Integer;

    (* multi-click vars *)
    upTime:     LongInt;
    ckMultiClk: Boolean;
    numClk:     Integer;

    (* temp vars until GetKeys is implemented *)
    lCmdKeyDn,rCmdKeyDn: Boolean;

    (* text fld update rects and flag *)
    b1,b2:      Rect;
    b3,bFlag:   Boolean;

    (* menu item font mapping *)
    { mapMFace:   Packed Array [1.. 7] of TByte;}
    {mapMFont:   Packed Array [1..11] of TByte;}
    mapFaceM:   Packed Array[StyleItem] of TByte;
    {mapFontM:   Packed Array[1..32]  of TByte;}


(* ********************************************************************* *)

(* miscellaneous *)

(* ********************************************************************* *)

(*$S Init*)
    procedure SetTimeState;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        (* set all time vars to known state to synchronize journal *)
        upTime     := 0;
        txtTime    := 0;
        mseTime    := 0;
        lstTxtTime := 0;
        numClk     := 0;
        ckMultiClk := FALSE;
    end; (* SetTimeState *)

(* ********************************************************************* *)

(*$S Init*)
    procedure InitFmtArrays;
    Var i: Integer;
    begin
        {LisaDraw offers 16 fonts yet there are really only 14. Small Moderns
         double for Small Classics}
        For i := iFontStart to iFontEnd do begin
            fontStyle[i] := 0;
            fontSize[i]  := 0;
        end;
        For i := iFntSzStart to iFntSzEnd do begin
            fontIndex[iFontModern,i] := 0;
            fontIndex[iFontClassic,i] := 0;
        end;
        For i := 4 to 9 do
            fontStyle[i] := iFontModern;
        fontStyle[15] := iFontModern;
        fontStyle[19] := iFontModern;
        For i := 10 to 14 do
            fontStyle[i] := iFontClassic;
        fontStyle[16] := iFontClassic;

        fontSize[4] := 7;
        fontSize[5] := 9;
        fontSize[6] := 10;
        fontSize[7] := 4;
        fontSize[8] := 5;
        fontSize[9] := 6;
        fontSize[10] := 7;
        fontSize[11] := 9;
        fontSize[12] := 10;
        fontSize[13] := 5;
        fontSize[14] := 6;
        fontSize[15] := 8;
        fontSize[16] := 8;
        fontSize[19] := 3;


        fontIndex[iFontModern,3] := 19;
        fontIndex[iFontModern,4] :=  7;
        fontIndex[iFontModern,5] :=  8;
        fontIndex[iFontModern,6] :=  9;
        fontIndex[iFontModern,7] :=  4;
        fontIndex[iFontModern,8] := 15;
        fontIndex[iFontModern,9] :=  5;
        fontIndex[iFontModern,10]:=  6;

        fontIndex[iFontClassic,3] :=  19;  {Same as Modern}
        fontIndex[iFontClassic,4] :=   7;  {Same as Modern}
        fontIndex[iFontClassic,5] :=  13;
        fontIndex[iFontClassic,6] :=  14;
        fontIndex[iFontClassic,7] :=  10;
        fontIndex[iFontClassic,8] :=  16;
        fontIndex[iFontClassic,9] :=  11;
        fontIndex[iFontClassic,10] := 12;

    end;{InitFmtArrays}

(* ********************************************************************* *)

(*$S Init*)
    procedure InitCtr;
    var i: Integer;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        lastMenu := iMnuColor;
        (* control state vars *)
        curPenFlag := FALSE;
        (* control flags *)
        lCmdKeyDn := FALSE;
        rCmdKeyDn := FALSE;
        sgnVrtFlag  := FALSE;
        noDblVrtFlag  := FALSE;
        (* text fld update *)
        bFlag := FALSE;
        b3 := FALSE;
        (* set up font indexing scheme corresponding to order in menu *)
        {mapMFace[1] := 5;               (* setNormal    *)
        mapMFace[2] := 2;               (* setUnderline *)
        mapMFace[3] := 1;               (* setBold      *)
        mapMFace[4] := 3;               (* setItalic    *)
        mapMFace[5] := 7;               (* setOutline   *)
        mapMFace[6] := 4;               (* setShadow    *)
        mapMFace[7] := 6;               (* setFont      *)   }

        {mapMFont[ 1] := p15Tile;
        mapMFont[ 2] := p12Tile;
        mapMFont[ 3] := p12cent;
        mapMFont[ 4] := p10Tile;
        mapMFont[ 5] := p10cent;
        mapMFont[ 6] := tile12;
        mapMFont[ 7] := cent12;
        mapMFont[ 8] := tile18;
        mapMFont[ 9] := cent18;
        mapMFont[10] := tile24;
        mapMFont[11] := cent24;}

        mapFaceM[underline]  := 2;
        mapFaceM[bold]       := 3;
        mapFaceM[italic]     := 4;
        mapFaceM[outline]    := 5;
        mapFaceM[shadow]     := 6;

        {for i := 1 to 32 do
            mapFontM[i] := 0;
        mapFontM[p15tile]  :=  1;
        mapFontM[p12tile]  :=  2;
        mapFontM[p12cent]    :=  3;
        mapFontM[p10tile]  :=  4;
        mapFontM[p10cent]  :=  5;
        mapFontM[tile12]   :=  6;
        mapFontM[cent12] :=  7;
        mapFontM[tile18]   :=  8;
        mapFontM[cent18]   :=  9;
        mapFontM[tile24]   := 10;
        mapFontM[cent24]   := 11;}

        curStyle := iFontModern;
        InitFmtArrays;

        (* disable blank menu items; menu mgr will eventually do this *)
        DisableItem(geMenus[iMnuFile], 3);
        DisableItem(geMenus[iMnuFile], 7);
        DisableItem(geMenus[iMnuEdit], 2);
        DisableItem(geMenus[iMnuEdit], 6);
        DisableItem(geMenus[iMnuEdit],10);
        DisableItem(geMenus[iMnuEdit],14);
        DisableItem(geMenus[iMnuEdit],20);
        DisableItem(geMenus[iMnuType], 7);
        DisableItem(geMenus[iMnuLayout], 4);
        DisableItem(geMenus[iMnuLayout], 7);
        DisableItem(geMenus[iMnuLayout],11);
        DisableItem(geMenus[iMnuLayout],15);
        DisableItem(geMenus[iMnuLayout],18);
        DisableItem(geMenus[iMnuArrange], 5);
        DisableItem(geMenus[iMnuArrange], 10);
        DisableItem(geMenus[iMnuArrange], 18);
        DisableItem(geMenus[iMnuLines],5);

        docsOpen := 0;
        quitFlag := FALSE;
        fActive  := FALSE;
        sActive  := FALSE;
        SetTimeState;

        (* init docLst *)
        docLst := Pointer(Ord(HAllocate(geHHz,SizeOf(TDocLst))));
        docLst^^.cnt := 0;
        docLst^^.cur := 0;

    end; (* InitCtr *)

(* ********************************************************************* *)

(*$S Core2*)
    function  RlrHit(thisPt: Point): Boolean;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        RlrHit := FALSE;
        if curDoc.atr.rlrOn then
            RlrHit := (PtInRect(thisPt,topRlrBox) or
                       PtInRect(thisPt,lftRlrBox) or
                       PtInRect(thisPt,zroRlrBox));
    end; (* RlrHit *)

(* ********************************************************************* *)

(*$S Core2*)
    function  GrowHit(where: Point): Boolean;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        SetFldrOrigin;
        GrowHit := FGrowHit(where);
        SetWrkOrigin;
    end; (* GrowHit *)

(* ********************************************************************* *)

(* cursor and timeout tracking *)

(* ********************************************************************* *)

(*$S Core2*)
    procedure UpdateCursor;
    VAR msePt: Point;
        (* scroll vars *)
        sBoxHit:    THSB;
        iconHit:    TIcon;

    (* ***************************************************************** *)

        function  CmdKeyDown: Boolean;
        CONST
            lApple = 127;
            rApple = 104;
        VAR
            keys: KeySet;
        begin
        {$IFC sampcalls} ALogCall; {$ENDC}
            GetKeys(keys);
            CmdKeyDown := (lApple in keys) or (rApple in keys);
        end; (* CmdKeyDown *)

    (* ***************************************************************** *)

    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        if fActive and (docsOpen>0) then begin
            (* see if in dialog folder *)
            SetPort(dialogFolder);
            SetOrigin(0,0);
            GetMouse(msePt);
            if PtInRgn(msePt,dialogFolder^.visRgn) then
                CallTrkProc(msePt,theDBox.TrkMseProc)
            else begin
                (* get wrkPt from mainCoords *)
                SetWrkOrigin;
                GetMouse(msePt);
                GEUnMapPt(msePt);
                (* if mse is in working area of folder, then track *)
                if PtInRect(msePt,wrkLBox) then begin
                    (* track the mouse with dist,rlrHrs *)
                    GetWrkMse;
                    (* track the mouse with poly *)
                    TrkPoly;
                    (* make sure cursor current *)
                    SetStdCursor(geCrsCur);
                end else begin
                    SetStdCursor(iCrsInactive);
                    ShowRlrHrs(msePt,OFF);
                    HideDist;
                end;
                (* check for cmd key transition *)
                if autoDrwFlag then
                    if not CmdKeyDown then
                        if not EventAvail then
                            (* select objects drawn while cmd key down *)
                            if (curDoc.proto<>iObjPoly) or polyStart then begin
                                SetWrkOrigin;
                                ResetAutoDrw;
                            end;
                (* set mainCoords *)
                SetOrigin(0,0);
            end;
        end else
            SetStdCursor(iCrsInactive);
    end; (* UpdateCursor *)

(* ********************************************************************* *)

(*$S Core2*)
    Procedure UpdtClBx(frgdClr, bkgdClr: TPByte); {called from ProtoCtrl and ShowClBx
                                                   below}

    Var clrBox: Rect;
        oldPenSt: PenState;

    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        {Calculate position and if there is room.}
        SetFldrOrigin;
        ClipRect(curDoc.fldr^.portRect);
        {Erase old values}
        SetRect(clrBox,penBox.left,(iProtoLast+1)*protoHeight,penBox.right,
                penBox.top-1);
        GetPenState(oldPenSt);
        PenMode(patCopy);
        FillRect(clrBox,White);
        SetPenState(oldPenSt);
        If penBox.top - (iProtoLast+1)*protoHeight > dvSBox then begin

            {Draw a line across top of the color box}
            PenNormal;
            MoveTo(penBox.left,penBox.top-dvSBox-2);
            Line(protoWidth-1,0);

            {With text drawing routines E-84 from QuickDraw draw in characters.}
            TextFont(p12Tile);                  {Modern 12 point}
            TextFace([]);
            TextMode(srcXor);
            TextSize(0);                  {default setting}
            MoveTo(penBox.left+1,penBox.top-5);
            DrawString(initLet[frgdClr]);
            MoveTo(penBox.left+protoWidth div 2 +1,penBox.top-5);
            DrawString(initLet[bkgdClr]);
        end;
        SetWrkOrigin;
    end; {UpdtClBx}

(* ********************************************************************* *)

(*$S Core2*)
    procedure ShowClBx;  {Cover for UpDtClBx for call from GEFldrEvent button down}
    Var  frgdClr, bkgdClr: TPByte;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        {Get the color of the selection.}
         GetSlClr(curDoc.sLst, frgdClr, bkgdClr);
         UpdtClBx(frgdClr, bkgdClr);
    end; {ShowClBx}

(* ********************************************************************* *)

(*$S Core2*)
    procedure CkTimeOut;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        if fActive and (docsOpen>0) then begin
            (* check for multi click time out *)
            if NOT StillDown and (Time>upTime+mseTime) then begin
                {
                If traceTrk then with wrkPt do
                    writeln('CkTimeOut, time',Time:8,', upTime',upTime:8,
                            ', mseTime',mseTime:4,', numClk',numClk:2);
                }
                if ckMultiClk then begin
                    ckMultiClk := FALSE;
                    numClk := 1;
                end;
            end;
            (* check for text time out *)
            if Time>lstTxtTime+txtTime then begin
                lstTxtTime:=Time;
                if curDoc.fldSet then begin
                    SetWrkOrigin;
                    GEClipRect(dwgLBox);
                    if TxtTimeOut then
                        if curDoc.atr.pgBOn then
                            DrwPgBreak(curDoc.txtObj^.bBox);
                    {
                    if bFlag then begin
                        bFlag := FALSE;
                        TxtHiLite(OFF);
                        ReShow(b1);
                        if b3 then
                            ReShow(b2);
                        TxtHiLite(ON);
                    end;
                    }
                    (* bFlag means that we have made a fld smaller by backspacing
                    or typing over a selection and need to redraw the background
                    of the text *)
                    if bFlag then begin
                        (* turn on the caret so don't get it confused *)
                        TxtCaretOn;
                        bFlag := FALSE;
                        ReShow(b1);
                        if b3 then
                            ReShow(b2);
                        (* blink the caret again *)
                        TxtBlinkCaret;
                    end;
                end;
            end;
        end;
    end; (* CkTimeOut *)

(* ********************************************************************* *)

(*$S Core2*)
    FUNCTION SkipEvent(event: EventRecord): BOOLEAN;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        SkipEvent:=FALSE;
        if Abort then
            SkipEvent := event.what in [keyDown,buttonDown,buttonUp];
    end; (* SkipEvent *)

(* ********************************************************************* *)

(*$S BasicOps*)
    procedure TxtKeyCtrl(ch: char; shFlag,aplFlag: Boolean);
    const
        ENT = 3;
        BSP = 8;
        RET = 13;
    var thisObj: TObjPtr;
        thisPt:  Point;
        this2D:  Point2D;
        lSpc2D:  Point2D;
        oldBBox: Rect;
        oldTLen: Integer;
        saveRt:  Integer;
        tState:  Integer;
        lSpace,ascent,descent: Integer;

    (* ***************************************************************** *)

        procedure UpdateBkgnd(oldR,newR: Rect; doAll: Boolean; tAlign: Integer);
        begin
        {$IFC sampcalls} ALogCall; {$ENDC}
            if doAll then
                UnionRect(oldR,newR,oldR)
            else if tAlign=cAlnLft then         (* left justify   *)
                oldR.left := newR.right
            else if tAlign=cAlnRgt then         (* right justify  *)
                oldR.right := newR.left
            else begin                          (* center justify *)
                saveRt   := oldR.right;
                oldR.right := newR.left;
                newR.left  := newR.right;
                newR.right := saveRt;
            end;
            newR.top := oldR.top;
            newR.bottom := oldR.bottom;

            b3 := tAlign=cAlnCtr;
            if bFlag then begin
                UnionRect(b1,oldR,b1);
                UnionRect(b2,newR,b2);
            end else begin
                bFlag := TRUE;
                b1 := oldR;
                b2 := newR;
            end;
        end; (* UpdateBkgnd *)

    (* ***************************************************************** *)

    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        if traceCmd then
            writeln('TxtKeyCtrl(',Ord(ch):2,',',shFlag,',',aplFlag,',',curDoc.fldSet,')');

        (* hide cursor *)
        if Ord(ch)<>ENT then
            ObscureCursor;

        (* make sure that field is visible *)
        CkCaretLoc;

        (* assume that curFld is set *)

        (* remember tLen *)
        oldtLen := curDoc.txtObj^.oDat.inf.tLen;
        if (Ord(ch) >= 32) then begin
            tState := TxtSelState;
            if (oldtLen<maxTLen) or (tState=iTStInvert) then begin
                if (tState=iTStInvert) OR ExpMiscMem(iCItmCh,1,0) then begin
                    CommitLastOp;
                    (* remember bBox *)
                    oldBBox := curDoc.txtObj^.bBox;
                    if curDoc.txtObj^.cSel<>iSelEdt then begin
                        HiliteSel(OFF);
                        MarkCSel(curDoc.sLst,iSelEdt);
                        TxtSelAll(curDoc.txtObj);
                        HiliteSel(ON);
                    end;
                    TxtCh(ch,FALSE,FALSE);
                    with curDoc.txtObj^,oDat,inf do
                    if (flPat=iFillClear) OR (tState=iTStInvert) then
                        UpdateBkgnd(oldBBox,bBox,TRUE,tDat);
                    curDoc.sLst^^.sBBox := curDoc.txtObj^.bBox;
                    SetLastOp(iOpTxt);
                    CkTxtBounds(iOpTCh);
                end;
            end else
                GEAlert(iStopTSize);
        end else if Ord(ch)=BSP then begin
            if (oldtLen>0) or shFlag then begin
                CommitLastOp;
                (* remember bBox *)
                oldBBox := curDoc.txtObj^.bBox;
                tState := TxtSelState;
                if curDoc.txtObj^.cSel<>iSelEdt then begin
                    HiliteSel(OFF);
                    MarkCSel(curDoc.sLst,iSelEdt);
                    TxtSelAll(curDoc.txtObj);
                    HiliteSel(ON);
                end;
                TxtCh(ch,shFlag,aplFlag);
                if not shFlag then with curDoc.txtObj^,oDat,inf do
                    UpdateBkgnd(oldBBox,bBox,tLen=0,tDat);
                curDoc.sLst^^.sBBox := curDoc.txtObj^.bBox;
                SetLastOp(iOpTxt);
            end;
        end else if Ord(ch)=ENT then begin
            if curDoc.txtObj^.cSel=iSelEdt then
                ResetTxt
            else
                CloseCFld;
            SetCurProto(iObjNil,TRUE);
            SetStdCursor(geCrsCur);
        end else if Ord(ch)=RET then begin
            if ExpMiscMem(iCItmCh,1,40) then begin
                (* get new insertion point, line space below current *)
                GetTxtLSpace(lSpace,ascent,descent);
                SetPt(thisPt,0,lSpace);
                ScrnToWld(thisPt,lSpc2D);
                (* text object may have been moved if we are typing in arrow mode *)
                thisObj := CkObjTrfo(curDoc.txtObj);
                this2D := thisObj^.oDat.tLoc;
                AddPt2D(lSpc2D,this2D);
                (* make sure gridded pt is not less than line space OR grid space*)
                this2D.y := this2D.y-ascent+(lSpace+curDoc.atr.grdSz2D.y)/2.0;
                GridP2D(this2D);
                if this2D.y-thisObj^.oDat.tLoc.y<lSpc2D.y then
                    this2D.y := this2D.y+curDoc.atr.grdSz2D.y;
                (* start new field if insert pt not off paper *)
                if this2D.y<=sizeLBox.bottom then begin
                    (* remember setting of current field *)
                    GetTxtInfo;
                    (* deselect the current field *)
                    ResetCurSel;
                    (* start a new field *)
                    TxtClkCmd(this2D,FALSE,TRUE);
                end;
            end;
        end;
    end; (* TxtKeyCtrl *)

(* ********************************************************************* *)

(* Button Down Events *)

(* ********************************************************************* *)

(*$S BasicOps*)
    procedure TextCtrl(shFlagOn: Boolean; nClk: Integer);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        if traceCmd then
            writeln('TxtCtrl(',shFlagOn,',',nClk:1,')');

        if shFlagOn or (nClk=1) then begin
            TxtClkCmd(wrk2D,shFlagOn,FALSE);
            TxtSelTrk(wrkPt,shFlagOn);
        end else if nClk=2 then begin
            TxtSelWrd(curDoc.txtObj,wrkPt);
            TxtSelTrk(wrkPt,shFlagOn);
        end else if nClk=3 then begin
            TxtSelAll(curDoc.txtObj);
            TxtHiLite(ON);
        end;
    end; (* TextCtrl *)

(* ********************************************************************* *)

(*$S Core2*)
    procedure EditCtrl(shFlagOn: Boolean; nClk: Integer);
    var whichObj:  TObjPtr;
        whichKnob: Integer;
        eFlag:     Boolean;
        reshapFl:  Boolean;
        onKnob:    Integer;
        knobPt:    Point;
        noVrtSelected: Boolean;
        edtState:  Integer;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        (* ResetTxt so don't hit test against null text objects *)
        ResetTxt;

            reshapFl := FALSE;
            eFlag := TRUE;
            edtState := curDoc.edtState;
            If (edtState=iObjPoly) OR (edtState=iObjSPoly) then begin
                reshapFl := TRUE;
                If shFlagOn then begin
                    MakeSelCmd(TRUE,FALSE,noVrtSelected);{Make vertex selection}
                    eFlag := noVrtSelected;
                end else begin
                    onKnob := PtOnObjKnob(wrkPt,CkObjTrfo(curDoc.edtObj),whichKnob);
                    eFlag := onKnob<>iZoneHot;
                    If onKnob=iZoneHot then
                        EditCLstCmd(whichKnob);
                end;
            end;
            If (edtState=iObjArc) OR (edtState=iObjFHnd) then begin

                reshapFl := TRUE;
                onKnob := PtOnObjKnob(wrkPt,CkObjTrfo(curDoc.edtObj),whichKnob);
                eFlag := onKnob<>iZoneHot;
                if onKnob=iZoneHot then begin
                    if curDoc.edtState=iObjArc then
                        EditArcCmd(whichKnob)
                    else
                        EditCLstCmd(whichKnob)
                end;
            end;
            if eFlag then begin
                if shFlagOn then
                    MakeSelCmd(TRUE,TRUE,noVrtSelected)
                    {MakeSelCmd(* shFlag,objFlag: Boolean;
                     Var noVrtSelected: Boolean*)}
                else begin
                    onKnob := PtOnSelKnob(wrkPt,whichObj,whichKnob);
                    if (onKnob=iZoneHot) and (NOT reshapFl) then
                        StrSelCmd(whichObj,whichKnob)
                    else if PtOnObj(wrkPt,whichObj) then
                        MoveSelCmd(whichObj)
                    else if onKnob=iZoneCld then begin
                        If (edtState=iObjPoly) OR (edtState=iObjSPoly) then begin
                            MakeSelCmd(FALSE,FALSE,noVrtSelected);
                            eFlag := noVrtSelected;
                        end;
                        If eFlag then
                            MakeSelCmd(FALSE,TRUE,noVrtSelected);
                    end;
                end;
            end;

    end; (* EditCtrl *)


(* ********************************************************************* *)

(*$S Core2*)
    procedure SnglClkCtrl(thisEvent: EventRec);
    var whichObj: TObjPtr;
        sPt: Point;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        with thisEvent do begin
            sgnVrtFlag := codeKey;
            noDblVrtFlag := NOT codeKey;
            if PtInRect(where,dwgBox) then begin
                (* reset pin state (last pt clicked in space) *)
                pasteState := FALSE;
                (* get wrkCoords of event *)
                wrkPt := where;
                SubPt(wrkBox.topLeft,wrkPt);
                sPt := curDoc.atr.scrollPt;
                GEMapPt(sPt);
                AddPt(sPt,wrkPt);
                SetWrkMse(wrkPt);
                TrkMse(wrkPt);

                If traceCmd then with wrkPt do
                    writeln('SnglClkCtrl(',h:2,',',v:2,')');

                (* set up anchor *)
                anchor2D := grid2D;
                anchorPt := gridPt;
                (* check for multi-click *)

                {
                If traceTrk then with wrkPt do
                    writeln('    shiftKey',shiftKey:6,
                            ', ckMulti',ckMultiClk:6,', when-upTime',(when-upTime):8,
                            ', clkDelay',mseTime:4,', numClk',numClk:2);
                }
                if not shiftKey then begin
                    mseTime := clickDelay;
                    if ckMultiClk then begin
                        if when-upTime<clickDelay then
                            if numClk<3 then
                                numClk := numClk+1
                            else
                                numClk := 1;
                    end else
                        ckMultiClk := TRUE;
                end;

                if curDoc.proto=iObjNil then
                    if appleKey and not autoDrwFlag then begin
                        ResetEdit;
                        ResetCurSel;
                        SetCurProto(lastProto,TRUE);
                        SetStdCursor(geCrsCur);
                    end;
                (* branch to appropriate routine *)
                if curDoc.proto=iObjNil then
                    EditCtrl(shiftKey,numClk)
                else if curDoc.proto=iObjText then
                    TextCtrl(shiftKey,numClk)
                else begin
                    MakeObjCmd;
                    if mkObjFlag then begin
                        autoDrwFlag := TRUE;
                        if not appleKey then
                            {if not EventAvail then}
                                ResetAutoDrw;
                    end;
                end;
            end;
        end;
        sgnVrtFlag  := FALSE;
        noDblVrtFlag  := FALSE;
    end; (* SnglClkCtrl *)

(* ********************************************************************* *)

(*$S Ring*)
    procedure ProtoCtrl(thisPt: Point);
    var index,lastIndex: integer;
        thisTime: LongInt;
        thisObj:  TObjPtr;

    (* ***************************************************************** *)

        function GetIndex: Integer;
        var tempIndex: Integer;
        begin
        {$IFC sampcalls} ALogCall; {$ENDC}
            if thisPt.v<0 then
                thisPt.v := 0
            else if thisPt.v > penBox.top-7 then
                thisPt.v := penBox.top-7;
            tempIndex := (thisPt.v-protoBox.top+1) div protoHeight;
            if tempIndex > iProtoLast then
                tempIndex := iProtoLast;
            GetIndex := tempIndex;
        end;

    (* ***************************************************************** *)

    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        If traceCmd then
            writeln('ProtoCtrl');

        (* set up *)
        SetStdCursor(iCrsHidden);
        ShowProto(curDoc.proto,FALSE);
        thisTime := Time+5;
        index := GetIndex;
        lastIndex := index;
        ShowCurPen(index);
        UpDtClBx(curDoc.pnClr,curDoc.fillClr[index]);
        BlinkProto(index);
        (* track blinking proto selection *)
        while StillDown do begin
            SetOrigin(0,0);
            GetMouse(thisPt);
            (* get new prototype index *)
            index := GetIndex;
            if index<>lastIndex then begin
                lastIndex := index;
                SetWrkOrigin;
                ShowCurPen(index);
                UpDtClBx(curDoc.pnClr,curDoc.fillClr[index]);
                SetOrigin(0,0);
            end;
            if Time>thisTime then begin
                BlinkProto(index);
                thisTime := Time+5;
            end;
        end;
        If (index=iObjText) and (curDoc.atr.zoomOn<>0) then begin
            index := iObjNil;
            GEAlert(iStopZText);
        end;
        BlinkProto(index);
        SetWrkOrigin;

        (* Reset Ops in Progress *);
        ResetTxt;
        ResetPoly;
        (* ResetAutoDrw *)
        if autoDrwFlag then begin
            MarkCSel(curDoc.sLst,iSelNml);
            HiliteSel(ON);
            autoDrwFlag := FALSE;
        end;

        ResetEdit;

        (* Reset Selection *);
        if index<>iObjNil then begin
            if curDoc.sLst^^.sState<>iSStPrt then
                ResetCurSel;
            curDoc.sLst^^.sState := iSStPrt;
            dupCtrFlag := OFF;
        end else if curDoc.sLst^^.sState<>iSStPrt then begin
            HiliteSel(OFF);
            MarkCSel(curDoc.sLst,iSelNml);
            HiliteSel(ON);
        end else
            curDoc.sLst^^.sState := iObjNil;


        SetCurProto(index,FALSE(*don't hilite*));
        ShowProto(index,TRUE);
        if index<>iObjNil then
            lastProto := index;
        SetStdCursor(geCrsCur);
    end; (* ProtoCtrl *)

(* ********************************************************************* *)

(*$S BasicOps*)
    procedure CurPenCtrl;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        curPenFlag := TRUE;
        if curDoc.sLst^^.sState<>iSStPrt then begin
            ResetEdit;
            ResetCurSel;
            curDoc.sLst^^.sState := iSStPrt;
        end;
        ResetTxt;
        SetCurProto(lastProto,TRUE);
    end; (* CurPenCtrl *)

(* ********************************************************************* *)

(*$S Core2*)
    function  ScrollDir(sBoxHit: THSB; iconHit: TIcon): Integer;
    var dir: Integer;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        if sBoxHit=hSBox then begin
            if iconHit in [iconArwA,iconPagA] then
                dir := iDirLft
            else
                dir := iDirRgt;
        end else begin
            if iconHit in [iconArwA,iconPagA] then
                dir := iDirTop
            else
                dir := iDirBot;
        end;
        ScrollDir := dir;
    end; (* ScrollDir *)

(* ********************************************************************* *)

(*$S Core2*)
    procedure SBoxCtrl(thisPt: Point; sBoxHit: THSB; iconHit: TIcon);
    var mThumb,cThumb,loc,range,fudge: Integer;
        dir,dir1,dir2: Integer;
        msePt:  Point;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        if traceCmd then
            writeln('SBoxCtrl(',ord(sBoxHit):6,',',ord(iconHit):2,')');

        (* get cThumb and dir *)
        cThumb := CThumbPos(sBoxHit);
        dir := ScrollDir(sBoxHit,iconHit);
        (* open clipRgn *)
        SetFldrOrigin;
        case iconHit of
            iconArwA,iconArwB,
            iconPagA,iconPagB:
                begin
                    PaintArw(sBoxHit,iconHit,TRUE);
                    repeat
                        case iconHit of
                            iconArwA,iconArwB:
                                begin
                                    CkGEUpdate;
                                    ScrollCmd(dir,cThumb,TRUE);
                                end;
                            iconPagA,iconPagB:
                                PageCmd(dir,cThumb);
                        end; (* case *)
                    until NOT StillDown;
                    SetFldrOrigin;
                    PaintArw(sBoxHit,iconHit,FALSE);
                end;
            iconThumb:
                begin
                    DragThumb(sBoxHit,thisPt,cThumb);
                    SetThumb(sBoxHit,cThumb);
                    ThumbCmd(dir,cThumb);
                end;
        end; (* case *)
        PenNormal;
        PenMode(patXor);
    end; (* SBoxCtrl *)

(* ********************************************************************* *)

(*$S IO*)
    procedure GrowCtrl(who: GrafPtr; thisPt: Point);
    var endMse: Point;
        hRect,vRect:  Rect;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        if traceCmd then
            writeln('GrowCtrl');

        (* set up *)
        SetPort(who);
        SetOrigin(0,0);
        ClipRect(who^.portRect);
        (* grow the folder *)
        DragFrame(thisPt,TRUE,endMse);
        with who^ do
        if not EqualPt(portRect.botRight,endMse) then begin
            (* erase scroll bars *)
            GetSBarRect(who,hRect,vRect);
            FillRect(hRect,white);
            InValRect(hRect);
            FillRect(vRect,white);
            InValRect(vRect);
            (* resize the folder *)
            FolderSize(who,endMse.h,endMse.v,TRUE);
            (* draw and validate scroll bars,
               depend on update event to redraw new area  *)
            if who=curDoc.fldr then begin
                (* reset all screen sizes based on new fldr size *)
                SetGEBox(TRUE);
                DrwGESBars(who^.portRect)
            end else
                DrwScrpSBars(who^.portRect);
            GetSBarRect(who,hRect,vRect);
            ValidRect(hRect);
            ValidRect(vRect);
        end;
    end; (* GrowCtrl *)

(* ********************************************************************* *)

(*$S PageLayout*)
    procedure RlrCtrl(thisPt: Point);
    var hairsPt: Point;
        msePt:   Point;
        saveOn:  Boolean;

    (* ***************************************************************** *)

        procedure DrwHairs;
        begin
        {$IFC sampcalls} ALogCall; {$ENDC}
            (* set up pen *)
            PenMode(notPatXor);
            PenPat(gray);
            PenSize(1,1);
            with dwgLBox do begin
                MoveTo(left,hairsPt.v);
                LineTo(right,hairsPt.v);
                MoveTo(hairsPt.h,top);
                LineTo(hairsPt.h,bottom);
            end;
        end; (* DrwHairs *)

    (* ***************************************************************** *)

    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        if traceCmd then
            writeln('RlrCtrl');

        with curDoc.atr do
        if not rlrLock[rlrUnit,rlrKind] then begin
            (* get wrkCoords of event *)
            wrkPt := thisPt;
            SubPt(wrkBox.topLeft,wrkPt);
            AddPt(curDoc.atr.scrollPt,wrkPt);
            GridMse(wrkPt,wrk2D);
            saveOn := curDoc.atr.msrOn;
            curDoc.atr.msrOn := FALSE;

            (* track crossHairs in wrkCoords on grid *)
            hairsPt := gridPt;
            DrwHairs;
            While StillDown do begin
                (* GetWrkMse makes wrkPt be in dwgLBox, keep msePt separate  *)
                (* so that can hit test properly in RlrZeroCmd               *)
                GetMouse(msePt);
                GetWrkMse;
                if not EqualPt(msePt,hairsPt) then begin
                    DrwHairs;
                    hairsPt := msePt;
                    DrwHairs;
                end;
            end;
            DrwHairs;
            (* restore pen *)
            PenNormal;
            PenMode(patXor);

            RlrZeroCmd(msePt);
            curDoc.atr.msrOn := saveOn;
        end;
    end; (* RlrCtrl *)

(*$I ApLd/CT1.text *)
(*$I ApLd/CT2.text *)

end. (* unit *)

ÿ