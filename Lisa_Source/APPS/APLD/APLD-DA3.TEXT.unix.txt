(* ********************************************************************* *)

(* CopyRight 1983, Apple Computer Inc. *)
(* GE Unit DAT Include File: Tracing Routines  *)

(* ********************************************************************* *)
(*$S DeBug*)

    procedure WrtRect(* r: Rect *);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        if traceOn then with r do
        write(left:2,',',top:2,',',right:2,',',bottom:2);
    end; (* WrtRect *)

(* ********************************************************************* *)

    procedure WLnRect(* s: String80; r: Rect *);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        if traceOn then begin
            write(s);
            WrtRect(r);
            writeln;
        end;
    end; (* WLnRect *)

(* ********************************************************************* *)

    procedure WlnRgn(* s: String80; r: RgnHandle *);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        if traceOn then begin
            Write(s,Ord(r):8,', rgnSize=',r^^.rgnSize:2,', rgnBBox=');
            WLnRect('',r^^.rgnBBox);
        end;
    end; (* WlnRgn *)

(* ********************************************************************* *)

    procedure WrtOLst(obj: TObjPtr; indentLevel: Integer);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        if traceOn then begin
            if indentLevel=1 then begin
                writeln;
                writeln('kind':4,'C':4,'P':2,'L':2,'bbox':8,'data':12);
                writeln;
            end;
            while obj <> NIL do begin
                WrtObj(obj,indentLevel);
                obj := obj^.fLink;
            end;
        end;
    end; (* WrtOLst *)

(* ********************************************************************* *)

    procedure WrtObj(* obj: TObjPtr; indentLevel: Integer *);
    var i: integer;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        if traceOn then
        with obj^,oDat do begin
            for i := 1 to indentLevel-1 do
                write('    ');
            write(kind:4,cSel:4,pSel:2,locked:2);
            with bBox do
                write(left:4,top:4,' ');
            case kind of
                iObjText:  Write(Round(100*tLoc.x):8,Round(100*tLoc.y):8);
                iObjArc:   with arc^^,aR2D do
                           Write(Round(100*xL):8,Round(100*yT):8,
                                 Round(100*xR):8,Round(100*yB):8);
                iObjRLin,iObjSLin,iObjRect,iObjRndR,iObjOval:
                           Write(Round(100*p1.x):8,Round(100*p1.y):8);
                iObjFHnd,iObjPoly,iObjSPoly:
                           with cLst^^ do
                           Write(' cnt',crdCnt:4,' close',cClose:4,' kind',crdKind:8);
                iObjGroup: write(' lCnt',grp.lCnt:4,
                                 ' lTot',grp.lTot:4,
                                 ' lSiz',grp.lSiz:8);
            end; (* case *)
            writeln(' inf: ',inf.pnSiz:4,inf.pnPat:4,inf.flPat:4,inf.dat:4);
            for i := 1 to indentLevel-1 do
                write('    ');
            with bBox do
                write(Ord(obj):10,'  ',right:4,bottom:4,' ');
            case kind of
                iObjText:  Write(inf.tLen:4,inf.fLen:4,Ord(tHnd):10,Ord(fHnd):10);
                iObjArc:   with arc^^ do Write(aStart:8,aAngle:8);
                iObjRLin,iObjSLin,iObjRect,iObjRndR,iObjOval:
                           Write(Round(100*p2.x):8,Round(100*p2.y):8);
                iObjGroup: write(' lBeg',Ord(grp.lBeg):8,' lEnd',Ord(grp.lEnd):8);
            end; (* case *)
            writeln;
            if obj^.oDat.kind=iObjGroup  then
                WrtOLst(obj^.oDat.grp.lBeg,indentLevel+1);
        end;
    end; (* WrtObj *)

(* ********************************************************************* *)

    procedure DumpDB(* dumpLst: Boolean *);

        procedure WrtDB(db: TODBLst; n: String80);
        begin
        {$IFC sampcalls} ALogCall; {$ENDC}
            if traceOn then with db do begin
                writeln;
                writeln(n:6,' lCnt=',lst.lCnt:4,
                            ' lTot=',lst.lTot:4,
                            ' lSiz=',lst.lSiz:8);
                writeln('       lBeg=',Ord(lst.lBeg):8,
                             ', lEnd=',Ord(lst.lEnd):8,
                             ', oEnd=',Ord(oEnd):8,
                             ', tBeg=',Ord(tBeg):8);
                if dumpLst then
                    if lst.lBeg<>NIL then
                        WrtOLst(lst.lBeg,1)
                    else
                        writeln(' is NIL');
            end;
        end; (* WrtDB *)

    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        if traceOn then begin
            WrtDB(curDoc.oLst, 'ObjDB ');
            {WrtDB(nObjOLst, 'NewDB ');} {nObjOLst no longer used}
        end;
    end; (* DumpDB *)

(* ********************************************************************* *)

    procedure DumpCLst(* cLst: TCLstHnd *);
    var i,count: Integer;
        crdPt: Point;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        if traceOn then
        with cLst^^ do begin
            writeln;
            writeln('DumpCLst(',Ord(cLst):10,') ----------------------');
            writeln;
            writeln('    begPt=',Round(beg2D.x):4,',',Round(beg2D.y):4,
                    ', crdCnt=',crdCnt:3,', crdMax=',crdMax:3,', crdKind=',crdKind:7);
            If crdKind=iObjFHnd then begin
                for i := 1 to crdCnt do begin
                    crdPt.h := 0;
                    crdPt.v := 0;
                    GetNode(cLst,i,crdPt);
                    writeln('        ',crdPt.h:4,crdPt.v:4);
                end;
            end else begin
                Writeln('curVrtLst^^.vCnt = ',curVrtLst^^.vCnt);
                Writeln('curVrtLst^^.vertex[i] = ');
                For i := 0 to crdCnt do
                    Write(curVrtLst^^.vertex[i]:4);
                Writeln('     ');
                If cClose=iCLstUsr then
                    count := crdCnt+2
                else
                    count := crdCnt;
                For i := 0 to count do
                    {$R-}
                    Writeln ('          ',Round(c2DLst[i].x):4,
                              Round(c2DLst[i].y):4 );
                    {$IfC rangeOn} {$R+} {$EndC}

            end;
        end;
    end; (* DumpCLst *)

(* ********************************************************************* *)

    procedure WrtDwgStats(* thisStat: TStatRec *);
    var maxObj,maxArc: Integer;

    (* ***************************************************************** *)

        procedure WrtAvg(siz,num: LongInt);
        var avg: LongInt;
        begin
        {$IFC sampcalls} ALogCall; {$ENDC}
            if traceOn then begin
                avg := 0;
                if num<>0 then
                    avg := siz div num;
                write(avg:8);
            end;
        end; (* WrtAvg *)

    (* ***************************************************************** *)

    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        if traceOn then begin
            maxObj := SizeOf(TObjRec);
            maxArc := SizeOf(TArcRec);
            with thisStat do begin
                write('DwgStats:   obj     arc     c2D');
                writeln('     crd     grp     txt     fmt');
                writeln('    num:',numObj:8,numArc:8,numCrd:8,numC2D:8,
                                   numGrp:8,numTxt:8,numFmt:8);
                writeln('    byt:',sizObj:8,sizArc:8,sizCrd:8,sizC2D:8,
                                   sizGrp:8,sizTxt:8,sizFmt:8);
                writeln('    max:',maxObj:8,maxArc:8,maxCrd:8,maxC2D:8,
                                   maxGrp:8,maxTxt:8,maxFmt:8);
                write('    avg:');
                WrtAvg(sizObj,numObj);
                WrtAvg(sizArc,numArc);
                WrtAvg(sizCrd,numCrd);
                WrtAvg(sizC2D,numC2D);
                WrtAvg(sizGrp,numGrp);
                WrtAvg(sizTxt,numTxt);
                WrtAvg(sizFmt,numFmt);
                Writeln;
            end;
        end;
    end; (* WrtDwgStats *)

(* ********************************************************************* *)

    procedure DumpDoc(* doc: TDocHnd *);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        if traceOn then with doc^^ do begin
            writeln('    Doc(',ord(doc):8,'): fldr=',Ord(fldr):8
                   ,', fName=',fName^^,', dwg,hdr Chgd=',dwgChgd,',',hdrChgd);
            with oLst.lst do
            writeln('        olst cnt=',lCnt:2,', lBeg=',Ord(lBeg):8,
                              ', lEnd=',Ord(lEnd):8);
            writeln('        sLst=',Ord(sLst):8,
                           ', pHz=',Ord(pHz):8,', pRef=',pRef:2,
                           ', hHz=',Ord(hHz):8,', hRef=',hRef:2);
        end;
    end; (* DumpDoc *)

(* ********************************************************************* *)

    procedure DumpDocLst(* s: String80 *);
    VAR i: Integer;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        if traceOn then with docLst^^ do begin
            writeln;
            writeln('Dump DocLst, cnt=',cnt:2,', cur=',cur:2,'    ',s);
            for i := 1 to cnt do
                (*$R-*)
                DumpDoc(lst[i]);
                (*$IfC rangeOn*) (*$R+*) (*$EndC*)
        end;
    end; (* DumpDocLst *)

(* ********************************************************************* *)

    procedure WlnPic(* s: String80; thisPic: PicHandle *);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        If traceOn then begin
            Write(s,Ord(thisPic):8,', picSize=',thisPic^^.picSize:2,', ');
            WlnRect('picFrame=',thisPic^^.picFrame);
        end;
    end; (* WlnPic *)

(* ********************************************************************* *)

    procedure WrtTime(* s: String80; VAR t: LongInt *);
    var tThis: LongInt;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        if traceOn then begin
            tThis := TIME;
            if s<>'' then
                writeln(s:20,(tThis-t):8);
            t := tThis;
        end;
    end; (* WrtTime *)

(* ********************************************************************* *)

    procedure CkGEHz(* s: String80 *);
    var numBlks:  Integer;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        if traceOn then begin
            writeln('Checking Heaps (',s,')...');
            if NOT FCheckHzOk(wmHeap,numblks) then
                Error('WmHeap is bad...',numblks,NONFATAL);
            if geHHz<>NIL then
                if NOT FCheckHzOk(geHHz,numblks) then
                    Error('GEHHz is bad...',numblks,NONFATAL);
            If docLst<>NIL then
                if docLst^^.cnt>0 then begin
                    if NOT FCheckHzOk(curDoc.hHz,numblks) then
                        Error('Doc.HHz is bad...',numblks,NONFATAL);
                    if NOT FCheckHzOk(curDoc.pHz,numblks) then
                        Error('Doc.PHz is bad...',numblks,NONFATAL);
                end;
        end;
    end; (* CkGEHz *)

(* ********************************************************************* *)

    procedure GEDumpSel(* sel: TSelHnd *);
    var i: Integer;
        tSel: TSelLst;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        (*$IfC traceOn *)
        tSel := sel^^;
        writeln;
        writeln('Selection',Ord(sel):10,
                ' sState=',tSel.sState:4,
                ' sCnt=',tSel.sCnt:4,
                ' sTot=',tSel.sTot:4,
                ' sSiz=',tSel.sSiz:4);
        WLnRect('    sBBox:',tSel.sBBox);
        with tSel do
        if (sState=iSStGrp) or (sState>iObjNil) then begin
            writeln;
            writeln('kind':4,'C':4,'P':2,'L':2,'bbox':8,'data':12);
            writeln;
            for i := 1 to tSel.sCnt do
                (*$R-*)
                WrtObj(GetSelObj(sel,i),1);
                (*$IfC rangeOn *) (*$R+*) (*$EndC*)
        end else if sState=iSStPrt then
            writeln('    is PRT.')
        else
            writeln('    is NIL.');
        (*$EndC *)
    end; (* DumpCurSel *)

(* ********************************************************************* *)

    procedure DumpCurCLst;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        (*$IfC traceOn *)
        with curDoc.sLst^^ do
        if (sState<>iObjPoly) AND (sState<>iObjFHnd) AND (sState<>iObjSPoly) then
            writeln('DumpCurCLst--selection inappropriate.')
        else
            (*$R-*)
            DumpCLst(curDoc.sLst^^.lst[1]^.oDat.cLst);
            (*$IfC rangeOn*) (*$R+*) (*$EndC*)
        (*$EndC *)
    end; (* DumpCurCLst *)



ÿ