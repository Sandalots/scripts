(* ********************************************************************* *)

(* CopyRight 1983,1984 Apple Computer Inc. *)
(* GE DMgr Interface *)

(* ********************************************************************* *)

UNIT Dbx;

(* Segment CMD *)
INTERFACE
USES
    (*$U UNITSTD *)    UnitStd,     (* interface dependency  *)
    (*$U UNITHZ*)      UnitHz,
    (*$U STORAGE*)     Storage,
    (*$U QUICKDRAW *)  QuickDraw,
    (*$U FONTMGR*)     FontMgr,
    (*$U LibOS/Syscall *)    Syscall,
    {$U LibPm/PmDecL}      PmDecL,
    {$U PrStdInfo}     PrStdInfo,
    {$U PrPublic}      PrPublic,
    (*$U ApLd/HDR*)      Hdr,

    {$IFC sampcalls}
    {$U apld/tracecalls.obj}  tracecalls,
    {$ENDC}
    (*$U WM.EVENTS*)   Events,
    (*$U WM.FOLDERS*)  Folders,
    (*$U ApLd/DMGR*)        DMgr,
    (*$U WM.MENUS*)    Menus,       (* implementation unit   *)
    (*$U WMLSTD*)      WMLStd,
    (*$U WMLCRS*)      WMLCrs,
    (*$U ALERTMGR*)    AlertMgr,

    (*$U ApLd/DAT*)      Dat,         (* implementation units  *)
    (*$U ApLd/MNU*)      Mnu,
    (*$U ApLd/SEL*)      Sel,
    (*$U ApLd/UOP*)      Uop,
    (*$U ApLd/UTL*)      Utl;

const
    (* 00-99 *)                         (* misc ge messages     *)

    (* 100-199 *)                       (* ge note alerts       *)
    (* FIO Notes 120-124 *)

    (* 200-299 *)                       (* ge wait alerts       *)
    iWaitSave   = 200;
    iWaitLoad   = 201;

    (* 300-399 *)                       (* ge stop alerts       *)
    iStopLock   = 300;
    iStopUndo   = 301;
    iStopSel    = 302;
    iStopNil    = 303;
    iStopSType  = 304;
    iStopPaste  = 305;
    iStopMove   = 306;
    iStopDSize  = 307;
    iStopTSize  = 308;
    iStopTBnds  = 309;
    iStopSSize  = 310;
    iStopRvrt   = 311;
    iStopUnG    = 312;
    iStopUGL    = 313;
    iStopLine   = 314;
    iStopFill   = 315;
    iStopRSize  = 316;
    iStopCase   = 317;
    iStopPClr   = 318;
    iStopSwt    = 319;
    iStopAHnd   = 320;
    iStopSHnd   = 321;
    (* defined in HDR, used in DAT,INT,CTR,CMD,FIO
    iStopMem    = 350;
    iStopDisk   = 351;
    iErrInternal= 1;
    *)

    (* 400-499 *)                       (* ge caution alerts    *)
    iCtnFont    = 400;
    iCtnRevert  = 401;

    iCItmNil    =  1;
    iCItmCut    =  2;
    iCItmCpy    =  3;
    iCItmClr    =  4;
    iCItmDup    =  5;
    iCItmTop    =  6;
    iCItmBot    =  7;
    iCItmGrp    =  8;
    iCItmUnG    =  9;
    iCItmAlO    = 10;
    iCItmLck    = 11;
    iCItmUnL    = 12;
    iCItmInf    = 13;
    iCItmDrw    = 14;
    iCItmRnd    = 15;
    iCItmMov    = 16;
    iCItmStr    = 17;
    iCItmEdt    = 18;
    iCItmPst    = 19;
    iCItmFmt    = 20;
    iCItmCh     = 21;
    iCItmRot    = 22;
    iCItmCase   = 23;
    iCItmSwt    = 24;
    iCItmAKnob  = 25;
    iCItmSKnob  = 26;

VAR
    (* DMgr control vars *)
    keyProc:    ProcPtr;                (* proc to send key events     *)
    keyPort:    GrafPtr;                (* port for showing key events *)
    fldrProc:   ProcPtr;                (* for setting keyProc         *)

    (* dMgr ckForms *)
    geRlrForm:  TCkFormHnd;                     (* dialog box ckForm   *)
    geRndForm:  TCkFormHnd;
    dwgCkForm:  TCkFormHnd;
    rndICk:     TCkBoxHnd;
    rndMCk:     TCkBoxHnd;
    rlrISpcCk:  TCkBoxHnd;
    rlrMSpcCk:  TCkBoxHnd;
    curPgCnt:   Point;

    geAlerts:   TAlertFile;   {Used both here and in in1}

    (* global data for Load, Save getName dialog *)
    geFName:   String80;

    (* alert cover procs *)
    procedure GetPgCnt(dSize: Point; VAR curPgCnt: Point);
    procedure GEAlert(msgNum: Integer);
    procedure GECmdAlert(msgNum,iCmd: Integer);
    function  GECaution(msgNum: Integer): Boolean;
    procedure GEGetAlert(msgNum: Integer; VAR str: Str255);
    procedure GEDTAlert(dtm: LongInt; VAR whenStr: Str255);

    (* GE Dialogs *)
    procedure SetUpRndCmd(default: Integer; Cmd: ProcPtr);
    procedure SetUpDwgCmd(Cmd: ProcPtr);
    procedure SetUpRlrCmd(Cmd: ProcPtr);
    procedure GEEndDBox;
    procedure InitDBX1;
    procedure InitDBX2;

(* ********************************************************************* *)

IMPLEMENTATION


(* ********************************************************************* *)

CONST
    vDBox   = 100;  (* dwgSize dBox size *)
TYPE
    TDRlrInfo =
        record
            rKind: TRlrKind;
            rIndx: TRlrIndx;
            rLock: Array[TRlrKind] of TByte;
        end;
VAR
    LoadCmdPtr:    ProcPtr;
    (* dMgr messages read in from text file  *)
    geHMsg:     TStrHnd;
    geMsg:      Str255;
    (* for dwgSize dialog *)
    hPg,vPg:    Integer;
    maxPCRect:  Rect;
    minPgCnt:   Point;
    maxPgCnt:   Point;
    (* for rlr dialog *)
    dRlrInfo: Array[TUnits] of TDRlrInfo;

(* ********************************************************************* *)
(*$S PageLayout*)
    procedure GetPgCnt(* dSize: Point; VAR curPgCnt: Point *);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        with curDoc.atr do begin
            If pageSz.h>0 then
                curPgCnt.h := 1+((dSize.h-1) Div pageSz.h);
            If pageSz.v>0 then
                curPgCnt.v := 1+((dSize.v-1) Div pageSz.v);
        end;
    end; (* GetPgCnt *)

(* ********************************************************************* *)
(*$S PageLayout*)
    procedure SetDPort;
    var tRect: Rect;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        SetPort(dialogFolder);
        TextFont(tile12);
        TextFace([]);
        (* should set clip on DialogHeight *)
        SetRect(tRect,-30000,-30000,30000,30000);
        ClipRect(tRect);
    end; (* SetDPort *)

(* ********************************************************************* *)
(*$S DBX*)
    procedure ClearMsg;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        if theDBox.isOpen then
            ClearDBox;
    end; (* ClearMsg *)

(* ********************************************************************* *)

    (* ProcessProc for msg dialog *)
(*$S DBX*)
    procedure GetMsg(thisEvent: EventRec);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        ClearMsg;
    end; (* GetMsg *)

(* ********************************************************************* *)

    (* EndProc for msg dialog *)
(*$S DBX*)
    procedure EndMsg(doItFlag: Boolean);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        ClearMsg;
    end; (* EndMsg *)

(* ********************************************************************* *)
(*$S DBX*)
    procedure GEAlert(* msgNum: Integer *);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        (* only alert if we are active *)
        if curDoc.fldr=activeFolder then begin
            ParamAlert(param1,param2,param3);
            if msgNum<200 then
                NoteAlert(geAlerts,msgNum)
            else if msgNum<300 then
                WaitAlert(geAlerts,msgNum)
            else if msgNum<400 then
                StopAlert(geAlerts,msgNum);
        end;

        if traceCmd then
            writeln('GEAlert(',msgNum:2,',"',param1,'","',
                              param2,'","',param3,'")"');
    end; (* GEAlert *)

(* ********************************************************************* *)
(*$S DBX*)
    procedure GECmdAlert(* msgNum,iCmd: Integer *);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        WouldAlert(geMenus[iMnuCmds],iCmd);
        GEAlert(msgNum);
    end; (* GESelAlert *)

(* ********************************************************************* *)
(*$S DBX*)
    function  GECaution(* msgNum: Integer): Boolean *);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        (* only alert if we are active *)
        if curDoc.fldr=activeFolder then begin
            ParamAlert(param1,param2,param3);
            GECaution := CautionAlert(geAlerts,msgNum);
        end;
        if traceCmd then
            writeln('GECaution(',msgNum:2,',"',param1,'","',
                              param2,'","',param3,'")"');
    end; (* GECaution *)

(* ********************************************************************* *)
(*$S DBX*)
    procedure GEGetAlert(* msgNum: Integer; VAR str: Str255 *);
    var pStr: TPstr255;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        pStr := @str;
        GetAlert(geAlerts,msgNum,pStr);
    end; (* GEGetAlert *)

(* ********************************************************************* *)
(*$S DBX*)
    procedure GEDTAlert{(dtm: LongInt; VAR whenStr: Str255)};
    var paramAlert: TParamAlert;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        DTAlert(geAlerts,dtm,paramAlert);
        whenStr := paramAlert;
    end; (* GEGetAlert *)

(* ********************************************************************* *)
(*$S DBX*)
    procedure TrkCkMse(msePt: Point);
    var info:     TCkFormHnd;
        i,ckItm:  Integer;
        inCkItm:  Boolean;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        (* assumes port is already set *)
        (* msePt is in dialog coords *)
        inCkItm := FALSE;
        info := Pointer(Ord(theDBox.inData));
        with info^^ do begin
            for i := 0 to numCkBox do
                (*$R-*)
                if CkBoxHit(ckBoxLst[i],msePt,ckItm) then begin
                    i := numCkBox;
                    inCkItm := ckItm<>0;
                end;
                (*$IfC rangeOn *)
                (*$R+*)
                (*$EndC *)
        end;
        if inCkItm then
            SetStdCursor(iCrsCheck)
        else
            SetStdCursor(iCrsInactive);
    end; (* TrkCkMse *)

(* ********************************************************************* *)

(* SetUp for GE Dialogs *)

(* ********************************************************************* *)
(*$S DBX*)
    procedure SetUpRndCmd(* default: Integer; Cmd: ProcPtr *);
    var endProc: ProcPtr;
        thisSel: TSelPtr;
        rIndex,i,sCnt: Integer;
        selObj: TObjPtr;
        allSame: Boolean;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        if traceCmd then
            writeln('SetUpRndCmd');

        (* finish any pending dialog before starting a new one *)
        if theDBox.isOpen then
            GEEndDBox;
        (* see if all rnd rects currently have same radius *)
        if curDoc.sLst^^.sState=-1 then
            rIndex := default
        else begin
            (* NOTE: check lastOp! *)
            if lastOp.op=iOpRnd then
                rIndex := lastOp.iDat1
            else begin
                rIndex := -1;
                allSame := TRUE;
                sCnt := curDoc.sLst^^.sCnt;
                for i := 1 to sCnt do begin
                    selObj := CurSelObj(i);
                    with selObj^.oDat do
                    if kind=iObjRndR then begin
                        if rIndex=-1 then
                            rIndex := inf.dat
                        else
                            allSame := rIndex=inf.dat;
                    end else if kind=iObjRect then
                        if rIndex=-1 then
                            rIndex := 0
                        else
                            allSame := rIndex=0;
                    if not allSame then
                        i := sCnt;
                end;
                if not allSame then
                    rIndex := default;
            end;
        end;
        (* make sure DoIt/ForgetIt boxes are unchecked *)
        geRndForm^^.ckBoxLst[0]^^.curCkItm := -1;
        (* display proper units *)
        (*$R-*)
        if curDoc.atr.rlrUnit=iUnitInch then
            geRndForm^^.ckBoxLst[1] := rndICk
        else
            geRndForm^^.ckBoxLst[1] := rndMCk;
        geRndForm^^.ckBoxLst[1]^^.curCkItm := rIndex+1;
        (* make sure DoIt/ForgetIt boxes are unchecked *)
        geRndForm^^.ckBoxLst[2]^^.curCkItm := -1;
        (*$IfC rangeOn *)
        (*$R+*)
        (*$EndC *)
        (* set up and draw dialog box *)
        SetDBox(Pointer(Ord(geRndForm)),NIL,
                @GetCkHit,@TrkCkMse,@RestoreCkForm,Cmd);
        SetDPort;
        ShowCkForm(geRndForm);
    end; (* SetUpRndCmd *)

(* ********************************************************************* *)

(* Drawing Size Dialog Routines *)

(* ********************************************************************* *)
(*$S PageLayout*)
    procedure ShowPages(pgCnt: Point);
    var (* grid vars *)
        pgRect: Rect;
        i,j: Integer;
        xSz,ySz: Real;
        (* text vars *)
        pgX,pgY,xDots,yDots: Real;
        uStr,numStr1,numStr2: String80;
        s: String80;
        sLoc: Point;
        sRect: Rect;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        if traceCmd then
            writeln('ShowPages(',pgCnt.h:2,',',pgCnt.v:2,')');

        (* draw the value *)
        if curDoc.atr.rlrUnit=iUnitInch then begin
            uStr := '"';
            xDots := xInchDots;
            yDots := yInchDots;
        end else begin
            uStr := 'cm';
            xDots := xCMDots;
            yDots := yCMDots;
        end;
        pgX := curDoc.atr.pageSz.h/xDots;
        pgY := curDoc.atr.pageSz.v/yDots;
        xSz := RMin(pgCnt.h*pgX,cHDwgMax/xDots);
        ySz := RMin(pgCnt.v*pgY,cVDwgMax/yDots);
        (* convert to strings *)
        GetDistStr(xSz,numStr1);
        GetDistStr(ySz,numStr2);
        {
        IntToStr(100*xSz,numStr1);
        Insert('.',numStr1,Length(numStr1)-1);
        IntToStr(100*ySz,numStr2);
        Insert('.',numStr2,Length(numStr2)-1);
        }
        s := Concat(' ',numStr1,uStr,' x',numStr2,uStr);
        (* get string loc *)
        TextFont(tile12);
        with dwgCkForm^^.ckBoxLst[0]^^.ckItmLst[0]^^ do begin
            sLoc := iLoc;
            sRect := hitBox;
            sRect.left  := hitBox.left+StringWidth(iTitle^^)+32;
            sRect.right := maxPCRect.left;
            if traceCmd then begin
                writeln('    iTitle: "',iTitle^^,'", width:',StringWidth(iTitle^^));
                WlnRect('    hitBox:',hitBox);
                WlnRect('    sRect: ',sRect);
                WlnRect('    maxPC: ',maxPCRect);
            end;
        end;
        sLoc.h := sRect.left;
        (* draw the string *)
        FillRect(sRect,white);
        MoveTo(sLoc.h,sLoc.v);
        DrawString(s);

        (* draw the page size grid *)
        PenNormal;
        for i := 1 to maxPgCnt.v do
            for j := 1 to maxPgCnt.h do begin
                (* get the pgRect *)
                SetRect(pgRect,0,0,hPg+1,vPg+1);
                with maxPCRect do
                OffsetRect(pgRect,left+(j-1)*hPg,top+(i-1)*vPg);
                pgRect.right  := CMin(pgRect.right ,maxPCRect.right );
                pgRect.bottom := CMin(pgRect.bottom,maxPCRect.bottom);
                (* fill it with black or white *)
                if (j<=pgCnt.h) and (i<=pgCnt.v) then begin
                    PenPat(white);
                    if j>1 then begin
                        MoveTo(pgRect.left,pgRect.bottom-1);
                        LineTo(pgRect.left,pgRect.top);
                    end;
                    if i>1 then begin
                        MoveTo(pgRect.left,pgRect.top);
                        LineTo(pgRect.right-1,pgRect.top);
                    end;
                    PenPat(black);
                    InsetRect(pgRect,1,1);
                    FillRect(pgRect,black)
                end else begin
                    MoveTo(pgRect.left,pgRect.bottom-1);
                    LineTo(pgRect.left,pgRect.top);
                    LineTo(pgRect.right-1,pgRect.top);
                    InsetRect(pgRect,1,1);
                    FillRect(pgRect,white);
                end;
            end;
    end; (* ShowPages *)

(* ********************************************************************* *)
(*$S PageLayout*)
    procedure GetDwgHit(thisEvent: EventRec);
    var oCPgs,msePt: Point;
        ckItm: Integer;
        aFlag: Boolean;

    (* ***************************************************************** *)

        procedure GetDwgSz(msePt: Point; var size: Point);
        begin
        {$IFC sampcalls} ALogCall; {$ENDC}
            (* get the dwg size in page units *)
            SetPt(size,1+((msePt.h-maxPCRect.left) Div hPg),
                       1+((msePt.v-maxPCRect.top ) Div vPg));
            (* set flag if trying to set size below minimum *)
            aFlag := (size.h<minPgCnt.h) or (size.v<minPgCnt.v);
            (* check page size limits *)
            if size.h<minPgCnt.h then
                size.h := minPgCnt.h
            else if size.h>maxPgCnt.h then
                size.h := maxPgCnt.h;
            if size.v<minPgCnt.v then
                size.v := minPgCnt.v
            else if size.v>maxPgCnt.v then
                size.v := maxPgCnt.v;
        end; (* GetDwgSz *)

    (* ***************************************************************** *)

        procedure DoTrk(msePt: Point);
        begin
        {$IFC sampcalls} ALogCall; {$ENDC}
            if PtInRect(msePt,maxPCRect) then begin
                GetDwgSz(msePt,curPgCnt);
                if not EqualPt(curPgCnt,oCPgs) then begin
                    oCPgs := curPgCnt;
                    ShowPages(curPgCnt);
                end;
            end;
        end; (* DoTrk *)

    (* ***************************************************************** *)

    begin
        {$IFC sampcalls} ALogCall; {$ENDC} (* GetDwgHit *)
        (* in page size box: track mouse *)
        if PtInRect(thisEvent.where,maxPCRect) then begin
            GetDwgSz(thisEvent.where,curPgCnt);
            ShowPages(curPgCnt);
            oCPgs := curPgCnt;
            while StillDown do begin
                GetMouse(msePt);
                DoTrk(msePt);
            end;
            if PeekEvent(thisEvent) then
                if thisEvent.what=buttonUp then begin
                    GetEvent(thisEvent);
                    DoTrk(thisEvent.where);
                end;
            if aFlag then
                GEAlert(iStopDSize);
        (* in doIt/forgetIt ckBox: get hit *)
        end else begin
            (* wait till mouse is up *)
            repeat
            until not StillDown;
            (* get mouse up pt *)
            GetMouse(msePt);
            if PeekEvent(thisEvent) then
                if thisEvent.what=buttonUp then begin
                    GetEvent(thisEvent);
                    msePt := thisEvent.where;
                end;
            (* see if we have a hit *)
            with dwgCkForm^^ do
            if CkBtnHit(ckBoxLst[0],msePt,ckItm) then with ckBoxLst[0]^^ do begin
                if ckItm>0 then
                    CallEndProc(ckItm=2,theDBox.endProc);
            end;
        end;
    end; (* GetDwgHit *)

(* ********************************************************************* *)
(*$S PageLayout*)
    procedure ShowDwgForm;
    const
        vDBox   = 100;  (* dBox height *)
    var thisPort: GrafPtr;
        tRect: Rect;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        (* set up port *)
        DialogHeight(vDBox,FALSE);
        SetDPort;
        theDBox.isOpen := TRUE;

        CallShowCkBox(dwgCkForm^^.ckBoxLst[0],dwgCkForm^^.ckBoxLst[0]^^.DrawProc);
        tRect := maxPCRect;
        tRect.right  := tRect.right+1;
        tRect.bottom := tRect.bottom+1;
        FillRect(tRect,black);
        ShowPages(curPgCnt);
    end; (* ShowDwgForm *)

(* ********************************************************************* *)
(*$S PageLayout*)
    procedure TrkDwgCk(msePt: Point);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        SetStdCursor(iCrsInactive);
    end; (* TrkDwgCk *)

(* ********************************************************************* *)
(*$S PageLayout*)
    procedure SetUpDwgCmd(* Cmd: ProcPtr *);
    var form: TCkFormHnd;
        gBox: Rect;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        if traceCmd then with curDoc.atr.pageSz do
            writeln('SetUpDwgCmd, pageSz:',h:2,',',v:2);

        (* finish any pending dialog before starting a new one *)
        if theDBox.isOpen then
            GEEndDBox;
        (* make sure DoIt/ForgetIt boxes are unchecked *)
        with dwgCkForm^^.ckBoxLst[0]^^ do
            curCkItm := -1;

        (* get min, max, and cur dwg sizes in page units *)
        (* min dwgSz determined by dwg GBox *)
        if (curDoc.oLst.lst.lBeg=NIL)
        OR ((curPgCnt.h=1) AND (curPgCnt.v=1)) then
            SetPt(minPgCnt,1,1)
        else begin
            GetDwgGBox(gBox);
            GetPgCnt(gBox.botRight,minPgCnt);
        end;
        (* max pgCnt is a constant *)
        SetPt(maxPgCnt,((cHDwgMax-1) Div curDoc.atr.pageSz.h)+1,
                       ((cVDwgMax-1) Div curDoc.atr.pageSz.v)+1);
        (* get current drawing size in page units *)
        GetPgCnt(curDoc.atr.dwgSz,curPgCnt);

        (* get scaled down sizes to use in dialog box *)
        (* page size *)
        hPg := curDoc.atr.pageSz.h Div 64;
        vPg := curDoc.atr.pageSz.v Div 64;
        (* maxPgCCnt Rect size *)
        SetRect(maxPCRect,0,0,cHDwgMax Div 64,
                              cVDwgMax Div 64);
        with maxPCRect do
        OffSetRect(maxPCRect,-(left+right) Div 2,
                             -(top+bottom) Div 2);
        with dialogFolder^.portRect do
        OffSetRect(maxPCRect,40 + (left+right) Div 2,
                             50 (* dialogHeight Div 2 *));

        (* set up and draw dialog box *)
        SetDBox(NIL,NIL,@GetDwgHit,@TrkDwgCk,@ShowDwgForm,Cmd);
        ShowDwgForm;
    end; (* SetUpDwgCmd *)

(* ********************************************************************* *)

(* Ruler Dialog Routines *)

(* ********************************************************************* *)

    (* Ruler Dialog units change with inch/centimeter option.  Check     *)
    (* the boxes according to current interim settings. *)
(*$S PageLayout*)
    procedure CkRlrBoxes(rUnit: Integer);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        (*$R-*)
        (* change the state of the rlrForm to new units *)
        with dRlrInfo[rUnit],geRlrForm^^ do begin
            (* "Major Division Spacing" text changes according to unit *)
            if rUnit=iUnitInch then
                ckBoxLst[iRlrSpc] := rlrISpcCk
            else
                ckBoxLst[iRlrSpc] := rlrMSpcCk;
            (* inch/centimeter ckBox *)
            ckBoxLst[iRlrAtr]^^.curCkItm := rUnit+2;
            (* standard/custom ckBox *)
            ckBoxLst[iRlrKnd]^^.curCkItm := rKind;
            (* ruler zero point ckBox *)
            ckBoxLst[iRlrLck]^^.curCkItm := rLock[rKind];
            (* custom ckBoxes *)
            ckBoxLst[iRlrSpc]^^.curCkItm := rIndx.iSpc;
            ckBoxLst[iRlrDvs]^^.curCkItm := rIndx.iDvs;
            ckBoxLst[iRlrInc]^^.curCkItm := rIndx.iInc;
            (* don't show custom dialog if custom not checked *)
            if rKind=iRlrStd then
                numCkBox := 3
            else
                numCkBox := 6;
            CkFormHeight(geRlrForm);
        end;
        (*$IfC rangeOn *)
        (*$R+*)
        (*$EndC *)
    end; (* CkRlrBoxes *)

(* ********************************************************************* *)

    (* custom ckBox process proc to be able to switch ckForms *)
(*$S PageLayout*)
    procedure GetRlrHit(thisEvent: EventRec);
    var ckBox,ckItm,rUnit,oUnit,oKind: Integer;
        doItFlag:Boolean;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        if traceCmd then
            writeln('GetRlrHit(',thisEvent.what:2,')');

        (*$R-*)
        if thisEvent.what=buttonDown then begin
            (* remember old units,kind,lock ckItm *)
            oUnit := geRlrForm^^.ckBoxLst[iRlrAtr]^^.curCkItm;
            oKind := geRlrForm^^.ckBoxLst[iRlrKnd]^^.curCkItm;

            (* check the boxes *)
            CkFormSelect(geRlrForm,ckBox,ckItm);
            if traceCmd then
                writeln('    ',ckBox:4,ckItm:4);

            (* see if ckBox is unit change or doIt/forgetIt *)
            if (ckItm<>0) then begin
                (* ruler unit change: inch/centimeter *)
                if (ckBox=iRlrAtr) and (ckItm>2) and (oUnit<>ckItm) then begin
                    rUnit := ckItm-2;
                    (* record the old state of rlrForm *)
                    oUnit := iUnitInch;
                    if rUnit=iUnitInch then
                        oUnit := iUnitMtrc;
                    with dRlrInfo[oUnit],geRlrForm^^ do begin
                        rKind        := ckBoxLst[iRlrKnd]^^.curCkItm;
                        rLock[rKind] := ckBoxLst[iRlrLck]^^.curCkItm;
                        rIndx.iSpc   := ckBoxLst[iRlrSpc]^^.curCkItm;
                        rIndx.iInc   := ckBoxLst[iRlrInc]^^.curCkItm;
                        rIndx.iDvs   := ckBoxLst[iRlrDvs]^^.curCkItm;
                    end;
                    (* check boxes and show form *)
                    CkRlrBoxes(rUnit);
                    if (dRlrInfo[iUnitInch].rKind=iRlrCustom)
                    or (dRlrInfo[iUnitMtrc].rKind=iRlrCustom) then begin
                        SetDPort;
                        ShowCkForm(geRlrForm);
                    end;
                (* ruler kind change: standard/custom *)
                end else if (ckBox=iRlrKnd) and (oKind<>ckItm) then begin
                    (* record the old state of rlrLock *)
                    with dRlrInfo[oUnit],geRlrForm^^ do
                        rLock[rKind] := ckBoxLst[iRlrLck]^^.curCkItm;
                    (* record new state of rlrUnit *)
                    rUnit := geRlrForm^^.ckBoxLst[iRlrAtr]^^.curCkItm-2;
                    with dRlrInfo[rUnit],geRlrForm^^ do
                        rKind := ckBoxLst[iRlrKnd]^^.curCkItm;
                    (* check boxes and show form *)
                    CkRlrBoxes(rUnit);
                    SetDPort;
                    ShowCkForm(geRlrForm);
                (* doIt/forgetIt *)
                end else if ckBox=0 then begin
                    doItFlag := ckItm=2;
                    CallEndProc(doItFlag,theDBox.endProc)
                end;
            end;
        end;
        (*$IfC rangeOn *)
        (*$R+*)
        (*$EndC *)
    end; (* GetRlrHit *)

(* ********************************************************************* *)
(*$S PageLayout*)
    procedure SetUpRlrCmd(* Cmd: ProcPtr *);
    var form: TCkFormHnd;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        if traceCmd then
            writeln('SetUpRlrCmd');

        (* finish any pending dialog before starting a new one *)
        if theDBox.isOpen then
            GEEndDBox;

        (* make sure DoIt/ForgetIt boxes are unchecked *)
        geRlrForm^^.ckBoxLst[0]^^.curCkItm := -1;

        (* set dRlrInfo to current settings of rlrFmts *)
        with dRlrInfo[iUnitInch] do begin
            rKind := curDoc.atr.rlrKind;
            rIndx := curDoc.atr.rlrFmts[iUnitInch,iRlrCustom];
            rLock[iRlrStd]    := 2-Ord(curDoc.atr.rlrLock[iUnitInch,iRlrStd]);
            rLock[iRlrCustom] := 2-Ord(curDoc.atr.rlrLock[iUnitInch,iRlrCustom]);
        end;
        with dRlrInfo[iUnitMtrc] do begin
            rKind := curDoc.atr.rlrKind;
            rIndx := curDoc.atr.rlrFmts[iUnitMtrc,iRlrCustom];
            rLock[iRlrStd]    := 2-Ord(curDoc.atr.rlrLock[iUnitMtrc,iRlrStd]);
            rLock[iRlrCustom] := 2-Ord(curDoc.atr.rlrLock[iUnitMtrc,iRlrCustom]);
        end;

        (* check boxes according to current rlr state *)
        (*$R-*)
        with geRlrForm^^ do begin
            ckBoxLst[iRlrAtr]^^.curOnOff := 2-Ord(curDoc.atr.rlrOn);
            CkRlrBoxes(curDoc.atr.rlrUnit);
        end;
        (*$IfC rangeOn *)
        (*$R+*)
        (*$EndC *)

        (* set up and draw dialog box *)
        SetDBox(Pointer(Ord(geRlrForm)),NIL,@GetRlrHit,
                @TrkCkBoxMse,@RestoreCkForm,Cmd);
        SetDPort;
        ShowCkForm(geRlrForm);
    end; (* SetUpRlrCmd *)

(* ********************************************************************* *)
(*$S DBX*)
    procedure GEEndDBox;
    var thisTime: LongInt;
        thisPort: GrafPtr;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        GetPort(thisPort);
        SetDPort;
        PushButn(0,TRUE);
        thisTime := Time;
        repeat until Time>thisTime+5;
        CallEndProc(FALSE,theDBox.endProc);
        SetPort(thisPort);
    end; (* GEEndDBox *)

(* ********************************************************************* *)

(* Initialization *)



(* ********************************************************************* *)
(*$S Init*)

    procedure InitDBX1;
    var refNum: Integer;
        pName:  pathName;
        pInfo:  ProcInfoRec;
        errNum: Integer;

    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        if traceCmd then
            writeln('InitDBX1');

        (* init alert Mgr params *)
        param1 := '';
        param2 := '';
        param3 := '';

        (*InitAlerts(3,2100,geHHz,NIL,@FailedAlert);
        if alertError>0 then
            FailedInit;
        Info_Process(errNum,My_ID,pInfo);
        pName := Copy(pInfo.progPathName,1,Pos('}',pInfo.progPathName));*){Bug fix}
        {OpenPhraseFile(refNum,pName);
        if alertError>0 then
            FailedInit;
        geAlerts := ReadAlerts(refNum,sVrsNum);
        if alertError>0 then
            FailedInit;
        if rangeOn then
            if alertError<0 then begin
                BeepAlert(2);
                Error('Warning from AlertMgr',alertError,NONFATAL);
            end;}
        (* allocate space for geMsg *)
        geHMsg := Pointer(Ord(HAllocate(geHHz,256)));
        (* set up dBox to do nothing *)
        theDBox.isOpen := FALSE;
        (* we shouldn't need this 10/19/83:
        SetDBox(@geMsg,NIL,@GetMsg,@TrkMsgMse,@ClearMsg,@EndMsg);
        *)
    end; (* InitDBX1 *)

(* ********************************************************************* *)
(*$S Init*)

    procedure InitDBX2;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        if traceCmd then
            writeln('InitDBX2');
        (* call after menus are read in *)
        (* initialize ^C *)
        WouldAlert(geMenus[iMnuCmds],iCItmNil);
    end; (* InitDBX2 *)

end. (* unit *)

ÿ