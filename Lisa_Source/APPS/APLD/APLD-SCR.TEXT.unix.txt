(* ********************************************************************* *)

(* CopyRight 1983,1984 Apple Computer Inc. *)
(* GE Screen Drawing Routines *)

(* ********************************************************************* *)

UNIT Scr;

(* Segment SCR,UTL *)
INTERFACE
USES
    (*$U UNITSTD *)    UnitStd,     (* interface dependency  *)
    (*$U UNITHZ*)      UnitHz,
    (*$U STORAGE*)     Storage,
    (*$U QUICKDRAW*)   QuickDraw,
    (*$U FONTMGR*)     FontMgr,
    (*$U libos/syscall.obj*)     Syscall,
    (*$U WM.EVENTS*)   Events,
    (*$U WMLSTD*)      WMLStd,
    (*$U WMLSB*)       WMLSB,
    {$U LibPm/PmDecL}      PmDecL,
    {$U PrStdInfo}     PrStdInfo,
    {$U PrPublic}      PrPublic,
    (*$U ApLd/HDR*)      Hdr,
    {$IFC sampcalls}
    {$U apld/tracecalls.obj}  tracecalls,
    {$ENDC}
    (*$U WM.FOLDERS*)  Folders,    (* implementation units  *)
    (*$U WM.MENUS*)    Menus,
    (*$U WMLGROW*)     WMLGrow,
    (*$U WMLCRS*)      WMLCrs,
    (*$U ApLd/DAT*)      Dat,
    (*$U ApLd/SEL*)      Sel,
    (*$U ApLd/MNU*)      Mnu,
    (*$U ApLd/UOP*)      UOp,
    (*$U ApLd/SCP*)      Scp,
    (*$U ApLd/TXT*)      Txt,
    (*$U ApLd/DRW*)      Drw,
    (*$U ApLd/UTL*)      Utl;

CONST
    (* proto pattern indices *)
    iPatGry     = 0;
    iPatBlk     = 1;
    (* proto dimensions *)
    protoWidth  = 36;
    protoHeight = 24;

VAR
    mkObjFlag:  Boolean;        (* used in CTR for AutoEdit Mode *)
    (* poly pt *)
    polyPt:     Point;
    lastPolyPt: Point;
    (* anchor pt *)
    anchor2D:   Point2D;
    anchorPt:   Point;
    autoDrwFlag:Boolean;
    (* poly obj gets fixed in SetCurProto *)
    polyCLst:   TCLstHnd;
    (* dupCmd info *)
    dupCtrFlag: Boolean;
    dupSame2D:  Point2D;

    (* The GE folder is subdivided into rectangular, non-overlapping     *)
    (* boxes that are used to display standard folder scroll bars and    *)
    (* grow box, drawing aids such as distance or rulers, drawing proto- *)
    (* types, and the actual user drawing. *)

    (* main folder *)
    fActive:    BOOLEAN;
    (* scrap folder *)
    sActive:    BOOLEAN;

    (* current cursor index *)
    geCrsCur:   Integer;
    geCrsIBeam: Integer;
    (* prototype box *)
    protoBox:   Rect;
    protoSzBox: Rect;
    (* pen setting box *)
    penBox:     Rect;
    (* ruler boxes *)
    topRlrBox:  Rect;
    lftRlrBox:  Rect;
    zroRlrBox:  Rect;
    (* scroll boxes *)
    sbList:     TSBList;
    hSBox:      THSb;
    vSBox:      THSb;

    sbListScrp: TSBList;
    hSBoxScrp:  THSb;
    vSBoxScrp:  THSb;

    (* Boxes are in folder coordinates.
       LBoxes are in world coords, i.e. scrolled and zoomed.
       The drawing work area, i.e. wrkLBox is used to interactively create
       drawings.  If the drawing paper is smaller than the work area,
       then part of the background will show around the edges of the paper.
       The actual visible paper area in the wrkBox is the dwgLBox, i.e. the
       intersection of the wrkLBox and the SizeLBox.  WrkLBox does not include
       the ruler bars.
    *)

    wrkBox:     Rect;
    dwgBox:     Rect;

    wrkLBox:    Rect;           (* wrkBox in wrk coords                 *)
    dwgLBox:    Rect;           (* dwgBox in wrk coords                 *)
    SizeLBox:   Rect;           (* size box in wrk coords               *)
    topRlrLBox: Rect;
    lftRlrLBox: Rect;
    zroRlrLBox: Rect;

    msePt:      Point;          (* screen mouse point                   *)
    wldPt:      Point;          (* wld mouse point                      *)
    wrkPt:      Point;          (* wldPt limited to lmtRect             *)
    wrk2D:      Point2D;        (* 2D coordinates of wrkPt              *)
    gridPt:     Point;          (* wrkPt rounded to nearest grid        *)
    grid2D:     Point2D;        (* 2D coordinates of gridPt             *)

    (* ruler size and zeroPt *)
    rlrSize:    Point;

    (* txt proto locations  *)
    txtLoc:     Array[1..3] of Point;
    (* curPen pallette char *)
    clearCh:    Char;
    (* ge cursor values *)
    geCrsVal:   Array[iObjNil..iObjPoly] of TByte;

    (* is the RLin horiz or vertical? used in TrkDist *)
    trkHoriz:  Boolean;

    (* Scrolling *)
    function  NeedScroll: Boolean;
    procedure AutoScroll (showSel: Boolean);
    function  ThumbPos(loc,range: Integer): Integer;
    function  DwgThumbPos(dir: Integer): Integer;
    procedure ScrollCmd  (dir,cThumb: Integer; showSel: Boolean);
    procedure PageCmd    (dir,dThumb: Integer);
    procedure ThumbCmd   (dir,cThumb: Integer);
    procedure ScrollTo   (h,v: Integer);
    procedure ScrollObj  (obj: TObjPtr);

    (* rulers *)
    procedure ShowRuler(rRect: Rect);
    procedure RedrawTopRlr;
    procedure RedrawLftRlr;
    procedure RedrawRlr;
    procedure ShowRlrHrs(lclPt: Point; display: Boolean);

    (* object display procedures *)
    procedure ShowRFrom(r: Rect; obj: TObjPtr);
    procedure ReShow(r: Rect);
    procedure ReShowUnion(r,r1: Rect);
    procedure ReShowInset(r: Rect);
    procedure ReShowSel;
    procedure RSSel(showHiLite: boolean);

    (* Selection Display *)
    procedure EraseSel;
    procedure HiLiteSel(sVal: Boolean);

    (* prototype objects *)
    procedure GetPRect(index: Integer; var r: Rect);
    procedure ShowProto(index: Integer; showHiLite: Boolean);
    procedure BlinkProto(index: Integer);
    procedure SetCurProto(index: Integer; showHiLite: Boolean);
    procedure GetProto(index: Integer; obj: TObjPtr);
    procedure ShowCurPen(index: Integer);

    (* distance *)
    procedure TrkPxlDist(thisPt: Point);
    procedure TrkDist(lcl2D: Point2D);
    procedure TurnOnDist(kind: Integer; beg2D,lcl2D: Point2D);
    procedure TurnOffDist;
    procedure HideDist;

    (* sBar display *)
    procedure GetSBarRect(who: GrafPtr; VAR hRect,vRect: Rect);
    procedure DrwScrpSBars(r: Rect);
    procedure DrwGESBars(r: Rect);

    (* folders *)
    procedure DrwScrpFldr;
    procedure DrwGEFldr;
    procedure CkGEUpdate;

    (* screen layout *)
    procedure SetLclBox;
    procedure SetGEBox(ckWrkRef: Boolean);
    procedure DumpGEBox;
    procedure SetFldrOrigin;
    procedure SetWrkOrigin;

    (* mouse *)
    procedure TrkMse(lclPt: Point);
    procedure SetMseLmt(r: Rect);
    procedure GridMse(var thisPt: Point; var this2D: Point2D);
    procedure SetWrkMse(thisPt: Point);
    procedure GetWrkMse;

    (* misc *)
    procedure SetCurSel(thisObj: TObjPtr; iSel: Integer);
    procedure ResetTxt;
    procedure ResetAutoDrw;
    procedure ResetCurSel;
    procedure ResetPoly;
    procedure ResetEd1;
    procedure ResetEdit;
    procedure RestartEdit;
    procedure InitScr;

(* ********************************************************************* *)

IMPLEMENTATION


(* ********************************************************************* *)

CONST
    hScroll = 16;
    vScroll = 16;

    rlrWidth    = 22;  (* dhSBox-1 *)    (* rlr panel width    *)
    rlrHeight   = 15;  (* dvSBox-1 *)    (* rlr panel height   *)
var (* ruler hairs state *)
    rlrPt:      Point;
    rStateOn:   Boolean;
    (* proto hilite state *)
    pHLStateOn: Boolean;
    pHLIndex:   Integer;
    (* mouse *)
    trkPt:      Point;                  (* used in TrkMse routine *)
    mseLmtBox:  Rect;
    (* rulers *)
    hRlrTicks:  Array[1..4] of Integer;
    vRlrTicks:  Array[1..4] of Integer;
    (* scrolling *)
    dirFactor:  Array[iDirLft..iDirBot] of Point;
    lastDir:    Integer;
    autoSFlag:  Boolean;
    (* distance *)
    mDistOn:    Boolean;
    mDistLoc:   Point;
    mDistBeg:   Point2D;
    mDistLast:  Point2D;
    mDistKind:  Integer;
    hMDistStr:  String80;
    vMDistStr:  String80;
    pxlPt:      Point;

(* ********************************************************************* *)

(* Scrolling Commands *)

(* ********************************************************************* *)
(*$S Core*)
    procedure CkBounds(max: Integer; VAR num: Integer);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        if num<0 then
            num := 0
        else if num>max then
            num := max;
    end;

(* ********************************************************************* *)
(*$S Ring*)
    procedure  CkScrollPt;
    var maxH,maxV: Integer;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        (* Called from ScrollTo, SetGEBoxes to make sure scrollPt
           doesn't leave excessive gray exposed at botRight.
           Returns TRUE if scrollPt is altered.
        *)
        with wrkLBox do begin
            maxH := CMax(0,curDoc.atr.dwgSz.h-(right-left));
            maxV := CMax(0,curDoc.atr.dwgSz.v-(bottom-top));
        end;
        CkBounds(maxH,curDoc.atr.scrollPt.h);
        CkBounds(maxV,curDoc.atr.scrollPt.v);
    end; (* CkScrollPt *)

(* ********************************************************************* *)
(*$S Ring*)
    procedure GetDirScroll(var dir: Integer; var sb: THSb);
    var sameDir: Boolean;
        hDir,vDir,cThumb: Integer;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        hDir := iDirCtH;
        vDir := iDirCtV;
        (* get possible h,v scroll dir *)
        if wldPt.h<wrkLBox.left then
            hDir := iDirLft
        else if wldPt.h>wrkLBox.right then
            hDir := iDirRgt;
        if wldPt.v<wrkLBox.top then
            vDir := iDirTop
        else if wldPt.v>wrkLBox.bottom then
            vDir := iDirBot;
        (* decide whether to go h or v *)
        if hDir<>iDirCtH then begin
            dir := hDir;
            if vDir<>iDirCtV then begin
                (* we can go either h or v; look at lastDir,cThumb to decide *)
                cThumb := DwgThumbPos(lastDir);
                if (lastDir=iDirLft) OR (lastDir=iDirTop) then
                    sameDir := cThumb>0
                else
                    sameDir := cThumb<1000;
                if (lastDir=iDirLft) OR (lastDir=iDirRgt) then begin
                    if sameDir then
                        dir := hDir
                    else
                        dir := vDir
                end else begin
                    if sameDir then
                        dir := vDir
                    else
                        dir := hDir
                end;
            end;
        end else
            dir := vDir;
        (* get the proper sBox *)
        if (lastDir=iDirLft) OR (lastDir=iDirRgt) then
            sb := hSBox
        else
            sb := vSBox;
    end; (* GetDirScroll *)

(* ********************************************************************* *)
(*$S Core*)
    function  OkToScroll(dir: Integer; var cThumb: Integer): Boolean;
    var ok: Boolean;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        ok := FALSE;
        cThumb := DwgThumbPos(dir);
        case dir of
            iDirLft:
                ok := (cThumb>0) AND (wrkLBox.left>0);
            iDirTop:
                ok := (cThumb>0) AND (wrkLBox.top>0);
            iDirRgt:
                ok := (cThumb<=1000) AND (wrkLBox.right<=sizeLBox.right);
            iDirBot:
                ok := (cThumb<=1000) AND (wrkLBox.bottom<=sizeLBox.bottom);
        end; (* case *)
        OkToScroll := ok;
    end; (* OkToScroll *)

(* ********************************************************************* *)

    function  NeedScroll(* : Boolean *);
    var cThumb,dir: Integer;
        sb: THsb;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        NeedScroll := FALSE;
        if not PtInRect(wldPt,wrkLBox) then begin
            GetDirScroll(dir,sb);
            if OkToScroll(dir,cThumb) then
                NeedScroll := TRUE;
        end;
    end; (* NeedScroll *)

(* ********************************************************************* *)

    procedure AutoScroll(* showSel: Boolean *);
    var dir:     Integer;
        sb:      THSb;
        cThumb:  Integer;
        thisPen: PenState;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        if NeedScroll then begin
            penPats[iPenWhite] := white;
            HideDist;
            autoSFlag := TRUE;
            GetDirScroll(dir,sb);
            cThumb := CThumbPos(sb);
            GetPenState(thisPen);
            ScrollCmd(dir,cThumb,showSel);
            SetPenState(thisPen);
            SetWrkOrigin;
            GEClipRect(dwgLBox);
            autoSFlag := FALSE;
            penPats[iPenWhite] := black;
        end;
    end; (* AutoScroll *)

(* ********************************************************************* *)

    function  ThumbPos(* loc,range: Integer): Integer *);
    var l,r: Real;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        l := loc;
        r := range;
        ThumbPos := Round(1000*(l/r));
    end; (* ThumbPos *)

(* ********************************************************************* *)

    function  DwgThumbPos(* dir: Integer): Integer *);
    var loc,range,pos: Integer;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        with curDoc.atr,wrkLBox do begin
            (* get range *)
            (* get thumb position *)
            if (dir=iDirLft) OR (dir=iDirRgt) then begin
                range := dwgSz.h-(right-left);
                loc   := scrollPt.h;
            end else begin
                range := dwgSz.v-(bottom-top);
                loc   := scrollPt.v;
            end;
        end;
        pos := ThumbPos(loc,range);
        DwgThumbPos := pos;

        if traceCmd then
            Writeln('    DwgThumbPos(',dir:2,')= ',pos:2);
    end; (* DwgThumbPos *)

(* ********************************************************************* *)

    procedure GetPtScroll(dir: Integer; var cThumb: Integer);
    var xRange,yRange: Real;
        dh,dv: Integer;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        (* get the scrollPt that corresponds to cThumb *)
        (* make sure cThumb is within bounds *)
        CkBounds(1000,cThumb);
        (* get range *)
        with wrkLBox do begin
            dh := right-left;
            dv := bottom-top;
            if dh>curDoc.atr.dwgSz.h then
                xRange := 0
            else
                xRange := curDoc.atr.dwgSz.h-dh+hScroll;
            if dv>curDoc.atr.dwgSz.v then
                yRange := 0
            else
                yRange := curDoc.atr.dwgSz.v-dv+vScroll;
        end;
        (* get new curDoc.atr.scrollPt, round to nearest scroll *)
        if dir in [iDirLft,iDirRgt] then
            curDoc.atr.scrollPt.h := (Round(xRange*cThumb/1000) div hScroll)*hScroll
        else
            curDoc.atr.scrollPt.v := (Round(yRange*cThumb/1000) div vScroll)*vScroll;
    end; (* GetPtScroll *)

(* ********************************************************************* *)

    procedure ShowNewPage(dir: Integer);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        (* reset coords *)
        SetLclBox;
        SetWrkOrigin;
        (* redraw *)
        if curDoc.atr.rlrOn then begin
            if dir in [iDirLft,iDirRgt] then
                RedrawTopRlr
            else
                RedrawLftRlr;
        end;
        GEClipRect(wrkLBox);
        ReShow(wrkLBox);
    end; (* ShowNewPage *)

(* ********************************************************************* *)

    procedure MvGEThumb(dir,pos: Integer);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        SetFldrOrigin;
        if (dir=iDirLft) or (dir=iDirRgt) then
            SetUpMvThumb(hSBox)
        else
            SetUpMvThumb(vSBox);
        if pos<0 then
            pos := 0
        else if pos>1000 then
            pos := 1000;
        MoveThumb(pos);
        PenMode(patXor);
    end; (* MvGEThumb *)

(* ********************************************************************* *)

    procedure ShowRect(r: Rect; showSelect,showHiLite,ersFlag: Boolean); FORWARD;

(* ********************************************************************* *)

    procedure ScrollCmd(* dir,cThumb: Integer; showSel: Boolean *);
    var thisRect,uRect: Rect;
        updateRgn:  RgnHandle;
        dPt: Point;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        if traceCmd then with curDoc.fldr^.portRect do
            Writeln('ScrollCmd(',dir:2,',',cThumb:2,')');

        if OkToScroll(dir,cThumb) then with curDoc.atr do begin
            lastDir := dir;
            (* turn off rlrHairs *)
            ShowRlrHrs(gridPt,OFF);
            (* get scroll direction *)
            with dirFactor[dir] do begin
                dPt.h := hScroll*h;
                dPt.v := vScroll*v;
            end;
            (* make sure we don't scroll past topLeft *)
            GEUnMapPt(dPt);
            with scrollPt do
                AddPt(dPt,scrollPt);
            if scrollPt.h<0 then begin
                dPt.h := dPt.h-scrollPt.h;
                scrollPt.h := 0
            end;
            if scrollPt.v<0 then begin
                dPt.v := dPt.v-scrollPt.v;
                scrollPt.v :=0;
            end;
            GeMapPt(dPt);
            (* get proper rect to scroll *)
            thisRect := wrkBox;
            if rlrOn then with thisRect do begin
                if (dir=iDirLft) OR (dir=iDirRgt) then
                    top := top-rlrSize.v
                else
                    left := left-rlrSize.h;
            end;
            (* scroll the drawing *)
            SetFldrOrigin;
            ClipRect(thisRect);
            updateRgn := NewRgn;
            ScrollRect(thisRect,-dPt.h,-dPt.v,updateRgn);
            (* remember update BBox and free rgn *)
            uRect := updateRgn^^.rgnBBox;
            DisposeRgn(updateRgn);
            (* offset uRect by scroll amount *)
            {OffsetRect(uRect,dPt.h,dPt.v);}
            (* get wld coords of uRect *)
            OffsetRect(uRect,-wrkBox.left,-wrkBox.top);
            GEUnMapRect(uRect);
            OffsetRect(uRect,scrollPt.h,scrollPt.v);

            SetLclBox;
            SetWrkOrigin;
            (* show the proper ruler *)
            if rlrOn then begin
                (* ruler scroll rects *)
                if (dir=iDirLft) or (dir=iDirRgt) then begin
                    thisRect := topRlrLBox;
                    if dir=iDirLft then
                        thisRect.right := thisRect.left+hScroll
                    else
                        thisRect.left := thisRect.right-hScroll;
                end else begin
                    thisRect := lftRlrLBox;
                    if dir=iDirTop then
                        thisRect.bottom := thisRect.top+hScroll
                    else
                        thisRect.top := thisRect.bottom-hScroll;
                end;
                ShowRuler(thisRect);
            end;
            (* show the rect uncovered by scrolling *)
            GEClipRect(wrkLBox);
            ShowRect(uRect,showSel,showSel,TRUE);
            (* paint thumb *)
            MvGEThumb(dir,DwgThumbPos(dir));
        end;
    end; (* ScrollCmd *)

(* ********************************************************************* *)

    procedure PageCmd(* dir: Integer; dThumb: Integer *);
    var cThumb,wThumb,range,loc: Integer;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        if traceCmd then
            Writeln('PageCmd(',dir:2,',',cThumb:2,')');

        (* if distance to cThumb is less than half wrkBox, *)
        (* then page to dThumb else page half wrkBox *)
        if OkToScroll(dir,cThumb) then begin
            (* get half wrkBox in thumb coordinates *)
            with wrkLBox do
            if (dir=iDirLft) OR (dir=iDirRgt) then begin
                range := curDoc.atr.dwgSz.h;
                loc := (right-left){ Div 2};
            end else begin
                range := curDoc.atr.dwgSz.v;
                loc := (bottom-top){ Div 2};
            end;
            wThumb := ThumbPos(loc,range);
            if (dir=iDirLft) OR (dir=iDirTop) then
                wThumb := -wThumb;
            (* get new curDoc.atr.scrollPt from cThumb, checking bounds of cThumb *)
            cThumb := cThumb+wThumb;
            GetPtScroll(dir,cThumb);
            (* paint thumb *)
            MvGEThumb(dir,DwgThumbPos(dir));
            (* page dwg *)
            ShowNewPage(dir);
        end;
    end; (* PageCmd *)

(* ********************************************************************* *)

    procedure ThumbCmd(* dir: Integer; cThumb: Integer *);
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        if traceCmd then
            Writeln('ThumbCmd(',dir:2,',',cThumb:2,')');

        (* get new curDoc.atr.scrollPt from cThumb *)
        GetPtScroll(dir,cThumb);
        ShowNewPage(dir);
    end; (* ThumbCmd *)

(* ********************************************************************* *)
(*$S Ring*)
    procedure ScrollTo(* h,v: Integer *);
    var f: Boolean;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        (* scroll the dwg so that h,v is in center *)
        with wrkLBox do
        SetPt(curDoc.atr.scrollPt,h-(right-left) Div 2,v-(bottom-top) Div 2);
        (* check curDoc.atr.scrollPt topLeft bounds *)
        CkScrollPt;
        (* reset coords *)
        SetGEBox(TRUE);
        SetWrkOrigin;
        (* redraw *)
        if curDoc.atr.rlrOn then
            RedrawRlr;
        ReShow(wrkLBox);
        (* paint thumb *)
        MvGEThumb(iDirLft,DwgThumbPos(iDirLft));
        MvGEThumb(iDirTop,DwgThumbPos(iDirTop));
        DrwGESBars(curDoc.fldr^.portRect);
    end; (* ScrollTo *)

(* ********************************************************************* *)
(*$S Ring*)
    procedure ScrollObj(* obj: TObjPtr *);
    var scrollPt: Point;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        with obj^.bBox do
        if ((right-left)>(dwgLBox.right-dwgLBox.left)) and
           ((bottom-top)>(dwgLBox.bottom-dwgLBox.top)) then
            scrollPt := topLeft
        else
            SetPt(scrollPt,(right+left) Div 2,(top+bottom) Div 2);
        ScrollTo(scrollPt.h,scrollPt.v);
    end; (* ScrollObj *)

(* ********************************************************************* *)

(* Ruler Drawing Routines *)

(* ********************************************************************* *)
(*$S Core*)
    procedure SetRlrClip(r: Rect);
    var fldrR: Rect;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        fldrR := curDoc.fldr^.portRect;
        with fldrR do begin
            left := left+protoWidth;
            if fActive then with fldrR do begin
                right := right-(dhSBox-1);
                bottom := bottom-(dvSBox-1);
            end;
        end;
        if SectRect(r,fldrR,r) then
            ClipRect(r)
        else
            ClipRect(fldrR);
    end; (* SetRlrClip *)

(* ********************************************************************* *)

    procedure MakeRlr(rlrRect: Rect; hDots,vDots: Real; inc,numDiv: Integer);
    const
        cMaxDiv = 16;
    var x,dx,y,dy,del: Real;
        thisRect: Rect;
        nUnits: Integer;
        rlrNum: Integer;
        minDelta: Integer;
        numStr: String6;

    (* ***************************************************************** *)

        procedure RlrTick(dh,dv: Integer);
        begin
        {$IFC sampcalls} ALogCall; {$ENDC}
            x := x+dx;
            y := y+dy;
            MoveTo(Round(x)-dh,Round(y)-dv);
            Line(dh,dv);
        end; (* RlrTick *)

    (* ***************************************************************** *)

        procedure WrtRlrNum(dh,dv,sh,sv: Integer);
        begin
        {$IFC sampcalls} ALogCall; {$ENDC}
            MoveTo(Round(x)-dh,Round(y)-dv);
            Line(dh,dv);
            MoveTo(Round(x)-sh,Round(y)-sv);
            DrawString(numStr);
            MoveTo(Round(x),Round(y));
        end; (* WrtRlrNum *)

    (* ***************************************************************** *)

        procedure GetRlrInfo(rStart,rLen: Integer; rUnitLen: Real;
                        var numUnits: Integer; var delta: Real);
        var rUnits,rUnitStart: Real;
        begin
        {$IFC sampcalls} ALogCall; {$ENDC}
            {
            if traceCmd then
                writeln('GetRlrInfo(',rStart:2,',',rLen:2,',',rUnitLen:4:2,')');
            }
            (* number of units in entire ruler bar *)
            numUnits := Round(0.5+rLen/rUnitLen) + 1;
            (* length of ruler sub units *)
            delta := rUnitLen/numDiv;
            (* fractional number of ruler units to zero point *)
            rUnits := rStart/rUnitLen;
            (* starting ruler unit number outside of ruler bar *)
            rlrNum := Round(rUnits-0.5);
            (* start loc of ruler unit straddling ruler bar start *)
            rUnitStart := rUnitLen*rlrNum;
            (* distance from start ruler unit to ruler bar *)
            del := Abs(rUnitStart-rStart);
            {
            if traceCmd then begin
                writeln('    numUnits=  ',numUnits:6,    ', delta= ',delta:6:2);
                writeln('    rStart=    ',rStart:6,      ', rLen=  ',rLen:6);
                writeln('    rUnits=    ',rUnits:6:2,    ', rlrNum=',rlrNum:6);
                writeln('    rUnitStart=',rUnitStart:6:2,', del=   ',del:6:2);
            end;
            }
        end; (* GetRlrInfo *)

    (* ********************************************************************* *)

        function  TickSize(tick: Integer): Integer;
        var size: Integer;
        begin
        {$IFC sampcalls} ALogCall; {$ENDC}
            if tick=numDiv then
                size := 1
            else if numDiv in [2,4,8,16] then begin
                tick := tick*16 Div numDiv;
                if tick=8 then
                    size := 4
                else if (tick Mod 4)=0 then
                    size := 3
                else if (tick Mod 2)=0 then
                    size := 2
                else
                    size := 1;
            end else if numDiv in [3,6,12] then begin
                tick := tick*12 Div numDiv;
                if tick=6 then
                    size := 4
                else if (tick Mod 3)=0 then
                    size := 3
                else
                    size := 1
            end else if numDiv in [1,5,10] then begin
                tick := tick*10 Div numDiv;
                if tick=5 then
                    size := 3
                else
                    size := 1
            end;
            TickSize := size;
        end; (* TickSize *)

    (* ***************************************************************** *)

        procedure MakeTopRlr(thisRect: Rect; dots: Real);
        var i,j: Integer;
        begin
        {$IFC sampcalls} ALogCall; {$ENDC}
            {
            if traceCmd then
                writeln('MakeTopRlr(',dots:4:2,')');
            }
            SetRlrClip(thisRect);
            with thisRect,curDoc.atr do begin
                GetRlrInfo(left-rlrZero[rlrUnit,rlrKind].h,right-left,dots,nUnits,dx);
                dy := 0;
                y := topRlrLBox.bottom;
                x := left-del;
            end;
            {
            if traceCmd then
                writeln('    x=',x:6:2,', y=',y:6:2);
            }
            IntToStr(Abs(rlrNum)*inc,numStr);
            WrtRlrNum(0,rlrSize.v,StringWidth(numStr),7);
            for i := 1 to nUnits do begin
                for j := 1 to numDiv do
                    RlrTick(0,hRlrTicks[TickSize(j)]);
                IntToStr(Abs(rlrNum+i)*inc,numStr);
                WrtRlrNum(0,rlrSize.v,StringWidth(numStr),7);
            end;
        end; (* MakeTopRlr *)

    (* ***************************************************************** *)

        procedure MakeLftRlr(thisRect: Rect; dots: Real);
        var i,j: Integer;
        begin
        {$IFC sampcalls} ALogCall; {$ENDC}
            {
            if traceCmd then
                writeln('MakeLftRlr(',dots:4:2,')');
            }
            SetRlrClip(thisRect);
            with thisRect,curDoc.atr do begin
                GetRlrInfo(top-rlrZero[rlrUnit,rlrKind].v,bottom-top,dots,nUnits,dy);
                dx := 0;
                x := lftRlrLBox.right;
                y := top-del;
            end;
            {
            if traceCmd then
                writeln('    x=',x:6:2,', y=',y:6:2);
            }
            IntToStr(Abs(rlrNum)*inc,numStr);
            WrtRlrNum(rlrSize.h,0,20,1);
            for i := 1 to nUnits do begin
                for j := 1 to numDiv do
                    RlrTick(vRlrTicks[TickSize(j)],0);
                IntToStr(Abs(rlrNum+i)*inc,numStr);
                WrtRlrNum(rlrSize.h,0,20,1);
            end;
        end; (* MakeLftRlr *)

    (* ***************************************************************** *)

    begin
        {$IFC sampcalls} ALogCall; {$ENDC} (* MakeRlr *)
        {
        if traceCmd then
            writeln('MakeRlr(',hDots:6:2,vDots:6:2,inc:3,numDiv:3,')');
        }
        minDelta := Round(xInchDots/cMaxDiv);
        while Round(hDots/numDiv)<minDelta do
            numDiv := numDiv div 2;
        if SectRect(topRlrLBox,rlrRect,thisRect) then
            MakeTopRlr(thisRect,hDots);
        if SectRect(lftRlrLBox,rlrRect,thisRect) then
            MakeLftRlr(thisRect,vDots);
    end; (* MakeRlr *)

(* ********************************************************************* *)

    procedure ShowRuler(* rRect: Rect *);
    var thisPen: PenState;
        clipR:   Rect;
        d2D:     Point2D;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        if traceCmd then begin
            WlnRect('ShowRuler:',rRect);
            with curDoc.atr,rlrZero[rlrUnit,rlrKind] do
            writeln('    zro: ',h:2,',',v:2);
        end;

        (* set up *)
        GetPenState(thisPen);
        ShowRlrHrs(gridPt,OFF);
        PenNormal;
        TextFont(p15tile);
        TextFace([]);
        TextMode(srcOr);
        SetRlrClip(curDoc.fldr^.portRect);
        (* ruler border *)
        MoveTo(lftRlrLBox.left,topRlrLBox.bottom-1);
        LineTo(topRlrLBox.right,topRlrLBox.bottom-1);
        MoveTo(lftRlrLBox.right-1,topRlrLBox.top);
        LineTo(lftRlrLBox.right-1,lftRlrLBox.bottom);
        (* draw ruler *)
        with curDoc.atr.rlrFmts[curDoc.atr.rlrUnit,curDoc.atr.rlrKind] do begin
            if curDoc.atr.rlrUnit=iUnitMtrc then
                SetPt2D(d2D,iSpc*xCMDots,iSpc*yCMDots)
            else
                SetPt2D(d2D,rlrInfo[iSpc].spc*xInchDots,
                            rlrInfo[iSpc].spc*yInchDots);
            MakeRlr(rRect,d2D.x,d2D.y,rlrInfo[iInc].inc,rlrInfo[iDvs].dvs);
        end;
        (* restore *)
        SetPenState(thisPen);
        TextMode(srcXor);
    end; (* ShowRuler *)

(* ********************************************************************* *)

    procedure RedrawTopRlr;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        ShowRlrHrs(wrkPt,Off);
        SetRlrClip(topRlrLBox);
        FillRect(topRlrLBox,white);
        ShowRuler(topRlrLBox);
    end; (* RedrawTopRlr *)

(* ********************************************************************* *)

    procedure RedrawLftRlr;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        ShowRlrHrs(wrkPt,Off);
        SetRlrClip(lftRlrLBox);
        FillRect(lftRlrLBox,white);
        ShowRuler(lftRlrLBox);
    end; (* RedrawLftRlr *)

(* ********************************************************************* *)

    procedure RedrawRlr;
    var thisRect: Rect;
    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        if traceCmd then
            writeln('ReDrawRuler(',curDoc.atr.rlrKind:2,')');

        SetRlrClip(zroRlrLBox);
        FillRect(zroRlrLBox,white);
        RedrawTopRlr;
        RedrawLftRlr;
    end; (* RedrawRlr *)

(* ********************************************************************* *)

    procedure ShowRlrHrs(* lclPt: Point; display: Boolean *);
    var thisPen: PenState;

    (* ***************************************************************** *)

        procedure DrwRlrHrs;
        begin
        {$IFC sampcalls} ALogCall; {$ENDC}
            SetRlrClip(lftRlrLBox);
            with lftRlrLBox do begin
                MoveTo(left,rlrPt.v);
                LineTo(right-1,rlrPt.v);
            end;
            SetRlrClip(topRlrLBox);
            with topRlrLBox do begin
                MoveTo(rlrPt.h,top);
                LineTo(rlrPt.h,bottom-1);
            end;
        end; (* DrwRlrHrs *)

    (* ***************************************************************** *)

    begin
        {$IFC sampcalls} ALogCall; {$ENDC}
        GetPenState(thisPen);
        PenMode(notPatXor);
        PenPat(gray);
        PenSize(1,1);
        (* erase old *)
        if rStateOn then begin
            DrwRlrHrs;
            rStateOn := false;
        end;
        (* draw new *)
        if display and PtInRect(lclPt,dwgLBox) then begin
            rlrPt := lclPt;
            DrwRlrHrs;
            rStateOn := true;
        end;
        (* restore *)
        SetPenState(thisPen);
        GEClipRect(dwgLBox);
    end; (* ShowRlrHrs *)

(*$I ApLd/SC1.text*)
(*$I ApLd/SC2.text*)

END. (* unit *)

ÿ