{------------------------------------------------------------------------------}
{                  Copyright 1983,1984, Apple Computer Inc.                    }
{                                                                              }
{                                                                              }
{   Beginning of the world initialization.                                     }
{                                                                              }
{                                                                              }
{------------------------------------------------------------------------------}


{$S SgPeInit}

Procedure InitScroll;
{ Inits all the scroll and grow box information for Pert and Gantt charts }

Const
   BotRt = 100;     { location of bottom right of scroll bar      }

var
   Dummy: point;    { dummy upper left point for the scroll boxes }
   I:       integer;{ once for Pert and once for gantt            }

Begin { InitScroll }

   for I := Pert to numCharts do begin

      { init the scroll bar list }
      InitSBList(sbList[I], theHeap);
      SetPt(Dummy, 0, 0);

      { Get two new scroll bars. This order puts horiz first, vert second }
      hsbV[I] :=
         SbCreate(SBList[I], hsbNil, tyvhV, Dummy, BotRt);
      hsbH[I] :=
         SbCreate(SBList[I], hsbNil, tyvhH, Dummy, BotRt);

      { Add in the appropriate icons and the thumb positions }
      if (i = Pert) OR (I > TaskGantt) then begin
         SetSBIcons( hsbV[I], [ iconArwA, iconThumb, iconArwB, iconPagA, iconPagB]);
         SetSBIcons( hsbH[I], [ iconArwA, iconThumb, iconArwB, iconPagA, iconPagB]);
      end
      else begin
         SetSBIcons( hsbV[I], [ iconArwA, iconThumb, iconArwB, iconPagA, iconPagB]);
         SetSBIcons( hsbH[I], [ iconArwA, iconThumb, iconArwB, iconPagA, iconPagB, iconSkewer]);
      end;
      SetThumb(hsbV[I], 0);
      SetThumb(hsbH[I], 0);
   end;

   { and then the scrap }
   InitSBList(SBListScrap, theHeap);
   SetPt(Dummy, 0,0);

   { Get two new scroll bars. This order puts horiz first, vert second }
   hsbVScrap :=
      SbCreate(SBListScrap, hsbNil, tyvhV, Dummy, BotRt);
   hsbHScrap :=
      SbCreate(SBListScrap, hsbNil, tyvhH, Dummy, BotRt);

   SetSBIcons( hsbVScrap, []);
   SetSBIcons( hsbHScrap, []);

End; { InitScroll }


{------------------------------------------------------------------------------}


PROCEDURE LastWishes(environPtr : P_env_blk; excepDataPtr : P_ex_data);
{ The Sys_Terminate exception handler.  Gets called when the program terminates
  normally (i.e., last statement executed) or abnormally (e.g., bus error)    }

var
   err : INTEGER;
   failReason : FReason;

begin { LastWishes }

   {$IFC PEDEBUG}
   writeln('LastWishes');
   {$ENDC}
   ImDying;
   IF NOT toldToDie then begin  { Must have crashed }
      {$IFC PEDEBUG}
      writeln('   Abnormal death.');
      {$ENDC}
      IF docsOpen > 0 THEN
         IF NOT CloseTheDoc(fcSuspend,failReason) THEN

         { Bad News!  We crashed and can't seem to suspend the document.
           The O/S will set a flag in the disk file(s) indicating that they were
           not closed when the process terminated.  The application should check
           this flag whenever a document is opened and try to recover the
           document. }
          ;
   end;
   {$IFC PEDEBUG}
   writeln('Halting');
   {$ENDC}
end; { LastWishes }


{------------------------------------------------------------------------------}


Procedure FailedInit;
{ tells filer that initialization failed  }

begin { FailedInit }

   {$IFC PEDEBUG}
   writeln('error on alert initialization ',alerterror);
   {$ENDC}
   Halt;
end; { FailedInit }


{------------------------------------------------------------------------------}


Procedure FailedAlert;
{ This procedure called on an error during an alert manager operation }

begin { FailedAlert }

   { output the swan song and terminate gracefully }
   StopAlert(peAlert, 2 {Swan Song});
   Halt;
end; { FailedAlert }


{------------------------------------------------------------------------------}


Procedure GrafErr( error: integer);
{ called on quickdraw error  }

begin { GrafErr }

   {$IFC PEDEBUG}
   writeln('entered GrafErr ', error);
   {$ENDC}
   Halt;
end; { GrafErr }


{------------------------------------------------------------------------------}


Procedure Initialize;
{ Master Controller Program (MCP) for the beginning of the world init for Pert. }

const
   heapldsn    = 5;         { my ldsn number                      }
   startLen    = 20;        { starting maxlen of edit fields      }
   growLen     = 20;        { the growth increment on fields      }
   padding     = 2;         { a little padding in my fields       }
   ifmts       = 1;         { initial number of formats           }
   gfmts       = 0;         { no growth in number of formats      }
   protect     = false;     { these fields not protected          }
   FEWordDef   = 6;         { field editor word def alert number  }

var
   heapFirst: LongInt;      { the first available address in heap }
   cHeapSize: LongInt;      { initial amount of heap requested    }
   SegName:   PathName;     { the heap's pathname of course       }
   error:     Integer;      { the OS error number returned        }
   i,j:       integer;      { loop counters                       }
   refNum:    integer;      { phrase file's ref num               }
   nilrect:   rect;         { for initialization of fields        }
   exceptName:T_Ex_Name;    { termination exception handler name  }
   MenuFile:  text;         { menu file for reading in menus      }
   mypath:    pathname;
   Proc_Info: ProcInfoRec;
   theTriple: str255;
   cErr:      TC;
   Copyright: String[35];
   marketString: String30;

{------------------------------------------------------------------------------}


procedure GetBuzzwords;
var i:         Integer;
    buzzStr:   str255;
    monthPos:  integer;
    YearPos:   integer;

begin

   { save off the buzz words }
   GetItem(Menu[lastBuzz], 1, @buzzStr);
   ASAP := buzzStr;
   GetItem(Menu[lastBuzz], 2, @buzzStr);
   toolMM_DD_YY := buzzStr;
   GetItem(Menu[lastBuzz], 3, @buzzStr);
   Title := buzzStr;
   GetItem(Menu[lastBuzz], 4, @buzzStr);
   Subtitle := buzzStr;
   GetItem(Menu[lastBuzz], 5, @buzzStr);
   StartStr := buzzStr;
   GetItem(Menu[lastBuzz], 6, @buzzStr);
   EndStr := buzzStr;
   GetItem(Menu[lastBuzz], 7, @buzzStr);
   ToolDateFormat := buzzStr;

end;


{------------------------------------------------------------------------------}


procedure ReadSMenus;
{ Reads in the menu items for the scrap folder }

var i: Integer;

begin { ReadSMenus }

   { read in all menu items for pert and gantt folders }
   for i := firstScrapMenu to lastScrapmenu do begin
      ReadMenu(menuFile,ScrapMenu[i]);
   end; { for }
end; { ReadSMenus }


{------------------------------------------------------------------------------}



Procedure ReadPCalendarForm;

var formsize: integer;
    i: integer;
    thefield: tEdFldHnd;
    localCkBox: tCkBoxHnd;

begin { ReadPCalendarForm }
{$R-}

   { read in calendar attributes edit form }
   formsize := SizeOf(TEdFormInfo) + SizeOf(TCkBoxPtr) +
      (lastCalEd + 1) * SizeOf(TEdFldPtr);
   PCalendarForm := Pointer(Ord(HAllocate(theHeap,formsize)));

   { pass a local handle into ReadCkBox because it allocates }
   ReadCkBox(MenuFile, localCkBox {PCalendarForm^^.ckBox});
   PCalendarForm^^.ckBox := localCkBox;

   for i := 0 to lastCalEd do begin
      ReadEdFld(MenuFile,thefield);
      pCalendarForm^^.EdFldLst[i] := thefield;
   end;

   { initialize the calendar edit Form }
   with PCalendarForm^^ do begin
      { set up item 0 as the buttons }
      with ckBox^^ do begin
         drawProc := @ShowCkBtn;
         HittstFunc := @CkBtnHit;
      end; { with }

      CkBox^^.hasOnOff := false;
      numEdFld := lastCalEd;
      EditInProgress := false;
      edFld := 0;
      edItm := 0;
   end; { with }

   EdFormHeight(PCalendarForm);
{$R+}
end;  { ReadPCalendarForm }


{------------------------------------------------------------------------------}


Procedure ReadSchedForm;

var formsize: integer;
    i: integer;
    thefield: tEdFldHnd;
    myCkBoxHnd: TCkBoxHnd;
begin { ReadSchedForm }
{$R-}

   { read in sheduled date edit form }
   formsize := SizeOf(TEdFormInfo) + SizeOf(TCkBoxPtr) +
      (lastSchedEd + 1) * SizeOf(TEdFldPtr);
   SchedForm := Pointer(Ord(HAllocate(theHeap,formsize)));

   { Pass in a local handle since it allocates }
   ReadCkBox(MenuFile, myCkBoxHnd {SchedForm^^.ckBox});
   SchedForm^^.ckBox := myCkBoxHnd;

   for i := 0 to lastSchedEd do begin
      ReadEdFld(MenuFile,thefield);
      SchedForm^^.EdFldLst[i] := thefield;
   end;

   { initialize the calendar edit Form }
   with SchedForm^^ do begin
      { set up item 0 as the buttons }
      with ckBox^^ do begin
         drawProc := @ShowCkBtn;
         HittstFunc := @CkBtnHit;
      end; { with }

      CkBox^^.hasOnOff := false;
      numEdFld := lastSchedEd;
      EditInProgress := false;
      edFld := 0;
      edItm := 0;
   end; { with }

   EdFormHeight(SchedForm);
{$R+}
end;  { ReadSchedForm }


{------------------------------------------------------------------------------}


Procedure ReadUpdForm;

var formsize: integer;
    i: integer;
    thefield: tEdFldHnd;
    myCkBoxHnd: TCkBoxHnd;

begin { ReadUpdForm }
{$R-}

   { read in gantt update form }
   formsize := SizeOf(TEdFormInfo) + SizeOf(TCkBoxPtr) +
      (lastUpdEd + 1) * SizeOf(TEdFldPtr);
   UpdateForm := Pointer(Ord(HAllocate(theHeap,formsize)));

   { Pass in a local handle since it allocates }
   ReadCkBox(MenuFile, myCkBoxHnd {UpdateForm^^.ckBox});
   UpdateForm^^.ckBox := myCkBoxHnd;

   for i := 0 to lastUpdEd do begin
      ReadEdFld(MenuFile,thefield);
      UPdateForm^^.EdFldLst[i] := thefield;
   end;

   { initialize the gantt update edit Form }
   with UpdateForm^^ do begin
      { set up item 0 as the buttons }
      with ckBox^^ do begin
         drawProc := @ShowCkBtn;
         HittstFunc := @CkBtnHit;
      end; { with }

      CkBox^^.hasOnOff := false;
      numEdFld := lastUpdEd;
      EditInProgress := false;
      edFld := 0;
      edItm := 0;
   end; { with }

   EdFormHeight(UpdateForm);
{$R+}
end;  { ReadUpdForm }


{------------------------------------------------------------------------------}


procedure ReadGCkForm;
{ Reads in the gantt folder check forms }

var i:       Integer;
    formsize:Integer;
    myCkBoxHnd: TCkBoxHnd;

begin { ReadGCkForm }
{$R-}

   { read in grid size ckForm }
   formsize := SizeOf(TCkFormInfo) + (lastGAtrCk + 1) * SizeOf(TCkBoxPtr);
   GGridForm := Pointer(Ord(Hallocate(theHeap,formsize)));
   for i := 0 to lastGAtrCk do begin
      ReadCkBox(MenuFile, myCkBoxHnd {GGridForm^^.ckBoxLst[i]});
      GGridForm^^.ckBoxLst[i] := myCkBoxHnd;
   end;

   { initialize the  Grid size ckForm }
   with GGridForm^^ do begin
      { set up item 0 as the buttons }
      with ckBoxLst[0]^^ do begin
         drawProc := @ShowCkBtn;
         HittstFunc := @CkBtnHit;
      end; { with }

       numCkBox := lastGAtrCk;
       CkFormHeight(GGridForm);
       for i := 1 to numCkBox do with ckBoxLst[i]^^ do begin
           curCkItm := 1;
           if hasOnoff then begin
               curOnOff := 1;
               curCkItm := 1;
           end;
       end;
   end;
{$R+}
end; { ReadGCkForm }

{------------------------------------------------------------------------------}


procedure ReadDatesForm;

var i:       Integer;
    formsize:Integer;
    myCkBoxHnd: TCkBoxHnd;

begin { ReadDatesForm }
{$R-}

   formsize := SizeOf(TCkFormInfo) + (lastDatesCk + 1) * SizeOf(TCkBoxPtr);
   DatesForm := Pointer(Ord(Hallocate(theHeap,formsize)));
   for i := 0 to lastDatesCk do begin
      ReadCkBox(MenuFile, myCkBoxHnd {DatesForm^^.ckBoxLst[i]});
      DatesForm^^.ckBoxLst[i] := myCkBoxHnd;
   end;

   { initialize the  Grid size ckForm }
   with datesForm^^ do begin
      { set up item 0 as the buttons }
      with ckBoxLst[0]^^ do begin
         drawProc := @ShowCkBtn;
         HittstFunc := @CkBtnHit;
      end; { with }

       numCkBox := lastDatesCk;
       CkFormHeight(datesForm);
       for i := 1 to numCkBox do with ckBoxLst[i]^^ do begin
           HasOnOff := true;
           curOnOff := 2;
       end;
   end;
{$R+}
end; { ReadDatesForm }

{------------------------------------------------------------------------------}


procedure ReadDataForm;

var i:       Integer;
    formsize:Integer;
    myCkBoxHnd: TCkBoxHnd;

begin { ReadDataForm }
{$R-}

   formsize := SizeOf(TCkFormInfo) + (lastDataCk + 1) * SizeOf(TCkBoxPtr);
   DataForm := Pointer(Ord(Hallocate(theHeap,formsize)));
   for i := 0 to lastDataCk do begin
      ReadCkBox(MenuFile, myCkBoxHnd {DataForm^^.ckBoxLst[i]});
      DataForm^^.ckBoxLst[i] := myCkBoxHnd;
   end;

   { initialize the  Grid size ckForm }
   with dataForm^^ do begin
      { set up item 0 as the buttons }
      with ckBoxLst[0]^^ do begin
         drawProc := @ShowCkBtn;
         HittstFunc := @CkBtnHit;
      end; { with }

       numCkBox := lastDataCk;
       CkFormHeight(dataForm);
       for i := 1 to numCkBox do with ckBoxLst[i]^^ do begin
           HasOnOff := true;
           curOnOff := 2;
       end;
   end;
{$R+}
end; { ReadDataForm }




{------------------------------------------------------------------------------}




Begin { Initialize }

   copyRight := 'Copyright 1983,1984, Apple Computer Inc.';

   {$IFC PEDEBUG}
   writeln('LisaProject - 4/20/84');
   {$ENDC}

   { If any error conditions are encountered, call 'ImDying'. }
   { Set up the O/S terminate exception handler. }
   { This should be the first thing done in case of a crase in initialization. }
   exceptName := 'SYS_TERMINATE';
   Declare_Excep_Hdl(error, exceptName, @LastWishes);
   if error <> 0 then begin
      {$IFC PEDEBUG}
      writeln (My_Id:1,' Declar_Excep_Hdl error: ',error);
      {$ENDC}
      ImDying;                                { insures someone else is OS active }
      halt;
   end; { if }

   { init the basics }
  { Sched_Class(error, true);}
   InitGraf(@thePort);           { Init LisaGraf.  Should be done by each process }
   OpenWm;                       { give W/M addressability to shared heap }
   SetPort(deskPort);            { To assure a default grafport, to be done in OpenWM }
   SetHeap(wmHeap);              { use W/M heap for now }
   OpenScrap(error);             { open the scrap       }
   if error <> 0 then begin
      {$IFC PEDEBUG}
      writeln('***error on scrap open call ', error);
      {$ENDC}
      halt;
   end;

   { set up my state vars }
   ToldToDie    := false;
   PulledAsTool := false;    { set true on fcterminate, flags an abnormal death }
   docsOpen   := 0;              { no documents yet }

   {InitFp; 11/8/83 }

   { get my process's prefix }
   Info_Process(error,My_ID, Proc_Info);
   mypath := proc_Info.progPathName;
   delete(mypath,pos('}',mypath)+1,length(mypath));
   Segname := concat(mypath,'DataSeg');

   { create my data segment and the heap }
   PertHzRef  := 0;
   error   := 0;
   cHeapSize := 8000;  { 8K allows for init and small chart }
   Make_DataSeg( error, segname, cHeapSize,cHeapSize + DeltaDisc,
                 PertHzRef, heapFirst, heapLdsn, ds_private);
   if error<>0 then begin
      {$IFC PEDEBUG}
      writeln; writeln('MakeDataSeg returned with error ',error);
      {$ENDC}
      Halt;
   end;
   theHeap := HzInit(Pointer(heapFirst),Pointer(heapFirst+cHeapSize),
                  NIL,300,0, @MyGrowProc,NIL,NIL,NIL);

   { init menus }
   {InitMenus;  removed 3/19/84 }

   { window manager library inits }
   InitWmlSb;

   { init the cursor library before any alerts output }
   InitWMlCrs(error);

   { Init the Menus and Phrase }
   InitMP(theheap, mypath, @FailedAlert, 1, @Menu[1], pealert, Error);

   { load in alert segment }
   LdSgAl;

   { and the PrintShop }
   PrMgrInit;

   { init field editor }
   GetAlert(Pealert, FEWordDef, @theTriple);
   FEInit(theHeap, @theTriple, error);

   { Init the timeout and nbhd, these guys are mine }
   setRect(curnbrhood, 0, 0, 2, 2);
   curtimeout := 0;

   { init the scroll bars }
   InitScroll;

   { set up the field edit state vars }
   hfstate := @pfstate;
   pfstate := @fstate;
   setrect( nilrect, 0,0,1,1);
   htaskfield := CreateField( NilRect, Startlen, growLen, center, padding,
      ifmts, gfmts, p15Tile, protect );
   hData := htaskfield^^.curvalue;
   hdrawfield := createfield( NilRect, Startlen, growLen, center, padding,
      ifmts, gfmts, p15Tile, protect );


   { dialog box manager inits }
   InitDmgr(theHeap, sysText);


   { read in the Dialogs }
   mypath := concat(mypath,'dialogs.text');
   reset(menuFile,mypath);
   ReadSMenus;
   ReadPCalendarForm;
   ReadSchedForm;
   ReadUpdForm;
   ReadGCkForm;
   ReadDatesForm;
   ReadDataForm;
   Close(MenuFile);
   GetBuzzwords;

   { init some display options, head and tail of task list }
   Printing  := false;
   preview := false;
   showDates := false;
   showES    := true;
   showLS    := true;
   showEF    := true;
   showLF    := true;
   Head := nil;
   Tail := nil;
   names := nil;

   { set up the standard Gantt grid increments, 1, 2, 4, and 8 week  }
   GridIncrement[1] := 7;     { 1 week }
   GridIncrement[2] := 14;    { 2 week }
   GridIncrement[3] := 28;    { 4 week }
   GridIncrement[4] := 56;    { 8 week }

   { my state variables inited }
   SkipToUp := false;
   eventDebug:=FALSE;
   caretTime:=Time+50;
   msgon := false;
   PEModernFlag := True;


   { create the calendar }
   InitCalendar;

   myfolder := nil;
   activeChart := pert;

   drawingScrap := false;
   today := 0;

   { Init the cost information }
   InitCost;

   {$IFC PeDebug}
   DebugFlag := true;
   {$ENDC}

end; { Initialize }

ÿ