{----------------------------------------------------------------------------}
{                                                                            }
{                  Copyright 1983,1984, Apple Computer Inc.                  }
{                                                                            }
{----------------------------------------------------------------------------}


{ Comments on Segmentation:
     PMain contains the basics necessary to do event processing, scrolling,
     folder grow, general folder display, scroll bar display,

     Pert contains the basics of displaying and editing the pert chart,

     Gantt contains the basics of displaying and editing the gantt charts,

     SgPeInit contains the one time initialization code,

     SgPeFileio contains the file input/output code,

     and the blank segment contains dialog box stuff, the dialog box manager code plus
     code to handle menu commands that require dialog boxes, }



program PEMain;

USES
   {$U UnitStd    }  UnitStd,    { The basics }
   {$U UnitHz     }  UnitHz,
   {$U LibOs/SysCall    }  SysCall,

   {$U QuickDraw  }  QuickDraw,
   {$U FontMgr    }  FontMgr,    { Fonts & LisaGraf }
   {$U Storage    }  Storage,
   {$U grafUtil   }  GrafUtil,
   {$U FMgrUtil   }  FmgrUtil,

   {$U PmDecl     }  PmDecl,
   {$U Pmm        }  Pmm,

   {$U WM.Events  }  Events,     { The window manager }
   {$U WM.Folders }  Folders,
   {$U WM.Windows }  Windows,
   {$U WM.Menus   }  Menus,
   {$U wmlstd     }  WMlstd,
   {$U wmlsb      }  WMlsb,
   {$U wmlcrs     }  WMlcrs,
   {$U wmlgrow    }  WMlGrow,

   {$U PrStdInfo  }  PrStdInfo,
   {$U PrPublic   }  PrPublic,

   {$U AlertMgr   }  AlertMgr,
   {$U Dbenv      }  Dbenv,
   {$U scrap      }  Scrap,
   {$U fedec      }  fedec,
   {$U fld        }  fieldEdit,

{$SETC IsIntrinsic = True}
{$SETC WithUObject = False}
{$SETC UTProblems  = False}
{$SETC LibraryVersion = 30}
   {$U Uunivtext  }  UUniversaltext,

   {$U FilerComm  }  FilerComm,  { and the filer }

   {$U aplp/globals  }  PEGlobals,
   {$U aplp/DMgr     }  PEDMgr,
   {$U aplp/general  }  PEGeneral,
   {$U aplp/pert     }  PEPert,
   {$U aplp/cost     }  PECost,  { tables and cash flow }
   {$U aplp/gantt    }  PEGantt,
   {$U aplp/fileIO   }  PEFileIO,
   {$U aplp/menus    }  PEMenus;

   { U fplib         fplib; }

{$IFC fsymok}
{$D+}
{$ELSEC}
{$D-}
{$ENDC}
{$IFC fdbgok}
{$R+}
{$ELSEC}
{$R-}
{$ENDC}


{$S MainLoop}


{-------------------------------------------------------------------------------}


Procedure myDeselect(var error: boolean);
{ general deselect procedure, calls pertDeselect or Ganttdeselect }

begin { myDeselect }
   Case ActiveChart of
      Pert     : PertDeselect(error);
      RescGantt,
      TaskGantt: GanttDeselect(error);
      RescCost : RTblDeselect(error);
      TaskCost : TTblDeselect(error);
      CashFlow : FlowDeselect(error);
      TaskTable: AllDeselect(error);
   End;
end; { MyDeselect }


{-------------------------------------------------------------------------------}

Procedure myUp;
{ general up procedure }

begin { myUp }

   setport(myfolder);
   clipRect(myfolder^.portrect);

   Case ActiveChart of
      Pert     : PertUp;
      RescGantt,
      TaskGantt: GanttUp;
      RescCost : RTblUp;
      TaskCost : TTblUp;
      CashFlow : FlowUp;
      TaskTable: AllUp;
   End;
end; { myUp }


{-------------------------------------------------------------------------------}


Procedure myDown(where:point);
{ general down procedure when mouse down is in myfolder but not a down on a
  scroll bar or a down in myfolder when dialog box in active }

begin { MyDown }

   setport(myfolder);
   clipRect(myfolder^.portrect);

   Case ActiveChart of
      Pert     : PertDown(where);
      RescGantt,
      TaskGantt: GanttDown(where);
      RescCost : RTblDown(where);
      TaskCost : TTblDown(where);
      CashFlow : FlowDown(where);
      TaskTable: AllDown(where);
   End;
end; { MyDown }


{-------------------------------------------------------------------------------}


Procedure myKey(theEvent: eventRec);
{ general key procedure, no keys pass through
  unless in scale to fit mode. }

begin { MyKey }
   if scale[activechart].state = scalereal then begin
      setport(myfolder);
      with myfolder^ do
      setrectRgn( cliprgn, portrect.left, portrect.top,
      portrect.right - dhsbox, portrect.bottom - dvsbox);

   Case ActiveChart of
      Pert     : PertKey(theEvent);
      RescGantt,
      TaskGantt: GanttKey(theEvent);
      RescCost : RTblKey(theEvent);
      TaskCost : TTblKey(theEvent);
      CashFlow : FlowKey(theEvent);
      TaskTable: AllKey(theEvent);
   End;
   end { if }
   else StopAlert(pealert, 37 {No Edit While Zoomed});
end; { MyKey }


{-------------------------------------------------------------------------------}

{$S MainLoop}

procedure UpdateCursor;
{ Called during slow times when no event in the queue.  Monitors the
  position of the cursor and changes its shape according to present location.}

var MsePt:      point;    { the mouse location                       }
    myContents: rect;     { myfolder rect minus scroll bars          }
    Found:      boolean;  { found one, yes or no                     }
    HitTask:    boolean;
    perimeter:  boolean;  { hit on the perimeter of a task, yes or no}
    index:      integer;  { the title index for a hit on chart title }
    icrs:       TC;
    TaskFrom,
    TaskTo:     htask;

begin { UpdateCursor }

   { if dialog box is active then let it track the cursor }
   If MsgOn then begin
      SetPort(dialogFolder);
      GetMouse(MsePt);
      CallTrkProc(Msept,theDBox.TrkMseProc)
   end

   else begin
      SetPort(activeFolder);
      GetMouse(MsePt);
      if (MsePt.h = LastTrackedMse.h) and (Msept.v = LastTrackedMse.v)
      then {don't track again, cursor accurate}
      else begin

         { look for something to hit on, set cursor accordingly }
         LastTrackedMse := MsePt;
         icrs := icrsInactive;
         if not PtinRect(MsePt, activefolder^.portrect ) then {not in the portrect}
         else begin
            if (activeFolder = myfolder) and (myfolder <> nil) then begin
               found := false;
               with myfolder^.portrect do
               setrect(myContents, left, top, right - dhSbox, bottom - dvSbox);

               if ptInRect(msePt, myContents) then begin

                  { dont hit test at all in reduced modes }
                  if scale[activechart].state = scalereal then begin

                     if activeChart = Pert then begin
                        if HitTestPert( MsePt, true{tracking}, HitTask, perimeter,
                                        TaskFrom, taskTo  ) then begin
                           if HitTask then begin
                              if perimeter then {}
                              else icrs := icrsIBeam;
                           end { if }
                           else { hit an arrow };
                           lastTrackedMse := MsePt;
                        end { if }
                        else begin
                           { check for a title in pert }
                           TitleHit(msePt, activeChart, index, found);
                           if found then begin
                              icrs := icrsIBeam;
                              lastTrackedMse := MsePt;
                           end { if }
                           else icrs := icrsGECross;
                        end; { else }
                     end { if }
                     else begin
                        { check for a title in gantts and the tables }
                        TitleHit(msePt, activeChart, index, found);
                        if found then begin
                           icrs := icrsIBeam;
                           lastTrackedMse := MsePt;
                        end
                        else if (ActiveChart > 3) Then Begin
                           CellHit(msePt, activeChart, icrs, Found); { in aplp/cost }
                           If Found then LastTrackedMse := MsePt;
                        end;
                     end;
                  end; { if }
               end; { if }
            end; { if }
         end; { else }

         SetStdCursor(icrs);
      end; { else }
   end; { else }
end; { UpdateCursor }


{-------------------------------------------------------------------------------}


function SkipEvent: BOOLEAN;
{ This routine filters out events, currently used to filter ups. }

begin { SkipEvent }

  skipevent := false;
  if skipToUp then begin
     SkipEvent := true;
     if theEvent.what = buttonup then SkipToUp := false;
  end
  else  if Abort then
     SkipEvent := theEvent.what In [keydown,buttondown,buttonup];

end; { SkipEvent }


{-------------------------------------------------------------------------------}


Function CkScroll( folder: windowptr; where: point): Boolean;
{ Returns true iff the user mousedowned in one of the scroll boxes.
  Handles the monitoring of the moves also. }

var
   myhsbHit:   Thsb;          { scroll box that the user hit             }
   myIconHit:  TIcon;         { icon in the scroll box that the user hit }
   COldThumb,                 { old thumb position before scroll         }
   CNewThumb:  TC;            { new thumb position after scroll          }
   Paper:      rect;          { scaled paper rect                        }
   dh, dv, ds: integer;       { deltas for h, v, and thumb position      }
   SbWhere:    point;         { down point assuming port origin of 0,0   }


{-------------------------------------------------------------------------------}


Procedure CkArrow;
{ Handles the events after the user has downed on a scroll arrow. }

const
   ScrollH = 30;              { the fixed scroll in H for arrow scrolls  }
   ScrollV = 20;              { the fixed scroll in V for arrow scrolls  }

var
   screenH,                   { current screen size in H                 }
   screenV: integer;          { current screen size in V                 }
   oldorig,                   { the old origin point for port            }
   neworig: point;            { the new origin point for port            }

Begin { CkArrow }

   with folder^ do begin

      { hilight the selected arrow }
      PaintArw(myHsbHit, myIconHit, True);

      oldorig := MyOrigin;
      ScreenH := portrect.right - portrect.left - dhSbox;
      ScreenV := portrect.bottom - portrect.top - dvSbox;

      if ((myhsbHit=hsbH[activeChart])and(screenH >=(paper.right - paper.left)))
      or ((myhsbHit=hsbV[activeChart])and(screenV >=(paper.bottom - paper.top)))
      then { do not scroll if screen bigger than paper }

      else Repeat
         { clip to full port }
         ClipRect( portrect );

         if myhsbHit = hsbH[activechart] then begin
            { define the new origin, h and then v }
            if myIconHit = IconArwB then neworig.h :=
               CMin( oldorig.h + ScrollH, paper.botright.h - screenh)
            else neworig.h :=
               CMax( oldorig.h - ScrollH, paper.topleft.h);
            neworig.v := oldorig.v;

            { define the corresponding dh and dv values }
            dh := oldorig.h - neworig.h;
            dv := 0;

            { and the new thumb position }
            CNewThumb := trunc(1000.0 * (neworig.h - paper.topleft.h)/
               (paper.botright.h - paper.topleft.h - screenh));
         end
         else begin
            { define the new origin, v and then h }
            if myIconHit = IconArwB then neworig.v :=
               CMin( oldorig.v + ScrollV, paper.botright.v - screenv)
            else neworig.v :=
               CMax( oldorig.v - ScrollV, paper.topleft.v);
            neworig.h := oldorig.h;

            { define the corresponding dh and dv values }
            dv := oldorig.v - neworig.v;
            dh := 0;

            { and the new thumb position }
            CNewThumb := trunc(1000.0 * (neworig.v - paper.topleft.v)/
               (paper.botright.v - paper.topleft.v - screenv));
         end;

         { move the thumb there }
         SetupMvThumb( myHsbHit );
         MoveThumb(CNewThumb);

         DoScroll(dh,dv);

         { set up new old origin }
         oldorig := neworig;

         { reset origin after doscroll }
         setorigin(0,0);
         ClipRect( portrect);

      until (not Stilldown) or (CNewThumb = 0) or (CNewThumb = 1000);

      { unhighlight the arrow }
      PaintArw(myhsbHit, myIconHit, False);
   end; { with }
End; { CkArrow }


{-------------------------------------------------------------------------------}


Procedure CkThumb;
{ Handles the thumb scrolling with the window manager library's help. }

var
   screenH,                   { current screen size in H }
   screenV: integer;          { current screen size in V }

begin { CkThumb }

   with folder^ do begin

      { determine amount moved and corresponding new origin }
      if myhsbHit = hsbH[activeChart] then begin
         ScreenH := portrect.right - portrect.left - dhSbox;
         { scroll only if screen smaller than paper }
         if ScreenH < (paper.right - paper.left) then begin

            { move the thumb to desired position }
            DragThumb(myhsbHit,SbWhere,cNewThumb);
            SetThumb(myhsbHit, cNewThumb);

            dh := myOrigin.h - trunc(CNewThumb/1000.0 *(paper.right -
               paper.left - screenh) + paper.left);
            dv := 0;
            DoScroll( dh,dv);
         end;
      end
      else begin
         ScreenV := portrect.bottom - portrect.top - dvSbox;
         { again check that screen smaller than paper }
         if screenV < (paper.bottom - paper.top) then begin

            { move the thumb to desired position }
            DragThumb(myhsbHit,SbWhere,cNewThumb);
            SetThumb(myhsbHit, cNewThumb);

            dv := MyOrigin.v - trunc(CNewThumb/1000.0 *(paper.botright.v -
               paper.topleft.v - screenv) + paper.topleft.v);
            dh := 0;
            DoScroll( dh,dv);
         end;
      end;
   end; { with }
end; { CkThumb }


{-------------------------------------------------------------------------------}


Procedure CkPage;
{ Handles the events after the user has downed on the page icon area. }

var
   done:       boolean;       { button up on mouse, end scrolling }
   screenH:    integer;       { current screen size in H }
   screenV:    integer;       { current screen size in V }
   neworig:    point;         { new origin for port }
   oldorig:    point;         { old origin for port }
   dResc:      integer;       { scaled width of resource bar }
   dTime:      integer;       { scaled width of time line }

Begin { CkPage }

   { calc delta scroll for thumb, want to scroll one full screen of data }
   with folder^ do begin

      { hilight the selected page icon }
      PaintArw(myHsbHit, myIconHit, True);

      oldorig := myorigin;
      ScreenH := (portrect.right - portrect.left - dhSbox);
      ScreenV := (portrect.bottom - portrect.top - dvSbox);
      if (activechart = taskgantt) or (activechart = rescgantt) then begin
         scaleHInt(dresc, drescLine[activechart]);
         scaleVInt(dtime, dtimeLine);
      end
      else begin
         dResc := 0;
         dTime := 0;
      end;


      done := false;
      Repeat
         if myhsbHit = hsbH[activeChart] then begin

            if ScreenH >= (paper.right - paper.left) then done := true
            else begin

               { define the new origin, h and then v }
               if myIconHit = IconPagB
               then neworig.h := CMin(oldorig.h + screenH - dResc, paper.right - screenH)
               else neworig.h := CMax(oldorig.h - screenH + dResc, paper.left);
               neworig.v := oldorig.v;

               { define the corresponding dh and dv values }
               dh := oldorig.h - neworig.h;
               dv := 0;

               { and the new thumb position }
               CNewThumb := trunc(1000.0 * (neworig.h - paper.left)/(paper.right - paper.left - screenh ));
            end; { else }
         end { if }
         else begin

            if ScreenV >= (paper.bottom - paper.top) then done := true
            else begin
               { define the new origin, v and then h }
               if myIconHit = IconPagB
               then neworig.v := CMin( oldorig.v + screenV - dTime, paper.bottom  - screenv)
               else neworig.v := CMax( oldorig.v - ScreenV + dTime, paper.top);
               neworig.h := oldorig.h;

               { define the corresponding dh and dv values }
               dv := oldorig.v - neworig.v;
               dh := 0;

               { and the new thumb position }
               CNewThumb := trunc(1000.0 * (neworig.v - paper.top)/(paper.bottom - paper.top - screenv));
            end;
         end;

         if not done then begin
            { move the thumb, this will draw it in new position }
            { clip to full screen }
            setport(myfolder);
            setorigin(0,0);
            ClipRect( portrect);
            SetupMvThumb(myhsbHit);
            MoveThumb(CNewThumb);
            DoScroll(dh,dv);
            { set up new old origin }
            oldorig := neworig;
         end; { if }
      Until ( not stilldown );

      { clip to full screen and unhilight the selected page icon }
      setorigin(0,0);
      ClipRect( portrect);
      PaintArw(myHsbHit, myIconHit, False);

   End; { with }
End; { CkPage }


{-------------------------------------------------------------------------------}


Procedure CkSkewer;
{ handles a mouse down on the skewer }

var rLimits:    TR;        { the limits of skewer }
    PtSkewerUp: TPt;       { up point for skewer draw }
    dTime:      integer;   { scaled width of time line }

begin { CkSkewer }

   { set up skewer bounds }
   ScaleVInt( dTime, dtimeLine );
   with myfolder^.portrect do SetRect( rLimits, left, top + dTime, right - dhsbox, bottom - dvsbox);

   { draw the skewer to determine new resc offset }
   DragSkewer( myHsbHit, SbWhere, rLimits, PtSkewerUp);

   dRescLine[activechart] := CMin( dTitleH div 2, CMax(2, { make it at least two pixels }
   trunc((PtSkewerUp.h - myfolder^.portrect.left) *       { unscale the value           }
   1/(Scale[activechart].factor*PertFontInfo[activechart].actual))));

   { now redraw folder }
   SetPort(myfolder);
   setorigin(myorigin.h, myorigin.v);
   ClipRect(myfolder^.portrect);
   CreateGantt;
   ShowScroll(myfolder);
   showContents(myfolder);
end; { CkSkewer }


{-------------------------------------------------------------------------------}


Begin { CkScroll }

   if folder = scrapFolder then CkScroll := false
   else begin

      { which folder are we looking at, pert, gantt, or scrap ? }
      if folder = myfolder then begin

         { see if the user hit a scroll box, reset port so origin at 0,0 }
         setport(myfolder);
         setorigin(0,0);
         cliprect(myfolder^.portrect);
         setpt(SbWhere, where.h - myOrigin.h, where.v - myOrigin.v);

         If FSBHit(SBList[activeChart], SbWhere, myhsbHit, myIconHit)
         Then Begin

            { we got one, go handle it }
            CkScroll := True;

            { do an update event before the scroll }
            setport(myfolder);
            setorigin(myorigin.h, myorigin.v);
            cliprect(myfolder^.portrect);
            BeginUpdate(myfolder);
            if not EmptyRgn(myfolder^.visrgn) then begin
               showScroll(myfolder);
               showContents(myfolder);
            end;
            endUpdate(myfolder);
            setport(myfolder);
            setorigin(0,0);
            cliprect(myfolder^.portrect);

            { set up the scaled paper rect for this folder }
            ScalePaper( Paper, activeChart);
            pennormal;

            { check it out }
            Case myIconHit of

               IconThumb: CkThumb;

               IconArwA,
               IConArwB:  CkArrow;

               IconPagA,
               IconPagB:  CkPage;

               IconSkewer: CkSkewer;

            End; { Case }

            { filter out the next up event }
            SkipToUP := True;

         end { if }
         else CkScroll := false;
         setorigin(myorigin.h, myorigin.v);
         ClipRect(myfolder^.portrect);
      end
      else CkScroll := false;
   end; { else }
End; { CkScroll }


{-------------------------------------------------------------------------------}


Function CkGrow( folder: windowptr; where: point ): Boolean;
{ Returns True iff the user mousedowned in a grow box. As a side effect,
it handles all the events for the user dragging the grow box to resize the
folder. Follows example on p. 17 of the 11/6/81 document by Brad Silverberg
on the WM library routines }

var
   ptNewBR:   TPt;      { new lower right corner set by the user     }
   sbWhere:   point;    { where point adjusted to a origin of 0,0    }


{-------------------------------------------------------------------------------}


Procedure GrowFolder;
{ Handles the adjustment of the folder after a move of the grow box. }

var
   mySize:    point;     { pt representing the new size of the folder }
   rH, rV:    rect;      { new scroll bar rectangles                  }
   temprgn:   rgnhandle; { temporary region to define updated region  }
   rGrow:     rect;      { the grow box rectangle                     }
   newport:   rect;      { the grown/shrunk port rect                 }
   neworigin: point;    { the new origin for port                    }
   paper:     rect;

begin { GrowFolder }

   { Erase where the scroll bars and grow box used to be. }
   { This is not necessary, but makes it look nicer.      }
   FlushRects(rH, rV);
   FillRect(rH, White);
   FillRect(rV, White);
   GetGrowRect(rGrow);
   FillRect(rGrow, white );

   { Tell WM the old scroll bars location must be updated }
   temprgn := NewRgn;
   With rH do SetRectRgn(TempRgn, left, top, right, bottom);
   InvalidateRgn(Folder, TempRgn);
   With rV do SetRectRgn(TempRgn, left, top, right, bottom);
   InvalidateRgn(Folder, TempRgn);
   with rGrow do SetRectRgn(temprgn, left, top, right, bottom);
   InvalidateRgn(folder, TempRgn);

   { get the new size of the folder and tell WM about it}
   mySize := ptNewBR;
   SetRect( newport, folder^.portrect.left, folder^.portrect.top,
            mysize.h, mysize.v);
   SubPt(folder^.portrect.topleft, mySize);

   { check for hiding, changing, displaying the scroll bars in myfolder }
   if folder = myfolder then begin

      { if in scale to fit mode then stay there }
      if scale[activechart].state = ScaleFit then begin
         FolderSize(myfolder, mysize.h, mysize.v, false); { no update }
         ShowFitReduce;
      end { if }

      { otherwise paper could now be smaller than screen }
      else begin
         scalePaper( paper, activechart);
         if (mysize.h >= (paper.right - paper.left)) and (mysize.v >= (paper.bottom - paper.top)) then begin
            FolderSize(myfolder, mysize.h, mysize.v, false); { no update }
            ShowFitReduce;
         end { if }

         { otherwise, no rescaling }
         else begin
            neworigin := myorigin;
            CkSbars(newport, neworigin);

            { if we are changing the origin then redisplay entire folder }
            if (neworigin.h <> myorigin.h) or
               (neworigin.v <> myorigin.v) then begin
               FolderSize(myfolder, mySize.h, mySize.v, false); { no update }
               myOrigin := neworigin;
               setport(myfolder);
               setorigin(myorigin.h, myorigin.v);
               ClipRect(myfolder^.portrect);
               ShowScroll(myfolder);
               ShowContents(myfolder);
            end { if }

            { otherwise just update the newly exposed rgn }
            else begin
               FolderSize(myfolder, mySize.h, mySize.v, True); { need an update }
               ShowScroll(myfolder);
            end; { else }
         end; { else }
      end; { else }
   end { if }
   else begin
      FolderSize(folder, mysize.h, mysize.v, true);
      ShowScroll(folder);
   end; { else }
   DisposeRgn( temprgn );
   if folder = myfolder then begin
      setorigin(myorigin.h, myorigin.v);
      ClipRect(myfolder^.portrect);
   end; { if }
end; { GrowFolder }


{-------------------------------------------------------------------------------}


Begin { CkGrow }

   if (folder = myfolder) or (folder = scrapFolder) then begin

      { make sure origin set up for WMLib }
      setport(folder);
      setorigin(0,0);
      cliprect(folder^.portrect);
      if folder = myfolder then setpt(SbWhere, where.h - myOrigin.h, where.v - myOrigin.v)
      else SbWhere := where;

      If FGrowHit(SbWhere) then Begin

         { hit on a grow box }
         CkGrow := True;

         { monitor the mouse moves }
         DragFrame(SbWhere, True, ptNewBr);

         { go adjust the folder coords for its new size }
         GrowFolder;

         { filter out the events to the up event }
         SkipToUp := true;

      End { if }
      Else CkGrow := False;
   end { if }
   else CkGrow := false;

End; { CkGrow }


{-------------------------------------------------------------------------------}


Procedure CommandKey;
{ an apple key was pressed, do appropriate command if any }

var mintime: longInt;      { time to highlight command if apple key used }

begin { CommandKey }

   MenuKey( theEvent.ascii, theMenu, theItem );

   if ((theMenu = 0) and (theItem = 0)) then StopAlert(pealert, 9 {No Apple Key})
   else begin
      { set port and clip out scrolls }
      setport(activefolder);
      with activefolder^ do setrectRgn( cliprgn, portrect.left,
      portrect.top, portrect.right - dhsbox, portrect.bottom - dvsbox);

      { send out the command }
      HiLiteMenu(theMenu);
      MinTime := time + 5;  { 1/20 of a second }
      MenuCommand( theMenu, theItem );
      Repeat until time > minTime;  { make sure the hilite shows }
      HiLiteMenu(0);
   end; { else }
end; { CommandKey }


{-------------------------------------------------------------------------------}


Procedure Blink;
{ for controlling blinking caret on text editing in dialog folder
  and in myfolder. }

begin { Blink }

   if ((theport = myfolder) and (myfolder <> nil)) or ((theport = dialogFolder) and msgon) then begin
      if msgon then dmBlink
      else with curselrec[activechart] do begin

         { dont blink in scaled mode }
         if scale[activechart].state = scalereal then

            { editing a field in a task box or milestone }
            if (tag = textsel) OR (tag = FixCost) OR (tag = unitCSel) then
               BlinkCaret(htaskfield, hfstate, curtimeout)

            { editing a chart title field }
            else if (tag = chartTitles) then
               BlinkCaret(Titles[activechart][index].htitlefield, hfstate, curtimeout);

      end; { with }
   end; { if }
end; { Blink }


{-------------------------------------------------------------------------------}

Procedure DoPEActivate;
{ does all the activation stuff when resuming or for an activate event }
var
    errnum:      integer;
    what:        scrapType;
    myhandle:    TH;
    scrapContent:tscrapHnd;

Begin
   HiLiteTab(myfolder,true);

   setport(myfolder);
   cliprect(myfolder^.portrect);
   showscroll(myfolder);

   { perform wake-up activities: }
   if not msgon then begin
      keyproc := @myKey;
      keyport := myfolder;
   end; { if }

   { install menus into menuBar and DrawMenuBar }
   InstallMenus(theEvent.who);
   {fVisible := True;}
   SetPort(DialogFolder);
   RestoreDBox;

   { check out scrap contents }
   StartGetScrap(errnum);
   if errnum <> 0 then begin
      {$IFC PEDEBUG}
      writeln('error on start get scrap ',errnum);
      {$ENDC}
      StopAlert(pealert, 29 {Scrap Error});
   end { if }
   else begin
      GetScrap(what, myhandle);
      if what = ScrapBGraf then begin
         scrapContents := pointer(ord(myhandle));
         if scrapcontents^^.tag = adocument
         then myscrapConts := adocument
         else myScrapConts := atask;
      end
      else MyScrapConts := atextsel;
   end;
   endGetScrap(errnum);
   {$IFC PEDEBUG}
   if errnum <> 0 then writeln('error on endgetscrap ',errnum);
   {$ENDC}

   Undovalid := false;
   AnyToUndo := false;
   clickTime := 0;
   clickCount:= 0;
End;



Procedure  ProcessTheEvent(theEvent: EventRecord);
{ Controls the handling of events as recieved from the window manager.}

const
    BSpace     = 8;
    keepmenus  = false;
    keepdialog = false;

var i: integer;
    error: boolean;
    filerParams: FilerExt;
    filerError:  integer;
    failReason:  FReason;
    errnum:      integer;
    what:        scrapType;
    myhandle:    TH;
    scrapContent:tscrapHnd;
    error_code:  integer;
    myFReply:    FReply;

begin { ProcessTheEvent }

  if (theEvent.who = myfolder) and (myfolder <> nil) then begin
     { set up for an event for myfolder }
     setport(theEvent.who);
     clipRect(myfolder^.portrect);
  end;
  if theEvent.who = dialogfolder then setport(dialogfolder);

  if not SkipEvent then
    case theEvent.what of

      buttonUp: begin
         clickTime:=theEvent.when + {50} clickDelay;
         if (theEvent.who = dialogfolder) and msgon then
            CallPrcsProc(theEvent, theDBox.processproc)
         else if theEvent.who = myfolder then myUp;
         { ignore a button up event in scrap or filer folders }
      end; { buttonUp }

      buttonDown:  begin
         if theEvent.who = menuFolder then begin
            if msgOn then StopAlert(pealert, 11 {No Dialog Box})
            else begin
               { let user select a menu item }
               CkMenuOptions;
               MenuSelect(theEvent.where,theMenu,theItem);
               MenuCommand(theMenu, theItem);
               HiLiteMenu(0);
            end; { else }
         end { if }
         else begin
            { keep track of double and triple clicks }
            if (theEvent.when < clickTime)  and (PtInRect(theEvent.where, MouseNbrhood))
            then clickCount:=clickCount+1
            else begin
               clickCount:=1;
               Pt2Rect(theEvent.where, theEvent.where, MouseNbrhood);
               insetRect(MouseNbrhood, -dHNbHd,-dVNbHd);
            end;

            if (theEvent.who = dialogFolder)  and msgon then
                CallPrcsProc(theEvent,theDBox.processProc)
            else begin
               if not CkGrow(theEvent.who, theEvent.where) then begin
                  if theEvent.who = myfolder then begin
                     if msgOn then CallEndProc(false,theDBox.endProc);
                     if not CkScroll(theEvent.who, theEvent.where) then MyDown(theEvent.where);
                  end; { if }
               end; { if }
            end; { if }
         end; { else }
      end; { buttonDown }

      FilerEvent: begin
         GetAddParams(filerError,theEvent,filerParams);
         CASE filerParams.theFlrOp OF

            fcResume: BEGIN
               DocDirtyFlag := false;  { A new doc is consistent }
               { save off the password }
               PEPassword := filerParams.thePassword;
               IF OpenTheDoc(filerParams,failReason, error_code) then {OK}
               else begin
                  myFReply := docclosd;
                  TellFiler(filerError,myFReply,failReason,theEvent.who);
               end;
               If (myFolder = ActiveFolder) Then DoPEActivate;
            end;

            fcNone: begin  { Pulling of this tool is not supported by Pert }
               TellFiler(filerError,docClosd,noMoreDocs,theEvent.who);
            end;

            fcClose,
            fcSuspend,
            fcShred:  BEGIN
               { ignore all the fcclose code for now }
               { if msgon then call endproc with doit false }
               if msgOn then CallEndProc(false,theDBox.endProc);
               MyDeselect(error);
               if filerParams.theFlrOp = fcClose then shrinkDrawing;
               if error then TellFiler(filerError, docNotClosed, DirtyDoc, myfolder)
               else begin
                  PEPassword := filerParams.thepassword;
                  IF CloseTheDoc(filerParams.theFlrOp,failReason) THEN begin
                     TellFiler(filerError,docClosd,allOK,myfolder);
                     myfolder := nil; { this guy not active any more }
                  end
                  ELSE begin
                     if ImActive then begin
                        case FailReason of
                           InternalError: begin { internal error }
                              StopAlert(Pealert, 18 {Internal Error});
                           end;

                           NoDiskSpace: begin { disk full }
                              StopAlert(pealert, 54 {Disk Full});
                           end;

                           { file I/O error other than disk full, CantWrite }
                           otherwise StopAlert( Pealert, 20 {No Save});
                        end; { case }
                     end;
                     TellFiler(filerError,docNotClosed,failReason,myfolder);
                  end; { else }
               end; { else }
            END;

            fcCopy,
            fcPut:  BEGIN
               pepassword := filerparams.thepassword;
               IF CopyTheDoc(filerParams.thePrefix,filerParams.theFlrOp,failReason) THEN
                  TellFiler(filerError,docXfered,allOK,theEvent.who)
               ELSE
                  TellFiler(filerError,docNotXfered,failReason,theEvent.who);
            END;

            fcTerminate:  BEGIN      { applications should not terminate until this event is
                         received or they crash }
               toldToDie := TRUE;
            END;

            fcDfClose: begin
               TellFiler(filerError,dfNotClosed, internalError, theEvent.who);
            end;
         end;
      end; { FilerEvent }

      keyDown:  begin
         if (theEvent.who = myfolder) or (theEvent.who = dialogFolder) then begin
            if (theEvent.appleKey) and ((ord(theEvent.ascii)<>Bspace) and (not msgon)) then CommandKey
            else begin
               SetPort(keyport);
               CallPrcsProc( theEvent,keyproc );
            end;
         end;
      end; { keyDown }


      folderActivate:  begin
         if (theEvent.who = filerfolder) then begin
            TakeControl(theEvent, keepMenus, keepdialog);
         end { if }
         else begin
            if (theEvent.who = scrapfolder) then begin
               TakeControl(theEvent, keepMenus, keepDialog);
               HiLiteTab(scrapfolder,true);
               setport(scrapfolder);
               cliprect(scrapfolder^.portrect);
               ShowScroll(scrapfolder);
               InstallMenus(scrapfolder);
            end
            else begin
               if (theEvent.who = myfolder) then begin
                  TakeControl(theEvent, keepmenus, keepdialog);
                  DoPEActivate;
               end; { if }
            end; { else }
         end; { else }
      end; { folderActivate }


      folderDeActivate:   begin
         if theEvent.who = filerFolder then begin
            GiveControl( theEvent );
         end
         else begin
           if theEvent.who = scrapFolder then begin
              GiveControl( theEvent );
              HiLiteTab(theEvent.who, false);
              setport(scrapfolder);
              cliprect(scrapfolder^.portrect);
              HideScroll(scrapfolder);
              CleanMenus;
           end
           else begin
              if theEvent.who = myfolder then begin
                 if msgon then dmDeactivate;
                 HiLiteTab(theEvent.who, false);
                 setport(myfolder);
                 ClipRect(myfolder^.portrect);
                 HideScroll(theEvent.who);
                 CleanMenus;
                 { prepare to swap, make my data segments small, etc }
                 {fVisible := False;}
                 feTerminate;
                 ShrinkHeap(theHeap, false {dont shrink the disc}, error_code);
                 { pass on the torch to the new active folder }
                 GiveControl(theEvent);
              end; { if }
           end; { else }
        end; { else }
      end; { folderDeactivate }


      folderUpdate:  begin
         if theEvent.who = scrapfolder then begin
            setport(scrapfolder);
            cliprect(scrapfolder^.portrect);
         end;
         BeginUpdate(theEvent.who);
         if (theEvent.who = myfolder) or (theEvent.who = scrapFolder) or
            (theEvent.who = dialogfolder) then begin
            ShowScroll(theEvent.who);
            ShowContents(theEvent.who);
         end;
         EndUpdate(theEvent.who);
      end; { folderUpdate }
    end; { case }
end; { ProcessTheEvent }

{-------------------------------------------------------------------------------}

{$S SgPEInit}
{$I aplp/init.text}

{-------------------------------------------------------------------------------}


Begin { Main }

   Initialize;

   repeat { main loop }

     While not (ImActive or toldtoDie) do begin
        GetEvent(theEvent);         { may suspend me }
        ProcessTheEvent(theEvent);
     end; { while }

     while (ImActive and (not toldToDie)) do begin
        UpdateCursor;
        IF EventAvail then begin
           GetEvent(theEvent);
           ProcessTheEvent(theEvent);
        end { if }
        else begin
           if not PulledAsTool and (docsOpen > 0) then begin
              now := Time;
              IF now > CaretTime THEN begin
                 CaretTime := now + 50;
                 if {fvisible} ImActive then Blink;
              end; { if }
           end; { if }
           LetOthersRun;
        end; { else }
     end; { while }

   until toldToDie;
   {$IFC PEDEBUG}
   writeln('So long from PERT');
   {$ENDC}
End. { Main }
ÿ