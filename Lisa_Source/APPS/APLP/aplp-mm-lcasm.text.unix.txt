;;;;;;;;;;;
;                           {'Copyright 1983, Apple Computer Inc.'}
;;;;;;;;;;;

LTVAL           .EQU    0
GTVAL           .EQU    1
EQVAL           .EQU    2

        .SEG    "calculator"
        .FUNC   ADDDHDV,12
;------------------------------------------------------------------------
;
;  addDhDv - adds the displacements `dh, dv` to the coordinates `coord`.
;            Returns equal to true if the resulting coordinate
;            is not within the bounds of the matrix.
;
;       (*$S calculator *)
;       FUNCTION  AddDhDv
;         (dh, dv       : integer;
;          var coord    : pident
;         ) : boolean;
;
;
;
;       USES
;               A0,A1
;               D1,D2
;
;

        MOVE.L  (SP)+,A1                ;GET THE RETURN ADDRESS
        MOVE.L  (SP)+,A0                ;GET COORDINATES

        MOVE.B  1(A0),D2                ;GET ROW COORDINATE
        ANDI.W  #$00FF,D2               ;NORMALIZE IT
        ADD.W   (SP)+,D2                ;ADD THE DISPLACEMENT TO THE ROW COORDINATE
        MOVE.B  D2,1(A0)                ;SAVE THE NEW ROW COORDINATE

        MOVE.B  (A0),D1                 ;GET COULMN COORDINATE
        ANDI.W  #$00FF,D1               ;NORMALIZE IT
        ADD.W   (SP)+,D1                ;ADD THE DISPLACEMENT TO THE COULMN COORDINATE
        MOVE.B  D1,(A0)                 ;SAVE THE NEW ROW COORDINATE

        BEQ     BAD                     ;CHECK FOR A ZERO ROW COORDINATE
        ANDI.W  #$FF00,D1               ;CLEAR THE LOWER ORDER BITS
        BNE     BAD                     ;CHECK FOR ANYTHING LEFT OVER

        TST.W   D2                      ;DO THE SAME WITH THE COLUMN COORDINATE
        BEQ     BAD
        ANDI.W  #$FF00,D2
        BNE     BAD

        CLR.B   (SP)                    ;CLEAR BIT FOR NON-BAD COORDINATE
        JMP     (A1)
BAD     MOVE.B  #01,(SP)                ;SET BIT FOR BAD COORDINATE
        JMP     (A1)


        .SEG    "compile"
        .FUNC   CCHR,2
;------------------------------------------------------------------------
;
;       (*$S compileeg *)
;       function cChr(x : integer) : char;
;
;  cChr - returns the proper character value from the integer given
;
;       USES    D0,A0
;
        MOVE.L  (SP)+,A0
        MOVE.W  (SP)+,D0                ;GET THE CHARACTER NUMBER
        BGE.S   LEAVE
        ADDI.W  #256,D0
LEAVE   MOVE.W  D0,(SP)
        JMP     (A0)


        .SEG     "matMgrSeg"
        .FUNC    CoordInMatrix,4
;------------------------------------------------------------------------
;
;       (*$S matMgrSeg *)
;       FUNCTION CoordInMatrix(rowId, colId) : boolean;
;
;  CoordInMatrix - Determines if the coordinate pair `rowId, colId` are within the usuable matrix size.
;                  Return true as the value of the function if inclusion exists.
;
;  NOTE: for the sake of performace some routines do not call this function so that if the usuable
;        matrix size chanages someone has to track down all the references.
;
;
;
;       USES    D0,D1,A0
;
        MOVE.L  (SP)+,A0
        MOVE.W  (SP)+,D0
        MOVE.W  (SP)+,D1
        BEQ     ERR                     ;test for zero colId
        TST.W   D0
        BEQ     ERR                     ;test for zero rowId
        ANDI.W  #$FF00,D1               ;clear the lower order bits
        BNE     ERR                     ;add check for residue
        ANDI.W  #$FF00,D0               ;repeat for the row coordinate
        BNE     ERR

        MOVE.B  #01,(SP)
        JMP     (A0)
ERR     CLR.B   (SP)
        JMP     (A0)


        .SEG    "fexec"
        .PROC   LCCALL,8
        .DEF    DRAWGRAFIX, IANOTSELECT, IBGDOWN, IBGEXIT, IBGGETTHUMB
        .DEF    IBGKEY, IBGMENUCOMMAND, IBGMOVE, IBGNEWRGIMG, IBGSCROLL, IBGRECONFIG
        .DEF    IBGSETGRFX, IBGTMOUT, IBGUP, IDOBGACT, IDRAWGRAPH, IERASEBG, IFXBGFREE
        .DEF    IFXBGSTUFF, IBGRGCHGED, IINITBGPNL, IINITBGSTORAGE, ITTLSELECT, IBTNPROC, ICHKPROC
;-------------------------------------------------------------------------
;
;       PROCEDURE CALL(VARIOUS PARAMETERS,PPROC : PROCPARAM)
;
;       GENERAL PURPOSE LEVEL-1 PROCEDURE PARAMETER INVOCATION
;
;
DRAWGRAFIX
IANOTSELECT
IBGDOWN
IBGEXIT
IBGGETTHUMB
IBGKEY
IBGMENUCOMMAND
IBGMOVE
IBGNEWRGIMG
IBGSCROLL
IBGSETGRFX
IBGTMOUT
IBGUP
IDOBGACT
IDRAWGRAPH
IERASEBG
IFXBGSTUFF
IBGRGCHGED
IINITBGPNL
IINITBGSTORAGE
IBGRECONFIG
IFXBGFREE
ITTLSELECT
IBTNPROC
ICHKPROC
        MOVE.L   (SP)+,A0
        MOVE.L   (SP)+,A1
        MOVE.L   A0,-(SP)
        JMP      (A1)


        .SEG    "matMgrSeg"
        .PROC   MOVELFAST,10
        .DEF    AMLEFT
;--------------------------------------------------------------------------
;
;       (*$S matMgrSeg *)
;       PROCEDURE MOVELFAST(P1,P2 : PTRDATA;NBYTES : INTEGER);
;
;       DOES A FAST MOVELEFT.  ASSUMES P1,P2 POINT TO WORD BOUNDARIES.
;       NBYTES CAN BE ODD OR EVEN.  DIFFERS FROM MOVELFAST IN THAT MOVES
;       ARE ATTEMPTED IN 4 BYTE CHUNKS WHICH MAY ACTUALLY TAKE MORE TIME
;       BECAUSE OF THE NEED TO ALIGN TO LONG-WORD BOUNDARIES.  ROUTINE
;       IS THE SAME AS MOVELFAST IF THE QUANTITY TO BE MOVED IS <= 7 BYTES
;
;
;       ENTRY POINT AT AMLEFT SHOULD HAVE THE FOLLOWING SETUP
;          NO PARAMETERS ON THE STACK
;          A0   = P2
;          A1   = P1
;          D0   = NBYTES
;          D2   = RETURN ADDRESS
;
;       USES
;               A0,A1
;               D0,D1,D2

        MOVE.L  (SP)+,D2                ;POP RETURN ADDRESS
        MOVE.W  (SP)+,D0                ;D0 := NBYTES
        MOVE.L  (SP)+,A0                ;A0 := P2
        MOVE.L  (SP)+,A1                ;A1 := P1
AMLEFT  MOVE.L  D2,-(SP)                ;PUSH RETURN ADDRESS FOR RTS
        TST     D0
        BLE.S   NOBYTE                  ;IF D0 <= 0, NOTHING TO MOVE

        ;LOAD THE SOURCE INTO A1, DESTINATION INTO A0, LENGTH INTO D0
        CMP     #7,D0
        BLE.S   FWD1                    ;SHORT MOVES GO 1 AT A TIME
        MOVE.L  A1,D2
        ADDQ.L  #2,D2
        CMP.L   A0,D2                   ;IF SOURCE+2 = DEST THEN
        BEQ.S   FWD1
        MOVE    A0,D1                   ;MOVE 1 AT A TIME BECAUSE 4 WON'T PROPAGATE
        AND     #1,D1                   ;D1 = 1 IFF DEST IS ODD
        AND     #1,D2                   ;D2 = 1 IFF SOURCE IS ODD
        ADD     D1,D2
        BEQ.S   @3                      ;SKIP IF BOTH EVEN
        SUBQ    #1,D2
        BEQ.S   FWD1                    ;ONE AT A TIME IF ONE EVEN, ONE ODD
        SUBQ    #1,D0                   ;BOTH OFF, TAKE 1 OFF TOTAL LENGTH
        MOVE.B  (A1)+,(A0)+             ; AND XFER 1 BYTE FIRST
@3      MOVE    D0,D1                   ;SOURCE AND DEST BOTH EVEN OR BOTH ODD
        AND     #3,D0                   ;1 BYTE COUNT
        LSR     #2,D1                   ;4 BYTE COUND
        SUBQ    #1,D1                   ;NON-NEG RESULT SINCE ALWAYS MOVE >= 4
@4      MOVE.L  (A1)+,(A0)+             ;4 BYTE MOVES
        DBF     D1,@4

        ;1 BYTE MOVES

        SUBQ    #1,D0                   ;LENGTH
        BMI.S   NOBYTE                  ;NO SINGLE BYTE TO MOVE
F       MOVE.B  (A1)+,(A0)+
FWD1    DBF     D0,F
NOBYTE  RTS


        .SEG    "showForms"
        .PROC   MOVERFAST,10
;--------------------------------------------------------------------------
;
;       (*$S specialCase *)
;       PROCEDURE MOVERFAST(P1,P2 : PTRDATA;NBYTES : INTEGER);
;
;       DOES A FAST MOVERIGHT.  ASSUMES P1,P2 POINT TO WORD BOUNDARIES.
;       NBYTES CAN BE ODD OR EVEN.
;
;
;       USES
;               A0,A1
;               D0,D1

        MOVE.L  (SP)+,D1                ;POP RETURN ADDRESS
        MOVE.W  (SP)+,D0                ;D0 := NBYTES
        MOVE.L  (SP)+,A1                ;A1 := P2
        MOVE.L  (SP)+,A0                ;A0 := P1
        MOVE.L  D1,-(SP)                ;PUSH RETURN ADDRESS FOR RTS
        TST.W   D0
        BLE.S   RETURN

        ADDA.W  D0,A0
        ADDA.W  D0,A1

        SUBQ.W  #1,D0
XFER    MOVE.B  -(A0),-(A1)
        DBF     D0,XFER

RETURN  RTS


        .SEG    "calculator"
        .PROC   NORMCOORDS,4
;------------------------------------------------------------------------
;
;  normcoords - normalizes a coordinate range such that COORD.FIRST represents the
;               upper left hand corner of the range and COORD.SECOND represents the
;               lower right hand corner of the range.
;
;     (*$S calculator *)
;      procedure normcoords
;         (* var coord : rangeType
;         *);
;
;
;
;       USES
;               A0,A1
;               D0,D1
;
;
        MOVE.L  (SP)+,A1                ;SAVE RETURN ADDRESS
        MOVE.L  (SP)+,A0                ;GET POINTER TO COORDINATE
        MOVE.B  (A0),D0                 ;FIRST.START
        ANDI.W  #$00FF,D0
        MOVE.B  2(A0),D1
        ANDI.W  #$00FF,D1               ;SECOND.START
        CMP.W   D0,D1
        BGE     CHKROW                  ;SECOND.START >= FIRST.START
        MOVE.B  D0,2(A0)
        MOVE.B  D1,(A0)
CHKROW  MOVE.B  1(A0),D0                ;FIRST.LENGTH
        ANDI.W  #$00FF,D0
        MOVE.B  3(A0),D1                ;SECOND.LENGTH
        ANDI.W  #$00FF,D1
        CMP.W   D0,D1                   ;SECOND.LENGTH >= FIRST.LENGTH
        BGE     LEAVE
        MOVE.B  D0,3(A0)
        MOVE.B  D1,1(A0)
LEAVE   JMP     (A1)


        .SEG    "matMgrSeg"
        .PROC   SETBITS,8
;------------------------------------------------------------------------
;
;       (*$S matMgrSeg *)
;       PROCEDURE SETBITS
;         (pCel  :TPCel;
;          cnRec : cnRecord
;         );
;
;
;
;       USES
;               A0,A1
;               D0,D1,D2
;
;

        MOVE.L  (SP)+,D2                ;SAVE RETURN ADDRESS
        MOVE.L  (SP)+,A1                ;GET CNREC
        CLR.W   D0
        OR.B    $0A(A1),D0              ;cnInCopy Bit   = bit 5 in pCel^.attributes
        ROR.W   #1,D0
        OR.B    $09(A1),D0              ;cnHasRule Bit  = bit 6 in pCel^.attributes
        ROR.W   #1,D0
        OR.B    $08(A1),D0              ;cnInvalid Bit  = bit 7 in pCel^.attributes
        ROR.W   #1,D0
        OR.B    $0B(A1),D0              ;cnMoneyBit Bit = bit 1 in pCel^.attributes

        MOVE.L  (SP)+,A0                ;GET PCEL
        ANDI.W  #$1FFE,(A0)             ;clear old bits in pCel^.attributes
        OR.W    D0,(A0)                 ;add the new ones to pCel^.attributes
        MOVE.L  $04(A1),D0              ;does cell.cnHContents = nil?
        BEQ.S   FINISH
        MOVE.L  D0,A0
        CLR.W   D0
        OR.B    $0C(A1),D0              ;cnIs1Marked = bit 7
        ASL.W   #1,D0
        OR.B    $0D(A1),D0              ;cnIs2Marked = bit 6
        ASL.W   #2,D0
        OR.B    $0F(A1),D0              ;add the TCSize bits
        ASL.W   #4,D0
        OR.B    $10(A1),D0              ;add the type of object
        TST.B   $0F(A1)
        BEQ     SYMBOL
        ANDI.W  #$FFF3,D0
SYMBOL  MOVE.L  (A0),A1                 ;get data area off the handle
        ANDI.W  #$FF00,(A1)             ;clear the marked and TCsize bits = bits 7..4
        OR.W    D0,(A1)                 ;save the new marked bits
FINISH  MOVE.L  D2,-(SP)
        RTS


        .SEG    "matMgrSeg"
        .PROC   SFROMB,10
        .REF    AMLEFT
;------------------------------------------------------------------------
;
;   SfromB - copies numBytes bytes from pBytes to strg and sets the length
;            of strg
;
;     (*$S matMgrSeg *)
;      procedure SfromB
;         (* var strg : stringType;
;            pBytes   : ptrData;
;            numBytes : integer
;         *);
;
;
;
;       USES
;               A0,A1
;               D0,D1,D2
;
;
        MOVE.L  (SP)+,D2
        MOVE.W  (SP)+,D0
        MOVE.L  (SP)+,A1
        MOVE.L  (SP)+,A0
        MOVE.W  #255,D1
        CMP.W   D1,D0
        BLE     LEAVE
        MOVE.W  D1,D0
LEAVE   MOVE.B  D0,(A0)+
        JMP     AMLEFT


        .SEG    "matMgrSeg"
        .PROC   STRTOB,12
        .DEF    XSTOB, STOB
        .REF    AMLEFT
;------------------------------------------------------------------------
;   StoB - copies length(strg) bytes from strg to numBytes and sets
;   numBytes to the length
;
;     (*$S matMgrSeg *)
;      procedure StoB
;        (* strg         : str25;   {str255 for xStoB}
;           pBytes       : ptrData;
;           var numBytes : integer
;         *);
;
;
;       USES
;               A0,A1
;               D0,D1,D2
;
;
XSTOB
STOB
        MOVE.L  (SP)+,D2                ;RETURN ADDRESS
        MOVE.L  (SP)+,A0                ;NUMBYTES
        MOVE.L  (SP)+,D1                ;PBYTES
        MOVE.L  (SP)+,A1                ;STRG
        MOVE.B  (A1)+,D0                ;GET THE LENGTH OF THE STRING AND MOVE PAST THE LENGTH BYTE
        ANDI.W  #$00FF,D0
        CMP.W   (A0),D0                 ;IS IT GREATER THAN NUMBYTES?
        BGT     LEAVE
        MOVE.W  D0,(A0)                 ;IS SO, SET NUMBYTES TO THE LENGTH OF THE STRING
LEAVE   MOVE.L  D1,A0
        JMP     AMLEFT

        .END

ÿ