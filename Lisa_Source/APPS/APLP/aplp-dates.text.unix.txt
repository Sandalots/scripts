
{----------------------------------------------------------------------------}
{                  Copyright 1983,1984, Apple Computer Inc.                  }
{                                                                            }
{ Calendar and Date Routines                                                 }
{                                                                            }
{ This unit does all the calendar and date stuff for the program.  When a    }
{ schedule is calculated, it is done in working days, that is in integers.   }
{ This unit manages the conversion of working days to real calendar days,    }
{ taking weekends and all holidays into account.                             }
{ This file is now an include file for aplp/globals.                         }
{----------------------------------------------------------------------------}


{$S MainLoop}

Function LeapYear( year: integer ): byte;
{ Determines if the year is a leap year, that is if the integer is evenly
  divisible by four, and returns the value 1 if so, 0 if not. }

begin { LeapYear }

   If ( year Mod 4 ) = 0 then  leapYear := 1
   else leapYear := 0;
end; { LeapYear }


{-------------------------------------------------------------------------------}


Function Julian( date:  tdate ): integer;
{ Converts a date to a julian date.  This is just the number of days within
  the current year. }

begin { Julian }

   Julian := MonthLen[ date.month ] + date.day +
      ord( date.month > 2 ) * leapYear( date.year );
end;  { Julian }


{-------------------------------------------------------------------------------}

Procedure JulianToMthDay( totaldays:  integer;  var date:   tdate );
{ Converts a julian date to a date.  Note that the year must be present in
  the date when passed in as a parameter.  On return, the month and day
  will be filled in. }

var I: integer;
    leap: integer;

begin { JulianToMthDay }

   with date do begin

      { add in the length of each month }
      for I := 1 to 13 do begin

         { add one day if leap year }
         leap := ord(I > 3) * LeapYear(year);

         { stop when we have exceeded the total number of days }
         if monthLen[I] + leap >= totaldays then begin

            { month is counter - 1, day is number days over }
            if I <> 1 then month := I - 1
            else begin
               month := 1;
            end;
            Day := totaldays - monthLen[month]  - leap;

            { special case if date is 2/29 of a leap year }
            if (month = 3) and (day = 0) then begin
               month := 2;
               day := 29;
            end; { if }
            exit( JulianToMthDay );
         end; { if }
      end; { for }
   end; { with }
end;  { JulianToMthDay }

{-------------------------------------------------------------------------------}


Procedure DateAdd( RDoffset: integer;  var newDate: tdate );
{ converts the CalStartDate plus a real day offset to a new date.  }

var total:  integer;        { total number of days }

begin { DateAdd }

   { add real offset to startdate and convert to julian }
   newDate.year := CalStartDate.year;
   total := Julian(CalStartDate) + RDoffset;

   { now break total into years, months and days }
   if total < 0 then begin
     while (total < 0) do begin
        newdate.year := newdate.year - 1;
        total := total + (365 + leapYear(newdate.year));
     end;
   end
   else begin
      while (total > (365 + leapYear( newdate.year ))) do begin
         total := total - ( 365 + leapYear( newDate.year ));
         newDate.year := newDate.year + 1;
      end; { while }
   end; { else }
   JulianToMthDay( total, newdate );
   dayOfWeek(newdate);
end; { DateAdd }


{-------------------------------------------------------------------------------}

Procedure NextRD( var RD, RDCode: integer );
{ get the next real day and its day code }

begin { NextRD }

   RD := RD + 1;
   RDcode := (RDCode + 1) mod 7;
end; { NextRD }


{-------------------------------------------------------------------------------}

Function Holiday( RD: integer ):  boolean;
{ determines if this real offset is a holiday }

var i: integer;

begin { Holiday }

   { look through each holiday for match }
   for i := 1 to maxholidays do begin
      if RD = Holidates[i].RD then begin
         Holiday := true;
         Exit(holiday);
      end;
   end;
   Holiday := false;
end; { Holiday }



{-------------------------------------------------------------------------------}
{                                                                               }
{           Date Routines, public.                                              }
{                                                                               }
{-------------------------------------------------------------------------------}


Procedure Str{( Numb: Integer;  var Strng: String3 )};
{ Converts Numb to ASCII and stuffs the result into Strng.  Gets called by dialog
  termination stuff. }

var
   Digit : Integer;    { the low order digit               }
   Next  : Integer;    { The rest of the number            }
   Chr1  : Char;       { the low order digit in ASCII      }
   myNumb: integer;    { what's left of the working number }
   I     : Integer;    { counter                           }
   negative: boolean;  { bool for sign of number           }

Begin { Str }

   Strng := '   ';
   negative := Numb < 0;
   myNumb := Abs(Numb);

   { Get the low order digit }
   For I := 1 to 3 do Begin
      Next        := myNumb div 10;
      Digit       := myNumb - 10 * Next;
      Strng[4 - I]:= chr(ord('0') + digit);
      myNumb      := Next;
   End;
   If myNumb > 0 then
      { got some left over, replace string with null string }
      Strng := ''
   else begin
      { delete leading zeros and blanks in string }
      If ((Strng[1] = '0') or (Strng[1] = ' ')) then begin
         Delete( Strng, 1, 1);
         if ((Strng[1] = '0') or (Strng[1] = ' ')) then
            Delete( Strng, 1, 1);
      end;
      if (length(Strng) = 3) and negative then Strng := ''
      else if negative then Strng := Concat( '-', Strng);
   end;
End; { Str }


{-------------------------------------------------------------------------------}


Function AsciiToInt{( var strng: String30 ): integer};
{ Converts a string of characters to an integer value. Negative numbers will
  not parse, neither will something with a plus sign in front.  Characters are
  deleted from the string as they are used to construct the number.   Gets called
  by dialog termination stuff}

var result: integer;
    charset: set of '0'..'9';

begin { AsciiToInt }

   result := 0;
   charset := ['0','1','2','3','4','5','6','7','8','9'];

   { kill leading blanks }
   while (length(Strng) > 0) and (Strng[1] = ' ') do
      Delete(Strng,1,1);

   { convert to integer as long as have integer characters }
   while (length(Strng) > 0) and (Strng[1] in charset) do begin
      result := result * 10 + (ord(strng[1]) - ord('0'));
      Delete(Strng,1,1);
   end;

   { kill trailing blanks }
   while (length(Strng) > 0) and (Strng[1] = ' ') do
      Delete(Strng,1,1);

   AsciiToInt := result;
end; { AsciiToInt }


{-------------------------------------------------------------------------------}

Procedure SetDateCode {(CodeString: String4; defaultstring: string30)};
{ sets up the date format code and format string. gets called by fileio unit
  when reading in a new file.  Gets called by dialog termination code. }

var monthPos: integer;
    yearPos:  integer;

begin { SetDateCode }

   DateFormat := CodeString;
   with DateCode do begin
      { figure out the date format }
      MonthPos := pos('m', DateFormat);
      YearPos  := pos('y', DateFormat);

      if monthPos = 1 then begin
         if yearPos = 2
         then Code := myd
         else Code := mdy;
      end { if }
      else begin
         if monthPos = 2 then begin
            if yearpos = 1
            then Code := ymd
            else Code := dmy;
         end { if }
         else begin
            if yearPos = 1
            then Code := ydm
            else Code := dym;
         end; { else }
      end; { else }

      Seperator := Copy(DateFormat, 4, 1);
   end; { with }
   MM_DD_YY := defaultString;
end; { SetDateCode }

{-------------------------------------------------------------------------------}


Procedure DayOfWeek{(var date: tDate )};
{ determines the day of week a particular day falls on.  This is useful for
  converting from working days to real days. The assumption is that the known
  date falls on a monday.  Called by the dialog termination code. Also called
  by DfltCalendar on creation of a new file. }

var
   total:     integer;    { total days between known date and parameter}
   I:         integer;    { year counter                               }

begin { DayOfWeek }

   { add up the total difference }
   if knownDate.year < date.year then begin
      total := Julian( date ) - Julian( knownDate );
      for I := KnownDate.year to Date.year - 1 do
         total := total + 365 + leapYear( I );
   end
   else begin
      total := - Julian( knownDate) - julian( date );
      for I := date.year to knownDate.year - 1 do
         total := total + 365 + leapYear( I );
   end;

   { day of week is just a mod 7 away }
   Date.DayCode := Abs(total) Mod 7;
end;  { DayOfWeek }



{-------------------------------------------------------------------------------}



Procedure DateString{( RDoffset:  integer;  var Date:  String30 )};
{ converts an offset plus a date to an ascii date string.  called by
  dialog termination code. }

var MonthString,             { the string with the month }
    DayString,               { the day                   }
    YearString:  String3;    { and the year              }
    NewDate:     tdate;      { the new date              }

begin { DateString }

   if (RDoffset <> minval) and (RDOffset <> maxval) then begin
      DateAdd( RDoffset,  NewDate );

      with NewDate do begin
         Str( month, MonthString);
         Str( day,   DayString );
         Str( year Mod 1900,  YearString);
      end; { with }

      with dateCode do
      case code of
         mdy: Date := Concat( MonthString, Seperator, DayString,   Seperator, YearString );
         myd: Date := Concat( MonthString, Seperator, YearString,  Seperator, DayString  );
         dmy: Date := Concat( DayString,   Seperator, MonthString, Seperator, YearString );
         dym: Date := Concat( DayString,   Seperator, YearString,  Seperator, MonthString);
         ydm: Date := Concat( YearString,  Seperator, DayString,   Seperator, MonthString);
         ymd: Date := Concat( YearString,  Seperator, MonthString, Seperator, DayString  );
      end; { case }
   end { if }
   else Date := '??';
end; { DateString }


{-------------------------------------------------------------------------------}

{$S   }

Function ParseDate{( len: integer; idata: hndData; var adate: tdate): boolean};
{ Parses data array into a date record.  Returns false if the characters do not parse
  into a legitimate date. Returns true if it does parse.  Gets called by the dialog
  termination code in the menu file. }

var
   theString: String30;       { the date string from field editor }
   i,                         { array counter for characters      }
   maxdate: integer;          { the max day number for this month }
   length:  integer;          { the length of the string          }
   firstfield:  integer;
   secondfield: integer;
   thirdfield:  integer;

begin { ParseDate }

{$R-}
   { copy the string into a string type of length 8 }
   theString := '';
   length    := CMin(8, len);
   for i := 0 to length - 1 do
      thestring := Concat(theString,' ');
   for i := 0 to length - 1 do
      theString[i+1] := chr(ord(idata^^[i]));

   { pick off the first field }
   FirstField := AsciiToInt( theString );
   if theString[1] <> datecode.Seperator[1] then parseDate := false
   else begin

      { delete the Seperator }
      Delete(theString,1,1);

      { pick off the second field }
      SecondField := AsciiToInt(theString);
      if theString[1] <> datecode.Seperator[1] then parseDate := false
      else begin

         { delete the Seperator }
         Delete(theString,1,1);

         { pick off the year }
         ThirdField := AsciiToInt(theString);

         { better have nothing left, or its not a date }
         if theString <> '' then parseDate := false
         else begin

            { so far its in the date format, check out the numbers }
            with adate do begin
               case datecode.code of
                  mdy: begin
                     month := firstfield;
                     day   := secondfield;
                     year  := thirdfield + 1900;
                  end;

                  myd: begin
                     month := firstfield;
                     year  := secondfield + 1900;
                     day   := thirdfield;
                  end;

                  dmy: begin
                     day   := firstfield;
                     month := secondfield;
                     year  := thirdfield + 1900;
                  end;

                  dym: begin
                     day   := firstfield;
                     year  := secondfield + 1900;
                     month := thirdfield;
                  end;

                  ymd: begin
                     year  := firstfield + 1900;
                     month := secondfield;
                     day   := thirdfield;
                  end;

                  ydm: begin
                     year  := firstfield + 1900;
                     day   := secondfield;
                     month := thirdfield;
                  end;
               end; { case }

               if Not((month >= 1) and (month <= 12)) then parsedate := false
               else begin

                  if Not((year >= 1900) and (year <= 1999)) then parsedate := false
                  else begin

                     { calc the max day number for this month }
                     maxdate :=  MonthLen[month+1] - MonthLen[month] +
                                 ord(month = 2) * LeapYear(year);
                     if Not((day >= 1) and (day <= maxdate)) then
                        parsedate := false
                     else begin
                        { it parsed OK, set day of week also }
                        parsedate := true;
                        DayOfWeek(aDate);
                     end; { else }
                  end; { else }
               end; { else }
            end; { with }
         end; { else }
      end; { else }
   end; { else }
   {$R+}
end; { ParseDate }


{-------------------------------------------------------------------------------}
{$S   }


Procedure DateSubtract {(minuend, subtrahend: tdate; var Realoffset: integer)};
{ subtracts two dates and returns a real and working day difference.  Gets called
  by the dialog termination code. }

var I:  integer;  { just a counter }

begin { DateSubtract }

   { convert both to julian dates and subtract }
   RealOffset := Julian( minuend ) - Julian( subtrahend );

   { now take difference in years into account }
   if minuend.year > subtrahend.year then
      for i := subtrahend.year to minuend.year - 1 do
         realOffset := RealOffset + 365 + LeapYear(i)
   else if minuend.year < subtrahend.year then
      for i := minuend.year to subtrahend.year - 1 do
         realOffset := RealOffset - (365 + leapYear(i));
end; { DateSubtract }


{-------------------------------------------------------------------------------}

{$S MainLoop}

Function WDEquiv{(realoffset: integer): integer};
{ if real offset in calendar range, then convert to a working
  day offset, otherwise pass back 0.  Gets called by the dialog
  termination stuff. }

var wdoffset: integer;

begin { WDEquiv }

   { set initially to 0 }
   wdoffset := 0;

   {$R-}
   { brute force method, count number of working days }
   if (realoffset <= Calendar^^[calsize]) then
      while (Calendar^^[WDOffset] < RealOffset) and (WDoffset < CalSize) do
         WDOffset := WDOFfset + 1;
   {$R+}

   WDEquiv := wdoffset;
end; { WDEquiv }


{-------------------------------------------------------------------------------}


Function RDEquiv{(WdOffset: integer): Integer};
{ returns the real equivalent offset to the Wdoffset parameter. another way
  to say it is that it takes weekends and holidays into account. If outside
  calendar range then returns maxval.  called by dialog termination code.}

begin { RDEquiv }

   {$R-}
   { if wdoffset in calendar range then do array access }
   If (Wdoffset >= 0) and (WDoffset <= CalSize)
   then RDEquiv := Calendar^^[WDoffset]
   else RDEquiv := maxval;
   {$R+}
end; { RDEquiv }


{-------------------------------------------------------------------------------}


Function FillCalendar{:boolean};
{ when the calendar Dbox is edited then this routines creates a new calendar }
{ gets called by peFileio unit when reading in a new file, and by pemenus for calendar
  dialogs }

var
   RD, WD,                  { real day and working day offsets }
   i,                       { local counter                    }
   NumRealDays,             { last Real day offset in calendar }
   MaxWorkDays,             { upper bound on calendar size     }
   RDcode: integer;         { real day offset day code         }
   RequestedSize:integer;   { requested size of calendar       }

begin { FillCalendar }

   { determine how many real calendar days are covered in the calendar }
   NumRealDays := 0;
   for i := calstartdate.year to endingyear do begin
      NumRealDays := NumRealDays + 365 +  leapYear(i);
   end; { for }

   { approximate the number of working days involved, calc an upper bound }
   MaxWorkDays := (endingYear - calstartdate.year + 1) * daysPerWeek * 53;

   { set up the calendar to be size of the working day estimate }
   RequestedSize := (MaxWorkDays + 1) * 2;
   ChangeSizeH(theHeap, pointer(Ord(calendar)),  RequestedSize);
   if (CBDataofH(theheap,Pointer(ord(calendar))) < RequestedSize) then FillCalendar := false
   else begin

      { fill in the calendar values }
      RD := 0;  { starting real offset }
      RDcode := CalStartDate.daycode;
      WD := 0;  { starting work offset }

      while (RD <= NumRealDays) and (WD <= MaxWorkDays) do begin
         While (RDcode >= daysPerWeek) or Holiday(RD) do NextRD(RD,RDcode);
         {$R-}
         Calendar^^[WD] := RD;
         {$R+}
         WD := WD + 1;
         NextRD(RD,RDcode);
      end; { while }

      { change the size of the calendar to the actual size used,
        smaller than requested size }
      ChangeSizeH(theheap, pointer(ord(Calendar)), WD*2);
      calsize := WD - 1;

      { send back message that all OK }
      FillCalendar := true;
   end; { else }
end; { FillCalendar }


{-------------------------------------------------------------------------------}

{$S MainLoop}

Procedure InitCalendar;
{ set up initial values for the calendar.  this gets called by the beginning of the
  world init code and by the fileIO code. }


{-------------------------------------------------------------------------------}


Procedure InitMonths;
{ Initializes the monthLen array.  This array containg the total days
  expired since the beginning of the year for the first day of each month. }

begin { InitMonths }

   MonthLen[1]  := 0;
   MonthLen[2]  := 31;
   MonthLen[3]  := 59;
   MonthLen[4]  := 90;
   MonthLen[5]  := 120;
   MonthLen[6]  := 151;
   MonthLen[7]  := 181;
   MonthLen[8]  := 212;
   MonthLen[9]  := 243;
   MonthLen[10] := 273;
   MonthLen[11] := 304;
   MonthLen[12] := 334;
   MonthLen[13] := 365;
end; { InitMonths }

{-------------------------------------------------------------------------------}


begin { InitCalendar }

   { init the length of the months }
   InitMonths;

   { init the date format to the tool date format }
   SetDateCode(toolDateFormat, toolMM_DD_YY);

   { Set up the known date for the day of week calculations.     }
   { Know date must fall on a monday else DayOFWeek must change. }
   with KnownDate do begin
      month := 6;
      day := 1;
      Year := 1981;
      dayCode := Monday;
   end;

   { create a dummy calendar }
   calendar := Pointer(Ord(HAllocate( theHeap, 2)));
end; { InitCalendar }


{-------------------------------------------------------------------------------}

{$S SgPeFile}

Procedure DateDefaults; external; {in file aplp/menus}

Function DfltCalendar{:boolean};
{ default all the calendar data.  this gets called by the fileio unit when reading in
  a new file  called only if this is a brand new file. }

var i: integer;

begin { DfltCalendar }

   { default to five working days per week }
   DaysPerWeek := 5;

   { set up the dates and calendar }
   InitCalendar;

   { set up default calendar start date }
   with CalStartDate do begin
      month := 1;
      day   := 1;
      year  := 1984;
   end;
   DayOfWeek(CalStartDate);

   { default ending year of calendar }
   endingYear := 1984;

   { and holidays  }
   for i := 1 to maxholiday do begin
      holidate[i].RD := maxval;
   end;

   { create the calendar }
   if FillCalendar then begin
      DateDefaults;
      dfltCalendar := true;
   end { if }
   else dfltCalendar := false;

end; { DfltCalendar }
ÿ