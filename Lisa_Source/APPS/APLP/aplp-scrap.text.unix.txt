{----------------------------------------------------------------------------}
{                                                                            }
{                  Copyright 1983,1984, Apple Computer Inc.                  }
{                                                                            }
{----------------------------------------------------------------------------}

{$S MainLoop}

Procedure ShowScrap;
{ Draw scrap folder. }

var error: integer;
    displayRect: rect;
    picture: pichandle;
    contents: TB;

begin { ShowScrap }

   { set theport to scrap, set clipping, erase }
   setport(scrapFolder);
   setorigin(0,0);
   with scrapfolder^ do SetRectRgn(cliprgn, portrect.left, portrect.top,
   portrect.right - dhsbox + 1, portrect.bottom - dvsbox + 1);
   EraseRect(scrapFolder^.portrect);

   { if I think I have text then drawscrap }
   if myscrapConts = atextsel then begin
      DrawScrap(error);
      {$IFC PEDebug}
      if error <> 0 then writeln('***error drawing field in scrap ',error);
      {$ENDC}
   end

   { otherwise draw a picture }
   else if (myscrapConts = atask) or (myscrapConts = adocument) then begin
      StartGetScrap( error );
      if error <> 0 then
      {$IFC PEDebug}
      writeln('***error in start get scrap ', error)
      {$ENDC}
      else begin
         picture := nil;
         GetGRScrap(picture);
         if picture <> nil then begin
            displayRect := picture^^.picFrame;
            offsetRect(displayRect, scrapfolder^.portrect.left - displayrect.left,
            scrapfolder^.portrect.top - displayrect.top);
            DrawPicture(picture, displayRect);
         end;
      end; { else }
      EndGetScrap( error);
      {$IFC PEDebug}
      if error <> 0 then writeln('***error in end get scrap ',error);
      {$EndC}
   end; { else }
end; { ShowScrap }


{-------------------------------------------------------------------------------}

{$S Events}

Procedure CopyText( oldtask, newtask: htask; var error: boolean);
{ copies text from one task to another creating a new set of text records
  in the current heap in the process. }

var
   newtext:  htext;
   lastText: htext;
   curtext:  htext;
   i:        integer;

begin { CopyText }

   { create a duplicate set of text lines }
   curtext := oldtask^^.title;
   lasttext := nil;
   while curtext <> nil do begin
      if NewTextRec(curtext^^.maxlen, newtext) then begin
         with newtext^^ do begin
            next := nil;
            curlen := curtext^^.curlen;
            maxlen := curtext^^.maxlen;
            {$R-}
            for i := 0 to maxlen - 1 do text^^[i] := curtext^^.text^^[i];
            {$R+}
            if curtext = oldTask^^.title then newTask^^.title := newText
            else lastText^^.next := newText;
            lastText := newtext;
         end; { with }
         curtext := curtext^^.next;
      end
      else begin
         { ran out of memory }
         error := true;
         Exit(CopyText);
      end; { else }
   end; { while }
   error := false;
end; { CopyText }


{------------------------------------------------------------------------------}

Procedure CopyResources( oldtask, newtask: htask; var newname: hname; var error: boolean);
{ copies the resources from one task to another, creating new resource
  records and name records in the current heap }

var
   lastresc,
   curresc,
   newresc:  hresc;
   thedata:  hnddata;
   j:        integer;

begin { CopyResources }

   curresc := oldtask^^.resource;
   lastresc := nil;
   while curresc <> nil do begin

      { create the resource record and link in to list }
      if NewRescRec(curresc^^.dmaxlen, curresc^^.rmaxlen, newresc) then begin

         with newresc^^ do begin
            dcurlen := curresc^^.dcurlen;
            dmaxlen := curresc^^.dmaxlen;
            {$R-}
            for j := 0 to dmaxlen  - 1 do dtext^^[j] := curresc^^.dtext^^[j];
            {$R+}

            duration  := curresc^^.duration;
            remaining := curresc^^.remaining;

            rcurlen := curresc^^.rcurlen;
            rmaxlen := curresc^^.rmaxlen;
            {$R-}
            for j := 0 to rmaxlen  - 1 do rtext^^[j] := curresc^^.rtext^^[j];
            {$R+}
            rname := nil;
            next := nil;
            nextResc := nil;
            nextTask := nil;
         end; { with }

         { set up the resource name and the newname list }
         FindResc(newname, newresc); {!!!!what about out of memory!!!}


         if lastresc <> nil then lastresc^^.next := newresc
         else newtask^^.resource := newresc;
         lastresc := newresc;
         curresc := curresc^^.next;
      end { if }
      else begin
         { ran out of memory }
         error := true;
         Exit(CopyResources);
      end; { else }

   end; { while }
   error := false;
end; { CopyResources }


{------------------------------------------------------------------------------}


Procedure CopyTask( oldtask: htask;  var newtask: htask; var newname: hname; var error: boolean);
{ creates a copy of the old task and puts into newtask parameter.
  sets links and flink, blink to nil. }

begin { CopyTask }

   { create the new task record and fill in data }
   if NewTaskRec(0, NewTask) then begin

      with newtask^^ do begin

         box       := oldtask^^.box;
         milestone := oldtask^^.milestone;

         earlystart:= minval;
         latestart := maxval;

         flink     := nil;
         blink     := nil;

         title     := nil;
         resource  := nil;

         dotline   := oldtask^^.dotline;
         maxduration:= oldtask^^.maxduration;

         SetStart  := oldtask^^.setStart;
         SetFinish := oldtask^^.setFinish;
         FixedCost := oldTask^^.FixedCost;

         NForward   := 0;
         NBackward  := 0;
      end;

      CopyText(oldtask, newtask, error);
      if not error then CopyResources( oldtask, newtask, newName, error);
   end { if }

   { out of memory }
   else error := true;
end; { CopyTask }


{------------------------------------------------------------------------------}


Procedure TaskToScrap( newtask: htask; newname: hname; var scrapContents: tScrapHnd; var error:boolean);
{ create the scrap record and puts the new task into the record }

begin { TaskTOScrap }

   ScrapContents :=  Pointer(Ord(HAllocate( theHeap, SizeOf(tScrapRec))));
   if Pointer(ord(ScrapContents)) = Hnil then error := true
   else with scrapContents^^ do begin
      tag  := atask;
      task := newtask;
      Resc := newname;

      { put the box at 0,0 }
      OffSetRect( task^^.box, -task^^.box.left, -task^^.box.top);
      error := false;
   end; { with }
end; { TaskToScrap }


{------------------------------------------------------------------------------}


Procedure PECopyTask{(curtask: htask)};
{ called on a copy command when task is currently selected }

var errnum: integer;
    mySaveHeap: tHz;
    newname:    hname;
    newtask:    htask;
    ScrapContents: tScrapHnd;
    mypicture:     pichandle;
    bigrect:       rect;
    newbox:        rect;
    error:         boolean;
    datesOn:       boolean;    { temp for copy of showdates boolean }

Procedure ErrorExit(message: integer);
begin
   case message of
      29: begin
         {$IFC PEDebug}
         writeln('scrap error is ',errnum);
         {$EndC}
         StopAlert(PeAlert, 29 {scrap Error});
      end;

      50: begin
         ParamAlert('Copy', '','');
         StopAlert( PeAlert, 50 {Out Of Memory});
      end;
   end; { case }

   endputscrap(errnum);
   setHeap(mysaveHeap);
   myscrapConts := nothing;
   exit(pecopytask);
end;

begin { PECopyTask }

   GetHeap(mySaveHeap);

   { claim the scrap, erase old data }
   InHeritScrap(true);
   EraseScrapData(errnum);
   if errnum>0 then ErrorExit(29);

   { set up heap to scrap's heap }
   SetHeap(HzofScrap);
   StartPutScrap(errnum);
   if errnum<>0 then ErrorExit(29);

   { save my data off to scraps heap }
   newname := nil;
   CopyTask(curtask, newtask, newname, error);
   if error then ErrorExit(50);

   TaskToScrap(newtask, newname, scrapContents, error);
   if error then ErrorExit(50);
   myscrapConts := atask;
   PutScrap(ScrapPert, pointer(ord(scrapContents)), errnum);
   if errnum<>0 then errorExit(29);

   { now put the picture in there too }
   drawingscrap := true;
   DatesOn := ShowDates;
   showDates := false;
   setport(scrapFolder);
   setorigin(0,0);
   ScaleRect(bigrect, newtask^^.box);
   ClipRect(bigrect);
   MyPicture := OpenPicture(bigrect);
   scalerect(newbox, newtask^^.box);
   DrawTask(newtask, newbox);
   ClosePicture;
   drawingScrap := false;
   showDates := DatesOn;
   PutGrScrap(myPicture, errnum);
   if errnum<>0 then ErrorExit(29);

   { thats it for the put, reset heap }
   EndPutScrap(errnum);
   if errnum <> 0 then errorExit(29);

   SetHeap(mySaveHeap);

   { and display the new scrap contents }
   ShowScrap;
end; { PECopyTask }


{------------------------------------------------------------------------------}


Procedure PEPasteTask{(curtask: htask)};
{ A task is selected. See about pasting the current scrap contents over it }
var taskBox: rect;
    error:   boolean;
    errnum:  integer;
    newtask: htask;
    PasteAt: point;
    which:   scrapType;
    scrapContents: tScrapHnd;
    myhandle:      tH;

Procedure ErrorExit(message: integer);
begin
   case message of
      29: begin
         StopAlert(PeAlert, 29 {scrap Error});
         {$IFC PEDebug}
         writeln('scrap error is ',errnum);
         {$ENdC}
      end;

      50: begin
         ParamAlert('Paste', '','');
         StopAlert( PeAlert, 50 {Out Of Memory});
      end;
   end; { case }

   endgetscrap(errnum);
   myscrapConts := nothing;
   exit(pepastetask);
end;

begin { PEPasteTask }

   if MyScrapConts = Nothing then StopAlert(pealert, 55 {Scrap Empty})
   else begin
      if MyscrapConts = aTask then begin
         { scroll task in, check if we can cut task }
         ScaleRect(taskbox, curtask^^.box);
         ScrollInRect(taskbox);
         RectCntr(PasteAt, curtask^^.box);

         { get the scrap contents, check if we have a task }
         StartGetScrap( errnum );
         if errnum <> 0 then errorExit(29);

         GetScrap(which, MyHandle);
         ScrapContents := pointer(ord(myhandle));

         if which = ScrapPert then begin
            with Scrapcontents^^ do begin
               CopyTask(ScrapContents^^.task, newtask, names, error);
               if error then ErrorExit(50);

               ScaleRect(taskbox,newtask^^.box);
               OffSetRect( taskbox,
                  PasteAt.h - (taskbox.right + taskbox.left) div 2,
                  PasteAt.v - (taskbox.bottom + taskbox.top) div 2);
               unScaleRect(newtask^^.box, taskbox);
               AddTask(newtask);

               RemoveTask( curtask);
               docChangedFlag := true;
               curtask^^.flink := nil;
               FreeTaskList(names,curtask);
               curselrec[pert].tag := air;
               if showdates then begin
                  CalcSchedule;
                  ShowContents(myfolder);
               end
               else begin
                  {ScaleRect(taskbox, newtask^^.box);}
                  setport(myfolder);
                  cliprect(myfolder^.portrect);
                  showcontents(myfolder);
               end;
            end; { with }
         end { if }
         else begin
            ParamAlert('a Task','','');
            StopAlert(PEAlert, 25 {Clip Incompatible});
         end; { else }
         endGetScrap(errnum);
         if errnum <> 0 then ErrorExit(29);
      end { if }
      else begin
         ParamAlert('a Task', '','');
         StopAlert(PeAlert, 25 {Clip Incompatible});
      end; { else }
   end; { else }
end; { PePasteTask }


{------------------------------------------------------------------------------}


Procedure PECutTask{(curtask: htask)};

var errnum: integer;
    mysaveHeap: tHz;
    newtask:    htask;
    newname:    hname;
    mypicture:  pichandle;
    taskBox:    rect;
    error:      boolean;
    scrapContents: tScrapHnd;
    bigrect:    rect;
    newbox:     rect;
    datesOn:    boolean;    { temp for copy of showdates boolean }

Procedure ErrorExit(Message:integer);
begin
   case message of
      29: begin
         StopAlert(PeAlert, 29 {scrap Error});
         {$IFC PEDebug}
         writeln('scrap error is ',errnum);
         {$ENdC}
      end;

      50: begin
         ParamAlert('Cut', '','');
         StopAlert( PeAlert, 50 {Out Of Memory});
      end;
   end; { case }
   endputscrap(errnum);
   setheap(mysaveHeap);
   myscrapConts := nothing;
   exit(pecuttask);
end;

begin
   GetHeap(mysaveHeap);

   ScaleRect(taskbox, curtask^^.box);
   ScrollInRect(taskbox);
   RemoveTask(curtask );
   docChangedFlag := true;

   { claim the scrap, erase old data }
   InHeritScrap(true);
   EraseScrapData(errnum);
   if errnum<>0 then errorExit(29);

   { set up the scrap's heap }
   SetHeap(HzofScrap);
   StartPutScrap(errnum);
   if errnum<>0 then errorExit(29);

   { save my data off also }
   newname := nil;
   CopyTask(curtask, newtask, newname, error);
   if error then ErrorExit(50);

   TaskToScrap( newtask, newname, scrapContents, error);
   if error then ErrorExit(50);
   myScrapConts := atask;
   PutScrap(ScrapPert, pointer(ord(scrapContents)), errnum);
   if errnum <> 0 then errorExit(29);

   { now put the picture in there }
   drawingscrap := true;
   datesOn := showDates;
   showdates := false;
   setport(scrapFolder);
   setorigin(0,0);
   scalerect(bigrect, newtask^^.box);
   ClipRect(bigrect);
   MyPicture := OpenPicture(bigrect);
   scalerect(newbox, newtask^^.box);
   DrawTask(newtask, newbox);
   ClosePicture;
   drawingScrap := false;
   showDates := datesOn;
   PutGrScrap(myPicture, errnum);
   if errnum <> 0 then errorExit(29);

   EndPutScrap(errnum);
   if errnum <> 0 then errorExit(29);

   SetHeap(mySaveHeap);
   ShowScrap;

   curselrec[activechart].tag := air;
   FreeTask(names, curtask );
   if showdates then CalcSchedule;
   setport(myfolder);
   cliprect(myfolder^.portrect);
   ShowContents(myfolder);
end; { PECutTask }


{------------------------------------------------------------------------------}

{$S MenuStuff}
Procedure PECopyDocument;

var errnum: integer;
    mySaveHeap: tHz;
    newhead,
    newtail:    htask;
    newname:    hname;
    newtask:    htask;
    ScrapContents: tScrapHnd;
    mypicture:     pichandle;
    bigrect,
    paper:       rect;

Procedure ErrorExit(Message:integer);
begin
   case message of
      70: begin
         StopAlert(PeAlert, 70 {Picture Size});
      end;

      29: begin
         StopAlert(PeAlert, 29 {scrap Error});
         {$IFC PEDebug}
         writeln('scrap error is ',errnum);
         {$ENdC}
      end;

      50: begin
         ParamAlert('Copy', '','');
         StopAlert( PeAlert, 50 {Out Of Memory});
      end;
   end; { case }
   endputscrap(errnum);
   setheap(mysaveHeap);
   myscrapConts := nothing;
   exit(pecopydocument);
end;

Procedure PertXRect(VAR BigRect: Rect);
{ returns myRect as the dimensions of a rect enclosing all the task boxes }
VAR
   myRect,
   scaled : Rect;
   curTask: hTask;
Begin
   curTask := Head;
   SetRect(myRect, 0, 0, 0, 0);
   While (curTask <> NIL) do Begin
      ScalOffRect(scaled, curtask^^.box);
      UnionRect(scaled, myRect, myRect);
      curTask := curTask^^.fLink;
   end;
   With myRect do Begin
      right  := right  + 10;
      bottom := bottom + 10;
   end;
   BigRect := myRect;
end;

begin
   GetHeap(mySaveHeap);

   InHeritScrap(true);
   EraseScrapData(errnum);
   if errnum <> 0 then ErrorExit(29);

   SetHeap(HzofScrap);
   StartPutScrap(errnum);
   if errnum<>0 then errorExit(29);

   { put my data in there }
   ScrapContents :=  Pointer(Ord(HAllocate( theHeap, SizeOf(tScrapRec))));
   if ScrapContents = nil then ErrorExit(50);

   scrapContents^^.tag  := aDocument;
   myscrapConts := aDocument;
   PutScrap(ScrapPert, pointer(ord(scrapContents)), errnum);
   if errnum <> 0 then ErrorExit(29);

   { now put the picture in there }
   setport(scrapFolder);
   drawingScrap := true;
   scalepaper(paper, activechart);
   setorigin(paper.left, paper.top);
   ClipRect(paper);

   { Get the enclosing rect and draw the picture }
   If (activeChart = Pert) then Begin
      PertXRect(BigRect);
      myPicture := OpenPicture(bigRect);
      showPert;
   end
   else begin
      myPicture := OpenPicture(paper);
      showGantt;
   end;

   ClosePicture;
   drawingscrap := false;

   { check size of picture for overflow,  >32K }
   if myPicture^^.picsize < 0 then ErrorExit(70);


   PutGrScrap(myPicture, errnum);
   if errnum<>0 then ErrorExit(29);

   EndPutScrap(errnum);
   if errnum<>0 then errorExit(29);

   SetHeap(mySaveHeap);

   ShowScrap;
end;


{------------------------------------------------------------------------------}

{$S MenuStuff}
Procedure PEPasteAir;

var taskBox:       rect;
    error:         boolean;
    errnum:        integer;
    newtask:       htask;
    PasteAt:       point;
    which:         scrapType;
    scrapContents: tScrapHnd;
    myhandle:      tH;
    mySaveHeap:    tHz;

Procedure ErrorExit(Message:integer);
begin
   case message of
      29: begin
         StopAlert(PeAlert, 29 {scrap Error});
         {$IFC PEDebug}
         writeln('scrap error is ',errnum);
         {$ENdC}
      end;

      50: begin
         ParamAlert('Paste', '','');
         StopAlert( PeAlert, 50 {Out Of Memory});
      end;
   end; { case }
   endGetScrap(errnum);
   {myscrapConts := nothing;}
   exit(pepasteair);
end;

begin
   GetHeap(mySaveHeap);

   if (MyScrapConts = nothing) then StopAlert(pealert, 55 {Scrap Empty})

   { only a task can be pasted over air }
   else if (myScrapConts <> atask) then begin
      ParamAlert('a task', '', '');
      StopAlert(PEAlert, 25);
   end

   { a task box can only be pasted into the pert chart }
   else if (activeChart <> Pert) then Begin
      ParamAlert('Paste','','');
      StopAlert(Pealert, 45 {Selection Required});
   end

   else if (MyScrapConts = atask) then begin
      { get the scrap contents, check if we have a task }
      StartGetScrap( errnum );
      if errnum <> 0 then ErrorExit(29);

      GetScrap(which, MyHandle);
      ScrapContents := pointer(ord(myhandle));

      if which = ScrapPert then begin
         if EnoughSpace( theHeap,
                         SizeTaskRec +
                         Linecount(Scrapcontents^^.task) * SizeTextRec +
                         RescCount(ScrapContents^^.task) * (sizeRescRec + sizeNameRec) ) then begin
            CopyTask(ScrapContents^^.task, newtask, names, error);
            if error then ErrorExit(50);

            ScaleRect(taskbox,newtask^^.box);
            OffSetRect( taskbox,
               curselrec[pert].curselloc.h - (taskbox.right + taskbox.left) div 2,
               curselrec[pert].curselloc.v - (taskbox.bottom + taskbox.top) div 2);
            unScaleRect(newtask^^.box, taskbox);
            AddTask(newtask);

            docChangedFlag := true;
            setport(myfolder);
            if showdates then begin
               cliprect(myfolder^.portrect);
               CalcSchedule;
               ShowContents(myfolder);
            end
            else begin
               insetrect(taskbox, -3,-2);
               cliprect(taskbox);
               showcontents(myfolder);
            end;
         end { if }
         else ErrorExit(50);
      end { if }
      else begin
         ParamAlert('a Task','','');
         StopAlert(PEAlert, 25 {Clip Incompatible});
      end; { else }
      endGetScrap(errnum);
      if errnum<>0 then errorExit(29);
   end { if }
end; { PePasteAir }
ÿ