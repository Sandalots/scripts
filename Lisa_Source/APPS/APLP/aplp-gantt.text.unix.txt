
{------------------------------------------------------------------------------}
{                  Copyright 1983,1984, Apple Computer Inc.                    }
{                                                                              }
{      Gantt and Task chart routines.                                          }
{                                                                              }
{  Both the Gantt and task charts are  created, draw, and events processed     }
{  in this unit.  The activechart is drawn, with a slight variation if this is }
{  the Gantt chart or the task chart.                                          }
{                                                                              }
{                                                                              }
{------------------------------------------------------------------------------}


UNIT PeGantt;


INTERFACE

USES
   {$U UnitStd    }  UnitStd,    { The basics }
   {$U UnitHz     }  UnitHz,
   {$U LibOs/SysCall    }  SysCall,

   {$U QuickDraw  }  QuickDraw,
   {$U FontMgr    }  FontMgr,    { Fonts & LisaGraf }
   {$U Storage    }  Storage,
   {$U GrafUtil   }  GrafUtil,

   {$U PmDecl     }  PmDecl,
   {$U Pmm        }  Pmm,

   {$U WM.Events  }  Events,     { The window manager }
   {$U WM.Folders }  Folders,
   {$U WM.Windows }  Windows,
   {$U WM.Menus   }  Menus,
   {$U wmlstd     }  WMlstd,
   {$U wmlsb      }  WMlsb,
   {$U wmlcrs     }  WMlcrs,
   {$U wmlgrow    }  WMlGrow,

   {$U PrStdInfo  }  PrStdInfo,
   {$U PrPublic   }  PrPublic,

   {$U AlertMgr   }  AlertMgr,
   {$U Dbenv      }  Dbenv,
   {$U scrap      }  Scrap,
   {$U fedec      }  fedec,
   {$U fld        }  fieldEdit,   { Misc managers }

{$SETC IsIntrinsic = True}
{$SETC WithUObject = False}
{$SETC UTProblems  = False}
{$SETC LibraryVersion = 30}
   {$U Uunivtext  }  UUniversaltext,

   {$U aplp/globals  }  PeGlobals,
   {$U aplp/dmgr     }  Pedmgr,
   {$U aplp/general  }  PeGeneral,
   {$U aplp/cost     }  PECost;        { My guys }


{$IFC PESymbols}
{$D+}
{$ElseC}
{$D-}
{$EndC}



const
   lastGAtrCk   = 1;        { last check for for gantt drawing attbs      }
   lastUpdEd    = 0;        { last update form ed field number            }
   IHOff        = 20;       { initial H offset for bars from namerect     }
   initHoffset  = 4;        { offset of characters within the bars        }
   initVoffset  = 5;        { ditto for v                                 }

   dGrid        = 112;      { grid tick lines are 112 pixels apart        }
   RescOffV     =  6;       { distance in V between each resource's bars  }



var

   GGridForm:  TCkFormHnd;  { dialog box check for for grid increment sel }
   PicsPerDur: integer;     { number pixels per duration value, scaled    }

   timerect,                { the rect for the time line at top of screen }
   namerect:   rect;        { the rect for the resource names at left     }

   UpdateForm: TEDFormHnd;  { the gantt chart update form                 }




{ drawing/printing routines }
Procedure ShowGantt;
Procedure CreateGantt;

{ event handlers }
Procedure GanttDeselect     (var error: boolean);
Procedure GanttUp;
Procedure GanttDown         (DownPt: point);
Procedure GanttKey          (theEvent: EventRec);




IMPLEMENTATION
{$IFC fsymok}
{$D+}
{$ELSEC}
{$D-}
{$ENDC}
{$IFC fdbgok}
{$R+}
{$ELSEC}
{$R-}
{$ENDC}




{-------------------------------------------------------------------------------}

{$S MenuStuff}

{$R-}
Procedure DrawBar( sched: hResc; resc: rect);
{ Draws/Prints one bar in the schedule chart, the rect, the slack, and the text. }

type
    PStr255 =  ^Str255;


var
    curline:  hText;     { the line pointer for each line of text   }
    curresc:  hresc;
    I,J:      integer;   { a counter for printing, till next FE rels}
    barrect,             { the bar in the chart representing task   }
    temprect: rect;      { a temporary rect for slack and complete  }
    title:      String[52];
    Addrs:      PStr255;
    specialBool: Boolean;

begin { DrawBar }

   ScalOffRect( barrect, Sched^^.bar);
   If Printing then SpecialBool := SectRect(barRect, PrintRect, temprect)
   else SpecialBool := DrawingScrap;
   if specialBool or
      ((barrect.top < theport^.portrect.bottom) and (barrect.bottom > theport^.portrect.top)) then begin
      PenNormal;
      Fillrect( barrect, white);

      { determine how much is completed and how much is slack }
      with sched^^ do begin
         with task^^ do begin
            j := RDequiv(earlystart);

            { first days completed with dark gray }
            if (remaining <> duration) and (activechart = rescGantt) then begin
               temprect := barrect;
               i := RDEquiv(earlystart + duration - remaining);
               if (i = maxval) then {fill in all of barrect}
               else temprect.right := barrect.left + picsperdur * (i - j);
               fillrect(temprect, dkgray);
            end; { if }

            { then slack with light gray }
            if earlystart < latestart then begin
               temprect := barrect;
               if activechart = RescGantt
               then i := RDEquiv(earlystart + duration)
               else i := RDEquiv(earlystart + maxduration);
               if i = maxval then {no slack displayed}
               else begin
                  temprect.left := barrect.left + picsPerdur * (i - j);
                  fillrect(temprect, ltgray);
               end; { else }
            end; { if }
         end;
      end;
      framerect( barrect );

      { move to starting point for text }
      moveto( barrect.left +  initHoffset,
              barrect.bottom - initVoffset );

      if (Scale[activechart].state = ScaleReal) or printing or drawingScrap then begin

         { set up for font characteristics, bolding, shadow, and source bit clear }
         SetUpFont;
         if not drawingScrap then begin
            TextFace( Pertfontinfo[activechart].myface + [shadow] );
            TextMode( srcBic );
         end { if }
         else begin
            TextFace( Pertfontinfo[activechart].myface + [outline]);
         end; { else }

         { if its a resource chart then dump the title lines }
         if activechart = RescGantt then begin
            curline := sched^^.task^^.title;
            while curline <> nil do
            with curline^^ do begin
               Addrs := pointer(ord(@title) + 1);
               MoveLeft(text^^, Addrs^, curlen);
               Addrs := pointer(ord(@title));
               MoveLeft(curlen, Addrs^, 1);
               Title := Concat(Title,' ' );
               DrawString(title);
               curline := curline^^.next;
            end; { while }
         end { if }

         { otherwise dump all the resource names }
         else begin
            curresc := sched^^.task^^.resource;
            while curresc <> nil do begin
               with curresc^^ do begin
                  Addrs := pointer(ord(@title) + 1);
                  MoveLeft(rtext^^, Addrs^, rcurlen);
                  Addrs := pointer(ord(@title));
                  MoveLeft(rcurlen, Addrs^, 1);
                  if (Rcurlen <> 0) and (next <> nil) then Title := Concat(Title,', ' );
                  DrawString(title);
               end; { with }
               curresc := curresc^^.next;
            end; { while }
         end; { else }
      end; { if }

      with curselrec[activechart] do
         if (not printing) and (not drawingscrap) then
            if (tag = document) or ((tag = bar) and (hitresc = sched)) then begin
               InsetRect(barrect,1,1);
               InvertRect(barrect);
            end; { if }

      { restore the font }
      SetUpFont;
   end; { if }
end; { DrawBar }

{------------------------------------------------------------------------------}


Procedure DrawTasks( time, resc, paper: rect; shoff, dgrd: integer; LabelRgn, BarRgn: rgnHandle);

type
    PStr255 =  ^Str255;

var curname:   hname;    { current name record in the list    }
    curresc:   hresc;    { current schedule record in the list}
    curline:   htext;    { current line of text for task      }
    I,                   { counter for chars in text arrays   }
    barHeight,           { scaled height of a bar             }
    locv:      integer;  { scaled offset for label value      }
    Title:     String[50];
    Addrs:     PStr255;

begin { DrawTasks }

   { if this is a resource gantt chart }
   if activechart = RescGantt then begin
      curname := names;
      while curname <> nil do begin
         curresc := curname^^.schedule;
         while curresc <> nil do begin
            copyRgn( BarRgn, theport^.cliprgn );
            drawBar( curresc, resc );

            if (Scale[activechart].state = ScaleReal) or printing or drawingScrap then begin
               { now the labels }
               if curresc^^.nextresc = nil then begin
                  { write out the name }
                  CopyRgn( LabelRgn, theport^.cliprgn );
                  scalOffVInt(locv, curresc^^.bar.bottom - InitVOffset);
                  MoveTo( Resc.left + 5, locV);
                  with curresc^^ do begin
                     Addrs := Pointer(ord(@title) + 1);
                     MoveLeft(rtext^^, addrs^, rcurlen);
                     Addrs := pointer(ord(@title));
                     MoveLeft(rcurlen, addrs^, 1);
                     DrawString(Title);
                  end; { with }
               end; { if }
            end; { if }

            curresc := curresc^^.nextresc;
         end; { while }
         curname := curname^^.next;
      end; { while }
   end { if }

   { otherwise it is a task gantt chart }
   else begin
      curresc := ESOrder;
      while curresc <> nil do begin
         CopyRgn( BarRgn, theport^.cliprgn);
         drawbar( curresc, resc );

         { now the labels }
         if (Scale[activechart].state = ScaleReal) or printing or drawingScrap then begin
            CopyRgn( LabelRgn, theport^.cliprgn );
            scalOffVInt(locv, curresc^^.bar.bottom - InitVOffset);
            curline := curresc^^.task^^.title;
            ScaleVInt( barHeight, (curresc^^.bar.bottom - curresc^^.bar.top));
            while curline <> nil do
            with curline^^ do begin
               MoveTo( Resc.left + 5, locV);
               Addrs := pointer(ord(@title) + 1);
               MoveLeft(text^^, addrs^, curlen);
               Addrs := pointer(ord(@title));
               MoveLeft(curlen, addrs^, 1);
               DrawString(Title);
               LocV := locV + Pertfontinfo[activechart].lspace;
               curline := curline^^.next;
            end; { if }
         end; { if }

         curresc := curresc^^.Nexttask;
      end; { while }
   end; { else }

   { if select all then invert the resc rect }
   if ((curselrec[activechart].tag = Document) and (not printing) and (not drawingscrap))  then
   InvertRect(Resc);
end; { DrawTasks }


{------------------------------------------------------------------------------}


Procedure DrawGrid( resc, paper: rect);
{ Draw the grid lines in light gray, spaced dgrid pixels apart and
  dRescLine + IHOff from the left margin.  }

var loc:   integer;
    sloc:  integer;

begin { DrawGrid }

   Pennormal;
   penpat(gray);

   if printing then pensize(2,1);

   loc := NameRect.right + Ihoff;
   ScalOffHInt(sloc, loc);

   while (sloc <= paper.right) do begin
      moveto( sloc, resc.top);
      lineto( sloc, resc.bottom);
      loc := loc + dgrid;
      ScalOffHInt(sloc,loc);
   end; { while }
end; { DrawGrid }


{------------------------------------------------------------------------------}


Procedure DrawToday( resc, paper: rect; shoff: integer);

var loc: integer;

begin { DrawToday }

   if today <> 0 then begin
      pennormal;
      pensize(5,0);
      loc := NameRect.right + Ihoff + trunc(dgrid / GridIncrement[curgridsize] * (today - startTime));
      ScalOffHInt(loc, loc);
      moveto(loc, resc.top);
      lineto(loc, resc.bottom);
   end; { if }
end; { drawToday }


{------------------------------------------------------------------------------}



Procedure DrawTimeLabels(time, paper: rect);
{ Draws the time line across the top of the folder or drawing }

var Date:      String30; { the date string drawn on the time line }
    day:       integer;  { the real day offset for date }
    loch:      integer;  { unscaled h location }
    sloch:     integer;  { scaled h location }
    sloch2:    integer;  { scaled h loc for the incremental cost }

    slocv0,              { v loc for the cumulative cost  }
    slocv1,              { v loc for the incremental cost }
    slocv2   : integer;  { v loc for the date             }

    pDate    : QDPtr;
    hDate    : hndData;
    bucketNum: Integer;  { index to bucket holding the cum cost figure }
    MaxLen   : Integer;
    Incremental: Longint;{ the incremental cost for a time period }

begin { DrawTimeLabels }

   { only display if unscaled }
   if (Scale[activechart].state = ScaleReal) or printing or drawingScrap then begin

      PenNormal;
      loch := NameRect.right + Ihoff;
      ScalOffHInt(sloch, loch);

      With PertFontInfo[activechart] do Begin
         slocv0 := (time.bottom + time.top) div 2 - lSpace;
         slocv1 := slocv0 + 5*lspace div 4;
         slocv2 := slocv1 + 5*lspace div 4;
      end;

      if head = nil then day := 0
                    else day := starttime;

      { Init things for drawing the cost figures }
      BucketNum := -1;
      pDate := pointer(ord(@Date));
      hDate := pointer(ord(@pDate));

      while (sloch <= paper.right) do begin
         { Convert and draw the cumulative cost figure }
         MaxLen := 30;
         LongToASC(Bucket[BucketNum], MaxLen, hDate);
         Moveto(slocH - TextWidth(pDate, 0, MaxLen) div 2, slocv0);;
         DrawText(pDate, 0, MaxLen);

         { No incremental cost the first time through }
         If (BucketNum > -1) Then Begin
            Incremental := Bucket[BucketNum] - Bucket[BucketNum - 1];
            MaxLen := 30;
            LongToASC(Incremental, MaxLen, hDate);
            Moveto(slocH2 - TextWidth(pDate, 0, MaxLen) div 2, slocv1);;
            DrawText(pDate, 0, MaxLen);
         end;

         { Draw the date }
         if day > RDEquiv(calsize - 1) then Date := '??'
                                       else DateString( day, Date );
         moveto( sloch - StringWidth(date) div 2, slocv2 );
         DrawString(date);

         { Calc the new h positions }
         slocH2 := sloch;
         loch := loch + dgrid;
         ScalOffHInt(sloch,loch);
         slocH2 := (sloch + slocH2) div 2;

         day := day + gridIncrement[curgridsize];
         BucketNum := BucketNum + 1;
      end; { while }
   end;

   { if entire document selected then invert rect also }
   if ((curselrec[activechart].tag = document) and (not printing) and (not drawingScrap)) then
   InvertRect(time);

end; { DrawTimeLabels }


{------------------------------------------------------------------------------}



Procedure ShowGantt;
{ Redraws the gantt chart, task gantt and resource gantt }

var resc,                   { the scaled and offset resc label  }
    Time,                   { the scaled and offset time label  }
    paper:    rect;         { the scaled paper rect             }
    shoff,                  { scaled ihoff                      }
    dgrd:     integer;      { scaled grid distance              }
    LabelRgn,               { clip rgn for resc label           }
    BarRgn:   rgnHandle;    { clip rgn for main portion of chart}

begin { ShowGantt }

   { draw the paper }
   if (not Printing) and (not drawingscrap) then begin

      SetPort(myfolder);
      DrawPaper;
      EraseRect(myfolder^.portrect);
      if Preview then PreviewPrint(activechart);
   end; { if }

   { scale all the important vars for the chart }
   ScaleRect( Time,  TimeRect);
   ScaleRect( Resc,  NameRect);
   scalePaper( paper, activechart);
   ScaleHInt( shoff, IHOff);
   scaleHInt( dgrd,  dgrid);
   dgrd := CMax(dgrd, 1);
   ScaleHInt(picsperdur, dgrid div GridIncrement[curgridsize]);

   { if special draw then draw chart timeline at top of page }
   if printing or drawingScrap then begin
      OffsetRect(time, paper.left - time.left, paper.top - time.top);
      time.right := paper.right;
      offsetrect(resc, paper.left - resc.left, time.bottom - resc.top - 2);
      resc.bottom := paper.bottom;

      { if printing then offset to origin of printed page }
      if printing then begin
         offsetRect(time, prHoffset, prVOffset);
         offsetRect(resc, prHOffset, prVOffset);
         offsetRect(paper, prHoffset, prvOffset);
      end; { if }
   end { if }
   { otherwise offset time and resc rects to current folder left and top }
   else begin
      with myfolder^.portrect do begin
         OffsetRect(Time, CMax(left, paper.left) - time.left,
                          CMax(top,  paper.top ) - time.top);
         OffsetRect(Resc, CMax(left, paper.left) - resc.left,
                          time.bottom - resc.top - 2);
         { dont let the right and bottom edges overflow 32K }
         time.right := myfolder^.portrect.right;
         resc.bottom := myfolder^.portrect.bottom;
      end; { with }
   end; { else }

   { frame and fill the time and resc rects }
   PenNormal;
   Eraserect(Resc);
   Eraserect(Time);
   pensize(3,2);
   framerect(Resc);
   framerect(Time);
   pennormal;

   { set up the font }
   SetUpFont;

   { draw the time line labels }
   DrawTimeLabels(time, paper);

   { Set up appropriate clip rgns }
   LabelRgn := NewRgn;
   BarRgn   := NewRgn;

   { clip out the time rect from the current clip }
   SetRectRgn( LabelRgn, theport^.portrect.left, Time.bottom, paper.right, paper.bottom);
   SectRgn( theport^.cliprgn, LabelRgn, theport^.ClipRgn );

   { get the rgn that is only the resc rect }
   SetRectRgn( LabelRgn, resc.left, resc.top, resc.right, resc.bottom);
   SectRgn( theport^.cliprgn, LabelRgn, LabelRgn);

   { get the rgn that excludes the resc rect }
   DiffRgn( theport^.ClipRgn, LabelRgn, BarRgn);

   { draw the grid ticks and the current day }
   CopyRgn( BarRgn, theport^.ClipRgn);
   DrawGrid(resc, paper);
   DrawToday(resc, paper, shoff);

   { now draw the task bars and resource labels }
   DrawTasks( time, resc, paper, shoff, dgrd, LabelRgn, BarRgn );

   { draw the chart titles }
   CopyRgn( BarRgn, theport^.cliprgn );
   DrawTitles(activechart);

   { this added to draw a frame around gantt for printing and drawing scrap only }
   if printing or drawingScrap then begin
      pensize(3,2);
      if printing then ClipRect(printRect)
                  else ClipRect(thePort^.portrect);
      framerect(paper);
      pennormal;
   end;

   { dispose of two rgns }
   DisposeRgn(LabelRgn);
   DisposeRgn(BarRgn);
end; { ShowGantt }


{------------------------------------------------------------------------------}


procedure CreateGantt;
{ Creates the bar chart representation of the schedule,
  both the task and resource charts.  The gantt charts are
  constructed from bottom to top, and always draw in this
  direction also. }

var
   CurName:     hname;       { current name record                   }
   CurResc:     hResc;       { current schedule record               }
   InitialV,                 { the starting V location of chart      }
   curV:        LongInt;     { the current v location in the chart   }
   i:           integer;     { local counter                         }
   maxh,                     { max h coord used in chart             }
   maxv:        real;
   startingLoc,              { place where bars begin to be located  }
   endingLoc:   integer;
   answer:      string[3];


{------------------------------------------------------------------------------}


Procedure FindTimeScale;
{ the time scale is changed if the chart is too large with current font
  and current time scale. An alert is issued if the time scale is changed.
  28K is used as the upper bound on the chart.  This was due to problems
  encountered with overflow when using up to 32K. }

var NumDays:  Real;
    Gridsize,
    MinGridSize: integer;

begin
   { find the chart boundries, start and finish dates }
   if endTime = maxval
   then NumDays := (RDEquiv(calsize) - startTime)
   else NumDays := (EndTime - StartTime);

   GridSize := trunc((NumDays * dGrid * Pertfontinfo[activechart].actual)/28000.00 + 0.99 );
   if gridsize <= 7 then MinGridSize := 1        { one week ok }
   else if gridsize <= 14 then MinGridSize := 2  { must go to two weeks at least }
   else if gridsize <= 28 then MinGridSize := 3  { need four weeks  }
   else MinGridSize := 4;                        { need eight weeks }

   if curgridsize < MinGridSize then begin
      { put up an alert, only if we are active }
      if {fvisible} ImActive then NoteAlert(pealert, 99 {Chart too large});
      curgridsize := MinGridsize;
   end;
end;


{------------------------------------------------------------------------------}


Procedure PlaceBar;
{ This procedure determines the values of the bar, slack, and complete
  rects in the schedule record for a task. }

var RealES,
    RealLF: integer;
    TheDuration: integer;

begin { PlaceBar }
{$R-}
   with CurResc^^ do begin
      with task^^ do begin

         RealES := RDEquiv(earlystart);
         if RealES = maxval then begin
            { task scheduled off the calendar, put it at endingLoc with duration 0 }
            setrect(bar, endingLoc, curv, endingLoc + 4, curv + p15lspace + 6);
         end { if }
         else begin

            { rescource chart has resource duration, task chart has maxduration }
            if activechart = RescGantt
            then TheDuration := duration
            else TheDuration := maxduration;

            if latestart < earlystart
            then RealLF := RDEquiv(earlystart + theduration)
            else RealLF := RDEquiv(latestart  + theduration);

            if RealLF = maxval
            then setrect(bar, picsperdur * (RealES - StartTime) + StartingLoc,
                              curv, endingLoc, curv + p15lspace + 6)
            else setrect(bar, picsperdur * (RealES - StartTime) + StartingLoc,
                              curv, picsperdur * (RealLF - startTime) + StartingLoc,
                              curv + p15lspace + 6);

            { make sure bar is not null }
            if bar.left = bar.right then bar.right := bar.left + 4;
         end; { else }
      end; { with }
   end; { with }
{$R+}
end; { PlaceBar }


{------------------------------------------------------------------------------}


begin { CreateGantt }

   if (activechart = RescGantt) or (activeChart = TaskGantt) then begin

      { find the proper time scale, issue an alert on a change }
      FindTimeScale;

      { determine the pixels per duration value }
      PicsPerDur := dgrid div (GridIncrement[curgridsize]);

      StartingLoc :=  drescLine[activechart] + ihoff;
      if EndTime = maxval
      then EndingLoc := trunc((RDEquiv(CalSize) - StartTime) / GridIncrement[curGridsize] + 0.99) *
                   GridIncrement[curgridsize] * picsPerDur + StartingLoc
      else EndingLoc := trunc((endtime - StartTime)/ GridIncrement[curGridsize] + 0.99) *
                   GridIncrement[curgridsize] * picsPerDur + StartingLoc;

      { set the starting V value }
      InitialV := trunc(16000 / Pertfontinfo[activechart].actual);
      curV := InitialV;

      { which chart are we laying out ? }
      if activechart = RescGantt then begin

         { run through the schedule records for each resource name }
         CurName := names;
         while CurName <> nil do begin
            { define the bar rectangle in the schedule records for this name }
            CurResc := Curname^^.schedule;
            curV := curV - p15lspace - 6;
            while CurResc <> nil do begin
               PlaceBar;
               CurResc := curResc^^.nextResc;
               curV := curV - p15lspace - 6;
            end; { while }
            curV := curV - RescOffV;
            curName := curName^^.next;

            { check for overflow on V, just stop making the chart,
              no alert }
            if curv < -InitialV then curname := nil;
         end; { while }
      end
      else begin
         curResc := ESOrder;
         curV := curV - p15lspace - 6;
         while curResc <> nil do begin
            i := LineCount(curResc^^.task);
            if i > 1 then curv := curv - (p15lspace * (i - 1));
            PlaceBar;
            curResc := curresc^^.nexttask;
            curV := curV - p15lspace - 6 - RescOffV;

            { check for overflow, stop the chart }
            if curv < -InitialV then curresc := nil;
         end; { while }
      end; { else }

      { now make room for titles and timeline }
      curV := curV - dTitleV - dtimeline - 10;

      { figure out the drawing size }
      with Pertfontinfo[activechart] do begin
         MaxH := (EndingLoc + ihoff) * actual;
         if MaxH < PageH * 0.99 then MaxH := PageH * 0.99;
         MaxV := (InitialV - curv) * actual;
         if maxV < pageV * 0.99 then MaxV := PageV * 0.99;
         Curv := trunc(curv * actual);

         SetRect( PaperRect[activechart], 0, curV, trunc(MaxH), trunc(MaxV + curV));

         { set up the time rect and name rect according to paper size }
         SetRect(timeRect, 0, PaperRect[activechart].top, PaperRect[activechart].right,
         PaperRect[activechart].top + dTimeLine);
         SetRect(nameRect,0, TimeRect.bottom, dRescLine[activechart],
         PaperRect[activechart].bottom);
      end;

      { Adjust the titles & calc the cash flow }
      for i := 1 to maxtitles do Titles[activechart][i].dx.v :=
         dTitleV + paperRect[activechart].top +((i-1) * d2Title);
      CalcFlow; { Important! Since the timeline shows the cost figures }
   end

   { Create the table for any table charts }
   Else If (ActiveChart > 3) Then Begin
      CreatTbl;
   end;
end; { CreateGantt }





{------------------------------------------------------------------------------}
{                                                                              }
{             Gantt event handlers.                                            }
{                                                                              }
{------------------------------------------------------------------------------}

{$S Events}


Procedure GanttDeselect{( var error: boolean)};
{ This deselects the current selection for the gantt chart.  }
var barrect: rect;

Begin { GanttDeselect }

   { update the current selection record }
   error := false;
   With CurSelRec[activechart] do Begin
      Case Tag of

         ChartTitle: begin
            tag := air;
            ExitField( Titles[activechart][index].hTitleField, hfstate );
            ShowCursor;
         end;

         Document: begin
            tag := air;
            setport(myfolder);
            cliprect(myfolder^.portrect);
            ShowContents(myfolder);
         end;

         Bar: begin
            tag := air;
            setport(myfolder);
            ScaleRect(barrect, hitresc^^.bar);
            RectRgn(myfolder^.cliprgn, barrect);
            ShowContents(myfolder);
         end;

      End; { Case }
   End; { With }
End; { Ganttdeselect }


{-------------------------------------------------------------------------------}


Function BarHit(pt: point; var curname: hname;var curresc: hresc): boolean;

var found: boolean;
    barrect: rect;

begin { BarHit }
   if activechart = RescGantt then begin
      curname := names;
      while curname <> nil do begin
         curresc := curname^^.schedule;
         while curresc <> nil do begin
            scaleRect(barrect, curresc^^.bar);
            if ptinRect(pt, barrect) then begin
               barhit := true;
               exit(barHit);
            end; { if }
            curresc := curresc^^.nextresc;
         end; { while }
         curname := curname^^.next;
      end; { while }
   end; { if }
   BarHit := false;
end;  { BarHit }

{-------------------------------------------------------------------------------}


Procedure GanttDown{( downpt: point )};
{ Handles a mouse down in the activechart chart. Check for hit on the chart titles. }

var
   found:    boolean;
   answer:   integer;
   index,
   I:        integer;
   error:    boolean;
   movePt:   point;
   loc:      point;
   curname:  hname;
   curresc:  hresc;
   barrect:   rect;
   dResc,
   dTime:    integer;

begin { GanttDown }

   { check for double triple and single clicks on titles }
   with curselrec[activechart] do begin
      SetPort(myfolder);
      scaleHInt(dResc, dRescLine[activechart]);
      scaleVInt(dTime, dTimeLine);
      with myfolder^ do
         SetRectRgn( clipRgn, portrect.left + dResc, portrect.top + dTime,
         portrect.right - dhSbox + 1, portrect.bottom - dvSbox + 1);
      if (tag=chartTitles) and (clickCount=2) then begin
         SelectWord(downpt, Titles[activechart][index].htitlefield, hfstate,
         curnbrhood, curtimeout);
      end
      else begin
         if (tag=charttitles) and (clickcount=3) then begin
            ExitField(Titles[activechart][index].htitlefield, hfstate );
            SelectAll(Titles[activechart][index].htitlefield, hfstate, curtimeout);
         end
         else begin
            GanttDeselect( error );

            { check if point with area excluding rescline and timeline }
            with myfolder^ do
               SetRectRgn( clipRgn, portrect.left + dResc, portrect.top + dTime,
               portrect.right - dhSbox + 1, portrect.bottom - dvSbox + 1);
            if ptInRgn(downpt, myfolder^.cliprgn) then begin
               if barhit(downpt, curname, curresc) then begin
                  ScaleRect(barrect, curresc^^.bar);
                  setport(myfolder);
                  cliprect(myfolder^.portrect);
                  ScrollInRect(barrect);
                  RectRgn(myfolder^.cliprgn, barrect);
                  tag := bar;
                  hitname := curname;
                  hitresc  := curresc;
                  ShowContents(myfolder);
               end { if }
               else begin
                  TitleHit(downpt, activechart, I, found);
                  if found then begin
                     tag := ChartTitle;
                     index := I;
                     {ScrollinRect(Titles[activechart][i].htitleField^^.coords);}
                     SetPort(myfolder);
                     with myfolder^ do
                        SetRectRgn( clipRgn, portrect.left + dResc, portrect.top + dTime,
                        portrect.right - dhSbox + 1, portrect.bottom - dvSbox + 1);
                     InitFstate(Titles[activechart][index].hTitleField, hfstate);
                     Select( downpt,Titles[activechart][index].hTitleField,hfstate,
                     curNbrhood, curTimeout);
                     { generate the simulated move events }
                     If StillDown then begin
                        While StillDown Do begin
                           GetMouse(MovePt);
                           if Not PtinRect(Movept, curnbrhood) then begin
                              GrowSel( Movept, Titles[activechart][index].htitlefield,
                              hfstate, curNbrhood);
                              setrect(curnbrhood, movept.h - dhNbhd,
                              movept.v - dvNbhd,  movept.h + dhNbhd,
                              movept.v + dvNbhd);
                           end; { if }
                       end; { while }
                     end; { if }
                  end; { if }
               end; { else }
            end; { if }
         end; { else }
      end; { else }
   end; { with }
end; { GanttDown }

{-------------------------------------------------------------------------------}

Procedure GanttUp;
{ handles a buttonup at Pt }

Begin { GanttUp }
   if curselrec[activechart].tag = chartTitles then Begin
      StartBlink( curTimeout );
   End;
End; { GanttUp }


{-------------------------------------------------------------------------------}


Procedure GanttKey{(theEvent: EventRec)};
{ This procedure handles a keystroke in the activechart chart. The only thing that
  takes key input in the activechart chart is the chart titles. }


Begin { GanttKey }

   if curselrec[activechart].tag = ChartTitle then
      TitleKey(activechart, curselrec[activechart].index,
      theEvent.ascii,theEvent.shiftKey, theEvent.AppleKey)
   else begin
      ParamAlert('type','','');
      StopAlert(pealert, 45 {Selection Required});
   end; { else }

End; { GanttKey }

{$S SgPeInit}

END. { Unit }


ÿ