{------------------------------------------------------------------------------}
{                  Copyright 1983,1984, Apple Computer Inc.                    }
{                                                                              }
{   Memory management routines.                                                }
{                                                                              }
{  This unit's job is to get and return specific record types from the heap.   }
{  All allocation and deallocation of heap storage goes though these guys,     }
{  except the management of the calendar which happens in PEDates.             }
{  This is now an include file for aplp/globals.                               }
{------------------------------------------------------------------------------}


{$S MenuStuff}


Function NamesEqual( name1: hndData; startchar, endchar: integer; curname: hName ):Boolean;
{ This procedure compares resource two names, name1 and the name pointed
   to by curname, and returns true if they are exactly equal.  At some later
   point this procedure should be changed so as to upshift both sets of
   characters before comparing the names. }

var
   I:     integer;     { local counter     }
   match: boolean;     { got a match ??    }

begin { NamesEqual }

   with curname^^ do
   if (endchar - startchar + 1)  = curlen then begin
      match := true;
      I := startchar;
      while ((match) and (I <= endchar)) do begin
         {$R-}
         match := (UpShift(chr(name1^^[I])) = chr(text^^[I - startchar]));
         {$R+}
         I := I + 1;
      end;
      NamesEqual := match;
   end
   else NamesEqual := false;
end; { NamesEqual }


{-------------------------------------------------------------------------------}
{                                                                               }
{           Memory management, public.                                          }
{                                                                               }
{-------------------------------------------------------------------------------}


Procedure FindResc{( var nameList: hname; var resc: hResc )};
{ FindResc looks through the global resource name list for a matching
   name to that in the parameter text. If found, then the count is
   incremented in this name record and the task pointer is set up to point
   to this name record.  If no match is found, then a new name record is
   created and linked in, and task is set up to point to this new record.}

var
   curname:    hName;      { the current name record in the list }
   found:      boolean;    { did we find a match ??              }
   I:          integer;    { local counter                       }
   newName:    hName;      { new name record created if no match }
   startchar,              { starting char of string, without ' '}
   endchar:    integer;    { ending char, with out blanks also   }
   blank:      boolean;    { is char a blank ??                  }
   text:       hndData;
   length:     integer;

begin { FindResc }

   { init the current name ptr and the found bool }
   curname := NameList;
   found := false;

   text := resc^^.rtext;
   length := resc^^.rcurlen;

   { skip any leading blanks in text array }
   startchar := 0;   blank := true;
   while (startchar <= length - 1) and (blank)  do begin
      {$R-}
      if text^^[startchar] = ord(' ') then startchar := startchar + 1
      {$R+}
      else blank := false;
   end; { while }

   { skip any trailing blanks also }
   endchar := length - 1;  blank := true;
   while (endchar > startchar) and (blank) do begin
      {$R-}
      if text^^[endchar] = ord(' ') then endchar := endchar - 1
      {$R+}
      else blank := false;
   end; { while }

   { look through the list of names for a match }
   while ((curname <> nil) and (not found)) do begin
      if NamesEqual( text, startchar, endchar, curname) then begin
         found := true;
         Resc^^.RName := curname;
         curname^^.count := curname^^.count + 1;
      end
      else curname := curname^^.next;
   end; { while }

   { didn't find one, create a new name record and link in }
   if not found then begin

      { got a new name here }
       if NewNameRec(endchar - startchar + 1, newName) then begin
         {$R-}
         {for i := 0 to (endchar - startchar) do newName^^.text^^[i] := ord(UPShift(chr(text^^[i])));}
         for i := startchar to endchar do newName^^.text^^[i - startchar] := ord(UPShift(chr(text^^[i])));
         {$R+}
         With newName^^ do Begin
            count := 1;
            UnitCost := 0;
            curlen := (endchar - startchar + 1);
            schedule := nil;
         end;
         if nameList = nil then begin
            namelist := newname;
            newname^^.next := nil;
         end
         else begin
            newname^^.next := namelist;
            namelist := newname;
         end;
         Resc^^.RName := newName;
      end
      else { ran out of memory }
   end; { if }
end; { FindResc }


{-------------------------------------------------------------------------------}


Procedure ReturnResc{(var NameList: hname; var resc: hResc )};
{ ReturnName is used to release a name pointer.  The usage count is
   decremented by one.  If the count is now zero, then the record in
   removed from the list and disposed of. }

var thisname,             { the namerec to be released }
    curname:    hName;    { for the dispose case only  }

begin { ReturnResc }

   { first look at the name record for this name }
   thisname := resc^^.RName;
   if thisname <> nil then begin
      thisname^^.count := thisname^^.count - 1;

      { if no one referencing this now then delete it }
      if thisname^^.count = 0 then begin

         { if it equals the head then special case it }
         if thisname = NameList then NameList := NameList^^.next

         else begin
            curname := NameList;
            if curname <> nil then begin
               while ((curname^^.next <> thisname)and(curname^^.next<>nil)) do
                  curname := curname^^.next;
               if curname^^.next <> nil then curname^^.next := thisname^^.next;
            end;
         end;

         FreeName( thisName );
      end;

      resc^^.RName := nil;
   end; { if }
end; { ReturnResc }


{-------------------------------------------------------------------------------}


Function NewTaskRec{(NLInks: integer; var newtask: htask):  boolean};
{ creates one new task record, out of memory  returns a boolean to the caller }

begin { NewTaskRec }

   newtask := Pointer(Ord(HAllocate( theHeap, SizeOf(taskRec) + 4*NLinks)));
   NewTaskRec := Not (Pointer(Ord(newtask)) = Hnil);

end; { NewTaskRec }


{-------------------------------------------------------------------------------}


Function NewNameRec {(len: integer; var newname: hname):  boolean};
{ creates on new name record }

var newData: hndData;

begin { NewNameRec }

   newData := Pointer(Ord(Hallocate(theHeap,len)));
   newName := Pointer(Ord(HAllocate(theHeap,SizeOf(nameRec))));

   { out of mem?? }
   if (Pointer(Ord(newData)) = Hnil) or (Pointer(ord(newName)) = Hnil) then newnamerec := false
   else begin
      newName^^.text := newData;
      newNameRec := true;
   end;

end; { NewNameRec }


{-------------------------------------------------------------------------------}


Function  NewTextRec  {(len: integer; var newtext: htext): boolean};
{ creates one new text record }

var newdata: hndData;

begin { NewTextRec }

   newData := Pointer(Ord(Hallocate(theHeap,len)));
   newText := Pointer(Ord(HAllocate(theHeap,SizeOf(textRec))));

   { out of mem??? }
   if (Pointer(Ord(newdata)) = Hnil) or (Pointer(Ord(newtext)) = Hnil) then newtextrec := false
   else begin
      newText^^.text := newData;
      NewTextRec := true;
   end;

end; { NewTextRec }


{-------------------------------------------------------------------------------}


Function  NewRescRec  {(Dlen, RLen: integer; var newresc: hresc): boolean};
{ creates on new resource record }

var newData: hndData;

begin { NewRescRec }

   newData := Pointer(Ord(Hallocate(theHeap,Dlen)));
   newResc := Pointer(Ord(HAllocate(theHeap,SizeOf(RescRec))));

   { out of mem yet ? }
   if (Pointer(Ord(newdata)) = Hnil) or (Pointer(Ord(newresc)) = Hnil) then newrescrec := false
   else begin
      newResc^^.Dtext := newData;
      newData := Pointer(Ord(Hallocate(theHeap,Rlen)));

      { out yet ?? }
      if (Pointer(Ord(newdata)) = Hnil) then newrescrec := false
      else begin

         newResc^^.Rtext := newData;
         NewRescRec := true;
      end;
   end;

end; { NewRescRec }


{-------------------------------------------------------------------------------}


Procedure FreeRsc{( Resc: hResc )};
{ frees up one resource record }

var
   theData: hndData;

begin { FreeRsc }

   if resc <> nil then begin
      { first free the data array }
      theData := resc^^.Dtext;
      if theData <> nil then FreeH( theHeap, Pointer(Ord(theData)));
      theData := resc^^.Rtext;
      if theData <> nil then FreeH( theHeap, Pointer(Ord(theData)));
      FreeH( theHeap, Pointer( Ord( resc )));
   end; { if }
end; { FreeRsc }


{-------------------------------------------------------------------------------}


Procedure FreeText{( Text: hText )};
{ frees up one text record }

var
   theData: hndData;

begin { FreeText }

   { first free the data array }
   theData := text^^.text;
   FreeH( theHeap, Pointer(Ord(theData)));
   FreeH( theHeap, Pointer( Ord( text )));
end; { FreeText }


{-------------------------------------------------------------------------------}


Procedure FreeName{( thisname: hName )};
{ frees up one resource name record }

var
   theData: hndData;

begin { FreeName }

   { first free the data array }
   theData := thisName^^.text;
   FreeH( theHeap, Pointer(Ord(theData)));
   FreeH( theHeap, Pointer(Ord( thisName )));
end; { FreeName }



{-------------------------------------------------------------------------------}


Procedure FreeTitleList{( title: hText )};
{ frees up a list of text records }

begin { FreeTitleList }

   if title <> nil then begin
      FreeTitleList( Title^^.next );
      FreeH( theHeap, pointer(ord(title^^.text)));
      FreeH( theHeap, Pointer(Ord(Title)));
   end;
end; { FreeTitleList }


{-------------------------------------------------------------------------------}


Procedure FreeRescList{(var nameList: hname; resource: hResc )};
{ frees up a list of resource records }

begin { FreeRescList }

   if Resource <> nil then begin
      FreeRescList( namelist, Resource^^.next );
      ReturnResc( nameList, Resource );
      FreeRsc( Resource);
   end;
end; { FreeRescList }


{-------------------------------------------------------------------------------}


Procedure FreeTaskList{(var namelist: hname; task: htask )};
{ frees up an entire list of task records and all hangers-on.  Note that
  it frees up the list along the flink path, not the blink path. }

begin { FreeTaskList }

   if task <> nil then begin

      { free its successor first }
      FreeTaskList( namelist, task^^.flink );

      { now free it, free all its lines of text, resource, and self }
      FreeTitleList( task^^.title );
      FreeRescList( namelist, task^^.resource );
      FreeH( theHeap, Pointer(Ord(task)));
   end; { if }
end; { FreeTaskList }


{-------------------------------------------------------------------------------}


Function MyGrowProc {( hz: THz; bytesNeeded: integer): integer};

var errnum,
    ds_refNum:      integer;
    curHzSize,
    newMsize,
    newDsize,
    minIncrement,
    unusedSegSpace: longInt;
    info_rec:       dsInfoRec;

begin { MyGrowProc }

   { get the info on the data segment }
   Info_Address(errnum, ord(hz), Ds_RefNum);
   if errNum = 0 then Info_DataSeg(errnum, ds_RefNum, info_rec);
   if errnum = 0 then begin

      { amount of unused space in data segment }
      UnusedSegSpace := Info_rec.disc_size - info_rec.mem_size;

      { the current size of the heap }
      curHzSize := CbofHz(hz);

      { minimum increment to enlarge heap - based on the original bytesneeded }
      minIncrement := ((ord4(bytesNeeded) + 4095) div 4096) * 4096;
      if minIncrement > Maxint then minIncrement := maxInt;

      { check if the current mem_size + unused space in the data segment will accomodate
        the new heap size.  If so, the mem_size will be enlarged to the new size of the heap.
        The disc_size in this case would not be changed. }
      if (info_rec.disc_size) >= (curHzSize + bytesNeeded + DeltaDisc) then begin

         { make an additional check to see if the mem_size already allows the heap to be grown
           without changing the data segment. }
         if (curHzSize + bytesNeeded) > (info_rec.mem_size) then begin

            { data segment must be resized,  Try to increase the mem_size by the minimnum increment.
              Failing that, an attempt will be made to increase the mem_size by the amount requested. }
            if (curHzSize + minIncrement + DeltaDisc) <= (info_rec.disc_size) then
            Size_dataSeg(errnum, ds_refNum, minIncrement, newMsize, 0, newDsize)
            else errnum := -1;
            if errnum <> 0 then
            Size_dataSeg(errnum, ds_refNum, bytesNeeded, newMsize, 0, newDsize);

         end { if }

         else begin

            { the heap can be grown without affecting the data segment.  check if the heap can
              be grown by the minimum increment instead of what was requested.  if this cannot
              be done, then use the amount requested. }
            newMsize := info_rec.mem_size;
            newDsize := info_rec.disc_size;

         end; { else }

      end { if }
      else begin

         { could not get away with just enlarging the mem_size of the data segment. Must
           increase the disc_size.  The disc_size will be increased by the minimum increment
           to try and avoid running out of space immediately.  If the data segment cannot be
           grown by the minimum increment, then an attempt will be made to grow the data segment
           by the amount requested.  The mem_size will be set to the same as the disc size. }
         Size_dataSeg(errnum, ds_refNum, minIncrement, newMsize,
                                         minIncrement - unusedSegspace + DeltaDisc, newDsize);

         { try growing by bytes needed, keep delta disc in there }
         if errnum <> 0 then
         Size_dataseg(errnum, ds_refnum, bytesNeeded, newMsize,
                                         bytesNeeded - unusedSegSpace + DeltaDisc, newDsize);

         if errnum <> 0 then begin

            { can't have the disc delta, grow memory by bytes needed, unless need disc growth }
            if bytesNeeded <= unusedSegSpace then
               size_dataSeg(errnum, ds_refnum, bytesNeeded, newMSize, 0, newDsize)
            else
               size_dataSeg(errnum, ds_refnum, bytesNeeded, newMSize,
                                               bytesNeeded - unusedSegSpace, newDsize);
         end; { if }

      end; { else }
   end; { if }

   { return the amount allocated - the difference between the new and old memory sizes }
   if errnum <> 0 then begin
      MyGrowProc := 0;
      HaveDeltaDisc := false;
   end
   else begin
      haveDeltaDisc := (newMsize + DeltaDisc <= newDsize);
      MyGrowProc := newMsize - curHzSize;
   end; { else }

   {$IFC PeDebug}
   if DebugFlag then begin
      writeln('In MyGrowProc...');
      writeln('   current heap size is ',curHzSize);
      writeln('   new  memory  size is ',newMsize);
      writeln('   new  disc    size is ',newDsize);
   end;
   {$EndC}

end; { MyGrowProc }


{-------------------------------------------------------------------------------}


Function  EnoughSpace  {(hz: THz; size: integer): boolean};
{ called on creation of a new task, new text record, new resource name.
  the expected size required by my dynamic data is passed in.  EnoughSpace
  sees if I currently have the space required, plus the disc slop.  If so,
  then returns true.  Otherwise, it tries to grow the segment, memory only
  or disk if required by size plus the delta slop.  Enough space returns
  false if we dont have  size + slop in the heap. }

begin { EnoughSpace }

   { if grow succeeded then enoughSpace is true if we still
     have the delta disc buffer. }
   if (MyGrowProc( theHeap, size ) <> 0)
   then enoughSpace := HaveDeltaDisc

   { otherwise, enoughSpace not true, grow failed }
   else enoughSpace := false;

end; { EnoughSpace }


{-------------------------------------------------------------------------------}


Procedure ShrinkHeap {(hz: THz; ShrinkDiscAlso: boolean; var status: integer)};
{ routine to shrink the heap upon deactivation }

var BytesRemoved,
    HeapSize,
    ReduceMemoryBy,
    ReduceDiscBy,
    newMSize,
    newDSize:     LongInt;
    SegRefNum:    integer;
    SegInfo:      DsInfoRec;

begin

   { ask the storage manager to compact the heap }
   BytesRemoved := CBShrinkHz(Hz, Hz^.CBFree);

   { get the new size of the heap zone }
   HeapSize := CBofHz(Hz);

   { get the current memory size of the data segment }
   Info_Address(status, ord(Hz), SegRefNum);
   If status <> 0 then exit(ShrinkHeap);
   Info_dataSeg(status, segRefNum, SegInfo);
   if status <> 0 then exit(ShrinkHeap);

   { now shrink the memory size of the segment down to the heap size }
   ReduceMemoryBy := SegInfo.Mem_size - Heapsize;
   if ShrinkDiscAlso then ReduceDiscBy := SegInfo.disc_size -
                                         (SegInfo.mem_size - ReduceMemoryBy + deltaDisc)
   else ReduceDiscBy := 0;
   Size_DataSeg(Status, segRefNum, -ReduceMemoryBy, newMSize, -ReduceDiscBy, newDSize);

end;
ÿ