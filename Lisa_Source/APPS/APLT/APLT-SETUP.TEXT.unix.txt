{ Copyright 1983, Apple Computer, Inc. }

{$S SegTESetup }

procedure ReportParms (HTerm: THTerm; Sol: Char);
  {ReportParms sends the stuff in response to a DECREPTPARMS or whenever Setup}
  {mode is exited.  The sequence is:}
  {ESC [ <Sol> ; <Par> ; <NBits> ; <XSpeed> ; <RSpeed> ; <ClkMul> ; <Flags> x}

  var
    BaudNr: Integer;

  {$S SegTESetup }
  begin {ReportParms}
    {$IFC SampCalls}   ALogCall;   {$ENDC}
    with HTerm^^, Setup do begin
      ReplySeq [ReplyLen+ 1]:=ESC;
      ReplySeq [ReplyLen+ 2]:='[';

      ReplySeq [ReplyLen+ 3]:=Sol;                      {<Sol>}
      ReplySeq [ReplyLen+ 4]:=';';

      case Parity of                                    {<Par> and <NBits>}
        DBNoParity: begin
          ReplySeq [ReplyLen+ 5]:='1';
          ReplySeq [ReplyLen+ 7]:='2';
        end;

        DBOddParity: begin
          ReplySeq [ReplyLen+ 5]:='4';
          ReplySeq [ReplyLen+ 7]:='1';
        end;

        DBEvenParity: begin
          ReplySeq [ReplyLen+ 5]:='5';
          ReplySeq [ReplyLen+ 7]:='1';
        end;
      end;
      ReplySeq [ReplyLen+ 6]:=';';
      ReplySeq [ReplyLen+ 8]:=';';

      BaudNr:=(Ord (BaudRate) - Ord (DB50))*8;          {<XSpeed>}
      ReplySeq [ReplyLen+11]:=Chr (BaudNr mod 10 + Ord ('0'));   BaudNr:=BaudNr div 10;
      ReplySeq [ReplyLen+10]:=Chr (BaudNr mod 10 + Ord ('0'));   BaudNr:=BaudNr div 10;
      ReplySeq [ReplyLen+ 9]:=Chr (BaudNr mod 10 + Ord ('0'));
      ReplySeq [ReplyLen+12]:=';';

      ReplySeq [ReplyLen+13]:=ReplySeq [ReplyLen+ 9];   {<RSpeed>}
      ReplySeq [ReplyLen+14]:=ReplySeq [ReplyLen+10];
      ReplySeq [ReplyLen+15]:=ReplySeq [ReplyLen+11];
      ReplySeq [ReplyLen+16]:=';';

      ReplySeq [ReplyLen+17]:='1';                      {<ClkMul>}
      ReplySeq [ReplyLen+18]:=';';

      ReplySeq [ReplyLen+19]:=Chr (STP.Value div 10 + Ord ('0')); {<Flags>}
      ReplySeq [ReplyLen+20]:=Chr (STP.Value mod 10 + Ord ('0'));
      ReplySeq [ReplyLen+21]:='x';

      ReplyLen:=ReplyLen + 21;
    end;
  end; {ReportParms}


function CanTalkTo (Serial: TPorts): TTalkReason;
  {CanTalkTo returns a TalkReason indicating why or why not Serial (A/B)}
  {is usable for LisaTerminal.}

  var
    eCode: Integer;
    VName: E_Name;
    UartName: Pathname;
    rNum: Integer;
    Config: ConfigDev;
    NextEntry: LongInt;
    attribs: fs_info;

  {$S SegTESetup }
  begin {CanTalkTo}
    {$IFC SampCalls}   ALogCall;   {$ENDC}
    CanTalkTo:=TalkOK;                          {Assume it's OK to communicate.}

    Mount (eCode, VName, Password, UartPath [Serial=SerialA]);

    UartName:=Concat ('-', UartPath [Serial=SerialA]);
    Lookup (eCode, UartName, attribs);
    if (eCode>0) or (attribs.opencount>0) then begin
      CanTalkTo:= TalkNotOk;
    end else begin
      NextEntry:=0;
      If Serial = SerialA then begin
        Repeat
          PMReadConfig (eCode,NextEntry,Config)
        UNTIL
          (Config.Pos.Slot=10) AND (Config.Pos.Chan=1);
          if (Config.DriverID <> Driver) AND (Config.DriverID <> ModemDriver)
          then CanTalkTo := TalkNotOk;


      end else begin
        Repeat
          PMReadConfig (eCode,NextEntry,Config)
        UNTIL
          (Config.Pos.Slot=10) AND (Config.Pos.Chan=2);
          if Config.DriverID <> Driver              {Can't use modem driver on serial B}
          then CanTalkTo:=TalkNotOk;
      end;
    end;
  end; {CanTalkTo}


procedure InstallSetup (HTerm: THTerm; ChangedPort: Boolean;
                                       ChangedModem: Boolean);
  {InstallSetup makes HTerm correspond to its Setup information.}

  const
    Mounted=1052;

  var
    eCode: Integer;
    vName: E_Name;
    AlertNr: Integer;
    TheSerial: TPorts;
    NewPort: Boolean;

  procedure UartError;
    {UartError is called after all of the OS calls to twiddle the UART.}
    {If eCode is zero then nothing is done, else HTerm is put in local}
    {and the UART is left closed.}

    {$S SegTESetup }
    begin {UartError}
      {$IFC SampCalls}   ALogCall;   {$ENDC}
      if eCode>0 then begin
        with HTerm^^, Setup do
          GiveAlert (HTerm, AlrtNote, AlertNr, SerialName [PortA], TheTitle, '');

        with HTerm^^, Setup do begin
          Close_Object (eCode, RefNum);
          Unmount (eCode, UartPath [PortA]);
          UartOpen:=False;

          OnLine:=False;
        end;

        Exit (InstallSetup);
      end;
    end; {UartError}

  {$S SegTESetup }
  begin {InstallSetup}
    {$IFC SampCalls}   ALogCall;   {$ENDC}
    with HTerm^^, Setup do
      if ((Modem=DBHayes1200) and not (BaudRate in [DB50..DB300, DB1200]) or
          (Modem=DBHayes300)  and not (BaudRate in [DB50..DB300])) and
         OnLine then begin
        OnLine:=False;

        GiveAlert (HTerm, AlrtNote, ALBaudModem, TheTitle, '', '');
      end;

    with HTerm^^, Setup do
      if PortA and ((BaudRate=DB3600) or (BaudRate=DB19200)) and OnLine then begin
        OnLine:=False;

        GiveAlert (HTerm, AlrtNote, ALBaudPort, TheTitle, '', '');
      end;

    with HTerm^^, Setup do
      if ((Modem=DBOther) or not OnLine) and AutoDial then begin
        AutoDial:=False;

        GiveAlert (HTerm, AlrtNote, ALCantDial, TheTitle, '', '');
      end;

    with HTerm^^, Setup do
      if (ADNumber='') and AutoDial then
        GiveAlert (HTerm, AlrtNote, ALSetDial, TheTitle, '', '');

    with HTerm^^, Setup do
      if UartOpen and OffHook and (not OnLine or ChangedPort) then
        HangUp (HTerm);

    with HTerm^^, Setup do
      if UartOpen and (not OnLine or ChangedPort) then begin
        AlertNr:=ALUart;
        Close_Object (eCode, RefNum);                           UartError;
        Unmount (eCode, UartPath [PortA <> {XOR} ChangedPort]); UartError;
        UartOpen:=False;
      end;

    NewPort:=False;

    with HTerm^^, Setup do begin
      if OnLine then begin
        if not UartOpen or ChangedPort then begin
          if PortA then
            TheSerial:=SerialA
          else
            TheSerial:=SerialB;

          eCode:=1;
          case CanTalkTo (TheSerial) of
            TalkOK   : eCode:=0;
            PortInUse: AlertNr:=ALInUse;
            TalkNotOk: AlertNr:=AlBadDev;
          end;
          UartError;




          AlertNr:=ALUart;
          Open (eCode, UartName [PortA], RefNum, [DRead, DWrite, Private]); UartError;
          UartOpen:=True;

          Device_Control(eCode,UartName [PortA],GroupZ);
          if eCode=645 then begin {no carrier from european modem driver}
            AlertNr:=AlBadDev;  UartError;
          end;

          Device_Control (eCode, UartName [PortA], GroupD);           UartError;
          Device_Control (eCode, UartName [PortA], GroupG);           UartError;
          Device_Control (eCode, UartName [PortA], GroupH);           UartError;
          Device_Control (eCode, UartName [PortA], GroupI);           UartError;
          Device_Control (eCode, UartName [PortA], GroupY);           UartError;


          NewPort:=True;
        end;

        AlertNr:=ALUart;
        Device_Control (eCode, UartName [PortA], GroupA [Parity  ]);  UartError;
        Device_Control (eCode, UartName [PortA], GroupB [XOnXOff ]);  UartError;
        Device_Control (eCode, UartName [PortA], GroupC [BaudRate]);  UartError;
        Device_Control (eCode, UartName [PortA], GroupE [XOnXOff ]);  UartError;
        Device_Control (eCode, UartName [PortA], GroupF [XOnXOff ]);  UartError;
      end;
    end;

    if NewPort or ChangedModem then begin
      Connected (HTerm);

      if HTerm^^.Setup.AutoDial and (not HTerm^^.OffHook) then
        DialPhone (HTerm);
    end;

    if ((HTerm^^.Setup.Modem<>DBOther)<> HTerm^^.Setup.PhoneVis)  then begin
      if HTerm^^.Setup.Modem=DBOther then begin
          DeleteMenu (DialMenu);
          HTerm^^.Setup.PhoneVis:=False;
        end else begin
          InsertMenu (TheMenus [DialMenu], 0);
          HTerm^^.Setup.PhoneVis:=True;
        end;
      DrawMenuBar;
    end;

    DoPhoneMenu (HTerm);

    with HTerm^^, Setup do
      MinUpdWait:=GroupC [BaudRate].DCData [0] div 160;
  end; {InstallSetup}


procedure TrackSetup (MsePt: Point);
  {TrackSetup tracks the mouse while it is in the dialog folder.}

  {$S SegTESetup }
  begin {TrackSetup}
    {$IFC SampCalls}   ALogCall;   {$ENDC}
    SetStdCursor (iCrsCheck);
  end; {TrackSetup}


procedure EndCompat (DoIt: Boolean);
  {EndCompat makes TheTerm^^.Setup match the boxes set}
  {in SetupForm [SetupCompat].}

  var
    ChangePort: Boolean;
    ChangeModem: Boolean;
    WasModem: TDBModem;

  {$S SegTESetup }
  begin {EndCompat}
    {$IFC SampCalls}   ALogCall;   {$ENDC}
    if DoIt then begin
      {$R-}
      with TheTerm^^, Setup, SetupForm [SetupCompat].HCkForm^^ do begin
        ChangePort:=PortA;
        PortA      := (CkBoxLst [DBPortA      ]^^.CurCkItm=DBSet);
        ChangePort:= (ChangePort<>PortA);

        WasModem:=Modem;
        Modem      :=  CkBoxLst [DBModem      ]^^.CurCkItm       ;
        ChangeModem:= (WasModem<>Modem);

        Terminal   :=  CkBoxLst [DBTerminal   ]^^.CurCkItm       ;
        BaudRate   :=  CkBoxLst [DBBaudRate   ]^^.CurCkItm       ;
        Parity     :=  CkBoxLst [DBParity     ]^^.CurCkItm       ;
        XOnXOff    := (CkBoxLst [DBXOnXOff    ]^^.CurCkItm=DBClr);
        FullDuplex := (CkBoxLst [DBFullDuplex ]^^.CurCkItm=DBSet);
        OnLine     := (CkBoxLst [DBOnLine     ]^^.CurCkItm=DBSet);
        AutoLF     := (CkBoxLst [DBAutoLF     ]^^.CurCkItm=DBSet);
        AutoDial   := (CkBoxLst [DBDial       ]^^.CurCkItm=DBSet);
      end;

      {$IFC RangeCheck }
        {$R+}
      {$ENDC }

      InstallSetup (TheTerm, ChangePort, ChangeModem);

      if TheTerm^^.TellSetup then
        ReportParms (TheTerm, '2');
    end;

    ClearDBox;
  end; {EndCompat}


procedure InstallCmfort (HTerm: THTerm; ChangedCols: Boolean);
  {InstallCmfort makes HTerm correspond to its Setup/Comfort settings.}

  var
    SavePort: GrafPtr;
    TheSeqNr: LongInt;
    RowNr: Integer;

  {$S SegTESetup }
  begin {InstallCmfort}
    {$IFC SampCalls}   ALogCall;   {$ENDC}
    with HTerm^^, Setup, ParmList, Parm [ParmNr], Display [RightNow], MoveArray do begin        {[JS]}
      if Cols80 then
        Style:=M80Style
      else
        Style:=M132Style;

      if ChangedCols then begin
        Cursor.Ln:=StartScreen+TMargin;
        Cursor.Ch:=MinCol;

        for RowNr:=MinRow to MaxRow do begin
          TheSeqNr:=Screen [RowNr]^^.SeqNr;
          Screen [RowNr]^^:=BlankLine;

          with Screen [RowNr]^^ do begin
            SeqNr:=TheSeqNr;
            Changed:=True;
            Repaint:=True;
          end;
        end;
      end;

      GetPort (SavePort);
      SetPort (Folder);
      TextFont (Style^^.Font.Fam);
      SetPort (SavePort);
    end;

    SizeTerm (HTerm);
    PaintTerm (HTerm);
  end; {InstallCmfort}


procedure EndCmfort (DoIt: Boolean);
  {EndCmfort makes TheTerm^^.Setup match the boxes set}
  {in SetupForm [SetupCmfort].}

  var
    ChangedCols: Boolean;

  {$S SegTESetup }
  begin {EndCmfort}
    {$IFC SampCalls}   ALogCall;   {$ENDC}
    if DoIt then begin
      {$R-}
      with TheTerm^^, Setup, SetupForm [SetupCmfort].HCkForm^^ do begin
        AutoRepeat := (CkBoxLst [DBAutoRepeat ]^^.CurCkItm=DBSet);
        MarginBell := (CkBoxLst [DBMarginBell ]^^.CurCkItm=DBSet);

        ChangedCols:=Cols80;
        Cols80     := (CkBoxLst [DBCols80     ]^^.CurCkItm=DBSet);
        ChangedCols:= (Cols80<>ChangedCols);

        Wraparound := (CkBoxLst [DBWraparound ]^^.CurCkItm=DBSet);
        WhiteScreen:= (CkBoxLst [DBWhiteScreen]^^.CurCkItm=DBSet);
        BlockCursor:= (CkBoxLst [DBBlockCursor]^^.CurCkItm=DBSet);
        SeeLights  := (CkBoxLst [DBSeeLights  ]^^.CurCkItm=DBSet);
        Runoff     := (CkBoxLst [DBRunoff     ]^^.CurCkItm=DBSet);
        WordWrap   := (CkBoxLst [DBWordWrap   ]^^.CurCkItm=DBSet);

        if Runoff and (HeapState<>HeapNormal) then begin
          Runoff:=False;
          GiveAlert (TheTerm, AlrtNote, ALStopCache, TheTitle, HeapDisk, '');
        end;
        if Runoff and CantRemember then begin
          Runoff:=False;
          StopAlert(MyAlerts,AlCantRemember);
        end;
      end;

      {$IFC RangeCheck }
        {$R+}
      {$ENDC }

      InstallCmfort (TheTerm, ChangedCols);

      if TheTerm^^.TellSetup then
        ReportParms (TheTerm, '2');
    end;

    ClearDBox;
  end; {EndCmfort}


procedure EndCustom (DoIt: Boolean);
  {EndCompat makes TheTerm^^.Setup match the boxes set}
  {in SetupForm [SetupCustom].}

  {$S SegTESetup }
  begin {EndCustom}
    {$IFC SampCalls}   ALogCall;   {$ENDC}
    if DoIt then begin
      {$R-}
      with TheTerm^^, Setup, SetupForm [SetupCustom].HCkForm^^ do begin
        STP.Bit3   := (CkBoxLst [DBBit3       ]^^.CurCkItm=DBSet);
        STP.Bit2   := (CkBoxLst [DBBit2       ]^^.CurCkItm=DBSet);
        STP.Bit1   := (CkBoxLst [DBBit1       ]^^.CurCkItm=DBSet);
        STP.Bit0   := (CkBoxLst [DBBit0       ]^^.CurCkItm=DBSet);
      end;

      {$IFC RangeCheck }
        {$R+}
      {$ENDC }

      if TheTerm^^.TellSetup then
        ReportParms (TheTerm, '2');
    end;

    ClearDBox;
  end; {EndCustom}


procedure EndCharSets (DoIt: Boolean);

  {$S SegTESetup }
  begin {EndCharSets}
    {$IFC SampCalls}   ALogCall;   {$ENDC}
    if DoIt then begin
      {$R-}
      with TheTerm^^, SetupForm [SetCharSets].HCkForm^^ do begin
        CharGroup       := (CkBoxLst [DBChooseCharSets]^^.CurCkItm  );
        CharSet[GroupG0]:= (CkBoxLst [DBG0            ]^^.CurCkItm  );
        CharSet[GroupG1]:= (CkBoxLst [DBG1            ]^^.CurCkItm  );
      end;

      {$IFC RangeCheck }
        {$R+}
      {$ENDC }

      if TheTerm^^.TellSetup then
        ReportParms (TheTerm, '2');
    end;

    ClearDBox;
  end; {EndCharSets}


procedure BeginSetup (Form: TFormNr);
  {BeginSetup starts the check box dialog for changing the Setup parameters}
  {of TheTerm.}

  {$S SegTESetup }
  begin {BeginSetup}
    {$IFC SampCalls}   ALogCall;   {$ENDC}
    with TheDBox do                               {Kill pending dialog.}
      if IsOpen then
        CallEndProc (False, EndProc);

    case Form of
      SetupCompat: begin
        {$R-}
        with SetupForm [SetupCompat].HCkForm^^, TheTerm^^, Setup do begin
          CkBoxLst [DBDoForget   ]^^.CurCkItm:=-1;

          CkBoxLst [DBTerminal   ]^^.CurCkItm:=           Terminal    ;
          CkBoxLst [DBPortA      ]^^.CurCkItm:=SetClrMap [PortA      ];
          CkBoxLst [DBBaudRate   ]^^.CurCkItm:=           BaudRate    ;
          CkBoxLst [DBParity     ]^^.CurCkItm:=           Parity      ;
          CkBoxLst [DBXOnXOff    ]^^.CurCkItm:=SetClrMap [not XOnXOff];
          CkBoxLst [DBFullDuplex ]^^.CurCkItm:=SetClrMap [FullDuplex ];
          CkBoxLst [DBOnLine     ]^^.CurCkItm:=SetClrMap [OnLine     ];
          CkBoxLst [DBAutoLF     ]^^.CurCkItm:=SetClrMap [AutoLF     ];
          CkBoxLst [DBModem      ]^^.CurCkItm:=           Modem       ;
          CkBoxLst [DBDial       ]^^.CurCkItm:=SetClrMap [AutoDial   ];
        end;

        {$IFC RangeCheck }
          {$R+}
        {$ENDC }

        SetDBox (Pointer (Ord (SetupForm [SetupCompat].HCkForm)), NIL,
                 @GetCkHit,                             {Event procedure.}
                 @TrackSetup,                           {Mouse track procedure.}
                 @RestoreCkForm,                        {Restore (Update) procedure.}
                 @EndCompat);                           {Dialog end procedure.}
      end;

      SetupCmfort: begin
        {$R-}
        with SetupForm [SetupCmfort].HCkForm^^, TheTerm^^, Setup do begin
          CkBoxLst [DBDoForget   ]^^.CurCkItm:=-1;

          CkBoxLst [DBAutoRepeat ]^^.CurCkItm:=SetClrMap [AutoRepeat ];
          CkBoxLst [DBMarginBell ]^^.CurCkItm:=SetClrMap [MarginBell ];
          CkBoxLst [DBCols80     ]^^.CurCkItm:=SetClrMap [Cols80     ];
          CkBoxLst [DBWraparound ]^^.CurCkItm:=SetClrMap [Wraparound ];
          CkBoxLst [DBWhiteScreen]^^.CurCkItm:=SetClrMap [WhiteScreen];
          CkBoxLst [DBBlockCursor]^^.CurCkItm:=SetClrMap [BlockCursor];
          CkBoxLst [DBSeeLights  ]^^.CurCkItm:=SetClrMap [SeeLights  ];
          CkBoxLst [DBRunoff     ]^^.CurCkItm:=SetClrMap [Runoff     ];
          CkBoxLst [DBWordWrap   ]^^.CurCkItm:=SetClrMap [WordWrap   ];
        end;

        {$IFC RangeCheck }
          {$R+}
        {$ENDC }

        SetDBox (Pointer (Ord (SetupForm [SetupCmfort].HCkForm)), NIL,
                 @GetCkHit,                             {Event procedure.}
                 @TrackSetup,                           {Mouse track procedure.}
                 @RestoreCkForm,                        {Restore (Update) procedure.}
                 @EndCmfort);                           {Dialog end procedure.}
      end;

      SetupCustom: begin
        {$R-}
        with SetupForm [SetupCustom].HCkForm^^, TheTerm^^, Setup do begin
          CkBoxLst [DBDoForget   ]^^.CurCkItm:=-1;

          CkBoxLst [DBBit3       ]^^.CurCkItm:=SetClrMap [STP.Bit3   ];
          CkBoxLst [DBBit2       ]^^.CurCkItm:=SetClrMap [STP.Bit2   ];
          CkBoxLst [DBBit1       ]^^.CurCkItm:=SetClrMap [STP.Bit1   ];
          CkBoxLst [DBBit0       ]^^.CurCkItm:=SetClrMap [STP.Bit0   ];
        end;

        {$IFC RangeCheck }
          {$R+}
        {$ENDC }

        SetDBox (Pointer (Ord (SetupForm [SetupCustom].HCkForm)), NIL,
                 @GetCkHit,                             {Event procedure.}
                 @TrackSetup,                           {Mouse track procedure.}
                 @RestoreCkForm,                        {Restore (Update) procedure.}
                 @EndCustom);                           {Dialog end procedure.}
      end;

      SetCharSets: begin
        {$R-}
        with SetupForm [SetCharSets].HCkForm^^, TheTerm^^ do begin
          CkBoxLst [DBDoForget     ]^^.CurCkItm:=-1;

          CkBoxLst [DBChooseCharSet]^^.CurCkItm:= CharGroup;
          CkBoxLst [DBG0           ]^^.CurCkItm:= CharSet[GroupG0];
          CkBoxLst [DBG1           ]^^.CurCkItm:= CharSet[GroupG1];
        end;

        {$IFC RangeCheck }
          {$R+}
        {$ENDC }

        SetDBox (Pointer (Ord (SetupForm [SetCharSets].HCkForm)), NIL,
                 @GetCkHit,                             {Event procedure.}
                 @TrackSetup,                           {Mouse track procedure.}
                 @RestoreCkForm,                        {Restore (Update) procedure.}
                 @EndCharSets);                           {Dialog end procedure.}
      end;
    end;

    SetPort (DialogFolder);
    ShowCkForm (SetupForm [Form].HCkForm);
  end; {BeginSetup}

{$S SegTE }
ÿ