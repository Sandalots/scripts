{ Copyright 1983, Apple Computer, Inc. }

{$S SegTESelect }

{NOTE:  Most of this code was stolen from Bruce Daniels' Mouse Editor.}

{---------------------------- FindChar ----------------------------}
  { Find Where (.Ln/.Ch) is the character at Pt }
PROCEDURE FindChar (Pt:point; VAR Where: TPosition);
VAR t: rect;
    HSp: Integer;
    HRow: THLine;
    Bottom: Integer;

{$S SegTESelect }
BEGIN
{$IFC SampCalls}   ALogCall;   {$ENDC}
t := TheTerm^^.Display [AtPaint].MoveArray.FolderZones [TextBox];                         {[JS]}
Pt.v := CMax (Pt.v, t.top);
Pt.v := CMin (Pt.v, t.bottom);
Pt.h := CMax (Pt.h, t.left);
Pt.h := CMin (Pt.h, t.right);

WITH TheTerm^^, Style^^, Display [AtPaint], MoveArray DO                                  {[JS]}
     BEGIN
     Where.Ln := (Pt.v - t.top) div VertSp + FirstRow;
     Bottom:=StartScreen+MaxRow-MinRow;
     if Where.Ln>Bottom then
          Where.Ln:=Bottom;

     HSp:=HorzSp;
     END;

GetRow (TheTerm, AtPaint, Where.Ln, HRow);
if HRow^^.TwoWide then
  HSp:=HSp*2;
FreeRow (HRow);

WITH TheTerm^^.Display [AtPaint], MoveArray DO                                       {[JS]}
    Where.Ch := (Pt.h - t.left + HSp div 2) div HSp + FirstCol;

TheTerm^^.Display[RightNow].MoveArray.Position := Where.Ch;                          {[JS]}
END; { FindChar }


{---------------------------- FindPoint ----------------------------}
PROCEDURE FindPoint (VAR Pt: point; Where: TPosition);
VAR t:rect;
    HSp: Integer;
    HRow: THLine;

{$S SegTESelect }
BEGIN
{$IFC SampCalls}   ALogCall;   {$ENDC}
if Where.Ln>TheTerm^^.Display [AtPaint].MoveArray.StartScreen+MaxRow-MinRow then     {[JS]}
  HSp:=TheTerm^^.Style^^.HorzSp

else begin
  GetRow (TheTerm, AtPaint, Where.Ln, HRow);
  with TheTerm^^.Style^^ do
    if HRow^^.TwoWide then
      HSp:=HorzSp*2
    else
      HSp:=HorzSp;
  FreeRow (HRow);
end;

WITH TheTerm^^, Style^^, Display [AtPaint], MoveArray DO                             {[JS]}
     BEGIN
     t := FolderZones [TextBox];
     IF Where.Ln < FirstRow THEN                       {Above visible window.}
          Pt := t.topleft
     ELSE IF Where.Ln >= FirstRow+NrRows-1 THEN        {Below visible window.}
          BEGIN
          Pt := t.botright;
          Pt.v := Pt.v - Below;
          END
     ELSE                                              {Within visible window.}
          BEGIN
          Pt.v := t.top  + Above + (Where.Ln - FirstRow) * VertSp;
          Pt.h := t.left +         (Where.Ch - FirstCol) *    HSp;
          END;
     END;
END; { FindPoint }


{---------------------------- Visible ----------------------------}
FUNCTION Visible (Line: Integer): Boolean;

{$S SegTESelect }
BEGIN
{$IFC SampCalls}   ALogCall;   {$ENDC}
WITH TheTerm^^.Display [AtPaint], MoveArray DO                                                      {[JS]}
     Visible := (Line >= FirstRow) AND (Line <= FirstRow+NrRows-1);
END;   { Visible }


{---------------------------- CharInLine ----------------------------}
FUNCTION CharInLine (Line: THLine; chx: integer):char;

{$S SegTESelect }
BEGIN
{$IFC SampCalls}   ALogCall;   {$ENDC}
IF (chx < MinCol) OR (chx > MaxCol) THEN
     CharInLine := ' '
ELSE
     CharInLine := Line^^.TextLine [chx].Ch;
END; { CharInLine }



{---------------------------- Drag ----------------------------}
PROCEDURE Drag (PrevPt: Point);
VAR SavePort: GrafPtr;
    CurrPt, TailPt: Point;
    Anchor: TPosition;
    Curr: TPosition;
    Prev: TPosition;
    DoScroll: Boolean;
    t: rect;
    Line: THLine;

{$S SegTESelect }
BEGIN
{$IFC SampCalls}   ALogCall;   {$ENDC}
GetPort (SavePort);
SetPort (TheTerm^^.Folder);
Anchor := TheTerm^^.Select.Left;
FindChar (PrevPt, Prev);       { recalculate to nearest boundaries }
WHILE StillDown DO BEGIN
     t := TheTerm^^.Display [AtPaint].MoveArray.FolderZones [BTextBox];                        {[JS]}
     IF Time <= DownTime + UpDelay THEN
          CurrPt := DownWhere
     ELSE
          GetMouse (CurrPt);
     DoScroll:=False;
     IF CurrPt.v < t.Top THEN
          WITH TheTerm^^.Display [RightNow], MoveArray DO                                      {[JS]}
               BEGIN
               IF FirstRow>MinRow THEN
                    BEGIN
                    FirstRow:=FirstRow-1;
                    DoScroll:=True;
                    END;
               END
     ELSE IF CurrPt.v > t.Bottom THEN
          WITH TheTerm^^.Display [RightNow], MoveArray DO                                      {[JS]}
               BEGIN
               IF FirstRow+NrRows-1<StartScreen+MaxRow-MinRow THEN
                    BEGIN
                    FirstRow:=FirstRow+1;
                    DoScroll:=True;
                    END;
               END
     ELSE
          WITH TheTerm^^.Style^^ DO
               BEGIN
               FindPoint (PrevPt, Prev);
               IF CurrPt.v >
                  PrevPt.v - Above - VertSp*tophysteresis div 100 THEN
               IF CurrPt.v <
                  PrevPt.v + Below + VertSp*bothysteresis div 100 THEN
                    CurrPt.v := PrevPt.v;    { stick to same line }
               END;
     IF CurrPt.h < t.Left THEN
          WITH TheTerm^^.Display [RightNow], MoveArray DO                                      {[JS]}
               BEGIN
               IF FirstCol>MinCol THEN
                    BEGIN
                    FirstCol:=FirstCol-1;
                    DoScroll:=True;
                    END;
               END
     ELSE IF CurrPt.h > t.Right THEN
          WITH TheTerm^^.Display [RightNow], MoveArray DO                                      {[JS]}
               BEGIN
               IF FirstCol+NrCols-1<MaxCol THEN
                    BEGIN
                    FirstCol:=FirstCol+1;
                    DoScroll:=True;
                    END;
               END;
     IF DoScroll THEN
          BEGIN
          SizeTerm (TheTerm);
          UpdateTerm (TheTerm);
          END;
     FindChar (CurrPt, Curr);
     IF (Curr.Ln <> Prev.Ln) OR (Curr.Ch <> Prev.Ch) THEN
          BEGIN
          Prev := Curr;   { remember for next time }
          IF (targsize=Double) AND Visible (Curr.Ln) THEN
               BEGIN   { extend to next word boundary }
               GetRow (TheTerm, AtPaint, Curr.Ln, Line);
               IF (Curr.Ln < Anchor.Ln) OR
                 ((Curr.Ln = Anchor.Ln) AND (Curr.Ch < Anchor.Ch)) THEN
                    IF CharInLine (Line, Curr.Ch) in alphanum THEN
                         WHILE CharInLine (Line, Curr.Ch-1) in alphanum DO
                              Curr.Ch := Curr.Ch - 1
                    ELSE
                         { Don't do anything }
               ELSE IF CharInLine (Line, Curr.Ch-1) in alphanum THEN
                    WHILE CharInLine (Line, Curr.Ch) in alphanum DO
                         Curr.Ch := Curr.Ch + 1;
               FreeRow (Line);
               END;
          IF (targsize=Triple) AND Visible (Curr.Ln) THEN
               BEGIN   { extend to next line boundary }
               IF Curr.Ln < Anchor.Ln THEN
                    Curr.Ch := MinCol
               ELSE
                    BEGIN
                    Curr.Ln := Curr.Ln + 1;
                    WITH TheTerm^^.Display [AtPaint], MoveArray DO                             {[JS]}
                         IF Curr.Ln > StartScreen+MaxCol-MinCol THEN
                            Curr.Ln := StartScreen+MaxCol-MinCol;
                    Curr.Ch := MinCol;
                    END;
               END;
          WITH TheTerm^^.Select DO
               IF (Curr.Ln < Anchor.Ln) OR
                 ((Curr.Ln = Anchor.Ln) AND (Curr.Ch < Anchor.Ch)) THEN
                    BEGIN                    { dragging backwards }
                    Left := Curr;
                    Right := Anchor;
                    END
               ELSE
                    BEGIN                    { dragging forwards }
                    Left := Anchor;
                    Right := Curr;
                    END;
          UpdateTerm (TheTerm);
          END;
END;
ObscureCursor;
SetPort (SavePort);
END; { Drag }



{---------------------------- CharSelect ----------------------------}
PROCEDURE CharSelect (MousePt: Point);
VAR Mouse: TPosition;

{$S SegTESelect }
BEGIN     {  de-select and position new start }
     {$IFC SampCalls}   ALogCall;   {$ENDC}
     FindChar (MousePt, Mouse);
     WITH TheTerm^^.Select DO
          BEGIN
          Left:=Mouse;
          Right:=Mouse;
          END;
END; { CharSelect }


{---------------------------- WordSelect ----------------------------}
PROCEDURE WordSelect (MousePt: Point);   { grow selection to a word }
VAR Line: THLine;

{$S SegTESelect }
BEGIN
{$IFC SampCalls}   ALogCall;   {$ENDC}
{  Extend Left edge of current Selection to next word boundary }
GetRow (TheTerm, AtPaint, TheTerm^^.Select.Left.Ln, Line);
WHILE CharInLine (Line, TheTerm^^.Select.Left.Ch-1) in alphanum DO
      TheTerm^^.Select.Left.Ch := TheTerm^^.Select.Left.Ch - 1;
{  Extend Right edge of current Selection to next word boundary }
IF TheTerm^^.Select.Right.Ln <> TheTerm^^.Select.Left.Ln THEN
      BEGIN
      FreeRow (Line);
      GetRow (TheTerm, AtPaint, TheTerm^^.Select.Left.Ln, Line);
      END;
WHILE CharInLine (Line, TheTerm^^.Select.Right.Ch) in alphanum DO
      TheTerm^^.Select.Right.Ch := TheTerm^^.Select.Right.Ch + 1;
FreeRow (Line);
END; { WordSelect }


{---------------------------- LineSelect ----------------------------}
PROCEDURE LineSelect (MousePt: Point);   { grow selection to a line }

{$S SegTESelect }
BEGIN
{$IFC SampCalls}   ALogCall;   {$ENDC}
WITH TheTerm^^, Display [AtPaint], MoveArray, Select DO                                        {[JS]}
     BEGIN
     {  Extend Left edge of current Selection to beginning of line }
     Left.Ch := MinCol;
     {  Extend Right edge of current Selection to next line }
     Right.Ln := Right.Ln + 1;
     IF Right.Ln > StartScreen+MaxCol-MinCol THEN
          Right.Ln := StartScreen+MaxCol-MinCol;
     Right.Ch := MinCol;
     END;
END; { LineSelect }



{---------------------------- StartSelect ----------------------------}
PROCEDURE StartSelect (Where: Point);

{$S SegTESelect }
BEGIN
{$IFC SampCalls}   ALogCall;   {$ENDC}
CASE TargSize of
     Single: CharSelect (Where);
     Double: WordSelect (Where);
     Triple: LineSelect (Where);
     Beyond: TargSize := Triple;
END;
UpdateTerm (TheTerm);    { repaint the selection/caret }
Drag (Where);            { drag thru after select }
END;   { StartSelect }


{---------------------------- ExtndSelect ----------------------------}
PROCEDURE ExtndSelect (Where: Point);
VAR OldPt: Point;

{$S SegTESelect }
BEGIN
{$IFC SampCalls}   ALogCall;   {$ENDC}
IF not IsInsertion (TheTerm) THEN
      BEGIN
      FindPoint (OldPt, TheTerm^^.Select.Right);
      Drag (OldPt);
      END;
END;   { ExtndSelect }

{$S SegTE }
ÿ