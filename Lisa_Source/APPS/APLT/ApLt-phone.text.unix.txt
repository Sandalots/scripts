{ Copyright 1983, Apple Computer, Inc. }

{$S SegTEPhone }

procedure HayesSend (HTerm: THTerm; Command: Str40;
                     var Reply: THayesReply; AddCR: Boolean; OutWait: Integer);
  {HayesSend sends Command to the Hayes modem open to HTerm^^.RefNum and}
  {waits for the reply.  The reply is then coded into Reply.  If AddCR is}
  {set then the command is terminated by a CR.  OutWait is the timeout delay}
  {in 1/100 secs.  If the timeout is reached then Reply = Error.  Note that}
  {the Hayes modems do not communicate with parity.}

  var
    Str: Str40;
    NrSent: LongInt;
    eCode: Integer;
    TimeOut: LongInt;
    TrialNr: Integer;
    RefNum: Integer;
    I: Integer;

  {$S SegTEPhone }
  begin {HayesSend}
    {$IFC SampCalls}   ALogCall;   {$ENDC}
    RefNum:=HTerm^^.RefNum;

    Device_Control (eCode, UartName [HTerm^^.Setup.PortA], GroupA [DBNoParity]);

    repeat                                   {Empty then input buffer.}
      Read_Data (eCode, RefNum, Ord (@Str)+1, 20, NrSent, Sequential, 0);
    until NrSent=0;

    if AddCR then begin                      {Set the command string.}
      Str:='x';
      Str [1]:=CR;
      Str:=Concat (Command, Str);

    end else
      Str:=Command;

                                             {Write the command string.}
    Write_Data (eCode, RefNum, Ord (@Str)+1, Length (Str), NrSent, Sequential, 0);
    if (eCode>0) or (NrSent<>Length (Str)) then begin
      Reply:=Error;
      Exit (HayesSend);
    end;

    for TrialNr:=1 to 2 do begin             {Trial #1: Command echo; Trial #2: Response.}
      Str:='';                               {Read the reply.}
      TimeOut:=Time + OutWait;
      repeat
        I:=0;
        Read_Data (eCode, RefNum, Ord (@I)+1, 1, NrSent, Sequential, 0);
        if eCode>0 then begin
          Reply:=Error;
          Exit (HayesSend);
        end;

        if NrSent=0 then
          I:=0;

        if Chr (I) in ['0'..'9', 'A'..'Z'] then begin
          Str:=Concat (Str, 'x');
          Str [Length (Str)]:=Chr (I);
        end;
      until (Chr (I)=LF) or (Time>TimeOut);
    end;

    with HTerm^^.Setup do
      Device_Control (eCode, UartName [PortA], GroupA [Parity]);

    if Str='OK' then                         {Verify and convert the reply.}
      Reply:=OKReply

    else if Str='CONNECT' then
      Reply:=Connect

    else if Str='RING' then
      Reply:=Ring

    else if Str='NOCARRIER' then
      Reply:=NoCarrier

    else if Str='CONNECT1200' then
      Reply:=Connect1200

    else
      Reply:=Error;
  end; {HayesSend}


procedure HayesReady (HTerm: THTerm; var DidOK: Boolean);
  {HayesReady makes sure that the Hayes modem open to HTerm^^.RefNum is in}
  {local mode.  DidOK is returned false if there was any problem trying to}
  {ready the modem.}

  var
    Reply: THayesReply;
    TrialNr: Integer;

  {$S SegTEPhone }
  begin {HayesReady}
    {$IFC SampCalls}   ALogCall;   {$ENDC}
    if HTerm^^.OffHook then                       {Assume it is on-line.}
      TrialNr:=0
    else                                          {Assume it of local.}
      TrialNr:=1;

    repeat
      TrialNr:=TrialNr+1;
      if Odd (TrialNr) then begin                 {On-line -> local.}
        Wait (100);
        HayesSend (HTerm, '+++', Reply, False, 100);

      end else                                    {Local -> local.}
        HayesSend (HTerm, 'AT', Reply, True, 100);
    until (Reply=OKReply) or (TrialNr>=6);

    DidOK:= (Reply=OKReply);
  end; {HayesReady}


procedure HayesLocal (HTerm: THTerm; var DidOK: Boolean);
  {HayesLocal tries to get the Hayes modem connected to HTerm from the on-line}
  {state into the local (command) state.  If this succeeds, then DidOK gets}
  {set to True.}

  var
    TrialNr: Integer;
    Reply: THayesReply;

  {$S SegTEPhone }
  begin {HayesLocal}
    {$IFC SampCalls}   ALogCall;   {$ENDC}
    TrialNr:=0;
    repeat
      TrialNr:=TrialNr+1;

      Wait (100);
      HayesSend (HTerm, '+++', Reply, False, 100);
    until (Reply=OKReply) or (TrialNr=3);

    DidOK:= (Reply=OKReply);
  end;  {HayesLocal}


procedure HayesDial (HTerm: THTerm; var DidOK: Boolean; var Connected: Boolean);
  {HayesDial dials HTerm's ADNumber on the Hayes modem open to HTerm^^.RefNum.}
  {DidOK is returned False if anything went wrong in communicating with the}
  {modem.  Connected is returned False if the modem couldn't establish the}
  {connection.}

  var
    Str: Str40;
    I: Integer;
    Reply: THayesReply;


  procedure SetBaud (HTerm: THTerm; TheBaud: TDBBaud);
    {SetBaud sets HTerm and its modem to the baud rate specified.  It does this}
    {by first taking the modem off-line (into command mode), then changing the}
    {baud rate to TheBaud, and finally resuming on-line with the new baud.}

    var
      Reply: THayesReply;
      eCode: Integer;

    {$S SegTEPhone }
    begin {SetBaud}
      {$IFC SampCalls}   ALogCall;   {$ENDC}
      HayesLocal (HTerm, DidOK);                    {Try to set local state.}

      if DidOK then begin                           {Set local succeeded.}
        with HTerm^^.Setup do begin                 {Set the new baud rate.}
          BaudRate:=TheBaud;
          Device_Control (eCode, UartName [PortA], GroupC [TheBaud]);
        end;

        HayesSend (HTerm, 'ATO', Reply, True, 100); {Try for resume on-line.}

        if TheBaud=DB1200 then
          DidOK:= (Reply=Connect1200)
        else
          DidOK:= (Reply=Connect);

        if not DidOK then                           {Oops, dropped it!!}
          HayesSend (HTerm, 'ATZ', Reply, True, 100); {Hang up the phone.}
      end;
    end;  {SetBaud}

  {$S SegTEPhone }
  begin {HayesDial}
    {$IFC SampCalls}   ALogCall;   {$ENDC}
    Connected:=False;                             {Expect the worst.}

    HayesReady (HTerm, DidOK);                    {Force to local mode.}
    if not DidOK then
      Exit (HayesDial);

    Str:='ATF1Q0V1S0=0S5=255S12=20M1';       {Configure the modem as we like it.}
    if HTerm^^.Setup.Modem=DBHayes1200 then
      Str:=Concat (Str, 'X1');

    HayesSend (HTerm, Str, Reply, True, 200);
    if Reply<>OKReply then begin
      DidOK:=False;
      Exit (HayesDial);
    end;

    Str:='';                                      {Strip the junk from the number.}
    with HTerm^^.Setup do
      for I:=1 to Length (ADNumber) do
        if ADNumber [I] in
           ['0'..'9', 'P', 'p', 'T', 't', ',', '*', '#'] then begin
          Str:=Concat (Str, 'x');
          Str [Length (Str)]:=ADNumber [I];
        end;

    if Str [1] in ['P', 'p', 'T', 't'] then       {Add the "DIAL" command.}
      Str:=Concat ('ATD' , Str)
    else                                          {Default to pulse dialing.}
      Str:=Concat ('ATDP', Str);

    HayesSend (HTerm, Str, Reply, True, 4000);    {Try to dial.}

    with HTerm^^, Setup do
      case Reply of
        Connect: begin                            {Got there at 300 baud.}
          Connected:=True;

          if BaudRate>DB300 then begin
            SetBaud   (HTerm,           DB300);
            GiveAlert (HTerm, AlrtNote, AL300, TheTitle, '', '');
          end;
        end;

        Connect1200: begin                        {Got there at 1200 baud.}
          Connected:=True;

          if BaudRate<>DB1200 then begin
            SetBaud   (HTerm,           DB1200);
            GiveAlert (HTerm, AlrtNote, AL1200, TheTitle, '', '');
          end;
        end;

        NoCarrier:
          GiveAlert (HTerm, AlrtStop, ALCantConnect, TheTitle, '', '');

        otherwise
          DidOK:=False;
      end;
  end; {HayesDial}


procedure HayesHangUp (HTerm: THTerm; var DidOK: Boolean);
  {HayesHangUp hangs up the Hayes modem open to HTerm^^.RefNum.}

  var
    Reply: THayesReply;

  {$S SegTEPhone }
  begin {HayesHangUp}
    {$IFC SampCalls}   ALogCall;   {$ENDC}
    HayesReady (HTerm, DidOK);
    if not DidOK then
      Exit (HayesHangUp);

    HayesSend (HTerm, 'ATZ', Reply, True, 100);
    if Reply<>OKReply then
      DidOK:=False;
  end; {HayesHangUp}


procedure HayesConnected (HTerm: THTerm; var DidOK: Boolean; var IsTalking: Boolean);
  {HayesConnected checks to see if the Hayes modem connected to HTerm is on-line}
  {to a host computer.  DidOK is returned true if there were no problems in trying}
  {to check this.  IsTalking is returned true if the Hayes was (and is) on-line.}

  var
    TrialNr: Integer;
    Reply: THayesReply;

  {$S SegTEPhone }
  begin {HayesConnected}
    {$IFC SampCalls}   ALogCall;   {$ENDC}
    HayesLocal (HTerm, DidOK);                    {Try to force to local mode.}

    IsTalking:=False;                             {Assume the worst.}
    if DidOK then begin                           {Set local succeeded (was on-line).}
      HayesSend (HTerm, 'ATO', Reply, True, 100); {Try for resume on-line.}
      if Reply in [Connect, Connect1200] then     {Resumed OK!}
        IsTalking:=True
      else
        DidOK:=False;                             {Couldn't resume ... oops, fumbled!}

    end else begin                                {It wasn't on-line.}
      HayesSend (HTerm, 'AT', Reply, True, 100);  {Make sure there's a modem.}
      DidOK:= (Reply=OKReply);
    end;
  end;  {HayesConnected}


procedure DoPhoneMenu (HTerm: THTerm);
  {DoPhoneMenu enables and disables items is the PhoneMenu depending on}
  {HTerm^^.OffHook, only if HTerm is the active terminal.}

  {$S SegTEPhone }
  begin {DoPhoneMenu}
    {$IFC SampCalls}   ALogCall;   {$ENDC}
    if HTerm=TheTerm then
      with HTerm^^, Setup do
        if OffHook then begin
          DisableItem (TheMenus [DialMenu], DialDial  );
          EnableItem  (TheMenus [DialMenu], DialHangUp);

        end else begin
          if OnLine and (Modem<>DBOther) then
            EnableItem  (TheMenus [DialMenu], DialDial)
          else
            DisableItem (TheMenus [DialMenu], DialDial);

          DisableItem  (TheMenus [DialMenu], DialHangUp);
        end;
  end;  {DoPhoneMenu}


procedure ForceModem (HTerm: THTerm);
  {ForceModem gives the alert saying that we had trouble using the modem}
  {connected to HTerm, and to turn it off and on again.}

  var
    ModemStr: Str40;
    PortStr:  Str40;

  {$S SegTEPhone }
  begin {ForceModem}
    {$IFC SampCalls}   ALogCall;   {$ENDC}
    with HTerm^^, Setup do begin
      with SetupForm [SetupCompat].HCkForm^^ do begin
      {$R-}
        ModemStr:=ckBoxLst [DBModem]^^.CkItmLst [           Modem ]^^.iTitle^^;
        PortStr :=ckBoxLst [DBPortA]^^.CkItmLst [SetClrMap [PortA]]^^.iTitle^^;
      {$IFC RangeCheck}
      {$R+}
      {$ENDC}
      end;

      GiveAlert (HTerm, AlrtStop, ALForceModem, TheTitle, ModemStr, PortStr);
    end;
  end;  {ForceModem}


procedure DialPhone (HTerm: THTerm);
  {DialPhone calls the AutoDial number if it is non-empty.}

  var
    DidOK: Boolean;
    Connected: Boolean;

  {$S SegTEPhone }
  begin {DialPhone}
    {$IFC SampCalls}   ALogCall;   {$ENDC}
    if (HTerm^^.Setup.Modem    <> DBOther) and
       (HTerm^^.Setup.ADNumber <> '') then begin
      with HTerm^^, Setup do
        if IsActive then
          GiveAlert (HTerm, AlrtWait, ALDialing, TheTitle, ADNumber, '')
        else
          GiveAlert (HTerm, AlrtNote, ALDialed , TheTitle, ADNumber, '');

      case HTerm^^.Setup.Modem of
        DBHayes1200, DBHayes300:
          HayesDial (HTerm, DidOK, Connected);

        {Other modems ... ?}
      end;

      if IsActive then
        DoneAlert;

      if not DidOK then
        ForceModem (HTerm);

      HTerm^^.OffHook:=Connected;
      DoPhoneMenu (HTerm);
    end;
  end; {DialPhone}


procedure HangUp (HTerm: THTerm);
  {HangUp hangs up the phone connected to HTerm's modem.}

  var
    DidOK: Boolean;

  {$S SegTEPhone }
  begin {HangUp}
    {$IFC SampCalls}   ALogCall;   {$ENDC}
    if HTerm^^.Setup.Modem<>DBOther then begin
      if IsActive then
        GiveAlert (HTerm, AlrtWait, ALHangUp, HTerm^^.TheTitle, '', '')
      else
        GiveAlert (HTerm, AlrtNote, ALHungUp, HTerm^^.TheTitle, '', '');

      case HTerm^^.Setup.Modem of
        DBHayes1200, DBHayes300:
          HayesHangUp (HTerm, DidOK);

        {Other modems ... ?}
      end;

      if IsActive then
        DoneAlert;

      if not DidOK then
        ForceModem (HTerm);

      HTerm^^.OffHook:=False;
      DoPhoneMenu (HTerm);
    end;
  end; {HangUp}


procedure Connected (HTerm: THTerm);
  {Connected tests to see if HTerm's modem is on-line to a host.  If not then}
  {Connected does nothing.  If it is then it checks with the user to see if s/he}
  {wants to leave it connected to the host or wants to hang it up.}

  var
    IsTalking: Boolean;
    DidOK: Boolean;

  {$S SegTEPhone }
  begin {Connected}
    {$IFC SampCalls}   ALogCall;   {$ENDC}
    if HTerm^^.Setup.Modem<>DBOther then begin
      case HTerm^^.Setup.Modem of
        DBHayes1200, DBHayes300:
          HayesConnected (HTerm, DidOK, IsTalking);

        {Other modems ... ?}
      end;

      if DidOK then begin
        if IsTalking then begin
          if IsActive then begin
            ParamAlert (HTerm^^.TheTitle, '', '');
            if AskAlert (MyAlerts, ALTalking)=ABtnHangUp then
              HangUp (HTerm);

          end else
            if HTerm^^.Setup.AutoDial then begin
              HangUp (HTerm);

              GiveAlert (HTerm, AlrtNote, ALWasTalking, HTerm^^.TheTitle, '', '');

            end else
              GiveAlert (HTerm, AlrtNote, ALIsTalking, HTerm^^.TheTitle, '', '');
        end;

      end else begin
        ForceModem (HTerm);

        IsTalking:=False;
      end;

      HTerm^^.OffHook:=IsTalking;
      DoPhoneMenu (HTerm);
    end;
  end;  {Connected}


procedure EndPhone (DoIt: Boolean);
  {EndPhone sets TheTerm^^.ADNumber to AnswerMsg, and dials if DoEndDial}
  {is set true.}

  var
    TheTitle: Str40;

  {$S SegTEPhone }
  begin {EndPhone}
    {$IFC SampCalls}   ALogCall;   {$ENDC}
    if DoIt and (AnswerMsg<>'') then begin
      if AnswerMsg<>'' then
        TheTitle:=Concat (BaseDialDial, ' "', AnswerMsg, '"')
      else
        TheTitle:=Concat (BaseDialDial, '...');
      SetItem (TheMenus [DialMenu], DialDial, @TheTitle);

      TheTerm^^.Setup.ADNumber:=AnswerMsg;

      if DoEndDial then
        DialPhone (TheTerm);
    end;
    Answering:=False;

    ClearDBox;
  end; {EndPhone}


procedure TrackMessage (MsePt: Point);
  {TrackMessage tracks the mouse while it's in the dialog folder.}

  {$S SegTEPhone }
  begin {TrackMessage}
    {$IFC SampCalls}   ALogCall;   {$ENDC}
    SetStdCursor (iCrsInactive);
  end; {TrackMessage}


procedure BeginPhone (ShouldDoEndDial: Boolean);
  {BeginPhone starts the keystroke dialog for the phone number for TheTerm.}

  var
    ThePrompt: Str40;

  {$S SegTEPhone }
  begin {BeginPhone}
    {$IFC SampCalls}   ALogCall;   {$ENDC}
    with TheDBox do                          {Kill pending dialog.}
      if IsOpen then
        CallEndProc (False, EndProc);

    DoEndDial:=ShouldDoEndDial;

    GetItem (TheMenus [MsgMenu], MsgPhone, @ThePrompt);

    AnswerMsg:='';
    Answering:=True;
    SetDBox (Pointer (Ord (@ThePrompt)),     {The prompt (in-data).}
             Pointer (Ord (@AnswerMsg)),     {The resulting number (out-data).}
             @GetName,                       {The event procedure.}
             @TrackMessage,                  {The mouse track procedure.}
             @RestoreName,                   {The restore (update) procedure.}
             @EndPhone);                     {Dialog end procedure.}

    SetPort (DialogFolder);
    RestoreName;
  end; {BeginPhone}



procedure HayesCheck (HTerm: THTerm; HRow: THLine; var Dropped: Boolean);
  {HayesCheck checks if the phone was dropped when a Hayes modem is used.}
  {We checks the text to see if the message matches the Hayes disconnect}
  {message (diddled by the parity because Hayes uses none).  If the message}
  {claims that the line was dropped then we try to verify it by forcing the}
  {Hayes off-line.  This should not succeed if the message was true.  If it}
  {did succeed then the message was spurrious, and we try to put the modem}
  {back on-line.  If resuming on-line doesn't succeed then we fumbled it.}
  {Oops.  So we placethe blame on the modem, as if it really dropped it in}
  {the first place.}

  var
    I: Integer;
    TrialNr: Integer;
    Reply: THayesReply;
    DidOK: Boolean;

  {$S SegTEPhone }
  begin {HayesCheck}
    {$IFC SampCalls}   ALogCall;   {$ENDC}
    Dropped:=True;                            {Assume hayes dropped it.}
    I:=1;
    with HTerm^^.Setup, HRow^^ do             {If row doesn't match message ...}
      while Dropped and (I<=Length (HayesDrop [Parity])) do
        if TextLine [I].Ch=HayesDrop [Parity] [I] then
          I:=I+1
        else
          Dropped:=False;                     {Then it wasn't dropped.}

    if Dropped then begin                     {Make sure message wasn't spurrious!}
      HayesReady (HTerm, DidOK);              {Try to set to local state.}

      if DidOK then begin                     {Force local succeeded (didn't drop)!!!}
        HayesSend (HTerm, 'ATO', Reply, True, 100); {Try for resume on-line.}
        if Reply in [Connect, Connect1200] then {Resumed OK!}
          Dropped:=False
        else
          {Do nothing.};                      {Couldn't resume ... oops, fumbled!}

      end else
        {Do nothing};                         {Yes, it was really dropped.}
    end;

    if Dropped then                           {Quiet the modem speaker.}
      HayesSend (HTerm, 'ATM0', Reply, True, 100);
  end; {HayesCheck}


procedure CheckDisconnect (HTerm: THTerm; HRow: THLine);
  {CheckDisconnect is called every time a linefeed is received from the host.}
  {HRow is the line that could tell us that the modem dropped the phone.}

  var
    Dropped: Boolean;

  {$S SegTEPhone }
  begin {CheckDisconnect}
    {$IFC SampCalls}   ALogCall;   {$ENDC}
    if HTerm^^.Setup.Modem<>DBOther then begin
      case HTerm^^.Setup.Modem of
        DBHayes1200, DBHayes300:
          HayesCheck (HTerm, HRow, Dropped);

        {Other modems ... ?}
      end;

      if Dropped then begin
        GiveAlert (HTerm, AlrtStop, ALDisconnect, HTerm^^.TheTitle, '', '');

        HTerm^^.OffHook:=False;
        DoPhoneMenu (HTerm);
      end;
    end;
  end; {CheckDisconnect}
ÿ