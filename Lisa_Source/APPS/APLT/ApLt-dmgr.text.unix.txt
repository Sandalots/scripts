{ Copyright 1983, Apple Computer, Inc. }

(* ********************************************************************* *)

(* Dialog Box Manager *)
(* version 4.0 *)

(* ********************************************************************* *)

UNIT DMgr;      (* Segment DMGR *)
{$E error.text}
{$SETC SampCalls := False }

INTERFACE
USES
    {$IFC SampCalls}
    {$U aplt/tracecalls.obj} tracecalls,
    {$ENDC}

    (*$U UNITSTD *)   UnitStd,      (* interface dependency *)
    (*$U UNITHZ*)     UnitHz,
    (*$U STORAGE*)    Storage,
    (*$U QUICKDRAW*)  QuickDraw,
    (*$U FONTMGR*)    FontMgr,

    (*$U libos/SYSCALL.obj*)    Syscall,      (* implementation units *)
    (*$U WM.EVENTS*)  Events,
    (*$U WM.FOLDERS*) Folders,
    (*$U WM.MENUS*)   Menus,
    (*$U ALERTMGR*)   AlertMgr;

const
    maxCkItm   = 20;           (* max number of items in a ckBox         *)
type
    TChSet     = Set Of Char;
    String30   = String[30];
    String40   = String[40];
    TStrHnd  = ^TStrPtr;
    TStrPtr  = ^Str255;

    TDBoxInfo  =
        record
            isOpen:      Boolean;       (* is Dialog Box open?           *)
            inData:      QdPtr;         (* pointer to proper input data  *)
            outData:     QdPtr;         (* pointer to proper output data *)
            ProcessProc: ProcPtr;       (* procedure to process events   *)
            TrkMseProc:  ProcPtr;       (* procedure to track cursor     *)
            RestoreProc: ProcPtr;       (* procedure to restore dialog   *)
            EndProc:     ProcPtr;       (* procedure to end dialog       *)
        end;

    TCkItmHnd   = ^TCkItmPtr;
    TCkItmPtr   = ^TCkItmInfo;
    TCkItmLst   = Array[0..maxCkItm] of TCkItmHnd;
    TCkItmInfo  =
        record
            font:       Integer;        (* font of ckBox item           *)
            iTitle:     TStrHnd;        (* name of ckBox item           *)
            iLoc:       Point;          (* location of start of iTitle  *)
            hitBox:     Rect;           (* actual hit box is bigger     *)
            itmBox:     Rect;           (* box that gets colored in     *)
        end;

    TCkBoxHnd  = ^TCkBoxPtr;
    TCkBoxPtr  = ^TCkBoxInfo;
    TCkBoxLst  = Array[0..0] of TCkBoxHnd;
    TCkBoxInfo =
        record
            ckBoxId:    TB;             (* logical index of ckBox       *)
            numCkItm:   TB;             (* number of items              *)
            curOnOff:   TB;             (* which on/off box is checked  *)
            curCkItm:   TB;             (* which item is checked        *)
            hasOnOff:   Boolean;        (* are items 1 and 2 special?   *)
            ckItmLst:   TCkItmLst;      (* ckItem list may be empty     *)
            DrawProc:   ProcPtr;        (* procedure to draw ckBox      *)
            HitTstFunc: ProcPtr;        (* function to hit test ckBox   *)
        end;

    TCkFormHnd  = ^TCkFormPtr;
    TCkFormPtr  = ^TCkFormInfo;
    TCkFormInfo =
        record
            numCkBox: TB;               (* number of ckBoxes in list    *)
            vCkForm:  Integer;          (* height of dialog box         *)
            ckBoxLst: TCkBoxLst;        (* list of ckBoxPtrs            *)
        end;

var
    dmHz:       THz;
    (* cursors *)
    dBoxCrs:    Cursor;
    arrwCrs:    Cursor;
    (* the dialog Box *)
    theDBox:    TDBoxInfo;
    (* tracing flag *)
    traceDMgr:  Boolean;

    (* message dialog *)
    procedure ShowMsg      (msg: Str255);
    procedure TrkMsgMse    (msePt: Point);

    (* keyStroke dialog *)
    procedure GetName      (thisEvent: EventRec);
    procedure TrkNameMse   (msePt: Point);
    procedure RestoreName;

    (* ckBox dialog *)
    procedure ReadCkBox    (var ckFile: Text; var info: TCkBoxHnd);
    procedure CkFormHeight (info: TCkFormHnd);
    procedure ShowCkBtn    (info: TCkBoxHnd);
    procedure ShowCkBox    (info: TCkBoxHnd);
    procedure ShowCkForm   (info: TCkFormHnd);
    function  CkBtnHit     (info: TCkBoxHnd; pt: Point;
                            var ckItm: Integer): Boolean;
    function  CkBoxHit     (info: TCkBoxHnd; pt: Point;
                            var ckItm: Integer): Boolean;
    procedure CkFormSelect (info: TCkFormHnd; var ckBox,ckItm: Integer);
    procedure GetCkHit     (thisEvent: EventRec);
    procedure TrkCkBoxMse  (msePt: Point);
    procedure RestoreCkForm;

    (* miscellaneous *)
    procedure InitDMgr     (hz: THz; font: Integer);
    procedure SetDFont     (font: Integer);
    procedure SetDBox      (inD,outD: QdPtr; prcP,trkP,rstP,endP: ProcPtr);
    procedure ClearDBox;
    procedure RestoreDBox;

    (* calling procs *)
    procedure CallPrcsProc (thisEvent: EventRec; ProcessProc: ProcPtr);
    procedure CallTrkProc  (msePt: Point; TrkProc: ProcPtr);
    procedure CallRstProc  (RestoreProc: ProcPtr);
    procedure CallEndProc  (doItFlag: Boolean; EndProc: ProcPtr);
    procedure CallShowCkBox(info: TCkBoxHnd; ShowProc: ProcPtr);
    function  CallHitTst   (info: TCkBoxHnd; pt: Point;
                            var ckItm: Integer; HitFunc: ProcPtr): Boolean;


(* ********************************************************************* *)

IMPLEMENTATION

{$IFC fsymok}
{$D+}
{$ELSEC}
{$D-}
{$ENDC}

(*$SetC  traceOn := FALSE  *)

(*$S DMGR*)

(*$SETC RangeCheck:=False *)
(*$IFC RangeCheck *)
  (*$R+*)
(*$ELSEC*)
  (*$R-*)
(*$ENDC*)


(* ********************************************************************* *)

var (* DMgr Font info used in laying out ckBoxes, displaying messages *)
    ascent:     Integer;
    descent:    Integer;
    lSpace:     Integer;
    mSpace:     Integer;        (* width of the character "m"            *)
    lSpTop:     Integer;        (* dist fm line top to ascender top      *)
    offset:     Integer;        (* baseLine := offset+row*lineSp         *)
    (* message info *)
    msgLoc:     Point;          (* col=4,row=1*)
    msgHeight:  Integer;        (* 2*lSpace   *)
    msgRect:    Rect;           (* bBox of msg*)
    (* ckBox info *)
    itmBoxSize: Rect;           (* 2/3 ascent *)

(* ********************************************************************* *)

(* Dialog Box GuideLines *)

(* ********************************************************************* *)

(* The only kinds of events that can be gotten in the dialog folder are  *)
(* keyDown, buttonDown, buttonUp, folderUpdate and nilEvent. Ignore      *)
(* folderUpdate, since the only way to get it is thru a call to          *)
(* DialogHeight, and it has a flag allowing you to specify if you want   *)
(* it.  We never do. *)

(* ********************************************************************* *)


    procedure OpenDBox(height: Integer);
    var thisPort: GrafPtr;

    (*$S DMGR*)
    begin
        {$IFC SampCalls}   ALogCall;   {$ENDC}
        GetPort(thisPort);
        if thisPort=dialogFolder then begin
            DialogHeight(height,FALSE);
            ClipRect (thisPort^.portRect);
            theDBox.isOpen := TRUE;
        end;
        FillRect(thisPort^.portRect,white);
    end; (* OpenDBox *)

(* ********************************************************************* *)

(* message dialog *)

(* ********************************************************************* *)

(* Messages never hang around.  They are always removed on the next      *)
(* available event.  Therefore, they do not need a RestoreProc or a      *)
(* ProcessProc, only a ShowProc. *)

(* ********************************************************************* *)


    procedure ShowMsg(* msg: Str255 *);

    (*$S DMGR*)
    begin
        {$IFC SampCalls}   ALogCall;   {$ENDC}
        (*$IfC traceOn *)
        if traceDMgr then
            writeln('    ShowMsg(',msg,')');
        (*$EndC *)
        OpenDBox(msgHeight);
        (* write msg *)
        MoveTo(msgLoc.h,msgLoc.v);
        DrawString(msg);
    end; (* ShowMsg *)

(* ********************************************************************* *)


    procedure TrkMsgMse(* msePt: Point *);

    (*$S DMGR*)
    begin
    {$IFC SampCalls}   ALogCall;   {$ENDC}
    end; (* TrkMsgMse *)

(* ********************************************************************* *)

(* keyStroke dialog *)

(* ********************************************************************* *)


    procedure GetName(* thisEvent: EventRec *);
    const
        maxLen = 40;            (* max length of names *)
        EOL = 13;               (* ascii codes *)
        ESC = 27;
        BSP = 8;
    var ch:        char;
        chStr:     String[1];
        ersRect:   Rect;
        chWidth:   Integer;
        curLen:    Integer;
        name:      ^String40;
        doneFlag:  Boolean;
        doitFlag:  Boolean;
        abortFlag: Boolean;
        thisPort:  GrafPtr;

    (*$S DMGR*)
    begin
        {$IFC SampCalls}   ALogCall;   {$ENDC}
        (* Process the event record, adding ascii characters to the      *)
        (* dialogbox output data until get a RET char.  The prompt has   *)
        (* already been displayed in the SetUpProc or RestoreProc. *)
        (*$IfC traceOn *)
        if traceDMgr then
            writeln('GetName(',thisEvent.what:2,').');
        (*$EndC *)
        (* process only keyDown events *)
        if thisEvent.what = keyDown then begin
            (* get state of output data *)
            name      := Pointer(Ord(theDBox.outData));
            curLen    := Length(name^);
            chStr     := ' ';
            ch        := thisEvent.ascii;
            (* control flags *)
            doneFlag  := FALSE;
            abortFlag := FALSE;

            if (ord(ch) in [32..126]) and (curLen<maxLen) then begin
                chStr[1] := ch;
                DrawChar(ch);
                name^ := concat(name^,chStr);
                curLen := curLen+1;
            end else if ord(ch)=EOL then begin
                doneFlag := TRUE;
            end else if Abort then begin
                name^ := '';
                abortFlag := TRUE;
            end else if (ord(ch)=BSP) and (curLen > 0) then begin
                ch := name^[curLen];
                chWidth := CharWidth(ch);
                Move(-chWidth,0);
                SetRect(ersRect,0,-ascent,chWidth,descent);
                GetPort(thisPort);
                with thisPort^.pnLoc do
                OffSetRect(ersRect,h,v);
                FillRect(ersRect,white);
                Delete(name^,curLen,1);
                curLen := curLen-1;
            end;

            if doneFlag or abortFlag then begin
                doItFlag := not abortFlag;
                CallEndProc(doItFlag,theDBox.endProc);
            end;
        end;
    end; (* GetName *)

(* ********************************************************************* *)


    procedure TrkNameMse(* msePt: Point *);

    (*$S DMGR*)
    begin
    {$IFC SampCalls}   ALogCall;   {$ENDC}
    end; (* TrkNameMse *)

(* ********************************************************************* *)


    procedure RestoreName;
    var strPtr: TStrPtr;
        name:   String40;
        prmt:   Str255;
        len:    Integer;

    (*$S DMGR*)
    begin
        {$IFC SampCalls}   ALogCall;   {$ENDC}
        (*$IfC traceOn *)
        if traceDMgr then
            writeln('RestoreName');
        (*$EndC *)
        (* reconstruct dialog box in state it was left *)
        with theDBox do begin
            strPtr := Pointer(Ord(inData));
            prmt   := strPtr^;
            prmt   := Concat(prmt, ' ');
            TextFace ([Bold]);
            ShowMsg(prmt);
            TextFace ([]);

            strPtr := Pointer(Ord(outData));
            name   := strPtr^;
            DrawString(name);
        end;
    end; (* RestoreName *)

(* ********************************************************************* *)

(* private ckBox utilities *)

(* ********************************************************************* *)


    procedure SetCkItm(var itm: TCkItmHnd; c,r,f: Integer; title: String40);
    var dh,dv: Integer;
        thisTitle: TStrHnd;

    (*$S DMGR*)
    begin
        {$IFC SampCalls}   ALogCall;   {$ENDC}
        itm       := Pointer(Ord(HAllocate(dmHz,SizeOf(TCkItmInfo))));
        thisTitle := Pointer(Ord(HAllocate(dmHz,Length(title)+1)));
        with itm^^ do begin
            (* set font of ckItm *)
            font := f;
            (* set name of ckItm *)
            iTitle := thisTitle;
            MoveLeft(title,iTitle^^,Length(title)+1);
            (* set starting point for text *)
            dh := c*mSpace;
            dv := r*lSpace+offset;
            SetPt(iLoc,dh+2*mSpace,dv);
            (* set itm ckBox *)
            itmBox := itmBoxSize;
            OffSetRect(itmBox,dh,dv-itmBoxSize.bottom);
            (* set hitBox *)
            SetRect(hitBox,0,0,StringWidth(title)+3*mSpace,lSpace);
            OffsetRect(hitBox,dh-(mSpace div 2),dv-ascent-lSpTop);
        end;
    end; (* SetCkItm *)

(* ********************************************************************* *)


    procedure ScanCkBox(lineBuf: Str255; var c,r,f: Integer; var s: String40);
    var bufIndx: Integer;
        digits: Set of Char;

    (* ***************************************************************** *)


        function  GetInt: Integer;
        var value: Integer;
        (*$S DMGR*)
        begin
            {$IFC SampCalls}   ALogCall;   {$ENDC}
            value := 0;
            while (bufIndx<=Length(lineBuf)) and
            not (lineBuf[bufIndx] in digits) do
                bufIndx := bufIndx+1;
            while (bufIndx<=Length(lineBuf)) and
            (lineBuf[bufIndx] in digits) do begin
                value := Ord(lineBuf[bufIndx])-Ord('0')+10*value;
                bufIndx := bufIndx+1;
            end;
            GetInt := value;
        end; (* GetInt *)

    (* ***************************************************************** *)


        procedure GetString(var thisStr: String40);
        var byteCnt: Integer;
            src: TStrPtr;

        (*$S DMGR*)
        begin
            {$IFC SampCalls}   ALogCall;   {$ENDC}
            thisStr := '';
            while (bufIndx<=Length(lineBuf)) and (lineBuf[bufIndx]<=' ') do
                bufIndx := bufIndx+1;
            byteCnt := Length(lineBuf)-bufIndx+2;
            if byteCnt>40 then
                byteCnt := 40;
            src := Pointer(Ord(@lineBuf)+bufIndx-1);
            MoveLeft(src^,thisStr,byteCnt);
            (*$R-*)
            thisStr[0] := Chr(byteCnt-1);
            (*$IFC RangeCheck *)
              (*$R+*)
            (*$ENDC*)
        end; (* GetString *)

    (* ***************************************************************** *)
(*$S DMGR*)
    begin
        {$IFC SampCalls}   ALogCall;   {$ENDC}
        if lineBuf<>'' then begin
            digits := ['0'..'9'];
            bufIndx := 1;
            c := GetInt;
            r := GetInt;
            f := GetInt;
            GetString(s);
        end;
    end; (* ScanCkBox *)

(* ********************************************************************* *)


    procedure ReadCkItm(var f: Text; info: TCkBoxHnd; var noMore: Boolean);
    var title: String40;
        c,r,font: Integer;
        lineBuf: Str255;
        thisItm: TCkItmHnd;  (* temp handle to pass to SetCkItm *)

    (*$S DMGR*)
    begin
        {$IFC SampCalls}   ALogCall;   {$ENDC}
        (* see if standard CkBox or user defined *)
        Readln(f,lineBuf);
        if lineBuf='' then
            noMore := TRUE
        else if info^^.numCkItm=maxCkItm then
            noMore := TRUE
        else begin
            with info^^ do begin
                noMore := FALSE;
                numCkItm := numCkItm+1;
                ScanCkBox(lineBuf,c,r,font,title);
            end;
            SetCkItm(thisItm,c,r,font,title);
            with info^^ do
                ckItmLst[numCkItm] := thisItm;
        end;
    end; (* ReadCkItm *)

(* ********************************************************************* *)


    procedure ShowCkItm(info: TCkItmHnd; showBox,boxChecked: Boolean);

    (*$S DMGR*)
    begin
        {$IFC SampCalls}   ALogCall;   {$ENDC}
        if info<>nil then with info^^ do begin
            TextFont(font);
            with iLoc do
            MoveTo(h,v);
            if not showBox then
                TextFace ([Bold]);
            DrawString(iTitle^^);
            TextFace ([]);
            if showBox then begin
                if boxChecked then
                    FillRect(itmBox,black)
                else
                    FrameRect(itmBox);
            end;
        end;
    end; (* ShowCkItm *)

(* ********************************************************************* *)

(* ckBox dialog public routines *)

(* ********************************************************************* *)


    procedure ReadCkBox(* var ckFile: Text; var info: TCkBoxHnd *);
    var noMore: Boolean;
        i: Integer;

    (*$S DMGR*)
    begin
        {$IFC SampCalls}   ALogCall;   {$ENDC}
        SetPort(dialogFolder);
        info := Pointer(Ord(HAllocate(dmHz,SizeOf(TCkBoxInfo))));
        with info^^ do begin
            (* init ckBox *)
            numCkItm   := -1;
            curOnOff   := -1;
            curCkItm   := -1;
            hasOnOff   := FALSE;
            DrawProc   := nil;
            HitTstFunc := nil;
            for i := 0 to maxCkItm do
                ckItmLst[i] := nil;
        end;

        (* read in ckBox info *)
        ReadLn(ckFile,info^^.ckBoxId);
        repeat
            ReadCkItm(ckFile,info,noMore);
        until noMore;

        with info^^ do begin
            (* see if standard ckBox *)
            if numCkItm>0 then with ckItmLst[0]^^ do begin
                (* get ckBox hitBox size *)
                for i := 1 to numCkItm do
                    UnionRect(hitBox,ckItmLst[i]^^.hitBox,hitBox);
                (* set standard Draw and Choose procs *)
                DrawProc   := @ShowCkBox;
                HitTstFunc := @CkBoxHit;
            end;
        end;
    end; (* ReadCkBox *)

(* ********************************************************************* *)


    procedure CkFormHeight(* info: TCkFormHnd *);
    var thisHeight,height: Integer;
        i,j,bh,bv: Integer;

    (*$S DMGR*)
    begin
        {$IFC SampCalls}   ALogCall;   {$ENDC}
        height := 0;
        with info^^ do begin
            for i := 0 to numCkBox do begin
                (*$R-*)
                thisHeight := ckBoxLst[i]^^.ckItmLst[0]^^.hitBox.bottom;
                (*$IFC RangeCheck *)
                  (*$R+*)
                (*$ENDC*)
                if thisHeight>height then
                    height := thisHeight;
            end;
            bh := CalcButWidth(0,1,bv);
            (* use MaxInt *)
            bh := height+lSpace div 2;
            bv := 2*bv+30;
            vCkForm := CMax(bh,bv);
        end;
    end; (* CkFormHeight *)

(* ********************************************************************* *)


    procedure ShowCkBtn(* info: TCkBoxHnd *);
    var i,bh,bv: Integer;
        boxChecked: Boolean;
        vLoc: Array[0..1] of Integer;

    (*$S DMGR*)
    begin
        {$IFC SampCalls}   ALogCall;   {$ENDC}
        if info=nil then
            (* do nothing ... should be error *)
        else with info^^ do begin
            (* show the button title *)
            ShowCkItm(ckItmLst[0],FALSE,FALSE);

            (* show two buttons *)
            bh := CalcButWidth(0,1,bv);
            with dialogFolder^.portRect do begin
                vLoc[0] := top+10;
                vLoc[1] := bottom-bv-10;
                for i := 0 to 1 do begin
                    HideButn(i);
                    PushButn(i,FALSE);
                    ShowButn(i,right-20-bh,vLoc[i],i=0);
                end;
            end;
        end;
    end; (* ShowCkBtn *)

(* ********************************************************************* *)


    procedure ShowCkBox(* info: TCkBoxHnd *);
    var i: Integer;
        boxChecked: Boolean;

    (*$S DMGR*)
    begin
        {$IFC SampCalls}   ALogCall;   {$ENDC}
        if info=nil then
            (* do nothing ... should be error *)
        else with info^^ do
            for i := 0 to numCkItm do begin
                boxChecked := (i=curOnOff) or (i=curCkItm);
                ShowCkItm(ckItmLst[i],i<>0,boxChecked);
            end;
    end; (* ShowCkBox *)

(* ********************************************************************* *)


    procedure ShowCkForm(* info: TCkFormHnd *);
    var i: Integer;
        thisPort: GrafPtr;

    (*$S DMGR*)
    begin
        {$IFC SampCalls}   ALogCall;   {$ENDC}
        (*$IfC traceOn *)
        if traceDMgr then
            writeln('ShowCkForm');
        (*$EndC *)
        OpenDBox(info^^.vCkForm);
        with info^^ do begin
            for i := 0 to numCkBox do
                (*$R-*)
                CallShowCkBox(ckBoxLst[i],ckBoxLst[i]^^.DrawProc);
                (*$IFC RangeCheck *)
                  (*$R+*)
                (*$ENDC*)
        end;
    end; (* ShowCkForm *)

(* ********************************************************************* *)


    function  CkBtnHit(* info: TCkBoxHnd; pt: Point;
                        var ckItm: Integer): Boolean *);
    var pushed: TButn;

    (*$S DMGR*)
    begin
        {$IFC SampCalls}   ALogCall;   {$ENDC}
        CkBtnHit := ButnPushed(0,1,pushed,pt);
        ckItm := pushed+1;
    end; (* CkBtnHit *)

(* ********************************************************************* *)


    function  CkBoxHit(* info: TCkBoxHnd; pt: Point;
                        var ckItm: Integer): Boolean *);
    var hit: Boolean;
        i: Integer;

    (*$S DMGR*)
    begin
        {$IFC SampCalls}   ALogCall;   {$ENDC}
        ckItm := 0;
        hit := PtInRect(pt,info^^.ckItmLst[0]^^.hitBox);
        if hit then with info^^ do begin
            for i := 1 to numCkItm do
                if PtInRect(pt,ckItmLst[i]^^.hitBox) then begin
                    ckItm := i;
                    i := numCkItm;
                end;
        end;
        CkBoxHit :=  hit;
    end; (* CkBoxHit *)

(* ********************************************************************* *)


    procedure CkFormSelect(* info: TCkFormHnd; var ckBox,ckItm: Integer *);
    var msePt: Point;
        oldCkItm,i: Integer;
        thisRect: Rect;
        thisEvent: EventRecord;

    (*$S DMGR*)
    begin
        {$IFC SampCalls}   ALogCall;   {$ENDC}
        (* assumes port is already set *)
        (*$IfC traceOn *)
        if traceDMgr then
            writeln('CkFormSelect');
        (*$EndC *)
        repeat
            (* do nothing *)
        until not StillDown;
        GetMouse(msePt);
        (* get rid of buttonUp event *)
        if PeekEvent(thisEvent) then begin
            if thisEvent.what=buttonUp then begin
                GetEvent(thisEvent);
                msePt := thisEvent.where;
            end;
        end;
        ckBox := 0;
        with info^^ do
        for i := 0 to numCkBox do begin
            (*$R-*)
            if CallHitTst(ckBoxLst[i],msePt,
                          ckItm,ckBoxLst[i]^^.HitTstFunc) then
            with ckBoxLst[i]^^ do begin
                ckBox := ckBoxId;
                i := numCkBox;
                if ckBox>0 then
                    if ckItm>0 then begin
                        (* figure out which item was checked *)
                        if hasOnOff and (ckItm<=2) then begin
                            oldCkItm := curOnOff;
                            curOnOff := ckItm;
                        end else begin
                            oldCkItm := curCkItm;
                            curCkItm := ckItm;
                        end;
                        (* clear old item *)
                        if oldCkItm>0 then begin
                            thisRect := ckItmLst[oldCkItm]^^.itmBox;
                            InSetRect(thisRect,1,1);
                            FillRect(thisRect,white);
                        end;
                        (* fill new item *)
                        FillRect(ckItmLst[ckItm]^^.itmBox,black);
                    end;
            end;
            (*$IFC RangeCheck *)
              (*$R+*)
            (*$ENDC*)
        end;
        (*$IfC traceOn *)
        if traceDMgr then
            writeln('    ',ckBox:2,',',ckItm:2);
        (*$EndC *)
    end; (* CkFormSelect *)

(* ********************************************************************* *)


    procedure GetCkHit(* thisEvent: EventRec *);
    var info: TCkFormHnd;
        ckBox,ckItm: Integer;

    (*$S DMGR*)
    begin
        {$IFC SampCalls}   ALogCall;   {$ENDC}
        (*$IfC traceOn *)
        if traceDMgr then
            writeln('GetCkHit(',thisEvent.what:2,')');
        (*$EndC *)
        if thisEvent.what=buttonDown then with theDBox do begin
            info := Pointer(Ord(inData));
            CkFormSelect(info,ckBox,ckItm);
            if (ckBox=0) and (ckItm<>0)  then
                CallEndProc(ckItm=2,endProc)
        end;
    end; (* GetCkHit *)

(* ********************************************************************* *)


    procedure TrkCkBoxMse(* msePt: Point *);
    var info:     TCkFormHnd;
        i,j,ckItm,dBoxv: Integer;
        inCkItm:  Boolean;
        mseMoved: Boolean;
        nHood:    Rect;
        thisPort: GrafPtr;

    (*$S DMGR*)
    begin
        {$IFC SampCalls}   ALogCall;   {$ENDC}
        (* assumes port is already set *)
        (* msePt is in dialog coords *)
        GetPort(thisPort);
        dBoxv := thisPort^.portRect.bottom;
        info := Pointer(Ord(theDBox.inData));
        with info^^ do
        repeat
            GetMouse(msePt);
            inCkItm := FALSE;
            for i := 0 to numCkBox do
                (*$R-*)
                if CkBoxHit(ckBoxLst[i],msePt,ckItm) then begin
                    nHood := ckBoxLst[i]^^.ckItmLst[ckItm]^^.hitBox;
                    i := numCkBox;
                    inCkItm := ckItm<>0;
                end;
                (*$IFC RangeCheck *)
                  (*$R+*)
                (*$ENDC*)
            if inCkItm then begin
                SetCursor(dBoxCrs);
                repeat
                    GetMouse(msePt);
                    mseMoved := not PtInRect(msePt,nHood);
                until EventAvail or mseMoved;
                if mseMoved then
                    SetCursor(arrwCrs);
            end;
        until EventAvail or (msePt.v>dBoxv);
    end; (* TrkCkBoxMse *)

(* ********************************************************************* *)


    procedure RestoreCkForm;
    var info: TCkFormHnd;

    (*$S DMGR*)
    begin
        {$IFC SampCalls}   ALogCall;   {$ENDC}
        (*$IfC traceOn *)
        if traceDMgr then
            writeln('RestoreCkForm');
        (*$EndC *)
        info := Pointer(Ord(theDBox.inData));
        ShowCkForm(info);
    end; (* RestoreCkForm *)

(* ********************************************************************* *)

(* miscellaneous *)

(* ********************************************************************* *)


    procedure SetDFont(* font: Integer *);
    var dh,dv: Integer;
        errNum:Integer;
        fInfo: FontInfo;
        fid:   TLFntId;

    (*$S DMGR*)
    begin
        {$IFC SampCalls}   ALogCall;   {$ENDC}
        SetPort(dialogFolder);
        TextFont(font);
        TextFace([]);
        fid.fam := font;
        fid.seteface := [];
        fid.dev := devScreen;
        (* get font info used in reading ckForms *)
        if not FmFontMetrics(fid,fInfo,errNum) then begin
            (* do nothing ... should be error *)
            Halt;
        end else begin
            ascent  := fInfo.ascent;
            descent := fInfo.descent;
            lSpace  := fInfo.ascent+fInfo.descent+fInfo.leading;
            mSpace  := CharWidth('m');
            lSpTop  := (lSpace-ascent-descent) div 2;
            offset  := lSpTop+ascent-lSpace div 2;
            (* ckBox vars *)
            dv:=ascent-3;
            dh:=(3*dv) div 2;
            SetRect(itmBoxSize,0,0,dh,dv);
            (* set msg height and loc same as one line ckBox at 4,1 *)
            msgHeight := 2*lSpace;
            dh := 4*mSpace;
            dv := 1*lSpace+offset;
            SetPt(msgLoc,dh+mSpace,dv);
            SetRect(msgRect,-30000,0,30000,lSpace);
            OffsetRect(msgRect,0,dv-ascent-lSpTop);
        end;
    end; (* SetDFont *)

(* ********************************************************************* *)


    procedure SetDBox(* inD,outD: QdPtr; prcP,trkP,rstP,endP: ProcPtr *);

    (*$S DMGR*)
    begin
        {$IFC SampCalls}   ALogCall;   {$ENDC}
        with theDBox do begin
            inData      := inD;
            outData     := outD;
            processProc := prcP;
            trkMseProc  := trkP;
            restoreProc := rstP;
            endProc     := endP;
        end;
    end; (* SetDBox *)

(* ********************************************************************* *)


    procedure ClearDBox;

    (*$S DMGR*)
    begin
        {$IFC SampCalls}   ALogCall;   {$ENDC}
        (*$IfC traceOn *)
        if traceDMgr then
            writeln('ClearDBox.');
        (*$EndC *)
        with theDBox do begin
            if isOpen then
                DialogHeight(0,False)
            else
                (* do nothing ... should be error *);
            isOpen := FALSE;
        end;
        SetDBox(NIL, NIL, NIL, NIL, NIL, NIL);
    end; (* ClearDBox *)

(* ********************************************************************* *)


    procedure RestoreDBox;

    (*$S DMGR*)
    begin
        {$IFC SampCalls}   ALogCall;   {$ENDC}
        (*$IfC traceOn *)
        if traceDMgr then
            writeln('RestoreDBox.');
        (*$EndC *)
        with theDBox do
        if isOpen then begin
            SetPort(dialogFolder);
            CallRstProc(restoreProc);
        end;
    end; (* RestoreDBox *)

(* ********************************************************************* *)

    procedure InitDMgr(* hz: THz; font: Integer *);

    (*$S DMGR*)
    begin
        {$IFC SampCalls}   ALogCall;   {$ENDC}
        dmHz := hz;
        traceDMgr := FALSE;
        theDBox.isOpen := FALSE;
        (* cursors *)
        with dBoxCrs do begin
            StuffHex(@data[0],'000000000000000000000000000C0018');
            StuffHex(@data[8],'00300060E0C031801B000E0004000000');
            StuffHex(@mask[0],'00000000000000000000000000000000');
            StuffHex(@mask[8],'00000000000000000000000000000000');
            setpt(hotspot,0,10);
        end;
        with arrwCrs do begin
            StuffHex(@data[0],'8000C000E000F000F800FC00FE00FF00');
            StuffHex(@data[8],'F800F800CC008C000600060003000300');
            StuffHex(@mask[0],'0000000040006000700078007C007800');
            StuffHex(@mask[8],'70005000080008000400040002000200');
            setpt(hotspot,0,0);
        end;
        SetDFont(font);
        SetDBox(NIL,NIL,NIL,NIL,NIL,NIL);
    end; (* InitDMgr *)

(* ********************************************************************* *)

(*$S DMGR*)
    procedure CallPrcsProc (* thisEvent: EventRec; ProcessProc: ProcPtr *);
        external;

    procedure CallTrkProc  (* msePt: Point; TrkProc: ProcPtr *);
        external;

    procedure CallRstProc  (* RestoreProc: ProcPtr *);
        external;

    procedure CallEndProc  (* doItFlag: Boolean; EndProc: ProcPtr *);
        external;

    procedure CallShowCkBox(* info: TCkBoxHnd; ShowProc: ProcPtr *);
        external;

    function  CallHitTst   (* info: TCkBoxHnd; pt: Point;
                              var ckItm: Integer; HitFunc: ProcPtr): Boolean *);
        external;

END. (* unit *)


ÿ