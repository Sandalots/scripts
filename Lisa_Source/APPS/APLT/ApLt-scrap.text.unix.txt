{ Copyright 1983, Apple Computer, Inc. }

{changed 05/09/84   In LpEncode changed declaration of Factor to LONGINT so the
                        multiplication won't overflow.}

{$S SegTEScrap }

procedure PaintScrap;
  {PaintScrap paints the scrap in ScrapFolder.}

  var
    SavePort: GrafPtr;
    Index: TB;
    TextRect: Rect;
    blText: Point;
    HRow: THLine;
    RowNr: Integer;
    ColNr: Integer;
    Str: packed array [MinCol..MaxCol] of Char;
    eCode: Integer;
    rH: Rect;
    rV: Rect;

  {$S SegTEScrap }
  begin {PaintScrap}
    {$IFC SampCalls}   ALogCall;   {$ENDC}
    GetPort (SavePort);
    SetPort (ScrapFolder);

    FlushRects (rH, rV);                                    {Size the scroll bars.}
    SetSBRect (TheScrap.VSBar, rV);
    SetSBRect (TheScrap.HSBar, rH);

    TextRect.TopLeft:=ScrapFolder^.PortRect.TopLeft;
    TextRect.Right :=rV.Left;
    TextRect.Bottom:=rH.Top;

    ClipRect (TextRect);                                    {Erase the folder.}
    EraseRect (TextRect);

    InsetRect (TextRect, HMargin, VMargin);                 {Margin for paint.}
    ClipRect (TextRect);

    TextFace ([]);
    TextMode (SrcOr);
    if TheScrap.HCache<>NIL then begin                      {We have the contents.}
      TextFont (TheScrap.Style^^.Font.Fam);

      blText.H:=TextRect.Left;                              {Paint it.}
      blText.V:=TextRect.Top + TheScrap.Style^^.Above;
      RowNr:=MinRow;
      while (blText.V<=TextRect.Bottom) and
            (RowNr<=TheScrap.HCache^^.LinesInCache) do begin
        ExpandRow (TheScrap.HCache, RowNr, HRow, TRUE);
        with HRow^^ do
          for ColNr:=MinCol to MaxCol do
            Str [ColNr]:=TextLine [ColNr].Ch;
        FreeRow (HRow);

        MoveTo (blText.H, blText.V);
        DrawText (@Str, 0, MaxCol-MinCol+1);

        blText.V:=blText.V + TheScrap.Style^^.VertSp;
        RowNr:=RowNr+1;
      end;

    end else begin                                          {Contents lost.}
      TextFont (DeskStyle.Fam);
      DrawAlert (MyAlerts, ALEmptyScrap, TextRect);
    end;

    ClipRect (ScrapFolder^.PortRect);
    if TheTerm=ScrapTerm then begin
      PaintSB (TheScrap.VSBar);
      PaintSB (TheScrap.HSBar);
      PaintGrow;

    end else
      HideSBorder (ScrapFolder^.PortRect);

    SetPort (SavePort);
  end; {PaintScrap}


PROCEDURE MySeqLpd(Lpd: TLpd; VAR AChad: TAchad);
{MySeqLpd is called for TE's class of scrap when someone (perhaps TE) wants}
{to read the scrap.}

VAR Scrap:      TPScrap;
    RowNr:      INTEGER;
    ColNr:      INTEGER;
    HRow:       THLine;
    LastCol:    INTEGER;
    Scanning:   BOOLEAN;
    ICh:        INTEGER;
    FaceSet:    BOOLEAN;
    Face:       CHAR;
    MyFaceOK:   BOOLEAN;
    MyFace:     CHAR;
    TheChar:    TTrix;
    eCode:      INTEGER;
    HCache:     THCache;
    LastFull:   BOOLEAN;


    PROCEDURE LpDecode(VAR RowNr: INTEGER; VAR ColNr: INTEGER; Lp: TLp);
    {LpDecode decodes Lp into its component parts: RowNr and ColNr.}

    VAR
        Factor: INTEGER;

    {$S SegTEScrap }
    BEGIN {LpDecode}
        {$IFC SampCalls}   ALogCall;   {$ENDC}
        Factor := MaxCol - MinCol + 2;
        RowNr  := Lp DIV Factor + MinRow;
        ColNr  := Lp MOD Factor + MinCol;
    END; {LpDecode}


    PROCEDURE LpEncode(RowNr: INTEGER; ColNr: INTEGER; VAR Lp: TLp);
    {LpEncode encodes RowNr and ColNr into Lp.}

    VAR Factor: LONGINT; {changed from INTEGER so multiplication won't overflow}

    {$S SegTEScrap }
    BEGIN {LpEncode}
        {$IFC SampCalls}   ALogCall;   {$ENDC}
        Factor := MaxCol - MinCol + 2;
        Lp := (RowNr - MinRow) * Factor + (ColNr - MinCol);     {Encode LpLim.}
    END; {LpEncode}


    PROCEDURE FindEnd(HRow: THLine; VAR LastCol: INTEGER);
    {FindEnd sets LastCol to the last character in HRow that is not blank.}
    {LastCol:=0 if the line is all blank.}

    VAR TheChar:    TTrix;
        TheBlank:   TTrix;

    {$S SegTEScrap }
    BEGIN {FindEnd}
        {$IFC SampCalls}   ALogCall;   {$ENDC}
        TheBlank.Ch := BlankChar;

        LastCol := MaxCol + 1;
        WITH HRow^^ DO
            REPEAT
            LastCol := LastCol - 1;
            TheChar.Ch := TextLine[LastCol];
            UNTIL (LastCol <= MinCol) OR (TheChar.Int <> TheBlank.Int);
    END; {FindEnd}

{$S SegTEScrap }
BEGIN {MySeqLpd}
    {$IFC SampCalls}   ALogCall;   {$ENDC}
    {$R- }
    Scrap := Pointer(ORD(RgHCs^ [Lpd^.ICs]^));     {Where is it?}
    {$IFC RangeCheck }
        {$R+}
    {$ENDC }
    HCache := Scrap^.HCache;                        {Get the info from the scrap.}
    LastFull := Scrap^.LastFull;

    WITH Lpd^ DO
        BEGIN
        Rce := @ARce;                                 {Make the pointers go to given records.}
        Rpe := @ARpe;

        ARce := ARceStd;                              {Set the ARce.}
        WITH ARce DO
            IF Scrap^.Use132Cols THEN
                IFnt:=14                                {Tile 15.}
            ELSE
                IFnt:=2;                                {Tile 12.}

        ARpe := ARpeStd;                              {Set the ARpe.}
        WITH ARpe DO
            BEGIN
            xLftFst := 100;                             {First indent =  1 inch.}
            xLftBody := 100;                            {Body  indent =  1 inch.}
            xRt := 1000;                                {Right margin = 10 inches.}
            {Add tab ruler code here}
            END;
        END;

    Lpd^.Lp := Lpd^.LpLim;
    LpDecode(RowNr, ColNr, Lpd^.LpLim);

    IF AChad.RgCh = NIL THEN                        {Supply default scrap buffer.}
        WITH AChad DO
            BEGIN
            RgCh := @ScrapBuffer;
            IChFst := 0;
            IChLim := CMin(ScrBufSize, IChLim);
            END;

    IF RowNr <= HCache^^.LinesInCache THEN
        BEGIN
        ExpandRow(HCache, RowNr, HRow, TRUE);            {Get the first row to return.}
        FindEnd(HRow, LastCol);
        END
    ELSE
        BEGIN
        LastCol := MaxCol;
        HRow := NIL;
        END;

    IF ((RowNr = HCache^^.LinesInCache) AND (ColNr > LastCol)) OR
        (RowNr>HCache^^.LinesInCache) THEN
        AChad.IChLim := AChad.IChFst                        {Flag end of scrap.}
    ELSE
        IF ColNr>LastCol THEN
            BEGIN                                           {Vanished run.}
            Lpd^.ARce.fVan := TRUE;
            LpEncode(RowNr+1, MinCol, Lpd^.LpLim);
            END
        ELSE
            BEGIN
            FaceSet := FALSE;
            Scanning := TRUE;
            ICh := AChad.IChFst;
            WHILE Scanning DO
                BEGIN
                MyFaceOK := ((ColNr >= MinCol) AND (ColNr <= MaxCol));   {Get the current attributes.}
                IF MyFaceOK THEN
                    BEGIN
                    TheChar.Ch := HRow^^.TextLine[ColNr];
                    MyFace := TheChar.Ch2;

                    IF NOT FaceSet THEN
                        BEGIN               {Set the overall attributes.}
                        FaceSet := TRUE;
                        Face := MyFace;
                        END;
                    END;

                IF ICh >= AChad.IChLim THEN                 {To end of output buffer.}
                    Scanning := FALSE
                ELSE
                    IF FaceSet AND MyFaceOK AND (MyFace <> Face) AND Lpd^.TySet.fRce THEN
                        Scanning := FALSE                         {Attributes changed}
                    ELSE
                        IF ColNr > LastCol THEN
                            BEGIN          {To end of line.}
                            IF (RowNr < HCache^^.LinesInCache) OR
                               (RowNr = HCache^^.LinesInCache) AND LastFull THEN
                                BEGIN
                                {$R- }
                                AChad.RgCh^ [ICh] := ORD(CR);
                                {$IFC RangeCheck }
                                    {$R+}
                                {$ENDC }
                                ICh := ICh + 1;
                                END;

                            IF (RowNr >= HCache^^.LinesInCache) OR      {To end of scrap.}
                               Lpd^.TySet.fRpe OR                       {To end of paragraph.}
                               Lpd^.TySet.fParBnds THEN                 {To end of paragraph.}
                                Scanning := FALSE
                            ELSE
                                BEGIN
                                RowNr := RowNr + 1;                     {Move to the next row.}
                                ColNr := MinCol;

                                FreeRow(HRow);
                                ExpandRow(HCache, RowNr, HRow, TRUE);         {Get the next row to return.}
                                FindEnd(HRow, LastCol);
                                END;

                            END
                        ELSE
                            BEGIN
                            {Should we add a tab?}
                            IF CopyByColumns THEN
                                IF TheTerm^^.Display[AtPaint].MoveArray.ColumnStops[ColNr] THEN
                                    IF (ICh + 1) >= AChad.IChLim THEN           {End of output buffer?}
                                        Scanning := FALSE
                                    ELSE
                                        BEGIN                                   {Add a column tab}
                                        {$R- }
                                        AChad.RgCh^[ICh] := ORD(HT);
                                        {$IFC RangeCheck }
                                            {$R+}
                                        {$ENDC }
                                        ICh := ICh + 1;
                                        END;

                            IF Scanning THEN
                                BEGIN
                                {$R- }                  {Stuff the scrap char into the output buffer.}
                                AChad.RgCh^[ICh] := ORD(HRow^^.TextLine[ColNr].Ch);
                                {$IFC RangeCheck }
                                    {$R+}
                                {$ENDC }
                                ICh := ICh + 1;
                                ColNr := ColNr + 1;
                                END;
                            END;
                END;

            AChad.IChLim := ICh;

            WITH Lpd^ DO
                BEGIN
                LpEncode(RowNr, ColNr, LpLim);             {Set LpLim.}
                LpEncode(RowNr  , MinCol, lpFstPar);       {Set paragraph bounds ...}
                LpEncode(RowNr+1, MinCol, lpLimPar);       {... junk if not asked for.}

                WITH ARce DO
                    BEGIN                                   {Fill in the ARce.}
                    TheChar.Ch2 := Face;
                    fBold := TheChar.Ch.Bold;
                    fUnderline := TheChar.Ch.Underscore;
                    END;
                END;
            END;

    IF HRow <> NIL THEN
        FreeRow(HRow);
END; {MySeqLpd}


PROCEDURE CopyScrap;
  {CopyScrap puts TheTerm's selection into the Universal Text stream}
  {tied to the scrap.}

VAR
    Index:      TB;
    Scrap:      TPScrap;
    TheSel:     TRange;
    RowNr:      INTEGER;
    HRow:       THLine;
    NewRow:     THLine;
    ColNr:      INTEGER;
    DestNr:     INTEGER;
    eCode:      INTEGER;
    LastRow:    INTEGER;
    CFirst:     INTEGER;
    CLast:      INTEGER;
    TheCh:      TCharacter;
    OKToCopy:   BOOLEAN;
    Trix1:      TTrix;
    Trix2:      TTrix;


    PROCEDURE ValidCh(OldCh: TCharacter; VAR NewCh: TCharacter; VAR OKToCopy: BOOLEAN);
    {ValidCh takes OldCh, strips it nontransferable attributes, and sets OKToCopy}
    {true if it can be copied by another application.}

    {$S SegTEScrap }
    BEGIN {ValidCh}
        {$IFC SampCalls}   ALogCall;   {$ENDC}
        IF OldCh.Graphic THEN
            OKToCopy := FALSE
        ELSE
            BEGIN
            OKToCopy := TRUE;
            NewCh:=OldCh;
            NewCh.Blink := FALSE;
            NewCh.Negative := FALSE;
            END;
    END; {ValidCh}

{$S SegTEScrap }
BEGIN {CopyScrap}
    {$IFC SampCalls}   ALogCall;   {$ENDC}
    KillCache (TheScrap.HCache);                       {Kill old cache.}

    TheSel := TheTerm^^.Select;                          {Copy the selection.}

    TheScrap.Use132Cols := FALSE;

    TheScrap.LastFull := (TheSel.Right.Ch = MinCol);
    IF TheScrap.LastFull THEN
        LastRow := TheSel.Right.Ln - 1
    ELSE
        LastRow := TheSel.Right.Ln;

    NewHLine(NewRow);
    FOR RowNr := TheSel.Left.Ln TO LastRow DO
        BEGIN
        GetRow(TheTerm, AtPaint, RowNr, HRow);

        IF NOT HRow^^.TwoTop OR (RowNr = LastRow) THEN
            BEGIN
            IF RowNr = TheSel.Left.Ln THEN                   {Find front of ok stuff.}
                CFirst := TheSel.Left.Ch
            ELSE
                CFirst := MinCol;

            IF RowNr = TheSel.Right.Ln THEN                  {Find tail of ok stuff.}
                CLast := TheSel.Right.Ch - 1
            ELSE
                CLast := MaxCol;

            NewRow^^ := BlankLine;                           {Copy the info.}
            WITH HRow^^ DO
                BEGIN
                DestNr := MinCol;

                FOR ColNr := CFirst TO CLast DO
                    BEGIN
                    ValidCh (TextLine [ColNr], TheCh, OKToCopy); {Strip the attributes.}

                    IF OKToCopy THEN
                        BEGIN                                   {If it's OK then copy it.}
                        NewRow^^.TextLine [DestNr] := TheCh;

                        Trix1.Ch := TheCh;
                        Trix2.Ch := BlankChar;
                        IF (DestNr > 80) AND (Trix1.Int <> Trix2.Int) THEN
                        TheScrap.Use132Cols := TRUE;

                        DestNr := DestNr + 1;
                        END;
                    END;
                END;

            PutRow(TheScrap.HCache, NewRow);              {Put it in the cache.}
            END;

        FreeRow (HRow);

        IF (HeapState <> HeapNormal) AND (RowNr < LastRow) THEN
            BEGIN
            GiveAlert(TheTerm, AlrtStop, ALCopyStop, HeapDisk, '', '');

            DspHLine(NewRow);
            Exit(CopyScrap);
            END;
        END;
    DspHLine(NewRow);

    WITH TheScrap DO
        IF Use132Cols THEN
            Style := M132Style
        ELSE
            Style := M80Style;

    BindUTDSeg(eCode);

    InheritScrap(True);                                     {It's mine now.}

    Index := ICsCreate(TyCsUS, SizeOf (TScrap), TheHeap);   {Make the stream.}
    {$R- }
    Scrap:=Pointer(ORD(RgHCs^ [Index]^));                   {Where is it?}
    {$IFC RangeCheck }
        {$R+}
    {$ENDC }

    TheScrap.Cs := Scrap^.Cs;
    WITH TheScrap.Cs.CsPd DO
        BEGIN                                               {Set up the procs.}
        ArgProc [IProcSeqLpd] := @MySeqLpd;
        ArgProc [IProcFindLpFixed] := @FindLpFstPar;
        ArgProc [IProcFSelLpBounds] := @TrueStdSelLpBounds;
        END;
    Scrap^ := TheScrap;

    PutCsScrap(Index, eCode);
    IF eCode = 0 THEN
        AcceptInheritScrap
    ELSE
        BEGIN
        UndoInheritScrap(eCode);
        GiveAlert(TheTerm, AlrtStop, ALCopyStop, HeapDisk, '', '');
        END;

    FreeICs(Index);

    UnbindUTDSeg(eCode);

    PaintScrap;
END; {CopyScrap}

{$S SegTE }
ÿ