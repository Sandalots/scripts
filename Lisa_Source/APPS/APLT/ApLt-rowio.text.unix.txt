{ Copyright 1983, Apple Computer, Inc. }

{$S SegTERow }

procedure ClipCache (HCache: THCache);
  {ClipCache removes the excess buffer storage from the last page}
  {of HCache.  This makes HCache occupy the minimum ammount of space.}

  var
    HPage: THPage;
    TheSize: Integer;

  {$S SegTERow }
  begin {ClipCache}
    {$IFC SampCalls}   ALogCall;   {$ENDC}
    if HCache<>NIL then
      if HCache^^.LastPage<>NIL then begin
        with HCache^^ do begin
          HPage:=LastPage;

          if Odd (WritePtr) then
            TheSize:=WritePtr+1
          else
            TheSize:=WritePtr;
        end;

        HPage^^.PageSize:=TheSize;
        SizeHPage (HPage, TheSize);
      end;
  end; {ClipCache}


procedure KillCache (var TheCache: THCache);
  {KillCache kills the cache hanging off of TheCache.}

  var
    HPage: THPage;
    DeadPage: THPage;
    eCode: Integer;
    HCache: THCache;

  {$S SegTERow }
  begin {KillCache}
    {$IFC SampCalls}   ALogCall;   {$ENDC}
    HCache:=TheCache;
    TheCache:=NIL;

    if HCache<>NIL then begin
      HPage:=HCache^^.LastPage;
      while HPage<>NIL do begin
        DeadPage:=HPage;
        HPage:=HPage^^.Prev;
        DspHPage (DeadPage);
      end;

      DspHCache (HCache);
    end;
  end; {KillCache}


procedure ExpandRow (HCache: THCache; RowNr: Integer; var HRow: THLine; Scrap:Boolean);
  {ExpandRow returns a handle to the expanded form of row RowNr in HCache.}

  var
    IdxRowNr: Integer;
    ColNr: Integer;
    I: Integer;
    Idx: Integer;
    Ch: Char;
    TheCh: TTrix;
    Nr: TNrTrix;
    BlankInt: Integer;
    LastCol: Integer;
    HPage: THPage;
    BoolTrix: TBoolTrix;

  {$S SegTERow }
  begin {ExpandRow}
    {$IFC SampCalls}   ALogCall;   {$ENDC}
    NewHLine (HRow);

    If (RowNr = HCache^^.SeqLine) AND (Scrap =False) then begin   {not calling this for expanding scrap}
      HPage:= HCache^^.SeqPage;
      Idx:= HCache^^.SeqIdx;
    end else begin

      with HCache^^ do begin
        HPage:=LastPage;
        IdxRowNr:=LinesInCache - LastPage^^.LinesInPage + 1;
      end;

      while RowNr<IdxRowNr do begin               {Find the page with RowNr.}
        HPage:=HPage^^.Prev;
        IdxRowNr:=IdxRowNr - HPage^^.LinesInPage;
      end;

      with HRow^^, HPage^^ do begin
        Idx:=1;                                   {Find start of row in page.}
        while IdxRowNr<RowNr do begin
          Idx:=Idx + Ord (Page [Idx]);
          IdxRowNr:=IdxRowNr+1;
        end;
        Idx:=Idx+1;                               {Skip our LineLen.}
      end;
    end;

    with HRow^^, HPage^^, HCache^^ do begin

      Cached:=True;

      BoolTrix.Char0:=Page [Idx];               {Get the boolean flags.}
      Changed:=BoolTrix.Bool0;
      TwoHigh:=BoolTrix.Bool1;
      TwoWide:=BoolTrix.Bool2;
      TwoTop :=BoolTrix.Bool3;
      Repaint:=BoolTrix.Bool4;
      Idx:=Idx+1;

      Nr.Ch0:=Page [Idx  ];                     {Get the Sequence Number.}
      Nr.Ch1:=Page [Idx+1];
      Nr.Ch2:=Page [Idx+2];
      Nr.Ch3:=Page [Idx+3];
      Idx:=Idx+4;
      SeqNr:=Nr.Nr;

      TheCh.Ch:=BlankChar;                      {Get the line's contents.}
      BlankInt:=TheCh.Int;
      TextLine:=BlankLine.TextLine;
      ColNr:=MinCol;
      repeat
        Ch:=Page [Idx];
        Idx:=Idx+1;

        if Ch=CR then begin                     {End of line ... do blank fill.}
          TheCh.Ch1:=SP;
          if TheCh.Int<>BlankInt then
            for I:=ColNr to MaxCol do
              TextLine [I]:=TheCh.Ch;

        end else if Ch=DLE then begin           {Expand blanks.}
          LastCol:=ColNr + Ord (Page [Idx]);
          Idx:=Idx+1;
          TheCh.Ch1:=SP;
          if TheCh.Int<>BlankInt then
            for I:=ColNr to LastCol - 1 do
              TextLine [I]:=TheCh.Ch;
          ColNr:=LastCol;

        end else if Ch=ESC then begin           {Change attributes.}
          TheCh.Ch2:=Page [Idx];
          Idx:=Idx+1;

        end else begin                          {TheCh.Ch isn't special.}
          TheCh.Ch1:=Ch;
          TextLine [ColNr]:=TheCh.Ch;
          ColNr:=ColNr+1;
        end;
      until Ch=CR;

      If RowNr >= NrRowsCached then SeqLine:=0  {If we're at the end of the page}
      Else begin
        SeqLine:= RowNr+1;
        SeqPage:= HPage;
        SeqIdx:= Idx +1;
      end;

    end;
  end; {ExpandRow}



procedure GetRow (HTerm: THTerm; When: Moment; RowNr: Integer; var HRow: THLine);
  {GetRow returns a handle to a row described by HTerm, When, and RowNr.}

  {$S SegTE }
  begin {GetRow}
    {$IFC SampCalls}   ALogCall;   {$ENDC}
    with HTerm^^, Display [When], MoveArray do                                                  {[JS]}
      if RowNr>=StartScreen then                {Row is in screen.}
        HRow:=Screen [RowNr-StartScreen+1]

      else begin                                {Row is encoded into the cache.}
        ExpandRow (HCache, RowNr, HRow, FALSE);

        if RowNr<HTerm^^.Display [AtPaint].MoveArray.StartScreen then                           {[JS]}
          with HRow^^ do begin
            Changed:=False;
            Repaint:=False;
          end;
      end;
  end; {GetRow}


procedure FreeRow (HRow: THLine);
  {FreeRow releases the handle HRow after a call to GetRow.}

  {$S SegTE }
  begin {FreeRow}
    {$IFC SampCalls}   ALogCall;   {$ENDC}
    if HRow^^.Cached then
      DspHLine (HRow);
  end; {FreeRow}



procedure PutRow (var HCache: THCache; HRow: THLine);
  {PutRow caches HRow into HCache.  First, HRow is compacted into Buffer, then}
  {Buffer is tacked onto the last page of the cache.}

  const
    LineSize=512;

  var
    Buffer: packed array [1..LineSize] of Char;
    Idx: Integer;
    Nr: TNrTrix;
    BoolTrix: TBoolTrix;
    SpCount: Integer;
    ColNr: Integer;
    LastChar: TTrix;
    ThisChar: TTrix;
    Spaces: Boolean;
    HPage: THPage;
    TheSize: Integer;
    I: Integer;
    MakePage: Boolean;
    TheTitle: Str255;

  {$S SegTERow }
  begin {PutRow}
    {$IFC SampCalls}   ALogCall;   {$ENDC}
    Idx:=2;                                     {Reserve space for LineLen.}

    with HRow^^ do begin
      BoolTrix.Bool0:=Changed;                  {Put boolean flags.}
      BoolTrix.Bool1:=TwoHigh;
      BoolTrix.Bool2:=TwoWide;
      BoolTrix.Bool3:=TwoTop;
      BoolTrix.Bool4:=Repaint;
      Buffer [Idx]:=BoolTrix.Char0;
      Idx:=Idx+1;

      Nr.Nr:=SeqNr;                             {Put Sequence Number.}
      Buffer [Idx  ]:=Nr.Ch0;
      Buffer [Idx+1]:=Nr.Ch1;
      Buffer [Idx+2]:=Nr.Ch2;
      Buffer [Idx+3]:=Nr.Ch3;
      Idx:=Idx+4;

      LastChar.Ch:=BlankChar;
      Spaces:=False;
      for ColNr:=MinCol to MaxCol do begin
        ThisChar.Ch:=TextLine [ColNr];

        if ThisChar.Ch2<>LastChar.Ch2 then begin        {Change attributes.}
          if Spaces then begin                  {Terminate blank compression.}
            Spaces:=False;
            if SpCount>2 then begin
              Buffer [Idx  ]:=DLE;
              Buffer [Idx+1]:=Chr (SpCount);
              Idx:=Idx+2;
            end else
              for I:=1 to SpCount do begin
                Buffer [Idx]:=SP;
                Idx:=Idx+1;
              end;
          end;

          Buffer [Idx  ]:=ESC;
          Buffer [Idx+1]:=ThisChar.Ch2;
          Idx:=Idx+2;
        end;

        if ThisChar.Ch1=SP then                 {Compress blanks.}
          if Spaces then
            SpCount:=SpCount+1
          else begin
            Spaces:=True;
            SpCount:=1;
          end

        else begin                              {Cache this character.}
          if Spaces then begin                  {Terminate blank compression.}
            Spaces:=False;
            if SpCount>2 then begin
              Buffer [Idx  ]:=DLE;
              Buffer [Idx+1]:=Chr (SpCount);
              Idx:=Idx+2;
            end else
              for I:=1 to SpCount do begin
                Buffer [Idx]:=SP;
                Idx:=Idx+1;
              end;
          end;

          Buffer [Idx]:=ThisChar.Ch1;
          Idx:=Idx+1;
        end;

        LastChar.Ch:=ThisChar.Ch;
      end;

      Buffer [Idx]:=CR;                         {Terminate the line.}
      Buffer [1]:=Chr (Idx);                    {Patch the LineLen.}
    end;

    if HCache=NIL then begin                    {Do we need to make a new page?}
      MakePage:=True;
      NewHCache (HCache);
      with HCache^^ do begin
        LastPage:=NIL;

        NrRowsCached:=NrRowsInit;

        NrPages:=0;
        LinesInCache:=0;
      end;

    end else
      with HCache^^ do
        if LastPage=NIL then
          MakePage:=True
        else
          with LastPage^^ do
            MakePage:= (LinesInPage >= NrRowsCached) or
                       (PageSize+PageBump > 32000  ) or
                       (WritePtr+Idx      > 32000  );

    if MakePage then begin                      {Make the new page.}
      ClipCache (HCache);                       {Trim unused space in old page.}

      NewHPage (HPage);                         {Make it.}
      with HPage^^ do begin
        Prev:=HCache^^.LastPage;
        LinesInPage:=0;
        PageSize:=PageInit;
      end;

      with HCache^^ do begin
        LastPage:=HPage;
        WritePtr:=1;
        NrPages:=NrPages+1;
        if NrPages>NrRowsCached then            {Try to keep LinesInPage near NrPages.}
          NrRowsCached:=NrRowsCached + NrRowsBump;
      end;
    end;

    with HCache^^, LastPage^^ do                {Make sure we have enough room for row.}
      if WritePtr+Idx-1>PageSize then begin
        PageSize:=PageSize + PageBump;
        TheSize:=PageSize;
        HPage:=LastPage;

      end else
        HPage:=NIL;

    if HPage<>NIL then
      SizeHPage (HPage, TheSize);

    with HCache^^, LastPage^^ do begin          {Put the row in the page.}
      for I:=1 to Idx do begin
        Page [WritePtr]:=Buffer [I];
        WritePtr:=WritePtr+1;
      end;

      LinesInPage :=LinesInPage  + 1;
      LinesInCache:=LinesInCache + 1;
    end;
  end; {PutRow}

{$S SegTE }
ÿ