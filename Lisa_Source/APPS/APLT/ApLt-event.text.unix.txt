 { Copyright 1983, Apple Computer, Inc. }

{$S SegTEEvent }

procedure KillLOT (HTerm: THTerm);
  {KillLOT kills the Lines Off Top of of HTerm.  This involves killing}
  {the cache and then adjusting all line indices to the new origin.}

  {$S SegTEEvent }
  begin {KillLOT}
    {$IFC SampCalls}   ALogCall;   {$ENDC}
    KillCache (HTerm^^.HCache);                   {Kill the info.}

    with HTerm^^, Display [RightNow], MoveArray do begin {Adjust the line indices.}                 {[JS]}
      CantRemember := False;
      FirstRow:=FirstRow - StartScreen + MinRow;  {Adjust first row}
      if FirstRow<MinRow then
        FirstRow:=MinRow;

      Cursor.Ln:=Cursor.Ln - StartScreen + MinRow;{Adjust cursor line}
      if Cursor.Ln<MinRow then
        Cursor.Ln:=MinRow;

      Select.Left.Ln:=Select.Left.Ln - StartScreen + MinRow;  {Clip selection top}
      if Select.Left.Ln<MinRow then begin
        Select.Left.Ln:=MinRow;
        Select.Left.Ch:=MinCol;
      end;

      Select.Right.Ln:=Select.Right.Ln - StartScreen + MinRow; {Clip selection bottom}
      if Select.Right.Ln<MinRow then begin
        Select.Right.Ln:=MinRow;
        Select.Right.Ch:=MinCol;
      end;

      StartScreen:=MinRow;
    end;

    SizeTerm (HTerm);                                    {Repaint it}
    PaintTerm (HTerm);
  end; {KillLOT}


procedure ProcessEvent (TheEvent: EventRecord);
  {ProcessEvent takes the appropriate action when an event occurs.}

  var
    SavePort: GrafPtr;
    TheMenu: Integer;
    TheItem: Integer;
    TheZone: TZone;
    TheParms: FilerExt;
    eCode: Integer;
    TheTitle: Str255;
    I: Integer;
    HTerm: THTerm;


    PROCEDURE AdornMenus;
    {$S SegTEEvent }
    BEGIN{AdornMenus}
        {$IFC SampCalls}   ALogCall;   {$ENDC}
        CheckItem(TheMenus[RulerMenu], ShowColumnRuler, theTerm^^.Setup.SeeColumns);
        CheckItem(TheMenus[RulerMenu], ShowTabRuler, theTerm^^.Setup.SeeTabs);

        IF theTerm^^.Setup.SeeColumns OR theTerm^^.Setup.SeeTabs THEN
            EnableItem(TheMenus[RulerMenu], HideRulers)
        ELSE
            DisableItem(TheMenus[RulerMenu], HideRulers);
    END;{AdornMenus}


    PROCEDURE DoGoAway(theTerm: THTerm);                                        {BEGIN DoGoAway [JS]}
    {Makes the ruler go away}
    {$S SegTEEvent }
    BEGIN{DoGoAway}
        {$IFC SampCalls}   ALogCall;   {$ENDC}
        theTerm^^.Setup.SeeTabs := FALSE;
        theTerm^^.Setup.SeeColumns := FALSE;

        SizeTerm(theTerm);
        PaintTerm(theTerm);
    END;{DoGoAway}                                                              {END DoGoAway [JS]}


    PROCEDURE DoSet(theTerm: THTerm);                                           {BEGIN DoSet [JS]}
    {Set tab or column marker}
    {$S SegTEEvent }
    BEGIN{DoSet}
        {$IFC SampCalls}   ALogCall;   {$ENDC}
        WITH theTerm^^, Setup, Display[RightNow].MoveArray DO
            IF SeeTabs THEN
                TabStops[position] := TRUE
            ELSE
            IF SeeColumns THEN
                ColumnStops[position] := TRUE;

        UpdateTerm(theTerm);
    END;{DoSet}                                                                 {END DoSet [JS]}


    PROCEDURE DoClr(theTerm: THTerm);                                           {BEGIN DoClr [JS]}
    {Clears tab or column marker}
    {$S SegTEEvent }
    BEGIN{DoClr}
        {$IFC SampCalls}   ALogCall;   {$ENDC}
        WITH theTerm^^, Setup, Display[RightNow].MoveArray DO
            IF SeeTabs THEN
                TabStops[position] := FALSE
            ELSE
            IF SeeColumns THEN
                ColumnStops[position] := FALSE;

        UpdateTerm(theTerm);
    END;{DoClr}
                                                                  {END DoClr [JS]}
  procedure InstallMenus;
    {InstallMenus inserts all of the visible menus and paints the menu bar.}

    var
      WhichMenu: Integer;

    {$S SegTEEvent }
    begin {InstallMenus}
      {$IFC SampCalls}   ALogCall;   {$ENDC}
      for WhichMenu:=FirstMenu to LastVisMenu do
        InsertMenu (TheMenus [WhichMenu], NullMenu);

      if TheTerm^^.Setup.Modem<>DBOther then
        InsertMenu (TheMenus [WhichMenu], NullMenu);

      DrawMenuBar;
    end; {InstallMenus}

  procedure MenuCommand (TheMenu: Integer; TheItem: Integer);
    {MenuCommand processes a menu hit.}

    var
      ColNr: Integer;
      RowNr: Integer;
      Why: FReason;
      eCode: Integer;
      WasSaved: Boolean;
      ChangePort: Boolean;
      ChangeModem: Boolean;
      WasModem: TDBModem;
      ChangeCols: Boolean;
      FilePath: PathName;
      Fileinfo: Fs_info;
      DateTime: TParamAlert;

      {$IFC WrapDebug}                                                                          {[JS]}
      chWrap: CHAR;                                                                             {[JS]}
      {$ENDC}                                                                                   {[JS]}                                                                  {[JS]}

    {$S SegTEEvent }
    begin {MenuCommand}
      {$IFC SampCalls}   ALogCall;   {$ENDC}
      case TheMenu of
        FilPrt:
          case TheItem of
            FPClosAll:
              DoFilingCmd (cmdClosAll);

            FPBlank1:;

            FPClose:
              DoFilingCmd (cmdClose);

            FPPutBack: begin
              GiveAlert(TheTerm,AlrtWait,AlSave,TheTerm^^.TheTitle,'','');
              if PutDoc (TheTerm, TheTerm^^.FileName, True, Why) then begin
                DoneAlert;
                TellFiler (eCode, DocClosd, DocPutBack, TheTerm^^.Folder);
                if eCode>0 then Quit;

                KillFolder (TheTerm);

              end else begin
                DoneAlert;
                TellFiler (eCode, DocNotClosed, Why, TheTerm^^.Folder);
                if eCode>0 then Quit;
              end;
            end;

            FPSave:
              if IsActive then
                if not PutDoc (TheTerm, TheTerm^^.FileName, True, Why) then
                  GiveAlert (TheTerm, AlrtStop, ALDocWrite, TheTerm^^.TheTitle, '', '');

            FPRevert:
              if IsActive then begin
                 if TheTerm^^.ComeFromBlank then begin
                   ParamAlert(TheTerm^^.TheTitle,' ',' ');
                   if CautionAlert(MyAlerts,AlRevrtNew) then
                   RevertFolder(TheTerm);
                 end else begin
                   FilePath:=TheTerm^^.FileName;
                   Lookup(eCode,FilePath,Fileinfo);  {Get file attributes}
                   DTAlert(MyAlerts,Fileinfo.DTM,DateTime); {Get date in usable form}
                   ParamAlert(TheTerm^^.TheTitle,DateTime,' ');
                   if CautionAlert(MyAlerts,AlRevrtOld) then
                   RevertFolder(TheTerm);
                 end;
                 with TheTerm^^.Setup do
                 if ADNumber<>'' then
                     TheTitle:=Concat (BaseDialDial, ' "', ADNumber, '"')
                 else
                     TheTitle:=Concat (BaseDialDial, '...');
                 SetItem (TheMenus [DialMenu], DialDial, @TheTitle);
                 InstallMenus;
                 DoPhoneMenu (TheTerm);

               end;


            FPBlank2:;

            FPRoute: {Printing not yet implemented};
            FPPrint: {Printing not yet implemented};
            FPIntend: PrBgdDlg;
          end;

        EditMenu:
          if IsActive then
            case TheItem of
              EditUndo: {Undo not yet implemented};
              EditCut: {Cut is always disabled.};

              EditPaste: begin
                {$IFC WrapDebug}                                                {BEGIN debug flag [JS]}
                WrapDebug := DbgYesNo('Debug word wrap');
                {$ENDC}                                                         {END debug flag [JS]}

                {$IFC DbgCursor}                                                {BEGIN debug flag [JS]}
                IF DbgCursor THEN
                    WRITELN('Paste start: HourGlass = ',HourGlass);
                {$ENDC}                                                         {END debug flag [JS]}

                HourGlass := TRUE;                                                              {[JS]}

                {$IFC DbgCursor}                                                {BEGIN debug flag [JS]}
                IF DbgCursor THEN
                    WRITELN('            HourGlass = ',HourGlass);
                {$ENDC}                                                         {END debug flag [JS]}

                SetStdCursor(iCrsHrGlass);          {Force cursor to hourglass during paste}    {[JS]}
                StartPaste:=True;
                PasteTerm:=TheTerm;
              end;

              EditCopy, EditCopColumns:                                                         {[JS]}
                  WITH TheTerm^^.Select DO                                                      {[JS]}
                      IF IsInsertion (TheTerm) THEN                                             {[JS]}
                          GiveAlert (TheTerm, AlrtStop, ALNilCopy, '', '', '')                  {[JS]}
                      ELSE                                                                      {[JS]}
                          BEGIN                                                                 {[JS]}
                          CopyByColumns := TheItem = EditCopColumns;                            {[JS]}

                          {$IFC DbgCursor}                                     {BEGIN debug flag [JS]}
                          IF DbgCursor THEN
                              WRITELN('Copy Start: HourGlass = ',HourGlass);
                          {$ENDC}                                                {END debug flag [JS]}

                          HourGlass := TRUE;                                                    {[JS]}

                          {$IFC DbgCursor}                                     {BEGIN debug flag [JS]}
                          IF DbgCursor THEN
                              WRITELN('            HourGlass = ',HourGlass);
                          {$ENDC}                                                {END debug flag [JS]}

                          SetStdCursor(iCrsHrGlass);    {Force cursor to hourglass during copy} {[JS]}
                          CopyScrap;                                                            {[JS]}

                          {$IFC DbgCursor}                                     {BEGIN debug flag [JS]}
                          IF DbgCursor THEN
                              WRITELN('Copy End: HourGlass = ',HourGlass);
                          {$ENDC}                                                {END debug flag [JS]}

                          HourGlass := FALSE;

                          {$IFC DbgCursor}                                     {BEGIN debug flag [JS]}
                          IF DbgCursor THEN
                              WRITELN('          HourGlass = ',HourGlass);
                          {$ENDC}                                                {END debug flag [JS]}

                          END;                                                                  {[JS]}

              EditAll: begin
                with TheTerm^^, Select do begin
                  Left.Ln:=MinRow;
                  Left.Ch:=MinCol;

                  Right.Ln:=Display [RightNow].MoveArray.StartScreen+MaxRow-MinRow+1;               {[JS]}
                  Right.Ch:=MinCol;
                end;

                UpdateTerm (TheTerm);
              end;

              EditErase: KillLOT (TheTerm);
            end;

        SetupMenu:
          if IsActive then
            case TheItem of
              SetupCompat, SetupCmfort, SetupCustom, SetCharSets:
                  BeginSetup (TheItem);
              SetupAnswrBk:
                BeginAnswrBk;
            end;
        RulerMenu:
          case TheItem of
            ShowTabRuler:                                                   {BEGIN Ruler display [JS]}
                BEGIN
                TheTerm^^.Setup.SeeTabs := TRUE;
                TheTerm^^.Setup.SeeColumns := FALSE;

                SizeTerm(TheTerm);
                PaintTerm(TheTerm);
                END;

            ShowColumnRuler:
                BEGIN
                TheTerm^^.Setup.SeeColumns := TRUE;
                TheTerm^^.Setup.SeeTabs := FALSE;

                SizeTerm(TheTerm);
                PaintTerm(TheTerm);
                END;

            HideRulers:
                DoGoAway(TheTerm);
                                                                            {END Ruler display [JS]}
            SetTabEvery8: begin
                with TheTerm^^.Display [RightNow], MoveArray do begin                               {[JS]}
                  TabStops [MinCol]:=False;

                  for ColNr:=MinCol+1 to MaxCol do
                    TabStops [ColNr]:= (ColNr mod 8 = 1);
                end;
                UpdateTerm (TheTerm);

            end;
            ClearTabs: begin
                with TheTerm^^.Display [RightNow], MoveArray do                                     {[JS]}
                  for ColNr:=MinCol to MaxCol do
                    TabStops [ColNr]:=False;
                UpdateTerm (TheTerm);

            end;
            ClearColumns:                                                       {BEGIN Clear columns [JS]}
                BEGIN
                WITH TheTerm^^.Display[RightNow], MoveArray DO
                    FOR ColNr := MinCol TO MaxCol DO
                        ColumnStops[ColNr] := FALSE;
                UpdateTerm(TheTerm);
                END;                                                            {END Clear columns [JS]}
          end;

        DialMenu:
          case TheItem of
            DialDial: begin
              if TheTerm^^.Setup.ADNumber='' then
                BeginPhone (True)
              else
                DialPhone (TheTerm);
            end;

            DialChange:
              BeginPhone (False);

            DialHangUp:
              if TheTerm^^.OffHook then
                HangUp (TheTerm);
          end;

        ScrapFile:
          case TheItem of
            ScrClosAll:
              DoFilingCmd (cmdClosAll);

            ScrClose:
              DoFilingCmd (cmdClose);
          end;
      end;
    end; {MenuCommand}


  procedure FindHit (HWhere: Point; var Zone: TZone);
    {FindHit sets Zone to the first FolderZone of TheTerm containing HWhere.}

    var
      Looking: Boolean;

    {$S SegTEEvent }
    begin {FindHit}
      {$IFC SampCalls}   ALogCall;   {$ENDC}
      Looking:=True;
      Zone:=TextBox;
      while (Zone<>Elsewhere) and Looking do
        if PtInRect (HWhere, TheTerm^^.Display [AtPaint].MoveArray.FolderZones [Zone]) then         {[JS]}
          Looking:=False
        else
          Zone:=Succ (Zone);
    end; {FindHit}


  procedure FolderHit (HWhere: Point; Zone: TZone);
    {FolderHit processes a click in Zone of TheTerm.}

    var
      SavePort: GrafPtr;


    PROCEDURE DragPositionMarker(HWhere: Point);                            {BEGIN DragPositionMarker [JS]}
      {DragPositionMarker handles hits in the tab ruler.}

    VAR
        Pos:                INTEGER;
        WasAt:              INTEGER;
        lBTextBox:          Rect;
        wasButton:          TButton;
        button:             TButton;
        holdingWrapMark:    BOOLEAN;
        holdingTabColMark:  BOOLEAN;
        oldTabColMark:      BOOLEAN;


        FUNCTION  ButtonHit(where: Point): TButton;

        {$S SegTEEvent }
        BEGIN
            {$IFC SampCalls}   ALogCall;   {$ENDC}
            WITH TheTerm^^.Display [AtPaint].MoveArray DO
                IF PtInRect(where, FolderZones[GoAway]) THEN
                    ButtonHit := GoAwayButton
                ELSE
                IF PtInRect(where, FolderZones[SetTabOrColumn]) THEN
                    ButtonHit := SetButton
                ELSE
                IF PtInRect(where, FolderZones[ClrTabOrColumn]) THEN
                    ButtonHit := ClrButton
                ELSE
                    ButtonHit := NoButton;
        END;


        FUNCTION  FindPos(H: Integer): INTEGER;
        {FindPos returns the tab number corresponding to H, the horizontal}
        {position of the mouse in TheTerm's port.}

        VAR Pos:  INTEGER;

        {$S SegTEEvent }
        BEGIN {FindPos}
            {$IFC SampCalls}   ALogCall;   {$ENDC}
            WITH TheTerm^^, Style^^, Display [AtPaint].MoveArray DO
                BEGIN
                Pos := (H - FolderZones[RulerBox].Left + (HorzSp DIV 2)) DIV HorzSp + FirstCol;
                Pos := CMax(Pos, FirstCol);
                Pos := CMin(Pos, FirstCol + NrCols - 1);
                END;
            FindPos := Pos;
        END; {FindPos}


        {$IFC Ouch}
        {$S SegTEEvent }
        PROCEDURE Ouch;
        CONST
            ouchText = 'Ouch!';

        VAR currentPort:    GrafPtr;
            oldMode:        INTEGER;
            oldFont:        INTEGER;
            oldFace:        Style;
            oldPen:         PenState;
            setBtnText:     Str40;
            clrBtnText:     Str40;

        {$S SegTEEvent }
        BEGIN {Ouch}
            {$IFC SampCalls}   ALogCall;   {$ENDC}
            GetPort(currentPort);
            oldFont := currentPort^.txFont;
            oldFace := currentPort^.txFace;
            oldMode := currentPort^.txMode;
            GetPenState(oldPen);

            PenNormal;
            PenSize(BtnBWidth, BtnBHeight);
            TextMode(srcBic);
            TextFont(sysText);
            TextFace([]);

            WITH TheTerm^^, Setup, Display [RightNow].MoveArray DO
                BEGIN
                FillRoundRect(FolderZone[GoAway], BtnOvalWidth, BtnOvalHeight, black);
                MoveTo(FolderZone[GoAway].left + BtnBWidth + BtnOvalWidth DIV 2 +
                       StringWidth(mGoAway) DIV 2 - StringWidth(ouchText) DIV 2,
                       FolderZone[GoAway].bottom - BtnBHeight - VMargin - sysDescent);
                DrawString(ouchText);
                END;

            TextFont(oldFont);
            TextFace(oldFace);
            TextMode(oldMode);
            SetPenState(oldPen);
        END; {Ouch}
        {$ENDC}

    {$S SegTEEvent }
    BEGIN {DragPositionMarker}
        {$IFC SampCalls}   ALogCall;   {$ENDC}
        button := ButtonHit(HWhere);
        TheTerm^^.Display[RightNow].MoveArray.HitButton := button;
        wasButton := button;
        pos := TheTerm^^.Display[RightNow].MoveArray.Position;

        oldTabColMark := FALSE;
        holdingTabColMark := FALSE;
        holdingWrapMark := FALSE;

        IF button = NoButton THEN
            BEGIN
            SetStdCursor(ICrsGECross);

            pos := FindPos(HWhere.H);
            WITH TheTerm^^.Display [RightNow].MoveArray DO
                BEGIN
                ShowGrayLine := TRUE;
                Position := pos;                  {Show it.}

                holdingWrapMark := VisWrap AND
                                   (HWhere.v >= blWrap) AND
                                   (HWhere.v <= blWrap + RectHeight(WrapMark.bounds)) AND
                                   (pos = VisWrapPoint);

                IF VisTabs THEN
                    holdingTabColMark := (HWhere.v >= blTStop) AND
                                         (HWhere.v <= blTStop + RectHeight(TabMark.bounds)) AND
                                         TabStops[pos]
                ELSE
                    holdingTabColMark := (HWhere.v >= blTStop) AND
                                         (HWhere.v <= blTStop + RectHeight(ColumnMark.bounds)) AND
                                         ColumnStops[pos];
                END;
            END
        ELSE
            SetStdCursor(ICrsCheck);

        UpdateTerm(TheTerm);

        wasAt := pos;

        WHILE StillDown DO
            BEGIN
            REPEAT
            GetMouse(HWhere);

            lBTextBox := TheTerm^^.Display[AtPaint].MoveArray.FolderZones[BRulerBox];
            IF HWhere.H < lBTextBox.Left THEN                                   {scroll left}
                WITH TheTerm^^.Display[RightNow].MoveArray DO
                    BEGIN
                    IF FirstCol > MinCol THEN
                        BEGIN
                        FirstCol := FirstCol - 1;
                        SizeTerm(TheTerm);
                        UpdateTerm(TheTerm);
                        END;
                    END
            ELSE
                IF HWhere.H > lBTextBox.Right THEN                              {scroll right}
                    WITH TheTerm^^.Display [RightNow].MoveArray DO
                        BEGIN
                        IF FirstCol + NrCols - 1 < MaxCol THEN
                            BEGIN
                            FirstCol := FirstCol + 1;
                            SizeTerm(TheTerm);
                            UpdateTerm(TheTerm);
                            END;
                        END;

            button := ButtonHit(HWhere);

            TheTerm^^.Display [RightNow].MoveArray.ShowGrayLine := (button = NoButton);
            IF button = NoButton THEN
                BEGIN
                SetStdCursor(ICrsGECross);
                Pos := FindPos(HWhere.H);

                WITH TheTerm^^,Display [RightNow].MoveArray DO
                    BEGIN
                    IF holdingWrapMark THEN
                        BEGIN
                        VisWrapPoint := pos;
                        SetUp.WrapPoint := pos;
                        END;
                    IF holdingTabColMark THEN
                        BEGIN
                        IF VisTabs THEN
                            BEGIN
                            TabStops[WasAt] := oldTabColMark;
                            oldTabColMark := TabStops[pos];
                            TabStops[pos] := TRUE;
                            END
                        ELSE
                            BEGIN
                            ColumnStops[WasAt] := oldTabColMark;
                            oldTabColMark := ColumnStops[pos];
                            ColumnStops[pos] := TRUE;
                            END;
                        END;
                    END;
                END
            ELSE
                SetStdCursor(ICrsCheck);

            IF (Pos = WasAt) AND (button = wasButton) THEN
                LetOthersRun;

            UNTIL (Pos <> WasAt) OR (button <> wasButton) OR NOT StillDown;

            WITH TheTerm^^.Display[RightNow].MoveArray DO
                BEGIN
                Position  := Pos;
                WasAt     := Pos;
                HitButton := button;
                wasButton := button;
                END;

            UpdateTerm(TheTerm);
            END;

        WITH TheTerm^^, Display [RightNow].MoveArray DO
            BEGIN
            ShowGrayLine := FALSE;              {Don't show the gray line}
            HitButton := NoButton;              {Clear button at next update}
            END;

        CASE button OF
            GoAwayButton:
                BEGIN
                {$IFC Ouch}
                Ouch;
                {$ENDC}
                DoGoAway(TheTerm);
                END;
            SetButton:    DoSet(TheTerm);
            ClrButton:    DoClr(TheTerm);
            OTHERWISE
                IF TheTerm^^.Display [AtPaint].MoveArray.ShowGrayLine THEN
                    UpdateTerm(TheTerm);
        END;{CASE}

    END; {DragPositionMarker}                                               {END DragPositionMarker [JS]}


    procedure SBHit (HWhere: Point);
      {SBHit processes a scroll bar hit in TheTerm.}

      var
        TheSB: THSb;
        TheIcon: TIcon;
        ScrollBy: Integer;
        LoLimit: Integer;
        HiLimit: Integer;
        IsAt: Integer;
        ThumbPos: Integer;


      procedure DoScroll (TheSB: THSb; TheIcon: TIcon; ScrollBy: Integer;
                          LoLimit: Integer; HiLimit: Integer; IsAt: Integer);
        {DoScroll scrolls the folder in the orientation of TheSB.  The}
        {position is kept within LoLimit..HiLimit, and is changed by ScrollBy.}

        var
          ThumbPos: Integer;

        {$S SegTEEvent }
        begin {DoScroll}
          {$IFC SampCalls}   ALogCall;   {$ENDC}
          PaintArw (TheSB, TheIcon, True);      {Darken the arrow or page.}

          while StillDown do begin
            IsAt:=IsAt+ScrollBy;                {Scroll within limits}
            if IsAt<LoLimit then begin
              IsAt:=LoLimit;
              LetOthersRun;
            end;

            if IsAt>HiLimit then begin
              IsAt:=HiLimit;
              LetOthersRun;
            end;

            with TheTerm^^.Display [RightNow], MoveArray do     {Do the scroll.}                {[JS]}
              if TyVHOfSB (TheSB)=TyVHV then
                FirstRow:=IsAt
              else
                FirstCol:=IsAt;
            SizeTerm (TheTerm);
            UpdateTerm (TheTerm);
          end;

          PaintArw (TheSB, TheIcon, False);          {Undarken the icon}
        end; {DoScroll}

      {$S SegTEEvent }
      begin {SBHit}
        {$IFC SampCalls}   ALogCall;   {$ENDC}
        if FSbHit (TheTerm^^.SBList, HWhere, TheSB, TheIcon) then
          if TheIcon in [IconArwA, IconArwB, IconPagA, IconPagB, IconThumb] then begin
            if TyVHOfSB (TheSB)=TyVHV then begin     {Vertical scroll bar}
              with TheTerm^^.Display [RightNow], MoveArray do begin                                 {[JS]}
                IsAt:=FirstRow;
                LoLimit:=MinRow;
                HiLimit:=StartScreen + MaxRow - MinRow - NrRows + 1;

                if TheIcon in [IconArwA, IconArwB] then    {line at a time}
                  ScrollBy:=1
                else                                       {page at a time}
                  ScrollBy:=NrRows;

                if TheIcon in [IconArwA, IconPagA] then
                  ScrollBy:=-ScrollBy;
              end;

            end else begin                                 {horizontal scroll bar}
              with TheTerm^^.Display [RightNow], MoveArray do begin                                 {[JS]}
                IsAt:=FirstCol;
                LoLimit:=MinCol;
                HiLimit:=MaxCol - NrCols + 1;

                if TheIcon in [IconArwA, IconArwB] then    {line at a time}
                  ScrollBy:=1
                else                                       {page at a time}
                  ScrollBy:=NrCols;

                if TheIcon in [IconArwA, IconPagA] then    {scroll left}
                  ScrollBy:=-ScrollBy;
              end;
            end;

            if TheIcon=IconThumb then begin                {drag the thumb}
              DragThumb (TheSB, HWhere, ThumbPos);

              IsAt:=(ThumbPos * Ord4((HiLimit-LoLimit))) div 1000 + LoLimit;
              with TheTerm^^.Display [RightNow], MoveArray do         {scroll it}                   {[JS]}
                if TyVHOfSB (TheSB)=TyVHV then
                  FirstRow:=IsAt
                else
                  FirstCol:=IsAt;
              SizeTerm (TheTerm);
              UpdateTerm (TheTerm);

            end else                                        {arrow/page hit}
              DoScroll (TheSB, TheIcon, ScrollBy, LoLimit, HiLimit, IsAt);
          end;
      end; {SBHit}


    procedure GBHit (HWhere: Point);
      {GBHit processes a grow box hit.}

      var
        SavePort: GrafPtr;
        NewBotRt: Point;

      {$S SegTEEvent }
      begin {GBHit}
        {$IFC SampCalls}   ALogCall;   {$ENDC}
        GetPort (SavePort);
        SetPort (TheTerm^^.Folder);
        DragFrame (HWhere, True, NewBotRt);
        SubPt (TheTerm^^.Folder^.PortRect.TopLeft, NewBotRt);
        FolderSize (TheTerm^^.Folder, NewBotRt.H, NewBotRt.V, False);
        ClipRect (TheTerm^^.Folder^.PortRect);
        SetPort (SavePort);

        KillSBs (TheTerm);
        SizeTerm (TheTerm);
        UpdateTerm (TheTerm);
        PaintSBs (TheTerm);
      end; {GBHit}

    {$S SegTEEvent }
    begin {FolderHit}
      {$IFC SampCalls}   ALogCall;   {$ENDC}
      GetPort (SavePort);
      SetPort (TheTerm^^.Folder);

      case Zone of
        TextBox, BTextBox:
          if TheEvent.ShiftKey then
            ExtndSelect (DownWhere)
          else
            StartSelect (DownWhere);

        RulerBox, BRulerBox, GoAway, SetTabOrColumn, ClrTabOrColumn:                        {[JS]}
          DragPositionMarker(HWhere);

        VScrollBox, HScrollBox:
          SBHit (HWhere);

        GrowBox:
          GBHit (HWhere);

        VGreyBox, HGreyBox, StatBox, Elsewhere: {Do nothing.};
      end;

      SetPort (SavePort);
    end; {FolderHit}


  procedure GrowScrap (HWhere: Point);
    {GrowScrap handles button-downs in the scrap's grow box.}

    var
      SavePort: GrafPtr;
      NewBotRt: Point;

    {$S SegTEEvent }
    begin {GrowScrap}
      {$IFC SampCalls}   ALogCall;   {$ENDC}
      GetPort (SavePort);
      SetPort (ScrapFolder);
      DragFrame (HWhere, True, NewBotRt);
      SubPt (ScrapFolder^.PortRect.TopLeft, NewBotRt);
      FolderSize (ScrapFolder, NewBotRt.H, NewBotRt.V, False);
      ClipRect (ScrapFolder^.PortRect);
      SetPort (SavePort);

      PaintScrap;
    end; {GrowScrap}

  procedure ActivateFolder;

    var
      SavePort: Grafptr;

    {$S SegTEEvent }
    begin {ActivateFolder}
      {$IFC SampCalls}   ALogCall;   {$ENDC}
      if TheEvent.Who=ScrapFolder then begin
        TheTerm:=ScrapTerm;
        GetPort (SavePort);
        SetPort (ScrapFolder);
        PaintSB (TheScrap.VSBar);
        PaintSB (TheScrap.HSBar);
        PaintGrow;
        SetPort (SavePort);

        GetFldrTitle (ScrapFolder, TheTitle);
        TheTitle:=Concat (BaseScrClose, ' "', TheTitle, '"');
        SetItem (TheMenus [ScrapFile], ScrClose, @TheTitle);

        InsertMenu (TheMenus [ScrapFile], NullMenu);
        DrawMenuBar;

      end else if TheEvent.Who<>FilerFolder then begin
        WMSpare1:=1;                        {Turn on Apple combo repeat.}

        TheTerm:=Pointer (GetFldrRefCon (TheEvent.Who));

        if TheTerm^^.ReadMe then begin      {need to make the folder!!}
          with TheTerm^^ do begin
            Next:=TermList;
            Prev:=NIL;
          end;
          if TermList<>NIL then
            TermList^^.Prev:=TheTerm;
            TermList:=TheTerm;

            MakeFolder (TheTerm);
            TheTerm^^.ReadMe:=False;

          end else begin                    {has the name changed??}
            GetFldrTitle (TheTerm^^.Folder, TheTerm^^.TheTitle);
            GetPort(SavePort);
            SetPort(TheTerm^^.Folder);
            ClipRect(TheTerm^^.Folder^.PortRect);
            SetPort(SavePort);
            UpdateTerm (TheTerm);
            PaintSBs (TheTerm);
          end;
                        {update the menu items}
          TheTitle:=Concat (BaseFPClose, ' "', TheTerm^^.TheTitle, '"');
          SetItem (TheMenus [FilPrt], FPClose, @TheTitle);

          with TheTerm^^.Setup do
            if ADNumber<>'' then
                TheTitle:=Concat (BaseDialDial, ' "', ADNumber, '"')
            else
                TheTitle:=Concat (BaseDialDial, '...');
          SetItem (TheMenus [DialMenu], DialDial, @TheTitle);
          InstallMenus;
          DoPhoneMenu (TheTerm);
        end;
      end; {ActivateFolder}


  procedure DoFilerCommand (TheParms: FilerExt);
    {DoFilerCommand processes a FilerEvent, using the given parameters.}

    const
      NoFile=972;

    type
      THFill=^TPFill;
      TPFill=^TFill;
      TFill=THFill;

    var
      HTerm: THTerm;
      Why: FReason;
      eCode: Integer;
      FileInfo: FS_Info;
      TermSize: LongInt;
      AllocSize: Integer;
      HFill: THFill;
      HTemp: THFill;

    {$S SegTEEvent }
    begin {DoFilerCommand}
      {$IFC SampCalls}   ALogCall;   {$ENDC}
      case TheParms.TheFlrOp of
        fcClose, fcShred, fcSuspend: begin
          HTerm:=Pointer (Ord (GetFldrRefCon (TheEvent.Who)));
          if HTerm^^.ReadMe then begin  {doc hasn't been read yet}
              TellFiler (eCode, DocClosd, AllOK, TheEvent.Who);
              if eCode>0 then Quit;

          end else begin
            if PutDoc (HTerm, HTerm^^.FileName,
              (TheParms.TheFlrOp=fcClose), Why) then begin
              KillFolder (HTerm);

              TellFiler (eCode, DocClosd, AllOK, TheEvent.Who);
              if eCode>0 then Quit;

            end else begin
              TellFiler (eCode, DocNotClosed, Why, TheEvent.Who);
              if eCode>0 then Quit;
            end;
          end;
        end;

        fcCopy, fcPut: begin
          HTerm:=Pointer (Ord (GetFldrRefCon (TheEvent.Who)));
          if PutDoc (HTerm, TheParms.ThePrefix, True, Why) then begin
            if TheParms.TheFlrOp=fcPut then
              KillFolder (HTerm);

            TellFiler (eCode, DocXfered, AllOK, TheEvent.Who);
            if eCode>0 then Quit;

          end else begin
            TellFiler (eCode, DocNotXfered, Why, TheEvent.Who);
            if eCode>0 then Quit;
          end;
        end;

        fcDfClose: begin
          TellFiler (eCode, DFNotClosed, InternalError, TheEvent.Who);
          if eCode>0 then Quit;
        end;

        fcNone: begin
          TellFiler (eCode, DocClosd, NoMoreDocs, TheEvent.Who);
          if eCode>0 then Quit;
        end;

        fcResume: begin
          LookUp (eCode, TheParms.ThePrefix, FileInfo);
          if (eCode>0) and (eCode<>NoFile) then begin
            GiveAlert (HTerm, AlrtStop, ALDocRead, HTerm^^.TheTitle, '', '');

            TellFiler (eCode, DocClosd, CantRead, TheEvent.Who);
            if eCode>0 then Quit;

          end else begin
            if eCode=NoFile then                        {Make sure we have room!!!}
              TermSize:=SizeOfTerminal
            else
              TermSize:=SizeOfTerminal + FileInfo.Size - SizeOf (TDocument);

            HFill:=NIL;                                 {Add filler to the heap.}
            while (HeapState=HeapNormal) and (TermSize>0) do begin
              if TermSize>32000 then
                AllocSize:=32000
              else
                AllocSize:=TermSize;
              TermSize:=TermSize-AllocSize;

              HTemp:=Pointer (Ord (HAllocate (TheHeap, AllocSize)));
              HTemp^^:=HFill;
              HFill:=HTemp;
            end;

            while HFill<>NIL do begin                   {Remove the filler.}
              HTemp:=HFill;
              HFill:=HTemp^^;
              FreeH (TheHeap, Pointer (Ord (HTemp)));
            end;

            if HeapState<>HeapNormal then begin         {Not enough disk space!}
              TellFiler (eCode, DocClosd, NoDiskSpace, TheEvent.Who);

            end else begin
              TakeWindow (TheEvent.Who);
              NrTerms:=NrTerms+1;

              NewHTerm (HTerm);
              with HTerm^^ do begin
                ReadMe:=True;
                ImBlank:= (eCode=0);

                FileName:=TheParms.ThePrefix;
                Folder:=TheEvent.Who;
                Password:=TheParms.thePassword;
              end;

              SetFldrRefCon (TheEvent.Who, Ord (HTerm));
              If HTerm^^.Folder = ActiveFolder then ActivateFolder;
            end;
          end;
        end;

        fcTerminate:
          Quit;
      end;
    end; {DoFilerCommand}

  {$S SegTEEvent }
  begin {ProcessEvent}
    {$IFC SampCalls}   ALogCall;   {$ENDC}
    if ((TheEvent.Who=DialogFolder) and (TheDBox.ProcessProc <> NIL)) or     {process dialog keystrokes.}
      ((TheEvent.What=KeyDown) and (Answering)) then
      CallPrcsProc (TheEvent, TheDBox.ProcessProc)

    else
      case TheEvent.What of
        KeyDown: {Seen by CheckRecieve only.};

        ButtonDown: begin
          if TheDBox.IsOpen then begin
            CallEndProc (False, TheDBox.EndProc);

          end else if TheEvent.Who=MenuFolder then begin
            if ActiveFolder <> ScrapFolder then
                AdornMenus;
            MenuSelect (TheEvent.Where, TheMenu, TheItem);
            MenuCommand (TheMenu, TheItem);
            HiLiteMenu (NullMenu);

          end else if TheEvent.Who=ScrapFolder then begin
            GetPort (SavePort);
            SetPort (ScrapFolder);
            if fGrowHit (TheEvent.Where) then
              GrowScrap (TheEvent.Where);
            SetPort (SavePort);

          end else if IsActive then
            if TheEvent.Who=TheTerm^^.Folder then begin
              DownTime:=TheEvent.When;
              if DownTime<=UpTime + DownDelay then
                DownWhere:=UpWhere
              else
                DownWhere:=TheEvent.Where;

              FindHit   (DownWhere, TheZone);
              case TheZone of
                TextBox, BTextBox:
                  if not TheEvent.ShiftKey then
                    if DownTime>=SelTime then
                      TargSize:=Single
                    else
                      if TargSize<>Beyond then
                        TargSize:=Succ (TargSize);
              end{CASE};

              FolderHit (DownWhere, TheZone);

              SelTime:=Time + ClickDelay;
            end;
        end;

        ButtonUp: begin
          UpTime:=TheEvent.When;
          if UpTime<=DownTime+UpDelay then
            UpWhere:=DownWhere
          else
            UpWhere:=TheEvent.Where;
        end;

        FolderUpdate: begin
          BeginUpdate (TheEvent.Who);
          if TheEvent.Who=FilerFolder then
            {Do nothing.}

          else if TheEvent.Who=ScrapFolder then
            PaintScrap

          else begin
            HTerm:=Pointer (GetFldrRefCon (TheEvent.Who));
            if HTerm^^.ReadMe then                      {HTerm not read in yet.}
              PaintEmpty (HTerm)
            else                                        {Normal update case.}
              PaintTerm (HTerm);
          end;

          EndUpdate (TheEvent.Who);
        end;

        FolderActivate: begin
          TakeControl (TheEvent, False, False);
          ActivateFolder;
        end;


        FolderDeactivate: begin
          if TheEvent.Who=ScrapFolder then begin
            GetPort (SavePort);
            SetPort (ScrapFolder);
            HideSBorder (ScrapFolder^.PortRect);
            SetPort (SavePort);

            TheTerm:=NIL;

          end else if IsActive and
                     (Pointer (GetFldrRefCon (TheEvent.Who))=TheTerm) then begin
            WMSpare1:=0;                        {Turn off Apple combo repeat.}
            UnpaintSBs (TheTerm);
            ChangeBlink (TheTerm^^.CaretRect, ZeroRect);
            TheTerm:=NIL;
          end;

          ShrinkSegMemory (TheHeap);
          GiveControl (TheEvent);
        end;

        FilerEvent: begin
          GetAddParms (eCode, TheEvent, TheParms);
          if eCode>0 then Quit;

          DoFilerCommand (TheParms);
        end;
      end;
  end; {ProcessEvent}

{$S SegTE }


ÿ