{ Copyright 1983, Apple Computer, Inc. }
{$S SegTECold}

procedure MyTextProc (Count: Integer; TextAddr: QDPtr; Numer, Denom: Point);
  {MyTextProc allows for double height and width text.  It is substuted}
  {for the standard QuickDraw text proc by InitUnits.}

  {$S SegTE}
  begin {MyTextProc}
    {$IFC SampCalls}   ALogCall;   {$ENDC}
    Numer.H:=Numer.H*HScale;
    Numer.V:=Numer.V*VScale;

    StdText (Count, TextAddr, Numer, Denom);
  end; {MyTextProc}


procedure Die;
  {Die does the body of the termination, either from Quit or from}
  {Handler.}

  var
    Why: FReason;
    eCode: Integer;

    {$IFC PrintTime }
      Pr: Text;
      I: Integer;
      J: Integer;
      P_Max: Integer;
      P_Fact: Integer;
      P_Top: Integer;
      P_Tot: LongInt;
    {$ENDC }

  {$S SegTECold}
  begin {Die}
    {$IFC SampCalls}   ALogCall;   {$ENDC}
    {$IFC PrintTime }
      Rewrite (Pr, '-PARAPORT-HIST.TEXT');
      if IOResult=0 then begin
        WriteLn (Pr, '                 >>>> LisaTerminal Paint Speed Histogram <<<<');
        WriteLn (Pr);

        P_Tot:=0;
        P_Max:=0;
        for I:=0 to 50 do begin
          P_Tot:=P_Tot + P_Hist [I];

          if P_Hist [I] > P_Max then
            P_Max:=P_Hist [I];
        end;

        P_Top:=P_Max;
        P_Fact:=1;
        while P_Top>60 do begin
          P_Fact:=P_Fact + 1;
          P_Top:=P_Max div P_Fact;
        end;

        WriteLn (Pr, 'Total number of print instances = ', P_Tot:1);
        WriteLn (Pr, '* = ', P_Fact:1, ' print instances.');
        WriteLn (Pr);

        WriteLn (Pr, '.01s  Count  Percent  Histogram...');
        for I:=0 to 50 do begin
          P_Top:=P_Hist [I];
          Write (Pr, I:4, P_Top:7, ((P_Top*100) div P_Tot):9, '  ');

          if P_Top>0 then begin
            P_Top:=(P_Top - 1) div P_Fact + 1;
            for J:=1 to P_Top do
              Write (Pr, '*');
          end;

          WriteLn (Pr);
        end;

        WriteLn (Pr);
        WriteLn (Pr, ' >50', P_Over:7);

        Close (Pr, Lock);
      end;
    {$ENDC }

    ImDying;

    if Running then begin
      Running:=False;

      while TermList<>NIL do begin              {Suspend all docs.}
        if PutDoc (TermList, TermList^^.FileName, False, Why) then;
        KillFolder (TermList);
      end;
    end;

    Close_Object (eCode, AlertNum);             {Clean up and goodbye...}
  end; {Die}


procedure Quit;
  {Quit is called to terminate TE.}

  {$S SegTECold}
  begin {Quit}
    {$IFC SampCalls}   ALogCall;   {$ENDC}
    Die;
    Exit (TE);
  end; {Quit}


procedure Handler (Env_Ptr: P_Env_Blk; Data_Ptr: P_Ex_Data);
  {Handler is the OS exception handler.}

  var
    eCode: Integer;

  {$S SegTECold}
  begin {Handler}
    {$IFC SampCalls}   ALogCall;   {$ENDC}
    Die;
  end; {Handler}


procedure DrawError (eCode: Integer);
 {DrawError gets called QuickDraw dies }

  {$S SegTECold}
  begin {DrawError}
    {$IFC SampCalls}   ALogCall;   {$ENDC}
    Quit;
  end; {DrawError}


procedure FailedAlert;
  {FailedAlert is called by the Alert Manager whenever it fails.}

  {$S SegTECold}
  begin {FailedAlert}
    {$IFC SampCalls}   ALogCall;   {$ENDC}
    StopAlert (MyAlerts, ALDefd2);
  end; {FailedAlert}



procedure Initialize;
  {Initialize initializes globals to put TE in the initial state.}


  procedure InitError;
    {InitError is called when anything in this procedure encounters}
    {a fatal error.}

    var
      eCode: Integer;

    {$S SegTEInit }
    begin {InitError}
      {$IFC SampCalls}   ALogCall;   {$ENDC}
      TellFiler (eCode, InitFailed, InternalError, NIL);
      Exit (TE);
    end; {InitError}


  procedure InitUnits;
    {InitUnits initializes all USES units necessary.}

    const
      phraseVersion = 1;

    var
      Proc_Info: ProcInfoRec;
      i: Integer;
      eCode: Integer;
      HeapRefNum: Integer;
      HeapAddr: LongInt;
      HeapStart,
      HeapLimit : Ptr;
      ToolDisk: Pathname;
      TEPrefix: Pathname;
      TheDisk: Integer;
      SavePort: GrafPtr;
      sysInfo:  FontInfo;


   procedure GetDataSeg (DsPathName: PathName;          {Disk segment name.}
                         DsMemSize, DsDiskSize: LongInt;{Memory and disk initial sizes.}
                         DsLdsn: Integer;               {LDSN to use for memory seg.}
                         VAR DsRefNum: Integer;         {The RefNum of the segment.}
                         VAR DsAddr: LongInt;            {The address of the segment.}
                         VAR TheDisk: Integer);         {Message menu item for flagging}
                                                        {alerts on the heap disk.}
     {GetDataSeg creates a data segment.}

     var
       eCode: Integer;
       ThePath: array [MsgPrefixDisk .. MsgToolDisk] of PathName;

     {$S SegTEInit }
     begin {GetDataSeg}
       {$IFC SampCalls}   ALogCall;   {$ENDC}
       ThePath [MsgPrefixDisk]:=                       DsPathName;
       ThePath [MsgToolDisk  ]:=Concat (ToolDisk, '-', DsPathName);
       TheDisk:=MsgPrefixDisk;

       Open_DataSeg (eCode, ThePath [TheDisk], DsRefNum, DsAddr, DsLdsn);
       if eCode>0 then begin
         TheDisk:=MsgToolDisk;
         Open_DataSeg (eCode, ThePath [TheDisk], DsRefNum, DsAddr, DsLdsn);
       end;

       if eCode<=0 then begin
         SetAccess_DataSeg (eCode, DsRefNum, False);
         if eCode>0 then
           InitError;

       end else begin
         Kill_DataSeg (eCode, ThePath [TheDisk]);

         TheDisk:=MsgPrefixDisk;
         Make_DataSeg (eCode, ThePath [TheDisk], DsMemSize, DsDiskSize,
                       DsRefNum, DsAddr, DsLdsn, ds_private);

         if eCode>0 then begin
           TheDisk:=MsgToolDisk;
           Make_DataSeg (eCode, ThePath [TheDisk], DsMemSize, DsDiskSize,
                         DsRefNum, DsAddr, DsLdsn, ds_private);
         end;

         if eCode>0 then
           InitError;
       end;
     end {GetDataSeg};


   procedure SetupPhrases;
     {SetupPhrases sets up phrase/menu file.}

     var
       DBoxFile: Text;
       Which: Integer;
       Form: TFormNr;
       HCkBox: TCkBoxHnd;
       TheTitle: Str30;
       IntCharSet: TSp;

     {$S SegTEInit }
     begin {SetupPhrases}
       {$IFC SampCalls}   ALogCall;   {$ENDC}
       {$I-}
       Reset(DBoxFile, Concat(TEPrefix,'DBox.Text'));
       {$I+}
       if IOResult>0 then InitError;

       GetItem (TheMenus [FilPrt   ], FPClose , @BaseFPClose);
       GetItem (TheMenus [DialMenu ], DialDial, @BaseDialDial);
       GetItem (TheMenus [ScrapFile], ScrClose, @BaseScrClose);

       DisableItem (TheMenus [FilPrt], FPBlank1);
       DisableItem (TheMenus [FilPrt], FPBlank2);
       DisableItem (TheMenus [FilPrt], FPRoute);
       DisableItem (TheMenus [FilPrt], FPPrint);
       DisableItem (TheMenus [FilPrt], FPPrintAsIs);

       DisableItem (TheMenus [EditMenu], EditUndo);
       DisableItem (TheMenus [EditMenu], Edit1Blank);
       DisableItem (TheMenus [EditMenu], EditCut);
       DisableItem (TheMenus [EditMenu], Edit2Blank);
       DisableItem (TheMenus [EditMenu], Edit3Blank);

       DisableItem (TheMenus [SetupMenu], Setup1Blank);

       DisableItem (TheMenus [DialMenu], DialBlank);

       DisableItem(TheMenus[RulerMenu], HideRulers);                                    {[JS]}

       SetupForm [SetupCompat].LastItem:=DBDial;
       SetupForm [SetupCmfort].LastItem:=DBRunoff;
       SetupForm [SetupCustom].LastItem:=DBBit0;
       SetupForm [SetCharSets].LastItem:=DBG1;
       for Form:=SetupCompat to SetCharSets do
         with SetupForm [Form] do begin

           NewHCkForm (HCkForm, LastItem);

           for Which:=DBDoForget to LastItem do begin
             ReadCkBox (DBoxFile, HCkBox);
             {$R-}
             HCkForm^^.CkBoxLst [Which]:=HCkBox;
             {$IFC RangeCheck }
               {$R+}
             {$ENDC }
           end;


           with HCkForm^^, CkBoxLst [DBDoForget]^^ do begin
             NumCkBox:=LastItem;

             DrawProc:=@ShowCkBtn;
             HitTstFunc:=@CkBtnHit;
           end;

           CkFormHeight (HCkForm);
         end;

       SetClrMap [True ]:=DBSet;
       SetClrMap [False]:=DBClr;

       Close(DboxFile,Lock);

       GetAlert(MyAlerts,ALDefaultCharSet,@IntCharSet);      {get default char set from phrase file}
       MakeSpUpper(IntCharSet);  {Uppshift IntCharSet}

       If IntCharSet = 'USASCII' then
            IntCharGroup:= ASCIICharSet
       Else
       If IntCharSet = 'UKCHARSET' then
            IntCharGroup:= UKCharSet
       Else
       If IntCharSet = 'E47SWEDISHFINNISH' then
            IntCharGroup:= E47SwedishFinnish
       Else
       If IntCharSet = 'D47SWEDISHFINNISH' then
           IntCharGroup:= D47SwedishFinnish
       Else
       If IntCharSet = 'NORWEGIAN' then
            IntCharGroup:= Norwegian
       Else
       If IntCharSet = 'DANISH' then
           IntCharGroup:= Danish
       Else
       If IntCharSet = 'GERMAN' then
            IntCharGroup:= German
       Else
       If IntCharSet = 'FRENCH' then
            IntCharGroup:= French
       Else
       If IntCharSet = 'FRENCHCANADIAN' then
            IntCharGroup:= FrenchCanadian
       Else
       If IntCharSet = 'SPANISH' then
            IntCharGroup:= Spanish
       Else
       If IntCharSet = 'YUGOSLAVIAN' then
            IntCharGroup:= Yugoslavian
       Else
       If IntCharSet = 'ITALIAN' then
            IntCharGroup:= Italian
       Else
         InitError;

        GetAlert(MyAlerts, AlGoAway, @mGoAway);                                 {BEGIN GetAlerts [JS]}
        GetAlert(MyAlerts, AlSetTab, @mSetTab);
        GetAlert(MyAlerts, AlClrTab, @mClrTab);
        GetAlert(MyAlerts, AlSetColumn, @mSetColumn);
        GetAlert(MyAlerts, AlClrColumn, @mClrColumn);                           {END GetAlerts [JS]}

     end; {SetupPhrases}


   {$S SegTEInit }
   begin {InitUnits}
      {$IFC SampCalls}   ALogCall;   {$ENDC}
      {Sched_Class (eCode, True);
       if eCode>0 then
       InitError;}

     Flush (I, -1000);                  {Init OS Stub.}

     Excep_Name:='SYS_TERMINATE';       {Declare the exception handler.}
     Declare_Excep_Hdl (eCode, Excep_Name, @Handler);

     InitGraf(@thePort);                {Initialize Quick Draw}

     OpenWM;                            {Each process calls OpenWM.}

     Info_Process (eCode, My_ID, Proc_Info);  {Create the data segment.}
     if eCode>0 then
       InitError;

     with Proc_Info do
       ToolDisk:=Concat (Copy (ProgPathname, 1, Pos ('-{', ProgPathname)-1));
     TEPrefix:=Concat (ToolDisk, '-{T10}');

     GetDataSeg ('TEHeap',
                 HeapInit + Ord4 (MinMem_Extra),
                 HeapInit + Ord4 (MinMem_Extra) + Ord4 (MinDiskExtra),
                 HeapLdsn,
                 HeapRefNum, HeapAddr,
                 TheDisk);

     {$IFC FindDecr }
       TermList:=NIL;
     {$ENDC }

     HeapState := HeapNormal;                   {Create the heap.}
     HeapStart := Pointer (HeapAddr);
     HeapLimit := Pointer (Ord(HeapStart) + HeapInit);
     InitHeap (HeapStart, HeapLimit, @HeapError);

     {$IFC Scramble }
       TheHeap^.fScramble:=True;
       TheHeap^.fCheck:=True;
     {$ELSEC }
       TheHeap^.fScramble:=False;
       TheHeap^.fCheck:=False;
     {$ENDC }


     InitMP(TheHeap, TEPrefix, @FailedAlert, phraseVersion,@TheMenus[FirstMenu],
             MyAlerts,eCode);
     if eCode>0 then InitError;


     SetPort (DeskPort);
     InitCursor;

     TextFont(p20Tile);
     GetFontInfo(sysInfo);
     tile20Ascent := sysInfo.ascent;

     TextFont(SysText);
     GetFontInfo(sysInfo);
     sysAscent := sysInfo.ascent;
     sysDescent := sysInfo.descent;

     InitDMgr (TheHeap, SysText);
     PrMgrInit;

     SetupPhrases;

     GetItem (TheMenus [MsgMenu], TheDisk, @HeapDisk);

     InitWmlSB;
     InitWmlCrs (eCode);

     OpenScrap (eCode);
     if eCode >0 then InitError;

    end; {InitUnits}


  procedure InitConsts;
    {InitConsts initializes all global structured constants.}

    var
      Ch: TTrix;
      I: Integer;
      TEPrefix: Pathname;
      Proc_Info: ProcInfoRec;
      eCode: Integer;
      Modifier: TModifier;
      Baud: INTEGER;                                                            {[JS]}


    function OddParity (Ch: Char): Boolean;
      {OddParity returns true if Ch has an odd number of bits set in bits}
      {zero through six.}

      var
        Int: Integer;
        BitCount: Integer;
        I: Integer;

      {$S SegTEInit }
      begin {OddParity}
        {$IFC SampCalls}   ALogCall;   {$ENDC}
        Int:=Ord (Ch);
        BitCount:=0;
        for I:=0 to 6 do begin
          if Odd (Int) then
            BitCount:=BitCount+1;

          Int:=Int div 2;
        end;

        OddParity:=Odd (BitCount);
      end; {OddParity}


    procedure InitStyle (var HStyle: THStyle; FontNr: Integer);
      {InitStyle creates a new style, pointed at by HStyle, using FontNr.}

      var
        FInfo: FontInfo;
        eCode: Integer;

      {$S SegTEInit }
      begin {InitStyle}
        {$IFC SampCalls}   ALogCall;   {$ENDC}
        NewHStyle (HStyle);
        with HStyle^^.Font do begin
          Fam:=FontNr;
          SetEFace:=[];
          Dev:=DevScreen;
        end;

        if not FMFontMetrics (HStyle^^.Font, FInfo, eCode) then
          InitError;

        with HStyle^^, FInfo do begin
          VertSp:=Leading + Ascent + Descent;   {Height of a character.}
          Below:=Descent + Leading div 2;       {Pixels below baseline.}
          Above:=VertSp - Below;                {Pixels above baseline.}
          HorzSp:=WidMax;                       {Width of a character.}
        end;
      end; {InitStyle}

    {$S SegTEInit }
    begin {InitConsts}
      {$IFC SampCalls}   ALogCall;   {$ENDC}
      Copyright:='Copyright 1983, 1984, Apple Computer, Inc.';

      SetStdProcs(GProcs);
      GProcs.TextProc:=@MyTextProc;

      with DeskStyle do begin
        Fam:=SysText;
        SetEFace:=[];
        Dev:=DevScreen;
      end;

      Info_Process (eCode, My_ID, Proc_Info);
      if eCode>0 then
        InitError;

      TEPrefix:=Concat (Copy (Proc_Info.ProgPathname, 1,
                   Pos ('-{', Proc_Info.ProgPathname)-1), '-{T10}');

      with Modifier do begin
        SetEFace:=[bold,underline];
        MpEFaceB [Bold      ]:=1;
        MpEFaceB [Italic    ]:=6;
        MpEFaceB [Underline ]:=1;
        MpEFaceB [Outline   ]:=1;
        MpEFaceB [Shadow    ]:=2;
      end;


      InitStyle (M80Style , p12TileLT);
      InitStyle (G80Style , p12Graf);
      InitStyle (M132Style, p20TileLT);
      InitStyle (G132Style, p20Graf);

      WITH TabMark DO                                                           {BEGIN Bitmaps [JS]}
          BEGIN                     {TabMark is the column upside down T.}
          BaseAddr := @TabMap;
          RowBytes := 2;
          SetRect(Bounds, 0, 0, 4, 4);
          END;
      StuffHex(@TabMap[1], '80008000C000F0000000');


      WITH ColumnMark DO
          BEGIN                     {ColumnMark is the tab up-arrow.}
          BaseAddr := @ColMap;
          RowBytes := 2;
          SetRect(Bounds, 0, 0, 5, 5);
          END;
      StuffHex(@ColMap[1], '20007000F80020002000');

      WITH PosMark DO
          BEGIN                     {PosMark is the positions marker pointer.}
          BaseAddr := @PosMap;
          RowBytes := 2;
          SetRect(Bounds, 0, 0, 7, 4);
          END;
      StuffHex(@PosMap[1], '100038007C00FE000000');

      WITH WrapMark DO
          BEGIN                     {WrapMark is the word wrap marker pointer.}
          BaseAddr := @WrapMap;
          RowBytes := 2;
          SetRect(Bounds, 0, 0, 7, 6);
          END;
      StuffHex(@WrapMap[1], 'F0003000300030003000F000');

      SmallV := RectHeight(PosMark.bounds) - 1;
      BigV   := SmallV + tile20Ascent;

      AlphaNum:=['0'..'9', 'A'..'Z', 'a'..'z', '_', '$', '£' ,'É' ,'Ä'
                 ,'Ö' ,'Å' ,'Ü' ,'é' ,'ä' ,'ö' ,'å' ,'ü' ,'Æ' ,'Ø' ,'æ'
                 ,'ø' ,'Ü' ,'ü' ,'ß' ,'ë' ,'à' ,'â' ,'ç' ,'ê' ,'î' ,'ô'
                 ,'é' ,'ù' ,'è' ,'û' ,'á' ,'Ñ' ,'í' ,'ñ' ,'ó' ,'ú' ,'ò' ,'ì' ,
                 '''', '-', '.'];

      NUL :=Chr ( 0);
      SOH :=Chr ( 1);
      STX :=Chr ( 2);
      ETX :=Chr ( 3);
      EOT :=Chr ( 4);
      ENQ :=Chr ( 5);
      ACK :=Chr ( 6);
      BELL:=Chr ( 7);
      BS  :=Chr ( 8);
      HT  :=Chr ( 9);
      LF  :=Chr (10);
      VT  :=Chr (11);
      FF  :=Chr (12);
      CR  :=Chr (13);
      SO  :=Chr (14);
      SI  :=Chr (15);
      DLE :=Chr (16);
      DC1 :=Chr (17);
      DC2 :=Chr (18);
      DC3 :=Chr (19);
      DC4 :=Chr (20);
      NAK :=Chr (21);
      SYN :=Chr (22);
      ETB :=Chr (23);
      CAN :=Chr (24);
      EM  :=Chr (25);
      SUB :=Chr (26);
      ESC :=Chr (27);
      FS  :=Chr (28);
      GS  :=Chr (29);
      RS  :=Chr (30);
      US  :=Chr (31);
      SP  :=Chr (32);
      DEL :=Chr (127);

      XON:=DC1;
      XOFF:=DC3;

      Ch.Ch1:=SP;
      Ch.Ch2:=Chr (0);
      BlankChar:=Ch.Ch;

      with BlankLine do begin
        for I:=MinCol to MaxCol do
          TextLine [I]:=BlankChar;

        Cached:=False;
        Changed:=False;
        Repaint:=False;
        SeqNr:=0;

        TwoHigh:=False;
        TwoTop:=False;
        TwoWide:=False;
      end;

      SetRect (ZeroRect, 0, 0, 0, 0);
      HourGlass := FALSE;                       {Allow cursor to be tracked}

      SizeOfTerminal:=SizeOf (TTerm)            {Minimum memory for a terminal.}
                    + SizeOf (TLine) * (MaxRow - MinRow) * 2
                    + SizeOf (TBlinkItem) * 4
                    + 1024;

      SizeOfTPage:=SizeOf (THPage)              {Prev}
                 + SizeOf (Integer)             {LinesInPage}
                 + SizeOf (Integer);            {PageSize}
      ScrapTerm:=Pointer (1234);

      with GroupA [DBNoParity] do begin         {Initialize the Device_Control groups.}
        DCVersion:=2;
        DCCode:=dvParity;
        DCData [0]:=6;
      end;

      with GroupA [DBEvenParity] do begin
        DCVersion:=2;
        DCCode:=dvParity;
        DCData [0]:=4;
      end;

      with GroupA [DBOddParity] do begin
        DCVersion:=2;
        DCCode:=dvParity;
        DCData [0]:=2;
      end;

      with GroupB [False] do begin
        DCVersion:=2;
        DCCode:=dvOutNoHS;
        DCData [0]:=0;
      end;

      with GroupB [True] do begin
        DCVersion:=2;
        DCCode:=dvOutXON;
      end;

                                                                                {BEGIN [JS]}
      FOR baud := DB50 TO DB19200 DO
          WITH GroupC[baud] DO
              BEGIN
              DCVersion:=2;
              DCCode:=dvBaud;
              END;

      GroupC[DB75].DCData[0]    :=50;
      GroupC[DB75].DCData[0]    := 75;
      GroupC[DB110].DCData[0]   := 110;
      GroupC[DB134_5].DCData[0] := 135;
      GroupC[DB150].DCData[0]   := 150;
      GroupC[DB200].DCData[0]   := 200;
      GroupC[DB300].DCData [0]  := 300;
      GroupC[DB600].DCData[0]   := 600;
      GroupC[DB1200].DCData[0]  := 1200;
      GroupC[DB1800].DCData[0]  := 1800;
      GroupC[DB2000].DCData[0]  := 2000;
      GroupC[DB2400].DCData[0]  := 2400;
      GroupC[DB3600].DCData[0]  := 3600;
      GroupC[DB4800].DCData[0]  := 4800;
      GroupC[DB9600].DCData[0]  := 9600;
      GroupC[DB19200].DCData[0] := 19200;
                                                                                {END [JS]}

      with GroupD do begin
        DCVersion:=2;
        DCCode:=dvInWait;
        DCData [0]:=1;
      end;

      with GroupE [False] do begin
        DCVersion:=2;
        DCCode:=dvInDTR;
      end;

      with GroupE [True] do begin
        DCVersion:=2;
        DCCode:=dvInXON;
      end;

      with GroupF [False] do begin
        DCVersion:=2;
        DCCode:=dvTypeAhead;
        DCData [0]:=-1;
        DCData [1]:=-1;
        DCData [2]:=HSBuffer+1;
      end;

      with GroupF [True] do begin
        DCVersion:=2;
        DCCode:=dvTypeAhead;
        DCData [0]:=HSBuffer;
        DCData [1]:=16;
        DCData [2]:=HSBuffer-32;
      end;

      with GroupG do begin
        DCVersion:=2;
        DCCode:=dvDiscon;
        DCData [0]:=0;
        DCData [1]:=0;
      end;

      with GroupH do begin
        DCVersion:=2;
        DCCode:=12 {dvTimeout};
        DCData [0]:=45;
      end;

      with GroupHRetry do begin
        DCVersion:=2;
        DCCode:=12 {dvTimeout};
        DCData [0]:=5;
      end;

      with GroupI do begin
        DCVersion:=2;
        DCCode:=dvAutoLF;
        DCData [0]:=0;
      end;

      with GroupX [False] do begin              {Short break.}
        DCVersion:=2;
        DCCode:=13;
        DCData [0]:=210;                        {.2333s - 10% for intr latency.}
      end;

      with GroupX [True ] do begin              {Long break.}
        DCVersion:=2;
        DCCode:=13;
        DCData [0]:=3150;                       {3.5s - 10% for intr latency.}
      end;

      with GroupY do begin
        DCVersion:=2;
        DCCode:=14;
        DCData [0]:=0;
      end;

      with GroupZ do begin
        DCVersion:=2;
        DCCode:=24;
      end;

      Password:='';
      UartName   [True ]:='-#10#1-xxx';
      UartPath   [True ]:= '#10#1'    ;
      SerialName [True ]:=      'A'    ;
      UartName   [False]:='-#10#2-xxx';
      UartPath   [False]:= '#10#2'    ;
      SerialName [False]:=      'B'    ;

      HayesDrop [DBNoParity  ]:='NO CARRIER';           {Since Hayes modems don't}
      HayesDrop [DBOddParity ]:='NO CARRIER';           {use parity, a different}
      HayesDrop [DBEvenParity]:='NO CARRIER';           {message is tested for}
      for I:=1 to Length (HayesDrop [DBNoParity]) do    {in each case.  Each msg}
        if OddParity (HayesDrop [DBNoParity] [I]) then  {is what it would look}
          HayesDrop [DBOddParity ] [I]:=ParityVis       {like with bit-7 = 0 in}
        else                                            {each parity mode.  Used}
          HayesDrop [DBEvenParity] [I]:=ParityVis;      {in CheckDisconnect.}
    end; {InitConsts}


  procedure InitVars;
    {InitVars initializes all global var's that weren't set in InitUnits.}

    var
      Pt: Point;
      {$IFC PrintTime }
        I: Integer;
      {$ENDC }

    {$S SegTEInit }
    begin {InitVars}
      {$IFC SampCalls}   ALogCall;   {$ENDC}
      Answering:=False;

      AlertHead:=NIL;
      AlertTail:=NIL;

      TheTerm:=NIL;
      TermList:=NIL;
      BlinkList:=NIL;
      Running:=True;

      HScale:=1;
      VScale:=1;

      PasteTerm:=NIL;
      BlinkVis:=False;
      MouseVis:=True;
      GetMouse (MousePt);
      TargSize:=Single;
      SelTime:=-1;
      UpTime:=-1;
      DownTime:=-1;

      Changed:=False;
      BigTerm:=False;

      with TheScrap do begin
        HCache:=NIL;

        InitSBList (SBList, TheHeap);

        SetPt (Pt, 0, 0);
        VSBar:=SBCreate (SBList, HSBNIL, TyVHV, Pt, 0);
        HSBar:=SBCreate (SBList, HSBNIL, TyVHH, Pt, 0);
      end;

      NrTerms:=0;
      NextSeqNr:=0;
      JunkRgn:=NewRgn;

      {$IFC PrintTime }
        for I:=0 to 50 do
          P_Hist [I]:=0;
        P_Over:=0;
      {$ENDC }

      {$IFC NrHandles }
        CountHandles:=True;
      {$ENDC }

      {$IFC FindDecr }
        Rgn1:=0;
        Rgn2:=0;
        Rgn3:=0;
        Rgn4:=0;
        OldProc:='Initial State';
      {$ENDC }

      {$IFC UpdateFreq }
        MaxUpdWait := 150;
        MaxChanges := 20;
      {$ENDC}
    end; {InitVars}

  {$S SegTEInit }
  begin {Initialize}
    {$IFC SampCalls}   ALogCall;   {$ENDC}
    Running:=False;

    InitUnits;
    InitConsts;
    InitVars;
  end; {Initialize}

{$S SegTE }

ˇ