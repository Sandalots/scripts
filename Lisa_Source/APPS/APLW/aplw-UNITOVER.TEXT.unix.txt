{$I APLW/COMPFLAGS }                                            {gdf 1/16/84}
{$R-}

UNIT UnitOver;

(* Copyright 1983, 1984, Apple Computer Inc. *)

{ gdf  2/24/84 -- insert trace calls for segmentation  }
{ gdf  1/16/84 -- printing compile flag & include COMPFLAGS added }
{ crc  2/02/03 -- Exported AddRs }
{ crc  2/01/83 -- ScrollR: exits if argument rec is empty }
{ crc  1/17/82 -- AdjustRecs: set dptScrnPrv conditionally on fImged }
{ crc  7/16/82 -- OboxVisOrec: commented out call to SetRectRgn }
{ jrm  6/29/82  --  USES changes: LisaGraf => QuickDraw; moved USES UnitFile }
{ jrm  6/29/82 -- replaced GetClipRgn call with
                  a direct reference to thePort^.clipRgn }
{ crc  6/02/82 -- Conditional compilation }
{ crc 05/25/82 -- USES PrStd added }
{ crc 04/06/82 -- integration with new font manager }
{ bas 03/08/82 -- Recs have tyxy's now }
{ bas 01/13/82 -- USES for new WM }
{ bas 12/24/81 -- more lisaprint }
{ bas 12/15/81 -- integration with lisaprint (make Inv exported) }
{ bas 12/10/81 -- integration with font manager }
{ edited by dsy 8/17/81 }

INTERFACE

USES
     {$IFC sampcalls}                                               {gdf 2/24/84}
        {$U aplw/tracecalls.obj} tracecalls,
     {$ENDC}
     {$U Obj:UnitStd.Obj  } UnitStd,
     {$U Obj:UnitHz.Obj   } UnitHz,
     {$U LibOS/Syscall.Obj }   Syscall,
     {$U Obj:UnitFile.Obj } UnitFile,
     {$U Obj:Storage.Obj  } Storage,
     {$U Obj:QuickDraw.Obj } QuickDraw,
     {$U Obj:FontMgr.Obj  } FontMgr,
     {$U Obj:PmDecl       } PmDecl,
     {$IFC fPrintOld}                                               {gdf 1/16/84}
        {$U Obj:PrStd.Obj }     PrStd,
     {$ELSEC}
        {$U Obj:PrStdInfo.Obj } PrStdInfo,
          (* {$U Obj:PrPublic.Obj }  PrPublic,  unneeded ? *)
     {$ENDC}                                                        {gdf 1/16/84}
     {$U Obj:UnitFmt.Obj  } UnitFmt,
     {$U Obj:UnitCs.Obj   } UnitCs,
     {$U Obj:Wm.Events.Obj  } Events,
     {$U Obj:Wm.Folders.Obj } Folders,
     {$U Obj:WmlStd     }    WmlStd,
     {$U Obj:WM.Menus.Obj } Menus,
     {$U Obj:AlertMgr.Obj } AlertMgr,
     {$U ApLW/Queue.Obj    } Queue,
     {$U ApLW/UnitBox.Obj  } UnitBox,
     {$U ApLW/UnitRec.Obj  } UnitRec;

CONST
    {$IFC NOT fDbgOk} fTstOver = FALSE; {$ENDC}
    dhCaret = 1;

{$IFC fDbgOk}
VAR
     fTstOver: TF;
{$ENDC}

FUNCTION  FClipR        (r: TR; orec: TC): TF;
FUNCTION  FClipOrec     (orec: TC): TF;
PROCEDURE EraseOrec     (orec: TC);
PROCEDURE Inv           (orec: TC; rInv: TR; tyxyR: TTyxy; fInvSibs: TF);
PROCEDURE InvalidR      (rInv: TR; tyxyR: TTyxy; orec: TC);
PROCEDURE InvalidOrec   (orec: TC);
PROCEDURE MoveRecs      (orec: TC; ptFst: TPt; tyxyPt: TTyxy);
FUNCTION  OboxVisOrec   (orec: TC): TC;
PROCEDURE AddRs         (VAR rSrc, rDst: TR);


IMPLEMENTATION

{$IFC fSymOK }
{$D+ }
{$ELSEC }
{$D- }
{$ENDC }

{(**VAR argnClip:  TArgn;
    rgnClip:   TRgn; **)}

FUNCTION FInSet(element: TW; setof: TW): TF; EXTERNAL;


{$S LWCommand}
FUNCTION OboxVisOrec {(orec: TC): TC};
VAR rec: TRec; pbox: TPbox; argo: ARRAY [0..1] OF TC;
         orecT: TC; obox: TC; rT: TR; tyxyVis: TTyxy;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
IF fTstOver THEN WRITELN('OboxVisOrec -- orec: ', orec);
IF orec = orecNil THEN
     obox := oboxNil
ELSE BEGIN
     SetRgoFromOrec(orec, @argo, 2);
     obox := argo[0];
     orecT := argo[1];
     rec := POINTER(ORD(dsp) + orecT);
     tyxyVis := rec^.tyxy;
     IF fTstOver THEN
          WRITELN('argo[0]: ', obox, ', argo[1]: ', argo[1]);
     IF obox <> oboxNil THEN
          BEGIN
          VisObox(obox);
          pbox := PboxFromObox(obox);
          orecT := pbox^.refCon;
          WHILE orecT <> argo[1] DO
               BEGIN
               rec := POINTER(ORD(dsp) + orecT);
               orecT := rec^.orecSib;
               ConvertRTyxy(rec^.r, rT, rec^.tyxy, tyxyScrn);
               {$ifc fDbgOk}
               IF fTstOver THEN PxR('rDiff: ', rT);
               {$endc}
               DiffRgnVis(rT);
               END;
          rec := POINTER(ORD(dsp) + orec);
          IF FInSet(fixedR, rec^.strec.setfixed) THEN
               BEGIN
               ConvertRTyxy(rec^.r, rT, rec^.tyxy, tyxyVis);
               {$ifc fDbgOk}
               IF fTstOver THEN PxR('rSect: ', rT);
               {$endc}
               SectRgnVis(rT);
               END;
          END
     ELSE BEGIN
          (* commented out to prevent clipping printer port.
          SetRectRgn(thePort^.clipRgn, 0,0,0,0);  { no garbaging the screen }
          IF fTstOver THEN WRITELN('obox = oboxNil, hrgnVis set to 0,0,0,0')
          *)
          END;
     END;
OboxVisOrec := obox;
END;


FUNCTION FClipR {(r: TR; orec: TC): TF };
 { Returns FALSE if nothing left after clipping. }
BEGIN
 {$IFC sampcalls}  ALogCall;  {$ENDC}
 {(**
 IF fTstOver THEN
     BEGIN
     WRITELN('FClipR -- orec: ', orec);
     PxR('     r: ', r);
     END;
argnClip.rgnBBox := r; argnClip.rgnSize := cbRgnRect;
rgnClip := @argnClip;
SetClipRgn(@rgnClip);
 **)}
 { since all it does is return TRUE, don't worry about tyxy }
FClipR := TRUE;
END;


{$S LWPreview}
FUNCTION FClipOrec { (orec: TC):TF };
VAR rec: TRec;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
rec := POINTER(ORD(dsp) + orec);
FClipOrec := FClipR(rec^.r, orec);
END;


{$S LWType}
PROCEDURE EraseInvert (orec: TC; fErase: TF);
VAR rec: TRec; rT: TR;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
{$ifc fDbgOk}
IF fTstOver THEN
   BEGIN
   WRITELN('EraseInvert - orec:', orec, ', fErase:',fErase); PxOrec(orec);
   END;
{$endc}
rec := POINTER(ORD(dsp) + orec);
IF FClipR(rec^.r, orec) THEN
     BEGIN
     ConvertRTyxy(rec^.r, rT, rec^.tyxy, tyxyScrn);
     {$ifc fDbgOk}
     IF fTstOver THEN PxR('r: ', rT);
     {$endc}
     IF fErase THEN
          BEGIN
          rT.left := rT.left - dhCaret;
          EraseRect(rT)
          END
     ELSE InvertRect(rT);
     IF fTstOver THEN FrameRect(rT);
     END;
IF fTstOver THEN WRITELN('...EraseInvert done');
END;


PROCEDURE EraseOrec{orec: TC};
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
EraseInvert(orec, TRUE);
END;


{$S LWSelect}
PROCEDURE Inv {(orec: TC; rInv: TR; tyxyR: TTyxy; fInvSibs: TF)};
VAR rec: TRec;  rInvalid, r2: TR;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
IF fTstOver THEN WRITELN('Inv: ');
IF orec <> orecNil THEN
   BEGIN
   rec := POINTER(ORD(dsp) + orec);
   ConvertRTyxy(rInv, rInvalid, tyxyR, rec^.tyxy);
   END;
WHILE orec <> orecNil DO
   BEGIN
   IF fTstOver THEN WRITE('orec: ', orec);
   rec := POINTER(ORD(dsp) + orec);
   r2 := rec^.r;
   {$ifc fDbgOk}
   IF fTstOver THEN BEGIN PxR('rInvalid ', rInvalid); PxR('rec r ', r2); END;
   {$endc}
   IF SectRect(rInvalid, r2, r2) THEN
      BEGIN
      IF fTstOver THEN WRITE('<-INVALID');
      rec^.strec.fValid := FALSE;
      IF rec^.orecSon <> orecNil THEN Inv(rec^.orecSon, rInv, tyxyR, TRUE);
      END;
   IF fInvSibs THEN
        BEGIN rec := POINTER(ORD(dsp) + orec); orec := rec^.orecSib; END
   ELSE orec := orecNil;
   END;
END;


PROCEDURE InvalidR {(rInv: TR; tyxyR: TTyxy; orec: TC)} ;
VAR pbox: TPbox; orecCon: TC; rec: TRec;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
{$ifc fDbgOk}
IF fTstOver THEN
   BEGIN WRITELN('InvalidR: orec: ', orec); PxR('r: ', rInv); WRITELN; END;
{$endc}
{ SetOboxPort(oboxPane); }
pbox := PboxFromObox(OboxOfOrec(orec));
orecCon := pbox^.refCon;
{ rec := POINTER(ORD(dsp) + orecCon);
rec^.strec.fValid := FALSE; }
Inv(orecCon, rInv, tyxyR, TRUE);
{ IF oboxPane = ruler.oboxPane THEN Inv(ruler.orec, rInv, FALSE); }
END;


PROCEDURE InvalidOrec {(orec: TC)} ;
VAR rec: TRec;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
rec := POINTER(ORD(dsp) + orec);
rec^.strec.fValid := FALSE;
InvalidR(rec^.r, rec^.tyxy, orec);
END;


{$S LWType}
FUNCTION OrecLstReverse(orec: TC) : TC;
VAR orecPrv: TC; orecT: TC;
    rec: TRec;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
orecPrv := orecNil;
WHILE orec <> orecNil DO
     BEGIN
     rec := POINTER(ORD(dsp) + orec);
     orecT := rec^.orecSib;
     rec^.orecSib := orecPrv;
     orecPrv := orec;
     orec := orecT;
     END;
OrecLstReverse := orecPrv;
END;

PROCEDURE AddRs {(VAR rSrc, rDst: TR)};
 {expands dst to enclose both}
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
{$ifc fDbgOk}
IF fTstRec THEN
   BEGIN
   PxR('AddRs  rSrc: ', rSrc); PxR('; rDst: ', rDst);
   END;
{$endc}
rDst.left   := CMin(rSrc.left, rDst.left);
rDst.top   := CMin(rSrc.top, rDst.top);
rDst.bottom   := CMax(rSrc.bottom, rDst.bottom);
rDst.right   := CMax(rSrc.right, rDst.right);
IF fTstRec THEN
   WITH rDst DO WRITELN(' -> [', left, top, right, bottom, ']');
END;

PROCEDURE ScrollR(rSrc: TR; dptScrn: TPt; hrgnInv: THrgn; rSrcClip: TR);
VAR
r: TR;
rec: TRec;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
IF (rSrc.left = rSrc.right) OR (rSrc.top = rSrc.bottom) THEN EXIT(ScrollR);
rSrc.left := rSrc.left - dhCaret;
IF SectRect(rSrc, rSrcClip, rSrc) THEN
     BEGIN
     IF dptScrn.h < 0 THEN
          rSrc.left   := CMax(0, rSrc.left + dptScrn.h)
     ELSE rSrc.right  := rSrc.right + dptScrn.h;
     IF dptScrn.v < 0 THEN
          rSrc.top    := CMax(0, rSrc.top + dptScrn.v)
     ELSE rSrc.bottom := rSrc.bottom + dptScrn.v;
     {$ifc fDbgOk}
     IF fTstOver THEN
          BEGIN
          PxR('rSrc: ', rSrc);
          WRITELN('dptScrn: [', dptScrn.h, ', ', dptScrn.v, ']');
          END;
     {$endc}
     ScrollRect(rSrc, dptScrn.h, dptScrn.v, hrgnInv);
     END;
END; {ScrollR}

{$S LWCommand}
PROCEDURE MoveRecs {(orec: TC; ptFst: TPt; tyxyPt: TTyxy)};
 { MoveRecs does not set its own visible clipping because of different
   requirements for scrolling and updating }
VAR hrgnT: THrgn; hrgnSrc: THrgn; hrgnClip: THrgn; rMax: TR;

    PROCEDURE AdjustRecs (orec1: TC; ptFst: TPt; tyxyPt: TTyxy; rSrcClip: TR);
    VAR rSrc: TR; rEncl: TR; fLeafCs: TF; rSonClip: TR;
        dpt: TPt; ptScrn: TPt; dptScrn : TPt; dptScrnPrv: TPt;
        orecLst: TC; recCur: TRec; recSon: TRec;
        fImged: TF; fT: TF;
    BEGIN
    {$IFC sampcalls}  ALogCall;  {$ENDC}
    IF fTstOver THEN
         BEGIN
         WRITELN('AdjustRecs - orec1: ', orec1, ', ptFst: [', ptFst.h,
                  ptFst.v, '], tyxyPt: ', ORD(tyxyPt));
         END;
    fLeafCs := TRUE;    {We assume fLeafCs is true until we see recursion }
                        {We only do screen operations on leaf cs's}
    SetPt(dptScrnPrv, MAXINT, MAXINT);
    rEncl.topLeft := dptScrnPrv; rEncl.botRight := dptScrnPrv;
    recCur := POINTER(ORD(dsp) + orec1);
    dpt := ptFst;
    ConvertTyxy(dpt.h, dpt.v, tyxyPt, recCur^.tyxy);
    SubPt(recCur^.r.topLeft, dpt);
    IF (dpt.v > 0) AND (NOT recCur^.strec.fRecCon) THEN
         BEGIN
         orecLst := OrecLstReverse(orec1);
         orec1 := orecLst;
         END
    ELSE orecLst := orecNil;
    WHILE orec1 <> orecNil DO
         BEGIN
         {$ifc fDbgOk}
         IF fTstOver THEN PxOrec(orec1);
         {$endc}
         recCur := POINTER(ORD(dsp) + orec1);
         rSrc := recCur^.r;
         OffsetRect(recCur^.r, dpt.h, dpt.v);
         ConvertRTyxy(rSrc, rSrc, recCur^.tyxy, tyxyScrn);
         recCur := POINTER(ORD(dsp) + orec1);
         IF recCur^.strec.fRecCon THEN
              BEGIN
              recCur^.strec.fValid := FALSE;
              IF fTstOver THEN WRITELN('...Invalidated!');
              END
         ELSE BEGIN
              fImged := recCur^.strec.fImged;
              fLeafCs := fLeafCs AND (recCur^.orecSon = orecNil);
              ptScrn := recCur^.r.topLeft;
              ConvertTyxy(ptScrn.h, ptScrn.v, recCur^.tyxy, tyxyScrn);
              dptScrn := ptScrn;
              SubPt(rSrc.topLeft, dptScrn);
              RectRgn(hrgnSrc, rSrc);
              SectRgn(hrgnSrc, hrgnClip, hrgnT); {hrgnT = visible source}
              IF (hrgnT^^.rgnSize <> cbRgnRect)
                OR (NOT FEqRs(rSrc, hrgnT^^.rgnBBox)) THEN
                  BEGIN
                  DiffRgn(hrgnSrc, hrgnT, hrgnT); {hrgnT = not visible source}
                  OffsetRgn(hrgnT, dptScrn.h, dptScrn.v);
                                                  {hrgnT = not visible dest}
                  SectRgn(hrgnT, hrgnClip, hrgnT);
                  IF NOT EmptyRgn(hrgnT) THEN
                       BEGIN
                       recCur^.strec.fValid := FALSE;
                       IF fTstOver THEN WRITELN('...Invalidated!');
                       END;
                  END;
              IF fLeafCs THEN
                   BEGIN
                   IF NOT fImged THEN rSrc.topLeft := rSrc.botRight;
                   IF (dptScrn.h = dptScrnPrv.h) AND (dptScrn.v = dptScrnPrv.v)
                       AND fImged THEN
                        AddRs(rSrc, rEncl)
                   ELSE BEGIN
                        ScrollR(rEncl, dptScrnPrv, hrgnT, rSrcClip);
                        rEncl := rSrc;
                        IF fImged THEN
                             dptScrnPrv := dptScrn
                        ELSE
                             SetPt(dptScrnPrv, MAXINT, MAXINT);
                        END;
                   recCur := POINTER(ORD(dsp) + orec1);
                   END
              ELSE BEGIN
                   rEncl.topLeft := dptScrnPrv; rEncl.botRight := dptScrnPrv;
                   END;
              END;
         WITH recCur^ DO BEGIN
              { adjust siblings if a con-type rec }
              IF orecSon <> orecNil THEN
                   BEGIN
                   rSonClip := rSrcClip;
                   IF FInSet(fixedR, strec.setfixed) THEN
                        fT := SectRect(rSonClip, rSrc, rSonClip);
                   END;
             IF strec.fRecCon THEN
                  BEGIN
                  orec1 := orecNil;
             { but first, recurse to adjust sons }
                  IF orecSon <> orecNil THEN
                       BEGIN
                       recSon := POINTER(ORD(dsp) + orecSon);
                       ptFst := recSon^.r.topLeft;
                       { since it is a fRecCon to NOT fRecCon transition
                       no ConvertTyxy is necessary }
                       AddPt(dpt, ptFst);
                       AdjustRecs(orecSon, ptFst, recSon^.tyxy, rSonClip);
                       END;
                  END
             ELSE BEGIN
                  orec1 := orecSib;
             { but first, recurse to adjust sons }
                  IF orecSon <> orecNil THEN
                      BEGIN
                      AdjustRecs(orecSon, r.topLeft, tyxy, rSonClip);
                      END;
                  END;
             END;
         END;
    ScrollR(rEncl, dptScrn, hrgnT, rSrcClip);
    IF orecLst <> orecNil THEN
         orecLst := OrecLstReverse(orecLst);
    END { AdjustRecs };
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
IF fTstOver THEN
   WRITELN('MoveOverOnce - ptFst: [', ptFst.h, ptFst.v, ']');
hrgnT := NewRgn; hrgnSrc := NewRgn; hrgnClip := NewRgn;
CopyRgn(thePort^.clipRgn, hrgnClip);
SectRgn(thePort^.visRgn, hrgnClip, hrgnClip);
 (* GetClipRgn(hrgnClip); hrgnClip := thePort^.clipRgn; *)
SetRect(rMax, 0, 0, MAXINT, MAXINT);
AdjustRecs(orec, ptFst, tyxyPt, rMax);
DisposeRgn(hrgnT); DisposeRgn(hrgnSrc); DisposeRgn(hrgnClip);
END;


END.




ÿ