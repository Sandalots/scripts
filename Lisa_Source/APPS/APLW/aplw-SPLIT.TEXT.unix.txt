{ ============================================================================ }
{                                 S p l i t                                    }
{                                                                              }
{                    an include file included in UnitFolder                    }
{                                                                              }
{ ============================================================================ }

(* Copyright 1983, 1984, Apple Computer Inc. *)

 { gdf  2/27/84 -- insert trace calls for segmentation  }
 { gdf  1/21/84 -- change Enqueue to EnqueueLotus  }
 { jrm  2/24/83 -- undangled dangling pboxes after Enqueue calls }
 { tjm  2/24/83 -- AdjScCorner: fPaint controls painting only }
 { bas  9/11/82 -- split min size fixed }
 { bas  7/20/82 -- no fUpdate parm to FreeObox }
 { jrm  5/26/82 -- Removed prcs reference }
 { bas 03/02/82 -- AdjScCorner: fPaint controls both event sending and painting }
 { bas 02/05/82 -- Replace ClipToScreen/SetPort with BegGlobal/EndGlobal }
 { bas 01/21/82 -- update SynSplit for icon-based scroll boxes }
 { edited by bas 9/22/81 }


{$S LWSplit}
{------------------------------------------}
PROCEDURE FillPane(oboxPane:TC);
{------------------------------------------}
VAR pbox: TPbox; r: TR;
BEGIN
    {$IFC sampcalls}  ALogCall;  {$ENDC}
    pbox := POINTER(ORD(bdsp) + oboxPane);
    r := pbox^.r;
    InsetRect(r, -1, -1);
    FillRect(r, white);
    FrameRect(r);
END;

{--------------------------------------------------------------}
PROCEDURE LinkToScrollChain (obox, oboxPrv: TC; tyvh: TTyvh);
{--------------------------------------------------------------}
VAR pbox, pboxPrv: TPbox;
BEGIN
    {$IFC sampcalls}  ALogCall;  {$ENDC}
    IF fTstBox THEN WRITELN('LinkToSC obox, oboxPrv, tyvhH?: ', obox, oboxPrv, tyvh=tyvhH);
    pbox := POINTER(ORD(bdsp) + obox);
    pboxPrv := POINTER(ORD(bdsp) + oboxPrv);
    IF pboxPrv^.tybox = tyboxScroll THEN BEGIN
        pbox^.rgoboxLink[tyvh] := pboxPrv^.oboxPaneFst;
        pbox^.rgFScHead[tyvh] := TRUE;
        IF pboxPrv^.oboxPaneFst <> oboxPrv THEN BEGIN
             { some other pane was first, it no longer is }
            pbox := POINTER(ORD(bdsp) + pboxPrv^.oboxPaneFst);
            pbox^.rgfScHead[tyvh] := FALSE;
        END;
        pboxPrv^.oboxPaneFst := obox;
    END ELSE BEGIN
        pbox^.rgFScHead[tyvh] := FALSE;
        pbox^.rgoboxLink[tyvh] := pboxPrv^.rgoboxLink[tyvh];
        pboxPrv^.rgoboxLink[tyvh] := obox;
    END;
END;

{-----------------------------}
PROCEDURE KillSc(oboxScrl: TC);
{-----------------------------}
 { delete the given scroll chain, including the scroll box }
VAR pboxScrl, pboxPane, pboxPrv, pboxNxt: TPbox;
    tyvh, tyvhOrtho: TTyvh;  evDsc: TEvDsc;
    oboxPane, oboxPrv, oboxNxt: TC;
BEGIN
    {$IFC sampcalls}  ALogCall;  {$ENDC}
    {$ifc fDbgOk}
    IF fTstBox THEN BEGIN
        WRITELN('KillSc  oboxScrl: ', oboxScrl);
        PxScrollChain(oboxScrl);
    END;
    {$endc}
    pboxScrl := POINTER(ORD(bdsp) + oboxScrl);
    tyvh := pboxScrl^.tyvh;
    tyvhOrtho := TyvhNOT(tyvh);

     { queue both an ScKill event for the chain and PaneKill events for each
       killed pane, to make everyone happy }
    evDsc.tyev := tyevScKill;
    evDsc.obox := oboxNil;  { disable call to VisObox }
    evDsc.scid := ScidFromObox(oboxScrl, tyvh);
    EnqueueLotus(bdsp^.pq^, @evDsc);
    evDsc.tyev := tyevPaneKill;

    pboxScrl := POINTER(ORD(bdsp) + oboxScrl);
    oboxPane := pboxScrl^.oboxPaneFst;
    WHILE oboxPane <> oboxScrl DO BEGIN
        pboxPane := POINTER(ORD(bdsp) + oboxPane);
         { get the boxes before and after in the orthogonal scroll chain }
        oboxPrv := OboxPrvSc(oboxPane, tyvhOrtho);
        oboxNxt := pboxPane^.rgoboxLink[tyvhOrtho];
         { fix the links in the orthogonal scroll chain }
        pboxPrv := POINTER(ORD(bdsp) + oboxPrv);
        IF pboxPrv^.tybox = tyboxScroll THEN BEGIN
             { before box is a scroll box => this one is an sc head }
             pboxPrv^.oboxPaneFst := oboxNxt;
             pboxNxt := POINTER(ORD(bdsp) + oboxNxt);
             pboxNxt^.rgfScHead[tyvhOrtho] := TRUE;
        END ELSE
             pboxPrv^.rgoboxLink[tyvhOrtho] := oboxNxt;

        evDsc.refCon := pboxPane^.refCon;
        EnqueueLotus(bdsp^.pq^, @evDsc);

        pboxPane := POINTER(ORD(bdsp) + oboxPane);
        oboxNxt := pboxPane^.rgoboxLink[tyvh];
        FreeObox(oboxPane);
        oboxPane := oboxNxt;
    END;
    FreeObox(oboxScrl);
END;

{$S LWResize}
{-----------------------------------------------------------------------------}
PROCEDURE AdjScCorner (oboxScrl: TC; dcdAdj: TCd; fTopLeft: TF; fPaint: TF);
{-----------------------------------------------------------------------------}
 { Adjust by dcdAdj either the topLeft or botRight corner of all boxes on the
   given scroll chain, including the scroll box.  For a horizontal scroll
   chain, the horizontal component of the corner is adjusted; vertical
   component for vertical scroll chains.  fPaint  controls both painting and
   event sending. }
VAR oboxPane: TC;  pbox: TPbox;  evDsc: TEvDsc; tyvh: TTyvh;
BEGIN
    {$IFC sampcalls}  ALogCall;  {$ENDC}
    pbox := POINTER(ORD(bdsp) + oboxScrl);
    tyvh := pbox^.tyvh;
    IF fTstBox THEN WRITELN('AdjScCorner  oboxScrl: ', oboxScrl:4, ', dcdAdj: ',
          dcdAdj:4, ', Horiz? ', tyvh=tyvhH, ', TopLeft? ', fTopLeft,
          ', Paint? ', fPaint);

    IF dcdAdj <> 0 THEN BEGIN
          { adjust the scroll box }
         WITH pbox^.r DO
             IF fTopLeft THEN topLeft.vh[tyvh]  := topLeft.vh[tyvh] + dcdAdj
             ELSE             botRight.vh[tyvh] := botRight.vh[tyvh] + dcdAdj;
         IF fPaint THEN BEGIN
             PaintBox(oboxScrl);
         END;
         evDsc.tyev := tyevScResize; evDsc.dcdAdjust := dcdAdj;
         evDsc.obox := oboxNil;  { disable call to VisObox }
         evDsc.fAdjFront := fTopLeft;
         evDsc.scid := ScidFromObox(oboxScrl, tyvh);
         EnqueueLotus(bdsp^.pq^, @evDsc);

         pbox := POINTER(ORD(bdsp) + oboxScrl);
         oboxPane := pbox^.oboxPaneFst;
         WHILE oboxPane <> oboxScrl DO BEGIN
             pbox := POINTER(ORD(bdsp) + oboxPane);
             WITH pbox^.r DO
                 IF fTopLeft THEN topLeft.vh[tyvh]  := topLeft.vh[tyvh] + dcdAdj
                 ELSE             botRight.vh[tyvh] := botRight.vh[tyvh] + dcdAdj;
             IF fPaint THEN FillPane(oboxPane);
             oboxPane := pbox^.rgoboxLink[tyvh];
         END;
    END;
END;


{$S LWSplit}
{----------------------------------------------------}
PROCEDURE SynSplit { (oboxScrl: TC; cdCoord: TCd) };
{----------------------------------------------------}
 { create a split.  oboxScrl and all of the panes on its scroll
   chain get split. cdCoord is the BOX coordinate where the split is to
   occur, i.e., the desired post-split size of oboxScrl's scroll chain.  }
VAR oboxNScrl, oboxNPane, oboxPane, oboxLstSc: TC;
    pboxNScrl, pboxNPane, pboxOScrl, pboxOPane: TPbox;
    rNBox: TR; evDsc: TEvDsc;
    tyvh: TTyvh;  dcdSub, dcdOldWorH, dcdMin: TCd;
    window: TWindow;
BEGIN
    {$IFC sampcalls}  ALogCall;  {$ENDC}
    {$ifc fDbgOk}
    IF fTstBox THEN BEGIN
        WRITELN('SynSplit: oboxScrl: ', oboxScrl, ', cdCoord: ', cdCoord);
        PxScrollChain(oboxScrl);
    END;
    {$endc}
    pboxOScrl := POINTER(ORD(bdsp) + oboxScrl);
    tyvh := pboxOScrl^.tyvh;
    window := pboxOScrl^.window;
     { determine dimensions of original and (to be) created scroll boxes }
    WITH pboxOScrl^.r DO BEGIN
        IF tyvh = tyvhH THEN
             SetRect(rNBox, right, top, right, bottom)
        ELSE SetRect(rNBox, left, bottom, right, bottom);
        dcdOldWorH := botRight.vh[tyvh] - topLeft.vh[tyvh];
        dcdMin := dptPaneMin.vh[tyvh] + 2; { +2 for frame }
        IF dcdOldWorH < 2*dcdMin THEN EXIT(SynSplit); { make sure room to split }
        cdCoord := CMax(cdCoord, dcdMin);
        dcdSub := CMax(dcdOldWorH-cdCoord, dcdMin);  { size of to be created sc }
        botRight.vh[tyvh] := botRight.vh[tyvh] - dcdSub;
    END;
    rNBox.topLeft.vh[tyvh] := rNBox.topLeft.vh[tyvh] - dcdSub;
     { allocate and initialize the new scroll box }
    oboxNScrl := OboxAlloc(rNBox, tyboxScroll, 0, oboxScrl, window);
    pboxNScrl := POINTER(ORD(bdsp) + oboxNScrl);
    pboxOScrl := POINTER(ORD(bdsp) + oboxScrl);
    WITH pboxNScrl^ DO BEGIN
        sicon := pboxOScrl^.sicon;  cPercent := pboxOScrl^.cPercent;
        tyvh := pboxOScrl^.tyvh;    oboxPaneFst := oboxNScrl;
    END;

    BegGlobal;
     { redraw both scroll boxes }
    PaintBox(oboxScrl); PaintBox(oboxNScrl);

    oboxLstSc := oboxNScrl;       { current end of new scroll chain }
    oboxPane := pboxOScrl^.oboxPaneFst;
    WHILE oboxPane <> oboxScrl DO BEGIN
        pboxOPane := POINTER(ORD(bdsp) + oboxPane);
        InsetRect(pboxOPane^.r, -1, -1);
        WITH pboxOPane^.r DO BEGIN
            IF tyvh = tyvhH THEN
                 SetRect(rNBox, right, top, right, bottom)
            ELSE SetRect(rNBox, left, bottom, right, bottom);
            botRight.vh[tyvh] := botRight.vh[tyvh] - dcdSub;
        END;
        rNBox.topLeft.vh[tyvh] := rNBox.topLeft.vh[tyvh] - dcdSub;
        InsetRect(pboxOPane^.r, 1, 1);
        oboxNPane := OboxAlloc(rNBox, tyboxPane, -1, oboxPane, window);
        pboxNPane := POINTER(ORD(bdsp) + oboxNPane);
        pboxOPane := POINTER(ORD(bdsp) + oboxPane);

         { link new pane to end of the new scroll chain }
        LinkToScrollChain(oboxNPane, oboxLstSc, tyvh);
        oboxLstSc := oboxNPane;
         { for orthogonal scroll chain, link after pane split from }
        LinkToScrollChain(oboxNPane, oboxPane, TyvhNOT(tyvh));
        FillPane(oboxPane);  FillPane(oboxNPane);
        oboxPane := pboxOPane^.rgoboxLink[tyvh];
    END;
    EndGlobal;
    evDsc.tyev := tyevScNew;
    evDsc.obox := oboxNil; { disable call to VisObox }
    evDsc.scid := ScidFromObox(oboxNScrl, tyvh);
    evDsc.scidOld := ScidFromObox(oboxScrl, tyvh);
    EnqueueLotus(bdsp^.pq^, @evDsc);

    {$ifc fDbgOk}
    IF fTstBox THEN BEGIN
        WRITELN('After split');
        PxScrollChain(oboxScrl); PxScrollChain(oboxNScrl);
    END;
    {$endc}
END;

{----------------------------------------------------}
PROCEDURE SynUndoSplit (oboxBefore, oboxAfter: TC; fKillBefore: TF) ;
{----------------------------------------------------}
 { undo the split between the scroll chains denoted by the given
   scroll boxes.  fKillBefore determines which scroll chain is killed
   (and its area merged in with the other) }
VAR oboxKill, oboxLive: TC; pboxKill: TPbox; tyvh: TTyvh; dcdAdj: TCd;
BEGIN
    {$IFC sampcalls}  ALogCall;  {$ENDC}
    IF fTstBox THEN WRITELN('SynUndoSplit   oboxBefore: ', oboxBefore,
                      ', oboxAfter: ', oboxAfter, ', fKillBefore? ', fKillBefore);
    IF fKillBefore THEN BEGIN
        oboxKill := oboxBefore;  oboxLive := oboxAfter;
    END ELSE BEGIN
        oboxKill := oboxAfter;   oboxLive := oboxBefore;
    END;
    pboxKill := POINTER(ORD(bdsp) + oboxKill);
    tyvh := pboxKill^.tyvh;
    WITH pboxKill^.r DO dcdAdj := botRight.vh[tyvh] - topLeft.vh[tyvh];
    IF fKillBefore THEN dcdAdj := -dcdAdj;
    KillSc(oboxKill);
     { adjust topLeft if adjusting oboxAfter, botRight if oboxBefore }
    AdjScCorner(oboxLive, dcdAdj, fKillBefore, TRUE {paint/event});
END;


{--------------------------------------------------------------}
PROCEDURE SynResize { (oboxBefore, oboxAfter: TC; dcdAdj: TCd) } ;
{--------------------------------------------------------------}
 { Adjust the split between the scroll chains denoted by the given scroll boxes.
   The split between them is moved by dcdAdj. }
LABEL 99;
VAR pboxBefore, pboxAfter: TPbox;  dcdOldWorH: TCd;
   tyvh: TTyvh; evDsc: TEvDsc;
BEGIN
    {$IFC sampcalls}  ALogCall;  {$ENDC}
    pboxBefore := POINTER(ORD(bdsp) + oboxBefore);
    tyvh := pboxBefore^.tyvh;
    pboxAfter := POINTER(ORD(bdsp) + oboxAfter);
    IF (pboxBefore^.tybox <> tyboxScroll) OR (pboxAfter^.tybox <> tyboxScroll) OR
        (pboxAfter^.tyvh <> pboxBefore^.tyvh) THEN BEGIN
        WRITELN('SynResize: mismatch scroll boxes.  oboxes: ', oboxBefore, oboxAfter,
                ', tyboxes: ', ORD(pboxBefore^.tybox), ORD(pboxAfter^.tybox),
                ', tyvhs: ', ORD(pboxBefore^.tyvh), ORD(pboxAfter^.tyvh));
        Break(360);
    END;
    {$ifc fDbgOk}
    IF fTstBox THEN BEGIN
        WRITELN('SynResize: oboxBefore: ', oboxBefore, ', oboxAfter: ', oboxAfter,
                ', dcdAdj: ', dcdAdj);
        PxScrollChain(oboxBefore); PxScrollChain(oboxAfter);
    END;
    {$endc}
    IF dcdAdj = 0 THEN GOTO 99;
    BegGlobal;

    IF dcdAdj < 0 THEN WITH pboxBefore^.r DO BEGIN
        dcdOldWorH := botRight.vh[tyvh] - topLeft.vh[tyvh];
        IF -dcdAdj >= dcdOldWorH THEN
            SynUndoSplit(oboxBefore, oboxAfter, TRUE {Kill "before" chain})
        ELSE BEGIN
            IF dcdOldWorH + dcdAdj < dptPaneMin.vh[tyvh]+2 THEN { +2 to incl frame }
                dcdAdj := -(dcdOldWorH - (dptPaneMin.vh[tyvh]+2));
            AdjScCorner(oboxBefore, dcdAdj, FALSE {botRight}, TRUE {paint/event});
            AdjScCorner(oboxAfter,  dcdAdj,  TRUE {topLeft},  TRUE {paint/event});
        END
    END ELSE WITH pboxAfter^.r DO BEGIN
       dcdOldWorH := botRight.vh[tyvh] - topLeft.vh[tyvh];
       IF dcdAdj >= dcdOldWorH THEN
           SynUndoSplit(oboxBefore, oboxAfter, FALSE {Kill "after" chain})
       ELSE BEGIN
           IF dcdOldWorH - dcdAdj < dptPaneMin.vh[tyvh]+2 THEN
               dcdAdj := dcdOldWorH - (dptPaneMin.vh[tyvh]+2);
           AdjScCorner(oboxBefore, dcdAdj, FALSE {botRight}, TRUE {paint/event});
           AdjScCorner(oboxAfter,  dcdAdj, TRUE  {topLeft }, TRUE {paint/event});
       END
    END;

    evDsc.tyev := tyevEnd;
    evDsc.obox := oboxNil; { disable call to VisObox }
    EnqueueLotus(bdsp^.pq^, @evDsc);

    EndGlobal;
99:
END;


ÿ