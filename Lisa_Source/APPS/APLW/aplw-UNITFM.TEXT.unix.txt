{$I APLW/COMPFLAGS }                                            {gdf 1/16/84}
{$R-}

UNIT UnitFm;

(* Copyright 1983, 1984, Apple Computer Inc. *)

{ gdf  2/27/84 -- insert trace calls for segmentation  }
{ gdf  1/16/84  -- printing compile flag & include COMPFLAGS added }
{ jrm  6/29/82  --  USES changes: LisaGraf => QuickDraw; moved USES UnitFile }
{ crc  6/02/82  --  Conditional compilation }
{ crc 05/27/82  --  Type defns made public for access by LotusMisc }
{ crc 05/25/82  --  USES PrStd moved }
{ crc 04/06/82  --  integration with new font manager }
{ bas 02/12/82  --  new lisaprint uses }
{ bas 01/13/82  --  USES for wm }
{ bas 12/14/81  --  USES for lisaprint }
{ bas 12/10/81 }
{ edited by dsy 9/15/81 }

INTERFACE

USES
    {$IFC sampcalls}                                               {gdf 2/24/84}
       {$U aplw/tracecalls.obj} tracecalls,
    {$ENDC}
    {$U Obj:UnitStd.Obj }   UnitStd,
    {$U Obj:UnitHz.Obj }    UnitHz,
    {$U LibOS/Syscall.Obj }   Syscall,
    {$U Obj:UnitFile.Obj }  UnitFile,
    {$U Obj:Storage.Obj  }  Storage,
    {$U Obj:QuickDraw.Obj }  QuickDraw,
    {$U Obj:FontMgr.Obj  }  FontMgr,
    {$U Obj:PmDecl       }  PmDecl,
     {$IFC fPrintOld}                                               {gdf 1/16/84}
        {$U Obj:PrStd.Obj }     PrStd,
     {$ELSEC}
        {$U Obj:PrStdInfo.Obj } PrStdInfo,
          (* {$U Obj:PrPublic.Obj }  PrPublic,  unneeded ? *)
     {$ENDC}                                                        {gdf 1/16/84}
    {$U Obj:UnitFmt.Obj }   UnitFmt,
    {$U Obj:UnitCs.Obj }    UnitCs,
    {$U Obj:UnitFf.Obj}     UnitFf,
    {$U Obj:Wm.Events.Obj  } Events,
    {$U Obj:Wm.Folders.Obj } Folders,
    {$U Obj:WmlStd     }    WmlStd,
    {$U Obj:WM.Menus.Obj }  Menus,
    {$U Obj:AlertMgr.Obj }  AlertMgr,
    {$U ApLW/Queue.Obj }     Queue,
    {$U ApLW/UnitBox.Obj }   UnitBox,
    {$U ApLW/UnitRec.Obj }   UnitRec,
    {$U ApLW/UnitOver.Obj }  UnitOver,
    {$U ApLW/UnitTld.Obj }   UnitTld,
    {$U ApLW/UnitPrt.Obj }   UnitPrt,
    {$U ApLW/UnitUs.Obj }    UnitUs,
    {$U ApLW/UnitEd.Obj }    UnitEd;

CONST
     {$IFC NOT fDbgOk} fTstFm = FALSE; {$ENDC}
     cbitShFm = 2;
     mskIprt = 3;
     iprtFst = 0;
     iprtVal = 1;
     iprtLst = 2;
     iprtNil = 3;
     chEsc = 27;      { escape:   means editable blank. }
     chUpArrow = 94;  { up-arrow: means non-editable blank. }

TYPE TLfp = RECORD  { Logical Field Position }
           CASE BOOLEAN OF
            TRUE:  ( lfld, ich: TC );
            FALSE: ( lp: TLp );
            END;

    TFfld = RECORD
            iichFst:        TB;
            icsVal:         TB;
            ointVal:        TC;
            fEditable:      TF;
            END;
    TPffld = ^TFfld;
    TArgffld = ARRAY [0..0] OF TFfld;
    TRgffld = ^TArgffld;

    TCsfm = RECORD
            cs:  TCs;
            icsPlate:       TB;
            iichMac:        TB;
            lfldMac:  TLp;
            argffld:        TArgffld;
            {argich:        TRgc;}
            END;
    TPhcsfm = ^TCsfm;
    THcsfm = ^TPhcsfm;

{$IFC fDbgOk}
VAR
     fTstFm: TF;
{$ENDC}

PROCEDURE PxHcsfm (hcs: THcs);

PROCEDURE SeqLpdFm (lpd: TLpd; VAR achad: TAchad);

FUNCTION IcsFmCreate (icsPlate: TB; lpFstPlate: TLp; hz: THz) : TB;

PROCEDURE FreeFmIcs (icsFm: TB);

FUNCTION FSelFmLpBounds (lp: TLp; ics: TB;
           VAR lpFstSel, lpLimSel, lpFstAtom, lpLimAtom: TLp) : TF;

FUNCTION IcsEdLfld (icsFm: TB; lfld: TC) : TB;

IMPLEMENTATION

{$IFC fSymOK }
{$D+ }
{$ELSEC }
{$D- }
{$ENDC }

{$ifc fDbgOk}
{$S LWDebug}
{--------------------------------}
PROCEDURE PxFfld(VAR ffld: TFfld);
{--------------------------------}
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
WITH ffld DO
  BEGIN
  WRITELN('; iich:', iichFst, '; icsVal:', icsVal, '; ointVal:',  ointVal);
  PxInt(ointVal);
  END;
END;

{$S LWDebug}
{-------------------------}
PROCEDURE PxLfp(lfp: TLfp);
{-------------------------}
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
WITH lfp DO
  WRITELN('lfp: [', lfld,  ich, '] = lp: ', lp);
END;
{$endc}

{------------------------------}
PROCEDURE PxHcsfm {(hcs: THcs) };
{------------------------------}
VAR pcsfm:   TPhcsfm;
   iich:   TC;    rgich: TRgc;
   iffld, iffldMac:  TC;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
{$ifc fDbgOk}
pcsfm := POINTER(ORD(hcs^));
WRITE('PxHcsfm - pcsfm: ', ORD(pcsfm));
WRITELN('; icsPlate:', pcsfm^.icsPlate:3, '; iichMac:', pcsfm^.iichMac:3,
       '; lfldMac:', pcsfm^.lfldMac:3);
iffldMac := pcsfm^.lfldMac DIV 4;
FOR iffld := 0 TO iffldMac - 1 DO
   BEGIN WRITE('  iffld:', iffld:3); PxFfld(pcsfm^.argffld[iffld]); END;
WRITELN;
rgich := @pcsfm^.argffld[iffldMac];
FOR iich := 0 TO pcsfm^.iichMac DO
  WRITELN('  iich: ', iich, ', ich: ', rgich^[iich]);
{$endc}
END;  { PxHcsfm }

{$S LWSearch}
{-----------------------------------------------}
FUNCTION IcsEdLfld { (icsFm: TB; lfld: TC) : TB };
{-----------------------------------------------}
VAR pintVal: TPint; hcsfm: THcsfm; ics: TB;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
hcsfm := POINTER(ORD(rghcs^[icsFm]));
IF lfld >= hcsfm^^.lfldMac THEN
    ics := icsNil
ELSE BEGIN
    pintVal := POINTER(ORD(lldInt) + hcsfm^^.argffld[lfld DIV 4].ointVal);
    ics := pintVal^.ics;
    END;
IcsEdLfld := ics;
IF fTstFm THEN WRITELN('IcsEdLfld(', icsFm:2, ', ', lfld:3, ') = ', ics:2);
END;

{$S LWMiscSearch}
{-----------------------------------------------------}
PROCEDURE SeqLpdFm { ( lpd: TLpd; VAR achad: TAchad ) };
{-----------------------------------------------------}
LABEL   100, 200, 999;
VAR cchSeq: TC;
   dlp, lpSon, lpLimSon:  TLp;       lpdSon: TLpd;
   lfldMac: TC;
   pcsfm:   TPhcsfm;
   iprt:   0..iprtNil;
   iffld, iffldMax:   TC;       ffld:   TFfld;
   iich:   TB;                  rgich:  TRgc;
   icsSon: TB;
   pint:   TPint;
   lfp:  TLfp;
   ichLimPrt: TC;
   tyset: TTyset;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
pcsfm := POINTER(ORD(rghcs^[lpd^.ics]^));
IF pcsfm^.cs.tycs <> tycsFm THEN Break(2);
IF lpd^.ilpd + 1 >= clpdDft THEN Break(17);
lpdSon := arglpd[lpd^.ilpd + 1];
lpd^.lpFstPar := 0;

lfldMac := pcsfm^.lfldMac; iffldMax := lfldMac DIV 4;
lfp.lp := 0; lfp.lfld := lfldMac; lpd^.lpLimPar := lfp.lp;
lfp.lp := lpd^.lpLim;
WHILE TRUE DO
  BEGIN
  tyset := tysetStd;
  IF achad.rgch = NIL
     THEN cchSeq := MAXINT
     ELSE cchSeq := achad.ichLim - achad.ichFst;
  IF lfp.lfld >= lfldMac THEN BEGIN achad.ichLim := achad.ichFst; GOTO 999; END;
  iffld := lfp.lfld DIV 4;  ffld := pcsfm^.argffld[iffld];
  iprt := WAnd(lfp.lfld, mskIprt);
  {$ifc fDbgOk}
  IF fTstFm THEN
     BEGIN WRITE('SeqLpdFm loop: '); PxLfp(lfp); PxFfld(ffld); END;
  {$endc}
  CASE iprt OF
   iprtFst, iprtLst:
             BEGIN  { 'lpSon' refers to the boilerplate }
             iich := ffld.iichFst; IF iprt = iprtLst THEN iich := iich + 1;
             rgich := @pcsfm^.argffld[iffldMax];
             icsSon := pcsfm^.icsPlate;
             lpSon := rgich^[iich] + lfp.ich;
             ichLimPrt := rgich^[iich + 1];
             IF iprt = iprtFst THEN ichLimPrt := ichLimPrt - 1; { escape char. }
             cchSeq := CMin(cchSeq, ichLimPrt - lpSon);
             IF fTstFm THEN
                WRITELN('iich:', iich, '; rgich:', ORD(rgich),
                        '; ich:', rgich^[iich], '; cchSeq:', cchSeq);
             IF cchSeq > 0 THEN GOTO 100;
             END;
   iprtVal:  BEGIN  { 'lpSon' refers to the class underlying the val }
             tyset.fRpe := FALSE;
             icsSon := ffld.icsVal;
             lpSon := lfp.ich;
             lpLimSon := LpMacEd(ffld.icsVal);
             IF lpSon < lpLimSon THEN GOTO 100;
             IF lpLimSon = lpSon THEN
                  IF lpd^.tyset.fRpe THEN
                       BEGIN
                        { must return vanished run for FFindEd to work }
                       lpd^.rce := @lpd^.arce;
                       MoveRgch(@lpd^.arce, @arceStd, cbRceVan);
                       lpd^.arce.ifnt := 2; {ifntp12Tile}
                       lpd^.arce.fVan := TRUE;
                       lpd^.arce.cb := cbRceVan;
                       lpd^.lp := lfp.lp;
                       lfp.lfld := lfp.lfld + 1; lfp.ich := 0;
                       GOTO 200;
                       END;
             END;
   iprtNil:  BEGIN END;
   END;
  lfp.lfld := lfp.lfld + 1; lfp.ich := 0;
  END;
100:
 { sequence through the son ics, either Val (tycsEd) or boilerplate (tycsUs) }
lpd^.lp := lfp.lp;
achad.ichLim := achad.ichFst + cchSeq;
SetLpd(lpdSon, icsSon, lpSon, tyset, achad);
IF (iprt = iprtVal) AND (lpLimSon <= lpdSon^.lpLim) THEN
  BEGIN      {truncate returned lpd to just this iprtVal's part}
  achad.ichLim := achad.ichLim - (lpdSon^.lpLim - lpLimSon);
  lfp.lfld := lfp.lfld + 1; lfp.ich := 0;
  END
ELSE          {not finished with the val, figure cch}
  lfp.ich := lfp.ich + (lpdSon^.lpLim - lpSon);
lpd^.rce := lpdSon^.rce;
200:
lpd^.lpLim := lfp.lp;
lpd^.lpSon := lpSon;
lpd^.icsSon := icsSon;
lpd^.rpe := lpdSon^.rpe;
999:
END;  { SeqLpdFm }



{---------------------------------}
PROCEDURE FreeFmIcs { (icsFm: TB) };
{---------------------------------}
VAR lfld: TC; pffld: TPffld; pintVal: TPint;
   hcsfm: THcsfm; rgh: TRgh; icsVal: TB;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
IF fTstFm THEN WRITELN('FreeFmIcs:', icsFm:3);
hcsfm := POINTER(ORD(rghcs^[icsFm]));
IF ORD(hcsfm) = ORD(hNil) THEN Break(72);
{$ifc fDbgOk}
IF fTstFm THEN PxCs(icsFm);
{$endc}
lfld := 0;
WHILE lfld < hcsfm^^.lfldMac DO
    BEGIN
    IF fTstFm THEN WRITELN('Freeing lfld', lfld:4);
    pffld := @hcsfm^^.argffld[lfld DIV 4];
    pintVal := POINTER(ORD(lldInt) + pffld^.ointVal);
    icsVal := pintVal^.ics;
    FreeInt(icsFm, pintVal^.ointParent);
    FreeInt(icsVal, pffld^.ointVal);
     { THIS TEST GOES OUT AFTER tycsEd IS CONVERTED }
    IF icsVal <> icsFm THEN FreeIcs(icsVal);
    lfld := lfld + 4;
    END;
IF rghcs^[icsFm]^^.ointFst <> ointNil THEN Break(70);
FreeIcs(hcsfm^^.icsPlate);
FreeBk(rghcs^[icsFm]^^.hz, POINTER(ORD(hcsfm)), tybkStd);
rgh := POINTER(ORD(rghcs));
rgh^[icsFm] := hNil;
IF fTstFm THEN WRITELN('...icsFm', icsFm:3, ' freed.');
END;

{$S LWMiscSearch}
{-----------------------------------------------------------------------------}
FUNCTION FSelFmLpBounds { (lp: TLp; ics: TB;
           VAR lpFstSel, lpLimSel, lpFstAtom, lpLimAtom: TLp) : TF};
{-----------------------------------------------------------------------------}
 { sets lpFstSel, lpLimSel to be the lps of "outside" selectable boundaries }
 { around lp. Returns TRUE if lp is selectable. }
 { if lp is selectable lpFstAtom, lpLimAtom are the "inside" selectable boundaries }
 { that is - the selection may not get smaller than [lpFstAtom..lpLimAtom). }
LABEL 43, 57;
VAR lfp: TLfp;
   hcsfm: THcsfm;
   lfldFst, lfldLim: TC;
   fSelectable: TF;
   lpDummy, lpTest: TLp;
FUNCTION FSelLfldBounds(lfldFst: TC; VAR lpFstLfld, lpLimLfld: TLp) : TF;
    VAR lfp: TLfp;
        lfldLim: TC;
        ichFst, ichLim: TC;
    FUNCTION LpMacFmLfld(icsFm: TB; lfld: TC) : TLp;
         BEGIN
         LpMacFmLfld := LpMacEd(IcsEdLfld(icsFm, lfld));
         END;
    BEGIN
    {$IFC sampcalls}  ALogCall;  {$ENDC}
    FSelLfldBounds := FALSE;
    ichFst := 0;
    ichLim := 0;
    CASE WAnd(lfldFst, mskIprt) OF
     iprtFst:  BEGIN
               lfldLim := lfldFst + 1;
               END;
     iprtVal:  BEGIN
               lfldLim := lfldFst;  ichLim := LpMacFmLfld(ics, lfldFst);
               FSelLfldBounds := hcsfm^^.argffld[lfldFst DIV 4].fEditable;
               END;
     iprtLst:  BEGIN
               lfldLim := lfldFst + 1;
               lfldFst := lfldFst - 1;
               ichFst := LpMacFmLfld(ics, lfldFst);
               END;
     iprtNil:  BEGIN
               lfldLim := lfldFst + 1;
               END;
     END;
    lfp.lfld := lfldFst;  lfp.ich := ichFst;  lpFstLfld := lfp.lp;
    lfp.lfld := lfldLim;  lfp.ich := ichLim;  lpLimLfld := lfp.lp;
    END;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
IF fTstFm THEN WRITE('FSelLpBounds(', lp, ')');
hcsfm := POINTER(ORD(rghcs^[ics]));
lfp.lp := lp;
lfldFst := lfp.lfld;
lfldLim := lfldFst;
fSelectable := FSelLfldBounds(lfldFst, lpFstSel, lpLimSel);
WHILE lfldFst > 0 DO
    BEGIN
    lfldFst := lfldFst - 1;
    IF FSelLfldBounds(lfldFst, lpTest, lpDummy) = fSelectable
         THEN lpFstSel := lpTest
         ELSE GOTO 43;
    END;
43:
lfldLim := lfldLim + 1;
WHILE lfldLim < hcsfm^^.lfldMac DO
    BEGIN
    IF FSelLfldBounds(lfldLim, lpDummy, lpTest) = fSelectable
         THEN lpLimSel := lpTest
         ELSE GOTO 57;
    lfldLim := lfldLim + 1;
    END;
57:
IF fSelectable THEN
     fSelectable := TrueStdSelLpBounds(lp, ics, lpDummy, lpDummy,
                                       lpFstAtom, lpLimAtom);
IF fTstFm THEN WRITELN('...returns ', fSelectable, ' on ',
                       lpFstSel, ' through ', lpLimSel, '(lpFstAtom: ',
                       lpFstAtom, ', lpLimAtom: ', lpLimAtom, ')');
FSelFmLpBounds := fSelectable;
END;

{$S LWCreate}
{------------------------------------------------------}
FUNCTION IcsFmCreate { ( icsPlate: TB; lpFstPlate: TLp; hz: THz) : TB };
{------------------------------------------------------}
VAR icsFm: TB;
   lfld, ichLim, lfldMacT:  TC;
   lfp, lfpLim: TLfp;
   ointVal, ointP: TC;
   lpMacBlank:  TLp;
   icsVal: TB;                  csFm, csVal: TCs;
   achad:  TAchad;
   argch:  ARRAY[0..10] OF TCh; rgch:  TRgch;
   ich: TC;                     ch:     TCh;
   hcsfm:   THcsfm;             cbCsfm:  TC;
   rgich:  TRgc;                iich:   TC;
   iffldMax, iffld:  TC;        pffld:  TPffld;
   fOintFst: TF;
   pglp: TPglp;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
IF fTstFm THEN WRITELN('IcsFmCreate');
achad.ichFst := 0; achad.ichLim := 10; achad.rgch := @argch;
SetLpd(lpdStd, icsPlate, lpFstPlate, tysetStd, achad);
lfldMacT := 0;
WHILE achad.ichFst <> achad.ichLim DO
    BEGIN
    FOR ich := achad.ichFst TO achad.ichLim - 1 DO
         BEGIN
         ch := achad.rgch^[ich];
         CASE WAnd(lfldMacT, mskIprt) OF
          iprtFst:  IF (ch = chEsc) OR (ch = chUpArrow) THEN
                         lfldMacT := lfldMacT + 2;
          iprtLst:  IF (ch = chTab) OR (ch = chCr) THEN lfldMacT := lfldMacT + 2;
          END;
         END;
    achad.ichFst := 0; achad.ichLim := 10;
    SeqLpd(lpdStd, achad);
    END;
IF fTstFm THEN WRITELN('lfldMacT: ', lfldMacT);
IF WAnd(lfldMacT, mskIprt) <> iprtFst THEN lfldMacT := lfldMacT - 2;
iffldMax := lfldMacT DIV 4;
cbCsfm := SIZEOF(TCsfm) + iffldMax*SIZEOF(TFfld) +
                        ((lfldMacT DIV 2) + 1)*SIZEOF(TC);
icsFm := IcsCreate(tycsFm, cbCsfm, hz);
WITH rghcs^[icsFm]^^.cspd DO
    BEGIN
    argproc[iprocSeqLpd]       := @SeqLpdFm;
    argproc[iprocFindLpFixed]  := @FindLpFstPar;
    argproc[iprocCrd]          := @GenCrdTld;
    argproc[iprocFreeIcs]      := @FreeFmIcs;
    argproc[iprocFSelLpBounds] := @FSelFmLpBounds;
    argproc[iprocPxHcs]        := @PxHcsfm;
    argproc[iprocHilight]      := @HilightTld;
    argimgp[iimgpScrn]         := @GenImgTld;
    argimgp[iimgpQume]         := @PrtImgTld;
    END;
hcsfm := POINTER(ORD(rghcs^[icsFm]));
hcsfm^^.icsPlate := icsPlate;
WITH hcsfm^^ DO
    BEGIN
    lfldMac := lfldMacT;
    iichMac := lfldMacT DIV 2;
    END;
achad.ichLim := 1;
rgich := @hcsfm^^.argffld[iffldMax];
rgich^[0] := lpFstPlate;  iich := 0;
achad.ichFst := 0; achad.ichLim := 10;
SetLpd(lpdStd, icsPlate, lpFstPlate, tysetStd, achad);
fOintFst := TRUE;
lfld := 0;
WHILE lfld < lfldMacT DO
    BEGIN
    rgich := @hcsfm^^.argffld[iffldMax];
    FOR ich := achad.ichFst TO achad.ichLim - 1 DO
         BEGIN
         ch := achad.rgch^[ich];
         ichLim := lpdStd^.lp + ich - achad.ichFst + 1;
         CASE WAnd(lfld, mskIprt) OF
          iprtFst:
              IF (ch = chEsc) OR (ch = chUpArrow) THEN
                   BEGIN
                   IF fTstFm THEN WRITELN('Escape.');
                    { create an icsEd for this blank }
                   icsVal := IcsEdCreate(4, pglpStd, FALSE, hz);
                    { create the ointParent and the ointVal }
                   lfp.lfld := lfld + 1; lfp.ich := 0;
                   lfpLim.lfld := lfp.lfld + 1; lfpLim.ich := 0;
                   ointP := OintMark(ointNil, icsFm, lfp.lp, lfpLim.lp, TRUE,
                   ointNil);
                    { [-1, MAXINT] is so its parent ALWAYS gets invalidated when }
                    { ANY shenanigans with the blank should invalidate it. }
                   lpMacBlank := MAXINT - 1;
                   ointVal := OintMark(ointNil, icsVal, 0, lpMacBlank, TRUE,
                   ointP);
                    { set up the fields for this blank }
                   iffld := lfld DIV 4;
                   pffld := @hcsfm^^.argffld[iffld];
                   pffld^.iichFst := iich;
                   pffld^.icsVal := icsVal;
                   pffld^.ointVal := ointVal;
                   pffld^.fEditable := (ch = chEsc);
                   IF fOintFst THEN
                      BEGIN rghcs^[icsFm]^^.ointFst := ointP; fOintFst := FALSE;
                      END;
                    { Skip the escape char. }
                   rgich := @hcsfm^^.argffld[iffldMax];
                   iich := iich + 1;  rgich^[iich] := ichLim;
                   {$ifc fDbgOk}
                   IF fTstFm THEN
                      BEGIN
                      WRITELN('iprtFst; rgich[', iich, '] := ', ichLim);
                      PxInt(pffld^.ointVal);
                      END;
                   {$endc}
                   lfld := lfld + 2;
                   END;
          iprtLst:
               IF (ch = chTab) OR (ch = chCr) THEN
                   BEGIN
                   rgich := @hcsfm^^.argffld[iffldMax];
                   iich := iich + 1;  rgich^[iich] := ichLim;
                   IF fTstFm THEN WRITELN('iprtLst; rgich[', iich, '] := ',
                   ichLim);
                   lfld := lfld + 2;
                   END;
          END;
         END;
    achad.ichFst := 0; achad.ichLim := 10;
    SeqLpd(lpdStd, achad);
    END;
{$ifc fDbgOk}
IF fTstFm THEN PxHcsfm(POINTER(ORD(hcsfm)));
{$endc}
IcsFmCreate := icsFm;
END;

END.

ÿ