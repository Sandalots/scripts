{$I APLW/COMPFLAGS }                                            {gdf 1/16/84}
{$R-}

UNIT UnitEd;

(* Copyright 1983, 1984, Apple Computer Inc. *)

 { gdf  2/27/84 -- insert trace calls for segmentation  }
 { gdf  1/16/84  -- printing compile flag & include COMPFLAGS added }
 { jrm  9/10/82  --  wrote LpLastCr }
 { jrm  6/29/82  --  USES changes: LisaGraf => QuickDraw; moved USES UnitFile }
 { crc  6/18.82  --  Added icsOrig field to TCsed }
 { crc  6/02/82  --  Conditional compilation }
 { crc 05/25/82  --  USES PrStd moved }
 { crc 04/06/82  --  integration with new font manager }
 { bas 01/13/82  --  USES for new wm }
 { bas 12/14/81  --  USES for lisaprint }
 { bas 12/10/81  --  integration with font manager }
 { edit -- SeqLpdEd does LpMinP with dlpNxtSs. }

INTERFACE

USES
    {$IFC sampcalls}                                               {gdf 2/24/84}
       {$U aplw/tracecalls.obj} tracecalls,
    {$ENDC}
    {$U Obj:UnitStd.Obj   } UnitStd,
    {$U Obj:UnitHz.Obj    } UnitHz,
    {$U LibOS/Syscall.Obj }   Syscall,
    {$U Obj:UnitFile.Obj  } UnitFile,
    {$U Obj:Storage.Obj   } Storage,
    {$U Obj:QuickDraw.Obj } QuickDraw,
    {$U Obj:FontMgr.Obj   } FontMgr,
    {$U Obj:PmDecl        } PmDecl,
     {$IFC fPrintOld}                                               {gdf 1/16/84}
        {$U Obj:PrStd.Obj }     PrStd,
     {$ELSEC}
        {$U Obj:PrStdInfo.Obj } PrStdInfo,
          (* {$U Obj:PrPublic.Obj }  PrPublic,  unneeded ? *)
     {$ENDC}                                                        {gdf 1/16/84}
    {$U Obj:UnitFmt.Obj   } UnitFmt,
    {$U Obj:UnitCs.Obj    } UnitCs,
    {$U Obj:UnitFf.Obj    } UnitFf,
    {$U Obj:Wm.Events.Obj  } Events,
    {$U Obj:Wm.Folders.Obj } Folders,
    {$U Obj:WmlStd     }    WmlStd,
    {$U Obj:WM.Menus.Obj  } Menus,
    {$U Obj:AlertMgr.Obj  } AlertMgr,
    {$U ApLW/Queue.Obj     } Queue,
    {$U ApLW/UnitBox.Obj   } UnitBox,
    {$U ApLW/UnitRec.Obj   } UnitRec,
    {$U ApLW/UnitOver.Obj  } UnitOver,
    {$U ApLW/UnitTld.Obj   } UnitTld,
    {$U ApLW/UnitPrt.Obj   } UnitPrt;

CONST
     {$IFC NOT fDbgOk} fTstEd = FALSE; {$ENDC}
     dlpFstNil = -1;
     dlpLstNil =  1;

TYPE TSs = RECORD             { Slug Source }
            lp:           TLp;
            lpSon:        TLp;
            hfcc:         THfcc;
            dlpFstParSt:  TC;
            dlpLstParSt:  TC;
            icsSon:       TB;
            fVan:         TF;
            END;
    TArgss = ARRAY [0..0] OF TSs;
    TRgss = ^TArgss;
    TPss = ^Tss;

    TPs = ( psFst, psLst, psMid, psNil );

    TCsed = RECORD        { document descriptor }
            cs:      TCs;
            pglp:    TPglp;
            fNeedCr: TF;
            tyrlu:   TTyRlU;
            icsOrig: TC;
            issMax:  TC;
            issMac:  TC;
            argss:   TArgss;
            END;
    TPcsed = ^TCsed;
    THcsed = ^TPcsed;

{$IFC fDbgOk}
VAR
     fTstEd: TF;
{$ENDC}

 { Stream procedures. }
FUNCTION IcsEdCreate (issMax: TC; pglp: TPglp; fNeedCr: TF; hz: THz) : TB;
PROCEDURE SeqLpdEd (lpd: TLpd; VAR achad: TAchad);
PROCEDURE FreeEdIcs (ics: TB);
PROCEDURE PxHcsed (hcs: THcs);

 { Utilities. }
PROCEDURE ChangeIssMax(ics: TB; issMaxNew: TC);
{$ifc fDbgOk}
PROCEDURE PxPss(pss: TPss);
{$endc}
FUNCTION LpMacEd (icsEd: TB) : TLp;
FUNCTION IssFromLp (pcsed: TPcsed; lp: TLp) : TC;
FUNCTION PsOfLpInPss(lp: TLp; pss: TPss; lpNxtSs: TLp) : TPs;
FUNCTION FFindEd (VAR ics: TB; VAR lpFst, lpLim: TLp): TF;
PROCEDURE SetIssDlps(hcsed: THcsed; iss: TC);
FUNCTION LpLastCr(ics: TB): TLp;

IMPLEMENTATION

{$IFC fSymOK }
{$D+ }
{$ELSEC }
{$D- }
{$ENDC }

CONST cbFbcInit = 6;

{$ifc fDbgOk}
{$S LWDebug}
{-----------------------------}
PROCEDURE PxPss { (pss: TPss) };
{-----------------------------}
BEGIN
WITH pss^ DO
    BEGIN
    {$IFC sampcalls}  ALogCall;  {$ENDC}
    WRITELN('  lp:', lp:4, '; fVan:', ORD(fVan):2, '; icsSon:', icsSon:3,
            '; lpSon: ', lpSon);
    WRITE('               dlpFstParSt: ', dlpFstParSt:4, '; -Lst: ',
    dlpLstParSt:4);
    IF hfcc <> NIL
         THEN WRITELN('; hfcc: ', ORD(hfcc), ' ; cref:', hfcc^^.cref:4)
         ELSE WRITELN;
    END;
END;
{$endc}

{--------------------------------}
PROCEDURE PxHcsed { (hcs: THcs ) };
{--------------------------------}
VAR pcsed:  TPcsed;
   rgss:   TRgss;
   iss, issMac: TC;
   pss:    TPss;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
{$ifc fDbgOk}
pcsed := POINTER(ORD(hcs^));
issMac := pcsed^.issMac;
WITH pcsed^ DO
    WRITELN('hcsed: ', ORD(hcs), '; pcsed: ', ORD(pcsed), '; issMax:',
              issMax:4, '; issMac:', issMac:4, '; fNeedCr: ', fNeedCr);
rgss := @pcsed^.argss;
iss := 0;
WHILE iss < issMac DO
    BEGIN
    pss := @rgss^[iss];
    WRITELN(' iss:', iss:4);
    PxPss(pss);
    iss := iss + 1;
    END;
IF (issMac > 0) AND (pcsed^.fNeedCr) THEN
    BEGIN
    IF pss^.dlpLstParSt <> 0 THEN Break(93);
    IF pss^.dlpFstParSt = dlpFstNil THEN Break(95);
    END;
WRITELN(' iss:', iss:4, rgss^[iss].lp);
{$endc}
END;

 {$S SegTld }

{$S LWType}
{------------------------------------}
FUNCTION LpMacEd { (icsEd: TB) : TLp };
{------------------------------------}
VAR hcsed: THcsed;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
hcsed := POINTER(ORD(rghcs^[icsEd]));
LpMacEd := hcsed^^.argss[hcsed^^.issMac].lp;
END;

{$S LWCom2}
{-----------------------------------------------}
PROCEDURE SetIssDlps { (hcsed: THcsed; iss: TC) };
{-----------------------------------------------}
LABEL 999;
VAR rgss: TRgss;
   icsSon: TB;
   lpSonSs, lpNxtSon: TLp;
   achad: TAchad;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
IF fTstEd THEN WRITELN('SetIssDlps -- iss:', iss:3);
IF (iss >= hcsed^^.issMac) OR (iss < 0) THEN GOTO 999;
rgss := @hcsed^^.argss;
IF rgss^[iss].fVan THEN
    BEGIN
    IF fTstEd THEN WRITELN('              vanished ss: all nil.');
    rgss^[iss].dlpFstParSt := dlpFstNil;
    rgss^[iss].dlpLstParSt := dlpLstNil;
    END
ELSE BEGIN
    icsSon := rgss^[iss].icsSon;
    lpSonSs := rgss^[iss].lpSon;
    lpNxtSon := (rgss^[iss + 1].lp - rgss^[iss].lp) + lpSonSs;
    achad.rgch := NIL;
    SetLpd(lpdStd, icsSon, lpSonSs, tysetBnds, achad);
    rgss := @hcsed^^.argss;
    IF lpdStd^.lpLimPar > lpNxtSon THEN
         BEGIN
         rgss^[iss].dlpFstParSt := dlpFstNil;
         rgss^[iss].dlpLstParSt := dlpLstNil;
         END
    ELSE BEGIN
         rgss^[iss].dlpFstParSt := lpdStd^.lpLimPar - lpSonSs;
         IF lpdStd^.lpLimPar = lpNxtSon THEN
              BEGIN
              IF hcsed^^.fNeedCr AND ((iss+1) = hcsed^^.issMac)THEN
                   rgss^[iss].dlpLstParSt := 0
              ELSE BEGIN
                   achad.rgch := NIL;
                   SetLpd(lpdStd, icsSon, lpNxtSon - 1, tysetBnds, achad);
                   rgss := @hcsed^^.argss;
                   {This test is to deal with files with no true
                    paragraphs in them; i.e. no <Cr>'s.
                    we will have problems here if we ever relax the definition
                    of paragraph}
                   IF lpdStd^.rce^.fVan THEN
                        BEGIN
                        rgss^[iss].dlpFstParSt := dlpFstNil;
                        rgss^[iss].dlpLstParSt := dlpLstNil;
                        END
                   ELSE IF (achad.rgch^[achad.ichFst] <> chCr) THEN
                        BEGIN
                        rgss^[iss].dlpFstParSt := dlpFstNil;
                        rgss^[iss].dlpLstParSt := dlpLstNil;
                        END
                   ELSE rgss^[iss].dlpLstParSt := 0;
                   END;
              END
         ELSE BEGIN
              achad.rgch := NIL;
              SetLpd(lpdStd, icsSon, lpNxtSon - 1, tysetBnds, achad);
              rgss := @hcsed^^.argss;
              IF lpdStd^.lpLimPar = lpNxtSon
                   THEN rgss^[iss].dlpLstParSt := 0
                   ELSE rgss^[iss].dlpLstParSt := lpdStd^.lpFstPar - lpNxtSon;
              END;
         END;
    END;
IF fTstEd THEN
    WITH rgss^[iss] DO
         WRITELN('              dlpFstParSt:', dlpFstParSt:5,
                             '; dlpLstParSt:', dlpLstParSt:5);
999:
END;

{$S LWMenu}
{---------------------------------------------------------------}
FUNCTION PsOfLpInPss { (lp: TLp; pss: TPss; lpNxtSs: TLp) : TPs };
{---------------------------------------------------------------}
VAR ps: TPs;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
{$IFC fDbgOk}
IF lp < pss^.lp THEN BEGIN fTstEd := TRUE; Break(99); END;
IF lp >= lpNxtSs THEN BEGIN fTstEd := TRUE; Break(96); END;
IF fTstEd THEN
    BEGIN
    WRITELN('PsOfLpInPss -- lp: ', lp, '; lpNxtSs: ', lpNxtSs, '; pss:');
    PxPss(pss);
    END;
{$endc}
IF pss^.dlpFstParSt = dlpFstNil THEN
    ps := psNil
ELSE IF lp < pss^.lp + pss^.dlpFstParSt THEN
    ps := psFst
ELSE IF lp >= lpNxtSs + pss^.dlpLstParSt THEN
    ps := psLst
ELSE ps := psMid;
PsOfLpInPss := ps;
IF fTstEd THEN
    WRITELN('               ps:', ORD(ps):2);
END;

{$S LWDialog}
{-------------------------------------------------}
PROCEDURE ChangeIssMax { (ics: TB; issMaxNew: TC) };
{-------------------------------------------------}
VAR hcsed:    THcsed;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
hcsed := POINTER(ORD(rghcs^[ics]));
ChangeSizeBk(hcsed^^.cs.hz, POINTER(ORD(hcsed)),
            SIZEOF(TCsed) + (issMaxNew + 1) * SIZEOF(TSs));
hcsed^^.issMax := issMaxNew;
END;

{$S LWMenu}
{--------------------------------------------------}
FUNCTION IssFromLp { (pcsed: TPcsed; lp: TLp) : TC };
{--------------------------------------------------}
LABEL   150;
VAR issFst, issMid, issLim: TC;
   rgss:   TRgss;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
issFst := 0;
issLim := pcsed^.issMac;
rgss := @pcsed^.argss;
WHILE TRUE DO
       BEGIN
       IF issLim - issFst < 2 THEN GOTO 150;
       issMid := (issFst + issLim) DIV 2;
       IF lp < rgss^[issMid].lp
            THEN issLim := issMid
            ELSE issFst := issMid;
       END;
150:
IF lp < rgss^[issLim].lp THEN IssFromLp := issFst ELSE IssFromLp := issLim;
END;

{$S LWMenu}
{---------------------------------------------------}
PROCEDURE SeqLpdEd { (lpd: TLpd; VAR achad: TAchad) };
{---------------------------------------------------}
LABEL 31, 41, 140;
VAR hcsed:    THcsed;
    pcsed:    TPcsed;
    tyset:  TTyset;
    tysetT:  TTyset;
    dlpAchad, lp, lpSs, dlpSs, dlpNxtSs: TLp;
    lpSonSs, lpSonPar, lpSon: TLp;
    issPar, iss, issMac:  TC;
    icsSon: TB;
    lpdSon: TLpd;
    ps:     TPs;
    ss:    TSs;
    rgss:   TRgss;
    rgchSrc, rgchDst:  TRgch;
    cbRce:  TC;
    achadSon: TAchad;
    ifbcDummy:      TC;
    rpeT:   TRpe;
    fSetSon: TF;
    hfcc: THfcc;
    lpMacEd: TLp;
{$S LWType}
PROCEDURE SkipToNextSs;
   BEGIN
   {$IFC sampcalls}  ALogCall;  {$ENDC}
   IF fTstEd THEN WRITELN('SkipToNextSs');
   lpd^.rce := @lpd^.arce;
   lpd^.arce.fVan := TRUE;
   lpd^.rce^.cb := cbRceVan;
   lpd^.lpLim := lp + dlpNxtSs;
   achad.ichLim := achad.ichFst + 1;    { Must have 1 char: otherwise, EOS. }
   END;

{$S LWMenu}
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
hcsed := POINTER(ORD(rghcs^[lpd^.ics]));
pcsed := hcsed^;
issMac := pcsed^.issMac;
rgss := @pcsed^.argss;
tyset := lpd^.tyset;

 { Set up lpd's 'lp' parameters; test for EOS. }
lpMacEd := rgss^[issMac].lp;
lp := LpMinP(lpd^.lpLim, lpMacEd);
lpd^.lp := lp;
lpd^.lpLim := lp;
IF lp = lpMacEd THEN
    BEGIN
    achad.ichLim := achad.ichFst;
    lpd^.lpFstPar := lp;
    lpd^.lpLimPar := lp;
    GOTO 140;
    END;

IF fTstEd THEN WRITELN('SeqLpdEd -- ics:', lpd^.ics:3, '; lp: ', lpd^.lpLim);

 { Set up where we are in document. }
iss := IssFromLp(pcsed, lp);
ss := rgss^[iss];
lpSs := ss.lp;
dlpSs := lp - lpSs;
dlpNxtSs := rgss^[iss + 1].lp - lp;

 { We're going to recurse on indirect streams. }
IF lpd^.ilpd + 1 >= clpdDft THEN Break(94);
lpdSon := arglpd[lpd^.ilpd + 1];
fSetSon := FALSE;

 { Set up the location of the son's class. }
IF NOT ss.fVan THEN
    BEGIN
    icsSon := ss.icsSon;
    lpSonSs := ss.lpSon;
    lpSon := lpSonSs + dlpSs;
    achad.ichLim := LpMinP(achad.ichFst + dlpNxtSs, achad.ichLim);
    END;

 { Get the paragraph properties and/or bounds. }
 { Always get the bounds if the props are wanted. }
IF tyset.fRpe OR tyset.fParBnds THEN
    BEGIN
     { Find the beginning of the paragraph for the bounds. }
    ps  := PsOfLpInPss(lp, @ss, lp + dlpNxtSs);
    IF (ps <> psNil) AND (ss.fVan) THEN Break(90);
    issPar := iss;
    CASE ps OF
     psNil,
     psFst: BEGIN
            REPEAT  { In first (or only) section: loop 'til dlpLstParSt. }
                 issPar := issPar - 1;
                 IF issPar < 0 THEN
                      BEGIN
                      lpd^.lpFstPar := 0;
                      issPar := 0;
                      GOTO 41;
                      END;
                 UNTIL rgss^[issPar].dlpFstParSt <> dlpFstNil;
            lpd^.lpFstPar := rgss^[issPar + 1].lp + rgss^[issPar].dlpLstParSt;
     41:
            lpd^.lpLimPar := lpSs + ss.dlpFstParSt;
            issPar := iss;
            END;
     psMid: BEGIN
             { Somewhere in middle. Use indirect cs. }
            tysetT := tyset; tysetT.fParBnds := TRUE;
            SetLpd(lpdSon, icsSon, lpSon, tysetT, achad);
            fSetSon := TRUE;
            lpd^.lpFstPar := lpSs + (lpdSon^.lpFstPar - lpSonSs);
            lpd^.lpLimPar := lpSs + (lpdSon^.lpLimPar - lpSonSs);
            END;
     psLst: { In last section. Par is last par in iss. }
            lpd^.lpFstPar := rgss^[iss + 1].lp + ss.dlpLstParSt;
     END;
    IF fTstEd THEN WRITE('            par bounds: [', lpd^.lpFstPar,', ');

     { Find the end of the paragraph for the bounds and the properties. }
    IF (ss.dlpFstParSt = dlpFstNil) OR (ps = psLst) THEN
         BEGIN     { In last section: loop 'till find fstParSt. }
         issPar := iss;
    31:  issPar := issPar + 1;
         IF issPar = issMac THEN
              BEGIN
              IF pcsed^.fNeedCr THEN Break(97);
              issPar := issPar - 1;
              lpd^.lpLimPar := lpMacEd;
              END
         ELSE WITH hcsed^^.argss[issPar] DO
                   BEGIN
                   IF dlpFstParSt <> dlpFstNil THEN
                        lpd^.lpLimPar := lp + dlpFstParSt
                   ELSE GOTO 31;
                   END;
         END;
    IF fTstEd THEN WRITELN(lpd^.lpLimPar, ']');

     { Get the properties if they're wanted. }
    IF tyset.fRpe THEN
         BEGIN
         IF NOT fSetSon THEN
              BEGIN
              IF iss = issPar THEN
                   BEGIN
                    { Sequence at lp -- the par props get there. }
                   IF ss.fVan THEN
                        BEGIN
                        lpdSon^.rpe := @arpeStd;
                        fSetSon := FALSE;
                        END
                   ELSE BEGIN
                        SetLpd(lpdSon, icsSon, lpSon, tyset, achad);
                        fSetSon := TRUE;
                        END;
                   END
              ELSE BEGIN
                    { Oops, wrong iss. Sequence there. }
                   lpSonPar := rgss^[issPar].lpSon;
                   lpSonPar := (lpd^.lpLimPar - rgss^[issPar].lp) + lpSonPar - 1;
                   achadSon.rgch := NIL;
                   SetLpd(lpdSon, rgss^[issPar].icsSon, lpSonPar,
                                                        tysetRpe, achadSon);
                   fSetSon := FALSE;
                   END;
              END;
         hfcc := hcsed^^.argss[issPar].hfcc;
         IF (hfcc = NIL) AND fSetSon THEN
              lpd^.rpe := lpdSon^.rpe
         ELSE BEGIN
              lpd^.rpe := @lpd^.arpe;
              MoveRgch(@lpd^.arpe, POINTER(ORD(lpdSon^.rpe)), lpdSon^.rpe^.cb);
              IF hfcc <> NIL THEN
                   ifbcDummy := IfbcLstApplyRgfbc(@hfcc^^.argfbc, lpd^.rpe, NIL);
              END;
         END;

    lpd^.fParSt := (lp = lpd^.lpFstPar);
    tyset.fRpe := FALSE;
    tyset.fParBnds := FALSE;
    END;

 { Now process the characters. }
IF ss.fVan THEN
    SkipToNextSs
ELSE BEGIN
    IF NOT fSetSon THEN SetLpd(lpdSon, icsSon, lpSon, tyset, achad);
    IF lpdSon^.lp = lpdSon^.lpLim THEN
         SkipToNextSs
    ELSE BEGIN
         IF ss.hfcc = NIL THEN
              lpd^.rce := lpdSon^.rce
         ELSE BEGIN
              lpd^.rce := @lpd^.arce;
              lpd^.arce := lpdSon^.rce^;
              ifbcDummy := IfbcLstApplyRgfbc(@ss.hfcc^^.argfbc, NIL, lpd^.rce);
              END;
         dlpAchad := LpMinP(dlpNxtSs, achad.ichLim - achad.ichFst);
         achad.ichLim := achad.ichFst + dlpAchad;
         lpd^.lpLim := lp + dlpAchad;
         END;
    END;
140:
IF fTstEd THEN WRITELN('SeqLpdEd end -- lpLim: ', lpd^.lpLim);
END;

{$S LWPut}
{-------------------------------}
PROCEDURE FreeEdIcs { (ics: TB) };
{-------------------------------}
VAR hcsed: THcsed;
   iss, issMac: TC; rgss: TRgss;
   hfcc: THfcc;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
{$ifc fDbgOk}
IF fTstEd THEN BEGIN WRITELN('FreeEdIcs:', ics:3); PxCs(ics); END;
{$endc}
hcsed := POINTER(ORD(rghcs^[ics]));
IF ORD(hcsed) = ORD(hNil) THEN Break(92);
issMac := hcsed^^.issMac;
rgss := @hcsed^^.argss;
 { Free all the hfcc's who have no more referents. }
FOR iss := 0 TO issMac - 1 DO
    BEGIN
    IF rgss^[iss].hfcc <> NIL THEN
         BEGIN
         hfcc := rgss^[iss].hfcc;
         (* d *) IF hfcc^^.cref <> -1 THEN
         hfcc^^.cref := hfcc^^.cref - 1;
         IF hfcc^^.cref = 0 THEN
              BEGIN
              FreeBk(hcsed^^.cs.hz, POINTER(ORD(hfcc)), tybkStd);
              rgss := @hcsed^^.argss;
              END;
         END;
    END;
IF hcsed^^.pglp.icsHdr <> icsNil THEN FreeIcs(hcsed^^.pglp.icsHdr);
IF hcsed^^.pglp.icsFtr <> icsNil THEN FreeIcs(hcsed^^.pglp.icsFtr);
FreeBk(hcsed^^.cs.hz, POINTER(ORD(hcsed)), tybkStd);
rghcs^[ics] := POINTER(ORD(hNil));
IF fTstEd THEN WRITELN('...ics', ics:3, ' freed.');
END;


{$S LWSave}
{---------------------------------------------------------------------------}
FUNCTION IcsEdCreate { (issMax: TC; pglp: TPglp; fNeedCr: TF; hz: THz) : TB };
{---------------------------------------------------------------------------}
VAR ics:    TB;
   pcsed:    TPcsed;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
ics := IcsCreate(tycsEd, SIZEOF(TCsed) + (issMax + 1) * SIZEOF(TSs), hz);
WITH rghcs^[ics]^^.cspd DO
    BEGIN
    argproc[iprocSeqLpd]       := @SeqLpdEd;
    argproc[iprocFindLpFixed]  := @FindLpFstPar;
    argproc[iprocCrd]          := @GenCrdTld;
    argproc[iprocFreeIcs]      := @FreeEdIcs;
    argproc[iprocFSelLpBounds] := @TrueStdSelLpBounds;
    argproc[iprocPxHcs]        := @PxHcsed;
    argproc[iprocHilight]      := @HilightTld;
    argimgp[iimgpScrn]         := @GenImgTld;
    argimgp[iimgpQume]         := @PrtImgTld;
    END;
pcsed := POINTER(ORD(rghcs^[ics]^));
pcsed^.fNeedCr := fNeedCr;
pcsed^.pglp := pglp;
pcsed^.issMax := issMax;
pcsed^.issMac := 0;
pcsed^.argss[0].lp := 0;
IcsEdCreate := ics;
END;

{$S LWType}
{-----------------------------------------------------------}
FUNCTION FFindEd { (VAR ics: TB; VAR lpFst, lpLim: TLp): TF };
{-----------------------------------------------------------}
LABEL 999;
VAR achad: TAchad; lpFstT, lpLimT, dlp: TLp; icsT: TB;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
IF fTstEd THEN
    WRITELN('FFindEd begin -- ics:', ics:3, '; lpFst, lpLim: ', lpFst, lpLim);
IF rghcs^[ics]^^.tycs = tycsEd THEN
    FFindEd := TRUE
ELSE BEGIN
    FFindEd := FALSE;
    icsT := ics;
    lpFstT := lpFst;
    dlp := lpLim - lpFst;
    WHILE TRUE DO
         BEGIN
         achad.rgch := NIL;
         SetLpd(lpdStd, icsT, lpFstT, tysetStd, achad);
         icsT := lpdStd^.icsSon;
         IF icsT = icsNil THEN GOTO 999;
         lpFstT := lpdStd^.lpSon;
         IF rghcs^[icsT]^^.tycs = tycsEd THEN
              BEGIN
              ics := icsT;
              lpFst := lpFstT;
              lpLim := lpFst  +  dlp;     { here is the one-to-one dependence }
              IF fTstEd THEN WRITELN('Returning TRUE!');
              FFindEd := TRUE;
              GOTO 999;
              END;
         END;
    END;
999:
IF fTstEd THEN
    WRITELN('FFindEd end   -- ics:', ics:3, '; lpFst, lpLim: ', lpFst, lpLim);
END;

{$S LWSelect}
{-----------------------------------}
FUNCTION LpLastCr { (ics: TB): TLp };
{-----------------------------------}
LABEL 666;
VAR achad: TAchad;
    tyset: TTyset;
    lpLst: TLp;
    hcs: THcs;
BEGIN
      {$IFC sampcalls}  ALogCall;  {$ENDC}
      IF fTstEd THEN WRITELN('LpLastCr... ics = ', ics);
      hcs := POINTER(ORD(rghcs^[ics]));
      IF hcs^^.tycs <> tycsEd THEN Break(1015);
WITH tyset DO BEGIN fRce := TRUE; fRpe := FALSE; fParBnds := TRUE; END;
lpLst := LpMacEd(ics) - 1;
      IF fTstEd THEN WRITELN('LpMacEd - 1 = ', lpLst);
IF lpLst >= 0 THEN WHILE TRUE DO
  BEGIN
  achad.ichFst := 0; achad.ichLim := MAXINT; achad.rgch := NIL;
  SetLpd(lpdStd, ics, lpLst, tysetBnds, achad);
  IF NOT lpdStd^.rce^.fVan THEN GOTO 666;
  lpLst := lpdStd^.lpFstPar - 1; (* back up to cr. ending previous paragraph *)
  IF lpLst < 0 THEN Break(1014);
  END
ELSE lpLst := 0;
666: IF fTstEd THEN WRITELN('LpLastCr about to return: ', lpLst);
LpLastCr := lpLst;
END;


END.

ÿ