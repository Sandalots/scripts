;------------------------------------------------------------------------------------
;
;  Copyright 1983, Apple Computer Inc.
;
;       FastGen called from GenImgTld
;       Procedure FastGen (rgoxWdFst {: TRgx;}, @argptInch {: TP;}, tyxyScrn {: TTyxy;},
;                          @MoveTo {: TP;},@NextTre {: TP}, atld {: TAtld;},
;                          rgichWdFst {: TRgc;}, achadTld {: TAchad;},
;                          rgichSepFst {: TRgc;}, @CToSp {: TP;}, @DrawText {: TP;},
;                          mpichdx {: TRgx;}, @DrawString {: TP;},
;                          @GenTabImg {: TP), fReqBrk{: TF})
;
;       Depends on the following:
;
;               declaration of local variables in GenImgTld
;               local/global status of procedures NextTre, GenTabImg
;               type declarations as listed in constants below
;
;       Register assignments:
;
;       D1      =       cchTre
;       D2      =       cchDraw
;       D3      =       cchWd, then cchSep
;       D4      =       ich
;       D5      =       x
;       D6      =       2 * iwd in bottom of register, 2 * (atld.iwdLim - 1) in top
;       D7      =       scratch
;       A1      =       treCur
;       A2      =       rgichWdFst

.seg 'LWCommand'
PARAMSIZE       .EQU    56
rgoxWdFst       .EQU    PARAMSIZE + 8 - 4       ; TRgx
argptInch       .EQU    rgoxWdFst - 4           ; TP
tyxyScrn        .EQU    argptInch - 2           ; TTyxy
PMoveTo         .EQU    tyxyScrn - 4            ; TP
PNextTre        .EQU    PMoveTo - 4             ; TP
atld            .EQU    PNextTre - 4            ; TAtld
rgichWdFst      .EQU    atld - 4                ; TRgc
achadTld        .EQU    rgichWdFst - 4          ; TAchad
rgichSepFst     .EQU    achadTld - 4            ; TRgc
CToSp           .EQU    rgichSepFst - 4         ; TP
DrawText        .EQU    CToSp - 4               ; TP
mpichdx         .EQU    DrawText - 4            ; TRgx
DrawString      .EQU    mpichdx -4              ; TP
GenTabImg       .EQU    DrawString - 4          ; TP
fReqBrk         .EQU    GenTabImg - 2           ; TF
;
;
VARSIZE         .EQU    0
;
;
H               .EQU    2
;
;
; Local variables in GenImgTld: accessed through A3 (old frame pointer)
treCur          .EQU    -4
dyBaseCur       .EQU    treCur - 2
cchTre          .EQU    dyBaseCur - 2
ich             .EQU    cchTre - 2
hLeftArec       .EQU    ich - 2
iwd             .EQU    hLeftArec - 2
tyxy            .EQU    iwd - 1
spIpg           .EQU    tyxy - 83
;
;
; record offsets from various places
;
rgch            .EQU    0                       ; TRgch in TAcrd
iwdLim          .EQU    2                       ; TC in TAtld
tyfill          .EQU    12                      ; TTyfill in TAtre
ipgCur          .EQU    10                      ; TC in TAtre
;
;
; constants
;
ipgNil          .EQU    -1                      ; UnitTld
chIpg           .EQU    31                      ; UnitTld
tyfillNil       .EQU    0                       ; TTyfill in UnitFmt
;
;
; MACRO definitions
;
; x := rgoxWdFst [iwd + (%1 / 2)]
;
        .MACRO  GetX
        MOVE.L  rgoxWdFst (A6), A0
        MOVE    %1 (A0, D6), D5                 ; leave x in D5
        .ENDM
;
;
; only essentials from ConvertTyxy
;
; x := (argptInch[tyxyScrn].h * x + (argptInch[tyxy].h DIV 2)) DIV argptInch[tyxy].h
;
        .MACRO  Convert
        CLR.L   D0                              ; for byte quantity
        MOVE.B  tyxyScrn (A6), D0               ; 4 byte index
        CMP.B   tyxy (A3), D0
        BEQ     %1
        ASL     #2, D0                          ; multiply by four
        MOVE.L  argptInch (A6), A0              ; address of argptInch
        MOVE    H (A0, D0), D0                  ; get offset (word sized)
        MULU    D0, D5                          ; assumes x in D5: argptInch[].h*x
        CLR.L   D0
        MOVE.B  tyxy (A3), D0                   ; 4 byte index
        ASL     #2, D0                          ; multiply by four
        MOVE    H (A0, D0), D0                  ; get offset, assumes 16-bit positive
        MOVE    D0, D7                          ; save argptInch[].h
        ASR.L   #1, D0                          ; quantity DIV 2
        ADD.L   D0, D5                          ; ...+ (argptInch[].h DIV 2)
        DIVS    D7, D5                          ; () DIV argptInch[].h
        .ENDM

;
; MOVETO calls the QuickDraw procedure MoveTo, assuming x is in D5
;
        .MACRO  MOVETO
        MOVEM.L A1-A3/D1-D7, -(SP)
        ADD     hLeftArec (A3), D5              ; MoveTo (x + hLeftArec, dyBaseCur)
        MOVE    D5, -(SP)                       ; push it
        MOVE    dyBaseCur (A3), -(SP)           ; push dyBaseCur
        MOVE.L  PMoveTo (A6), A0                ; get procedure address
        JSR     (A0)
        MOVEM.L (SP)+, A1-A3/D1-D7
        .ENDM

;
; NEXTTRE calls the local procedure NextTre
;
        .MACRO  NEXTTRE
        MOVE    D4, ich (A3)                    ; update ich in GenImg space
        MOVEM.L A1-A3/D1-D7, -(SP)
        MOVE.L  A3, -(SP)                       ; push static link for local procedure
        MOVE.L  PNextTre (A6), A0
        JSR     (A0)
        MOVEM.L (SP)+, A1-A3/D1-D7
        MOVE    ich (A3), D4                    ; restore ich
        MOVE    cchTre (A3), D1                 ; restore cchTre
        MOVE.L  treCur (A3), A1                 ; restore treCur to A1
        .ENDM

; beginning of procedure

        .PROC   FASTGEN


        LINK    A6, #VARSIZE
        MOVEM.L D3-D7/A2-A3, -(SP)              ; save old registers
        MOVE.L  (A6), A3                        ; recover old frame pointer

;
;
; note that for convenience iwd and atld.iwdLim - 1 are doubled (for word indices)
;
SETUP   MOVE.L  atld (A6), A0                   ; get iwdLim, put in top of D6
        MOVE    iwdLim (A0), D6
        SUBQ    #1, D6                          ; D6 := 2 * (iwdLim - 1)
        ADD     D6, D6                          ; note quantity times two
        SWAP    D6                              ; put it in top of register
; bottom of register is iwd--top is iwdLim
        MOVE    #-2, D6                         ; for convenience in the increment
        MOVE.L  rgichWdFst (A6), A2             ; rgichWdFst gets kept in A2
        MOVE    cchTre (A3), D1                 ; cchTre gets kept in D1
        MOVE.L  treCur (A3), A1                 ; and treCur is in A1


; outer for loop
; for iwd := 0 to atld.iwdLim - 1               ; atld.iwdLim - 1 in top of D6
;
FORLP   ADDQ    #2, D6                          ; index for 'for loop'
; note that for first pass D6 = -2
        MOVE    D6, D0                          ; update in mem
        ASR     #1, D0                          ;    div by 2 to get real val
        MOVE    D0, iwd (A3)                    ;    and store back
        SWAP    D6                              ; get iwdLim
        MOVE    D6, D0                          ; put in D0
        SWAP    D6                              ; and restore iwd
; SWAPs actually save time by freeing up a register for achad.rgch
        CMP     D6, D0                          ; termination condition
        BLT     RETURN                          ; stop when iwd > atld.iwdLim

;
; x := rgoxWdFst^[iwd]; ConvertTyxy(x,xOrYDummy,tyxy,tyxyScrn);
; MoveTo(x+hLeftAre,dyKBaseCur);ich := rgichWdFst^[iwd]; cchWd := rgichSepFst^[iwd]-ich
;
        GETX    0                               ; x := rgichWdFst^[iwd]
        CONVERT @99                             ; ConvertTyxy(x,xOrYDummy,tyxy,tyxyScrn)
@99     MOVETO                                  ; MoveTo(x+hLeftArec, dyBaseCur)
        MOVE    0 (A2, D6), D4                  ; put ich in D4
        MOVE.L  rgichSepFst (A6), A0            ; cchWd := rgichSepFst^[iwd]
        MOVE    0 (A0, D6), D3                  ; D3 now equals cchWd
        SUB     D4, D3                          ; ...-ich

;
; WHILE cchWd > 0 DO
;
;
WHILE1  TST     D3                              ; termination for while loop
        BLE     EXIT
;       cchDraw := CMin(cchTre,cchWd);
        MOVE    D1, D2                          ; put cchTre in cchDraw
        CMP     D1, D3                          ; CMin (cchTre, cchWd)
        BGE     @1                              ; if it's less, okay
        MOVE    D3, D2                          ; else put the other in cchDraw
;
;
;       if (treCur^.ipgCur=ipgNil) OR (achadTld.rgch^[ich]<>chIpg THEN
@1      MOVE.L  achadTld (A6), A0
        MOVE.L  rgch (A0), A0                   ; get achadTld.rgch into A0
                                                ; which must be retained until
                                                ; @2 below
        CMP     #ipgNil, ipgCur (A1)            ; if treCur^.ipgCur=ipgNil
        BEQ     @2                              ; DrawText
;                       OR
        CMP.B   #chIpg, 0 (A0, D4)              ; if achadTld.rgch^[ich]<>chIpg
        BNE     @2                              ; DrawText
;
;
;                       ELSE
; CToSp(treCur^.ipgCur+1,spIpg); DrawString(spIpg); cchDraw:=1;
        MOVEM.L A1-A3/D1-D7, -(SP)
        MOVE    ipgCur (A1), -(SP)              ; push ipgCur
        ADDQ    #1, (SP)                        ; and add 1
        MOVE.L  A3, -(SP)                       ; var spIpg (address in GenImg)
        ADD.L   #spIpg, (SP)
        MOVE.L  CToSp (A6), A0
        JSR     (A0)
; since we call two procedures in a row, we don't bother to recover registers yet--
; but we have to push old A6 so that the local can get variables
        MOVE.L  (A6), -(SP)                     ; push A3 (old frame pointer)
        ADD.L   #spIpg, (SP)                    ; var spIpg (address in GenImg)
        MOVE.L  DrawString (A6), A0
        JSR     (A0)
        MOVEM.L (SP)+, A1-A3/D1-D7
        MOVEQ   #1, D2                          ; cchDraw := 1
        BRA     @3
;
; DrawText (POINTER(ORD(achadTld.rgch)),ich,cchDraw)
@2      MOVEM.L A1-A3/D1-D7, -(SP)
        MOVE.L  A0, -(SP)                       ; push POINTER(ORD(achadTld.rgch))
        MOVE    D4, -(SP)                       ; push ich
        MOVE    D2, -(SP)                       ; push cchDraw
        MOVE.L  DrawText (A6), A0
        JSR     (A0)
        MOVEM.L (SP)+, A1-A3/D1-D7
;
; ich := ich + cchDraw; cchWd := cchWd - cchDraw;
@3      ADD     D2, D4                          ; ich := ich + cchDraw
        SUB     D2, D3                          ; cchWd := cchWd - cchDraw
;
;
; if cchTre = cchDraw then NextTre ELSE cchTre := cchTre - cchDraw
        CMP     D1, D2                          ; if cchTre = cchDraw
        BNE     @4                              ; ELSE
        NEXTTRE
        BRA     WHILE1                          ; end of while loop

@4      SUB     D2, D1                          ; cchTre := cchTre - cchDraw
        BRA     WHILE1                          ; end of while loop

;
;
; this is the code between the loop for words and the loop for seps
;
; cchSep := rgichWdFst^[iwd+1]-ich; IF (cchTre>cchSep) AND (treCur^.tyfill=tyfillNil)
; THEN cchTre := cchTre-cchSep ELSE BEGIN
;
EXIT    MOVE    2 (A2, D6), D3
        SUB     D4, D3                          ; cchSep := rgichWdFst^[iwd+1]-ich
        CMP     D3, D1                          ; if cchTre > cchSep
        BLE     WHILE2                          ; (if not then go to sep processor
        CMP.B   #tyfillNil, tyfill (A1)         ; if treCur^.tyfil = tyfillNil
        BNE     WHILE2                          ; (ifnot then go to sep processor)
; this happens if condition not satisfied
;                       THEN
        SUB     D3, D1                          ; cchTre := cchTre - cchSep
        BRA     FORLP                           ; next iteration of outer loop

;
;
; ELSE
; WHILE cchSep > 0 DO
;
WHILE2  TST     D3                              ; termination condition cchSep = 0
        BLE     FORLP                           ; return to main for-loop
;
; cchDraw := CMin(cchTre, cchSep);
        MOVE    D1, D2                          ; cchDraw := cchTre
        CMP     D1, D3                          ; unless cchSep is less
        BGT     @1
        MOVE    D3, D2                          ; in which case cchDraw := cchSep

; if treCur^.tyfill <> tyfillNil THEN GenTabImg(ich,cchDraw)
@1      CMP.B   #tyfillNil, tyfill (A1)         ; if treCur^.tyfill = tyfillNil
        BEQ     @2                              ; then skip

; ELSE  GenTabImg
;
        MOVEM.L A1-A3/D1-D7, -(SP)
        MOVE    D4, -(SP)                       ; push ich
        MOVE    D2, -(SP)                       ; push cchDraw
        MOVE.L  A3, -(SP)                       ; push static link
        MOVE.L  GenTabImg (A6), A0
        JSR     (A0)
        MOVEM.L (SP)+, A1-A3/D1-D7


; ich := ich + cchDraw; cchSep := cchSep - cchDraw;
; if cchTre=cchDraw THEN BEGIN

@2      ADD     D2, D4                          ; ich := ich + cchDraw
        SUB     D2, D3                          ; cchSep := cchSep - cchDraw
        CMP     D1, D2                          ; if cchTre = cchDraw
        BEQ     LONG                            ; if equal, do drawing, else
        SUB     D2, D1                          ; cchTre := cchTre - cchDraw
        BRA     WHILE2                          ; end of while loop

LONG    SWAP    D6                              ; if iwd <> iwdLst OR fReqBrk THEN
        MOVE    D6, D0
        SWAP    D6
        CMP     D6, D0
        BNE     @77
        TST.B   fReqBrk(A6)
        BEQ     @88

; x := rgoxWdFst^[iwd+1];
@77     GETX    2
; FOR ichT := rgichWdFst^[iwd+1]-1 DOWNTO ich DO x := x-mpichdx^[ichT];
        MOVE    2 (A2, D6), D0                  ; ichT := rgichWdFst^[iwd+1]
        SUBQ    #1, D0                          ; ...-1
        ADD     D0, D0                          ; for word index
        ADD     D4, D4                          ; temporary: ich := 2*ich
        MOVE.L  mpichdx (A6), A0                ; put address of mpichdx in A0
; For loop
@1      CMP     D0, D4                          ; branch when we go by
        BGT     @2                              ; end of loop
        SUB     0 (A0, D0), D5                  ; x := x-mpichdx^[ichT]
        SUBQ    #2, D0                          ; downto
        BRA     @1                              ; loop
; end of loop
@2      ASR     #1, D4                          ; restore ich
        CONVERT @99                             ; ConvertTyxy(x,xOrYDummy,tyxy,tyxyScrn)
@99     MOVETO                                  ; MoveTo(x+hLeftArec,dyBaseCur)
@88     NEXTTRE                                 ; NextTre
        BRA     WHILE2                          ; end of while loop





RETURN  MOVEM.L (SP)+, D3-D7/A2-A3              ; restore old registers
        UNLK    A6
        MOVE.L  (SP)+, A0
        ADD     #PARAMSIZE, SP
        JMP     (A0)


;---------------------------------------------
.FUNC   FCallEosLppg,5; (ics: TB; lp: TLp; proc: TProc) : TF
;---------------------------------------------
        MOVE.L  (SP)+,A0
        MOVE.L  (SP),A1        ;proc
        MOVE.L  A0, (SP)       ; restore return address
        JMP     (A1)

        .END
ÿ