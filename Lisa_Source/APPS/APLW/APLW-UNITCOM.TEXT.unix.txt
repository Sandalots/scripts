{$I APLW/COMPFLAGS }                                            {gdf 1/16/84}
{$R-}

UNIT UnitCom;

(* Copyright 1983, 1984, Apple Computer Inc. *)

{ gdf  2/29/84 -- put in Janis' stuff: remove Undo  }
{ gdf  2/27/84 -- insert trace calls for segmentation  }
{ med  2/23/84  --  new combined menus and alerts }
{ gdf  1/16/84  --  printing compile flag & include COMPFLAGS added }
{ tjm  4/11/83  --  RemoveKid(fSaveKid) - (WP 258) }
{ crc  3/21/83  --  DoPopDown: Always check to conform with manuals. Sorry! }
{ tjm  3/11/83  --  DoPopDown: Call VerifyArceKid (WP 245) }
{ crc  2/25/83  --  DoPopDown: Par properties checked if sel within single par }
{ crc  2/11/83  --  DoPopDown: Checks disabled if selection not a point;
                    moved code to set document name to DrawInitialMenu }
{ crc  2/09/83  --  DoPopDown: Commented out test for non-point selection }
{ crc  2/08/83  --  DoFontImid: CautionAlert changed to NoteAlert }
{ crc 11/05/82  --  More rearrangement of menu items; Undo calls SelToRuler }
{ crc 10/25/82  --  Undo manages fScrapWord }
{ jrm 10/15/82  --  added UndoInheritScrap call; moved in imtdPrv, imidPrv }
{ crc 10/12/82  --  Fix to imidOffMargin }
{ crc 10/07/82  --  Rearrange menu items }
{ crc 10/01/82  --  Alert msg 8 --> 9 }
{ jrm  9/17/82  --  changed DoTypeImid to reference imidFNPlain, imidLNPlain}
{ jrm  8/31/82  --  renamed LISA:LOTUSMENU.TEXT }
{ bas  7/20/82  --  no fUpdate parm to FreeDialogObox }
{ jrm  6/29/82  --  USES changes: LisaGraf => QuickDraw; moved USES UnitFile }
{ crc  6/02/82  --  Conditional compilation }
{ crc 06/02/82  --  Deleted parameter My_ID in call of OboxDialogCreate }
{ crc 05/28/82  --  Added more fonts }
{ crc 05/26/82  --  IcsActive & prcsMine moved to EventMgr }
{ crc 05/25/82  --  USES PrStd added; icsPageRuler & icsMarginRuler to EventMgr
}
{ jrm  5/25/82  --  removed lpLimScrap and lpLimUndo references }
{ crc 04/06/82  --  integration with new font manager }
{ crc  3/29/82  --  moved some imid decls into interface for use by UnitLotus.
}
{ jrm  1/29/82  --  integration with new menumgr }
{ bas 01/13/82  --  USES for new wm }
{ bas 12/10/81  --  integration with font manager }

INTERFACE

USES
     {$IFC sampcalls}                                               {gdf 2/24/84}
        {$U aplw/tracecalls.obj} tracecalls,
     {$ENDC}
     {$U Obj:UnitStd.Obj  } UnitStd,
     {$U LibOS/Syscall.Obj }   Syscall,
     {$U Obj:UnitHz.Obj   } UnitHz,
     {$U Obj:UnitFile.Obj } UnitFile,
     {$U Obj:Storage.Obj  } Storage,
     {$U Obj:QuickDraw.Obj } QuickDraw,
     {$U Obj:FontMgr.Obj  } FontMgr,
     {$U Obj:Wm.Events.Obj  } Events,
     {$U Obj:Wm.Folders.Obj } Folders,
     {$U Obj:Wm.Menus.Obj } Menus,
     {$U Obj:WmlStd     }   WmlStd,
     {$U Obj:PmDecl       } PmDecl,
     {$IFC fPrintOld}                                               {gdf 1/16/84}
        {$U Obj:PrStd.Obj }     PrStd,
     {$ELSEC}
        {$U Obj:PrStdInfo.Obj } PrStdInfo,
          (* {$U Obj:PrPublic.Obj }  PrPublic,  unneeded ? *)
     {$ENDC}                                                        {gdf 1/16/84}
     {$U Obj:Filercomm    } Filercomm,
     {$U Obj:UnitFmt.Obj  } UnitFmt,
     {$U Obj:UnitCs.Obj   } UnitCs,
     {$U Obj:UnitFf.Obj   } UnitFf,
     {$U Obj:Scrap        } Scrap,
     {$U Obj:AlertMgr.Obj } AlertMgr,
     {$U ApLW/Queue.Obj    } Queue,
     {$U ApLW/UnitBox.Obj  } UnitBox,
     {$U ApLW/BoxScid.Obj  } BoxScid,
     {$U ApLW/UnitFolder.Obj } UnitFolder,
     {$U ApLW/UnitRec.Obj  } UnitRec,
     {$U ApLW/UnitOver.Obj } UnitOver,
     {$U ApLW/UnitTld.Obj  } UnitTld,
     {$U ApLW/UnitUs.Obj   } UnitUs,
     {$U ApLW/UnitUf.Obj   } UnitUf,
     {$U ApLW/UnitEd.Obj   } UnitEd,
     {$U ApLW/UnitEdit.Obj } UnitEdit,
     {$U ApLW/UnitKid.Obj  } UnitKid,
     {$U ApLW/UnitFm.Obj   } UnitFm,
     {$U ApLW/UnitDsp.Obj  } UnitDsp,
     {$U ApLW/UnitSel.Obj  } UnitSel,
     {$U ApLW/UnitScrap.Obj} UnitScrap,
     {$U ApLW/UnitPg.Obj}    UnitPg,
     {$U ApLW/UnitRuler.Obj} UnitRuler,
     {$U ApLW/EventMgr.Obj } EventMgr,
     {$U ApLW/UnitLDialog.Obj } UnitLDialog;

CONST
    {$IFC NOT fDbgOk} fTstCom = FALSE; {$ENDC}
    {$IFC NOT fDbgOk} fTstCur = FALSE; {$ENDC}
      imtdNil = -1;
   (*
      imtdFst = 2;  Defined in Eventmgr
   *)
   (*
      imtdFile = 2;
         imidFile = 1;
         imidRefile = 2;
         imidPrint = 3;
         imidPull = 4;
         imidUCSDFile = 5;
   *)
      (* see EventMgr for definitions of imtdFile and its imids *)

      imtdEdit = 3;
         imidUndo = 1;
         imidCut = 3;
         imidCopy = 4;
         imidPaste = 5;
         imidSelectAll = 7;
      imtdType = 4;
      imtdPar = 5;
      imtdPage = 6;
         imidPreview = 1;
         imidNoPreview = 2;
         imidPageRuler = 4;
         imidNoPageRuler = 5;
         imidIpg = 7;
         imidFormFeed = 8;
         imidKeep = 10;
         imidNoKeep = 11;
      imtdFind = 7;
         imidSetString = 1;
         imidFind = 2;
         imidChange = 3;
         imidCAndF = 4;
         imidCAll = 5;
         imidOnCase = 7;
         imidOffCase = 8;
         imidWildCard = 10;
         imidClearPrompt = 12;
      imtdSpelling = 8;
         imidFMisspell = 1;
         imidSuggest = 2;
         imidPGuess = 3;
         imidAndFind = 4; {Doesn't appear in menu; Used for "Paste & Find"}
         imidPut = 5;
         imidRmv = 6;
         imidWriteDict = 7;
         imidClrDict = 8;
         imidDbug = 10;
      imtdMargTab = 9;
         imidMEnglish = 1;
         imidMMetric = 2;
         imidTabClear = 4;
         imidLTab = 5;
         imidCTab = 6;
         imidRTab = 7;
         imidDPTab = 8;
         imidDCTab = 9;
         imidSpace = 11;
         imidDots = 12;
         imidDashes = 13;
         imidUL = 14;
      imtdPRuler = 10;
         imidPREnglish = 1;
         imidPRMetric = 2;
      imtdBuzz = 11;
         imidAborted = 1;
         imidNFound = 2;
         imidKeyInput = 3;  {Used in UnitLotus for undo}
(*
      imtdInvisible = 12;  Defined in Eventmgr
         imidGuess = 1;
         imidGNext = 2;
*)
(*
      imtdLst=12;  Defined in Eventmgr
*)
(*         iimtdLabel = 9;  {See imtdLimLotus} *)

VAR
    {$IFC fDbgOk} fTstCom: TF; {$ENDC}
    {$IFC fDbgOk} fTstCur: TF; {$ENDC}
    iimtdLabel: TC;
(*  Moved to UnitLDialog:
    icsPrmt: TB;
    ointPrmt: TC;
*)
    hzCom: THz;

    icsParStyle: TB;

(* Moved to Eventmgr:
    argMenu: ARRAY [imtdScrap..imtdLst] OF MenuInfo;
*)
    (* menuScrap: MenuInfo; *)

    fMatchCase: TF;
    imtdPrv,imidPrv: TC;  {last command, used in undo }

 { command utilities }
PROCEDURE InitCom(hz: THz);
PROCEDURE LotusInitMenu;

 { menu commands }
PROCEDURE DoTypeImid(imid: TC);
PROCEDURE DoParImid(imid: TC; oboxPane: TC);
PROCEDURE DoSpacingImid(fLnSpacing: TF; dmid: TC);
PROCEDURE DoMarginImid(imid: TC);
                (* PROCEDURE DoTabFillImid(imid: TC); *)
PROCEDURE DoFontImid(imid: TC);
PROCEDURE DoPopDown(VAR menu: MenuInfo);
PROCEDURE DisplaySubImtd(imtd: TC);
PROCEDURE DisplayFstSubImtd(imtd: TC);
PROCEDURE DrawInitialMenu(fScrapActive: TF);
FUNCTION FPreviewObox(oboxPane: TC; VAR orecCon: TC; VAR icsCon: TB) : TF;
PROCEDURE RemovePageRuler;
FUNCTION FShowSel(imtd: TC; imid: TC) : TF;

IMPLEMENTATION

{$IFC fSymOK }
{$D+ }
{$ELSEC }
{$D- }
{$ENDC }

CONST (* imtdLimLotus = 9; {See iimtdLabel}   *)
      imidLst = 31;
      cBlkMenu = 2;
      cbRgfbcInit = 10;
       { imtdTypestyle }
           imidPlainText = 1;
           imidUnderline = 2; imidFNPlain = 2 (* first non - plain typestyle *);
           imidBold = 3;
           imidItalic = 4;
           imidSuperscript = 5;
           imidSubscript = 6; imidLNPlain = 6 (* last non - plain typestyle *);

           imidModern = 8;       imidFstTyfam = 8;
           imidClassic = 9;      imidLstTyfam = 9;

           imid8P20   = 11;      imidFstFsp = 11;
           imid8P15   = 12;
           imidTwelveP12  = 13;
           imid12P10 = 14;
           imid12 = 15;
           imid14 = 16;
           imid18 = 17;
           imid24 = 18;         imidLstFsp = 18;  imidTypeLst = 18;

{ The remaining fonts are not available from the standard menu ?}
           imidFBold = 22;
           imidcourier = 19;
           imidelite = 20;
           imidocrB    = 21;
       { imtdParstyle }
           imidLeft = 1;              imidFstQuad = 1;
           imidCenter = 2;
           imidRight = 3;
           imidJustified = 4;         imidLstQuad = 4;
           imidOnMargin = 6;
           imidOffMargin = 7;
           imidParSingle = 9;         imidFstSpacePar = 9;
           imidParOneAndHalf = 10;
           imidParDouble = 11;
           imidParTriple = 12;         imidLstSpacePar = 12;
           imidLnSingle = 14;         imidFstLn = 14;
           imidLnOneAndHalf = 15;
           imidLnDouble = 16;
           imidLnTriple = 17;         imidLstLn = 17;
           imidSyPaste = 19;          imidLstPar = 19;

           dmidSingle = 0;
           dmidOneAndHalf = 1;
           dmidDouble = 2;
           dmidTriple = 3;

           ySingle = 0;
           yOneAndHalf = 6;
           yDouble = 12;
           yTriple = 18;

TYPE
    TSetimid = SET OF 0..imidLst;

VAR dxStdLFst, dxStdLBody, dxStdRt: TX;
    {appears to be defined elsewhere: orecPrmt: TC;}
    StdDrawProc: TProc;
    spCloseMid: TSp;
    argsetimidShowSel: ARRAY [0..imtdLst] OF TSetimid;
    ampimidtyfam: ARRAY [imidFstTyfam..imidLstTyfam] OF TTyfam;
    ampimidfsp: ARRAY [imidFstFsp..imidLstFsp] OF TFsp;

       {-----------------------------------------------------------------}
       {--------------- C O M M A N D   U T I L I T I E S ---------------}
       {-----------------------------------------------------------------}


{$S LWStyle}
{---------------------------------------------------------}
FUNCTION FShowSel{imtd: TC; imid: TC) : TF};
{---------------------------------------------------------}
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
FShowSel := imid IN argsetimidShowSel[imtd];
END;

{$S LWPreview}
{------------------------}
PROCEDURE RemovePageRuler;
{------------------------}
VAR hcspg: THcspg;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
IF icsPageRuler <> icsNil THEN
      BEGIN
      hcspg := POINTER(ORD(rghcs^[IcsPgOfIcsRuler(icsPageRuler)]));
      hcspg^^.pgd.icsPageRuler := icsNil;
      FreeIcs(icsPageRuler);
      icsPageRuler := icsNil;
      END;
END;


{$S LWStyle}
{----------------------------------------}
PROCEDURE DisplaySubImtd { (imtd: TC) };
{----------------------------------------}
VAR iimtd: TC;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
IF fTstCom THEN WRITELN('DisplaySubImtd(', imtd, ')');
FOR iimtd := iimtdLabel TO imtdLst DO
     DeleteMenu(iimtd);
IF imtd <> imtdNil THEN InsertMenu(argMenu[imtd], 0);
DrawMenuBar;
END;

{----------------------------------------}
PROCEDURE DisplayFstSubImtd { (imtd: TC) };
{----------------------------------------}
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
IF fTstCom THEN WRITELN('DisplayFstSubImtd(', imtd, ')');
IF (icsMarginRuler <> icsNil) AND (imtd <> imtdMargin) THEN
     BEGIN
     FreeIcs(icsMarginRuler);
     icsMarginRuler := icsNil;
     END;
IF (icsPageRuler <> icsNil) AND (imtd <> imtdPRuler) THEN
     RemovePageRuler;
DisplaySubImtd(imtd);
END;


{$S LWScroll}
{--------------------------------------------------------------------------}
FUNCTION FPreviewObox{oboxPane: TC; VAR orecCon: TC; VAR icsCon: TB) : TF};
{--------------------------------------------------------------------------}
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
icsCon := IcsConFromPane(oboxPane, orecCon);
FPreviewObox := (rghcs^[icsCon]^^.tycs = tycsPg);
END;


{$S LWCommand}
{------------------------------------------}
PROCEDURE DoPopDown { (VAR menu: MenuInfo) };
{------------------------------------------}
VAR achad: TAchad;
    pint:       TPint;
    quad:       TQuad;
    imid:       TC;
    f: TF;
    orecCon: TC;
    ics: TB;
    fCheck: TF;
    tysetAll: TTyset;
    fstyleCur: TFstyle;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
IF selCur.oint <> ointNil THEN
     BEGIN
     pint := POINTER(ORD(lldInt)+selCur.oint);
     fCheck := TRUE;  {WAS pint^.lpFst = pint^.lpLim;}
     achad.ichFst := 0; achad.ichLim := 1; achad.rgch := NIL;
     WITH tysetAll DO BEGIN fRce := TRUE; fRpe := TRUE; fParBnds := TRUE; END;
     SetLpd(lpdStd, pint^.ics, pint^.lpFst, tysetAll, achad);
     CASE menu.menuId OF
      imtdType:
          BEGIN
          { WITH lpdStd^.rce^ DO
          }
          VerifyArceKid(selCur.oint);
          fCheck := (lpdStd^.rce^.icsFig = icsNil);
          WITH arceKid DO
            BEGIN
            CheckItem(menu, imidBold, fBold AND fCheck);
            CheckItem(menu, imidItalic, fItalic AND fCheck);
            CheckItem(menu, imidUnderline, fUnderline AND fCheck);
            CheckItem(menu, imidSuperscript, (cbSuperscript > 0) AND fCheck);
            CheckItem(menu, imidSubscript, (cbSuperscript < 0) AND fCheck);
            CheckItem(menu, imidPlainText, (cbSuperscript = 0) AND fCheck
                      AND NOT (fBold OR fItalic OR fUnderline));
            fstyleCur := ampifntfstyle[ifnt];
            FOR imid := imidFstTyfam TO imidLstTyfam DO
                 CheckItem(menu, imid,
                             (fstyleCur.tyfam = ampimidtyfam[imid]) AND fCheck);
            FOR imid := imidFstFsp TO imidLstFsp DO
                 CheckItem(menu, imid,
                            (fstyleCur.fsp = ampimidfsp[imid]) AND fCheck);
            END;
          END;
      imtdPar:
          BEGIN
          {fCheck := (lpdStd^.lpFstPar <= pint^.lpFst) AND
                    (lpdStd^.lpLimPar >= pint^.lpLim);}
          WITH lpdStd^.rpe^ DO
            BEGIN
            CheckItem(menu, imidLeft, (quad = quadL) AND fCheck);
            CheckItem(menu, imidCenter, (quad = quadC) AND fCheck);
            CheckItem(menu, imidRight, (quad = quadR) AND fCheck);
            fCheck := (lpdStd^.rce^.icsFig = icsNil);
            CheckItem(menu, imidJustified, (quad = quadJ) AND fCheck);
            CheckItem (menu, imidOnMargin, icsMarginRuler <> icsNil);
            CheckItem (menu, imidOffMargin, icsMarginRuler = icsNil);
            CheckItem(menu, imidParSingle, (yLd = ySingle) AND fCheck);
            CheckItem(menu, imidParOneAndHalf, (yLd = yOneAndHalf) AND fCheck);
            CheckItem(menu, imidParDouble, (yLd = yDouble) AND fCheck);
            CheckItem(menu, imidParTriple, (yLd = yTriple) AND fCheck);
            CheckItem(menu, imidLnSingle, (yLine = ySingle) AND fCheck);
            CheckItem(menu, imidLnOneAndHalf, (yLine = yOneAndHalf) AND fCheck);
            CheckItem(menu, imidLnDouble, (yLine = yDouble) AND fCheck);
            CheckItem(menu, imidLnTriple, (yLine = yTriple) AND fCheck);
            END;
          END;
      imtdFind:
          BEGIN
          CheckItem(argMenu[imtdFind], imidOnCase, fMatchCase);
          CheckItem(argMenu[imtdFind], imidOffCase, NOT fMatchCase);
          END;
      imtdPage:
          BEGIN
          CheckItem(menu, imidKeep, lpdStd^.rce^.fKeep AND fCheck);
          CheckItem(menu, imidNoKeep, (NOT lpdStd^.rce^.fKeep) AND fCheck);
          f := FPreviewObox(
                 OboxFstOnSc(ScidFstFromFid(FidOfWindow(activeFolder), tyvhV)),
                 orecCon, ics);
          IF icsPageRuler = icsNil THEN BEGIN
              EnableItem (menu, imidPageRuler);
              DisableItem (menu, imidNoPageRuler);
          END ELSE BEGIN
              EnableItem (menu, imidNoPageRuler);
              DisableItem (menu, imidPageRuler);
          END;
          CheckItem(menu, imidNoPreview, NOT f);
          CheckItem(menu, imidPreview, f);
          END;
      imtdSpelling:
          BEGIN
          END;
      END;
     END;
END;

{----------------------}
PROCEDURE CallDrawProc (VAR menu: menuInfo; r: TR; proc: TProc); EXTERNAL;
{----------------------}

{----------------------}
PROCEDURE LotusDrawProc(VAR menu: menuInfo; r: TR);
{----------------------}
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
{called by MenuSelect when menu pops down}
IF fTstCom THEN writeln('LotusDrawProc: imtd=', menu.menuId);
DoPopDown(menu);
CallDrawProc(menu, r, StdDrawProc);
END;


{$S LWCreate}
{-----------------------------}
PROCEDURE InitCom { (hz: THz) };
{-----------------------------}
VAR
imid: TC; imtd: TC;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
icsMarginRuler := icsNil;
icsPrmt := icsNil;
orecPrmt := orecNil;
ointPrmt := ointNil;
hzCom := hz;
dxStdLFst := 40; dxStdLBody := 40; dxStdRt := 0;

ampimidtyfam[imidModern] := tyfamModern;
ampimidtyfam[imidClassic] := tyfamClassic;
ampimidfsp[imid8P20] := fsp8P20;
ampimidfsp[imid8P15] := fsp8P15;
ampimidfsp[imid12P10] := fsp12P10;
ampimidfsp[imidTwelveP12] := fsp12P12;
ampimidfsp[imid12] := fsp12;
ampimidfsp[imid14] := fsp14;
ampimidfsp[imid18] := fsp18;
ampimidfsp[imid24] := fsp24;

FOR imtd := 0 TO imtdLst-1 DO argsetimidShowSel[imtd] := [];
argsetimidShowSel[imtdFile] := [imidSavAndCont, imidRevert, imidPrPrf];
FOR imid := imidUndo TO imidPaste DO
     argsetimidShowSel[imtdEdit] := argsetimidShowSel[imtdEdit] + [imid];
argsetimidShowSel[imtdFind] := [imidWildCard];
FOR imid := 0 TO imidTypeLst DO
     argsetimidShowSel[imtdType] := argsetimidShowSel[imtdType] + [imid];
FOR imid := 0 TO imidLstPar DO
     argsetimidShowSel[imtdPar] := argsetimidShowSel[imtdPar] + [imid];
     argsetimidShowSel[imtdPar] := argsetimidShowSel[imtdPar]
                                      - [imidOnMargin, imidOffMargin];
FOR imid := imidFormFeed TO imidNoKeep DO
     argsetimidShowSel[imtdPage] := argsetimidShowSel[imtdPage] + [imid];

argsetimidShowSel[imtdSpelling] :=  [imidFMisspell, (* imidSuggest, *)
                                     imidPut, imidRmv, imidWriteDict];

LotusInitMenu;
END;

{$S LWCommand}
{-----------------------------------------}
PROCEDURE DrawInitialMenu{fScrapActive: TF};
{-----------------------------------------}
VAR iimtd: TC;
    spClose1Mid,
    spClose2Mid,
    spTitle: Str255;
    quotePos: Integer;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
IF fScrapActive THEN
     BEGIN
     InsertMenu(argMenu[imtdScrap], 0);
     END
ELSE BEGIN
     FOR iimtd := imtdFst TO iimtdLabel - 1 DO InsertMenu(argMenu[iimtd], 0);

     { Set document name under File/Print menu }
     GetAlert (WPAlerts, 912, @spClose1Mid);
     GetFldrTitle(activeFolder, spTitle);
     Insert (spTitle, spClose1Mid, length (spClose1Mid) + 1);
     GetAlert (WPAlerts, 913, @spClose2Mid);
     Insert (spClose2Mid, spClose1Mid, length (spClose1Mid) + 1);
     SetItem(argMenu[imtdFile], imidClose, @spClose1Mid);
     END;
DrawMenuBar;
END;


{$S LWCreate}
{----------------------}
PROCEDURE LotusInitMenu;
{----------------------}
VAR iimtd: TC;
    tempStr: Str255;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
StdDrawProc := argMenu[imtdLst].DrawProc;
FOR iimtd := imtdFst TO imtdLst DO
      BEGIN
      argMenu[iimtd].DrawProc := @LotusDrawProc;
      END;
 (* DrawInitialMenu; *)

     GetAlert (WPAlerts, 911, @tempStr);
     iimtdLabel := 8;
     if length (tempStr) = 1 then if tempStr [1] = 'Y' then iimtdLabel := 9;
END;


       {-----------------------------------------------------------------}
       {----------- P A R  A N D  T Y P E  C O M M A N D S --------------}
       {-----------------------------------------------------------------}



{$S LWStyle}
{---------------------------------}
PROCEDURE DoFontImid { (imid: TC) };
{---------------------------------}
VAR fsp: TC;
    tyfam: TW;
    pint: TPint;
    arce: TArce;
    fDisplayIt: TF;
    ifld: TC;
    cT: TC;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
IF selCur.oint <> ointNil THEN
     BEGIN
     IF (imid >= imidFstTyfam) AND (imid <= imidLstTyfam) THEN
          BEGIN
          ifld := ifldTyfam;
          tyfam := ampimidtyfam[imid];
          END
     ELSE BEGIN
          ifld := ifldFsp;
          fsp := ampimidfsp[imid];
          END;
     arce := arceStd;
     cT := CfbcApplyIfld(ifld, tyfam, @fsp, NIL, @arce);
     pint := POINTER(ORD(lldInt) + selCur.oint);
     fDisplayIt := FRceFidelity(pint^.ics, @arce);
     IF NOT fDisplayIt THEN NoteAlert(WPAlerts, 7);
     pint := POINTER(ORD(lldInt) + selCur.oint);
     WITH pint^ DO
          IF lpFst = lpLim THEN
               ApplyKidFop(selCur.oint, ifld, tyfam, @fsp)
          ELSE ApplyFopEd(ics, lpFst, lpLim, ifld, tyfam, @fsp);
     END;
END;

{---------------------------------}
PROCEDURE DoTypeImid { (imid: TC) };
{---------------------------------}
VAR ifld: TC;
    pint: TPint;
PROCEDURE DoValImid(imidFld: TC; val: TC);
   LABEL 99;
   BEGIN
   {$IFC sampcalls}  ALogCall;  {$ENDC}
   IF fTstCom THEN WRITELN('DoValImid: imid:', imid:3, ' val: ', val);
   CASE imidFld OF
    imidBold:         ifld := ifldBold;
    imidItalic:       ifld := ifldItalic;
    imidUnderline:    ifld := ifldUnderline;
    imidSuperscript,
    imidSubscript:    ifld := ifldSuperscript;
    OTHERWISE         BEGIN
                      DoFontImid(imid);
                      GOTO 99;
                      END;
    END;{case}
   pint := POINTER(ORD(lldInt) + selCur.oint);
   WITH pint^ DO
        IF lpFst = lpLim
             THEN ApplyKidFop(selCur.oint, ifld, val, @val)
             ELSE ApplyFopEd(ics, lpFst, lpLim, ifld, val, @val);
   99:
   END;{doValImid}

BEGIN{doTypeImid}
{$IFC sampcalls}  ALogCall;  {$ENDC}
CASE imid OF
 imidPlainText:   FOR imid := imidFNPlain TO imidLNPlain DO DoValImid(imid, 0);
 imidSuperscript: DoValImid(imid, 4);
 imidSubscript:   DoValImid(imid, -4);
 OTHERWISE        DoValImid(imid, 1);
 END;
END;

(*
{------------------------------------------}
PROCEDURE Indent(dxLFst, dxLBody, dxRt: TX);
{------------------------------------------}
LABEL 99;
VAR tyset: TTyset; achad: TAchad; lpFst, lpLim: TLp; ics: TB; pint: TPint;
    lpLimPar: TLp; xLFst, xLBody, xRt: TX;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
IF fTstCom THEN WRITELN('Indent: dxLFst:', dxLFst:4, ' dxLBody:', dxLBody:4,
                               ' dxRt:', dxRt:4);
achad.rgch := NIL; achad.ichFst := 0; achad.ichLim := 0;
pint := POINTER(ORD(lldInt) + selCur.oint);
ics := pint^.ics;
lpLim := pint^.lpLim;
tyset.fRce := FALSE; tyset.fRpe := TRUE; tyset.fParBnds := TRUE;
SetLpd(lpdStd, ics, pint^.lpFst, tyset, achad);
lpFst := lpdStd^.lpFstPar;
IF lpFst > 0 THEN
     BEGIN
     lpLimPar := lpdStd^.lpLimPar;
     SetLpd(lpdStd, ics, lpFst - 1, tyset, achad);
     xLFst := CMax(0, lpdStd^.rpe^.xLftFst + dxLFst);
     xLBody := CMax(0, lpdStd^.rpe^.xLftBody + dxLBody);
     xRt := lpdStd^.rpe^.xRt + dxRt;
     WHILE TRUE DO
          BEGIN
          ApplyFopEd(ics, lpFst, lpLimPar, ifldXLFst, xLFst, @xLFst);
          ApplyFopEd(ics, lpFst, lpLimPar, ifldXLBody, xLBody, @xLBody);
          ApplyFopEd(ics, lpFst, lpLimPar, ifldXRt, xRt, @xRt);
          IF lpLimPar >= lpLim THEN GOTO 99;
          IF fTstCom THEN WRITELN('Par loop; lpLimPar: ', lpLimPar);
          lpFst := lpLimPar;
          SetLpd(lpdStd, ics, lpFst, tyset, achad);
          lpLimPar := lpdStd^.lpLimPar;
          END;
     END;
99:
END;

{-------------------------------}
PROCEDURE DoIndentImid(imid: TC);
{-------------------------------}
LABEL 99;
VAR tyset: TTyset;
    lpLim, lpFst1, lpLim1, lpFst2, lpLim2: TLp;
    pint: TPint;
    achad: TAchad;
    ics: TB;
    arpe1, arpe2: TArpe;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
CASE imid OF
 imidSetIndent:
     BEGIN
     IF selCur.oint = ointNil THEN GOTO 99;
     achad.rgch := NIL; achad.ichFst := 0; achad.ichLim := 1;
     pint := POINTER(ORD(lldInt) + selCur.oint);
     lpLim := pint^.lpLim;
     ics := pint^.ics;

      { Get the par properties and the bounds of the two paragraphs. }
     tyset.fRce := FALSE; tyset.fRpe := TRUE; tyset.fParBnds := TRUE;
     SetLpd(lpdStd, ics, pint^.lpFst, tyset, achad);
     lpFst1 := lpdStd^.lpFstPar;
     lpLim1 := lpdStd^.lpLimPar;
     MoveRgch(@arpe1, POINTER(ORD(lpdStd^.rpe)), lpdStd^.rpe^.cb);
     achad.rgch := NIL; achad.ichFst := 0; achad.ichLim := 1;
     SetLpd(lpdStd, ics, lpLim - 1, tyset, achad);
     lpFst2 := lpdStd^.lpFstPar;
     IF lpFst2 <> lpLim1 THEN GOTO 99;   { not exactly two pars selected }
     lpLim2 := lpdStd^.lpLimPar;
     MoveRgch(@arpe2, POINTER(ORD(lpdStd^.rpe)), lpdStd^.rpe^.cb);

      { Set up the indent sizes and invert if the pars are switched. }
     dxStdLFst := arpe1.xLftFst - arpe2.xLftFst;
     dxStdLBody := arpe1.xLftBody - arpe2.xLftBody;
     dxStdRt := arpe1.xRt - arpe2.xRt;
     IF dxStdLFst < 0 THEN
          BEGIN
          dxStdLFst := - dxStdLFst;
          dxStdLBody := -dxStdLBody;
          dxStdRt := -dxStdRt;
          END;
     LSetSel(ics, lpFst1, lpLim2);
     END;
 imidIndent:
     Indent(dxStdLFst, dxStdLBody, dxStdRt);
 imidUnIndent:
     Indent(-dxStdLFst, -dxStdLBody, -dxStdRt);
 END;
99:
END;
*)


{$S LWMargin}
{------------------------------------------}
PROCEDURE DoSpacingImid { (fLnSpacing: TF; dmid: TC) };
{------------------------------------------}
LABEL 7;
VAR ifld: TC; val: TC;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
CASE dmid OF
 dmidSingle:       val := ySingle;
 dmidOneAndHalf:   val := yOneAndHalf;
 dmidDouble:       val := yDouble;
 dmidTriple:       val := yTriple;
 END;
IF fLnSpacing THEN ifld := ifldYLine ELSE ifld := ifldYld;
DoParFop(ifld, val, @val);
7:
END;


{$S LWStyle}
{----------------------------------------------}
PROCEDURE DoParImid { (imid: TC; oboxPane: TC) };
{----------------------------------------------}
LABEL 99;
VAR ifld, val, imtd: TC; quad: TQuad;
    achad: TAchad;
    pint: TPint;
    tyset: TTyset;
    lpFst, lpLim: TLp;
    ics: TB;
    hfcc: THfcc;
    hz: THz;
    lpFstSel, lpFstParSel, lpLimSel, lpLimParSel: TLp;
    arpeScrap: TArpe;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
CASE imid OF
 imidSyPaste:
      BEGIN
      achad.rgch := NIL;
      tyset.fRce := FALSE; tyset.fRpe := TRUE; tyset.fParBnds := TRUE;
      SetLpd(lpdStd, icsScrap, 0, tyset, achad);
      MoveRgch(@arpeScrap, POINTER(ORD(lpdStd^.rpe)), lpdStd^.rpe^.cb);
      pint := POINTER(ORD(lldInt) + selCur.oint);
      ics := pint^.ics;
      IF rghcs^[ics]^^.tycs = tycsEd THEN
           BEGIN
           lpFst := pint^.lpFst;
           lpLim := pint^.lpLim; (* Lim of selection *)
           hz := rghcs^[ics]^^.hz;
           hfcc := POINTER(ORD(HAllocate(hz, cbRgfbcInit)));
           achad.rgch := NIL;
           SetLpd(lpdStd, ics, lpFst, tyset, achad);
           lpFstParSel := lpdStd^.lpFstPar;
           achad.rgch := NIL; (* do I need this ? What about resetting tyset ? *)
           SetLpd(lpdStd, ics, LpMaxP(lpLim - 1, LpFst), tyset, achad);
           lpLimParSel := lpdStd^.lpLimPar;
           zzGenParFops(hfcc, @arpeScrap, hz);
           IF hfcc^^.argfbc[0] <> fopcEnd THEN
               ApplyRgfbcEd(ics, lpFstParSel, lpLimParSel, hfcc);
           FreeBk(hz, POINTER(ORD(hfcc)), tybkStd);
           LSetSel(ics, lpFstParSel, lpLimParSel);
           IF icsMarginRuler <> icsNil THEN
                SelToRuler(icsMarginRuler, orecNil, FALSE, TRUE);
           END;
      GOTO 99;
      END;
 imidOnMargin:
      BEGIN
      IF icsMarginRuler <> icsNil THEN
           BEGIN
           DisplaySubImtd(imtdNil);
           END;
      icsMarginRuler := IcsRulerCreate(oboxPane, TRUE, hzCom);
      IF icsMarginRuler = icsNil
           THEN GOTO 99
           ELSE imtd := imtdMargTab;
      END;
imidOffMargin:
      BEGIN
      IF icsMarginRuler <> icsNil THEN
           BEGIN
           FreeIcs(icsMarginRuler);
           { HiLiteMenu(0); }
           icsMarginRuler := icsNil;
           imtd := imtdNil;
           END
      ELSE GOTO 99;
      END;
OTHERWISE
     BEGIN
     IF (imid >= imidFstSpacePar) AND (imid <= imidLstSpacePar) THEN
          DoSpacingImid(FALSE, imid - imidFstSpacePar)
     ELSE IF (imid >= imidFstLn) AND (imid <= imidLstLn) THEN
          DoSpacingImid(TRUE, imid - imidFstLn)
     ELSE IF (imid >= imidFstQuad) AND (imid <= imidLstQuad) THEN
          BEGIN
          ifld := ifldQuad;
          CASE imid OF
           imidLeft:        val := ORD(quadL);
           imidRight:       val := ORD(quadR);
           imidCenter:      val := ORD(quadC);
           imidJustified:   val := ORD(quadJ);
           (* OTHERWISE             BEGIN DoIndentImid(imid); GOTO 99; END; *)
           END;
          DoParFop(ifldQuad, val, @val);
          END;
     GOTO 99;
     END;
END;
DisplayFstSubImtd(imtd);
99:
END;

(*{------------------------------------}
PROCEDURE DoTabFillImid { (imid: TC) };
{------------------------------------}
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
CASE imid OF
 imidTFDone:    DisplayFstSubImtd(imtdMargTab);
 imidSpace:     ApplyRuler(ifldFilTb, ORD(tyfillNil ), FALSE, icsMarginRuler);
 imidDashes:    ApplyRuler(ifldFilTb, ORD(tyfillHyph), FALSE, icsMarginRuler);
 imidDots:      ApplyRuler(ifldFilTb, ORD(tyfillDots), FALSE, icsMarginRuler);
 imidUL:        ApplyRuler(ifldFilTb, ORD(tyfillUL  ), FALSE, icsMarginRuler);
 END;
END;
*)

{$S LWMargin}
{-----------------------------------}
PROCEDURE DoMarginImid { (imid: TC) };
{-----------------------------------}
VAR val: TC; f: TF; fDecimalComma: TF;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
CASE imid OF
   imidTabClear: ApplyRuler(ifldClrTb, 0, FALSE, icsMarginRuler);
   imidMEnglish: BEGIN
                 f := FSetRulers(tyrluEnglish);
                 SetTyRlu(IcsActive, tyrluEnglish);
                 END;
   imidMMetric:  BEGIN
                 f := FSetRulers(tyrluMetric);
                 SetTyRlu(IcsActive, tyrluMetric);
                 END;
   imidSpace:     ApplyRuler(ifldFilTb, ORD(tyfillNil ), FALSE, icsMarginRuler);
   imidDashes:    ApplyRuler(ifldFilTb, ORD(tyfillHyph), FALSE, icsMarginRuler);
   imidDots:      ApplyRuler(ifldFilTb, ORD(tyfillDots), FALSE, icsMarginRuler);
   imidUL:        ApplyRuler(ifldFilTb, ORD(tyfillUL  ), FALSE, icsMarginRuler);
   OTHERWISE     BEGIN
                 CASE imid OF
                  imidLTab: val := ORD(quadL);
                  imidCTab: val := ORD(quadC);
                  imidRTab: val := ORD(quadR);
                  imidDPTab: BEGIN
                             val := ORD(quadJ);
                             fDecimalComma := FALSE;
                             END;
                  imidDcTab: BEGIN
                             val := ORD(quadJ);
                             fDecimalComma := TRUE;
                             END;
                  END;
                 ApplyRuler(ifldXTb, val, fDecimalComma, icsMarginRuler);
                 END;
   END;
END;


END.
ÿ