{$I APLW/COMPFLAGS }                                            {gdf 1/16/84}
{$R-}

UNIT UnitFolder;

(* Copyright 1983, 1984, Apple Computer Inc. *)

{ gdf  2/24/84 -- insert trace calls for segmentation  }
{ gdf  1/21/84 -- change Enqueue to EnqueueLotus  }
{ gdf  1/16/84 -- printing compile flag & include COMPFLAGS added & unit
                  and units quickdraw & fontmgr changed }
{ bas 10/22/82 -- grow }
{ bas  9/27/82 -- MkLPosts --> LMkPosts }
{ bas  9/13/82 -- SetLThumb bug }
{ jrm  9/ 7/82 -- added fPaintPane parameter to OboxCreFldr and FIdCreateFolder }
{ bas  8/09/82 -- hide scroll boxes in RemoveScrollBoxes }
{ bas  7/20/82 -- no fUpdate parm to FreeObox, FreeDialogObox;
                  dialog uses dialogFolder^.portRect; new box structure }
{ jrm  7/19/82 -- Added FixRectsOf }
{ jrm  6/29/82 -- USES changes: LisaGraf => QuickDraw; moved USES UnitFile }
{ jrm  6/29/82 -- replaced SetClipRgn call with SetClip; EndGlobal no
                  longer restores the old clipRgn }
{ jrm  5/26/82 -- removed prcs parameters from OboxDialogCreate, FidCreateFolder,
                  and OboxCreFldr; commented out GetFidActive, SetFidPrcs,
                  RetitleFid, and DeleteFid }
{ crc 04/02/82 -- USES changes for new font manager }
{ crc 03/26/82 -- AdjustRectsOf does nothing if box not found by FidOfWindow. }
{ jrm 03/16/82 -- Modified FidCreateFolder for filer integration }
{ bas 03/01/82 -- Clear opened old scroll box area in GrowFolder }
{ bas 02/26/82 -- Export BegGlobal & EndGlobal }
{ bas 02/06/82 -- ClipToScreen/SetPort -> BegGlobal/EndGlobal }
{ bas 01/21/82 -- make look more like wm library }
{ edited by jrm 1/6/82 }

INTERFACE

USES
      {$IFC sampcalls}                                               {gdf 2/24/84}
         {$U aplw/tracecalls.obj} tracecalls,
      {$ENDC}
      {$U Obj:UnitStd.Obj }   UnitStd,
      {$U Obj:UnitHz.Obj }    UnitHz,
      {$U LibOS/Syscall.Obj }   Syscall,
      {$U Obj:UnitFile.Obj }  UnitFile,
      {$U Obj:Storage.Obj }   Storage,

     {$IFC fPrintOld}                                               {gdf 1/16/84}
      {$U Obj:FontMgr.Obj }   FontMgr,
      {$U Obj:QuickDraw.Obj } QuickDraw,
     {$ELSEC}
      {$U Obj:QuickDraw.Obj } QuickDraw,
      {$U Obj:FontMgr.Obj }   FontMgr,
     {$ENDC}                                                        {gdf 1/16/84}

      {$U Obj:WM.Events.Obj}  Events,
      {$U Obj:WM.Folders.Obj} Folders,
      {$U Obj:WmlStd     }    WmlStd,
      {$U Obj:WM.Menus.Obj }  Menus,
      {$U Obj:AlertMgr.Obj }  AlertMgr,
      {$U ApLW/Queue.Obj }    Queue,
      {$U ApLW/UnitBox.Obj }  UnitBox,
      {$U ApLW/BoxScid.Obj }  BoxScid;

VAR hrgnScreen: THrgn;

 { dialog box operators }
PROCEDURE FreeDialogObox       (obox: TC);
FUNCTION  OboxDialogCreate     (vSize: TC; refCon: LONGINT; fPaint: TF): TC;

 { folder operators -- creating, shrinking etc. }
FUNCTION  FidCreateFolder      (window: TWindow; refConPane: LONGINT;
                                    spTitle: TSp; fPaintPane: TF): TFid;
PROCEDURE MakeFolderActive     (obox: TC; fDeactivateOld: TF);
PROCEDURE RemoveScrollBoxes    (oboxTab: TC);
PROCEDURE MakeFDeactivated     (obox: TC);
PROCEDURE FixRectsOf           (window: TWindow);
PROCEDURE AdjustRectsOf        (window: TWindow);
PROCEDURE GrowFolder           (obox: TC; ptAdj: TPt);
 (* PROCEDURE GetFidActive         (VAR fidActive: TFid; VAR prcsActive: TPrcs);
    PROCEDURE SetFidPrcs           (fid: TFid; prcs: TPrcs);
    PROCEDURE RetitleFid           (fid:TFid; spTitle:TSp);
    PROCEDURE DeleteFid            (fid: TFid);
 *)
PROCEDURE BegGlobal;
PROCEDURE EndGlobal;


 { pane and scroll bar operators }
PROCEDURE SetLSbIcons          (scid: TScid; siconNew: TSicon);
FUNCTION  CLThumbPos           (scid: TScid): TC;
PROCEDURE SetLThumb            (scid: TScid; cPct: TC);
PROCEDURE SynSplit             (oboxScrl: TC; cdCoord: TCd);
PROCEDURE SynResize            (oboxBefore, oboxAfter: TC; dcdAdj: TCd);


IMPLEMENTATION

{$IFC fSymOK }
{$D+ }
{$ELSEC }
{$D- }
{$ENDC }

VAR ptOldOrg: TPt; (*hrgnOldClip: THrgn;*)
             { set by BegGlobal for use by EndGlobal }


{$S LWSelect}
{--------------------------------------------------}
PROCEDURE BegGlobal;
{--------------------------------------------------}
 { set the origin so that the current port is in global coordinates;  also,
   open up clipping.  Sets ptOldOrg and ptOldClip which are used to EndGlobal
   to reset the origin to local space and restore clipping. }
VAR ptT: TPt;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
WITH thePort^ DO BEGIN
    ptOldOrg := portRect.topLeft;
    ptT := ptOldOrg;  LocalToGlobal(ptT); { make it global }
    SetOrigin(ptT.h, ptT.v);
    (* hrgnOldClip := clipRgn;
       clipRgn := hrgnScreen; *)
    SetClip(hrgnScreen);
    END;
END;

{---------------------------------------------------}
PROCEDURE EndGlobal;
{---------------------------------------------------}
 { Undoes a BegGlobal, restoring local origin - no longer restores clipRgn}
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
SetOrigin(ptOldOrg.h, ptOldOrg.v);
 (* SetClipRgn(hrgnOldClip); *)
END;


{$I ApLW/SPLIT.TEXT }


 { In setting icons and thumb position for a scroll chain (scid),

{$S LWSave}
{-------------------------------------------------------}
PROCEDURE SetLSbIcons  {(scid: TScid; siconNew: TSicon)} ;
{-------------------------------------------------------}
VAR oboxPane, oboxScrl: TC; pbox: TPbox; tyvh: TTyvh;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
IF fTstBox THEN
    BEGIN
    WRITE('SetLSbIcons scid: ', scid, ', sicon: ');
                 (*  I'll restore the PxSIcon calls when I have time to
                     put all the stuff used by both Lotus and Brad's Wmlib
                     into one place... JRM
     PxSicon(siconNew);
                 *)
    WRITELN;
    END;
UnpackScid(scid, oboxPane, tyvh);
oboxScrl := OboxScrollFromObox(oboxPane, tyvh);
pbox := POINTER(ORD(bdsp) + oboxScrl);
pbox^.sicon := siconNew;
BegGlobal;  PaintBox(oboxScrl);  EndGlobal;
END; { SetLSbIcons }

{$S LWCommand}
{-------------------------------------------------}
FUNCTION CLThumbPos {(scid: TScid): TC};
{-------------------------------------------------}
VAR oboxPane: TC; tyvh: TTyvh; pboxScrl: TPbox;
BEGIN
    {$IFC sampcalls}  ALogCall;  {$ENDC}
    UnpackScid(scid, oboxPane, tyvh);
    pboxScrl := POINTER(ORD(bdsp) + OboxScrollFromObox(oboxPane, tyvh));
    CLThumbPos := pboxScrl^.cPercent;
END;


{$S LWSelect}
{--------------------------------------------}
PROCEDURE SetLThumb { (scid: TScid; cPct: TC) };
{--------------------------------------------}
VAR oboxScrl, oboxPane: TC; pbox, pboxTab: TPbox;
    tyvh: TTyvh; rT: TR; posts: TPosts;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
 { Sets the thumb to cPct. If the scroll box is active, the thumb is
   repositioned without repainting the entire scroll box. }
IF fTstBox THEN WRITELN('SetScidThumb: ', scid, ' percent: ', cPct);
UnpackScid(scid, oboxPane, tyvh);
oboxScrl := OboxScrollFromObox(oboxPane, tyvh);
IF tyvh <> tyvhV THEN EXIT(SetLThumb);
pbox := POINTER(ORD(bdsp) + oboxScrl);
pbox^.cPercent := cPct;
pboxTab := POINTER(ORD(bdsp) + FidFromScid(scid));
IF NOT pboxTab^.fSbox THEN EXIT(SetLThumb);
LMkPosts(pbox, posts);
IF posts[iconThumb] = posts[PRED(iconThumb)] THEN EXIT(SetLThumb);
rT := pbox^.r; InsetRect(rT,1,0);
rT.top := posts[PRED(iconGryA)]; rT.bottom := posts[iconGryB];
BegGlobal;
FillRect(rT, ltgray);
RThumb(pbox, posts[iconGryA], rT);
SetWMFont;
EraseRect(rT);
{$ifc fThumbNumber}
PaintThumb (rT, cPct);
{$elsec}
FrameRect(rT);
{$endc}
RestoreLFont;
EndGlobal;
END;  { SetLThumb }


{$S LWActive}
{----------------------------------}
PROCEDURE MakeScroll (oboxTab: TC) ;
{----------------------------------}
VAR pbox: TPbox; obox, oboxLim: TC;
BEGIN
    {$IFC sampcalls}  ALogCall;  {$ENDC}
    IF fTstBox THEN WRITELN('MakeScroll for folder ', oboxTab);
    pbox := POINTER(ORD(bdsp) + oboxTab);
    IF pbox^.tybox <> tyboxTab THEN BREAK(305);
    pbox^.fSbox := TRUE;
    oboxLim := OboxNxtFldr(oboxTab);
    obox := pbox^.oboxNxt;
    BegGlobal;
    WHILE obox <> oboxLim DO BEGIN
        pbox := POINTER(ORD(bdsp) + obox);
        IF (pbox^.tybox = tyboxScroll) OR (pbox^.tybox = tyboxGrow) THEN
            PaintBox(obox);
        obox := pbox^.oboxNxt;
    END;
    EndGlobal;
END; { MakeScroll }


{----------------------------------------------------}
PROCEDURE RemoveScrollBoxes { (oboxTab: TC) };
{----------------------------------------------------}
 { remove the scroll boxes from the folder given by oboxTab }
VAR pbox: TPbox; r: TR; cdOldLeft: TCd;
BEGIN
    {$IFC sampcalls}  ALogCall;  {$ENDC}
    IF fTstBox THEN WRITELN('RemoveScrollBoxes for folder ', oboxTab);
    pbox := POINTER(ORD(bdsp) + oboxTab);
    pbox^.fSbox := FALSE;
    r := pbox^.window^.portRect;
    ClipRect(r);
    cdOldLeft := r.left;
    r.left := r.right-dhScroll+2;
    FillRect(r, white);
    SetPt(r.topLeft, cdOldLeft, r.bottom-dvScroll+2);
    FillRect(r, white);
END;  { RemoveScrollBoxes }



{-------------------------------------------------------------}
PROCEDURE MakeFolderActive { (obox: TC; fDeactivateOld: TF) };
{-------------------------------------------------------------}
 {activates and draws new folder. }
LABEL 99;
VAR oboxOld, oboxTab, oboxLim: TC;
    pbox: TPbox; evDsc: TEvDsc;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
IF fTstBox THEN WRITELN('MakeFolderActive: obox ', obox:4);
oboxTab := OboxFldr(obox);
IF oboxTab = oboxNil THEN GOTO 99;
{abvd.fValid := FALSE;}
oboxOld := oboxActive;
      { Remove the scroll bars from the old active folder. }
      { This has to be done BEFORE LinkFolder for uncovered regions. }
IF oboxOld <> oboxTab THEN BEGIN
    IF fDeactivateOld THEN
       BEGIN
       evDsc.obox := oboxNil;  { don't want VisObox called }
       IF oboxOld <> oboxNil THEN BEGIN
            RemoveScrollBoxes(oboxOld);
            { SetClipRgn(hrgnScreen); }
            evDsc.tyev := tyevDeactivated;
            evDsc.fid := oboxOld;
            EnqueueLotus(bdsp^.pq^, @evDsc);
            END;
       END;
    pbox := POINTER(ORD(bdsp) + oboxTab);
    HiLiteTab(pbox^.window, TRUE);
    evDsc.obox := oboxNil;  { don't want VisObox called }
    evDsc.tyev := tyevActivated;
    evDsc.fid := oboxTab;
    EnqueueLotus(bdsp^.pq^, @evDsc);
    MakeScroll(oboxTab);
    LinkFolder(oboxTab, OboxPrvFromObox(OboxFstFldr)); { it's now on top }
    oboxActive := oboxTab;                             { and active }
    IF fDeactivateOld THEN
           { THIS has to be done AFTER LinkFolder, so it's not active: }
         IF oboxOld <> oboxNil THEN BEGIN
               pbox := POINTER(ORD(bdsp) + oboxOld);
               HiLiteTab(pbox^.window, FALSE);
               END;
    END;
IF fTstBox THEN WRITELN('Folder ', oboxTab, ' activated.');
99:
END;  { MakeFolderActive }

{---------------------------------------}
PROCEDURE MakeFDeactivated { (obox: TC) };
{---------------------------------------}
LABEL 99;
VAR oboxOld, oboxTab, oboxLim: TC;
    pbox: TPbox; evDsc: TEvDsc;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
IF fTstBox THEN WRITELN('MakeFDeactivated: obox ', obox:4);
oboxTab := OboxFldr(obox);
IF oboxTab = oboxNil THEN GOTO 99;
pbox := POINTER(ORD(bdsp) + oboxTab);
HiLiteTab(pbox^.window, FALSE);
{abvd.fValid := FALSE;}
oboxOld := oboxActive;
oboxActive := oboxNil;
{ Remove the scroll bars from the old active folder. }
{ This has to be done BEFORE LinkFolder for uncovered regions. }
IF oboxOld <> oboxTab THEN Break(1509);
evDsc.obox := oboxNil;  { don't want VisObox called }
IF oboxOld <> oboxNil THEN BEGIN
     RemoveScrollBoxes(oboxOld);
     evDsc.tyev := tyevDeactivated;
     evDsc.fid := oboxOld;
     EnqueueLotus(bdsp^.pq^, @evDsc);
     END;
99:
END;  { MakeFDeactivated }


{$S LWSave}
{------------------------------------------------------------------------------}
FUNCTION OboxCreFldr (rBody: TR; refConPane: LONGINT; spTitle: TSp;
                       oboxPrv: TC; window: TWindow; fPaintPane: TF): TC;
{------------------------------------------------------------------------------}
VAR r, rGrow: TR; h: TH;
    oboxTab, obox, oboxH, oboxV: TC; pbox: TPbox;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
 { only rTab bottom left (= rBody.topLeft) is relevant }
SetRect(r, rBody.left, rBody.top-15, 0, rBody.top);
oboxTab := OboxAlloc(r, tyboxTab, 0, oboxPrv, window);
h := HAllocate(bdsp^.hz, LENGTH(spTitle) + 1);
MoveRgch(POINTER(ORD(h^)), @spTitle, LENGTH(spTitle) + 1);
pbox := POINTER(ORD(bdsp) + oboxTab);
pbox^.hspTitle := @h^;
pbox^.fSbox := FALSE;           { ignore scroll/grow boxes when painting }

 { create pane, horiz scroll box, vert scroll box, grow box (in reverse order) }
SetRect(rGrow, rBody.right-dhScroll, rBody.bottom-dvScroll, rBody.right, rBody.bottom);
obox := OboxAlloc(rGrow, tyboxGrow, 0, oboxTab, window);
SetRect(r, rGrow.left, rBody.top, rBody.right, rGrow.top+1);
oboxV := OboxAlloc(r, tyboxScroll, 0, oboxTab, window);
SetRect(r, rBody.left, rGrow.top, rGrow.left+1, rBody.bottom);
oboxH := OboxAlloc(r, tyboxScroll, 0, oboxTab, window);
SetRect(r, rBody.left, rBody.top, rGrow.left+1, rGrow.top+1);
obox := OboxAlloc(r, tyboxPane, refConPane, oboxTab, window);

 { the only pane is the head of both scroll chains }
pbox := POINTER(ORD(bdsp) + obox);
WITH pbox^ DO BEGIN
    rgfScHead[tyvhH]  := TRUE;   rgfScHead[tyvhV]  := TRUE;
    rgoboxLink[tyvhH] := oboxH;  rgoboxLink[tyvhV] := oboxV;
    END;
IF fPaintPane THEN
    BEGIN SetPort(window); BegGlobal; PaintBox(obox); EndGlobal; END;
 { setup the scroll boxes }
pbox := POINTER(ORD(bdsp) + oboxH);
WITH pbox^ DO BEGIN
    tyvh := tyvhH;   oboxPaneFst := obox;
    sicon := [];     siconVis := [];
    cPercent := 0;
    END;
pbox := POINTER(ORD(bdsp) + oboxV);
WITH pbox^ DO BEGIN
    tyvh := tyvhV;   oboxPaneFst := obox;
    sicon := [];     siconVis := [];
    cPercent := 0;
    END;
OboxCreFldr := oboxTab;
END;


{$S LWGuess}
{-------------------------------------------------}
PROCEDURE FreeDialogObox { (obox: TC) };
{-------------------------------------------------}
VAR pbox: TPbox; oboxFree, oboxLim: TC; evDsc: TEvDsc;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
DialogHeight(0, FALSE);
obox := OboxFldr(obox);
pbox := POINTER(ORD(bdsp) + obox);
IF pbox^.window <> dialogFolder THEN BREAK(1106);
pbox^.fSbox := FALSE;
oboxLim := OboxNxtFldr(obox);
WHILE obox <> oboxLim DO
    BEGIN
    oboxFree := obox;
    pbox := POINTER(ORD(bdsp) + obox);
    obox := pbox^.oboxNxt;
    FreeObox(oboxFree);
    END;
{real kludge for spelling}
theEvDsc.obox := oboxNil;  { don't want VisObox called }
oboxDwn := oboxNil;
{end of kludge}
END;

{$S LWDialog}
{-------------------------------------------------------------------------}
FUNCTION OboxDialogCreate { (vSize: TC; refCon: LONGINT; fPaint: TF): TC };
{-------------------------------------------------------------------------}
VAR portSave: TPort; rDialog: TR; oboxTab, oboxD, oboxS: TC; pbox: TPbox;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
IF fPaint THEN DialogHeight(vSize+4+dvScroll, FALSE)
ELSE DialogHeight(vSize, FALSE);
rDialog := dialogFolder^.portRect;
WITH dialogFolder^.portBits.bounds.topLeft DO  { globalize }
        OffsetRect(rDialog, -h, -v);
InsetRect(rDialog, -1, -1); { add frame }
oboxTab := OboxCreFldr(rDialog, refCon, 'Dialog Box', oboxMenu,
                                    dialogFolder, fPaint (* paint the pane *) );
pbox := POINTER(ORD(bdsp) + oboxTab);
oboxD := pbox^.oboxNxt;
pbox := POINTER(ORD(bdsp) + oboxD);
oboxS := pbox^.oboxNxt;
pbox := POINTER(ORD(bdsp) + oboxS); pbox^.sicon := [iconArwA, iconArwB];
oboxS := pbox^.oboxNxt;
pbox := POINTER(ORD(bdsp) + oboxS); pbox^.sicon := [iconArwA, iconArwB];
GetPort(portSave); SetPort(dialogFolder);
IF fPaint THEN MakeScroll(oboxTab);
SetPort(portSave);
OboxDialogCreate := oboxD;
{$ifc fDbgOk}
IF fTstBox THEN
    BEGIN
    PxObox(oboxTab); PxObox(oboxD);
    END;
{$endc}
END;


{$S LWSave}
{-----------------------------------------------------------------------}
FUNCTION FidCreateFolder { (window: TWindow; refConPane: LONGINT;
                            spTitle:TSp; fPaintPane: TF): TFid };
{-----------------------------------------------------------------------}
  { Create a folder. Return obox of tab travelling as a TFid. }
VAR rFolder: TR;
BEGIN
    {$IFC sampcalls}  ALogCall;  {$ENDC}
    IF fTstBox THEN
         BEGIN
         WRITELN('FidCreateFolder called with ', spTitle);
         WRITELN('window = ', ORD(window));
         END;
    rFolder := window^.portRect;
    WITH window^.portBits.bounds.topLeft DO  { globalize }
        OffsetRect(rFolder, -h, -v);
    InsetRect(rFolder, -1, -1);
    FidCreateFolder := OboxCreFldr(rFolder, refConPane, spTitle,
                                   OboxPrvFromObox(oboxNil), window, fPaintPane);
END;  { FidCreateFolder }


{$S LWResize}
{---------------------------------------------------------}
PROCEDURE OffsetBoxes (obox, oboxLim: TC; dhAdj, dvAdj: TC);
{---------------------------------------------------------}
VAR pbox: TPbox;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
WHILE obox <> oboxLim DO BEGIN
    pbox := POINTER(ORD(bdsp) + obox);
    OffsetRect(pbox^.r, dhAdj, dvAdj);
    obox := pbox^.oboxNxt;
    END;
END;

{$S LWCommand}
{-------------------------------------------}
PROCEDURE AdjustRectsOf { (window: TWindow) };
{-------------------------------------------}
LABEL 99;
VAR oboxTab, oboxPane, oboxLim: TC;
    pboxTab, pboxPane: TPbox; ptAdj: TPt;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
IF (window <> menuFolder) AND (window <> dialogFolder)
    AND (window <> filerFolder) THEN
   BEGIN
      oboxTab := FidOfWindow(window);
      IF oboxTab = oboxNil THEN GOTO 99;  {Unknown box, do nothing}
      pboxTab := POINTER(ORD(bdsp) + oboxTab);
      oboxPane := pboxTab^.oboxNxt;
      pboxPane := POINTER(ORD(bdsp) + oboxPane);
      IF pboxPane^.tybox <> tyboxPane THEN Break(1506);
       { the following depends on the fact that the first pane's topLeft
          aligns with the portRect's topLeft }
      ptAdj := window^.portRect.topLeft;
      WITH window^.portBits.bounds DO Subpt(topLeft, ptAdj); { globalize }
      SubPt(pboxPane^.r.topLeft, ptAdj);
      IF (ptAdj.h <> 0) OR (ptAdj.v <> 0) THEN BEGIN
         IF fTstBox THEN
             WRITELN('About to OffsetBoxes by [dh,dv] = ', ptAdj.h, ptAdj.v);
         OffsetBoxes(oboxTab, OboxNxtFldr(oboxTab), ptAdj.h, ptAdj.v);
         END;
   END;
99:
END; {AdjustRectOf}

{$S LWActive}
{-------------------------------------------}
PROCEDURE FixRectsOf { (window: TWindow) };
{-------------------------------------------}
  { smashes sizes of window's boxes based on the current portRect. assumes
    window is tab, pane, hscroll, vscroll, grow.  for scrap only (for now).
    called BEFORE every call of MakeFolderActive. }
VAR rBody, rGrow, r: TR; pbox: TPbox; obox, oboxLim: TC;
BEGIN
    {$IFC sampcalls}  ALogCall;  {$ENDC}
    IF window <> scrapFolder THEN Break(990);
    rBody := window^.portRect;
    WITH window^.portBits.bounds.topLeft DO  { globalize }
        OffsetRect(rBody, -h, -v);

    InsetRect(rBody, -1, -1);

    SetRect(r, rBody.left, rBody.top-15, 0, rBody.top);            { tab }
    obox := FidOfWindow(window);
    pbox := POINTER(ORD(bdsp) + obox);
    pbox^.r := r;

    SetRect(rGrow, rBody.right-dhScroll, rBody.bottom-dvScroll,
                   rBody.right, rBody.bottom);

    SetRect(r, rBody.left, rBody.top, rGrow.left+1, rGrow.top+1);  { pane }
    InsetRect(r, 1, 1);
    obox := pbox^.oboxNxt;
    pbox := POINTER(ORD(bdsp) + obox);
    pbox^.r := r;

    SetRect(r, rBody.left, rGrow.top, rGrow.left+1, rBody.bottom); { hscroll }
    obox := pbox^.oboxNxt;
    pbox := POINTER(ORD(bdsp) + obox);
    pbox^.r := r;

    SetRect(r, rGrow.left, rBody.top, rBody.right, rGrow.top+1);   { vscroll }
    obox := pbox^.oboxNxt;
    pbox := POINTER(ORD(bdsp) + obox);
    pbox^.r := r;

    obox := pbox^.oboxNxt;
    pbox := POINTER(ORD(bdsp) + obox);                             { grow }
    pbox^.r := rGrow;
END;

{$S LWResize}
{--------------------------------------------------}
PROCEDURE GrowFolder { (obox: TC; ptAdj: TPt) } ;
{--------------------------------------------------}
LABEL 11, 21, 99;
VAR oboxScrl, oboxHFst, oboxVFst, oboxLim, oboxTab, oboxGrow, oboxPane: TC;
    pboxTab, pboxGrow, pboxScrl: TPbox; wdw: TWindow;
    ptOldBR, ptPaneBR: TPt;

    PROCEDURE ClearEm (obox: TC);
     { obox comes in as a scroll box. clear the boxes obox..oboxGrow and mark
       them using  the box's refCon, which is otherwise not used and equal to 0.
       The mark is reset by PaintEm. }
    VAR pbox: TPbox; hrgn: THrgn; box: TBox;
    BEGIN
        {$IFC sampcalls}  ALogCall;  {$ENDC}
        IF (ptAdj.h<=0) AND (ptAdj.v<=0) THEN EXIT(ClearEm);
        hrgn := NewRgn;
        BegGlobal;
        REPEAT
            pbox := POINTER(ORD(bdsp) + obox); box := pbox^;
            IF (box.tybox=tyboxGrow) OR (ptAdj.vh[TyvhNOT(box.tyvh)]>0) THEN BEGIN
                RectRgn(hrgn, box.r); InvalidateRgn(wdw, hrgn);
                FillRect(box.r, white);
                box.refCon := 1;  { any nonzero value will do }
            END;
            obox := box.oboxNxt;
        UNTIL box.tybox = tyboxGrow;
        EndGlobal;
        DisposeRgn(hrgn);
    END;{clearem}

    PROCEDURE PaintEm(obox: TC);
    VAR pbox: TPbox;
    BEGIN
        {$IFC sampcalls}  ALogCall;  {$ENDC}
        IF (ptAdj.h>0) AND (ptAdj.v>0) THEN EXIT(PaintEm);
        BegGlobal;
        REPEAT
            pbox := POINTER(ORD(bdsp) + obox);
            IF pbox^.refCon = 0 THEN
                PaintBox(obox)  { paint if uncleared (unmarked) }
            ELSE
                pbox^.refCon := 0;                  { clear mark }
            obox := pbox^.oboxNxt;
        UNTIL pbox^.tybox = tyboxGrow;
        EndGlobal;
    END;{paintem}

    PROCEDURE DoAdjust(oboxScrl, oboxLim: TC; tyvh: TTyvh);
    VAR obox: TC; pboxScrl: TPbox; cdPaneBR: TCd;
    BEGIN
        {$IFC sampcalls}  ALogCall;  {$ENDC}
        cdPaneBR := ptPaneBR.vh[tyvh];
        IF ptAdj.vh[tyvh] > 0 THEN
           { adjust botRight corner of last scroll chain on that axis  }
           AdjScCorner(OboxPrvFromObox(oboxLim), ptAdj.vh[tyvh],
                   FALSE {botRight}, FALSE {no paint/event})
        ELSE
            { kill or adjust affected scroll chains }
           WHILE oboxScrl <> oboxLim DO BEGIN
               pboxScrl := POINTER(ORD(bdsp) + oboxScrl);
               obox := pboxScrl^.oboxNxt;
               WITH pboxScrl^.r DO
                   IF topLeft.vh[tyvh] >= cdPaneBR THEN
                       KillSc(oboxScrl)
                   ELSE IF (topLeft.vh[tyvh] < cdPaneBR) AND
                       (cdPaneBR < botRight.vh[tyvh]) THEN
                       AdjScCorner(oboxScrl, cdPaneBR - botRight.vh[tyvh],
                               FALSE {botRight}, FALSE {no paint/event});
               oboxScrl := obox;
           END;
    END; { DoAdjust }

BEGIN
    {$IFC sampcalls}  ALogCall;  {$ENDC}  {first executable in growfolder!?}
    oboxTab := OboxFldr(obox);
    IF fTstBox THEN WRITELN('GrowFolder  oboxTab: ', oboxTab,
        ', ptAdj: [',ptAdj.h, ptAdj.v, ']');
    pboxTab := POINTER(ORD(bdsp) + oboxTab);
    wdw := pboxTab^.window;
    IF wdw = dialogFolder THEN ptAdj.h := 0;
    IF (ptAdj.v = 0) AND (ptAdj.h = 0) THEN GOTO 99;
    oboxPane := pboxTab^.oboxNxt;
    oboxLim := OboxNxtFldr(oboxTab);
    oboxGrow := OboxPrvFromObox(oboxLim);

    ptOldBR := wdw^.portRect.botRight;
     { enforce min size.  remember, boxes lie one pixel out of portRect
       both horiz and vert. also, scroll and grow boxes overlap 1 pixel, ergo
       the magic #'s 2 & 3 }

    WITH wdw^.portRect DO
        FolderSize(wdw, CMax(dhPaneMin+dhScroll-3, right-left+ptAdj.h),
                        CMax(dvPaneMin+dvScroll-3, bottom-top+ptAdj.v),
                        TRUE);
    ptAdj := wdw^.portRect.botRight; Subpt(ptOldBR, ptAdj); { refigure ptAdj }

    WITH wdw^.portRect DO
        Setpt(ptPaneBR, right-dhScroll+2, bottom-dvScroll+2);
    WITH wdw^.portBits.bounds DO Subpt(topLeft, ptPaneBR);

     { find the 1st horizontal and 1st vertical scroll bars }
    oboxHFst := oboxTab;
    WHILE TRUE DO BEGIN
        pboxScrl := POINTER(ORD(bdsp) + oboxHFst);
        IF pboxScrl^.tybox = tyboxScroll THEN GOTO 11;
        oboxHFst := pboxScrl^.oboxNxt;
    END;
11: oboxVFst := oboxHFst;
    WHILE TRUE DO BEGIN
        pboxScrl := POINTER(ORD(bdsp) + oboxVFst);
        IF pboxScrl^.tyvh = tyvhV THEN GOTO 21;
        oboxVFst := pboxScrl^.oboxNxt;
    END;
21: { for the adjustment to work, panes must be at least dhPaneMin by dvPaneMin }

    RectRgn(wdw^.clipRgn, wdw^.portRect);
    ClearEm(oboxHFst);

     { do horizontal adjust first }
    IF ptAdj.h <> 0 THEN BEGIN
        DoAdjust(oboxHFst, oboxVFst, tyvhH);
        OffsetBoxes(oboxVFst, oboxGrow, ptAdj.h, 0);
    END;

     { now do vertical adjust }
    IF ptAdj.v <> 0 THEN BEGIN
        DoAdjust(oboxVFst, oboxGrow, tyvhV);
         { offset horiz scroll boxes }
        OffsetBoxes(oboxHFst, oboxVFst, 0, ptAdj.v);
    END;

    pboxGrow := POINTER(ORD(bdsp) + oboxGrow);
    OffsetRect(pboxGrow^.r, ptAdj.h, ptAdj.v);

    PaintEm(oboxHFst);
99:
END;


 { end of unit }
END.
ÿ