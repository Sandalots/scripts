    {=======================================================}
    { GenCrdTld.Text  ====  to be included in UnitTld.Text. }
    {                       This file contains the routine  }
    {                       GenCrdTld and its related       }
    {                       procedures.                     }
    {=======================================================}

(* Copyright 1983, 1984, Apple Computer Inc. *)

 { gdf 2/27/84 -- insert trace calls for segmentation  }
 { tjm 2/14/84  -- set tre^.dySuper in FAddTreProp }
 { gdf 2/10/84  -- replace devFromIcs with FillInLFntId }
 { crc 05/21/82 -- tyxyTld replaced by the function TyxyTld }
 { crc 04/19/82 -- dev assigned from global devPrint }
 { crc 04/02/82 -- integration with new font manager }
 { bas 03/18/82 -- RptLoop doesn't work now, Pascal code uncommented-out }
 { bas 03/08/82 -- arec.tyxy instead of crd^.tyxyArec }


{$S LWInsert}
{-----------------------------------------------------------------------}
PROCEDURE Quadify(crd: TCrd; quad: TQuad; VAR xCurQuad: TX; xRtQuad: TX;
  cspTot: TC; ichTbQuad: TC; ichLimQuad: TC; iwdFstQuad: TC;
  iwdLim: TC; fStripTrailingSpaces: TF);
{-----------------------------------------------------------------------}
LABEL   66, 99;
VAR dxPhrase, dxL, dxSp, dxCum:  TX;
    cwdMod, iwd, ich:    TC;
    oxFstWd: TX;
    chT: TCh; icsSon: TB;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
IF fTstTld THEN
    BEGIN
    WRITELN('Quadify -- quad:', ORD(quad):2, '; xCurQ:', xCurQuad:4,
          '; xRtQ: ', xRtQuad:4, '; cspTot:', cspTot:4);
    WRITELN('           ichTbQuad:', ichTbQuad:4, '; ichLimQuad: ',
    ichLimQuad:4,
          ' iwdFstQ:', iwdFstQuad:3, ' iwdLim:', iwdLim:3);
    END;

icsSon := crd^.icsSon;
 { Trailing spaces in the quadifications? }
ich := ichLimQuad - 1;
WHILE (ich >= 0) DO
    BEGIN
    chT := ORD(achadTld.rgch^[ich]);
    IF ((chT = chCr) AND (icsSon = icsNil))
       OR (fStripTrailingSpaces AND (chT = chSp)) THEN
         BEGIN
         cspTot := cspTot - 1;
         xCurQuad := xCurQuad - mpichdx^[ich];
         ich := ich - 1;
         END
    ELSE GOTO 66;
    END;
66:

 { 'dxL' is how much more space has to be added to the line to pad it. }
dxL := xRtQuad - xCurQuad;
IF fTstTld THEN WRITELN(' dxL: ', dxL);

 { Empty tabs just go to the tab stop. }
IF ichLimQuad = ichTbQuad + 1 THEN
    BEGIN
    END
ELSE BEGIN
    CASE quad OF
    quadL:  GOTO 99;
    quadJ:  BEGIN
          IF cspTot > 0 THEN
                 BEGIN
                 xCurQuad := xCurQuad + dxL;
                 DivMod(dxL, cspTot, dxSp, cwdMod);
                 dxSp := dxSp + 1;
                 iwd := iwdFstQuad;
                 dxCum := 0;
                 WHILE TRUE DO
                       BEGIN
                       IF iwd + 1 < iwdLim THEN
                             FOR ich := rgichSepFst^[iwd] TO
                             rgichWdFst^[iwd+1]-1
                             DO
                                  BEGIN
                                  IF cwdMod = 0 THEN dxSp := dxSp - 1;
                                  mpichdx^[ich] := mpichdx^[ich] + dxSp;
                                  dxCum := dxCum + dxSp;
                                  cwdMod := cwdMod - 1;
                                  END;
                       iwd := iwd + 1;
                       rgoxWdFst^[iwd] := rgoxWdFst^[iwd] + dxCum;
                       IF iwd = iwdLim THEN GOTO 99;
                       END;
                 END;
          END;
    quadR:  BEGIN END;
    quadC:  BEGIN
          oxFstWd := rgoxWdFst^[iwdFstQuad];
          dxPhrase := xCurQuad - (crd^.arec.r.left + oxFstWd);
          dxL := dxL + (dxPhrase DIV 2);
          END;
    END;
    END;

IF fTstTld THEN WRITELN('dxL: ', dxL);
{ No tabs with width < dxMinTab }
IF (iwdFstQuad > 0) AND (dxL < 0) THEN
     dxL := CMax(dxL, - mpichdx^[ichTbQuad] + dxMinTab);
xCurQuad := xCurQuad + dxL;
IF iwdFstQuad > 0 THEN
    BEGIN
    {ich := rgichWdFst^[iwdFstQuad] - 1;}
    mpichdx^[ichTbQuad] := mpichdx^[ichTbQuad] + dxL;
    FOR iwd := iwdFstQuad TO iwdLim DO rgoxWdFst^[iwd] := rgoxWdFst^[iwd] +
    dxL;
    END
ELSE WITH crd^.arec.r DO
          BEGIN
          left := left + dxL;
          right := right + dxL;
          END;
99:
END;

{$S LWScroll}
{-----------------------------------------------------------------------------}
FUNCTION FAddTreProp(tyfill: TTyfill; ipgCur: TC; ich: TC; VAR ibTreLim: TC) : TF;
{-----------------------------------------------------------------------------}
VAR tre, trePrv: TTre;
    cbTre:TB;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
IF fTstTld THEN WRITELN('FAddTreProp -- tyfill:', ORD(tyfill):2, '; ipgCur: ',
 ipgCur, '; ich:', ich:3);
tre := POINTER(ORD(grptre) + ibTreLim);
trePrv := POINTER(ORD(tre) - tre^.cbPrv);
IF trePrv^.ichFst = ich THEN
    BEGIN
    { Set the bit in THIS tre. }
    trePrv^.tyfill := tyfill;
    trePrv^.ipgCur := ipgCur;
    FAddTreProp := TRUE;
    END
ELSE BEGIN
    cbTre := trePrv^.cb;
    IF (ibTreLim + cbTre + cbOvhAtre) > cbGrptre THEN
          BEGIN
          FAddTreProp := FALSE;
          END
    ELSE BEGIN
          { Insert another TRE into the list with tab-fill on. }
          tre^.dlpFst := trePrv^.dlpFst + (ich - trePrv^.ichFst);
          tre^.ichFst := ich;
          tre^.cb := cbTre;
          tre^.ipgCur := ipgCur;
          MoveRgch(@tre^.arce, @trePrv^.arce, trePrv^.arce.cb);
          tre^.dySuper := trePrv^.dySuper;                        {tjm 2/14/84}
          tre^.tyfill := tyfill;
          ibTreLim := ibTreLim + cbTre;
          tre := POINTER(ORD(grptre) + ibTreLim);
          tre^.cbPrv := cbTre;
          tre^.cb := 0;
          FAddTreProp := TRUE;
          END;
    END;
END;

{$S LWType}
{-----------------------------------------------------------------------}
PROCEDURE DxTbdFromX(crd: TCrd; x: TX; VAR dxtb: TX; VAR tbd: TTbd;
                     VAR itbMin: TC; rpe: TRpe);
{-----------------------------------------------------------------------}
VAR itb:    TC;
    itbLim: TC;
    rgtbd:  TRgtbd;
    xOrig:  TX;
    dxMin:  TX;
BEGIN
 { Do the work in tyxyCs, then convert back. }
{$IFC sampcalls}  ALogCall;  {$ENDC}
xOrig := x;
ConvertTyxy(x, xOrYDummy, crd^.tyxy, tyxyCs);

itbLim := rpe^.itbLim;
IF fTstTld THEN WRITELN('DxTbdFromX -- x:', x:4, '; itbLim:', itbLim:2);
IF itbLim < 0 THEN
    BEGIN
    { dxtb := -itbLim;   }         { If itbLim < 0 THEN -itbLim = dxtb }
    dxtb := dxtbStd;
    itb := 0;
    itbLim := itb;  { Fool the itb = itbLim check below }
    END
ELSE IF itbLim <= itbMin THEN
    BEGIN
    itb := itbLim;
    dxtb := dxtbStd;
    END
ELSE BEGIN
    rgtbd := POINTER(ORD(rpe) + ibRgtbd);
    IF x + dxMinTab <= rgtbd^[itbMin].x
          THEN itb := itbMin
          ELSE itb := ItbFromX(rgtbd, itbLim-1, x + dxMinTab - 1) + 1;
    dxtb := dxtbStd; { In case itb = itbLim }
    END;
IF itb = itbLim THEN
    BEGIN
    tbd := tbdNil;
    tbd.x := x + dxtb; tbd.x := tbd.x - (tbd.x MOD dxtb);
    tbd.tyfill := tyfillNil;
    END
ELSE tbd := rgtbd^[itb];

dxMin := dxMinTab;
IF tyxyCs <> crd^.tyxy THEN
    BEGIN
    x := tbd.x;
    ConvertTyxy(x, xOrYDummy, tyxyCs, crd^.tyxy);
    tbd.x := x;
    ConvertTyxy(dxMin, xOrYDummy, tyxyCs, crd^.tyxy);
    END;
IF tbd.quad = quadL
    THEN dxtb := CMax(tbd.x - xOrig, dxMin)
    ELSE dxtb := dxMin;
IF fTstTld THEN
 WRITELN('              itb:', itb:3, '; tbd.x:', tbd.x:4,
 '; dxtb:', dxtb:4, '; tbd.quad:', ORD(tbd.quad):2, ', tbd.tyfill: ',
 ORD(tbd.tyfill));
END;

PROCEDURE RptLoop (argtych:  TP; mpichdx:  TRgx; rgichSepFst:  TRgc;
rgichWdFst:
TRgc;
                   rgoxWdFst:  TRgx; achadTld:  TAchad; acrd:  TAcrd;
                   DxTbdFromX:
                   TP;
                   Quadify: TP; FAddTreProp: TP; CToSp: TP; ipgCur: TC; grptre:
                   TTre);
EXTERNAL;


{$S LWCom3}
{------------------------------------------------------------}
PROCEDURE GenCrdTld { (crd: TCrd; ics: TB; lpFst: TLp; tyxyArec: TTyxy) };
{------------------------------------------------------------}
{ arec will be in the coordinates of the crd until the last moment, }
{ when they will be converted to tyxyArec coordinates. }
LABEL 10, 30, 40;
VAR fSepFst, fWdFst, fEndPar, fReqBrk: TF;
    xOrig, xLim, xCur, dx: TX;
    dxExtra: TB;
    dyLine, dyNeeded, dyAboveBase, dyBelowBase, dyLd, cbSuper: TY;
    ich, ichLim, ichTbPrv, dch:    TC;
    iwd, iwdFstQuad: TC;
    itbLst: TC;
    ibTreLim: TC;
    cbTre:  TB;
    ifnt: TC;
{Kludge record to convert TB (i.e. -128..127) to 0..255}
    chch1:  PACKED RECORD ch: 0..255; ch1: 0..255; END;
    cspTbTot: TC;
    tbd, tbdPrv: TTbd;
    tre, trePrv: TTre;
    rce: TRce;
    rgchDst: TRgch;
    pint:       TPint;
    lpT:    TLp;
    lfntid: TLfntid;
    lfntidT: TLfntid;
    finfo: FontInfo;
    hmpccwid: THmpccwid;
    quadPar: TQuad;
    spIpg:     TSp;
    fOut: TF;
    arpe: TArpe;
    cError: TC;
    cbitSuperMax: TC; cbitSubMax: TC;
    dyT: TY;

    crdSon: TCrd; icsSon: TB; lpSon: TLp;
    xT: TX;
    seteface: style;                                             {gdf 2/10/84 }

{$S LWInsert}
PROCEDURE InitCrock;
    LABEL 444;
    VAR ptPgWidHt: TPt;
    BEGIN
    {$IFC sampcalls}  ALogCall;  {$ENDC}
    DptPaperSize(ics, tyxyCs, {OUT} ptPgWidHt);
    cbitSuperMax := 0; cbitSubMax := 0;
    achadTld.ichFst := 0; achadTld.ichLim := cchSeqTld;
    SetLpd(lpdStd, ics, lpFst, tysetStd, achadTld);
    atld.fParSt := lpdStd^.fParSt;
    {Skip vanish runs}
    WHILE TRUE DO
         BEGIN
         { initialize dangling pointers for graceful termination}
         IF lpdStd^.lp = lpdStd^.lpLim THEN
              BEGIN
              lpdStd^.rpe := @arpeStd;
              lpdStd^.rce := @arceStd;
              GOTO 444;
              END;
         IF NOT lpdStd^.rce^.fVan THEN GOTO 444;
         achadTld.ichFst := 0; achadTld.ichLim := cchSeqTld;
         SeqLpd(lpdStd, achadTld);
         END;
444:
    ich := 0;
    ichTbPrv := ichNil;
    MoveRgch(@arpe, POINTER(ORD(lpdStd^.rpe)), lpdStd^.rpe^.cb);
    WITH arpe DO
          BEGIN
          IF atld.fParSt
               THEN crd^.arec.r.left := xLftFst
               ELSE crd^.arec.r.left := xLftBody;
          dyLine := yLine;
          dyLd := yLd;
          xLim := xRt;   { xLim is relative to the box, not the arec }
          xLim := CMin(xLim, ptPgWidHt.h - dhFromRtPgEdge);
          crd^.arec.r.left := CMin(crd^.arec.r.left, xLim - dhMinMarg);
          tbdPrv := tbdNil;
          quadPar := quad;
          END;
    ConvertTyxy(crd^.arec.r.left, dyLd, tyxyCs, crd^.tyxy);
    ConvertTyxy(xLim, dyLine, tyxyCs, crd^.tyxy);
    IF fTstTld THEN WRITELN('Line width: ', xLim);
    WITH crd^.arec.r DO
          BEGIN
          xOrig := left;
          xCur := left;
          bottom := top;
          END;
    cspTbTot := 0;
    fReqBrk := FALSE;
    iwd := 0;
    fWdFst := TRUE; fSepFst := TRUE;
    rgichWdFst^[0] := 0;
    rgoxWdFst^[0] := 0;
    iwdFstQuad := 0;
    ibTreLim := 0;
    itbLst := 0;
    grptre^.cbPrv := 0;
    atld.dyBase := 0;
    fEndPar := FALSE;
    {$ifc fDbgOk}
    IF fTstTld THEN BEGIN
        PxR('After initcrock, arec.r: ', crd^.arec.r);
        WRITELN('xCur, xOrig: ', xCur, xOrig);
        END;
    {$endc}
    END; { InitCrock }

PROCEDURE NewWd;
    BEGIN
    {$IFC sampcalls}  ALogCall;  {$ENDC}
    rgichWdFst^[iwd] := ich;
    rgoxWdFst^[iwd] := xCur - crd^.arec.r.left;
    IF fTstTld THEN WRITELN('NewWd: ich: ', ich, ', iwd: ', iwd,
                            ', rgoxWdFst: ', rgoxWdFst^[iwd]);
    fWdFst := FALSE; fSepFst := TRUE;
    END;

PROCEDURE NewSep;
    BEGIN
    {$IFC sampcalls}  ALogCall;  {$ENDC}
    rgichSepFst^[iwd] := ich;
    fSepFst := FALSE; fWdFst := TRUE;
    iwd := iwd + 1;
    END;

PROCEDURE EndCrock;
    VAR xLimT, xRtQuad:     TX;
    BEGIN
    {$IFC sampcalls}  ALogCall;  {$ENDC}
    IF iwd = 0 THEN fReqBrk := TRUE;
    IF fReqBrk THEN
          BEGIN
          IF fSepFst THEN NewSep;
          NewWd;
          END;
    atld.ichLim := rgichWdFst^[iwd];
    atld.iwdLim := iwd;
    xLimT := crd^.arec.r.left + rgoxWdFst^[atld.iwdLim];

    IF tbdPrv.quad <> quadL THEN
         BEGIN
         IF tbdPrv.quad = quadJ THEN tbdPrv.quad := quadR;
         Quadify(crd, tbdPrv.quad, xLimT, tbdPrv.x, cspTbTot,
                     ichTbPrv, atld.ichLim, iwdFstQuad, iwd, FALSE);
         END
    ELSE IF (quadPar <> quadL) AND ((quadPar <> quadJ) OR (NOT fReqBrk)) THEN
          BEGIN
          IF quadPar = quadC THEN
               xRtQuad := (xLim + crd^.arec.r.left + rgoxWdFst^[iwdFstQuad]) DIV 2
          ELSE xRtQuad := xLim;
          Quadify(crd, quadPar, xLimT, xRtQuad, cspTbTot, ichTbPrv, atld.ichLim,
                      iwdFstQuad, iwd, crd^.icsSon = icsNil);
          END;
    IF ibTreLim > 0 THEN
          BEGIN
          tre := POINTER(ORD(grptre) + ibTreLim);
               REPEAT
               tre := POINTER(ORD(tre) - tre^.cbPrv);
               ibTreLim := ibTreLim - tre^.cb;
               UNTIL (tre^.cbPrv = 0) OR
                     ((tre^.ichFst < atld.ichLim) AND (NOT tre^.arce.fVan));
          { tre is now treLst or treLim. }
          ibTreLim := ibTreLim + tre^.cb;
          dch := atld.ichLim - tre^.ichFst;
          lpT := lpFst + tre^.dlpFst + dch;
          END
    ELSE lpT := lpFst;
    atld.ibTreLim := ibTreLim;
    tre := POINTER(ORD(grptre) + ibTreLim);
    { tre is now treLim. }
    tre^.cb := 0;
    tre^.ichFst := atld.ichLim + 1;  { "+1" is for speed in GenImgTld }

    crd^.arec.dlpLim := lpT - lpFst;
    lpT := lpdStd^.lp;
    dch := ich-achadTld.ichFst;
    lpT := lpT + dch;
    IF NOT fReqBrk THEN
         { We looked at the next character to decide to break the line, so...}
         lpT := lpT + 1;
    crd^.arec.strec.setfixed := fixedLpFst + fixedLpLim;
    crd^.arec.strec.fParSt := atld.fParSt;
    crd^.arec.strec.fFormFeed :=
      ((atld.ichLim > 0) AND (achadTld.rgch^[atld.ichLim - 1] = chFormFeed));
    crd^.tyloc := tylocLstSib;
    crd^.arec.ointDep := OintMark(crd^.arec.ointDep, ics, lpFst, lpT, TRUE,
                                  ointNil);
    WITH crd^.arec.r DO
          BEGIN
          IF fEndPar THEN dyT := dyLd ELSE dyT := dyLine;
          dyT := CMax(0, dyT - cbitSubMax);
          IF crd^.icsSon = icsNil THEN
               BEGIN
               right := left + rgoxWdFst^[atld.iwdLim];
               bottom := bottom + dyT;
               ConvertTyxy(right, bottom, crd^.tyxy, crd^.arec.tyxy);
               ConvertTyxy(left, xOrYDummy, crd^.tyxy, crd^.arec.tyxy);
               END
          ELSE BEGIN
               crdSon := argcrd[crd^.icrd + 1];
               WITH crdSon^ DO
                 BEGIN
                 OffsetRect(arec.r, left - arec.r.left, -arec.r.top);
                 ConvertRTyxy(arec.r, arec.r, crd^.tyxy, crd^.arec.tyxy);
                 arec.tyxy := crd^.arec.tyxy;
                 ptOrigTopLft := arec.r.topLeft;
                 END;
               crd^.arec.r := crdSon^.arec.r;

               { For crazy clipping }
               xT := xLim;
               ConvertTyxy(xT, xOrYDummy, crd^.tyxy, crd^.arec.tyxy);
               IF xT < right THEN right := xT;
               xT := xOrig;
               ConvertTyxy(xT, xOrYDummy, crd^.tyxy, crd^.arec.tyxy);
               IF xT > left THEN left := xT;
               END;
          END;
    END; {EndCrock}

BEGIN {GenCrdTld}
{$IFC sampcalls}  ALogCall;  {$ENDC}
crd^.tyxy := TyxyTld(ics);
crd^.arec.tyxy := tyxyArec;
InitCrock;
{   lfntid.dev := DevFromIcs(ics);                     removed, gdf 2/10/84 }
seteface := [];
FillInLFntId (ics, argfam[0], seteface, lfntid);              { gdf 2/10/84 }
WHILE TRUE DO
    BEGIN
    rce := lpdStd^.rce;
    ichLim := achadTld.ichLim;
    cbTre := cbOvhAtre + rce^.cb;
    IF (ich = ichLim) OR ((ibTreLim + cbTre + cbOvhAtre) > cbGrptre) THEN
          BEGIN
          { fReqBrk := TRUE; }
          IF (iwd > 1) THEN
               BEGIN
               IF fWdFst THEN iwd := iwd - 1;
               END
          ELSE IF (iwd = 1) THEN
               BEGIN
               IF fWdFst THEN fReqBrk := TRUE;
               END
          ELSE {IF iwd = 0 THEN} fReqBrk := TRUE;
          GOTO 10;
          END;
    crd^.arec.strec.fKeep := crd^.arec.strec.fKeep OR rce^.fKeep;
    tre := POINTER(ORD(grptre) + ibTreLim);
    WITH tre^ DO
          BEGIN
          dlpFst := lpdStd^.lp - lpFst;
          ichFst := ich;
          cb := cbTre;
          tyfill := tyfillNil;
          ipgCur := ipgNil;
          MoveRgch(@arce, @rce^, rce^.cb);
          IF arce.ifnt > ifntLst THEN arce.ifnt := ifntLst;
          ifnt := arce.ifnt;
          END;

    IF rce^.fVan THEN GOTO 40;

    WITH lfntid DO BEGIN
        fam := argfam[ifnt];
        seteface := [];
        tre := POINTER(ORD(grptre) + ibTreLim);
        WITH tre^ DO BEGIN
            IF arce.fBold      THEN seteface := seteface + [bold];
            IF arce.fItalic    THEN seteface := seteface + [italic];
            END;
        END;
    if fTstTld then
       WRITELN('gencrdtld edev: ', ord(lfntid.edev));   { gdf 2/13/84 }
    lfntidT := lfntid;
    IF {NOT} FmFontMetrics(lfntid, finfo, cError) THEN BEGIN END;
    { figure the superscript/subscript height needed }
    { only the sign of cbSuper is significant }
    { it should be a tri-state enumeration}
    { we superscript by 50% of the ascent and subscript by the descent }
    cbSuper := rce^.cbSuperScript;
    IF cbSuper <> 0 THEN
         BEGIN
         IF cbSuper > 0 THEN
              cbSuper := finfo.ascent DIV 2
         ELSE cbSuper := -finfo.descent;
         END;
    tre := POINTER(ORD(grptre) + ibTreLim);
    tre^.dySuper := cbSuper;
    IF lfntid.fam <> lfntidT.fam THEN
         BEGIN
         IF fTstTld THEN
              WRITE('Font sustitution - fam: ', lfntidT.fam, ' -> ',
              lfntid.fam);
         tre^.arce.ifnt := argifnt[lfntid.fam];
         END;
    tre^.arce.fItalic := italic IN lfntid.seteface;
    IF {NOT} FmFontWidths(lfntid, hmpccwid, dxExtra, cError) THEN BEGIN END;

    ibTreLim := ibTreLim + cbTre;
    tre := POINTER(ORD(grptre) + ibTreLim);
    tre^.cbPrv := cbTre;
    tre^.cb := 0;

    dyAboveBase := finfo.ascent;
    cbitSuperMax := CMax(cbSuper, cbitSuperMax);
    cbitSubMax := CMax(-cbSuper, cbitSubMax);
    IF cbSuper > 0 THEN dyAboveBase := dyAboveBase + cbSuper;
    dyNeeded := dyAboveBase - atld.dyBase;

    {$ifc fDbgOk}
    IF fTstTld THEN
        BEGIN
        WRITELN('dyNeeded 0: ', dyNeeded); PxR('crd^.arec.r 0: ', crd^.arec.r);
        END;
    {$endc}
    WITH crd^.arec.r DO
          BEGIN
          IF dyNeeded > 0  THEN
               BEGIN
               (******************)
               bottom := bottom + dyNeeded;
               atld.dyBase := dyAboveBase;
               (******************)
               (******************
               IF atld.dyBase = 0
                 THEN bottom := bottom + dyNeeded
                 ELSE top := top - dyNeeded;
               atld.dyBase := dyAboveBase;
               *******************)
               END;
          dyBelowBase := finfo.descent + finfo.leading + CMax(0, -cbSuper);
          dyNeeded := dyBelowBase - ((bottom - top) - atld.dyBase);
          IF dyNeeded > 0 THEN bottom := bottom + dyNeeded;
          END;
    {$ifc fDbgOk}
    IF fTstTld THEN
        BEGIN
        WRITELN('dyNeeded 1: ', dyNeeded); PxR('crd^.arec.r 1: ', crd^.arec.r);
        END;
    {$endc}

(******************************************************************************
*)
    REPEAT
          chch1.ch := achadTld.rgch^[ich];
          IF fTstTld THEN
               CASE ORD(chch1.ch) OF
                chTab:     WRITE('<tab>');
                chSp:      WRITE('<sp>');
                chCr:      WRITE('<cr>');
                OTHERWISE  WRITE(chch1.ch);
                END;
          dx := hmpccwid^^[chch1.ch] + dxExtra;

          CASE argtych[chch1.ch] OF
          tychTab:
               BEGIN
               IF fTstTld THEN WRITELN;
               IF fSepFst THEN NewSep;
                { Quadify the previous tab, since it ends here. }
               IF tbdPrv.quad <> quadL THEN
                 BEGIN
                 IF tbdPrv.quad = quadJ THEN tbdPrv.quad := quadR;
                 Quadify(crd, tbdPrv.quad, xCur, tbdPrv.x,
                       cspTbTot, ichTbPrv, ich, iwdFstQuad, iwd, FALSE);
                 IF fTstTld THEN
                       WRITELN(' xCur:', xCur:4, '; oxWdLim:',
                       rgoxWdFst^[iwd]:4);
                 END;
               DxTbdFromX(crd, xCur, dx, tbd, itbLst, @arpe);
               trePrv := POINTER(ORD(grptre) + ibTreLim);
               trePrv := POINTER(ORD(trePrv) - trePrv^.cbPrv);
               IF tbd.tyfill <> trePrv^.tyfill THEN
                 BEGIN
                 IF NOT FAddTreProp(tbd.tyfill, ipgNil, ich, ibTreLim) THEN
                       BEGIN
                       fReqBrk := TRUE;
                       GOTO 10;
                       END;
                 END;
               cspTbTot := 0;
               iwdFstQuad := iwd;
               tbdPrv := tbd;
               ichTbPrv := ich;
               GOTO 30;
               END;
          tychSp:
               BEGIN
               IF quadPar = quadJ THEN dx := (2 * dx + 2) DIV 3; {minimum space width for
                                                       justified line is 1/2 !2/3! med
                                                       space}
               cspTbTot := cspTbTot + 1;
               END;
          tychCr:
               BEGIN
               fReqBrk := TRUE;
                { Little trick to save check in main loop }
               ichLim := ich + 1;
               fEndPar := TRUE;
               IF rce^.icsFig <> icsNil THEN
                    BEGIN
                    icsSon := rce^.icsFig;
                    lpSon := rce^.lpFig;
                    crd^.icsSon := icsSon;
                    crdSon := argcrd[crd^.icrd + 1];
                    GenCrd(crdSon, icsSon, lpSon, crd^.tyxy);
                    dx := crdSon^.arec.r.right - crdSon^.arec.r.left;
                    { horizontal scaling to margins...
                    dx := xLim - acrd.arec.r.left;
                    rPic.right := rPic.left + dx;
                    }
                    END;
               END;
          tychPeriod:
               BEGIN
               IF fWdFst THEN NewWd;
               IF (xCur + dx > xLim) THEN IF (ich > 0) THEN GOTO 10;
               IF (tbdPrv.quad = quadJ) AND (NOT tbdPrv.fDecimalComma) THEN
                 BEGIN
                 IF fTstTld THEN WRITELN;
                 Quadify(crd, quadR, xCur, tbdPrv.x, cspTbTot,
                                ichTbPrv, ich, iwdFstQuad, iwd, FALSE);
                 cspTbTot := 0;
                 iwdFstQuad := iwd;
                 tbdPrv.quad := quadL;
                 IF fTstTld THEN
                       WRITELN(' xCur:', xCur:4, '; oxWdLim:', rgoxWdFst^[iwd +
                       1]:4);
                 END;
               GOTO 30;
               END;
          tychComma:
               BEGIN
               IF fWdFst THEN NewWd;
               IF (xCur + dx > xLim) THEN IF (ich > 0) THEN GOTO 10;
               IF (tbdPrv.quad = quadJ) AND tbdPrv.fDecimalComma THEN
                 BEGIN
                 IF fTstTld THEN WRITELN;
                 Quadify(crd, quadR, xCur, tbdPrv.x, cspTbTot,
                                ichTbPrv, ich, iwdFstQuad, iwd, FALSE);
                 cspTbTot := 0;
                 iwdFstQuad := iwd;
                 tbdPrv.quad := quadL;
                 IF fTstTld THEN
                       WRITELN(' xCur:', xCur:4, '; oxWdLim:', rgoxWdFst^[iwd +
                       1]:4);
                 END;
               GOTO 30;
               END;
          tychFormFeed:
               BEGIN
               fReqBrk := TRUE;
               ichLim := ich + 1;  { Little trick to save check in main loop }
               IF fWdFst THEN NewWd;
               GOTO 30;
               END;
          tychIpg:
               BEGIN
               IF fTstTld THEN
                 BEGIN WRITELN; WRITELN('tychIpg - ipgCur:', ipgCur:4); END;
               IF fWdFst THEN NewWd;
               IF ipgCur <> ipgNil THEN
                 BEGIN
                 IF FAddTreProp(tyfillNil, ipgCur, ich, ibTreLim) THEN
                       BEGIN
                       CToSp(ipgCur + 1, spIpg);
                       dx := hmpccwid^^[ORD('0')] * LENGTH(spIpg);
                       END
                 ELSE BEGIN
                       fReqBrk := TRUE;
                       GOTO 10;
                       END;
                 END;
               IF (xCur + dx > xLim) THEN IF (ich > 0) THEN GOTO 10;
               GOTO 30;
               END;
          tychDft:
               BEGIN
               IF fWdFst THEN NewWd;
               IF (xCur + dx > xLim) THEN IF (ich > 0) THEN GOTO 10;
               GOTO 30;
               END;
          END;
          { all separators, except tab, come here }
          IF fSepFst THEN NewSep;
30:           { non-separators skip to here };
          mpichdx^[ich] := dx;
          ich := ich + 1;
          xCur := xCur + dx;
          UNTIL ich = ichLim;
(*
**********************************************************************)

(*          RptLoop (@argtych, mpichdx, rgichSepFst, rgichWdFst, rgoxWdFst,
achadTld,
                   acrd, @DxTbdFromX, @Quadify, @FAddTreProp, @CToSp, ipgCur,
                   grptre);
          if fOut then goto 10; *)

40:           { Next tre: goes directly here if vanished. }
    IF fReqBrk THEN
          GOTO 10
    ELSE BEGIN
          lpdStd^.tyset.fRpe := FALSE;
          achadTld.ichLim := CMin(ich + cchSeqTld, ichLimTld);
          IF achadTld.ichLim > ich THEN
               BEGIN
               achadTld.ichFst := ich;
               SeqLpd(lpdStd, achadTld);
               END;
          {$ifc fDbgOk}
          IF fTstTld THEN
               BEGIN
               WRITELN('GenCrdTld -- SeqLpd returns:');
               PxAchad(achadTld);
               END;
          {$endc}
          END;
    END;
10:
EndCrock;
END;


ÿ