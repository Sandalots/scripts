{$I APLW/COMPFLAGS }                                            {gdf 1/16/84}

{$S SegCs}
{$R-}

UNIT UnitKid;

(* Copyright 1983, 1984, Apple Computer Inc. *)

{ gdf  2/27/84 -- insert trace calls for segmentation  }
{ gdf  1/16/84  -- printing compile flag & include COMPFLAGS added }
{ tjm  4/05/83  --  RemoveKid(fSaveRce) - (WP 258) }
{ tjm  3/11/83  --  VerifyArceKid (WP 245) }
{ crc 11/15/82  --  Backspace, Forwardspace --> LBackspace, LForwardspace }
{ crc 10/29/82  --  Word recognition; replaced LpFstWdP with LpFstPWd, modified
                    Backspace and Forwardspace, modified LpPrvVis to return -1
                    when no visible chars are found}
{ jrm  9/14/82  --  use LpLastCr in FSetKid }
{ jrm  6/29/82  --  USES changes: LisaGraf => QuickDraw; moved USES UnitFile }
{ crc  6/02/82  --  Conditional compilation }
{ crc 05/25/82  --  USES PrStd added }
{ jrm  5/25/82  --  removed lpLimUndo references }
{ crc 04/07/82  --  integration with new font manager }
{ bas 12/10/81  --  integration with font manager }

INTERFACE

USES
     {$IFC sampcalls}                                               {gdf 2/24/84}
        {$U aplw/tracecalls.obj} tracecalls,
     {$ENDC}
     {$U Obj:UnitStd.Obj   } UnitStd,
     {$U Obj:UnitHz.Obj    } UnitHz,
     {$U LibOS/Syscall.Obj }   Syscall,
     {$U Obj:UnitFile.Obj  } UnitFile,
     {$U Obj:Storage.Obj   } Storage,
     {$U Obj:QuickDraw.Obj } QuickDraw,
     {$U Obj:FontMgr.Obj   } FontMgr,
     {$U Obj:PmDecl        } PmDecl,
     {$IFC fPrintOld}                                               {gdf 1/16/84}
        {$U Obj:PrStd.Obj }     PrStd,
     {$ELSEC}
        {$U Obj:PrStdInfo.Obj } PrStdInfo,
          (* {$U Obj:PrPublic.Obj }  PrPublic,  unneeded ? *)
     {$ENDC}                                                        {gdf 1/16/84}
     {$U Obj:WM.Events     } Events,
     {$U Obj:WM.Folders    } Folders,
     {$U Obj:WmlStd     }    WmlStd,
     {$U Obj:UnitFmt.Obj   } UnitFmt,
     {$U Obj:UnitCs.Obj    } UnitCs,
     {$U Obj:UnitFf.Obj    } UnitFf,
     {$U Obj:WM.Menus.Obj  } Menus,
     {$U Obj:AlertMgr.Obj  } AlertMgr,
     {$U ApLW/Queue.Obj     } Queue,
     {$U ApLW/UnitUs.Obj    } UnitUs,
     {$U ApLW/UnitEd.Obj    } UnitEd,
     {$U ApLW/UnitEdit.Obj  } UnitEdit,
     {$U ApLW/UnitRec.Obj  } UnitRec,
     {$U ApLW/UnitTld.Obj  } UnitTld,
     {$U ApLW/UnitDsp.Obj  } UnitDsp,
     {$U ApLW/UnitSel.Obj  } UnitSel;

CONST
 {$IFC NOT fDbgOk} fTstKid = FALSE; {$ENDC}
 chHyphen = 45;
 chSmallA = 97; chSmallZ = 122;
 chBigA = 65; chBigZ = 90;
 chZero = 48; chNine = 57;

TYPE TKid = RECORD
        hcsed:      THcsed;
        dlpSelIns:  TLp;
        dlpBs:    TLp;
        ointVan:    TC;
        fJustSet:   TF;
        fNewLooks:  TF;
        END;

VAR kid:    TKid;
{$IFC fDbgOk}
  fTstKid: TF;
{$ENDC}
 arceKid: TArce;

PROCEDURE InsChKid (ch: TCh);

PROCEDURE LBackspace (fWord: TF);

PROCEDURE LForwardspace (fWord: TF);
PROCEDURE InitKid (hz: Thz);

PROCEDURE RemoveKid(fSaveRce: TF);

PROCEDURE ApplyKidFop (ointIns: TC; ifld: TC; sval: TW; pval: TPc);

PROCEDURE VerifyArceKid(ointIns: TC);

FUNCTION FFigureAt(ics: TB; lpFst: TLp): TF;

PROCEDURE Ins1ChKid (ch: TCh; fInitChron: TF);

FUNCTION  LpFstPWd (ics: TB; lp: TLp) : TLp;

IMPLEMENTATION

{$IFC fSymOK }
{$D+ }
{$ELSEC }
{$D- }
{$ENDC }

CONST ichMaxScr = 50;

VAR
 ssScrInit: TSs;
 hileKid, hileSel: THile;


{$S LWDebug}
{------------------------------------------}
PROCEDURE PxKid(spHeader: TSp; ointIns: TC);
{------------------------------------------}
VAR
pint: TPint;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
{$ifc fDbgOk}
WRITE(spHeader);
PxInt(ointIns);
WITH kid DO
     BEGIN
     WRITE('   Kid - fJustSet: ', fJustSet);
     WRITELN('       dlpBs: ', dlpBs, '; fNewLooks: ', fNewLooks);
     WRITE('IntVan - ');
     PxInt(ointVan);
     pint := POINTER(ORD(lldInt) + kid.ointVan);
     IF pint^.ics <> icsNil THEN
          PxCs(pint^.ics);
     END;
{$endc}
END;


{$S LWCutPas}
{------------------------------------------}
FUNCTION FFigureAt{ics: TB; lpFst: TLp): TF};
{------------------------------------------}
VAR achadT: TAchad; argchT: TArgch;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
achadT.rgch := @argchT;
achadT.ichFst := 0; achadT.ichLim := 1;
SetLpd(lpdStd, ics, lpFst, tysetStd, achadT);
WHILE lpdStd^.rce^.fVan AND (lpdStd^.lpLim > lpdStd^.lp) DO
     BEGIN
     achadT.rgch := NIL; achadT.ichFst := 0; achadT.ichLim := 1;
     SeqLpd(lpdStd, achadT);
     END;
FFigureAt := (lpdStd^.lpLim > lpdStd^.lp) AND (lpdStd^.rce^.icsFig <> icsNil);
END;


{$S LWType}
FUNCTION FAtScratchLim(iss: TC) : TF;
VAR
ss: TSs;
dlpSs: TLp;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
ss := kid.hcsed^^.argss[iss];
dlpSs := kid.hcsed^^.argss[iss + 1].lp - ss.lp;
FAtScratchLim := (ss.icsSon = ssScrInit.icsSon)
  AND ((ss.lpSon + dlpSs) = ssScrInit.lpSon);
END;

{-------------------------------------}
PROCEDURE SetArceKid(ics: TB; lpFst: TLp; lpLim: TLp);
{-------------------------------------}
LABEL 9;
VAR achad: TAchad;
    chT: TCh;
    lpT: TLp;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
{ Find the looks. }
achad.rgch := NIL; achad.ichFst := 0; achad.ichLim := 1;
SetLpd(lpdStd, ics, lpFst, tysetStd, achad);
WHILE lpdStd^.rce^.fVan DO
     BEGIN
     achad.rgch := NIL; achad.ichFst := 0; achad.ichLim := 1;
     SeqLpd(lpdStd, achad);
     IF lpdStd^.lpLim = lpdStd^.lp THEN
          BEGIN
          MoveRgch(@arceKid, @arceStd, arceStd.cb);
          GOTO 9;
          END;
     END;
MoveRgch(@arceKid, POINTER(ORD(lpdStd^.rce)), lpdStd^.rce^.cb);
IF achad.ichLim <= achad.ichFst THEN GOTO 9;
chT := achad.rgch^[achad.ichFst];
IF (lpFst = lpLim) AND ((chT = chCr) OR (chT = chSp) OR (chT = chTab)) THEN
     BEGIN
     achad.rgch := NIL; achad.ichFst := 0; achad.ichLim := 1;
     lpT := LpPrvVis(ics, lpFst);
     IF lpT >= 0 THEN
          BEGIN
          SetLpd(lpdStd, ics, lpT, tysetStd, achad);
          IF lpdStd^.rce^.fVan THEN
               GOTO 9;
          END;
     END;
MoveRgch(@arceKid, POINTER(ORD(lpdStd^.rce)), lpdStd^.rce^.cb);
9:
arceKid.icsFig := icsNil; arceKid.lpFig := lpNil;
END;

{$S LWStyle}
{-------------------------------------}
PROCEDURE VerifyArceKid{ointIns: TC};
{-------------------------------------}
VAR fOldKidOk: TF;
    pint: TPint;
    intIns: TInt;
    lpFst: TLp; lpLim : TLp;
    icsKid: TB;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
IF ointIns <> ointNil THEN
     BEGIN
     pint := POINTER(ORD(lldInt) + ointIns);
     pint^.fValid := TRUE;
     intIns := pint^;
     END
ELSE intIns.ics := icsNil;
fOldKidOk := FALSE;
pint := POINTER(ORD(lldInt) + kid.ointVan);
icsKid := pint^.ics;
IF icsKid <> icsNil THEN
     BEGIN
     lpFst := pint^.lpFst;
     lpLim := pint^.lpLim;
     IF intIns.ics <> icsNil THEN
          BEGIN
          IF FFindEd(intIns.ics, intIns.lpFst, intIns.lpLim) THEN
               fOldKidOk :=  (icsKid = intIns.ics) AND (intIns.lpLim = lpFst);
          END;
     END;
IF (NOT fOldKidOk) AND (intIns.ics <> icsNil) THEN
     SetArceKid(intIns.ics, intIns.lpFst, intIns.lpLim);
END;


{$S LWInsert}
{-----------------------------------------}
FUNCTION FSetKid(ointIns: TC; fBs: TF; fInitChron: TF): TF;
{-----------------------------------------}
LABEL 99;
VAR
iss, issT:  TC;
rgss:   TRgss;
achad: TAchad;
pcsed:    TPcsed;
pint:   TPint;
lpLimPc, lpFst, lpLim, lpSelIns, lpLast, lpLimUndo: TLp;
intIns, intChron: TInt;
fOldKidOK: TF;
icsKid: TB;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
{$ifc fDbgOk}
IF fTstKid THEN PxKid('FSetKid -- ', ointIns);
{$endc}
IF ointIns <> ointNil THEN
     BEGIN
     pint := POINTER(ORD(lldInt) + ointIns);
     pint^.fValid := TRUE;
     intIns := pint^;
     END
ELSE intIns.ics := icsNil;
pint := POINTER(ORD(lldInt) + kid.ointVan);
icsKid := pint^.ics;
fOldKidOK := (icsKid <> icsNil);
IF fOldKidOk OR (intIns.ics <> icsNil) THEN ShowSel(selCur, tyshowOff);
IF fOldKidOK THEN
     BEGIN
     pint := POINTER(ORD(lldInt) + kid.ointVan);
     lpFst := pint^.lpFst;
     lpLim := pint^.lpLim;
     IF intIns.ics <> icsNil THEN
          BEGIN
          IF FFindEd(intIns.ics, intIns.lpFst, intIns.lpLim) THEN
               BEGIN
               fOldKidOK := (icsKid = intIns.ics) AND (intIns.lpLim = lpFst);
               pcsed := kid.hcsed^;
               iss := IssFromLp(pcsed, lpFst);
               rgss := @pcsed^.argss;
               IF fOldKidOK AND (lpFst < lpLim) AND
                 (fBs OR
                   (FAtScratchLim(IssFromLp(kid.hcsed^, lpFst) - 1) AND
                     (rgss^[iss + 1].lp = lpLim))) THEN
                    BEGIN
                    IF fTstKid THEN WRITELN('          using old kid.');
                    IF lpFst + 1 >= lpLim THEN
                         BEGIN
                         { Add some more lp's to piece. }
                         pcsed := kid.hcsed^;
                         iss := IssFromLp(pcsed, lpLim);
                         rgss := @pcsed^.argss;
                         FOR issT := iss TO pcsed^.issMac DO
                              rgss^[issT].lp := rgss^[issT].lp + ichMaxScr;
                         {$ifc fDbgOk}
                         IF fTstKid THEN
                              BEGIN
                              WRITELN('          adding more lp''s from piece:',
                              iss:4);
                              PxCs(intIns.ics);
                              END;
                         {$endc}
                         fNoInvalidate := TRUE;
                         AdjustLps(icsKid, lpLim, lpLim, ichMaxScr);
                         fNoInvalidate := FALSE;
                         pint := POINTER(ORD(lldInt) + kid.ointVan);
                         pint^.lpLim := pint^.lpLim + ichMaxScr;
                         END;
                    GOTO 99;
                    END;
               END;
         END;
    IF lpFst < lpLim THEN Replace(icsKid, lpFst, lpLim, icsNil, 0, 0);
    END;

{IF insertion interval is invalid it is because intVan intersected
intIns and intIns.lpLim must be adjusted
}
IF (intIns.ics <> icsNil) THEN
     BEGIN
     pint := POINTER(ORD(lldInt) + ointIns);
     intIns := pint^;
     IF NOT intIns.fValid THEN
          BEGIN
          intIns.lpLim := LpMaxP(intIns.lpFst, intIns.lpLim  - (lpLim - lpFst));
          intIns.lpFst := LpMinP(intIns.lpFst, intIns.lpLim);
          END;
     lpSelIns := intIns.lpFst;
     IF FFindEd(intIns.ics, intIns.lpFst, intIns.lpLim) THEN
          BEGIN
          kid.hcsed := POINTER(ORD(rghcs^[intIns.ics]));
          icsKid := intIns.ics;
          kid.dlpSelIns := lpSelIns - intIns.lpFst;

          { Record the sel change. }
          IF fInitChron THEN InitChronicle;
          hileSel := hileLim;
          pint := POINTER(ORD(lldInt) + ointIns);
          intChron := pint^;
          Chronicle(intChron.ics, intChron.lpFst, intChron.lpFst,
                    intChron.ics, intChron.lpFst, intChron.lpLim, hopSelect);

          { IF kid.hcsed^^.fNeedCr THEN
               BEGIN
               lpLast := LpLastCr(intIns.ics);
               IF (intIns.lpLim > lpLast) THEN
                    BEGIN
                    pint := POINTER(ORD(lldInt) + ointIns);
                    pint^.lpLim := pint^.lpLim - (intIns.lpLim - lpLast);
                    pint^.lpFst := LpMinP(pint^.lpFst, pint^.lpLim);
                    intIns.lpLim := lpLast;
                    intIns.lpFst := LpMinP(intIns.lpFst, intIns.lpLim);
                    END;
               END;
          }

          IF NOT fOldKidOk THEN
               SetArceKid(intIns.ics, intIns.lpFst, intIns.lpLim);

          lpLimUndo := LpMacEd(icsUndo);
          Replace(icsUndo, lpLimUndo, lpLimUndo,
                       intIns.ics, intIns.lpFst, intIns.lpLim);

          { Record the replacement of the selection contents. }
          hileKid := hileLim;
          lpLim := intIns.lpFst + ichMaxScr;
          Chronicle(intIns.ics, intIns.lpFst, intIns.lpFst, icsUndo, lpLimUndo,
                    lpLimUndo + (intIns.lpLim - intIns.lpFst), hopSwitch);

          { Make room for the insertion pieces. }
          iss := IssReplace1(intIns.ics, intIns.lpFst, intIns.lpLim, 2, ichMaxScr);
          pcsed := kid.hcsed^;
          rgss := @pcsed^.argss;

          { Fill them in. }
          rgss^[iss] := ssScrInit;
          rgss^[iss].lp := intIns.lpFst;
          iss := iss + 1;
          rgss^[iss] := ssScrInit;
          rgss^[iss].lp := intIns.lpFst + 1;
          rgss^[iss].hfcc := NIL;

          { The remainder of kid's fields. }
          kid.dlpBs := 0;
          kid.fNewLooks := TRUE;    { TRUE if we need to set the looks. }
          END
     ELSE intIns.ics := icsNil;
     END;
kid.ointVan := OintMark(kid.ointVan, intIns.ics, intIns.lpFst, lpLim, TRUE,
  ointNil);
kid.fJustSet := TRUE;
99:
{$ifc fDbgOk}
IF fTstKid THEN PxKid('', ointIns);
{$endc}
FSetKid := (intIns.ics <> icsNil);
END;

{------------------}
PROCEDURE RemoveKid{fSaveRce: TF};
{------------------}
VAR f: TF;
    pint: TPint;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
IF fSaveRce THEN
     BEGIN
     pint := POINTER(ORD(lldInt) + kid.ointVan);
     IF pint^.ics <> icsNil THEN
          BEGIN
          Replace(pint^.ics, pint^.lpFst, pint^.lpLim, icsNil, 0, 0);
          pint := POINTER(ORD(lldInt) + kid.ointVan);
          pint^.lpLim := pint^.lpFst;
          END;
     END
ELSE f := FSetKid(ointNil, FALSE, TRUE);
ShowSel(selCur, tyshowOn);
END;

{$S LWType}
{--------------------------------------------}
PROCEDURE SwapFwd(VAR issBs: TC; rgss: TRgss);
{--------------------------------------------}
VAR ssT: TSs; lpT: TLp;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
issBs := issBs + 1;
ssT := rgss^[issBs];
ssT.fVan := FALSE;
ssT.lp := rgss^[issBs - 1].lp;
lpT := rgss^[issBs].lp;
rgss^[issBs] := rgss^[issBs - 1];
rgss^[issBs].lp := lpT;
rgss^[issBs - 1] := ssT;
END;

{--------------------------------------}
PROCEDURE SetInts(lp: TLp);
{--------------------------------------}
VAR pint: TPint;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
pint := POINTER(ORD(lldInt)+kid.ointVan);
pint^.lpFst := lp;
pint^.fValid := TRUE;
pint := POINTER(ORD(lldInt)+selCur.oint);
LSetSel(pint^.ics, lp + kid.dlpSelIns, lp + kid.dlpSelIns);
hileKid^.lpLim1 := lp;
hileSel^.lpFst1 := lp;
hileSel^.lpLim1 := lp;
ShowSel(selCur, tyshowOn);
END;

{----------------------------------------------------}
PROCEDURE Ins1ChKid {ch: TCh; fInitChron: TF};
{----------------------------------------------------}
VAR rgss:   TRgss;
    pcsed:  TPcsed;
    issIns: TC;
    lpScr, lpIns: TLp;
    pint:  TPint;
    hcsus: THcsus;
    icsUs: TB;
    achad: TAchad;
    hzKid: THz;
    cb: TC;
    icsKid: TB;

    PROCEDURE SetHfcc(tydst: TTydst; rcpeNew: TP);
    VAR hfccSs, hfcc: THfcc;
        rce: TRce;
        rpe: TRpe;
        arcpeOld: TArpe;
        ifbcT: TC;
    BEGIN
    {$IFC sampcalls}  ALogCall;  {$ENDC}
    IF fTstKid THEN WRITELN('SetHfcc.');
    IF tydst = tydstRce THEN
          BEGIN
          MoveRgch(@arcpeOld, @arceStd, arceStd.cb);
          rce := @arcpeOld;
          rpe := NIL;
          END
    ELSE BEGIN
          MoveRgch(@arcpeOld, @arpeStd, arpeStd.cb);
          rce := NIL;
          rpe := @arcpeOld;
          END;
    hfccSs := rgss^[issIns].hfcc;
    IF hfccSs <> NIL THEN
          ifbcT := IfbcLstApplyRgfbc(@hfccSs^^.argfbc, rpe, rce);
    hfcc := POINTER(ORD(HAllocate(hzKid, cbHfccInit)));
    GenFopsDiff(hfcc, tydst, @arcpeOld, rcpeNew, hzKid);
    IF hfcc^^.argfbc[0] = fopcEnd THEN
          FreeBk(hzKid, POINTER(ORD(hfcc)), tybkStd)
    ELSE BEGIN
          IF hfccSs = NIL THEN
               BEGIN
               hfcc^^.cref := 1;
               hfccSs := hfcc;
               END
          ELSE BEGIN
               AddRgfbc(hfccSs, hfcc, hzKid);
               FreeBk(hzKid, POINTER(ORD(hfcc)), tybkStd);
               END;
          kid.hcsed^^.argss[issIns].hfcc := hfccSs;
          END;
    IF fTstKid THEN CheckHfccs;
    END;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
IF FSetKid(selCur.oint, FALSE, fInitChron) THEN
     BEGIN
     { Put the character into the string ics. }
     lpScr := ssScrInit.lpSon;
     ssScrInit.lpSon := lpScr + 1;
     icsUs := ssScrInit.icsSon;
     hcsus := POINTER(ORD(rghcs^[icsUs]));
     kid.dlpBs := 0;

     { Add ichMaxScr more chars to icsUs, adjust icsEd by that many. }
     IF lpScr >= hcsus^^.ichMac THEN
           ChangeIchMacUs(icsUs, hcsus^^.ichMac + ichMaxScr);

     hcsus^^.argch[lpScr] := ch;

     pint := POINTER(ORD(lldInt) + kid.ointVan);
     icsKid := pint^.ics;
     lpIns := pint^.lpFst;
     pcsed := kid.hcsed^;
     hzKid := pcsed^.cs.hz;
     issIns := IssFromLp(pcsed, lpIns);
     lpIns := lpIns + 1;
     rgss := @pcsed^.argss;
     IF kid.fJustSet THEN
          BEGIN
          { FSetKid starts backward for Backspace. }
          SwapFwd(issIns, rgss);
          kid.fJustSet := FALSE;
          rgss^[issIns].lp := lpIns;
          END
     ELSE BEGIN
          rgss^[issIns].lp := lpIns;
          IF kid.fNewLooks THEN
               BEGIN
               issIns := IssReplace1(icsKid, lpIns-1, lpIns-1, 0, 0) + 1;
               rgss := @kid.hcsed^^.argss;
               END;
          END;
     issIns := issIns - 1;
     IF kid.fNewLooks THEN
          BEGIN
          IF rgss^[issIns].lp <> lpIns - 1 THEN Break(41);
          SetHfcc(tydstRce, @arceKid);
          kid.fNewLooks := FALSE;
          END;

     AdjustLps(icsKid, lpIns - 1, lpIns, 0);
     SetInts(lpIns);

     rgss := @kid.hcsed^^.argss;
     IF (ch = chCr) OR (ch = chFormFeed) THEN
          BEGIN
          rgss^[issIns].dlpLstParSt := 0;
          { If no carriage return so far, put one in. }
          IF rgss^[issIns].dlpFstParSt = dlpFstNil THEN
               rgss^[issIns].dlpFstParSt := lpIns - rgss^[issIns].lp;
          achad.rgch := NIL;
          pint := POINTER(ORD(lldInt) + kid.ointVan);
          SetLpd(lpdStd, pint^.ics, pint^.lpLim, tysetRpe, achad);
          rgss := @kid.hcsed^^.argss;
          SetHfcc(tydstRpe, POINTER(ORD(lpdStd^.rpe)));
          END
     ELSE WITH rgss^[issIns] DO
             IF dlpFstParSt <> dlpLstNil THEN dlpLstParSt := dlpLstParSt - 1;
     END;
END;

{----------------------------------------------------}
PROCEDURE InsChKid { (ch: TCh) };
{----------------------------------------------------}
VAR pint: TPint;
    intSel: TInt;
    fT: TF;
    fSelNil: TF;
    lpT, lpLimAtom: TLp;
    fInitChron: TF;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
IF fTstKid THEN WRITELN('InsChKid(ch: ', ORD(ch), ')');
pint := POINTER(ORD(lldInt) + selCur.oint);
intSel := pint^;
fSelNil := (intSel.lpFst = intSel.lpLim);
fInitChron := TRUE;
IF (NOT fSelNil) THEN
     BEGIN
     IF (ch <> chCr) THEN
          BEGIN
          FindLpFstPar(intSel.ics, intSel.lpFst, lpT);
          IF intSel.lpFst = lpT THEN
               BEGIN
               FindLpFstPar(intSel.ics, intSel.lpLim, lpT);
               IF intSel.lpLim = lpT THEN
                    BEGIN
                    fT := FSelLpBounds(intSel.lpLim - 1, intSel.ics,
                                       lpT, lpT, lpT, lpLimAtom);
                    pint := POINTER(ORD(lldInt) + selCur.oint);
                    pint^.lpLim := lpLimAtom;
                    intSel.lpLim := lpLimAtom;
                    END;
               END;
          END;
     IF FFigureAt(intSel.ics, intSel.lpLim) THEN
          BEGIN
          Ins1ChKid(chCr, TRUE);
          LSetSel(intSel.ics, intSel.lpFst, intSel.lpFst);
          fInitChron := FALSE;
          END;
     END;
Ins1ChKid(ch, fInitChron);
{$ifc fDbgOk}
IF fTstKid THEN PxKid('Exit InsChKid -', selCur.oint);
{$endc}
END;


{--------------------------------------------------------------------}
PROCEDURE ApplyKidFop { (ointIns: TC; ifld: TC; sval: TW; pval: TPc) };
{--------------------------------------------------------------------}
VAR cbDummy: TC;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
IF FSetKid(ointIns, FALSE, TRUE) THEN
     BEGIN
     cbDummy := CfbcApplyIfld(ifld, sval, pval, NIL, @arceKid);
     kid.fNewLooks := TRUE;
     END;
END;


{-----------------------------------------}
FUNCTION LpFstPWd {(ics: TB; lp: TLp) : TLp};
{-----------------------------------------}
LABEL
   1;
CONST
   fBackward=TRUE;  fForward=FALSE;
   ichLeft=0;  ichThis=1;  ichRight=2;
VAR
   lpLeft,lpThis,lpRight: TLp;
   achad: TAchad;
   argch: ARRAY[ichLeft..ichRight] OF TCh;
   ich: TC;
BEGIN
   {$IFC sampcalls}  ALogCall;  {$ENDC}
   achad.rgch := @argch;
   {Characters that don't exist act as blanks}
   FOR ich:=ichLeft TO ichRight DO argch[ich] := ORD(' ');

   {Get the character at the insertion point}
   achad.ichFst := ichRight;  achad.ichLim := achad.ichFst+1;
   lpRight  := LpVisAdjacent(ics, lp-1, fForward, achad);

   {Get the character to the LEFT of the insertion point}
   achad.ichFst := ichThis;  achad.ichLim := achad.ichFst+1;
   lpThis  := LpVisAdjacent(ics, lp, fBackward, achad);

   {Get the second character to the LEFT of the insertion point}
   achad.ichFst := ichLeft;  achad.ichLim := achad.ichFst+1;
   lpLeft  := LpVisAdjacent(ics, lpThis, fBackward, achad);

   {find first qualifying position to left of selection}
   WHILE lpLeft < lpThis DO
      BEGIN
      achad.ichFst := ichLeft;  achad.ichLim := ichRight+1;
      IF FQualifies(@achad, ichThis) THEN GOTO 1;
      achad.rgch^[ichRight] := achad.rgch^[ichThis];
      lpRight := lpThis;
      achad.rgch^[ichThis]  := achad.rgch^[ichLeft];
      lpThis  := lpLeft;
      achad.rgch^[ichLeft]  := ORD(' ');
      {Get the next character to the LEFT}
      achad.ichFst := ichLeft;  achad.ichLim := achad.ichFst+1;
      lpLeft  := LpVisAdjacent(ics, lpLeft, fBackward, achad);
      END;
1:
   REPEAT {find beginning of word}
      achad.rgch^[ichRight] := achad.rgch^[ichThis];
      lpRight := lpThis;
      achad.rgch^[ichThis]  := achad.rgch^[ichLeft];
      lpThis  := lpLeft;
      achad.rgch^[ichLeft]  := ORD(' ');
      {Get the next character to the LEFT}
      achad.ichFst := ichLeft;  achad.ichLim := achad.ichFst+1;
      lpLeft  := LpVisAdjacent(ics, lpLeft, fBackward, achad);
      achad.ichFst := ichLeft;  achad.ichLim := ichRight+1;
   UNTIL NOT FQualifies(@achad, ichThis);

   LpFstPWd := LpRight;
END; {LpFstPWd}

{----------------------------------------------}
PROCEDURE LBackspace { (fWord: TF) };
{----------------------------------------------}
VAR lpT: TLp;
    rgss:   TRgss;
    ssT:    TSs;
    lpBs:       TLp; lpBsOrig: TLp;
    pcsed:    TPcsed;
    issBs:  TC;
    lpPrvBs, lpFstAdj: TLp;
    pint:   TPint;
    achad: TAchad;
    ics: TB;
    fSelNil, fBsEndOfPar: TF;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
pint := POINTER(ORD(lldInt) + selCur.oint);
fSelNil := (pint^.lpFst = pint^.lpLim);
fBsEndOfPar := FALSE;
IF FSetKid(selCur.oint, TRUE, TRUE) THEN
     BEGIN
     pint := POINTER(ORD(lldInt) + kid.ointVan);
     lpBs := pint^.lpFst;
     lpBsOrig := lpBs;
     ics := pint^.ics;
     IF fSelNil THEN
          BEGIN
          IF fWord THEN
               lpT := LpFstPWd(ics, lpBs)
          ELSE
               lpT := LpPrvVis(ics, lpBs);
          IF lpT < 0 THEN lpT := 0;  {no previous visible character}
          IF fTstKid THEN WRITELN('Backspace from lp', lpBsOrig, ' to lp', lpT);
          pcsed := kid.hcsed^;
          issBs := IssFromLp(pcsed, lpBs);
          WHILE lpT < lpBs DO
               BEGIN
               IF FAtScratchLim(issBs - 1) THEN
                    ssScrInit.lpSon := ssScrInit.lpSon - 1;
               rgss := @pcsed^.argss;
               WITH rgss^[issBs - 1] DO
                 BEGIN
                 lpPrvBs := lp;
                 IF dlpLstParSt = 0 THEN
                      fBsEndOfPar := TRUE;
                 END;
               { Set up for undoing this piece, too. }
               IF lpPrvBs < hileKid^.lpFst1 THEN
                    BEGIN
                    IF fTstKid THEN WRITELN('Saving [', lpPrvBs, ' - ', lpBs, ']');
                    Replace(icsUndo, hileKid^.lpFst2, hileKid^.lpFst2,
                            ics, lpPrvBs, lpBs);
                    hileKid^.lpFst1 := lpPrvBs;
                    hileKid^.lpLim2 := hileKid^.lpLim2 + (lpBs - lpPrvBs);
                    IF fTstKid THEN WRITELN('Saved previous piece.');
                     { Replace might change heap around... }
                    pcsed := kid.hcsed^;
                    rgss := @pcsed^.argss;
                    END;
               IF lpPrvBs + 1 < lpBs THEN
                    BEGIN
                     { Move the iss-lp boundary. }
                    lpBs := lpBs - 1;
                    rgss^[issBs].lp := lpBs;
                    END
               ELSE BEGIN
                     { Skip issBs over the iss before it. }
                    ssT := rgss^[issBs - 1];
                    WITH ssT DO
                         BEGIN
                         fVan := TRUE;
                         dlpFstParSt := dlpFstNil;
                         dlpLstParSt := dlpLstNil;
                         lp := lpBs;
                         END;
                    rgss^[issBs - 1] := rgss^[issBs];
                    rgss^[issBs] := ssT;
                    issBs := issBs - 1;
                    lpBs := lpPrvBs;
                    rgss^[issBs].lp := lpBs;
                    END;
               SetIssDlps(kid.hcsed, issBs - 1);
               END;
          kid.dlpBs := kid.dlpBs + (lpBsOrig - lpBs);
          IF fBsEndOfPar THEN
               FindLpFstPar(ics, lpBs, lpFstAdj)
          ELSE lpFstAdj := lpBs;
          AdjustLps(ics, lpFstAdj, lpBsOrig, 0);
          END
     ELSE IF FFigureAt(ics, lpBs) THEN RemoveKid(FALSE);
     SetInts(lpBs);
     END;
{$ifc fDbgOk}
IF fTstKid THEN PxKid('Exit BS -', selCur.oint);
{$endc}
END; {LBackspace}

{-------------------------------------------------}
PROCEDURE LForwardspace { (fWord: TF) };
{-------------------------------------------------}
 { This procedure UNDOES the backspace command, rather than }
 { implementing the equivalent of backspace in the forward }
 { direction: it is a null action if backspace has not happened. }
LABEL 3;
CONST
    fBackward = TRUE;
VAR lpT: TLp;
    rgss:   TRgss;
    ssT:    TSs;
    pcsed:   TPcsed;
    lpFs:       TLp;
    issBs:  TC;
    pint,pintSel: TPint;
    lpFsOrig:        TLp;
    achad: TAchad;
    ich: TC;
    ics,icsSel: TB;
    tyset: TTyset;
    lpFstAdj: TLp;
    fFsEndOfPar: TF;
    chLeft, chThis, chRight: TCh;
    dlpBs: TLp;
    lpDummy: TLp;
    lpFsMax: TLp;
    fChOk: TF;
    fBeyond: TF;

FUNCTION FQualifies(chLeft, chThis, chRight: TCh): TF;
VAR
   i,j: TC;
BEGIN
   {$IFC sampcalls}  ALogCall;  {$ENDC}
   FOR i := 1 TO (LENGTH(spTriples) DIV 4) + 1 DO
      BEGIN
      j := 4*i-2;
      IF ((spTriples[j-1] = CHR(ChClass(chLeft))) OR (spTriples[j-1] = 'x')) AND
         (spTriples[j] = CHR(ChClass(chThis))) AND
         ((spTriples[j+1] = CHR(ChClass(chRight))) OR (spTriples[j+1] = 'x')) THEN
         BEGIN
         FQualifies := TRUE;
         EXIT(FQualifies)
         END;
      END;
   FQualifies := FALSE;
END; {FQualifies}

FUNCTION FChForward(VAR lp,dlp: TLp; VAR ch: TCh):TF;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
REPEAT
   IF dlp > 0 THEN {Uncover next backed-over character}
      BEGIN
      IF FAtScratchLim(issBs - 1) THEN ssScrInit.lpSon := ssScrInit.lpSon + 1;
      rgss := @pcsed^.argss;
      IF (lp+1) < rgss^[issBs + 1].lp THEN rgss^[issBs].lp := lp+1
         ELSE SwapFwd(issBs, rgss);
      SetIssDlps(kid.hcsed, issBs - 1);
      IF rgss^[issBs - 1].dlpLstParSt = 0 THEN fFsEndOfPar := TRUE;
      dlp := dlp-1;
      FChForward := FALSE;
      END
   ELSE FChForward := TRUE;
   achad.rgch := NIL;
   SetLpd(lpdStd, ics, lp, tyset, achad);
   lp := lp+1;
UNTIL (NOT lpdStd^.rce^.fVan) OR (lpdStd^.lpLim = lpdStd^.lp);
IF lpdStd^.lpLim = lpdStd^.lp THEN
   BEGIN
   ch := ORD(' ');
   IF fTstKid THEN WRITE('<>');
   END
ELSE
   BEGIN
   ch := achad.rgch^[achad.ichFst];
   IF fTstKid THEN WRITE(CHR(achad.rgch^[achad.ichFst]));
   END;
END;  {FChForward}

BEGIN  {LForwardspace}
{$IFC sampcalls}  ALogCall;  {$ENDC}
{$ifc fDbgOk}
IF fTstKid THEN PxKid('FS -', selCur.oint);
{$endc}
pint := POINTER(ORD(lldInt) + kid.ointVan);
ics := pint^.ics;
fFsEndOfPar := FALSE;
IF (ics <> icsNil) THEN
     BEGIN
     WITH tyset DO
          BEGIN fRce := TRUE; fRpe := FALSE; fParBnds := FALSE; END;
     lpFs := pint^.lpFst;
     lpFsOrig := lpFs;
     pcsed := kid.hcsed^;
     issBs := IssFromLp(pcsed, lpFs);
     dlpBs := kid.dlpBs;
     IF dlpBs = 0 THEN GOTO 3;  {nothing to forward space over}
     lpFsMax := lpFsOrig + dlpBs;

     {Get the character to the left of the insertion point}
     pintSel := POINTER(ORD(lldInt) + selCur.oint);
     icsSel := pint^.ics;
     achad.rgch := NIL;
     lpDummy := LpVisAdjacent(icsSel, pintSel^.lpFst, fBackward, achad);
     IF achad.ichLim = achad.ichFst THEN chThis := ORD(' ')
        ELSE chThis := achad.rgch^[achad.ichFst];
     IF fTstKid THEN WRITELN('Fs preceeding character is ''', CHR(chThis), '''');

     IF fTstKid THEN WRITE('Fs over ''');
     fBeyond := FALSE;
     fChOk := TRUE;
     IF FChForward(lpFs, dlpBs, chRight) THEN Break(837);

     IF fWord THEN
        BEGIN
        WHILE fChOk AND (NOT fBeyond) DO
              BEGIN
              chLeft := chThis;
              chThis := chRight;
              fBeyond := FChForward(lpFs, dlpBs, chRight);
              fChOk := FQualifies(chLeft, chThis, chRight);
              END;

        fChOk := TRUE;  {first non-qualifying character is ok}

        IF (NOT fBeyond) THEN
              REPEAT
                    chLeft := chThis;
                    chThis := chRight;
                    fBeyond := FChForward(lpFs, dlpBs, chRight);
                    fChOk := NOT FQualifies(chLeft, chThis, chRight);
              UNTIL (NOT fChOk) OR fBeyond;
        END;

     {ignore look-ahead beyond vanished chars}
     IF lpFs > lpFsMax THEN lpFs := lpFsMax;
     IF fTstKid THEN WRITELN('''; dlpBs =', dlpBs);
     IF fFsEndOfPar THEN
          FindLpFstPar(ics, lpFsOrig, lpFstAdj)
     ELSE lpFstAdj := lpFsOrig;
     AdjustLps(ics, lpFstAdj, lpFs, 0);
     SetInts(lpFs);
     kid.dlpBs := dlpBs;
     IF NOT fChOk THEN {Some chars un-vanished but rejected}
        LBackspace(TRUE); {backspace to previous non-word/word boundary}
     END;
3:
{$ifc fDbgOk}
IF fTstKid THEN PxKid('Exit FS -', selCur.oint);
{$endc}
END; {LForwardspace}

{$S LWCreate}
{-----------------------------}
PROCEDURE InitKid { (hz: Thz) };
{-----------------------------}
VAR spScr:  TSp;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
spScr := '``````````````````````````````````````````````````````````````````````';
WITH ssScrInit DO
 BEGIN
 icsSon := IcsUsCreate(spScr, hz);
 lpSon := 0;
 fVan := TRUE;
 dlpLstParSt := dlpLstNil;
 dlpFstParSt := dlpFstNil;
 hfcc := NIL;
 END;
kid.ointVan := OintMark(ointNil, icsNil, lpNil, lpNil, TRUE, ointNil);
END;


END.

ÿ