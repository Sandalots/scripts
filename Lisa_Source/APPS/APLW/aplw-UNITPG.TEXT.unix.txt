{$I APLW/COMPFLAGS }                                            {gdf 1/16/84}
{$R-}

UNIT UnitPg;

(* Copyright 1983, 1984, Apple Computer Inc. *)

{ gdf  2/27/84 -- insert trace calls for segmentation  }
{ gdf  1/16/84 -- printing compile flag & include COMPFLAGS added }
{ crc  4/11/83 -- Moved assignment to acrd AFTER call of FEosLpPg }
{ tjm  3/15/83 -- call OboxVisOrec after TytermUpdateRec (WP 252) }
{ crc  1/06/83 -- pass fLayoutPage parm to TytermUpdateRec }
{ jrm 11/15/82 -- set .fSelectable := FALSE for rec before header in GenCrdPg }
{ jrm  6/29/82  --  USES changes: LisaGraf => QuickDraw; moved USES UnitFile }
{ crc  6/02/82 -- Conditional compilation }
{ crc 05/21/82 -- configSizePaperIn6ths replaced by function DptPaperSize,
                  tyxyTld by function TyxyTld }
{ crc 04/06/82 -- integration with new font manager }
{ bas 03/11/82 -- orec tyxy update }
{ bas 01/27/82 -- change (free) tyxyIP to tyxyTld }
{ bas 01/13/82 -- USES for new wm }
{ bas 12/14/81 -- integration with lisaprint (USES + configSizePaperIn6ths) }
{ bas 12/10/81 -- integration with font manager }

INTERFACE

USES
   {$IFC sampcalls}                                               {gdf 2/24/84}
      {$U aplw/tracecalls.obj} tracecalls,
   {$ENDC}
   {$U Obj:UnitStd.Obj  }  UnitStd,
   {$U LibOS/Syscall.Obj }   Syscall,
   {$U Obj:UnitHz.Obj     }  UnitHz,
   {$U Obj:UnitFile.Obj   }  UnitFile,
   {$U Obj:Storage.Obj    }  Storage,
   {$U Obj:QuickDraw.Obj  }  QuickDraw,
   {$U Obj:FontMgr.Obj    }  FontMgr,
   {$U Obj:Wm.Events.Obj  }  Events,
   {$U Obj:Wm.Folders.Obj }  Folders,
   {$U Obj:WmlStd         }  WmlStd,
   {$U Obj:WmlCrs         }  WmlCrs,
   {$U Obj:PmDecl         }  PmDecl,
     {$IFC fPrintOld}                                               {gdf 1/16/84}
        {$U Obj:PrStd.Obj }     PrStd,
        {$U Obj:PrFile.Obj }    PrFile,
        {$U Obj:PrProcs.Obj }   PrProcs,
        {$U Obj:PrMgrUtil.Obj } PrMgrUtil,
        {$U Obj:PrMgr.Obj }     PrMgr,
     {$ELSEC}
        {$U Obj:PrStdInfo.Obj } PrStdInfo,
        {$U Obj:PrPublic.Obj }  PrPublic,
     {$ENDC}                                                        {gdf 1/16/84}
   {$U Obj:UnitFmt.Obj    }  UnitFmt,
   {$U Obj:UnitCs.Obj     }  UnitCs,
   {$U Obj:UnitFf.Obj     }  UnitFf,
   {$U Obj:WM.Menus.Obj   }  Menus,
   {$U Obj:AlertMgr.Obj   }  AlertMgr,
   {$U ApLW/Queue.Obj      }  Queue,
   {$U ApLW/UnitEd.Obj     }  UnitEd,
   {$U ApLW/UnitBox.Obj    }  UnitBox,
   {$U ApLW/UnitRec.Obj    }  UnitRec,
   {$U ApLW/UnitOver.Obj   }  UnitOver,
   {$U ApLW/UnitTld.Obj    }  UnitTld,
   {$U ApLW/UnitPrt.Obj    }  UnitPrt,
   {$U ApLW/UnitDsp.Obj    }  UnitDsp,
   {$U ApLW/UnitSel.Obj    }  UnitSel;

CONST
    {$IFC NOT fDbgOk} fTstPg = FALSE; {$ENDC}
    ptpgFst = 0;
    ptpgBeforeHdr = 0;
    ptpgHdr = 1;
    ptpgBody = 2;
    ptpgFtr = 3;
    ptpgMax = 4;
    cpgMore = 10;

TYPE TBody = PACKED RECORD
           fEos: TF;
           oint: 0..MAXINT;
           END;

   TPgd = RECORD
           pglp: TPglp;
           icsBody: TB;
           icsPageRuler: TB;
           ointBodyAll: TC;
           ointHdr: TC;
           ointFtr: TC;
           ipgLst: TC;
           ipgMax: TC;
           argbody: ARRAY [0..0] OF TBody;
           END;

   TCsPg = RECORD
           cs: TCs;
           pgd: TPgd;
           END;
   TPcspg = ^TCspg;
   THcspg = ^TPcspg;

{$IFC fDbgOk}
VAR
   fTstPg: TF;
{$ENDC}


PROCEDURE PxHcspg         (hcspg: THcspg);
 { There is no SeqLpdPg -- its underlying class is used. }
FUNCTION  IcsPgCreate     (ipgMax: TC; VAR pgd: TPgd; hz: THz) : TB;
PROCEDURE FreePgIcs       (ics: TB);
PROCEDURE GenImgPg        (crd: TCrd);
PROCEDURE GenCrdPg        (crd: TCrd; ics: TB; lpFst: TLp; tyxy: TTyxy);
PROCEDURE FindLpPgFixed   (ics: TB; lp: TLp; VAR lpFixed: TLp);
PROCEDURE FindIpgLst      (ics: TB);
PROCEDURE IncIpgLst       (ics: TB);
FUNCTION  FEosLpPg        (ics: TB; lp: TLp) : TF;
FUNCTION  LpBodyFromLp (icsPg: TB; lpPg: TLp) : TLp;
PROCEDURE ConvertIcsPglpTyxy (icsPg: TB; VAR pglpDst: TPglp; tyxyDst: TTyxy);
PROCEDURE ConvertPglpTyxy (VAR pglpSrc, pglpDst: TPglp; tyxyDst: TTyxy);


IMPLEMENTATION

{$IFC fSymOK }
{$D+ }
{$ELSEC }
{$D- }
{$ENDC }

CONST dxWidPgMax = 1400;      {random larg number}

VAR xOrYDummy: TCd;


{$S LWPage}
PROCEDURE ConvertPglpTyxy {VAR pglpSrc, pglpDst: TPglp; tyxyDst: TTyxy};
 { copy pglpSrc into pglpDst, converted from pglpSrc.tyxy to tyxyDst }
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
pglpDst := pglpSrc;
WITH pglpDst DO BEGIN
    ConvertTyxy(xOrYDummy, yTopHdr,   tyxy, tyxyDst);
    ConvertTyxy(xOrYDummy, dyHdr,     tyxy, tyxyDst);
    ConvertTyxy(xOrYDummy, dyHtBody,  tyxy, tyxyDst);
    ConvertTyxy(xOrYDummy, dyBotBody, tyxy, tyxyDst);
    tyxy := tyxyDst;
    END;
END;


PROCEDURE ConvertIcsPglpTyxy {(icsPg: TB; VAR pglpDst: TPglp; tyxyDst: TTyxy)};
 { copy pglpSrc into pglpDst, converted from pglpSrc.tyxy to tyxyDst }
CONST dyMinHt = 8;
VAR ptPgWidHt:  TPt;
    yCur, dyHtMin: TCd;
    hcspg:      THcspg;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
hcspg := POINTER(ORD(rghcs^[icsPg]));
DptPaperSize(icsPg, tyxyDst, {OUT} ptPgWidHt);
dyHtMin := dyMinHt;
ConvertTyxy(xOrYDummy, dyHtMin, tyxyIp, tyxyDst);
ConvertPglpTyxy(hcspg^^.pgd.pglp, pglpDst, tyxyDst);
WITH pglpDst DO
    BEGIN
    yTopHdr := CMax (0, CMin(yTopHdr, ptPgWidHt.v - 3 * dyMinHt));
    dyHdr := CMax (dyMinHt, CMin(dyHdr, ptPgWidHt.v - 2 * dyMinHt - yTopHdr));
    yCur := yTopHdr + dyHdr;
    dyBotBody := CMax (dyMinHt, CMin(dyBotBody, ptPgWidHt.v - 1 * dyMinHt - yCur));
    dyHtBody := CMax (dyBotBody + dyMinHt, CMin(dyHtBody, ptPgWidHt.v - yCur));
    END;
END;


{$S LWPreview}
FUNCTION LpFstBody(icsPg: TB; ipg: TC) : TLp;
VAR hcspg: THcspg;
    oint: TC;
    pint: TPint;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
hcspg := POINTER(ORD(rghcs^[icsPg]));
WHILE (hcspg^^.pgd.ipgLst < ipg)
 AND (NOT hcspg^^.pgd.argbody[hcspg^^.pgd.ipgLst].fEos) DO
    IncIpgLst(icsPg {, tyxyTld});
IF (hcspg^^.pgd.ipgLst < ipg) THEN
     LpFstBody := LpMacEd(hcspg^^.pgd.icsBody)
ELSE BEGIN
     oint := hcspg^^.pgd.argbody[ipg].oint;
     pint := POINTER(ORD(lldInt) + oint);
     LpFstBody := pint^.lpFst;
     END;
END;

{----------------------------------------------------}
FUNCTION LpBodyFromLp{icsPg: TB; lpPg: TLp) : TLp};
{----------------------------------------------------}
VAR lpOrLppg: TLpOrLppg;
    oint: TC;
    pint: TPint;
    lpBody: TLp;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
FindIpgLst(icsPg);
lpOrLppg.lp := lpPg;
IF lpOrLppg.lppg.ptpg = ptpgFtr THEN
     lpBody := LpFstBody(icsPg, lpOrLppg.lppg.ipg + 1)
ELSE BEGIN
     lpBody := LpFstBody(icsPg, lpOrLppg.lppg.ipg);
     IF lpOrLppg.lppg.ptpg = ptpgBody THEN
          lpBody := lpBody + lpOrLppg.lppg.ich;
     END;
LpBodyFromLp := lpBody;
END;

{$S LWPage}
{-------------------------------}
FUNCTION FEosLpPg{ics: TB; lp: TLp) : TF};
{-------------------------------}
VAR hcspg: THcspg;
  lpOrLppg: TLpOrLppg;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
FindIpgLst(ics);
hcspg := POINTER(ORD(rghcs^[ics]));
lpOrLppg.lp := lp;
WHILE (hcspg^^.pgd.ipgLst < lpOrLppg.lppg.ipg)
 AND (NOT hcspg^^.pgd.argbody[hcspg^^.pgd.ipgLst].fEos) DO
    IncIpgLst(ics {, tyxyTld});
FEosLpPg := (hcspg^^.pgd.ipgLst < lpOrLppg.lppg.ipg);
END;

{-------------------------------}
PROCEDURE FreePgIcs { (ics: TB) };
{-------------------------------}
VAR rgh: TRgh; ipg: TC; hcspg: THcspg;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
hcspg := POINTER(ORD(rghcs^[ics]));
FOR ipg := 1 TO hcspg^^.pgd.ipgMax - 1 DO
   IF hcspg^^.pgd.argbody[ipg].oint <> ointNil THEN
        FreeInt(hcspg^^.pgd.icsBody, hcspg^^.pgd.argbody[ipg].oint);
FreeInt(hcspg^^.pgd.icsBody, hcspg^^.pgd.ointBodyAll);
FreeInt(hcspg^^.pgd.pglp.icsFtr, hcspg^^.pgd.ointFtr);
FreeInt(hcspg^^.pgd.pglp.icsHdr, hcspg^^.pgd.ointHdr);
rgh := POINTER(ORD(rghcs));
FreeBk(rghcs^[ics]^^.hz, rgh^[ics], tybkStd);
rgh^[ics] := hNil;
END;

{----------------------------------------------------------------}
FUNCTION IcsPgCreate { (ipgMax: TC; VAR pgd: TPgd; hz: THz) : TB };
{----------------------------------------------------------------}
VAR hcspg: THcspg; ipg: TC; icsPg: TB;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
pgd.ointBodyAll := OintMark(ointNil, pgd.icsBody, 0, lpMax, TRUE, ointNil);
pgd.ointHdr := OintMark(ointNil, pgd.pglp.icsHdr, 0, lpMax, TRUE, ointNil);
pgd.ointFtr := OintMark(ointNil, pgd.pglp.icsFtr, 0, lpMax, TRUE, ointNil);
pgd.icsPageRuler := icsNil;
pgd.ipgLst := 0;
pgd.ipgMax := ipgMax;
pgd.argbody[0].oint := OintMark(ointNil, pgd.icsBody, 0, lpMax, TRUE, ointNil);
pgd.argbody[0].fEos := FALSE;
icsPg := IcsCreate(tycsPg, SIZEOF(TCspg) + ipgMax * SIZEOF(TC), hz);
WITH rghcs^[icsPg]^^.cspd DO
   BEGIN
   argproc[iprocSeqLpd]       := POINTER(procNil);
   argproc[iprocFindLpFixed]  := @FindLpPgFixed;
   argproc[iprocCrd]          := @GenCrdPg;
   argproc[iprocFreeIcs]      := @FreePgIcs;
   argproc[iprocFSelLpBounds] := @TrueStdSelLpBounds;
   argproc[iprocPxHcs]        := @PxHcspg;
   argproc[iprocHilight]      := POINTER(procNil);
   argimgp[iimgpScrn]         := @GenImgPg;
   argimgp[iimgpQume]         := @GenImgPg;
   END;
procFEosLppg := @FEosLppg;
hcspg := POINTER(ORD(rghcs^[icsPg]));
hcspg^^.pgd := pgd;
FOR ipg := 1 TO ipgMax - 1 DO hcspg^^.pgd.argbody[ipg].oint := ointNil;
IcsPgCreate := icsPg;
END;

{-----------------------------------------------------------------------}
PROCEDURE SetLpsIpgLst(icsPg: TC; lpLimDep: TLp; lpFstNxt: TLp; fEos: TF);
{-----------------------------------------------------------------------}
LABEL   30;
VAR hcspg:    THcspg;
  pcspg:    TPcspg;
  icsSon: TB;
  ointSon: TC;
  ipgLst:   TC;
  ipg:    TC;
  pint:   TPint;
  lpFstPg: TLp;
  lpOrLppg: TLpOrLppg;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
IF fTstPg THEN
   BEGIN
   WRITELN('SetLpsIpgLst(', icsPg:3, '; ', lpLimDep, '; ', lpFstNxt,
     '; ', fEos);
   END;
hcspg := POINTER(ORD(rghcs^[icsPg]));
pcspg := hcspg^;
icsSon := pcspg^.pgd.icsBody;
ipgLst := pcspg^.pgd.ipgLst;
pcspg^.pgd.argbody[ipgLst].fEos := fEos;
IF pcspg^.pgd.icsPageRuler <> icsNil THEN
     BEGIN
     lpOrLppg.lp := 0; LpOrLppg.lppg.ipg := ipgLst;
     AdjustLps(pcspg^.pgd.icsPageRuler, lpOrLppg.lp, lpMax, 0);
     END;
ointSon := pcspg^.pgd.argbody[ipgLst].oint;
ointSon := OintMark(ointSon, icsSon, lpNil, lpLimDep, TRUE, ointNil);
IF NOT fEos THEN
   BEGIN
   pcspg := hcspg^;
   ipgLst := pcspg^.pgd.ipgLst + 1;
   pcspg^.pgd.ipgLst := ipgLst;
   IF fTstPg THEN WRITELN('  ipgLst:', ipgLst:3, '; ipgMax:',
   pcspg^.pgd.ipgMax:3);
   IF ipgLst >= pcspg^.pgd.ipgMax THEN
        BEGIN
        pcspg^.pgd.ipgMax := pcspg^.pgd.ipgMax + cpgMore;
        ChangeSizeBk(pcspg^.cs.hz, POINTER(ORD(hcspg)),
                     SIZEOF(TCspg) + pcspg^.pgd.ipgMax * SIZEOF(TC));
        pcspg := hcspg^;
        FOR ipg := ipgLst TO pcspg^.pgd.ipgMax - 1 DO
                pcspg^.pgd.argbody[ipg].oint := ointNil;
        END;
   ointSon := pcspg^.pgd.argbody[ipgLst].oint;
   IF ointSon = ointNil THEN WITH hcspg^^.pgd.argbody[ipgLst] DO BEGIN
        oint := OintMark(ointSon, icsSon, lpFstNxt, lpMax, TRUE, ointNil);
        fEos := FALSE;
     END
   ELSE BEGIN
        pint := POINTER(ORD(lldInt) + ointSon);
        IF (lpFstNxt = pint^.lpFst) THEN
             BEGIN
             WHILE pint^.fValid AND (ipgLst + 1 < pcspg^.pgd.ipgMax) DO
                  BEGIN
                  ipgLst := ipgLst + 1;
                  ointSon := pcspg^.pgd.argbody[ipgLst].oint;
                  IF ointSon = ointNil THEN GOTO 30;
                  pint := POINTER(ORD(lldInt) + ointSon);
                  END;
30:
             pcspg^.pgd.ipgLst := ipgLst;
             END
        ELSE BEGIN
             lpOrLppg.lp := 0;
             lpOrLppg.lppg.ipg := ipgLst;
             lpOrLppg.lppg.ptpg := ptpgBody;
             lpFstPg := lpOrLppg.lp;
             lpOrLppg.lppg.ptpg := ptpgFtr;
             AdjustLps(icsPg, lpFstPg, lpOrLppg.lp, 0);
             hcspg^^.pgd.argbody[ipgLst].oint :=
               OintMark(ointSon, icsSon, lpFstNxt, lpMax, TRUE, ointNil);
             END;
        END;
   END;
{$ifc fDbgOk}
IF fTstPg THEN PxHcspg(hcspg);
{$endc}
END;

{--------------------------------}
PROCEDURE GenImgPg { (crd: TCrd) };
{--------------------------------}
VAR pint: TPint; orecSon: TC; rec, recSon: TRec; icsPg:  TB; ointT:  TC;
    lpFstPg: TLp; lpOrLppg: TLpOrLppg; ptpg: 0..ptpgMax; hcspg: THcspg;
    foPrnt: TFo; tyterm: TTyterm; oboxPane: TC; r, rT: TR; pglp: TPglp;
    tyxy: TTyxy;
    hrgnT, hrgnSave: THrgn;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
{$ifc fDbgOk}
IF fTstPg THEN
   BEGIN
   WRITELN('GenImgPg(', ORD(crd), ')');
   WRITE('ointSon: '); PxInt(crd^.ointSon);
   END;
{$endc}
pint := POINTER(ORD(lldInt) + crd^.arec.ointDep);
lpOrLppg.lp := pint^.lpFst;
ptpg := lpOrLppg.lppg.ptpg;
icsPg := pint^.ics;
tyxy := TyxyTld(icsPg);
hcspg := POINTER(ORD(rghcs^[icsPg]));
rec := POINTER(ORD(dsp) + crd^.orec);
orecSon := rec^.orecSon;
pint := POINTER(ORD(lldInt) + crd^.ointSon);
ConvertRTyxy(crd^.arec.r, r, crd^.arec.tyxy, tyxy);
ConvertIcsPglpTyxy(icsPg, pglp, tyxy);
IF ptpg = ptpgBeforeHdr THEN
     BEGIN
     rT := r;
     ConvertRTyxy(rT, rT, tyxy, tyxyScrn);
     EraseRect(rT);
     END
ELSE BEGIN
     IF ptpg = ptpgBody THEN
          BEGIN
          rT := r; rT.top := rT.top + pglp.dyBotBody;
          ConvertRTyxy(rT, rT, tyxy, tyxyScrn);
          { Should this be done after the MoveRecs or TytermUpdateRec ? }
          EraseRect(rT);
          r.bottom := CMin(r.bottom, r.bottom + (pglp.dyBotBody - pglp.dyHtBody));
          END;
     IF orecSon <> orecNil THEN
          BEGIN
          recSon := POINTER(ORD(dsp) + orecSon);
          IF recSon^.tyxy <> tyxy THEN
               BEGIN
               EraseOrec(orecSon);
               orecSon := OrecSibFree(orecSon);
               orecSon := orecNil;
               END;
          END;
     IF orecSon = orecNil THEN
          BEGIN
          foPrnt.fObox := FALSE; foPrnt.o := crd^.orec;
          orecSon := Orec1Alloc(crd^.icsSon, pint^.lpFst, pint^.lpLim, r.topLeft,
                       r.botRight, tyxy, crd^.arec.strec.setfixed, TRUE,
                       foPrnt);
          END
     ELSE BEGIN
          recSon := POINTER(ORD(dsp) + orecSon);
          IF recSon^.r.top <> r.top THEN
               BEGIN
               ConvertRTyxy(recSon^.r, rT, recSon^.tyxy, tyxy);
               rT.top := CMin(rT.top, r.top);
               rT.bottom := CMax(rT.bottom, r.bottom);
               ConvertRTyxy(rT, rT, tyxy, tyxyScrn);
               hrgnT := NewRgn;
               RectRgn(hrgnT, rT);
               SectRgn(thePort^.clipRgn, hrgnT, hrgnT);
               hrgnSave := thePort^.clipRgn;
               thePort^.clipRgn := hrgnT;
               MoveRecs(orecSon, r.topLeft, tyxy);
               thePort^.clipRgn := hrgnSave;
               DisposeRgn(hrgnT);

{               recSon := POINTER(ORD(dsp) + orecSon);
               ConvertRTyxy(recSon^.r, rT, recSon^.tyxy, tyxy);
               IF rT.bottom > r.bottom THEN
                    BEGIN
                    rT.top := r.bottom;
                    ConvertRTyxy(rT, rT, tyxy, tyxyScrn);
                    EraseRect(rT);
                    END;
}
               END;
          END;
     rec := POINTER(ORD(dsp) + crd^.orec);
     foPrnt := rec^.foPrnt;
     recSon := POINTER(ORD(dsp) + orecSon);
     recSon^.r.botRight := r.botRight;
     rec^.orecSon := orecSon;
     pint := POINTER(ORD(lldInt) + crd^.ointSon);
     ointT := OintMark(recSon^.ointDep, crd^.icsSon, pint^.lpFst,
                                            pint^.lpLim, FALSE, rec^.ointDep);
     tyterm := TytermUpdateRec(orecSon, crd^.iimgp, TRUE);
     { TytermUpdateRec may have made the clip rgn smaller - so reset it }
     IF NOT foPrnt.fObox THEN oboxPane := OboxVisOrec(foPrnt.o);
     rec := POINTER(ORD(dsp) + orecSon);
     IF tyterm <> tytermOutOfWw THEN
          BEGIN
          IF fTstPg THEN
               WRITELN('lp: ', lpOrLppg.lp, ', ipgLst:', hcspg^^.pgd.ipgLst:3);
          IF (ptpg = ptpgBody)
            AND (lpOrLppg.lppg.ipg = hcspg^^.pgd.ipgLst) THEN
               BEGIN
               recSon := POINTER(ORD(dsp) + orecSon);
               pint := POINTER(ORD(lldInt) + recSon^.ointDep);
               SetLpsIpgLst(icsPg, pint^.lpLim, LpLimCon(orecSon),
               tyterm=tytermEos);
               END;
          END;
     END;
END;

  {--------------------------------}
  PROCEDURE FindIpgLst { (ics: TB) };
  {--------------------------------}
  VAR hcspg:    THcspg;
    pcspg:    TPcspg;
    icsSon: TB;
    ointSon: TC;
    pint:   TPint;
    ipgLst:   TC;
    ipg:   TC;
  BEGIN
  {$IFC sampcalls}  ALogCall;  {$ENDC}
  hcspg := POINTER(ORD(rghcs^[ics]));
  pint := POINTER(ORD(lldInt) + hcspg^^.pgd.ointBodyAll);
  IF NOT pint^.fValid THEN
     BEGIN
     pcspg := hcspg^;
     icsSon := pcspg^.pgd.icsBody;
     ipgLst := pcspg^.pgd.ipgLst;
     ipg := 0;
     REPEAT
          ointSon := pcspg^.pgd.argbody[ipg].oint;
          pint := POINTER(ORD(lldInt) + ointSon);
     IF NOT pint^.fValid THEN ipgLst := ipg;
        ipg := ipg + 1;
        UNTIL ipg > ipgLst;
   pcspg^.pgd.ipgLst := ipgLst;
   IF fTstPg THEN WRITELN('FindIpgLst -- ipgLst:' , ipgLst:3);
   ointSon := OintMark(pcspg^.pgd.ointBodyAll, icsSon, 0, lpMax, TRUE, ointNil);
   END;
END;


PROCEDURE SetDptWidHtPtpg(icsPg: TB; ptpg: TC; VAR ptpgNxt: TC; tyxy: TTyxy;
                          VAR dptWidHt: TPt);
VAR ptPgWidHt:  TPt;
    yCur, dyHt: TCd;
    pglp:       TPglp;
    hcspg:      THcspg;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
ConvertIcsPglpTyxy(icsPg, pglp, tyxy);
hcspg := POINTER(ORD(rghcs^[icsPg]));
DptPaperSize(icsPg, tyxy, {OUT} ptPgWidHt);
dptWidHt.h := ptPgWidHt.h;
WITH pglp DO BEGIN
    CASE ptpg OF
       ptpgBeforeHdr:
                 BEGIN
                 yCur := 0; dyHt := yTopHdr;
                 END;
       ptpgHdr:  BEGIN
                 yCur := yTopHdr; dyHt := dyHdr;
                 END;
       ptpgBody: BEGIN
                 yCur := yTopHdr + dyHdr; dyHt := dyHtBody;
                 END;
       ptpgFtr:  BEGIN
                 yCur := yTopHdr + dyHdr + dyHtBody;
                 dyHt := ptPgWidHt.v - yCur;
                 END
       END;
    END;
IF fTstPg THEN
   WRITELN('yCur: ', yCur, ', dyHt: ', dyHt, ', vHtPg: ', ptPgWidHt.v);
ptpgNxt := ptpg + 1; IF ptpgNxt = ptpgMax THEN ptpgNxt := ptpgFst;
{ IF (yCur + dyHt) < ptPgWidHt.v THEN ptpgNxt := ptpg + 1
ELSE BEGIN dyHt := (ptPgWidHt.v - yCur); ptpgNxt := 0; END;
}
dptWidHt.v := dyHt;
{ ConvertTyxy(dptWidHt.h, dptWidHt.v, hcspg^^.pgd.pglp.tyxy, tyxy); }
END;


{$S LWPreview}
{--------------------------------------------}
PROCEDURE IncIpgLst { (ics: TB) };
{--------------------------------------------}
VAR hcspg: THcspg; pcspg: TPcspg; icsSon: TB; ointSon: TC;
    pint: TPint; orecBody: TC; rec: TRec; lpFst, lpLimDep: TLp;
    ipgLst, ipg: TC; tyterm: TTyterm;
    ptBotRt, ptOffScreen: TPt; ptpgDummy: TC; cd: TCd;
    tyxy: TTyxy;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
tyxy := TyxyTld(ics);   { used to be a parm; now wired in as tyxyTld }
FindIpgLst(ics);
ptOffScreen.h := -MAXINT;  ptOffScreen.v := -MAXINT;  { into the boonies }
hcspg := POINTER(ORD(rghcs^[ics]));
pcspg := hcspg^;
icsSon := pcspg^.pgd.icsBody;
ipgLst := pcspg^.pgd.ipgLst;
ointSon := pcspg^.pgd.argbody[ipgLst].oint;
pint := POINTER(ORD(lldInt) + ointSon);
lpFst := pint^.lpFst;
SetDptWidHtPtpg(ics, ptpgBody, ptpgDummy, tyxy, ptBotRt);
WITH pcspg^.pgd DO BEGIN
    cd := pglp.dyBotBody; ConvertTyxy(xOrYDummy, cd, pglp.tyxy, tyxy);
    END;
ptBotRt.v := CMin(ptBotRt.v, cd);
AddPt(ptOffScreen, ptBotRt);
orecBody := Orec1Alloc(icsSon, lpFst, lpFst, ptOffScreen, ptBotRt,
                       tyxy, fixedLpFst + fixedR, TRUE, foNil);
 { This is a kludge until I can check this out. }
{ IF tyxy = tyxyScrn THEN iimgp := iimgpScrn ELSE iimgp := iimgpQume; }
tyterm := TytermUpdateRec(orecBody, iimgpScrn, TRUE);
rec := POINTER(ORD(dsp) + orecBody);
pint := POINTER(ORD(lldInt) + rec^.ointDep);
lpLimDep := pint^.lpLim;
SetLpsIpgLst(ics, lpLimDep, LpLimCon(orecBody), tyterm = tytermEos);
orecBody := OrecSibFree(orecBody);
END;


{$S LWPage}
{-------------------------------------------------------}
PROCEDURE GenCrdPg { (crd: TCrd; ics: TB; lpFst: TLp; tyxy: TTyxy) };
{-------------------------------------------------------}
VAR lppgInt, lpOrLppg: TLpOrLppg; lpLim:  TLp;
  ipg, ipgT, ptpg, ptpgNxt, ointSon: TC;
  hcspg: THcspg; pcspg: TPcspg; icsSon: TB; ptPgWidHt: TPt; pint: TPint;
  fEos: TF;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
lpOrLppg.lp := lpFst;
ipg := lpOrLppg.lppg.ipg;
ptpg := lpOrLppg.lppg.ptpg;
hcspg := POINTER(ORD(rghcs^[ics]));
IF fTstPg THEN
   BEGIN
   WRITELN('GenCrdPg(', ics:3, '; ', lpFst, ')');
   WRITELN('ipg:', ipg:3, ', ptpg:', ptpg:3);
   END;
pint := POINTER(ORD(lldInt) + hcspg^^.pgd.ointBodyAll);
IF NOT pint^.fValid THEN FindIpgLst(ics);
WITH hcspg^^ DO
   BEGIN
   fEos := FEosLpPg(ics, lpFst);
   crd^.arec.strec.setfixed := fixedLpFst + fixedLpLim + fixedR; {Unless body
     -- see below}
   crd^.arec.tyxy := tyxy;
   IF fEos THEN lpLim := lpFst
   ELSE BEGIN
        WITH crd^.arec.r DO
             BEGIN
             SetPt(topLeft, 0, 0);
             SetDptWidHtPtpg(ics, ptpg, ptpgNxt, tyxy, botRight);
             END;
        pcspg := hcspg^;
        CASE ptpg OF
         ptpgBeforeHdr:  BEGIN
                   icsSon := icsNil;
                   ointSon := ointNil;
                   crd^.arec.strec.fSelectable := FALSE;
                   END;
         ptpgHdr:  BEGIN
                   icsSon := pcspg^.pgd.pglp.icsHdr;
                   ointSon := OintMark(pcspg^.pgd.ointHdr, icsSon, 0,
                                LpMacEd(pcspg^.pgd.pglp.icsHdr), TRUE, ointNil);
                   END;
         ptpgBody: BEGIN
                   icsSon := pcspg^.pgd.icsBody;
                   ointSon := pcspg^.pgd.argbody[ipg].oint;
                   crd^.arec.strec.setfixed := fixedLpFst + fixedR;
                   END;
         ptpgFtr:  BEGIN
                   icsSon := pcspg^.pgd.pglp.icsFtr;
                   ointSon := OintMark(pcspg^.pgd.ointFtr, icsSon, 0,
                                LpMacEd(pcspg^.pgd.pglp.icsFtr), TRUE, ointNil);
                   END
         END;
        lpOrLppg.lppg.ich := 0;
        lpOrLppg.lppg.ptpg := ptpgNxt;
        IF ptpgNxt = 0 THEN
             lpOrLppg.lppg.ipg := ipg + 1;
        lpLim := lpOrLppg.lp;
        END;
   END;
crd^.arec.dlpLim := lpLim-lpFst;
crd^.arec.ointDep := OintMark(crd^.arec.ointDep, ics, lpFst, lpLim, TRUE, ointNil);
crd^.tyloc := tylocLstSib;
crd^.icsSon := icsSon;
crd^.ointSon := ointSon;
{$ifc fDbgOk}
IF fTstPg THEN
   BEGIN
   WRITELN('icsSon: ', icsSon, ', ointSon: ', ointSon); PxTld;
   END;
{$endc}
END;

{$S LWPreview}
{--------------------------------------------------------------}
PROCEDURE FindLpPgFixed { (ics: TB; lp: TLp; VAR lpFixed: TLp) };
{--------------------------------------------------------------}
VAR lpOrLppg: TLpOrLppg;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
lpOrLppg.lp := lp;
lpOrLppg.lppg.ptpg := ptpgFst;
lpOrLppg.lppg.ich := 0;
lpFixed := lpOrLppg.lp;
END;

{$S LWDebug}
{-----------------------------------}
PROCEDURE PxHcspg { (hcspg: THcspg) };
{-----------------------------------}
VAR ipg:    TC;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
{$ifc fDbgOk}
WRITELN('hcspg: ', ORD(hcspg), ', pcspg: ', ORD(hcspg^));
WITH hcspg^^.pgd DO
   BEGIN
   WRITELN('   tyxy:', ORD(pglp.tyxy):1, '; yTopHdr:', pglp.yTopHdr:3,
           '  dyHdr:', pglp.dyHdr:3, '; icsHdr:', pglp.icsHdr:3);
   WRITELN('  icsFtr:', pglp.icsFtr:3);
   WRITELN('  dyHtBody: ', pglp.dyHtBody:4, '  dyBotBody: ', pglp.dyBotBody:4,
           '; ipgLst:', ipgLst:3, '; ipgMax:', ipgMax:3);
   FOR ipg := 0 TO ipgLst DO
           BEGIN
           WRITE('  ipg:', ipg:3, ', fEos: ', argbody[ipg].fEos, '  ');
           PxInt(argbody[ipg].oint);
           END;
   END;
{$endc}
END;



END.



ÿ