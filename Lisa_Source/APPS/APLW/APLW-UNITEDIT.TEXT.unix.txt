{$I APLW/COMPFLAGS }                                            {gdf 1/16/84}
{$R-}

UNIT UnitEdit;

(* Copyright 1983, 1984, Apple Computer Inc. *)

INTERFACE

{ gdf  2/29/84 -- put in Janis' canges: hopPutDict, hopRmvDict  }
{ gdf  2/27/84 -- insert trace calls for segmentation  }
{ gdf  1/16/84  -- printing compile flag & include COMPFLAGS added }
{ tjm  3/10/83  --  Minimize on LpMacEd if not fNeedCr }
{ tjm  1/20/83  --  FindLpFstPar logic in IssReplace1 }
{ crc 10/25/82  --  New hops, fScrapWord }
{ jrm  9/13/82  --  in IssReplace1 and Replace, don't replace LpLastCr }
{ jrm  6/29/82  --  USES changes: LisaGraf => QuickDraw; moved USES UnitFile }
{ crc  6/02/82  --  Conditional compilation }
{ crc 05/25/82  --  USES PrStd added }
{ jrm  5/25/82  --  removed lpLimScrap and lpLimUndo references }
{ crc 04/06/82  --  integration with new font manager }
{ bas 01/13/82  --  USES for new wm }
{ bas 12/10/81  --  integration with font manager }

USES
    {$IFC sampcalls}                                               {gdf 2/24/84}
       {$U aplw/tracecalls.obj} tracecalls,
    {$ENDC}
    {$U Obj:UnitStd.Obj  } UnitStd,
    {$U Obj:UnitHz.Obj   } UnitHz,
    {$U LibOS/Syscall.Obj }   Syscall,
    {$U Obj:UnitFile.Obj } UnitFile,
    {$U Obj:Storage.Obj  } Storage,
    {$U Obj:QuickDraw.Obj } QuickDraw,
    {$U Obj:FontMgr.Obj  } FontMgr,
    {$U Obj:PmDecl       } PmDecl,
     {$IFC fPrintOld}                                               {gdf 1/16/84}
        {$U Obj:PrStd.Obj }     PrStd,
     {$ELSEC}
        {$U Obj:PrStdInfo.Obj } PrStdInfo,
        {$U Obj:PrPublic.Obj }  PrPublic,    (* needed ? *)
     {$ENDC}                                                        {gdf 1/16/84}
    {$U Obj:UnitFmt.Obj  } UnitFmt,
    {$U Obj:UnitCs.Obj   } UnitCs,
    {$U Obj:UnitFf.Obj}     UnitFf,
    {$U Obj:Wm.Events.Obj  } Events,
    {$U Obj:Wm.Folders.Obj } Folders,
    {$U Obj:WmlStd     }    WmlStd,
    {$U Obj:WM.Menus.Obj } Menus,
    {$U Obj:AlertMgr.Obj } AlertMgr,
    {$U ApLW/Queue.Obj    } Queue,
    {$U ApLW/UnitEd.Obj   } UnitEd,
    {$U ApLW/UnitBox.Obj  } UnitBox,
    {$U ApLW/UnitRec.Obj  } UnitRec,
    {$U ApLW/UnitTld.Obj  } UnitTld;

CONST
    {$IFC NOT fDbgOk} fTstEdit = FALSE; {$ENDC}
    ihileLst = 20;
    cssAbsMax = 512;

TYPE THop = ( hopReplace, hopSelect, hopMove, hopInsert, hopSwitch,
              hopRplWord, hopMovWord, hopPutDict, hopRmvDict );

    TAhile = RECORD   { A HIstory List Entry }
              fParEnd1:  TF;
              fParEnd2:  TF;
              ics1:      TB;
              ics2:      TB;
              lpFst1:    TLp;
              lpFst2:    TLp;
              lpLim1:    TLp;
              lpLim2:    TLp;
              hop:       THop;
             END;
    THile = ^TAhile;

VAR
   {$IFC fDbgOk} fTstEdit: TF; {$ENDC}
   icsScrap, icsUndo: TB;
   icsUndo2: TB;
   fScrapWord: TF;
   hileLim, hileMax: THile;
   arghile: ARRAY [0..ihileLst] OF TAhile;

FUNCTION FEdited (icsedDoc: TB): TF;

FUNCTION IssReplace1 (ics: TB; lpFst, lpLim: TLp; cssIns: TC; clpIns: TLp) : TC;

PROCEDURE Replace (icsD: TB; lpFstD, lpLimD: TLp;
                  icsIns: TB; lpFstIns, lpLimIns: TLp);

PROCEDURE ApplyRgfbcEd (ics: TB; lpFst, lpLim: TLp; hfccSrc: THfcc);

PROCEDURE ApplyFopEd (ics: TB; lpFst, lpLim: TLp; ifld: TC; sval: TW; pval: TPc);

PROCEDURE InitChronicle;

PROCEDURE Chronicle(icsA: TB; lpFstA, lpLimA: TLp;
                   icsB: TB; lpFstB, lpLimB: TLp; hopC: THop);

{$ifc fDbgOk}
PROCEDURE PxHile(hile: THile);
{$endc}

PROCEDURE InitUndo(hz: THz);

PROCEDURE CheckHfccs;

IMPLEMENTATION

{$IFC fSymOK }
{$D+ }
{$ELSEC }
{$D- }
{$ENDC }

CONST cssMinExtra = 32;

TYPE TAspl = RECORD ssFst, ssLst: TSs; END;
    TSpl = ^TAspl;

{$S LWMisc}
{-------------------}
PROCEDURE CheckHfccs;
{-------------------}
LABEL 100;
VAR ics: TB;
   hcsed: THcsed;
   issMac, iss: TC;
   hfcc: THfcc;
   count: TC;
FUNCTION CountHfcc(hfcc: THfcc): TC;
    LABEL 10;
    VAR ics: TB;
        hcsed: THcsed;
        cref: TC;
        issMac, iss: TC;
        rgss: TRgss;
    BEGIN
    {$IFC sampcalls}  ALogCall;  {$ENDC}
    cref := 0;
    FOR ics := 0 TO icsMac - 1 DO
         BEGIN
         hcsed := POINTER(ORD(rghcs^[ics]));
         IF ORD(hcsed) = ORD(hNil) THEN GOTO 10;
         IF hcsed^^.cs.tycs = tycsEd THEN
              BEGIN
              issMac := hcsed^^.issMac;
              rgss := @hcsed^^.argss;
              FOR iss := 0 TO issMac - 1 DO
                   IF rgss^[iss].hfcc = hfcc THEN cref := cref + 1;
              END;
    10:  END;
    CountHfcc := cref;
    END;{CountHfcc}
BEGIN{CheckHfCcs}
{$IFC sampcalls}  ALogCall;  {$ENDC}
FOR ics := 0 TO icsMac - 1 DO
    BEGIN
    hcsed := POINTER(ORD(rghcs^[ics]));
    IF ORD(hcsed) = ORD(hNil) THEN GOTO 100;
    IF hcsed^^.cs.tycs = tycsEd THEN
         BEGIN
         issMac := hcsed^^.issMac;
         FOR iss := 0 TO issMac - 1 DO
              BEGIN
              hfcc := hcsed^^.argss[iss].hfcc;
              IF hfcc <> NIL THEN
                   BEGIN
                   count := CountHfcc(hfcc);
                   IF (count <> hfcc^^.cref) (* d *) AND
                        (hfcc^^.cref <> -1) THEN
                        BEGIN
                        {$ifc fDbgOk}
                        WRITELN('BAD CREF OF HFCC: ics:', ics:4, '; iss:', iss:4);
                        WRITELN('                  ', ORD(hfcc), '; hfcc^^.cref:',
                                 hfcc^^.cref:4, '; count:', count:4);
                        PxHcsed(POINTER(ORD(hcsed)));
                        {$endc}
                        Break(120);
                        END;
                   END;
              END;
         END;
100: END;
END;

{$S LWEdited}
{---------------------------------------}
FUNCTION FEdited (* (icsedDoc: TB): TF *);
{---------------------------------------}
LABEL 666; (* collection point for proof of being edited *)

VAR icsffDoc: TB;
    hcsedDoc, hcsedHdr, hcsedFtr: THcsEd;
    ffleader: TFfleader;


{$IFC fPrintOld}                                                    {gdf 1/16/84}

   FUNCTION FPrPrfsEqual (VAR prPrf1, prPrf2: TPrPrf): TF;
   VAR i:    TEdev;
       flag: TF;
   BEGIN
     flag := (prPrf1.cVersion = prPrf2.cVersion) AND (prPrf1.iArb = prPrf2.iArb)
                                                 AND (prPrf1.edev = prPrf2.edev);
     FOR i := edevDotMx TO edevLaser DO BEGIN
      flag := flag AND (prPrf1.rgstl[i].pgSize.cWidth  = prPrf2.rgstl[i].pgSize.cWidth)
                   AND (prPrf1.rgstl[i].pgSize.cHeight = prPrf2.rgstl[i].pgSize.cHeight)
                   AND (prPrf1.rgstl[i].adev.dev       = prPrf2.rgstl[i].adev.dev);
     END;
     FPrPrfsEqual := flag;
   END;

{$ELSEC}                                                            {gdf 1/16/84}

{ use printshop FPrPrfEqual instead... }

{$ENDC}                                                             {gdf 1/16/84}

   FUNCTION FEdited1 (hcsed: THcsed; lp: Tlp): TF;
   VAR ssT: Tss;
   BEGIN
   FEdited1 := FALSE;
   IF hcsed^^.issMac <> 1 THEN FEdited1 := TRUE {check: slug source not divided}
   ELSE BEGIN
      ssT := hcsed^^.argss[0];
      IF (ssT.icsSon <> icsffDoc) THEN FEdited1 := TRUE {check: comes from formated file}
      ELSE IF (ssT.lpSon <> lp) THEN FEdited1 := TRUE {check: no initial deletions}
      ELSE IF (ssT.hfcc <> Nil) THEN FEdited1 := TRUE; {check: no formating changes}
      END;
   END;

BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
FEdited := TRUE; (* assume guilty until proven innocent *)
IF fTstEdit THEN WRITELN ('Entering FEdited...');

hcsedDoc := POINTER(ORD(rghcs^[icsedDoc]));
icsffDoc := hcsedDoc^^.icsOrig;
IF rghcs^[icsffDoc]^^.tycs <> tycsFf THEN GOTO 666; (* don't want to bother with
                                                  checking unformatted files *)
GetLeaderOfIcsFf(icsffDoc, ffLeader);

IF FEdited1 (hcsedDoc, ffleader.lpTxtFst) THEN GOTO 666;

hcsedHdr := POINTER(ORD(rghcs^[hcsedDoc^^.pglp.icsHdr]));
IF FEdited1 (hcsedHdr, ffleader.lpHdrFst) THEN GOTO 666;

hcsedFtr := POINTER(ORD(rghcs^[hcsedDoc^^.pglp.icsFtr]));
IF FEdited1 (hcsedFtr, ffleader.lpFtrFst) THEN GOTO 666;

IF hcsedDoc^^.tyrlu <> ffLeader.tyrlu THEN GOTO 666;

{$IFC fPrintOld}                                                    {gdf 1/16/84}
IF NOT FPrPrfsEqual(hcsedDoc^^.cs.prprf, ffLeader.prprf) THEN GOTO 666;
{$ELSEC}                                                            {gdf 1/16/84}
{Let's use FPrPrfEqual from print shop -- gdf 1/23/84}
IF NOT FPrPrfEqual(hcsedDoc^^.cs.prprf, ffLeader.prprf) THEN GOTO 666;  { ??? }
{$ENDC}                                                             {gdf 1/16/84}

WITH hcsedDoc^^.pglp DO
   IF (ffLeader.pglp.yTopHdr <> yTopHdr) OR
      (ffLeader.pglp.dyHdr <> dyHdr) OR
      (ffLeader.pglp.dyHtBody <> dyHtBody) OR
      (ffLeader.pglp.dyBotBody <> dyBotBody) THEN GOTO 666;

IF fTstEdit THEN WRITELN ('FEdited about to return FALSE.....');
FEdited := FALSE; (* made it through the gauntlet *)
666:
END;


{$S LWCom2}
{-----------------------------------------------------}
FUNCTION IssSplitSs (ics: TB; lp: TLp; spl: TSpl) : TC;
{-----------------------------------------------------}
 { Split the piece running through lp at lp. }
 { Put the resulting ss's into spl. Returns  }
 { the iss in the doc that WOULD contain lp  }
 { if the split really happened.             }
VAR pcsed:    TPcsed;
   iss:    TC;
   rgss:   TRgss;
   dlpSs, lpFstSs: TLp;
   ps:     TPs;
   achad:  TAchad;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
IF fTstEdit THEN WRITELN('IssSplitSs -- ics:', ics:3, '; lp: ', lp);
pcsed := POINTER(ORD(rghcs^[ics]^));
iss := IssFromLp(pcsed, lp);
rgss := @pcsed^.argss;
spl^.ssLst := rgss^[iss];
lpFstSs := spl^.ssLst.lp;
dlpSs := lp - lpFstSs;
IF (iss = pcsed^.issMac) OR (dlpSs = 0) THEN
    BEGIN
    IssSplitSs := iss;
    spl^.ssFst := rgss^[iss - 1];
    END
ELSE BEGIN
    WITH spl^ DO
         BEGIN
         ssFst := ssLst;
         ssLst.lp := lp;
         ssLst.lpSon := ssFst.lpSon + dlpSs;
         IssSplitSs := iss + 1;
         END;
    END;
END;

{---------------------------------------------------------------------------------}
FUNCTION IssReplace1 { (ics: TB; lpFst, lpLim: TLp; cssIns: TC; clpIns: TLp) : TC
};
{---------------------------------------------------------------------------------}
 { Remove the span [lpFst, lpLim) from ics, splitting ss's if necessary;  }
 { then split the dd up to make room for cssIns more ss's.  Adjust }
 { all lp's of ss's after lpFst by (lpLim-lpFst+clpIns). This does }
 { NOT actually insert the new ss's; it just makes room for them.  }
VAR hfcc:   THfcc;
    pcsed:  TPcsed;
    issFst, issLim, issSetLp: TC;
    iss, issMacNew: TC;
    cssDiff, cssMove: TC;
    rgss: TRgss;
    lp, lpFstAdj, lpT:     TLp;
    dlp:    TLp;
    hcsed:  THcsed;
    cref:   TC;
    asplFst, asplLst: TAspl;
    fNewSplFst, fNewSplLim: TF;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
IF fTstEdit THEN
    WRITELN('IssReplace1 -- ics:', ics:3, '; lpFst: ', lpFst:5,
            '; lpLim: ', lpLim:5, '; cssIns:', cssIns:3,
            '; clpIns: ', clpIns:5);

{ Find out if we are invalidating paragraph properties}
lpFstAdj := lpFst;
IF lpLim > 0 THEN
     BEGIN
     FindLpFstPar(ics, lpLim, lpT);
     IF (lpT = lpLim) OR (lpFst < lpT) THEN
          FindLpFstPar(ics, lpFst, lpFstAdj);
     END;

hcsed := POINTER(ORD(rghcs^[ics]));

IF (lpFst < lpLim) THEN
     BEGIN
     { Can we get rid of the last carriage-return? }
     pcsed := hcsed^;
     IF pcsed^.fNeedCr THEN
          lpLim := LpMinP(LpLastCr(ics), lpLim)
     ELSE lpLim := LpMinP(LpMacEd(ics), lpLim);
     END;
lpFst := LpMinP(lpFst, lpLim);

 { Create the splits. }
issFst := IssSplitSs(ics, lpFst, @asplFst);
pcsed := hcsed^;
rgss := @pcsed^.argss;
fNewSplFst := rgss^[issFst].lp <> lpFst;
issLim := IssSplitSs(ics, lpLim, @asplLst);
pcsed := hcsed^;
rgss := @pcsed^.argss;
fNewSplLim := rgss^[issLim].lp <> lpLim;
IF fNewSplLim THEN issLim := issLim - 1;

 { What difference to issMac? }
cssDiff := issFst - issLim;
{ The replace is within one ss - 1 old ss becomes 2 }
IF cssDiff = 1 THEN
    WITH asplLst.ssLst DO
         IF hfcc <> NIL THEN
             (* d *) IF hfcc^^.cref <> -1 THEN hfcc^^.cref := hfcc^^.cref + 1;
IssReplace1 := issFst;
IF fTstEdit THEN
    WRITELN('               issFst:', issFst:3, '; issLim:', issLim:3,
      '; fNewSplFst: ', fNewSplFst, '; fNewSplLim: ', fNewSplLim,
      '; cssDiff:', cssDiff:3);

 { Dec cref for ss's to be removed. }
FOR iss := issFst TO issLim - 1 DO
    BEGIN
    hfcc := rgss^[iss].hfcc;
    IF hfcc <> NIL THEN
         BEGIN
         IF hfcc^^.cref = 1
              THEN BEGIN
                   FreeBk(pcsed^.cs.hz, POINTER(ORD(hfcc)), tybkStd);
                   pcsed := hcsed^;
                   rgss := @pcsed^.argss;
                   END
              ELSE (* d *) IF hfcc^^.cref <> -1 THEN
                             hfcc^^.cref := hfcc^^.cref - 1;
         END;
    END;

 { Split the dd to make room for the new ss's. }
cssDiff := cssIns + cssDiff;
issMacNew := pcsed^.issMac + cssDiff;
IF cssDiff <> 0 THEN
    BEGIN
    IF issMacNew + 1 >= pcsed^.issMax THEN
         BEGIN
         ChangeIssMax(ics, issMacNew + cssMinExtra);
         pcsed := hcsed^;
         rgss := @pcsed^.argss;
         END;
    cssMove := pcsed^.issMac - issLim;
    IF fTstEdit THEN
         WRITELN('               issMacNew:', issMacNew:3,
           '; cssMove:', cssMove:3, '; cssDiff:', cssDiff:3);
    IF cssMove > 0 THEN
         MoveOvRgch(@rgss^[issLim + cssDiff + 1], @rgss^[issLim + 1],
                                    cssMove * SIZEOF(TSs));
    pcsed^.issMac := issMacNew;
    issLim := issLim + cssDiff;
    END;

 { Move the split ss's back into the dd. }
{ IF fNewSplFst AND (issFst > 0) THEN
    BEGIN
    rgss^[issFst - 1] := asplFst.ssFst;
    WITH asplFst.ssFst DO
         IF hfcc <> NIL THEN hfcc^^.cref := hfcc^^.cref + 1;
    END;
}
IF (issLim <= issMacNew) THEN rgss^[issLim] := asplLst.ssLst;

 { Adjust the lp's of the ss's after the split. }
dlp := clpIns - (lpLim - lpFst);
FOR issSetLp := issLim TO issMacNew DO
    BEGIN
    IF fTstEdit THEN
         WRITELN('issSetLp: ', issSetLp:3, ', lp: ', rgss^[issSetLp].lp,
           ', dlp: ', dlp);
    rgss^[issSetLp].lp := rgss^[issSetLp].lp + dlp;
    END;

 { Set up the par bounds again. }
rgss^[issFst].lp := lpFst;
SetIssDlps(hcsed, issFst - 1);
SetIssDlps(hcsed, issLim);

 { Make adjustments to non-dd structures. }
AdjustLps(ics, lpFstAdj, lpLim, dlp);
IF fTstEdit THEN WRITELN('IssReplace1 END.');
END;

{--------------------------------------------------------------}
PROCEDURE Replace { (icsD: TB; lpFstD: TLp; lpLimD: TLp;
                    icsIns: TB; lpFstIns: TLp; lpLimIns: TLp) };
{--------------------------------------------------------------}
LABEL   999;
VAR issNxt, issFstSrc, issLimSrc, iss, issDst:    TC;
   cssIns:    TC;
   rgss, rgssDst: TRgss;
   hcsedD, hcsedIns: THcsed;
   pcsedD: TPcsed;
   lpLimEd, lpT:    TLp;
   asplFst, asplLst: TAspl;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
{$ifc fDbgOk}
IF fTstEdit THEN
  BEGIN
   CheckHfccs;
  WRITELN('Replace -- deletion: [', icsD:3,   ', ', lpFstD,   ', ', lpLimD,
  ']');
  WRITELN('          insertion: [', icsIns:3, ', ', lpFstIns, ', ', lpLimIns,
  ']');
  WRITELN(' Insertion doc:');
  PxCs(icsIns);
  WRITELN(' Deletion  doc:');
  PxCs(icsD);
  END;
{$endc}

IF NOT FFindEd(icsD, lpFstD, lpLimD) THEN  GOTO 999;
hcsedD := POINTER(ORD(rghcs^[icsD]));
IF hcsedD^^.fNeedCr THEN
     lpLimD := LpMinP(LpLastCr(icsD), lpLimD)
ELSE lpLimD := LpMinP(LpMacEd(icsD), lpLimD);
lpFstD := LpMinP(lpFstD, lpLimD);

lpFstIns := LpMinP(lpFstIns, lpLimIns);

IF (icsIns = icsNil) OR (lpFstIns = lpLimIns) THEN
    BEGIN
    IF fTstEdit THEN WRITELN('No insertion');
    issDst := IssReplace1(icsD, lpFstD, lpLimD, 0, 0);
    END
ELSE BEGIN
    IF FFindEd(icsIns, lpFstIns, lpLimIns) THEN
         BEGIN
         lpLimIns := LpMinP(LpMacEd(icsIns), lpLimIns);
         lpFstIns := LpMinP(lpFstIns, lpLimIns);
         IF (lpFstIns = lpLimIns) THEN
             BEGIN
             IF fTstEdit THEN WRITELN('No insertion');
             issDst := IssReplace1(icsD, lpFstD, lpLimD, 0, 0);
             END
         ELSE BEGIN
              { Don't replace anything within itself. }
             IF (icsD = icsIns) AND (lpLimD > lpFstIns)
               AND (lpFstD < lpLimIns) THEN
                  GOTO 999;

             { Find out if we are invalidating paragraph properties}
             IF lpLimIns > 0 THEN
                  BEGIN
                  FindLpFstPar(icsIns, lpLimIns - 1, lpT);
                  IF lpFstIns <= lpT THEN
                       BEGIN
                       FindLpFstPar(icsD, lpFstD, lpT);
                       AdjustLps(icsD, lpT, lpFstD, 0);
                       END;
                  END;


              { Make the split in the source doc. }
             issFstSrc := IssSplitSs(icsIns, lpFstIns, @asplFst);
             hcsedIns := POINTER(ORD(rghcs^[icsIns]));
             rgss := @hcsedIns^^.argss;
              { Is lpLimIns within the same source ss? }
             IF lpFstIns = rgss^[issFstSrc].lp
                  THEN issNxt := issFstSrc + 1
                  ELSE issNxt := issFstSrc;
             issLimSrc := IssFromLp(hcsedIns^, lpLimIns);
             IF lpLimIns <> rgss^[issLimSrc].lp THEN issLimSrc := issLimSrc + 1;
             { issLimSrc := IssSplitSs(icsIns, lpLimIns, @asplLst); }
             IF fTsTEdit THEN
                  WRITELN('  issFstSrc:', issFstSrc:3, '; issLimSrc:', issLimSrc:3);

              { Make room for the ss's in the dest doc. }
             cssIns := issLimSrc - issFstSrc;
             IF lpFstIns <> rgss^[issFstSrc].lp THEN cssIns := cssIns + 1;
             { IF rgss^[issLimSrc].lp <> lpLimIns THEN cssIns := cssIns + 1; }
             issDst := IssReplace1(icsD, lpFstD, lpLimD, cssIns, lpLimIns - lpFstIns);

              { Move 'em in. }
             IF fTstEdit THEN
                  WRITELN(' issDst:', issDst:3, '; cssIns:', cssIns:3);
             rgssDst := @hcsedD^^.argss;
             rgssDst^[issDst] := asplFst.ssLst;
             IF cssIns > 1 THEN
                  MoveRgch(@rgssDst^[issDst + 1], @hcsedIns^^.argss[issNxt],
                                                     (cssIns - 1) * SIZEOF(Tss));
             { IF issLimSrc <> issFstSrc THEN
                  rgssDst^[issDst + cssIns - 1] := asplLst.ssFst;
             }
             IF fTstEdit THEN WRITELN('Ss''s moved in.');
             {$ifc fDbgOk}
             IF fTstEdit THEN PxHcsed(POINTER(ORD(hcsedD)));
             {$endc}

              { Update lp's and hfcc counts. }
             FOR iss := issDst TO issDst + cssIns - 1 DO
                  WITH rgssDst^[iss] DO
                       BEGIN
                       IF hfcc <> NIL THEN WITH hfcc^^ DO
                         (* d *) IF cref <> -1 THEN cref := cref + 1;
                       lp := (lp - lpFstIns) + lpFstD;
                       END;

              { Set the par bounds. }
             SetIssDlps(hcsedD, issDst - 1);
             SetIssDlps(hcsedD, issDst + cssIns - 1);
             END;
         END
    ELSE BEGIN
         IF fTstEdit THEN WRITELN('            Replacing with indirect cs.');
         issDst := IssReplace1(icsD, lpFstD, lpLimD, 1, lpLimIns - lpFstIns);
         WITH hcsedD^^.argss[issDst] DO
              BEGIN
              icsSon := icsIns;
              lpSon := lpFstIns;
              fVan := FALSE;
              hfcc := NIL;
              END;
         SetIssDlps(hcsedD, issDst);
         END;
    END;
{$ifc fDbgOk}
IF fTstEdit THEN
   BEGIN {CheckHfccs;}
   IF rghcs^[icsD]^^.tycs = tycsEd THEN PxHcsed(rghcs^[icsD]);
   PxCs(icsD); END;
{$endc}
999:
END;

{$ifc fDbgOk}
{$S LWDebug}
{--------------------------------}
PROCEDURE PxHile { (hile: THile) };
{--------------------------------}
BEGIN
WITH hile^ DO
    BEGIN
    {$IFC sampcalls}  ALogCall;  {$ENDC}
     WRITE('PxHile -- hop');
    CASE hop OF
     hopReplace:   WRITE('Replace');
     hopSelect:    WRITE('Select');
     hopMove:      WRITE('Move');
     hopInsert:    WRITE('Insert');
     hopSwitch:    WRITE('Switch');
     hopPutDict:   WRITE('PutDict');
     hopRmvDict:   WRITE('RmvDict');
     OTHERWISE     BEGIN WRITE(' ', ORD(hop)); Break(121); END;
     END;
    WRITELN(';  ohile: ', ORD(hile) - ORD(@arghile));
    WRITE('   1:  ics');
    IF ics1 = icsScrap THEN
         WRITE('Scrap:')
    ELSE IF ics1 = icsUndo THEN
         WRITE('Undo: ')
    ELSE WRITE(':     ');
    WRITELN(ics1:3, '; [ ', lpFst1, ' to ', lpLim1, ' ]');
    WRITE('   2:  ics');
    IF ics2 = icsScrap THEN
         WRITE('Scrap:')
    ELSE IF ics2 = icsUndo THEN
         WRITE('Undo: ')
    ELSE WRITE(':     ');
    WRITELN(ics2:3, '; [ ', lpFst2, ' to ', lpLim2, ' ]');
    END;
END;
{$endc}

{$S LWType}
{----------------------}
PROCEDURE InitChronicle;
{----------------------}
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
IF fTstEdit THEN WRITELN('InitChronicle.');
hileLim := @arghile;
hileMax := POINTER(ORD(hileLim) + ihileLst * SIZEOF(TAhile));
IF LpMacEd(icsUndo) <> 0 THEN
            Replace(icsUndo, 0, LpMacEd(icsUndo), icsNil, 0, 0);
END;

{---------------------------------------------------------------------------}
PROCEDURE Chronicle { (icsA: TB; lpFstA, lpLimA: TLp; ----------------------}
{--------------------  icsB: TB; lpFstB, lpLimB: TLp; hopC: THop)  ---------};
{---------------------------------------------------------------------------}
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
IF fTstEdit THEN WRITELN('Chronicle.');
IF ORD(hileLim) = ORD(hileMax) THEN
     BEGIN {Nothing more will happen until InitChronicle is called} END
ELSE BEGIN
    WITH hileLim^ DO
         BEGIN
         hop := hopC;
         ics1 := icsA;
         ics2 := icsB;
         lpFst1 := lpFstA;
         lpFst2 := lpFstB;
         lpLim1 := lpLimA;
         lpLim2 := lpLimB;
         END;
    {$ifc fDbgOk}
    IF fTstEdit THEN PxHile(hileLim);
    {$endc}
    hileLim := POINTER(ORD(hileLim) + SIZEOF(TAhile));
    END;
END;

{$S LWStyle}
{-------------------------------------------------------------------------------}
PROCEDURE Apply1FopOrRgfbc(ics: TB; lpFst, lpLim: TLp; fRgfbc: TF;
                                 ifld: TC; sval: TW; pval: TPc);
{-------------------------------------------------------------------------------}
LABEL 999;
VAR hcsed:    THcsed;
   issFst, issLim, iss: TC;
   rgss:   TRgss;
   rgfbc: TRgb;
   hfccPrv, hfcc, hfccWasNil:   THfcc;
   hz: THz;
   lpLimUndo: TLp;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
IF fTstEdit THEN WRITELN('Apply1FopOrRgfbc(', ics:3, ' [', lpFst, ', ',
       lpLim, ']', ifld:3, ' (', sval:5, ', ', ORD(pval), ')');
IF NOT FFindEd(ics, lpFst, lpLim) THEN GOTO 999;

 { Split the document at beginning and end of the span. }
issFst := IssReplace1(ics, lpFst, lpFst, 0, 0);
issLim := IssReplace1(ics, lpLim, lpLim, 0, 0);

 { This will be switched with a portion of the undo: }
lpLimUndo := LpMacEd(icsUndo);
Replace(icsUndo, lpLimUndo, lpLimUndo, ics, lpFst, lpLim);
Chronicle(ics, lpFst, lpLim,
         icsUndo, lpLimUndo, lpLimUndo + (lpLim - lpFst), hopSwitch);

hcsed := POINTER(ORD(rghcs^[ics]));
hz := hcsed^^.cs.hz;

 { Add in the new fop to all the hfcc's in the ss's. }
hfccWasNil := NIL;
hfccPrv := NIL;
FOR iss := issFst TO issLim - 1 DO
    BEGIN
    rgss := @hcsed^^.argss;
    IF (hfccWasNil <> NIL) AND (rgss^[iss].hfcc = NIL) THEN
         BEGIN
         (* d *) IF hfccWasNil^^.cref <> -1 THEN
            hfccWasNil^^.cref := hfccWasNil^^.cref + 1;
         rgss^[iss].hfcc := hfccWasNil;
         END
    ELSE IF (hfccPrv <> NIL) AND (rgss^[iss].hfcc = hfccPrv) THEN
         BEGIN
         IF hfccPrv^^.cref = 1
              THEN FreeBk(hz, POINTER(ORD(hfccPrv)), tybkStd)
              ELSE  (* d *)IF hfccPrv^^.cref <> -1 THEN
                             hfccPrv^^.cref := hfccPrv^^.cref - 1;
                   (* d *) IF hfcc^^.cref <> -1 THEN
                           hfcc^^.cref := hfcc^^.cref + 1;
         rgss^[iss].hfcc := hfcc;
         END
    ELSE BEGIN
         hfccPrv := rgss^[iss].hfcc;
         hfcc := hfccPrv;
          { Add in the new fbc to the end of our new hfcc. }
         IF fRgfbc
              THEN AddRgfbc(hfcc, POINTER(ORD(pval)), hz)
              ELSE AddFop(hfcc, ifld, sval, pval, hz);
         hcsed^^.argss[iss].hfcc := hfcc;
         IF hfccPrv = NIL THEN hfccWasNil := hfcc;
         END;
    END;

 { Now "adjust" lp's in order to invalidate. }
AdjustLps(ics, lpFst, lpLim, 0);
999:
{$ifc fDbgOk}
IF fTstEdit THEN
   BEGIN WRITELN('Apply1FopOrRgfbc finished.'); PxHcsed(POINTER(ORD(hcsed))); END;
{$endc}
END;

{---------------------------------------------------------------------------------}
PROCEDURE ApplyFopEd { (ics: TB; lpFst, lpLim: TLp; ifld: TC; sval: TW pval: TPc)
};
{---------------------------------------------------------------------------------}
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
Apply1FopOrRgfbc(ics, lpFst, lpLim, FALSE, ifld, sval, pval);
END;

{$S LWMargin}
{---------------------------------------------------------------------}
PROCEDURE ApplyRgfbcEd { (ics: TB; lpFst, lpLim: TLp; hfccSrc: THfcc) };
{---------------------------------------------------------------------}
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
Apply1FopOrRgfbc(ics, lpFst, lpLim, TRUE, 0, 0, POINTER(ORD(hfccSrc)));
END;

{$S LWCreate}
{------------------------------}
PROCEDURE InitUndo { (hz: THz) };
{------------------------------}
VAR icsUf: TB;
   pglp: TPglp;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
IF fTstEdit THEN WRITELN('InitUndo');
icsUndo := IcsEdCreate(10, pglpStd, FALSE, hz);
IF fTstEdit THEN WRITELN('icsUndo:', icsUndo:3);
icsUndo2 := IcsEdCreate(5, pglpStd, FALSE, hz);
InitChronicle;
END;


END.

ÿ