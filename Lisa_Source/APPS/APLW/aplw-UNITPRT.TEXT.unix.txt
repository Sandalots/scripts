{$I APLW/COMPFLAGS }                                            {gdf 1/16/84}
{$R-}

UNIT UnitPrt;

(* Copyright 1983, 1984, Apple Computer Inc. *)

{ gdf  2/24/84 -- insert trace calls for segmentation  }
{ gdf  2/2/84  -- more print fixes, use thePort }
{ gdf  1/24/84 -- fix PrMgrInit call }
{ gdf  1/16/84 -- printing compile flag & include COMPFLAGS added }
{ crc  4/20/83 -- PrtImgTld: commented out first call to GenTabImg }
{ crc  4/05/83 -- New globals fOffPaper & rPrintable for communication
                  between PrtImgTld & Print in LotusMisc }
{ tjm  3/30/83 -- Move DptPaperSize to UnitTld }
{ crc  1/19/83 -- GenImgPrt: supress printing of formfeed character }
{ crc 12/01/82  --  DptPaperSize fixed to work in landscape }
{ crc 11/12/82  --  Removed call to PrInit from InitPrt; InitPrt takes no parms }
{ crc  7/28/82  --  Converted from PrText... to DrawText... }
{ jrm  6/29/82  --  USES changes: LisaGraf => QuickDraw; moved USES UnitFile }
{ crc  6/02/82 Conditional compilation }
{ crc 05/27/82 DptPaperSize now responds to tycsRuler }
{ crc 05/21/82 integration with Print Manager }
{ crc 05/13/82 integration with 5.0 LisaPrint }
{ crc 04/19/82 initialization of global devPrint used by GenCrdTld }
{ crc 04/02/82 integration with new font manager }
{ bas 02/12/82 underlining 1 below baseline; tab fill underline 1 above }
{ bas 02/17/82 PrTab junk; underlining with PrHRule }
{ bas 01/28/82 dot matrix printing }
{ bas 01/13/82 USES for new wm }
{ bas 12/14/81 integration with lisaprint }
{ bas 12/10/81 integration with font manager }
{ edited by dsy 8/17/81 }

INTERFACE

USES
     {$IFC sampcalls}                                               {gdf 2/24/84}
        {$U aplw/tracecalls.obj} tracecalls,
     {$ENDC}
     {$U Obj:UnitStd.Obj }  UnitStd,
     {$U LibOS/Syscall.Obj }   Syscall,
     {$U Obj:UnitHz.Obj }   UnitHz,
     {$U Obj:UnitFile.Obj } UnitFile,
     {$U Obj:Storage.Obj }  Storage,
     {$U Obj:QuickDraw.Obj } QuickDraw,
     {$U Obj:FontMgr.Obj }  FontMgr,
     {$U Obj:Wm.Events.Obj  } Events,
     {$U Obj:Wm.Folders.Obj } Folders,
     {$U Obj:WmlStd     }    WmlStd,
     {$U Obj:WmlCrs     }    WmlCrs,
     {$U Obj:PmDecl       } PmDecl,

     {$IFC fPrintOld}                                               {gdf 1/16/84}
        {$U Obj:PrStd.Obj }     PrStd,
        {$U Obj:PrFile.Obj }    PrFile,
        {$U Obj:PrProcs.Obj }   PrProcs,
        {$U Obj:PrMgrUtil.Obj } PrMgrUtil,
        {$U Obj:PrMgr.Obj }     PrMgr,
     {$ELSEC}
        {$U Obj:PrStdInfo.Obj } PrStdInfo,
        {$U Obj:PrPublic.Obj }  PrPublic,
     {$ENDC}                                                        {gdf 1/16/84}

     {$U Obj:UnitFmt.Obj }  UnitFmt,
     {$U Obj:UnitCs.Obj }   UnitCs,
     {$U Obj:UnitFf.Obj }   UnitFf,
     {$U Obj:WM.Menus.Obj } Menus,
     {$U Obj:AlertMgr.Obj } AlertMgr,
     {$U ApLW/Queue.Obj }    Queue,
     {$U ApLW/UnitBox.Obj }  UnitBox,
     {$U ApLW/UnitRec.Obj }  UnitRec,
     {$U ApLW/UnitOver.Obj } UnitOver,
     {$U ApLW/UnitTld.Obj }  UnitTld;

{$IFC NOT fDbgOk}
CONST
  fTstPrt = FALSE;
{$ENDC}

VAR
{$IFC fDbgOk}
     fTstPrt: TF;
{$ENDC}
     fOffPaper: TF;   {Set by PrtImgTld if crd not entirely within printable page}
     rPrintable: TR;  {Set by Print for the convenience of PrtImgTld}
     fPrinting: Boolean;        {set by Print for genfigimage}

PROCEDURE InitPrt;
PROCEDURE PrtImgTld (crd: TCrd);


IMPLEMENTATION

{$IFC fSymOK }
{$D+ }
{$ELSEC }
{$D- }
{$ENDC }


VAR xOrYDummy: TC;
    amptyxydyUl: ARRAY [TTyxy] OF TC;

{$ifc fDbgOk}
{$S LWDebug}
PROCEDURE PxBitMap;
LABEL 10;
VAR
    cRow,cByte,oByte,cRow1,cRow2,c,c2,cLstRow: TC;
    pByte: TP;
    argch: ARRAY[0..79] OF CHAR;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
cLstRow := thePort^.portBits.bounds.bottom - thePort^.portBits.bounds.top;

{Is entire bitmap empty?}
FOR cByte := 0 TO cLstRow*thePort^.portBits.rowBytes DO BEGIN;
    pByte := POINTER(ORD(thePort^.portBits.baseAddr)+cByte);
    IF pByte^ <> 0 THEN GOTO 10;
    END;
WRITELN('Empty bit map');
EXIT(PxBitMap);

10:
WRITE(' ');
FOR cByte:=0 TO CMin(79, thePort^.portBits.rowBytes) DO WRITE('-');
WRITELN;

cRow1 := 0;
WHILE cRow1 < cLstRow DO
    BEGIN
    WRITE('|');
    FOR c := 0 TO 79 DO argch[c] := ' ';
    FOR cRow2 := 0 TO 7 DO
        BEGIN
        FOR cByte:=0 TO CMin(80, thePort^.portBits.rowBytes) DO
            BEGIN
            oByte := ((cRow1+cRow2)*thePort^.portBits.rowBytes)+cByte;
            pByte := POINTER(ORD(thePort^.portBits.baseAddr)+oByte);
            IF pByte^ <> 0 THEN argch[cByte] :='*';
            END;
        END;
    c2 := 79;
    WHILE argch[c2] = ' ' DO c2 := c2-1;
    FOR c := 0 TO c2 DO WRITE(argch[c]);
    WRITELN;
    cRow1 := cRow1+8;
    END;

WRITE(' ');
FOR cByte:=0 TO CMin(79, thePort^.portBits.rowBytes) DO WRITE('-');
WRITELN;
END;  {PxBitMap}
{$endc}

{$S LWPrint}
{---------------------------------}
PROCEDURE PrtImgTld { (crd: TCrd) };
{---------------------------------}
VAR
   { !!! BEWARE:  do not change the following decls !!! }
    treCur: TTre;
    dyBaseCur: TC; cchTre: TC; ich: TC; hLeftArec: TX; iwd: TC;
    tyxy, tyxyPrt: TTyxy;
    spIpg: TSp;
   { !!! do not change the above decls !!! }

    r, rT: TR;
    treNxt: TTre;
    dyBaseline: TC;
    x, xFstUl: TC;
    ichT, cchWd, cchSep, cchDraw: TC;
    fUl: TF;
    dxTb: TC;
    ichFstSep, ichLimSep: TC;
    spT: TSp; ichTmp: TC;  { debug }
    fReqBrk,fDummy: TF;
    pport: TPort;  { debug only }
    ptRight: TPt;
    dhRule: TC;
    pint: TPint;
    crdSon: TCrd;
    dptOffset: TPt;

    PROCEDURE GenTabImg(ichFstSep: TC; dchSep: TC);
    VAR ichTb: TC; ichT: TC;

        PROCEDURE DoDotHyph (ch: CHAR);
        VAR ich2, i: TC; xStart, xCur: TX; cchTb, dxCh: TC;
        BEGIN
        {$IFC sampcalls}  ALogCall;  {$ENDC}
        dxCh := CharWidth(ch) + 2;
        IF fTstPrt THEN WRITELN('dxCh:', dxCh:5, '; dxTb:', dxTb:5);
         { Find where the start of tab is. }
        xCur := rgoxWdFst^[iwd] + 2;
        FOR ich2 := rgichWdFst^[iwd] TO ichTb - 1 DO
            xCur := xCur + mpichdx^[ich2];
        ConvertTyxy(xCur, xOrYDummy, tyxy, tyxyPrt);
        xCur := xCur + hLeftArec;
        xStart := ((xCur DIV dxCh) + 1) * dxCh;
        MoveTo(xStart, dyBaseLine);
         { Set up the width of the tab. }
        dxTb := dxTb - 3 - (xStart - xCur);
        cchTb := dxTb DIV dxCh;
        FOR i := 1 TO cchTb DO BEGIN DrawChar(ch); Move(2, 0); END;
        END; { DoDotHyph }

    BEGIN
    {$IFC sampcalls}  ALogCall;  {$ENDC}
    IF fTstPrt THEN
      WRITELN('PrtGenTabImg: tyfill:', ORD(treCur^.tyfill):3,
                          ';  ich:', ichFstSep:4, '; dchSep:', dchSep:3);
    FOR ichTb := ichFstSep TO ichFstSep + dchSep - 1 DO
        IF achadTld.rgch^[ichTb] = chTab THEN
            BEGIN
            IF fTstPrt THEN WRITELN('ichTb:', ichTb:4);
             { Generate the image... }
            dxTb := mpichdx^[ichTb];
            ConvertTyxy(dxtb, xOrYDummy, tyxy, tyxyPrt);
            CASE treCur^.tyfill OF
               tyfillDots: DoDotHyph('.');
               tyfillHyph: DoDotHyph('-');
               tyfillUL:   BEGIN
                           { put pen in right place. }
                           x := rgoxWdFst^[iwd + 1];
                           FOR ichT := rgichWdFst^[iwd + 1] - 1 DOWNTO ichTb DO
                                x := x - mpichdx^[ichT];
                           ConvertTyxy(x, xOrYDummy, tyxy, tyxyPrt);
                           MoveTo(x + hLeftArec + 2,
                             dyBaseline + amptyxydyUl[tyxyPrt]);

{$IFC fPrintOld}                                                    {gdf 1/16/84}

                           HRule(dxtb - 4);

{$ELSEC}                                                            {gdf 1/16/84}

                           Line(dxtb - 4, 0);                       {gdf 2/1/84 }

{$ENDC}                                                             {gdf 1/16/84}

                          Move(0, dyBaseCur - dyBaseLine - amptyxydyUl[tyxyPrt]);
                           END;
               OTHERWISE   Break(98);
               END;
            END;
     END; { GenTabImg }

     PROCEDURE SetTreStyle;
     VAR seteface: Style; ptPen: TPt; cbSuper: TC; dyBaseOld: TC;
     BEGIN
      { Set the style for a new tre. }
     {$IFC sampcalls}  ALogCall;  {$ENDC}
     IF fTstPrt THEN WRITE('PrtSetStyle: ');
     IF treCur^.arce.fUnderline THEN
          BEGIN
          IF NOT fUl THEN

{$IFC fPrintOld}                                                    {gdf 1/16/84}

              BEGIN xFstUl := ppr^.gp^.pnLoc.h; fUl := TRUE; END

{$ELSEC}                                                            {gdf 1/16/84}

              BEGIN xFstUl := thePort^.pnLoc.h; fUl := TRUE; END {???}  {gdf 2/2/84}

{$ENDC}                                                             {gdf 1/16/84}
         END
     ELSE IF fUl THEN    { Finish the underlining }
          BEGIN

{$IFC fPrintOld}                                                    {gdf 1/16/84}


          ptPen := ppr^.gp^.pnLoc;
          MoveTo(xFstUl, dyBaseline + amptyxydyUl[tyxyPrt]);
          HRule(ptPen.h - xFstUl);
          MoveTo(ptPen.h, ptPen.v);
{$ELSEC}                                                            {gdf 1/16/84}

          ptPen := thePort^.pnLoc;                           {???}  {gdf 2/2/84}
          MoveTo(xFstUl, dyBaseline + amptyxydyUl[tyxyPrt]);
          Line(ptPen.h - xFstUl, 0);
          MoveTo(ptPen.h, ptPen.v);

{$ENDC}                                                             {gdf 1/16/84}

          fUl := FALSE;
          END;

     TextFont(argfam[treCur^.arce.ifnt]);
     seteface := [];  { Normal face }
     IF treCur^.arce.fBold   THEN seteface := seteface + [bold];
     IF treCur^.arce.fItalic THEN seteface := seteface + [italic];
     TextFace(seteface);
     {PrTxMode(???);}  {unspecified 4/2/82}

     cbSuper := treCur^.dySuper;
     ConvertTyxy(xOrYDummy, cbSuper, tyxy, tyxyPrt);
     dyBaseOld := dyBaseCur;
     dyBaseCur := dyBaseline - cbSuper;
      { Adjust the vertical line from the old. }
     Move(0, dyBaseCur - dyBaseOld);
     END;{SetTreStyle}

     PROCEDURE NextTre;
     BEGIN
     {$IFC sampcalls}  ALogCall;  {$ENDC}
     treCur := treNxt;
     treNxt := POINTER(ORD(treCur) + treCur^.cb);
     IF treCur^.cb <> 0 THEN
          BEGIN
          WHILE treCur^.arce.fVan DO
               BEGIN
               treCur := treNxt;
               treNxt := POINTER(ORD(treCur) + treCur^.cb);
               END;
          ich := treCur^.ichFst;
          cchTre := treNxt^.ichFst - ich;
          SetTreStyle;
          END;
     END;{NextTre}

BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
IF fTstPrt THEN BEGIN WRITELN('PrtImgTld'); END;
pint := POINTER(ORD(lldInt)+crd^.arec.ointDep);
tyxyPrt := TyxyTld(pint^.ics);   { I think these are always equal - TJM }
tyxy := crd^.tyxy;

ConvertRTyxy(crd^.arec.r, r, crd^.arec.tyxy, tyxyPrt);
hLeftArec := r.left;

IF crd^.icsSon = icsNil THEN
     BEGIN
      { Set the baseline and underline parameters. }
     dyBaseline := atld.dyBase;
     ConvertTyxy(xOrYDummy, dyBaseline, tyxy, tyxyPrt);
     dyBaseline := r.top + dyBaseline;
     dyBaseCur := dyBaseline;
     MoveTo(hLeftArec, dyBaseCur);
     fUl := FALSE;
     fReqBrk := achadTld.rgch^[atld.ichLim - 1] = chCr;

     treCur := grpTre;
     treNxt := POINTER(ORD(treCur) + treCur^.cb);
     IF treCur^.arce.fVan THEN
          NextTre
     ELSE BEGIN
          cchTre := treNxt^.ichFst;
          SetTreStyle;
          END;

     { crc 4/20/83 -- the following code is unnecessary and wrong }
     (*
     IF (rgichSepFst^[0] = 0) AND (treCur^.tyfill <> tyfillNil) THEN
          BEGIN
           { This is not quite right: should be copy of space-processing below. }
          iwd := 0;
          GenTabImg(0, rgichWdFst^[1]);
          END;
     *)

     IF crd^.arec.strec.fformfeed THEN
          BEGIN
          IF fTstPrt THEN IF achadTld.rgch^[atld.ichLim-1] <> chFormFeed THEN
            WRITELN('*** Yech: formfeed not found when expected!');
          rgichSepFst^[atld.iwdLim - 1] := rgichSepFst^[atld.iwdLim - 1] - 1;
          END;

      { Print the characters, a word at a time. }
     FOR iwd := 0 TO atld.iwdLim - 1 DO
          BEGIN
           { Do the x-positioning only at the beginning of words. }
          x := rgoxWdFst^[iwd];
          ConvertTyxy(x, xOrYDummy, tyxy, tyxyPrt);
          IF fTstPrt THEN WRITELN('PrtImgTld - MoveTo(', x:4, ',', dyBaseCur:3, ')');
          MoveTo(x + hLeftArec, dyBaseCur);

          ich := rgichWdFst^[iwd];

          cchWd := rgichSepFst^[iwd] - ich;
          WHILE cchWd > 0 DO         { Process the word, using tre's as neccesary. }
               BEGIN
               cchDraw := CMin(cchTre, cchWd);
               {IF fTstPrt THEN
                   BEGIN
                   GetPort(pport);
                   WRITELN('Port at ', ORD(pport));
                   PxR('Clip rgn bbox:', pport^.clipRgn^^.rgnBBox);
                   PxR('Vis  rgn bbox:', pport^.visRgn^^.rgnBBox);
                   PxR('portRect:     ', pport^.portRect);
                   WRITELN('Clip rgn size:', pport^.clipRgn^^.rgnSize);
                   WRITELN('Vis  rgn size:', pport^.visRgn^^.rgnSize);
                   END;
               }
               IF (treCur^.ipgCur = ipgNil) OR (achadTld.rgch^[ich] <> chIpg) THEN
                    BEGIN
                    IF fTstPrt THEN BEGIN
                        FOR ichTmp := 1 TO cchDraw DO
                            spT[ichTmp] := CHR(achadTld.rgch^[ichTmp+ich-1]);
                        spT[0] := CHR(cchDraw);
                        WRITELN('DrawText of "', spT, '"');
                        END;
                    DrawText(POINTER(ORD(achadTld.rgch)), ich, cchDraw);
                    IF fTstPrt THEN WRITELN('Done DrawText');
                    END
               ELSE BEGIN
                    CToSp(treCur^.ipgCur + 1, spIpg);
                    DrawString(spIpg);
                    cchDraw := 1;
                    END;
               {IF fTstPrt THEN PxBitMap;}
               ich := ich + cchDraw;
               cchWd := cchWd - cchDraw;                   { Chars left in word.   }
               IF cchTre = cchDraw
                    THEN NextTre                           { New style in word.    }
                    ELSE cchTre := cchTre - cchDraw;       { Chars left in tre.    }
               END;

          cchSep := rgichWdFst^[iwd + 1] - ich;
           { Do simple test for 99.99% of cases: }
          IF (cchTre > cchSep) AND (treCur^.tyfill = tyfillNil) THEN
               cchTre := cchTre - cchSep
          ELSE BEGIN
               WHILE cchSep > 0 DO    { Process the sep, using tre's as neccesary. }
                    BEGIN
                    cchDraw := CMin(cchTre, cchSep);
                    IF treCur^.tyfill <> tyfillNil THEN GenTabImg(ich, cchDraw);
                    ich := ich + cchDraw;
                    cchSep := cchSep - cchDraw;           { Chars left in sep. }
                    IF (cchTre = cchDraw) THEN
                         BEGIN
                         IF (iwd <> (atld.iwdLim - 1)) OR fReqBrk THEN
                              BEGIN
                              { If fUl, put pen in right place. }
                              x := rgoxWdFst^[iwd + 1];
                              FOR ichT := rgichWdFst^[iwd + 1] - 1 DOWNTO ich DO
                                   x := x - mpichdx^[ichT];
                              ConvertTyxy(x, xOrYDummy, tyxy, tyxyPrt);
                              {PrTabTo(x+hLeftArec); }
                              MoveTo(x + hLeftArec, dyBaseCur);
                              END;
                         NextTre;
                         END
                    ELSE cchTre := cchTre - cchDraw;            { Chars left in tre. }
                    END;
               END;

          IF fTstPrt THEN WRITELN('Next word.');
          END;

     IF fReqBrk THEN
          BEGIN
          x := rgoxWdFst^[atld.iwdLim] - mpichdx^[atld.ichLim - 1];
          ConvertTyxy(x, xOrYDummy, tyxy, tyxyPrt);
          MoveTo(x + hLeftArec, dyBaseCur);
          END;
     IF fUl THEN
        BEGIN
        Move(0, dyBaseline - dyBaseCur + amptyxydyUl[tyxyPrt]);

{$IFC fPrintOld}                                                    {gdf 1/16/84}

        dhRule := xFstUl - ppr^.gp^.pnLoc.h;
        IF dhRule <> 0 THEN HRule(dhRule);

{$ELSEC}                                                            {gdf 1/16/84}

        dhRule := xFstUl - thePort^.pnLoc.h;  {???}            {gdf 2/2/84}
        IF dhRule <> 0 THEN Line(dhRule,0);

{$ENDC}                                                             {gdf 1/16/84}

        Move(0,-1);
        END;
     GetPen(ptRight);
     END
ELSE BEGIN
     crdSon := argcrd[crd^.icrd + 1];
     crdSon^.iimgp := crd^.iimgp;
     crdSon^.orec := crd^.orec;
     {crdSon^.arec.r.topLeft := crdSon^.ptOrigTopLft
                                 + (crd^.ptOrigTopLft - crd^.arec.r.topLeft);
      crdSon^.arec.r.botRight := offset to maintain width and height
     }
     dptOffset := crd^.arec.r.topLeft;
     SubPt(crd^.ptOrigTopLft, dptOffset);
     AddPt(crdSon^.ptOrigTopLft, dptOffset);
     SubPt(crdSon^.arec.r.topLeft, dptOffset);
     OffsetRect(crdSon^.arec.r, dptOffSet.h, dptOffset.v);
     GenImg(crdSon);
     ptRight := r.botRight;
     END;
(*
IF ptRight.h > r.left THEN
*)
IF not fOffPaper THEN
     BEGIN
     r.right := ptRight.h;
        {try catching bottom and top, too}
     UnionRect (r, rPrintable, rT);
     IF NOT EqualRect (rT, rPrintable) THEN fOffPaper := TRUE; {some part outside rprintable}
(*
     fDummy := SectRect(r, rPrintable, rT);
     fOffPaper := fOffPaper OR (NOT fEqRs(r, rT));
*)
     END;
{$ifc fDbgOk}
IF fTstPrt THEN BEGIN
    WRITELN('tyxy =', ORD(tyxyPrt));
    PxR('r:             ', r);
    PxR('printable part:', rT);
    END;
{$endc}
END;


{$S LWCreate}
{----------------}
PROCEDURE InitPrt;
{----------------}
VAR ch: CHAR;
    cErr: TC;
    i:          Integer;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
IF fTstPrt THEN WRITELN('InitPrt.');
fPrinting := false;
xOrYDummy := 0;

(* d *) IF fTstPrt THEN WRITELN('About to call PrMgrInit');

{$IFC fPrintOld}                                                    {gdf 1/16/84}

PrMgrInit(cErr);

{$ELSEC}                                                            {gdf 1/16/84}

PrMgrInit;
PrDlgDefault (FALSE, ePrNormal,
              24 * prPgFract, 99 * prPgFract,
              1 * prPgFract, 1 * prPgFract);

{$ENDC}                                                             {gdf 1/16/84}

(* d *) IF fTstPrt THEN WRITELN('Back from PrMgrInit');

FOR i := 0 to (sizeof (amptyxydyUl) div sizeof (TC)) - 1
  do amptyxydyUl [TTyxy (i)] := 1;       {quick kludge for ink jet}

(* amptyxydyUl[tyxyScrn] := 1; *)
amptyxydyUl[tyxyIP] := 2;
(* amptyxydyUl[tyxyDotMx] := 1; *)
amptyxydyUl[tyxyHiDotMx] := 4;

END; {InitPrt}

END.




ÿ