{$I APLW/COMPFLAGS }                                            {gdf 1/16/84}
{$R-}

UNIT UnitUf;

(* Copyright 1983, 1984, Apple Computer Inc. *)

{ gdf  2/27/84 -- insert trace calls for segmentation  }
{ gdf  1/16/84 -- printing compile flag & include COMPFLAGS added }
{ crc  8/17/82  --  fixed LpMacUf; new parms to CleanIfil, CloseIfil.
                    Note: FreeUfIcs currently ignores errors from above procs }
{ jrm  8/16/82  --  wrote LpMacUf }
{ crc  8/10/82  --  Added cError parm to WriteUCSDFolder }
{ jrm  6/29/82  --  USES change: LisaGraf => QuickDraw }
{ crc  6/16/82  --  Added function IfilOfUf; USES UnitHz before UnitFile }
{ crc  6/11/82  --  String parm to WriteUCSDFolder no longer VAR untill everyone
                    uses Pathname type }
{ crc  6/02/82  --  Conditional compilation }
{ crc 05/25/82  --  USES PrStd moved }
{ crc 05/03/82  --  FreeUfIcs now closes ifil }
{ crc 04/06/82  --  integration with new font manager }
{ bas 01/13/82  --  USES for Lisaprint, new wm }
{ bas 12/10/81  --  integration with font manager }

INTERFACE

USES
    {$IFC sampcalls}                                               {gdf 2/24/84}
       {$U aplw/tracecalls.obj} tracecalls,
    {$ENDC}
    {$U Obj:UnitStd.Obj }   UnitStd,
    {$U Obj:UnitHz.Obj }    UnitHz,
    {$U LibOS/Syscall.Obj }   Syscall,
    {$U Obj:UnitFile.Obj }  UnitFile,
    {$U Obj:Storage.Obj }   Storage,
    {$U Obj:QuickDraw.Obj }  QuickDraw,
    {$U Obj:FontMgr.Obj }   FontMgr,
    {$U Obj:PmDecl       }  PmDecl,
     {$IFC fPrintOld}                                               {gdf 1/16/84}
        {$U Obj:PrStd.Obj }     PrStd,
     {$ELSEC}
        {$U Obj:PrStdInfo.Obj } PrStdInfo,
          (* {$U Obj:PrPublic.Obj }  PrPublic,  unneeded ? *)
     {$ENDC}                                                        {gdf 1/16/84}
    {$U Obj:UnitFmt.Obj }   UnitFmt,
    {$U Obj:UnitCs.Obj }    UnitCs,
    {$U Obj:UnitFf.Obj}     UnitFf,
    {$U Obj:Wm.Events.Obj }  Events,
    {$U Obj:Wm.Folders.Obj } Folders,
    {$U Obj:WmlStd     }    WmlStd,
    {$U Obj:WM.Menus.Obj }  Menus,
    {$U Obj:AlertMgr.Obj }  AlertMgr,
    {$U ApLW/Queue.Obj }     Queue,
    {$U ApLW/UnitBox.Obj }   UnitBox,
    {$U ApLW/UnitRec.Obj }   UnitRec,
    {$U ApLW/UnitOver.Obj }  UnitOver,
    {$U ApLW/UnitTld.Obj }   UnitTld,
    {$U ApLW/UnitPrt.Obj }   UnitPrt;

CONST
    {$IFC NOT fDbgOk} fTstUf = FALSE; {$ENDC}
    chNull = 0;
    chDle = 16;
    cchPgUf = 1024;
    mskRelPgUf = 1023;

{$IFC fDbgOk}
VAR
     fTstUf: TF;
{$ENDC}

PROCEDURE PxHcsuf (hcs: THcs);

PROCEDURE SeqLpdUf (lpd: TLpd; VAR achad: TAchad);

FUNCTION IcsUfCreate (ifil: TB; hz: THz) : TB;

PROCEDURE FreeUfIcs (icsUf: TB);

PROCEDURE WriteUCSDFolder(VAR cError: TC; icsUf: TB; spTitle: TSp);

FUNCTION IfilOfUf (icsUf: TB):TB;

FUNCTION LpMacUf (icsUf: TB): TLp;


IMPLEMENTATION

{$IFC fSymOK }
{$D+ }
{$ELSEC }
{$D- }
{$ENDC }

CONST dxWidSp = 8;
    xLimLftUf = 552;
    xLftUf = 8;
    xRtUf = 672;

TYPE TCsuf = RECORD
          cs: TCs;
          lpLim:  TLp;
          ifil:   TB;
          END;
    TPcsuf = ^TCsuf;
    THcsuf = ^TPcsuf;

{$S LWDebug}
{------------------------------}
PROCEDURE PxHcsuf {(hcs: THcs) };
{------------------------------}
VAR hcsuf:   THcsuf;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
{$ifc fDbgOk}
hcsuf := POINTER(ORD(hcs));
WITH hcsuf^^ DO WRITELN('lpLim: ', lpLim, '; ifil:', ifil:3);
{$endc}
END;  { PxHcsuf }

{$S LWUsUf}
{---------------------------------------------------}
PROCEDURE SeqLpdUf { (lpd: TLpd; VAR achad: TAchad) };
{---------------------------------------------------}
LABEL  120, 130;
VAR lp, lpLim, lpPgUf:  TLp;
    hcsuf:     THcsuf;
    cchSeq: TC;
    achadSrc: TAchad;
    ichOfLp, ichT:   TC;
    hzT:    THz;
    wT:     TW;
    chT:        TCh;
    ifil: TB;
PROCEDURE SetXLft(ich: TC);
    VAR cSp:  TC;
    BEGIN
    {$IFC sampcalls}  ALogCall;  {$ENDC}
    IF achadSrc.rgch^[ich] = chDle THEN
          BEGIN
          cSp := achadSrc.rgch^[ich + 1] - 32;
          lpd^.arpe.xLftFst := CMin(xLftUf + cSp * dxWidSp, xLimLftUf);
          END;
    END;

BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
IF fTstUf THEN WRITELN('SeqLpdUf -- ics:', lpd^.ics:3, '; lp: ', lpd^.lpLim);
WITH rghcs^[lpd^.ics]^^ DO
    BEGIN
    IF tycs <> tycsUf THEN Break(220);
    hzT := hz;
    END;
hcsuf := @rghcs^[lpd^.ics]^;
lpd^.rce := @lpd^.arce;
MoveRgch(POINTER(ORD(lpd^.rce)), @arceStd, arceStd.cb);
lpd^.arce.ifnt := 2;
lpLim := hcsuf^^.lpLim;
cchSeq := achad.ichLim - achad.ichFst;
lp := LpMinP(lpd^.lpLim, lpLim);
lpd^.lp := lp;
cchSeq := LpMinP(lpLim - lp, cchSeq);
wT := lp;
ichOfLp := WAnd(wT, mskRelPgUf);
ifil := hcsuf^^.ifil;
lpPgUf := lp - ichOfLp;

IF lpd^.tyset.fRpe OR lpd^.tyset.fParBnds THEN
    BEGIN
    lpd^.rpe := @lpd^.arpe;
    MoveRgch(@lpd^.arpe, @arpeStd, arpeStd.cb);
    WITH lpd^.arpe DO
          BEGIN
          xLftFst := xLftUf;
          xRt := xRtUf;
          yLine := 2;
          END;
    achadSrc.ichLim := ichOfLp;
    achadSrc.ichFst := 0;
    WHILE TRUE DO
          BEGIN
          achadSrc.rgch :=
          POINTER(ORD(PMapN(hzT, NFromIfilIb(ifil, lpPgUf + cchPgUf))));
          ichT := IchCr(achadSrc, FALSE) + 1;
          IF (ichT <> 0) OR (lpPgUf = 0) THEN
               BEGIN
               lpd^.lpFstPar := lpPgUf + ichT;
               lpd^.fParSt := (lpd^.lpFstPar = lp);
               IF (ichT < cchPgUf) AND (achadSrc.rgch^[ichT] <> chNull) THEN
                 SetXLft(ichT)
               ELSE BEGIN
                 lpPgUf := lpPgUf + cchPgUf;
                 IF lpPgUf < hcsuf^^.lpLim THEN
                       BEGIN
                       achadSrc.rgch :=
                       POINTER(ORD(PMapN(hzT, NFromIfilIb(ifil, lpPgUf + cchPgUf))));
                       SetXLft(0);
                       END;
                 END;
               lpd^.arpe.xLftBody := lpd^.arpe.xLftFst;
               GOTO 120;
               END;
          lpPgUf := lpPgUf - cchPgUf;
          achadSrc.ichLim := cchPgUf;
          END;
120: achadSrc.ichFst := ichOfLp;
    lpPgUf := lp - ichOfLp;
    WHILE TRUE DO
          BEGIN
          achadSrc.ichLim := LpMinP(cchPgUf, lpLim - lpPgUf);
          achadSrc.rgch :=
          POINTER(ORD(PMapN(hzT, NFromIfilIb(ifil, lpPgUf + cchPgUf))));
          ichT := IchCr(achadSrc, TRUE);
          IF ichT <> cchPgUf THEN
               BEGIN
               IF ichT < achadSrc.ichLim THEN ichT := ichT + 1;
               lpd^.lpLimPar := lpPgUf + ichT;
               GOTO 130;
               END;
          lpPgUf := lpPgUf + cchPgUf;
          achadSrc.ichFst := 0;
          END;
130:
    cchSeq := CMin(cchSeq, lpd^.lpLimPar - lp);
    END;
achadSrc.ichFst := ichOfLp;
lpPgUf := lp - ichOfLp;
achadSrc.rgch := POINTER(ORD(PMapN(hzT, NFromIfilIb(ifil, lpPgUf + cchPgUf))));
chT := achadSrc.rgch^[ichOfLp];
lpd^.arce.fVan := TRUE;
IF (chT = chDle) THEN
    cchSeq := 2
ELSE IF (ichOfLp > 0) AND (achadSrc.rgch^[ichOfLp  - 1] = chDle) THEN
    cchSeq := 1
ELSE IF chT = chNull THEN
    cchSeq := cchPgUf - ichOfLp
ELSE BEGIN
    lpd^.arce.fVan := FALSE;
    cchSeq := CMin(cchPgUf - ichOfLp, cchSeq);
    WHILE (cchSeq <> 0) AND (achadSrc.rgch^[ichOfLp + cchSeq - 1] = chNull) DO
          cchSeq := cchSeq - 1;
    IF achad.rgch <> NIL THEN
          BEGIN
          achad.ichLim := achad.ichFst + cchSeq;
          MoveAchad(achad, achadSrc);
          END
    ELSE achad := achadSrc;
    END;
achad.ichLim := achad.ichFst + cchSeq;
lpd^.lpLim := lp + cchSeq;
{$ifc fDbgOk}
IF fTstUf THEN PxLpd(lpd, achad);
{$endc}
END;  { SeqLpdUf }

{$S LWUsUf}
{------------------------------------------------------------}
FUNCTION IcsUfCreate { ( ifil: TB; hz: THz) : TB };
{------------------------------------------------------------}
LABEL 100;
VAR pcsuf: TPcsuf;
    ics: TB;
    lpLim: TLp;
    ichOfLp: TC;
    rgch: TRgch;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
IF fTstUf THEN WRITELN('IcsUfCreate');
ics := IcsCreate(tycsUf, SIZEOF(TCsuf), hz);
WITH rghcs^[ics]^^.cspd DO
    BEGIN
    argproc[iprocSeqLpd]       := @SeqLpdUf;
    argproc[iprocFindLpFixed]  := @FindLpFstPar;
    argproc[iprocCrd]          := @GenCrdTld;
    argproc[iprocFreeIcs]      := @FreeUfIcs;
    argproc[iprocFSelLpBounds] := @TrueStdSelLpBounds;
    argproc[iprocPxHcs]        := @PxHcsuf;
    argproc[iprocHilight]      := @HilightTld;
    argimgp[iimgpScrn]         := @GenImgTld;
    argimgp[iimgpQume]         := @PrtImgTld;
    END;
pcsuf := POINTER(ORD(rghcs^[ics]^));
lpLim := ORD4(CBlkOfIFil(ifil) - 1) * cchPguf;
WHILE lpLim > 0 DO
      BEGIN
      ichOfLp := WAnd(lpLim - 1, mskRelPgUf);
      rgch := POINTER(ORD(
                PMapN(hz, NFromIfilIb(ifil, lpLim - 1 - ichOfLp + cchPgUf))));
      WHILE ichOfLp >= 0 DO
           BEGIN
           IF rgch^[ichOfLp] = chCr THEN GOTO 100;
           ichOfLp := ichOfLp - 1;
           lpLim := lpLim - 1;
           END;
      END;
100:
pcsuf^.lpLim := lpLim;
pcsuf^.ifil := ifil;
{$ifc fDbgOk}
IF fTstUf THEN PxHcsuf(rghcs^[ics]);
{$endc}
IcsUfCreate := ics;
END;

{$S LWUsUf}
{---------------------------------}
PROCEDURE FreeUfIcs { (icsUf: TB) };
{---------------------------------}
VAR rgh: TRgh;  hcsuf: THcsuf;  cError: TC;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
IF fTstUf THEN WRITELN('FreeUfIcs:', icsUf:3);
hcsuf := POINTER(ORD(rghcs^[icsUf]));
CleanIfil(cError, rghcs^[icsUf]^^.hz, hcsuf^^.ifil, FALSE);
CloseIfil(cError, hcsuf^^.ifil);
rgh := @rghcs^;
FreeBk(rghcs^[icsUf]^^.hz, rgh^[icsUf], tybkStd);
rgh^[icsUf] := hNil;
IF fTstUf THEN WRITELN('...icsUf', icsUf:3, ' freed.');
END;

{$S LWUsUf}
{----------------------------------------------------------------------}
PROCEDURE WriteUCSDFolder { (VAR cError: TC; icsUf: TB; spTitle: TSp) };
{----------------------------------------------------------------------}
LABEL 50, 100;
VAR achad: TAchad;
    tyset: TTyset;
    ich, ichExt: TC;
    fileOut: TEXT;
    cSp: TC;
    dxSpDiv2: TX;
    bufText: TRgch;
    spBuf: STRING[255];
    ichMacBuf: TC;
    lpCur:	TLp;
PROCEDURE WriteTextBuf;
    VAR ich: TC;
    BEGIN
    {$IFC sampcalls}  ALogCall;  {$ENDC}
    { Write out the buffer. }
    spBuf[0] := CHR(ichMacBuf - 2);
    WRITELN(fileOut, spBuf);
    ichMacBuf := 0;
    END;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
IF fTstUf THEN WRITELN('WriteUCSDFolder.');

 { Get the file name and open it. }
IF fTstUf THEN WRITELN('File name: ', spTitle, '!');
REWRITE(fileOut, spTitle);

 { Set up some constants. }
bufText := @spBuf;
dxSpDiv2 := dxWidSp DIV 2;
lpCur := 0;

 { Sequence through the ics, writing. }
WHILE TRUE DO
    BEGIN
    GenCrd(crdStd, icsUf, lpCur, tyxyScrn);
    IF crdStd^.arec.dlpLim = 0 THEN         { End of class. }
          GOTO 100;

    { Take care of the first of the paragraph. }
    cSp := (crdStd^.arec.r.left + dxSpDiv2 - xLftUf) DIV dxWidSp + 32;
    achadTld.ichFst := 0;
    achadTld.ichLim := atld.ichLim;
    WITH achadTld DO
          FOR ich := 0 TO ichLim - 1 DO
               IF rgch^[ich] = chSp THEN
                 BEGIN
                 cSp := cSp + 1;
                 END
               ELSE IF rgch^[ich] = chTab THEN
                 BEGIN
                 cSp := cSp + 5;
                 END
               ELSE BEGIN
                 ichFst := ich;
                 GOTO 50;
                 END;
    50:
    IF cSp > 33 THEN
          BEGIN
          bufText^[1] := chDle;
          bufText^[2] := cSp;
          ichMacBuf := 3;
          END
    ELSE IF cSp = 33 THEN
          BEGIN
          bufText^[1] := chSp;
          ichMacBuf := 2;
          END
    ELSE ichMacBuf := 1;

 { Copy the text into the buffer. }
    WITH achadTld DO
          BEGIN
          MoveRgch(@bufText^[ichMacBuf], @rgch^[ichFst], ichLim - ichFst);
          ichMacBuf := ichMacBuf + ichLim - ichFst;
          IF ichMacBuf >= 255 THEN Break(221);
          END;

    { Write the line out if this is it. }
    WriteTextBuf;

    lpCur := lpCur + crdStd^.arec.dlpLim;
    END;
100:
 { Close the file. }
CLOSE(fileOut, LOCK);
cError := 0;
END;


{$S LWUsUf}
{---------------------------------}
FUNCTION IfilOfUf {(icsUf: TB):TB};
{---------------------------------}
VAR
  hcsuf: THcsuf;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
IF icsUf<>icsNil THEN
   IF rghcs^[icsUf]^^.tycs=tycsUf THEN BEGIN
      hcsuf := POINTER(ORD(rghcs^[icsUf]));
      IfilOfUf := hcsuf^^.ifil;
      END
   ELSE BEGIN
      break(1630);
      IfilOfUf := 0;
      END
ELSE BEGIN
   break(1631);
   IfilOfUf := 0;
   END;
END;  {IfilOfUf}

{$S LWUsUf}
{--------------------------------------}
FUNCTION LpMacUf (* (icsUf: TB): TLp *);
{--------------------------------------}
VAR hcsuf: THcsuf;
BEGIN
   {$IFC sampcalls}  ALogCall;  {$ENDC}
   hcsuf := POINTER(ORD(rghcs^[icsUf]));
   LpMacUf := hcsuf^^.lpLim;
END;


END.


ÿ