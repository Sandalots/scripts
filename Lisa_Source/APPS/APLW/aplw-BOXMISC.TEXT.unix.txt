{ BOXMISC.TEXT -- split off from UNITBOX because UNITBOX got too big
  to edit.  }

(* Copyright 1983, 1984, Apple Computer Inc. *)

{ gdf  2/27/84 -- insert trace calls for segmentation  }
{ jrm 10/15/82 -- check for oboxNil in OboxFldr }
{ bas  7/28/82 -- Tybox function }
{ bas  7/20/82 -- PxObox new box structure }
{ bas  1/21/82 -- handle icon-based scroll boxes }
{ edited by jrm 11/30/81 }


{$S LWActive}
{---------------------}
FUNCTION OboxFstFldr;
{---------------------}
LABEL 99;
VAR pbox: TPbox; obox: TC;
BEGIN
   {$IFC sampcalls}  ALogCall;  {$ENDC}
    obox := bdsp^.oboxRoot;
    WHILE obox <> oboxNil DO BEGIN
       pbox := POINTER(ORD(bdsp) + obox);
       IF pbox^.tybox = tyboxTab THEN GOTO 99;
       obox := pbox^.oboxNxt;
    END;
99: OboxFstFldr := obox;
END;

{$S LWSelect}
{----------------------------------}
FUNCTION OboxFldr { (obox: TC): TC };
{----------------------------------}
 { OboxFldr of obox, oboxNil if not in displayed list. }
VAR pbox: TPbox; oboxF, oboxTst, oboxLst: TC;
BEGIN
    {$IFC sampcalls}  ALogCall;  {$ENDC}
    IF fTstBox THEN WRITE('OboxFldr of ', obox);
    oboxF := oboxNil;
    IF obox <> oboxNil THEN
       BEGIN
       pbox := POINTER(ORD(bdsp) + obox);
       IF pbox^.tybox = tyboxTab THEN
           oboxF := obox
       ELSE BEGIN
           oboxTst := bdsp^.oboxRoot;
           REPEAT
              pbox := POINTER(ORD(bdsp) + oboxTst);
              IF pbox^.tybox = tyboxTab THEN oboxF := oboxTst;
              oboxLst := oboxTst;
              oboxTst := pbox^.oboxNxt;
           UNTIL (oboxLst = obox) OR (oboxTst = oboxNil);
           END;
       END;
    IF fTstBox THEN WRITELN('...', oboxF);
    OboxFldr := oboxF;
END;

{$S LWActive}
{-------------------------------------}
FUNCTION OboxNxtFldr { (obox: TC): TC };
{-------------------------------------}
 { Returns oboxTab of next folder = oboxLim of this folder. }
LABEL 99;
VAR pbox: TPbox;
BEGIN
    {$IFC sampcalls}  ALogCall;  {$ENDC}
    IF fTstBox THEN WRITE('OboxNxtFldr --', obox:4);
    pbox := POINTER(ORD(bdsp) + obox);
    REPEAT
       obox := pbox^.oboxNxt;
       IF obox = oboxNil THEN GOTO 99;
       pbox := POINTER(ORD(bdsp) + obox);
       UNTIL pbox^.tybox = tyboxTab;
99: IF fTstBox THEN WRITELN('...', obox:4);
    OboxNxtFldr := obox;
END;


{-------------------------------------------------------}
PROCEDURE LinkFolder {(oboxLftTab: TC; oboxPrv: TC)};
{-------------------------------------------------------}
VAR pboxLim, pboxLst, pboxPrv: TPbox; oboxLim, oboxPrvOld, oboxLst: TC;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
IF fTstBox THEN WRITELN('LinkFolder -- Linking ', oboxLftTab, ' to ', oboxPrv);
 { search for the last obox in the to-be-linked folder }
oboxLst := OboxPrvFromObox(OboxNxtFldr(oboxLftTab));
pboxLst := POINTER(ORD(bdsp) + oboxLst);
 { search for the old previous obox }
oboxPrvOld := OboxPrvFromObox(oboxLftTab);
 { splice this folder out from where it is, if on the displayable list }
IF oboxPrvOld <> oboxNil THEN
   BEGIN
   pboxPrv := POINTER(ORD(bdsp) + oboxPrvOld);
   pboxPrv^.oboxNxt := pboxLst^.oboxNxt;
   END;
 { put it where it's going }
pboxPrv := POINTER(ORD(bdsp) + oboxPrv);
pboxLst^.oboxNxt := pboxPrv^.oboxNxt;
pboxPrv^.oboxNxt := oboxLftTab;
END;  { LinkFolder }


{ *********** ********** }
{$IFC fDbgOk }
{$S LWDebug}
{-------------------------------------------------}
PROCEDURE PxClip;
{-------------------------------------------------}
VAR port: TPort; rWind: TR;
BEGIN
    {$IFC sampcalls}  ALogCall;  {$ENDC}
    WRITE('PxClip -- ');
    GetPort(port);
    WRITELN('  thePort = ', ORD(port));
    rWind := port^.portRect;
    PxR('port^.portRect:   ', rWind);
    PxR('port^.portBits.bounds:   ', port^.portBits.bounds);
    WITH port^.portBits.bounds DO OffsetRect(rWind, -left, -top);
    PxR('Port rect (global):   ', rWind);
    (*
       WRITELN('clipRgn: ');    Note: DumpRgn no longer exists. Thanks, Bill.
       DumpRgn(port^.clipRgn);
       WRITELN('visRgn: ');
       DumpRgn(port^.visRgn);
    *)
END;

(*
       PROCEDURE PxSicon {(sicon: TSicon)};
       BEGIN
           WRITE('[');
           IF iconSkewer IN sicon THEN WRITE(' Skr');
           IF iconArwA   IN sicon THEN WRITE(' ArwA');
           IF iconPagA   IN sicon THEN WRITE(' PagA');
           IF iconThumb  IN sicon THEN WRITE(' Thmb');
           IF iconPagB   IN sicon THEN WRITE(' PagB');
           IF iconArwB   IN sicon THEN WRITE(' ArwB');
           WRITE(' ]');
       END; { PxSicon }
*)

{-------------------------------------------------}
PROCEDURE PxObox {(obox: TC)};
{-------------------------------------------------}
VAR pbox: TPbox;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
pbox := POINTER(ORD(bdsp) + obox);
WITH pbox^ DO
   BEGIN
   WRITELN('OBOX:', obox:4, ', oboxNxt:', oboxNxt:4, 'refCon: ', refCon);
   PxR('    r: ', r);
   IF fUpdate THEN WRITELN('needs updating;  ');
   WRITELN('     window: ', ORD(window));
   CASE tybox OF
      tyboxTab:  BEGIN
                 WRITELN('LEFT TAB:  fSbox? ', fSbox);
                 IF hspTitle = NIL THEN WRITELN('(TITLE)') ELSE WRITELN(hspTitle^^);
                 END;
      tyboxPane: BEGIN
                 WRITELN('PANE  : oboxLink[H V]: [', rgoboxLink[tyvhH]:4,
                       rgoboxLink[tyvhV]:4, '],  fScHead[H V]: [',
                       rgfScHead[tyvhH]:4, ' ', rgfScHead[tyvhV]:4, ']');
                 END;
      tyboxScroll:
                 BEGIN
                 WRITELN('SCROLL:  Horiz?: ', tyvh=tyvhH, ', oboxPaneFst: ',
                         oboxPaneFst:4);
                 WRITE('cPct: ', cPercent, ', sicon: ');
                 (*  I'll restore the PxSIcon calls when I have time to
                     put all the stuff used by both Lotus and Brad's Wmlib
                     into one place... JRM
                 PxSicon(sicon); WRITE(', vis: '); PxSicon(siconVis); WRITELN;
                 *)
                 END;
      tyboxGrow: WRITELN('GROW');
      tyboxMenu: WRITELN('MENU');
      OTHERWISE  WRITELN(ORD(tybox));
      END;
   END;
END;


{-------------------------------------------------}
PROCEDURE PxBdsp;
{-------------------------------------------------}
VAR pbox: TPbox; i: INTEGER; obox: TC;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
WRITELN('bdsp: ', ORD(bdsp), ', oboxLim: ', bdsp^.oboxLim:4,
  ', oboxFreeFst: ', bdsp^.oboxFreeFst:4, ', oboxActive: ', oboxActive:4,
  ', oboxRoot: ', bdsp^.oboxRoot:4);
obox := bdsp^.oboxRoot;
WRITELN('Allocated boxes:');
i := 0;
WHILE obox <> oboxNil DO
        BEGIN
        WRITE(obox);
        WRITE(' (');
        pbox := POINTER(ORD(bdsp) + obox);
        CASE pbox^.tybox OF
           tyboxTab:      WRITE('Tab');
           tyboxPane:     WRITE('Pane');
           tyboxScroll:   WRITE('Scroll');
           tyboxNil:      WRITE('Root');
           tyboxMenu:     WRITE('Menu');
           tyboxGrow:     WRITE('Grow');
           END;
        obox := pbox^.oboxNxt;
        WRITE(')');
        i := i + 1;
        IF i MOD 3 = 0 THEN WRITELN;
        IF obox <> oboxNil THEN WRITE(' --> ');
        END;
IF i MOD 3 <> 0 THEN WRITELN;
WRITELN('Free boxes:');
obox := bdsp^.oboxFreeFst;
i := 0;
WHILE obox <> oboxNil DO
        BEGIN
        WRITE(obox:4, ',');
        i := i + 1;
        IF i MOD 8 = 0 THEN WRITELN;
        pbox := POINTER(ORD(bdsp) + obox);
        obox := pbox^.oboxNxt;
        END;
WRITELN;
END;

{-------------------------------------------------}
PROCEDURE PxScrollChain { (obox:TC) };
{-------------------------------------------------}
VAR pboxT: TPbox; oboxT: TC; tyvh: TTyvh;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
   oboxT := obox;
   pboxT := PboxFromObox(oboxT);
   IF pboxT^.tybox = tyboxScroll
       THEN tyvh := pboxT^.tyvh
       ELSE tyvh := tyvhH;
   WRITELN('PxScrollChain with obox: ', obox, ', Horiz? ', tyvh=tyvhH,
            ', scroll bar? ', pboxT^.tybox=tyboxScroll);
   oboxT := OboxNxtSc(oboxT, tyvh);
   WHILE (oboxT <> obox) DO BEGIN
       WRITELN('   obox: ', oboxT);
       oboxT := OboxNxtSc(oboxT, tyvh);
       END;

   IF pboxT^.tybox <> tyboxScroll THEN BEGIN
       tyvh := tyvhV;
       WRITELN('PxScrollChain vertical direction');
       oboxT := obox;
       REPEAT
           WRITELN('   obox: ', oboxT);
           oboxT := OboxNxtSc(oboxT, tyvh);
       UNTIL oboxT = obox;
       END;
END;

{ *********** ********** }
(*
PROCEDURE  PxBdsp; EXTERNAL;
PROCEDURE  PxObox; EXTERNAL;
PROCEDURE  PxClip; EXTERNAL;
PROCEDURE  PxScrollChain; EXTERNAL;
*)
{$endc}

{$S LWCommand}
{-------------------------------------------------}
FUNCTION PboxFromObox; { (obox: TC): TPbox; }
{-------------------------------------------------}
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
PboxFromObox := POINTER(ORD(bdsp) + obox);
END;

{-------------------------------------------------}
FUNCTION OboxNxtSc { (obox:TC; tyvh:TTyvh): TC } ;
{-------------------------------------------------}
 { Returns the next obox in the (horizontal or vertical) scroll chain }
VAR pbox: TPbox;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
pbox := POINTER(ORD(bdsp) + obox);
IF pbox^.tybox = tyboxScroll
    THEN OboxNxtSc := pbox^.oboxPaneFst
    ELSE OboxNxtSc := pbox^.rgoboxLink[tyvh];
END;

{$S LWSplit}
{-------------------------------------------------}
FUNCTION OboxPrvSc { (obox:TC; tyvh:TTyvh): TC} ;
{-------------------------------------------------}
 { Returns the previous obox in the (horizontal or vertical) scroll chain }
VAR oboxChase, oboxLst: TC;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
oboxChase := obox;
REPEAT
    oboxLst := oboxChase;
    oboxChase := oboxNxtSc(oboxLst, tyvh);
    UNTIL oboxChase = obox;
OboxPrvSc := oboxLst;
END;

{$S LWCommand}
{-------------------------------------------------}
FUNCTION OboxScrollFromObox { (obox:TC; tyvh:TTyvh): TC } ;
{-------------------------------------------------}
 { Returns the scroll bar associated with the given obox in the given
   direction.  Value is oboxNil if no scroll bar is associated (shouldn't happen).
   Returns obox if obox is a scroll bar.}
LABEL 99;
VAR oboxT: TC; pboxT: TPbox;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
IF fTstBox THEN WRITE('OboxScrollFromObox of ', obox, ', Horiz? ',
                      tyvh=tyvhH);
oboxT := obox;
REPEAT
    pboxT := POINTER(ORD(bdsp) + oboxT);
    IF pboxT^.tybox = tyboxScroll THEN GOTO 99;
    oboxT := pboxT^.rgoboxLink[tyvh];
    UNTIL oboxT = obox;
{ looped around w/o finding a scroll box }
BREAK(1120);
99:
IF fTstBox THEN WRITELN('  ... = ', oboxT);
OboxScrollFromObox := oboxT;
END;


{$S LWActive}
{-------------------------------------------------}
FUNCTION OboxPrvFromObox {(obox: TC): TC} ;
{-------------------------------------------------}
 { returns the previous obox, nil if it's not on the active list }
 { returns the last obox on the active list if given oboxNil }
VAR oboxTst, oboxPrv: TC; pbox: TPbox;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
oboxTst := bdsp^.oboxRoot;
WHILE (oboxTst <> obox) AND (oboxTst <> oboxNil) DO
   BEGIN
   oboxPrv := oboxTst;
   pbox := POINTER(ORD(bdsp) + oboxTst);
   oboxTst := pbox^.oboxNxt;
   END;
IF oboxTst = obox
   THEN OboxPrvFromObox := oboxPrv
   ELSE OboxPrvFromObox := oboxNil;
END;


{$S LWScroll}
{-------------------------------------------------}
PROCEDURE UnlinkObox {(oboxOut: TC)} ;
{-------------------------------------------------}
 { splice out oboxOut: kind of slow... }
LABEL 10;
VAR obox, oboxPrv: TC; pboxPrv, pboxOut: TPbox; i: INTEGER;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
 { find the previous obox }
oboxPrv := OboxPrvFromObox(oboxOut);
IF oboxPrv = oboxNil THEN GOTO 10;
IF fTstBox THEN WRITELN('UnlinkObox -- unlinking ', oboxOut, ' from ', oboxPrv);
pboxPrv := POINTER(ORD(bdsp) + oboxPrv);
pboxOut := POINTER(ORD(bdsp) + oboxOut);
 { simple splice-out }
pboxPrv^.oboxNxt := pboxOut^.oboxNxt;
pboxOut^.oboxNxt := oboxNil;
10:
END;

ÿ