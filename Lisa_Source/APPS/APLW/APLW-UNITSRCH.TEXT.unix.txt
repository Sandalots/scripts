{$I APLW/COMPFLAGS }                                            {gdf 1/16/84}
{$R-}

UNIT UnitSrch;

(* Copyright 1983, 1984, Apple Computer Inc. *)

{ gdf  2/27/84 -- insert trace calls for segmentation  }
{ med  2-20/84 -- password protection }
{ gdf  1/16/84 -- printing compile flag & include COMPFLAGS added }
{ tjm  4/06/83  --  Undo Change - (WP 284) }
{ tjm  4/06/83  --  RemoveKid(fSaveKid) - (WP 258) }
{ jrm 11/ 9/82  --  changed vsize parameter in ShowPrompt to 25 }
{ jrm 11/ 3/82  --  added RemoveKid call to Change }
{ jrm 10/28/82  --  added LpLastCr call; Alert if no Find string }
{ jrm  8/31/82  --  renamed LISA:SEARCH.LOTUS }
{ crc  8/17/82  --  Addt'l parms to IfilOpen; error code currently ignored }
{ jrm  6/29/82  --  USES changes: LisaGraf => QuickDraw; moved USES UnitFile }
{ crc  6/02/82  --  Conditional compilation }
{ crc 06/01/82  --  InitSrch created }
{ crc 05/25/82  --  USES PrStd added }
{ crc 04/06/82  --  integration with new font manager }
{ jrm  1/28/82  --  integration with new menumgr }
{ bas 01/13/82  --  USES for new wm }
{ bas 12/10/81  --  integration with font manager }

INTERFACE

USES
     {$IFC sampcalls}                                               {gdf 2/24/84}
        {$U aplw/tracecalls.obj} tracecalls,
     {$ENDC}
     {$U Obj:UnitStd.Obj  }  UnitStd,
     {$U Obj:UnitHz.Obj   }  UnitHz,
     {$U LibOS/Syscall.Obj }   Syscall,
     {$U Obj:UnitFile.Obj }  UnitFile,
     {$U Obj:Storage.Obj  }  Storage,
     {$U Obj:QuickDraw.Obj }  QuickDraw,
     {$U Obj:FontMgr.Obj  }  FontMgr,
     {$U Obj:PmDecl       }  PmDecl,
     {$IFC fPrintOld}                                               {gdf 1/16/84}
        {$U Obj:PrStd.Obj }     PrStd,
     {$ELSEC}
        {$U Obj:PrStdInfo.Obj } PrStdInfo,
          (* {$U Obj:PrPublic.Obj }  PrPublic,  unneeded ? *)
     {$ENDC}                                                        {gdf 1/16/84}
     {$U Obj:UnitFmt.Obj  }  UnitFmt,
     {$U Obj:UnitCs.Obj   }  UnitCs,
     {$U Obj:UnitFf.Obj   }  UnitFf,
     {$U Obj:Wm.Events.Obj } Events,
     {$U Obj:Wm.Folders.Obj} Folders,
     {$U Obj:Wm.Menus.Obj }  Menus,
     {$U Obj:WmlStd       }  WmlStd,
     {$U Obj:Filercomm    }  Filercomm,
     {$U Obj:AlertMgr.Obj }  AlertMgr,
     {$U ApLW/Queue.Obj    }  Queue,
     {$U ApLW/UnitBox.Obj  }  UnitBox,
     {$U ApLW/UnitRec.Obj  }  UnitRec,
     {$U ApLW/UnitOver.Obj }  UnitOver,
     {$U ApLW/UnitTld.Obj  }  UnitTld,
     {$U ApLW/UnitUs.Obj   }  UnitUs,
     {$U ApLW/UnitEd.Obj   }  UnitEd,
     {$U ApLW/UnitEdit.Obj }  UnitEdit,
     {$U ApLW/UnitFm.Obj   }  UnitFm,
     {$U ApLW/UnitKid.Obj  }  UnitKid,
     {$U ApLW/UnitDsp.Obj  }  UnitDsp,
     {$U ApLW/UnitSel.Obj  }  UnitSel,
     {$U ApLW/UnitScrap.Obj}  UnitScrap,
     {$U ApLW/UnitCom.Obj  }  UnitCom,
     {$U IntrLib.Obj      }  International,
     {$U ApLW/EventMgr.Obj }  EventMgr,
     {$U ApLW/UnitLDialog.Obj } UnitLDialog;

{$IFC NOT fDbgOk}
CONST
  fTstSrch = FALSE;
{$ENDC}

VAR
{$IFC fDbgOk} fTstSrch: TF; {$ENDC}
icsFind: TB;
chElip: TCh;

PROCEDURE DoFindImid(imid: TC; orecActive: TC);
PROCEDURE InitSrch;

IMPLEMENTATION

{$IFC fSymOK }
{$D+ }
{$ELSEC }
{$D- }
{$ENDC }

CONST  ichMaxKey = 100;
       lfldBFind = 0;
       lfldBChange = 4;
       lfldBMsg = 8;

       dchCase = 32;

TYPE TTyrslt = (tyrsltNotFound, tyrsltFound, tyrsltAbort);
    TChch1 =  PACKED RECORD ch: 0..255; ch1: 0..255; END;
             {Kludge record to convert TB (i.e. -128..127) to 0..255}

VAR tyrsltMsg: TTyrslt;
    icsNotFound: TB;
    icsAborted: TB;


{$S LWSearch}
{---------------------------------------------------------------------}
PROCEDURE FindAchad(ointSrch: TC; achadKey: TAchad;
                    VAR ointFound: TC; VAR tyrslt: TTyrslt);
{---------------------------------------------------------------------}
       { The algorithm is this:  we look ahead to try to
       match the last character first:  ampchdch is a
       mapping from what character we find to the least
       number we'd have to skip before we MIGHT match
       again.  When we no longer have enough characters
       in achad to check the last, save the ones we
       can't yet check, and SeqLpd to fill in more of
       them.  Keep a mapping of the lp corresponding to
       EACH ich so we know lpFind and dlpFind.  }
LABEL   200, 100;
CONST
    chLst = 255;        {This should be changed in UnitStd }
VAR achadK, achad, achadSave:  TAchad;
    argch, argchSave:  ARRAY [0..ichMaxKey] OF TCh;
    arglpSave: ARRAY[0..ichMaxKey] OF TLp;
    dchSrc, dch, dchNxtChLst:    TB;
    ampchdch:  ARRAY [0..chLst] OF TB;
    ampchfNotSeen: ARRAY [0..chLst] OF TF;
    argcharMatching: PACKED ARRAY[0..MaximumCharsMatching] OF CHAR;
    fFirstLoop, fDone: TF;
    lpFstFind, lpLimFind, lpLimT: TLp;
    tyset: TTyset;
    chch1, chch1T, chch1T1: TChch1;
    ichLstKey, ichT, ichT1, ichFst, ichSrc, ichLp, ich2: TC;
    pint:   TPint;
    icsSrch: TB;
    p1, p2: TP;
    cT, cMatching: TC;
    charT, charT1: CHAR;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
achadK.ichFst := 0; achadK.ichLim := achadKey.ichLim - achadKey.ichFst;
achadK.rgch := @achadKey.rgch^[achadKey.ichFst];
ichLstKey := achadK.ichLim - 1;
IF fTstSrch THEN
     BEGIN
     WRITELN('FindAchad -- MEMAVAIL: ', MemAvail);
     WRITE('Searching for: ');
     FOR ichT := 0 to ichLstKey DO WRITE(CHR(achadK.rgch^[ichT]));
     WRITELN;
     END;

 { create the mapping from last-char mismatch to how many to skip }
IF fTstSrch THEN WRITELN('@ampchdch: ', ORD(@ampchdch));
FOR cT := 0 TO chLst DO
   BEGIN
   ampchdch[cT] := achadK.ichLim;
   ampchfNotSeen[cT] := TRUE;
   END;
dchNxtChLst := ichLstKey + 1;
FOR ichT := ichLstKey DOWNTO 0 DO
   BEGIN
   chch1.ch := achadK.rgch^[ichT];
   IF ampchfNotSeen[chch1.ch] THEN
      BEGIN
      ampchfNotSeen[chch1.ch] := FALSE;
      IF fMatchCase THEN ampchdch[chch1.ch] := ichLstKey - ichT
      ELSE BEGIN
           charT := CHR(chch1.ch);
           CharsMatching(charT, compWeak, @argcharMatching, cMatching);
           IF cMatching = 0 THEN Break(702);
           FOR cT := 0 TO cMatching - 1 DO
              BEGIN (* another unsavory chch1 hack *)
              chch1T.ch := ORD(argcharMatching[cT]);
              IF ampchdch[chch1T.ch] > ichLstKey - ichT THEN
                 ampchdch[chch1T.ch] := ichLstKey - ichT;
              END;
           END;
      END;
   IF (ampchdch[chch1.ch] = 0) AND (ichT <> ichLstKey) THEN
      dchNxtChLst := CMin(dchNxtChLst, ichLstKey - ichT);
   END;

IF fTstSrch THEN
     BEGIN
     WRITELN('ichLstKey: ', ichLstKey:4, '; dchNxtChLst: ', dchNxtChLst:4,
     ' ampchdch: ');
     FOR cT := 0 to chLst DO
          BEGIN
          IF ampchdch[cT] <> ichLstKey + 1 THEN
             WRITELN(CHR(cT), ' (', cT:3, ')', ' =', ampchdch[cT]:3);
          END;
     WRITELN;
     END;

fDone := FALSE;
achadSave.rgch := @argchSave;
achadSave.ichLim := 0;
achad.rgch := @argch; achad.ichFst := 0; achad.ichLim := ichMaxKey;
pint := POINTER(ORD(lldInt) + ointSrch);
lpLimT := pint^.lpLim;
icsSrch := pint^.ics;
{$ifc fDbgOk}
IF fTstSrch THEN BEGIN WRITE('intSrch: '); PxInt(ointSrch); END;
{$endc}
tyset.fParBnds := FALSE; tyset.fRce := FALSE; tyset.fRpe := FALSE;
SetLpd(lpdStd, icsSrch, pint^.lpFst, tyset, achad);
dchSrc := ichLstKey;
WHILE TRUE DO
     BEGIN
     IF (lpdStd^.lpLim = lpdStd^.lp) THEN
         BEGIN
         tyrslt := tyrsltNotFound;
         GOTO 200;
         END;
     IF Abort THEN
         BEGIN
         tyrslt := tyrsltAbort;
         GOTO 200;
         END;
     IF (lpdStd^.lpLim >= lpLimT) THEN
         BEGIN
         IF fTstSrch THEN WRITELN('Document ending...');
         fDone := TRUE;
         IF lpdStd^.rce^.fVan THEN
              BEGIN
              tyrslt := tyrsltNotFound;
              GOTO 200;
              END
         ELSE achad.ichLim := achad.ichLim - (lpdStd^.lpLim - lpLimT);
         END;
     IF NOT lpdStd^.rce^.fVan THEN
         BEGIN
         {$ifc fDbgOk}
         IF fTstSrch THEN PxAchad(achad);
         {$endc}
         ichSrc := achad.ichFst + dchSrc;
         WHILE ichSrc < achad.ichLim DO
              BEGIN
              chch1.ch := achad.rgch^[ichSrc];
              dch := ampchdch[chch1.ch];
              IF dch = 0 THEN
                   BEGIN
                   ichT := ichSrc - 1;
                   ichT1 := ichLstKey - 1;
                   ichFst := CMax(ichSrc - ichLstKey, achad.ichFst);
                   IF fTstSrch THEN
                   WRITELN('ichSrc: ', ichSrc, '; ichT: ', ichT,
                   '; ichT1: ', ichT1, '; ichFst: ', ichFst);
                   WHILE ichT >= ichFst DO
                        BEGIN
                        charT := CHR(achad.rgch^[ichT]);
                        charT1 := CHR(achadK.rgch^[ichT1]);
                        IF charT <> charT1 THEN
                           BEGIN (* bizarre coding style is to get around the
                                    compiler's bad habit of evaluating ALL 'OR'
                                    clauses, even if the first is TRUE *)
                           IF fMatchCase THEN GOTO 100;
                           IF NOT CompareChar(charT,
                                   charT1, compWeak) THEN GOTO 100;
                           END;
                        IF fTstSrch THEN WRITE('.');
                        ichT := ichT - 1;
                        ichT1 := ichT1 - 1;
                        END;
                   ichT := achadSave.ichLim - 1;
                   WHILE ichT1 >= 0 DO
                        BEGIN
                        charT := CHR(argchSave[ichT]);
                        charT1 := CHR(achadK.rgch^[ichT1]);
                        IF charT <> charT1 THEN
                           BEGIN (* bizarre coding style is to get around the
                                    compiler's bad habit of evaluating ALL 'OR'
                                    clauses, even if the first is TRUE *)
                           IF fMatchCase THEN GOTO 100;
                           IF NOT CompareChar(charT,
                                       charT1, compWeak) THEN GOTO 100;
                           END;
                        IF fTstSrch THEN WRITE('+');
                        ichT := ichT - 1;
                        ichT1 := ichT1 - 1;
                        END;
                   tyrslt := tyrsltFound;
                   GOTO 200;
    100:    {Not found}
                   dch := dchNxtChLst;
                   END;
              ichSrc := ichSrc + dch;
              END;
         ichT := ichSrc - ichLstKey;
         IF ichT < achad.ichFst THEN
              BEGIN
              dch:= achad.ichFst - ichT;      {i.e. ichLimSaveNew}
              IF dch > achadSave.ichLim THEN Break(51);
              MOVELEFT(argchSave[achadSave.ichLim - dch], argchSave, dch);
              MOVELEFT(arglpSave[achadSave.ichLim - dch], arglpSave, dch *
              SIZEOF(TLp));
              achadSave.ichLim := dch;
              ichT := achad.ichFst;
              END
         ELSE achadSave.ichLim := 0;
         { store the leftover characters and lp's in the Save arrays }
         achadSave.ichFst := achadSave.ichLim;
         achadSave.ichLim := achadSave.ichLim + (achad.ichLim - ichT);
         achad.ichFst := ichT;
         MoveAchad(achadSave, achad);
         FOR ich2 := ichT TO achad.ichLim - 1 DO
              arglpSave[ich2 - ichT + achadSave.ichFst] := lpdStd^.lp + ich2;
         { dchSrc is how far into the new achad we'll have to start }
         dchSrc := ichSrc - achad.ichLim;
         END;
     IF fDone THEN BEGIN tyrslt := tyrsltNotFound; GOTO 200; END;
     { get the next run }
     achad.ichFst := 0;
     achad.ichLim := ichMaxKey;
     achad.rgch := @argch;
     SeqLpd(lpdStd, achad);
     END;
200:
IF tyrslt = tyrsltFound THEN
     BEGIN
     IF ichSrc >= ichLstKey
       THEN lpFstFind := lpdStd^.lp + (ichSrc - ichLstKey - achad.ichFst)
       ELSE lpFstFind := arglpSave[achadSave.ichLim - (ichLstKey - ichSrc)];
     lpLimFind := lpdStd^.lp + ichSrc + 1 - achad.ichFst;
     ointFound := OintMark(ointNil, icsSrch, lpFstFind, lpLimFind, TRUE, ointNil);
     IF fTstSrch THEN WRITELN('Found it from ', lpFstFind, ' to ', lpLimFind);
     END
ELSE ointFound := ointNil;
END;


(*************************************************************
{-------------------------------------------}
FUNCTION OintFind(ointKey, ointSrch: TC): TC;
{-------------------------------------------}
LABEL   10, 100;
VAR achad:  TAchad;
 argchKey:  ARRAY [0..ichMaxKey] OF TCh;
 tyset: TTyset;
 pint:   TPint;
 ointFound: TC;
 lpLimT, lp: TLp;
 ichFst, ichLim, ich: TC;
 ch: TCh;
 lpFstFound: TLp;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
 { create an achad with the search string in it }
achad.ichFst := 0; achad.ichLim := ichMaxKey; achad.rgch := @argchKey;
pint := POINTER(ORD(lldInt) + ointKey);
lpLimT := pint^.lpLim;
tyset.fParBnds := FALSE; tyset.fRce := FALSE; tyset.fRpe := FALSE;
SetLpd(lpdStd, pint^.ics, pint^.lpFst, tyset, achad);
WHILE TRUE DO
 BEGIN
 IF lpdStd^.lpLim >= lpLimT THEN
   BEGIN
   IF lpdStd^.rce^.fVan THEN
        achad.ichLim := achad.ichFst
   ELSE achad.ichLim := achad.ichLim - (lpdStd^.lpLim - lpLimT);
   GOTO 10;
   END;
 { This will trigger on runs too long or end-of-stream. }
 IF NOT lpdStd^.rce^.fVan THEN
     BEGIN
     IF achad.ichFst = achad.ichLim THEN GOTO 10;
     achad.ichFst := achad.ichLim;
     END;
 achad.ichLim := ichMaxKey;
 SeqLpd(lpdStd, achad);
 END;
10:
achad.ichFst := 0;
IF achad.ichLim = ichMaxKey THEN
  BEGIN
  IF fTstSrch THEN WRITELN('String too long to find.');
  ointFound := ointNil;
  END
ELSE BEGIN
  lpFstFound := lpNil;
  ichFst := achad.ichFst;
  ichLim := achad.ichLim;
  FOR ich := ichFst TO ichLim - 1 DO
       BEGIN
        { For each ellipsis, try to find the previous segment. }
       ch := achad.rgch^[ich];
       IF ch = chElip THEN
            BEGIN
            achad.ichLim := ich;
            FindAchad(ointSrch, achad, ointFound);
            IF ointFound = ointNil THEN GOTO 100;
            pint := POINTER(ORD(lldInt) + ointFound);
            IF achad.ichFst = ichFst THEN lpFstFound := pint^.lpFst;
            lp := pint^.lpLim;
            pint := POINTER(ORD(lldInt) + ointSrch);
            pint^.lpFst := lp;
            FreeInt(pint^.ics, ointFound);
            achad.ichLim := ichLim;
            achad.ichFst := ich + 1;
            END;
       END;
  FindAchad(ointSrch, achad, ointFound);
  IF (ointFound <> ointNil) AND (lpFstFound <> lpNil) THEN
       BEGIN
       pint := POINTER(ORD(lldInt) + ointFound);
       pint^.lpFst := lpFstFound;
       END;
  END;
100:
OintFind := ointFound;
END;
*********************************************************************)

{-------------------------------------------}
FUNCTION OintFind(ointKey, ointSrch: TC; VAR tyrslt: TTyrslt): TC;
{-------------------------------------------}
LABEL   100, 200, 300;
VAR achad:  TAchad;
 achadT:  TAchad;
 argchKey:  ARRAY [0..ichMaxKey] OF TCh;
 tyset: TTyset;
 pint:   TPint;
 ointFound: TC;
 lpLimKey, lp: TLp;
 ch: TCh;
 chch1T, chch1T1: TChch1;
 lpFstFound: TLp;
 lpLimSubKey: TLp;
 ics:        TB;
 charT, charT1: CHAR;

 PROCEDURE FillAchad;
   LABEL 10;
   VAR
   ich: TC;
   BEGIN
   {$IFC sampcalls}  ALogCall;  {$ENDC}
   IF fTstSrch THEN
        WRITELN('lpLimSubKey: ', lpLimSubKey, ', lpLimKey: ', lpLimKey);
   achad.ichFst := 0; achad.ichLim := ichMaxKey;
   SetLpd(lpdStd, ics, lpLimSubKey, tyset, achad);
   WHILE TRUE DO
      BEGIN
      IF lpdStd^.lpLim >= lpLimKey THEN
           BEGIN
           IF lpdStd^.rce^.fVan THEN
                achad.ichLim := achad.ichFst
           ELSE achad.ichLim := achad.ichLim - (lpdStd^.lpLim - lpLimKey);
           END;
      IF NOT lpdStd^.rce^.fVan THEN
           BEGIN
           FOR ich := achad.ichFst TO achad.ichLim - 1 DO
                BEGIN
                ch := achad.rgch^[ich];
                IF ch = chElip THEN
                     BEGIN
                     achad.ichLim := ich;
                     lpLimSubKey := lpdStd^.lp + (ich + 1 - achad.ichFst);
                     GOTO 10;
                     END;
                END;
           { This will trigger on runs too long or end-of-stream. }
           IF (achad.ichLim = ichMaxKey) OR (achad.ichFst = achad.ichLim) THEN
                BEGIN
                lpLimSubKey := lpdStd^.lpLim;
                GOTO 10;
                END;
           achad.ichFst := achad.ichLim;
           END;
      IF lpdStd^.lpLim >= lpLimKey THEN
           BEGIN
           lpLimSubKey := lpLimKey;
           GOTO 10;
           END;
      achad.ichLim := ichMaxKey;
      SeqLpd(lpdStd, achad);
      END;
   10:
   achad.ichFst := 0;
   END;{fillAchad}

BEGIN{OintFind}

{$IFC sampcalls}  ALogCall;  {$ENDC}
300:    { used to restart after matching ichMaxKey characters and then failing }
 { create an achad with the search string in it }
achad.rgch := @argchKey;
pint := POINTER(ORD(lldInt) + ointKey);
ics := pint^.ics;
lpLimSubKey := pint^.lpFst;
lpFstFound := lpNil;
lpLimKey := pint^.lpLim;
tyset.fParBnds := FALSE; tyset.fRce := FALSE; tyset.fRpe := FALSE;
WHILE TRUE DO
     BEGIN
     FillAchad;
     FindAchad(ointSrch, achad, ointFound, tyrslt);
     IF ointFound = ointNil THEN GOTO 100;
     WHILE achad.ichLim = ichMaxKey DO
          BEGIN
          FillAchad;
          IF fTstSrch THEN
               BEGIN
               WRITELN('Overflowed key matching');
               {$ifc fDbgOk}
               PxAchad(achad);
               {$endc}
               END;
          pint := POINTER(ORD(lldInt) + ointFound);
          achadT.ichFst := 0; achadT.ichLim := 1; achadT.rgch := NIL;
          SetLpd(lpdStd, pint^.ics, pint^.lpLim, tyset, achadT);
          WHILE (achad.ichFst < achad.ichLim) DO
               BEGIN
               IF lpdStd^.lpLim = lpdStd^.lp THEN
                    BEGIN
                    FreeInt(pint^.ics, ointFound);
                    ointFound := ointNil;
                    tyrslt := tyrsltNotFound;
                    GOTO 100;
                    END;
               IF (NOT lpdStd^.rce^.fVan) THEN
                    BEGIN     (* NOTE!!! - assignment to chch1s is necessary
                                 until Johan recodes Compare in assembler -
                                 then, this must be changed !!! *)
                    charT := CHR(achadT.rgch^[achadT.ichFst]);
                    charT1 := CHR(achad.rgch^[achad.ichFst]);
                    IF charT <> charT1 THEN
                       IF fMatchCase OR NOT CompareChar(charT,
                                               charT1, compWeak) THEN
                         BEGIN
                         pint := POINTER(ORD(lldInt) + ointFound);
                         lp := pint^.lpFst + 1;
                         pint := POINTER(ORD(lldInt) + ointSrch);
                         pint^.lpFst := lp;
                         GOTO 300;
                         END;
                    achad.ichFst := achad.ichFst + 1;
                    achadT.ichFst := achadT.ichFst + 1;
                    END
               ELSE achadT.ichFst := achadT.ichLim;
               IF achadT.ichFst = achadT.ichLim THEN
                    BEGIN
                    achadT.ichFst := 0; achadT.ichLim := 1; achadT.rgch := NIL;
                    SeqLpd(lpdStd, achadT);
                    END;
               END;
          pint := POINTER(ORD(lldInt) + ointFound);
          pint^.lpLim := lpdStd^.lpLim - (achadT.ichLim - achadT.ichFst);
          END;
     IF lpLimKey = lpLimSubKey THEN GOTO 200;
     pint := POINTER(ORD(lldInt) + ointFound);
     IF lpFstFound = lpNil THEN lpFstFound := pint^.lpFst;
     lp := pint^.lpLim;
     pint := POINTER(ORD(lldInt) + ointSrch);
     pint^.lpFst := lp;
     FreeInt(pint^.ics, ointFound);
     END;
200:
IF (ointFound <> ointNil) THEN
     BEGIN
     tyrslt := tyrsltFound;
     IF (lpFstFound <> lpNil) THEN
          BEGIN
          pint := POINTER(ORD(lldInt) + ointFound);
          pint^.lpFst := lpFstFound;
          END;
     END;
100:
{$ifc fDbgOk}
IF fTstSrch THEN BEGIN WRITELN('OintFound := '); PxInt(ointFound); END;
{$endc}
OintFind := ointFound;
END;



{----------------------------------------}
PROCEDURE MsgFound(tyrslt: TTyrslt; fShow: TF);
{----------------------------------------}
LABEL 999;
VAR icsEd: TB;
    icsUsSrc: TB;
    lpLimSrc: TLp;
    hcsus: THcsus;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
 { Put a message up saying nothing was found. }
IF icsFind = icsNil THEN Break(54);
IF tyrslt = tyrsltFound THEN
     BEGIN
     IF (tyrslt = tyrsltMsg) OR ((NOT fShow) AND (icsPrmt = icsNil)) THEN GOTO 999;
     icsUsSrc := icsNil;
     END
ELSE IF tyrslt = tyrsltNotFound THEN
     BEGIN
     IF (tyrslt = tyrsltMsg) AND (icsPrmt = icsFind) THEN GOTO 999;
     icsUsSrc := icsNotFound;
     END
ELSE BEGIN
     IF (tyrslt = tyrsltMsg) AND (icsPrmt = icsFind) THEN GOTO 999;
     icsUsSrc := icsAborted;
     END;

icsEd := IcsEdLfld(icsFind, lfldBMsg);
IF icsUsSrc = icsNil THEN
     lpLimSrc := 0
ELSE BEGIN
     hcsus := POINTER(ORD(rghcs^[icsUsSrc]));
     lpLimSrc := hcsus^^.ichMac;
     END;
Replace(icsEd, 0, LpMacEd(icsEd), icsUsSrc, 0, lpLimSrc);
IF icsPrmt = icsNil
  THEN ShowPrompt(icsFind, 25) (* magic number to get scroll arrows to appear *)
  ELSE AdjustLps(icsFind, 0, lpMax, 0);

tyrsltMsg := tyrslt;
999:
END;

{--------------------------------}
FUNCTION TyrsltFind(orecFind: TC) : TTyrslt;
{--------------------------------}
VAR lpFstSrch, lpLimSrch: TLp;
 pint: TPint;
 ointSrch, ointBlank, ointFound: TC;
 icsSrch, icsBlank: TB;
 tyrslt: TTyrslt;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
tyrslt := tyrsltNotFound;
IF icsFind <> icsNil THEN
     BEGIN
     { Mark out the territory to be searched: from the end }
     { of whatever selection is in the active folder. }
     lpFstSrch := lpNil;
     IF selCur.oint <> ointNil THEN
          BEGIN
          pint := POINTER(ORD(lldInt) + selCur.oint);
          IF (pint^.ics = icsFind) THEN
               BEGIN
               IF ointPrmt <> ointNil THEN
                    BEGIN
                    pint := POINTER(ORD(lldInt) + ointPrmt);
                    icsSrch := pint^.ics;
                    lpFstSrch := pint^.lpLim;
                    END
               ELSE icsSrch := icsNil;
               END
          ELSE BEGIN
               icsSrch := pint^.ics;
               lpFstSrch := pint^.lpLim;
               END;
          END;
     IF icsSrch = icsNil THEN
          BEGIN
          { alert message should go here? }
          END
     ELSE BEGIN
          lpLimSrch := LpLastCr(icsSrch);
          ointSrch := OintMark(ointNil, icsSrch, lpFstSrch, lpLimSrch, TRUE,
          ointNil);

           { the oint describing the string to find }
          icsBlank := IcsEdLfld(icsFind, lfldBFind);
          ointBlank := OintMark(ointNil, icsBlank, 0, LpMacEd(icsBlank), TRUE,
          ointNil);

           { try to find it }
          ointFound := OintFind(ointBlank, ointSrch, tyrslt);
           { Free up our mess }
          FreeInt(icsBlank, ointBlank);  FreeInt(icsSrch, ointSrch);

           { select it if it is found }
          IF ointFound <> ointNil THEN
               BEGIN
               pint := POINTER(ORD(lldInt) + ointFound);
               WITH pint^ DO LSetSel(ics, lpFst, lpLim);
               pint := POINTER(ORD(lldInt) + ointFound);
               FreeInt(pint^.ics, ointFound);
                { Show it in the window. }
               IF orecFind <> orecNil THEN IntInWindow(selCur.oint, orecFind);
               END;
          END;
     END;
TyrsltFind := tyrslt;
END;

{-------------------------------------------}
PROCEDURE Change(VAR fExceededCssAbsMax: TF);
{-------------------------------------------}
{ Change is vey similar to PasteFrom, in UnitScrap }
{ Someday they could/should(?) be merged }
VAR ointBlank: TC;
 dlpNew, lpFstOut, lpLimOut: TLp;
 icsOut, icsBlank: TB;
 pint: TPint;
 hcsed: THcsed;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
IF fTstSrch THEN WRITELN('Change.');
IF icsFind <> icsNil THEN
 BEGIN
 RemoveKid(TRUE);
 { set up the oint for where to search: from selection on }
 pint := POINTER(ORD(lldInt) + selCur.oint);
 IF pint^.ics = icsFind THEN pint := POINTER(ORD(lldInt) + ointPrmt);
 WITH pint^ DO BEGIN icsOut := ics; lpFstOut := lpFst; lpLimOut := lpLim; END;
 hcsed := POINTER(ORD(rghcs^[icsOut]));
 IF hcsed^^.issMac >= cssAbsMax THEN fExceededCssAbsMax := TRUE ELSE
    BEGIN
    fExceededCssAbsMax := FALSE;
    { Reset selection and chronicle old selection. }
    LSetSel(icsNil, 0, 0);    { un-hilight current selection }
    Chronicle(icsNil, 0, 0, icsOut, lpFstOut, lpLimOut, hopSelect);
    { Selection goes to undo buffer; save selection contents. }
    Replace(icsUndo, 0, LpMacEd(icsUndo), icsOut, lpFstOut, lpLimOut);
    { In historical terms: selection MOVED to undo buffer. }
    Chronicle(icsOut, lpFstOut, lpLimOut, icsUndo, 0, LpMacEd(icsUndo), hopMove);

    { We want to replace with the CONTENTS of the blank -- the icsEd }
    icsBlank := IcsEdLfld(icsFind, lfldBChange);
    dlpNew := LpMacEd(icsBlank);
    Replace(icsOut, lpFstOut, lpLimOut, icsBlank, 0, dlpNew);
    { In historical terms: source INSERTED at lpFst of selection. }
    Chronicle(icsBlank, 0, dlpNew, icsOut, lpFstOut, lpFstOut + dlpNew, hopInsert);

    LSetSel(icsOut, lpFstOut, lpFstOut + dlpNew);
    Chronicle(icsOut, lpFstOut, lpFstOut + dlpNew, icsNil, 0, 0, hopSelect);
    END;
 END;
END;

{-------------------------------------------------}
PROCEDURE DoFindImid { (imid: TC; orecActive: TC) };
{-------------------------------------------------}
LABEL 77;
VAR f: TF;
    ich: TC;
    icsBlank, icsFindString: TB;
    pint: TPint;
    lfp:        TLfp;
    lpFstFindVal: TLp;
    lpLimFIndVal: TLp;
    fDummy, fTooManySs, fFoundAny: TF;
    tyrslt: TTyrslt;
    lpT: TLp;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
InitChronicle;
IF (FWhatsUp <> TyPrmt) THEN RmvAnyDbox;
CASE imid OF
  imidSetString:
     BEGIN
     IF icsFind = icsNil THEN break(57);
     { fMatchCase := FALSE; }
     tyrsltMsg := tyrsltNotFound;
     MsgFound(tyrsltFound, TRUE);
     lfp.lfld := lfldBFind + iprtVal;
     lfp.ich := 0;
     fDummy := FSelLpBounds(lfp.lp, icsFind, lpFstFindVal, lpLimFindVal, lpT, lpT);
     LSetSel(icsFind, lpFstFindVal, lpLimFindVal);
     END;
  imidOnCase:
     BEGIN
     fMatchCase := TRUE;
     END;
  imidOffCase:
     BEGIN
     fMatchCase := FALSE;
     END;
OTHERWISE
     BEGIN
     IF icsFind <> icsNil THEN
          BEGIN
          MsgFound(tyrsltFound, FALSE);
          CASE imid OF
            imidClearPrompt:
               IF (icsPrmt <> icsNil) AND (icsPrmt = icsFind) THEN
                    FreePrompt;
            imidWildCard:
               BEGIN
               IF icsFind <> icsNil THEN
                    BEGIN
                    pint := POINTER(ORD(lldInt) + selCur.oint);
                    IF pint^.ics = icsFind THEN
                         InsChKid(chElip);
                    END;
               END;
            OTHERWISE
               BEGIN
               icsFindString := IcsEdLfld(icsFind, lfldBFind);
               IF LpPrvVis(icsFindString, LpMacEd(icsFindString)) < 0 THEN
                  NoteAlert(WPAlerts, 11) (* no Find string *)
               ELSE CASE imid OF
                  imidFind:
                     MsgFound(TyrsltFind(orecActive), FALSE);
                  imidChange:
                     Change(fDummy);
                  imidCAndF:
                     BEGIN
                     Change(fDummy);
                     MsgFound(TyrsltFind(orecActive), FALSE);
                     END;
                  imidCAll:
                     BEGIN
                     pint := POINTER(ORD(lldInt) + selCur.oint);
                     LSetSel(pint^.ics, pint^.lpFst, pint^.lpFst);
                     fFoundAny := FALSE;
                     fCbMoreFailed := FALSE;
                     WHILE TRUE DO
                          BEGIN
                          tyrslt := TyrsltFind(orecNil);
                          IF tyrslt = tyrsltFound THEN
                               BEGIN
                               fFoundAny := TRUE;
                               Change(fTooManySs);
                               IF fCbMoreFailed THEN GOTO 77;
                               IF fTooManySs THEN
                                  BEGIN
                                  NoteAlert(WPAlerts, 12); (* too many iss's *)
                                  GOTO 77;
                                  END;
                               END
                          ELSE GOTO 77;
                          END;
               77:   IF fFoundAny AND (tyrslt = tyrsltNotFound) THEN
                         tyrslt := tyrsltFound; (* don't reset in case of Abort *)
                     MsgFound(tyrslt, FALSE);
                     IntInWindow(selCur.oint, orecActive);
                     END;
                  END (* CASE *);
               END (* OTHERWISE *);
           END (* CASE *);
         END (* IF *);
     END (* OTHERWISE *);
 END (* CASE *);
END (* DoFindImid *);


{$S LWCreate}
{-----------------}
PROCEDURE InitSrch;
{-----------------}
VAR
    icsPlate: TB;
    ffleader: TFfleader;
    cError:   TC;
    spPrmt: TSp;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
 (* InitCompare; *)  (* d *) (* should be done by Filer *)
chElip := ORD(ccElip);  { assign to var of type TCh to thwart compiler
                   confusion over bytes/words and signed/unsigned }
icsPlate := IcsFfCreate (IfilOpen (cError,
                                   CONCAT(spLotusPrefix, '{T1}search.lotus'),
                                   cbPgFf, [dRead], hzCom, ''),
                         icsNil, hzCom, ffleader);
icsFind := IcsFmCreate(icsPlate, ffleader.lpTxtFst, hzCom);
fMatchCase := FALSE;
GetItem(argMenu[imtdBuzz], imidNFound, @spPrmt); {Not Found}
icsNotFound := IcsUsCreate(spPrmt, hzCom);
GetItem(argMenu[imtdBuzz], imidAborted, @spPrmt); {Aborted}
icsAborted := IcsUsCreate(spPrmt, hzCom);
END;

END.


ÿ