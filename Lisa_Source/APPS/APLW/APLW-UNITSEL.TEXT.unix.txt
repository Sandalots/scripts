{$I APLW/COMPFLAGS }                                            {gdf 1/16/84}
{$R-}

UNIT UnitSel;

(* Copyright 1983, Apple Computer Inc. *)

{ gdf  2/27/84 -- insert trace calls for segmentation  }
{ gdf  1/16/84 -- printing compile flag & include COMPFLAGS added }
{ tjm  3/08/83 -- ValidateDsp after ResetSel in LSelect (WP 239)}
{ crc  3/02/83 -- ChClass: fix to recognize chars above 127}
{ crc  3/02/83 -- Pass lpFst of current selection to OrecConOfOboxIcsLp }
{ crc  1/20/83 -- OintSelect: exclude carriage return during word-drag }
{ crc 11/15/82 -- Select, SetSel, GrowSel --> LSelect, LSetSel, LGrowSel }
{ crc 10/29/82 -- Word select; exports new routines ChClass, FQualifies, and
                  LpVisAdjacent; added WordFromIch }
{ jrm  6/29/82 -- USES changes: LisaGraf => QuickDraw; moved USES UnitFile }
{ crc  6/02/82 -- Conditional compilation }
{ crc 05/25/82 -- USES PrStd added }
{ crc 04/06/82 -- integration with new font manager }
{ bas 03/10/82 -- orec tyxy update }
{ bas 03/09/82 -- cleanup FPuncCh }
{ tjm 01/18/82 -- bug in HilightSel (IF fValid AND ...) }
{ bas 01/13/82 -- USES for new wm }
{ bas 12/10/81 -- integration with font manager }

INTERFACE

USES
     {$IFC sampcalls}                                               {gdf 2/24/84}
        {$U aplw/tracecalls.obj} tracecalls,
     {$ENDC}
     {$U Obj:UnitStd.obj   } UnitStd,
     {$U Obj:UnitHz.obj    } UnitHz,
     {$U LibOS/Syscall.Obj} Syscall,
     {$U Obj:UnitFile.obj  } UnitFile,
     {$U Obj:Storage.Obj   } Storage,
     {$U Obj:QuickDraw.obj } QuickDraw,
     {$U Obj:FontMgr.Obj   } FontMgr,
     {$U Obj:PmDecl        } PmDecl,
     {$IFC fPrintOld}                                               {gdf 1/16/84}
        {$U Obj:PrStd.Obj }     PrStd,
     {$ELSEC}
        {$U Obj:PrStdInfo.Obj } PrStdInfo,
          (* {$U Obj:PrPublic.Obj }  PrPublic,  unneeded ? *)
     {$ENDC}                                                        {gdf 1/16/84}
     {$U Obj:UnitFmt.obj   } UnitFmt,
     {$U Obj:UnitCs.obj    } UnitCs,
     {$U Obj:UnitFf.Obj    } UnitFf,
     {$U Obj:Wm.Events.Obj } Events,
     {$U Obj:Wm.Folders.Obj} Folders,
     {$U Obj:WmlStd        } WmlStd,
     {$U Obj:WM.Menus.Obj  } Menus,
     {$U Obj:AlertMgr.Obj  } AlertMgr,
     {$U ApLW/Queue.obj     } Queue,
     {$U ApLW/UnitBox.obj   } UnitBox,
     {$U ApLW/UnitRec.obj   } UnitRec,
     {$U ApLW/UnitOver.obj  } UnitOver,
     {$U ApLW/UnitTld.obj   } UnitTld,
     {$U ApLW/UnitDsp.obj   } UnitDsp;

{$IFC NOT fDbgOk}
CONST
    fTstSel = FALSE;
{$ENDC}

TYPE TTyshow = (tyshowOn, tyshowOff, tyshowToggle);

VAR
{$IFC fDbgOk}
    fTstSel: TF;
{$ENDC}
    spTriples: TSp;

PROCEDURE LSelect       (ptScrn: TPt; tysel: TTysel; orecCon: TC);
PROCEDURE LGrowSel      (ptScrn: TPt; orecCon: TC);
PROCEDURE LSetSel       (ics: TB; lpFst: TLp; lpLim: TLp);
FUNCTION  DxSel         (oboxPane: TC; VAR orec: TC): TC;
PROCEDURE ShowSel       (VAR sel: TSel; tyshow: TTyshow);
PROCEDURE InitSel;
FUNCTION  ChClass        (ch: TCh): TCh;
FUNCTION  FQualifies     (chad: Tchad; ich: TC): TF;
FUNCTION  LpVisAdjacent  (ics: TB; lp: TLp; fPrv: TF; VAR achad: TAchad): TLp;
PROCEDURE WordFromIch(chad: TChad; ichOrig: TC; VAR ichFstWd, ichLimWd: TC);


IMPLEMENTATION

{$IFC fSymOK }
{$D+ }
{$ELSEC }
{$D- }
{$ENDC }

(*
CONST chPound = 35;
      chDollar = 36;
      chPercent = 37;
      chAsterisk = 42;
      chPlus = 43;
      chHyphen = 45;
      chSlash = 47;
      chEqual = 61;
      chAtsign = 64;
      chSmallA = 97; chSmallZ = 122;
      chBigA = 65; chBigZ = 90;
      chZero = 48; chNine = 57;
*)

VAR xOrYDummy: TCd;

{$ifc fDbgOk}
{$S LWDebug}
PROCEDURE PxSel(sel: TSel);
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
WRITELN('PxSel - tysel:', ORD(sel.tysel),', tyHilight:', ORD(sel.tyHilight),
  ', fShowing: ', sel.fShowing);
PxInt(sel.oint); PxInt(sel.ointAnchor);
END;
{$endc}

{$S LWMargin}
FUNCTION DxSel { (oboxPane: TC; VAR orec: TC) : TC };
 { returned x coordinate is a screen coordinate (tyxyScrn) }
VAR dh: TC; rec: TRec; pint, pintSel: TPint; dxLftCon: TC;
    lpLim: TLp; orecCon: TC;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
{$ifc fDbgOk}
IF fTstSel THEN BEGIN WRITELN('DxSel'); PxSel(selCur); END;
{$endc}
pintSel := POINTER(ORD(lldInt) + selCur.oint);
orecCon := OrecConOfOboxIcsLp(oboxPane, pintSel^.ics, pintSel^.lpFst);
IF fTstSel THEN WRITELN('orecCon:', orecCon:4);
IF orecCon = orecNil THEN
     BEGIN
     orec := orecNil;
     END
ELSE BEGIN
     rec := POINTER(ORD(dsp) + orecCon);
     dxLftCon := rec^.r.left;
     ConvertTyxy(dxLftCon, xOrYDummy, rec^.tyxy, tyxyScrn);
     pintSel := POINTER(ORD(lldInt) + selCur.oint);
     orec := OrecOfLp(rec^.orecSon, pintSel^.ics, pintSel^.lpFst, FALSE);
     IF fTstSel THEN WRITELN('orec:', orec:4);
     IF orec <> orecNil THEN
          BEGIN
          GenOrec(orec, iimgpScrn);

          pintSel := POINTER(ORD(lldInt) + selCur.oint);
          dh := HOfLp(pintSel^.lpFst, crdStd);
          rec := POINTER(ORD(dsp) + orec);
          pint := POINTER(ORD(lldInt) + rec^.ointDep);
          lpLim := pint^.lpFst + rec^.dlpLim;
          dh := dh - dxLftCon;
          IF (pintSel^.lpLim > lpLim) AND (rec^.orecSib <> orecNil) THEN
               orec := rec^.orecSib;
          END;
     END;
IF fTstSel THEN WRITELN('DxSel -- dh:', dh, ', orec:', orec);
DxSel := dh;
END;


{$S LWCommand}
PROCEDURE HilightSel(VAR sel: TSel; fShow: TF; orecConFst: TC);
LABEL   10, 99;
VAR rec: TRec; pint, pintSel: TPint; oboxPane: TC; pbox: TPBox; lpLimRec: TLp;
    orec, orecCon: TC; icsSel: TB; tyterm: TTyterm; rRec: TR;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
{$ifc fDbgOk}
IF fTstSel THEN
     BEGIN
     WRITELN('HilightSel - fShow: ', fShow, ', orecConFst: ', orecConFst);
     PxSel(sel);
     END;
{$endc}
IF (sel.tyHilight = tyHiNil) OR (sel.fShowing = fShow) THEN GOTO 99;
IF orecConFst = orecNil THEN orecConFst := dsp^.orecConRoot;
orecCon := orecConFst;
REPEAT
{$ifc fDbgOk}
IF fTstSel THEN BEGIN WRITELN('orecCon loop'); PxOrec(orecCon); END;
{$endc}
rec := POINTER(ORD(dsp) + orecCon);
pintSel := POINTER(ORD(lldInt) + sel.oint);
pint := POINTER(ORD(lldInt) + rec^.ointDep);
icsSel := pintSel^.ics;
IF icsSel = pint^.ics THEN
     BEGIN
     oboxPane := OboxVisOrec(orecCon);
     pintSel := POINTER(ORD(lldInt) + sel.oint);
     IF pintSel^.lpFst = lpNil THEN GOTO 10;
     rec := POINTER(ORD(dsp) + orecCon);
     orec := rec^.orecSon;
     ipgCur := IpgCurFromOrecCon(orecCon);
     WHILE orec <> orecNil DO
          BEGIN
          rec := POINTER(ORD(dsp) + orec);
          pint := POINTER(ORD(lldInt) + rec^.ointDep);
          lpLimRec := pint^.lpFst + rec^.dlpLim;
          {$ifc fDbgOk}
          IF fTstSel THEN BEGIN WRITE(' ointDep:'); PxInt(rec^.ointDep); END;
          {$endc}
          IF pint^.fValid AND (pintSel^.lpLim < pint^.lpFst) THEN
               GOTO 10; { done with window }
          IF rec^.strec.fValid AND pint^.fValid
            AND (NOT rec^.strec.fRecDep) AND (rec^.r.bottom > 0) THEN
               { Remember - pintSel.lpLim >= pint.lpFst, from above }
               IF (pintSel^.lpFst < lpLimRec)
                 AND ((pintSel^.lpLim = pintSel^.lpFst)
                   OR (pintSel^.lpLim > pint^.lpFst)) THEN
                    BEGIN
                    rRec := rec^.r;
                    GenCrd(crdStd, icsSel, pint^.lpFst, rec^.tyxy);
                     { move crdStd^.arec.r to ptTL }
                    { WITH crdStd^.arec DO
                         OffsetRect(r, ptTL.h  - r.left, ptTL.v - r.top); }
                    crdStd^.arec.r := rRec;
                    crdStd^.orec := orec;
                    HilightCrd(crdStd, sel.oint, sel.tyHilight, fShow);
                    pintSel := POINTER(ORD(lldInt) + sel.oint);
                    rec := POINTER(ORD(dsp) + orec);
                    END;
          orec := rec^.orecSib;
          END;
     END;
10:
rec := POINTER(ORD(dsp) + orecCon);
orecCon := rec^.orecNxtCon;
IF orecCon = orecNil THEN orecCon := dsp^.orecConRoot;
UNTIL orecCon = orecConFst;
sel.fShowing := fShow;
99:
END;


{$S LWInsert}
PROCEDURE TruncSelLps(ics: TB; lpInclude: TLp; orec: TC; VAR lpFst, lpLim:
TLp);
LABEL 8;
VAR lpFstSel, lpLimSel, lpFstAtom, lpLimAtom, lpFst1: TLp; fSelectable: TF;
    rec: TRec;
    pint: TPint;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
 { truncate to the selectable portion of [lpFst, lpLim) }
IF fTstSel THEN WRITELN('TruncSelLps(', lpFst, ', ', lpLim, ')');
IF FSelLpBounds(lpInclude, ics, lpFst1, lpLimSel, lpFstAtom, lpLimAtom) THEN
     BEGIN
     lpLim := LpMaxP(lpLimAtom, LpMinP(lpLim, lpLimSel));
     lpFst := LpMinP(lpFstAtom, LpMaxP(lpFst, lpFst1));
     END
ELSE BEGIN
      { find the closest selectable region }
     IF orec <> orecNil THEN
          BEGIN
          rec := POINTER(ORD(dsp) + orec);
          pint := POINTER(ORD(lldInt) + rec^.ointDep);
          IF (pint^.lpFst <= lpFst1) AND (lpFst1 < pint^.lpLim)
            AND (lpFst1 <> 0) THEN
               BEGIN
               lpFst := lpFst1;
               lpLim := lpFst;
               GOTO 8;
               END;
          END;
     lpFst := lpLimSel; lpLim := LpMaxP(lpFst, lpLim);
     IF FSelLpBounds(lpLimSel, ics, lpFstSel, lpLimSel, lpFstAtom, lpLimAtom) THEN
          lpLim := LpMaxP(lpLimAtom, LpMinP(lpLim, lpLimSel))
     { ELSE IF lpFst1 = 0 THEN
          BEGIN
          lpFst := 0;
          lpLim := 0;
          END
     }
     ELSE BEGIN
          lpFst := lpFst1 { - 1};
          lpLim := lpFst;
          { TruncSelLps(ics, lpFst, lpLim);}
          END;
     END;
8:
IF fTstSel THEN WRITELN('TruncSelLps returning ', lpFst, ' through ', lpLim);
END;


{$S LWType}
{------------------------------}
FUNCTION ChClass{(ch: TCh): TCh};
{------------------------------}
VAR
sOrU: PACKED RECORD
      CASE TF OF
        TRUE:  (s: -128..127);
        FALSE: (u:    0..255);
      END;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
sOrU.s := ch;
IF sOrU.u IN [65..90, 97..122, 128..159, 167, 174..175, 187, 188, 190..191,
              202..207, 216] THEN
   sOrU.u := ORD('A')
ELSE
   IF (48 <= sOrU.u) AND (sOrU.u <= 57) THEN
      sOrU.u := ORD('9')
   ELSE
      IF (sOrU.u = 162) OR (sOrU.u = 163) OR (sOrU.u = 180) THEN sOrU.u := ORD('$');
ChClass := sOrU.s;
END; {ChClass}

{---------------------------------------------}
FUNCTION FQualifies{(chad: Tchad; ich: TC): TF};
{---------------------------------------------}
VAR
   i,j: TC;
   chLeft, chRight, chThis: PACKED RECORD
         CASE TF OF
           TRUE:  (s: -128..127);
           FALSE: (u:    0..255);
         END;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
   IF (ich < chad^.ichFst) OR (ich >= chad^.ichLim) THEN
       BEGIN
       FQualifies := FALSE;
       EXIT(FQualifies);
       END;
   chThis.s := ChClass(chad^.rgch^[ich]);
   IF ich <= chad^.ichFst
       THEN chLeft.s := ORD(' ')
       ELSE chLeft.s := ChClass(chad^.rgch^[ich-1]);
   IF ich+1 >=  chad^.ichLim
       THEN chRight.s := ORD(' ')
       ELSE chRight.s := ChClass(chad^.rgch^[ich+1]);
   FOR i := 1 TO (LENGTH(spTriples) DIV 4) + 1 DO
      BEGIN
      j := 4*i-2;
      IF ((spTriples[j-1] = CHR(chLeft.u)) OR (spTriples[j-1] = 'x')) AND
         (spTriples[j] = CHR(chThis.u)) AND
         ((spTriples[j+1] = CHR(chRight.u)) OR (spTriples[j+1] = 'x')) THEN
         BEGIN
         FQualifies := TRUE;
         EXIT(FQualifies)
         END;
      END;
   FQualifies := FALSE;
END; {FQualifies}

{----------------------------------------------------------------------------}
FUNCTION LpVisAdjacent {(ics: TB; lp: TLp; fPrv: TF; VAR achad: TAchad) : TLp};
{----------------------------------------------------------------------------}
{ Yields the lp of the adjacent visible character, in either the
  forward (fPrv = FALSE) or the backward (fPrv = TRUE) direction,
  and places the character in the argument achad.  If fPrv = TRUE and
  no preceeding visible character exists, -1 is returned.
  This is not the most efficient way to get the next character in the
  forward direction, as SetLpd is called each time.
}
VAR tyset: TTyset;
    rgch:  TRgch;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
LpVisAdjacent := -1;
rgch := achad.rgch;
IF achad.rgch = NIL THEN achad.ichLim := achad.ichFst; {in case SetLpd not
called}
WITH tyset DO BEGIN fRce := TRUE; fRpe := FALSE; fParBnds := FALSE; END;
REPEAT
    IF fPrv THEN lp:=lp-1 ELSE lp:=lp+1;
    IF lp < 0  THEN EXIT(LpVisAdjacent);
    achad.rgch := rgch; {In case rgch was NIL - we should fix this interface !!! }
    SetLpd(lpdStd, ics, lp, tyset, achad);
UNTIL (NOT lpdStd^.rce^.fVan) OR (lpdStd^.lpLim = lpdStd^.lp);
LpVisAdjacent := lp;
END;  {LpVisAdjacent}


{$S LWStyle}
{---------------------------------------------------------------------------}
PROCEDURE WordFromIch{(chad: TChad; ichOrig: TC; VAR ichFstWd, ichLimWd: TC)};
{---------------------------------------------------------------------------}
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
   ichFstWd := ichOrig;
   ichLimWd := ichOrig;
   IF FQualifies(chad, ichOrig) THEN
      BEGIN
      WHILE FQualifies(chad, ichFstWd-1) DO
          ichFstWd := ichFstWd -1;
      WHILE FQualifies(chad, ichLimWd+1) DO
          ichLimWd := ichLimWd +1;
      END;
   ichLimWd := ichLimWd +1;
END; {WordFromIch}

{$S LWInsert}
{----------------------------------------------------------------}
FUNCTION OintSelect (orecCon: TC; ptScrn: TPt; tysel: TTysel): TC;
{----------------------------------------------------------------}
LABEL 999;
VAR ics: TB; orec: TC; rec: TRec; iwd: TC; achad: TAChad;
    lpFst, lpLim: TLp; pint: TPint;
    ichOrig, ichFstWd, ichLimWd, ich: TC; ch: TCh;
    fWord: TF;
    lpInclude: TLp;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
IF fTstSel THEN WRITELN('OintSelect...');
fWord := (tysel = tyselWd);
lpFst := LpOfPt(orecCon, ptScrn, iwd, orec, fWord);
lpInclude := lpFst;
IF lpFst = lpNil THEN
     BEGIN OintSelect := ointNil; GOTO 999; END;
rec := POINTER(ORD(dsp) + orec);
pint := POINTER(ORD(lldInt) + rec^.ointDep);
ics := pint^.ics;
CASE tysel OF
 tyselPt:  BEGIN lpLim := lpFst; END;
 tyselWd:  BEGIN
           ichOrig := IchFromLp(lpFst);
           achad := achadTld;  {limits in achadTld are not accurate}
           achad.ichFst := 0;
           achad.ichLim := atld.ichLim;
           WordFromIch(@achad, ichOrig, ichFstWd, ichLimWd);
           IF fTstSel THEN WRITELN('Select word? -- ich''s', ichFstWd:4,
                                                 ' through', ichLimWd:4);
           ichLimWd := CMin(ichLimWd, atld.ichLim-1);
           LpOfIch(ichFstWd, lpFst);
           LpOfIch(ichLimWd, lpLim);
           END;
 tyselPar: BEGIN
           achad.ichFst := 0; achad.ichLim := 1; achad.rgch := NIL;
           SetLpd(lpdStd, ics, lpFst, tysetBnds, achad);
           lpFst := lpdStd^.lpFstPar;
           lpLim := lpdStd^.lpLimPar;
           END;
END;
TruncSelLps(ics, lpInclude, orec, lpFst, lpLim);
OintSelect := OintMark(ointNil, ics, lpFst, lpLim, TRUE, ointNil);
999:
IF fTstSel THEN WRITELN('...OintSelect returning.');
END; {OintSelect}


PROCEDURE ResetSel(VAR sel: TSel; tyHiNew: TTyHilight; tyselNew: TTysel);
VAR pint: TPint;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
WITH sel DO
     BEGIN
     fShowing := FALSE;
     tyHilight := tyHiNew;
     tysel := tyselNew;
     IF oint <> ointNil THEN
          BEGIN
          pint := POINTER(ORD(lldInt) + oint);
          FreeInt(pint^.ics, oint);
          oint := ointNil;
          END;
     IF ointAnchor <> ointNil THEN
          BEGIN
          pint := POINTER(ORD(lldInt) + ointAnchor);
          FreeInt(pint^.ics, ointAnchor);
          ointAnchor := ointNil;
          END;
   END;
END;


PROCEDURE LSelect { (ptScrn: TPt; tysel: TTysel; orecCon: TC) };
LABEL 999;
VAR pintNew: TPint; ointNew: TC;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
ointNew := OintSelect(orecCon, ptScrn, tysel);
IF ointNew = ointNil THEN GOTO 999;
HilightSel(selCur, FALSE, orecCon);
IF fTstSel THEN WRITELN ('Select - ptScrn: [', ptScrn.h, ptScrn.v, ']');
ValidateDsp;
ResetSel(selCur, tyHiInvert, tysel);
selCur.oint := ointNew;
pintNew := POINTER(ORD(lldInt) + ointNew);
WITH pintNew^ DO
  selCur.ointAnchor := OintMark(selCur.ointAnchor, ics, lpFst, lpLim, TRUE,
                                ointNil);
IF fTstSel THEN WRITELN('Select set selCur...');
HilightSel(selCur, TRUE, orecCon);
999:
END;


{$S LWDialog}
PROCEDURE SwitchLps (pint: TPint);
VAR lp: TLp;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
IF pint^.lpFst > pint^.lpLim THEN
   BEGIN
   lp := pint^.lpFst;
   pint^.lpFst := pint^.lpLim;
   pint^.lpLim := lp;
   END;
END;


PROCEDURE LGrowSel { (ptScrn: TPt; orecCon: TC) };
LABEL 999;
VAR pintCur, pintNew, pintAnchor: TPint; ointNew: TC; selNew: TSel;
    fSelectable, fGrownFwd, fGrowingFwd: TF;
    lpFstSel, lpLimSel, lpDummy, lpFstAnchor, lpLimAnchor: TLp;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
{ ********** BEWARE DANGLING REFERENCES ******************** }
IF fTstSel THEN WRITELN ('GrowSel - ptScrn:[ ', ptScrn.h, ptScrn.v, ']');
ValidateDsp;
ointNew := OintSelect(orecCon, ptScrn, selCur.tysel);
IF ointNew = ointNil THEN GOTO 999;
pintNew := POINTER(ORD(lldInt) + ointNew);
pintCur := POINTER(ORD(lldInt) + selCur.oint);
IF pintNew^.ics = pintCur^.ics THEN
   BEGIN
   pintAnchor := POINTER(ORD(lldInt) + selCur.ointAnchor);
   lpFstAnchor := pintAnchor^.lpFst;
   lpLimAnchor := pintAnchor^.lpLim;
   selNew := selCur;
   selNew.oint := ointNew;
   selNew.tyHilight := tyHiInvert;
   selNew.fShowing := FALSE;
   IF (pintNew^.lpFst = lpFstAnchor) AND (lpFstAnchor = lpLimAnchor)
     AND (selCur.tysel = tyselPt) THEN
      BEGIN
      IF (selCur.tyHilight = tyHiInvert) AND (pintCur^.lpFst < pintCur^.lpLim)
      THEN
         BEGIN
         HilightSel(selCur, FALSE, orecCon);
         pintCur := POINTER(ORD(lldInt) + selCur.oint);
         pintCur^.lpFst := lpFstAnchor;
         pintCur^.lpLim := lpLimAnchor;
         {selCur.tyHilight := tyHiCaret;}
         HilightSel(selCur, TRUE, orecCon);
         END;
      END
   ELSE
      BEGIN
       { find if we can grow as much as we want to }
      fSelectable := FSelLpBounds(pintCur^.lpFst, pintCur^.ics,
                                  lpFstSel, lpLimSel, lpDummy, lpDummy);
      IF NOT fSelectable THEN Break(34);
      pintNew := POINTER(ORD(lldInt) + ointNew);
      pintNew^.lpFst := LpMinP(lpLimSel, LpMaxP(pintNew^.lpFst, lpFstSel));
      pintNew^.lpFst := LpMinP(pintNew^.lpFst, lpFstAnchor);
      pintNew^.lpLim := LpMaxP(pintNew^.lpFst, LpMinP(pintNew^.lpLim, lpLimSel));
      pintNew^.lpLim := LpMaxP(pintNew^.lpLim, lpLimAnchor);
      pintCur := POINTER(ORD(lldInt) + selCur.oint);
      fGrownFwd := (lpFstAnchor = pintCur^.lpFst);
      fGrowingFwd := (lpFstAnchor <= pintNew^.lpFst);
      IF fGrowingFwd <> fGrownFwd THEN
           BEGIN { was going the other way }
           HilightSel(selCur, FALSE, orecCon);
           pintCur := POINTER(ORD(lldInt) + selCur.oint);
           pintNew := POINTER(ORD(lldInt) + ointNew);
           pintCur^.lpFst := LpMinP(lpFstAnchor, pintNew^.lpFst);
           pintCur^.lpLim := LpMaxP(lpLimAnchor, pintNew^.lpLim);
           HilightSel(selCur, TRUE, orecCon);
           END
      ELSE BEGIN
           IF (pintCur^.lpFst = pintCur^.lpLim)
             AND ((pintCur^.lpFst < pintNew^.lpLim)
               OR (pintNew^.lpFst < pintCur^.lpLim)) THEN
              HilightSel(selCur, FALSE, orecCon);
           pintCur := POINTER(ORD(lldInt) + selCur.oint);
           pintNew := POINTER(ORD(lldInt) + ointNew);
           IF fGrowingFwd THEN
                BEGIN
                pintNew^.lpFst := pintCur^.lpLim;
                pintCur^.lpLim := pintNew^.lpLim;
                END
           ELSE BEGIN
                pintNew^.lpLim := pintCur^.lpFst;
                pintCur^.lpFst := pintNew^.lpFst;
                END;
           SwitchLps(pintNew);
           IF pintNew^.lpFst < pintNew^.lpLim THEN
                HilightSel(selNew, TRUE {or FALSE}, orecCon);
           selCur.fShowing := TRUE;
           END;
      END;
   END;
pintNew := POINTER(ORD(lldInt) + ointNew);
FreeInt(pintNew^.ics, ointNew);
999:
END;


{$S LWType}
PROCEDURE LSetSel { (ics: TB; lpFst: TLp; lpLim: TLp) };
VAR pint: TPint;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
HilightSel(selCur, FALSE, orecNil);
IF ics = icsNil THEN
     ResetSel(selCur, tyHiNil, tyselPt)
ELSE BEGIN
     ResetSel(selCur, tyHiInvert, tyselPt);
     TruncSelLps(ics, lpFst, orecNil, lpFst, lpLim);
     selCur.oint := OintMark(ointNil, ics, lpFst, lpLim, TRUE, ointNil);
     selCur.ointAnchor := OintMark(ointNil, ics, lpFst, lpLim, TRUE, ointNil);
     HilightSel(selCur, TRUE, orecNil);
     END;
END;


{$S LWCommand}
PROCEDURE ShowSel{VAR sel: TSel; tyshow: TTyshow};
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
IF fTstSel THEN WRITELN('ShowSel - tyshow: ', ORD(tyshow));
IF ((tyshow = tyshowOn) AND (NOT sel.fShowing))
  OR ((tyshow = tyshowOff) AND sel.fShowing)
  OR (tyshow = tyshowToggle) THEN
     HilightSel(sel, NOT sel.fShowing, orecNil);
END;


{$S LWCreate}
PROCEDURE InitSel;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
WITH selCur DO
     BEGIN
     oint := ointNil;
     ointAnchor := ointNil;
     tyHilight := tyHiNil;
     END;
xOrYDummy := 0;
IF fTstSel THEN WRITELN('InitSel');
END;


END.






ÿ