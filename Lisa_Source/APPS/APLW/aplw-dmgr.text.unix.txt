{$I APLW/COMPFLAGS }                                            {gdf 1/16/84}
(* ********************************************************************* *)

(* CopyRight 1983, 1984, Apple Computer Inc. *)
(* LisaWrite's Dialog Box Manager - adapted from LisaDraw's Dialog Box Manager *)
(* For dialog boxes with check boxes, text, and buttons *)

(* ********************************************************************* *)

{ gdf  2/27/84 -- insert trace calls for segmentation  }
(* JBB 11/30/83 - To avoid need for floating point software, changed Round(integer/2)
                    to (integer DIV 2) in two places within SetCkItm *)

UNIT DMgr;

INTERFACE
USES
    {$IFC sampcalls}                                               {gdf 2/24/84}
       {$U aplw/tracecalls.obj} tracecalls,
    {$ENDC}
    (*$U UNITSTD *)   UnitStd,      (* interface dependency *)
    (*$U UNITHZ*)     UnitHz,
    (*$U STORAGE*)    Storage,
    (*$U QUICKDRAW*)  QuickDraw,
    (*$U FONTMGR*)    FontMgr,
    (*$U libos/syscall.obj*)    Syscall,      (* implementation units *)

    (*$U WM.EVENTS*)  Events,
    (*$U WM.FOLDERS*) Folders,
    (*$U WM.MENUS*)   Menus,
    (*$U Obj:WmlStd*) WmlStd,
    (*$U WMLCRS*)     WMLCrs,
    (*$U ALERTMGR*)   AlertMgr,
    {$U ApLW/Queue.Obj    }  Queue;  (* Needed temporarily for PxR *)



const
{$IFC NOT fDbgOk}
    fTstDmgr = FALSE;
{$ENDC}
    maxCkItm   = 15;           (* max number of items in a ckBox         *)
    maxCharsStr = 51;           (* max number of chars in a string         *)


type
    TChSet     = Set Of Char;
    StrChars   = String[maxCharsStr];
    TStrHnd  = ^TStrPtr;
    TStrPtr  = ^StrChars;

    TDBoxInfo  =
        record
            isOpen:      Boolean;       (* is Dialog Box open?           *)
            inData:      QDPtr;         (* pointer to proper input data  *)
            outData:     QDPtr;        (* pointer to proper output data *)
            ProcessProc: ProcPtr;       (* procedure to process events   *)
            TrkMseProc:  ProcPtr;       (* procedure to track cursor     *)
            RestoreProc: ProcPtr;       (* procedure to restore dialog   *)
            EndProc:     ProcPtr;       (* procedure to end dialog       *)
        end;

    TCkItmHnd   = ^TCkItmPtr;
    TCkItmPtr   = ^TCkItmInfo;
    TCkItmLst   = Array[0..maxCkItm] of TCkItmHnd;
    TCkItmInfo  =
        record
            font:       Integer;        (* font of ckBox item           *)
            iTitle:     TStrHnd;        (* name of ckBox item           *)
            iLoc:       Point;          (* location of start of iTitle  *)
            hitBox:     Rect;           (* actual hit box is bigger     *)
            itmBox:     Rect;           (* box that gets colored in     *)
        end;

    TCkBoxHnd  = ^TCkBoxPtr;
    TCkBoxPtr  = ^TCkBoxInfo;
    TCkBoxLst  = Array[0..0] of TCkBoxHnd;
    TCkBoxInfo =
        record
            ckBoxId:    TB;             (* logical index of ckBox       *)
            numCkItm:   TB;             (* number of items              *)
            curOnOff:   TB;             (* which on/off box is checked  *)
            curCkItm:   TB;             (* which item is checked        *)
            hasOnOff:   Boolean;        (* are items 1 and 2 special?   *)
            ckItmLst:   TCkItmLst;      (* ckItem list may be empty     *)
            DrawProc:   ProcPtr;        (* procedure to draw ckBox      *)
            HitTstFunc: ProcPtr;        (* function to hit test ckBox   *)
        end;

    TCkFormHnd  = ^TCkFormPtr;
    TCkFormPtr  = ^TCkFormInfo;
    TCkFormInfo =
        record
            numCkBox: TB;               (* number of ckBoxes in list    *)
            vCkForm:  Integer;          (* height of dialog box         *)
            ckBoxLst: TCkBoxLst;        (* list of ckBoxPtrs            *)
        end;

var
  {$IFC fDbgOk} fTstDmgr: TF; {$ENDC}
    (* the dialog Box *)
    theDBox:    TDBoxInfo;

    (* ckBox dialog *)
    procedure ReadCkBox    (var ckFile: Text; var info: TCkBoxHnd; allocMax: boolean);
    procedure CkFormHeight (info: TCkFormHnd);
    procedure ChgCkItm  (info: TCkBoxHnd; newCkItm: TB);
    procedure ShowCkBtn    (info: TCkBoxHnd);
    procedure ShowCkBox    (info: TCkBoxHnd);
    procedure ShowJust1CkBox (info: TCkFormHnd; ckBox: Integer);
    procedure ShowCkForm   (info: TCkFormHnd);
    function  CkBtnHit     (info: TCkBoxHnd; pt: Point;
                            var ckItm: Integer): Boolean;
    function  CkBoxHit     (info: TCkBoxHnd; pt: Point;
                            var ckItm: Integer): Boolean;
    procedure CkFormSelect (info: TCkFormHnd; var ckBox,ckItm: Integer);
    procedure GetCkHit     (thisEvent: EventRec);
    procedure TrkCkBoxMse  (msePt: Point; retTime: TL);
    procedure RestoreCkForm;

    (* miscellaneous *)
    procedure InitDmgr     (btnsLim: Integer);
    procedure SetDBox      (inD,outD: QDPtr; prcP,trkP,rstP,endP: ProcPtr);
    procedure ClearDBox;
    procedure RestoreDBox;
    function fDboxIsOpen: Boolean;

{$IFC fDbgOk}
    procedure DebugCkForm(CkFormHnd: TCkFormHnd);
    procedure DebugTheDbox;
{$ENDC}

    (* calling procs *)
    procedure CallPrcsProc (thisEvent: EventRec; ProcessProc: ProcPtr);
    procedure CallTrkProc  (msePt: Point; retTime: TL; TrkProc: ProcPtr);
    procedure CallRstProc  (RestoreProc: ProcPtr);
    procedure CallEndProc  (butnItm: Integer; EndProc: ProcPtr);
    procedure CallShowCkBox(info: TCkBoxHnd; ShowProc: ProcPtr);
    function  CallHitTst   (info: TCkBoxHnd; pt: Point;
                            var ckItm: Integer; HitFunc: ProcPtr): Boolean;
    procedure DboxPrcsProc (thisEvent: EventRec);
    procedure DboxTrkProc  (msePt: Point; retTime: TL);
    procedure DboxRstProc;
    procedure DboxEndProc  (butnItm: Integer);


(* ********************************************************************* *)

IMPLEMENTATION

var
    numBtns: Integer;

(* ********************************************************************* *)

(* Dialog Box GuideLines *)

(* ********************************************************************* *)

(* The only kinds of events that can be gotten in the dialog folder are  *)
(* keyDown, buttonDown, buttonUp, folderUpdate and nilEvent. Ignore      *)
(* folderUpdate, since the only way to get it is thru a call to          *)
(* DialogHeight, and it has a flag allowing you to specify if you want   *)
(* it.  We never do. *)

(* ********************************************************************* *)

(* ckBox reading *)

(* ********************************************************************* *)


{$S LWDebug}
{$IFC fDbgOk}
    procedure DebugCkForm{(CkFormHnd: TCkFormHnd)};
    {Write out all info about the check form.  Doesn't check any debug flag.  User must
     type RETURN after each ckItm has been described.}
    var
        i, j: integer;
    begin
        {$IFC sampcalls}  ALogCall;  {$ENDC}
        with CkFormHnd^^, CkFormHnd^^ do begin
            for i := 0 to numCkBox do with ckBoxLst[i]^^ do begin
                writeln('ckBoxId: ', ckBoxId, ', numCkItm: ', numCkItm);
                write('DrawProc: ');
                if (ord(DrawProc) = ord(@ShowCkBox)) then writeln('ShowCkBox')
                else if (ord(DrawProc) = ord(@ShowCkBtn)) then writeln('ShowCkBtn')
                else writeln(ord(DrawProc));
                write('HitTstFunc: ');
                if (ord(HitTstFunc) = ord(@CkBoxHit)) then writeln('CkBoxHit')
                else if (ord(HitTstFunc) = ord(@CkBtnHit)) then writeln('CkBtnHit')
                else writeln(ord(HitTstFunc));
                for j := 0 to numCkItm do with ckItmLst[j]^^ do begin
                    writeln(iTitle^^);
                    writeln('font: ', font, ', iLoc(h,v): ', iLoc.h, iLoc.v);
                    writeln('hitBox(left, top, right, bottom):');
                    with hitBox do writeln(topLeft.h, topLeft.v, botRight.h, botRight.v);
                    writeln('itmBox(left, top, right, bottom):');
                    with itmBox do writeln(topLeft.h, topLeft.v, botRight.h, botRight.v);
                end;
            end;
        end;
    end;


    procedure DebugTheDbox;
    {Write out info about theDbox.  Doesn't check any debug flags.}
    begin
        {$IFC sampcalls}  ALogCall;  {$ENDC}
        writeln('theDBox is at location: ', ord(@theDBox));
        writeln('theDBox.isOpen is at location: ', ord(@theDBox.isOpen));
        write('theDBox.isOpen is ');
        if theDBox.isOpen then writeln('TRUE') else writeln('FALSE');
     end;
{$ENDC}


{$S LWInsert}
     function fDboxIsOpen{: Boolean};
     begin
         {$IFC sampcalls}  ALogCall;  {$ENDC}
         fDboxIsOpen := theDbox.isOpen;
         {$IFC fDbgOk}
         {if ftstDmgr then DebugTheDbox;}
         {$ENDC}
     end;


{$ifc fDbgOk}
{$S LWDSpell}
{$elsec}
{$S LWSpInit}
{$endc}

    procedure SetCkItm(var itm: TCkItmHnd; c,r,f: Integer; title: StrChars; itsABtn: boolean;
                         allocMax: boolean);
    {If allocMax is true then allocate the max no. of chars for title; otherwise, allocate
     as many as are needed for title.}
    var dh,dv,bh,bv,btnRight: Integer;
        thisTitle: TStrHnd;

        ascent:     Integer;
        descent:    Integer;
        lSpace:     Integer;
        mSpace:     Integer;        (* width of the character "m"            *)
        lSpTop:     Integer;        (* dist fm line top to ascender top      *)
        offset:     Integer;        (* baseLine := offset+row*lineSp         *)
        (* ckBox info *)
        itmBoxSize: Rect;           (* 2/3 ascent *)

        errNum:Integer;
        fInfo: FontInfo;
        fid:   TLFntId;
    begin
        {$IFC sampcalls}  ALogCall;  {$ENDC}
        SetPort(dialogFolder);
        TextFont(sysText);
        TextFace([]);
        fid.fam := sysText;
        fid.seteface := [];
        fid.dev := devScreen;
        (* get font info used in reading ckForms *)
        if not FmFontMetrics(fid,fInfo,errNum) then begin
            {
            writeln('DMgr Error -- SetDFont can''t find font');
            Halt;
            }
        end else begin
            ascent  := fInfo.ascent;
            descent := fInfo.descent;
            lSpace  := fInfo.ascent+fInfo.descent+fInfo.leading;
            mSpace  := CharWidth('m');
            (* lSpTop  := Round((lSpace-ascent-descent)/2); JBB *)
            lSpTop  := (lSpace-ascent-descent) DIV 2;
            (* offset  := lSpTop+ascent-Round(lSpace/2); JBB *)
            offset  := lSpTop+ascent-lSpace DIV 2;
            (* ckBox vars *)
            SetRect(itmBoxSize,0,0,(3*ascent) div 2,ascent);
        end;

        itm       := Pointer(Ord(HAllocate(theHeap,SizeOf(TCkItmInfo))));
        If allocMax then
           thisTitle := Pointer(Ord(HAllocate(theHeap,maxCharsStr+1)))
           else thisTitle := Pointer(Ord(HAllocate(theHeap,Length(title)+1)));
        with itm^^ do begin
            (* set font of ckItm *)
            font := f;
            (* set name of ckItm *)
            iTitle := thisTitle;
            MoveLeft(title,iTitle^^,Length(title)+1);
            (* set starting point for text *)
            dh := c*mSpace;
            dv := r*lSpace+offset;
            SetPt(iLoc,dh+2*mSpace,dv);
            (* set itm ckBox *)
            itmBox := itmBoxSize;
            OffSetRect(itmBox,dh,dv-itmBoxSize.bottom);
            (* set hitBox *)
            SetRect(hitBox,0,0,StringWidth(title)+{BBB 4/27/83}2*mSpace,lSpace);
            OffsetRect(hitBox,dh{-(mSpace div 2) BBB 4/27/83},dv-ascent-lSpTop);
            if itsABtn then begin
                bh := CalcButWidth(0, numBtns, bv);
                btnRight := dialogfolder^.portrect.right-20;
                SetRect(hitBox, btnRight-bh , dv-bv, btnRight, dv);
                end;
        end;
    end; (* SetCkItm *)

(* ********************************************************************* *)

    procedure ScanCkBox(lineBuf: Str255; var c,r,f: Integer; var s: StrChars);
    var bufIndx: Integer;
        digits: Set of Char;

    (* ***************************************************************** *)

        function  GetInt: Integer;
        var value: Integer;
        begin
            {$IFC sampcalls}  ALogCall;  {$ENDC}
            value := 0;
            while (bufIndx<=Length(lineBuf)) and
            not (lineBuf[bufIndx] in digits) do
                bufIndx := bufIndx+1;
            while (bufIndx<=Length(lineBuf)) and
            (lineBuf[bufIndx] in digits) do begin
                value := Ord(lineBuf[bufIndx])-Ord('0')+10*value;
                bufIndx := bufIndx+1;
            end;
            GetInt := value;
        end; (* GetInt *)

    (* ***************************************************************** *)

        procedure GetString(var thisStr: StrChars);
        var byteCnt: Integer;
            src: TStrPtr;
        begin
            {$IFC sampcalls}  ALogCall;  {$ENDC}
            thisStr := '';
            while (bufIndx<=Length(lineBuf)) and (lineBuf[bufIndx]<=' ') do
                bufIndx := bufIndx+1;
            byteCnt := Length(lineBuf)-bufIndx+2;
            if byteCnt>30 then
                byteCnt := 30;
            src := Pointer(Ord(@lineBuf)+bufIndx-1);
            MoveLeft(src^,thisStr,byteCnt);
            (*$R-*)
            thisStr[0] := Chr(byteCnt-1);
            (*$R+*)
        end; (* GetString *)

    (* ***************************************************************** *)

    begin
        {$IFC sampcalls}  ALogCall;  {$ENDC}
        if lineBuf<>'' then begin
            digits := ['0'..'9'];
            bufIndx := 1;
            c := GetInt;
            r := GetInt;
            f := GetInt;
            GetString(s);
        end;
    end; (* ScanCkBox *)

(* ********************************************************************* *)

    procedure ReadCkItm(var f: Text; info: TCkBoxHnd; var noMore: Boolean;
                          allocMax: boolean);
    {If allocMax is true then allocate the max no. of chars for title; otherwise, allocate
     as many as are needed for title.}
    var title: StrChars;
        c,r,font: Integer;
        lineBuf: Str255;
        thisItm: TCkItmHnd;  (* temp handle to pass to SetCkItm *)
    begin
        {$IFC sampcalls}  ALogCall;  {$ENDC}
        (* see if standard CkBox or user defined *)
        Readln(f,lineBuf);
        If ftstDmgr then Writeln('ReadCkItm read: ');
        If ftstDmgr then Writeln(lineBuf);
        if lineBuf='' then
            noMore := TRUE
        else if info^^.numCkItm=maxCkItm then
            noMore := TRUE
        else begin
            with info^^ do begin
                noMore := FALSE;
                numCkItm := numCkItm+1;
                ScanCkBox(lineBuf,c,r,font,title);
            end;
            SetCkItm(thisItm,c,r,font,title, (info^^.ckBoxId = 0), allocMax);
            with info^^ do
                ckItmLst[numCkItm] := thisItm;
        end;
    end; (* ReadCkItm *)

(* ********************************************************************* *)

    procedure ReadCkBox(* var ckFile: Text; var info: TCkBoxHnd; allocMax: boolean *);
    var noMore: Boolean;
        i: Integer;
    begin
        {$IFC sampcalls}  ALogCall;  {$ENDC}
        SetPort(dialogFolder);
        info := Pointer(Ord(HAllocate(theHeap,SizeOf(TCkBoxInfo))));
        with info^^ do begin
            (* init ckBox *)
            numCkItm   := -1;
            curOnOff   := -1;
            curCkItm   := -1;
            hasOnOff   := FALSE;
            DrawProc   := nil;
            HitTstFunc := nil;
            for i := 0 to maxCkItm do
                ckItmLst[i] := nil;
        end;

        (* read in ckBox info *)
        ReadLn(ckFile,info^^.ckBoxId);
        repeat
            ReadCkItm(ckFile,info,noMore, allocMax);
        until noMore;

        with info^^ do begin
            (* see if standard ckBox *)
            with ckItmLst[0]^^ do begin
                (* get ckBox hitBox size *)
                if numCkItm>0 then for i := 1 to numCkItm do
                    UnionRect(hitBox,ckItmLst[i]^^.hitBox,hitBox);
                (* set standard Draw and Choose procs *)
                DrawProc   := @ShowCkBox;
                HitTstFunc := @CkBoxHit;
            end;
        end;
    end; (* ReadCkBox *)

(* ********************************************************************* *)

(* ckBox display and tracking routines *)


(* ********************************************************************* *)

    procedure CkFormHeight(* info: TCkFormHnd *);
    var thisHeight,height: Integer;
        i,j,bh,bv: Integer;
    begin
        {$IFC sampcalls}  ALogCall;  {$ENDC}
        height := 0;
        with info^^ do begin
            for i := 0 to numCkBox do begin
                (*$R-*)
                thisHeight := ckBoxLst[i]^^.ckItmLst[0]^^.hitBox.bottom;
                (*$R+*)
                if thisHeight>height then
                    height := thisHeight;
            end;
            vCkForm := height+8 { Round(lSpace/2) };
        end;
    end; (* CkFormHeight *)

(* ********************************************************************* *)

{$S LWGuess}
    procedure ShowCkItm(info: TCkItmHnd; showBox,boxChecked: Boolean);
    var
        thisPort: GrafPtr;
    begin
        {$IFC sampcalls}  ALogCall;  {$ENDC}
        if info<>nil then with info^^ do begin
        {$ifc fDbgOk}
           GetPort(thisPort);
           if ftstDmgr then
               begin
               writeln('   In ShowCkItm for ', iTitle^^);
               PxR('   itmBox is ', itmBox);
               writeln('font: ', font, ', iLoc(h,v): ', iLoc.h, iLoc.v);
               InvertRect(thisPort^.portRect);
               PxR('The PortRect is ', thisPort^.portRect);
               write(CHR(7),
                     'PortRect is inverted. Type <CR> to un-invert and invert ClipRgn:');
               readln;
               InvertRect(thisPort^.portRect);
               InvertRgn(thisPort^.clipRgn);
               write(CHR(7),
                     'ClipRgn is inverted. Type <CR> to un-invert and invert VisRgn:');
               readln;
               InvertRgn(thisPort^.clipRgn);
               InvertRgn(thisPort^.visRgn);
               write(CHR(7), 'VisRgn is inverted. Type <CR> to un-invert:');
               readln;
               InvertRgn(thisPort^.visRgn);
               end;
            if thisPort<>dialogFolder then
               writeln(CHR(7),
                       'SHOWCKITM CALLED WITH PORT <> DIALOGFOLDER!!!!!!!!!!!!!!!!');
        {$endc}
            PenNormal;
            ShowPen;
            TextMode(srcOr);
            TextFont(font);
            with iLoc do
            MoveTo(h,v);
            DrawString(iTitle^^);
            if showBox then begin
                if boxChecked then
                    FillRect(itmBox,black)
                else
                    FrameRect(itmBox);
                {$ifc fDbgOk}
                if ftstDmgr then PxR('Just drew itmBox = ', itmBox);
                {$endc}
            end
            else if ftstDmgr then writeln('showBox is FALSE');
            if ftstDmgr then writeln;
        end;
    end; (* ShowCkItm *)

(* ********************************************************************* *)

    procedure ChgCkItm(* info: TCkBoxHnd; newCkItm: TB *);
    var
        thisPort: GrafPtr;
    begin
       (*$R-*)
       {$IFC sampcalls}  ALogCall;  {$ENDC}
       PenNormal;
       ShowPen;
       TextMode(srcOr);
       {$ifc fDbgOk}
       GetPort(thisPort);
       if ftstDmgr then
       begin
          writeln('In ChgCkItm:');
          InvertRect(thisPort^.portRect);
          PxR('The PortRect is ', thisPort^.portRect);
          write(CHR(7), 'PortRect is inverted. Type <CR> to un-invert and invert ClipRgn:');
          readln;
          InvertRect(thisPort^.portRect);
          InvertRgn(thisPort^.clipRgn);
          write(CHR(7), 'ClipRgn is inverted. Type <CR> to un-invert and invert VisRgn:');
          readln;
          InvertRgn(thisPort^.clipRgn);
          InvertRgn(thisPort^.visRgn);
          write(CHR(7), 'VisRgn is inverted. Type <CR> to un-invert:');
          readln;
          InvertRgn(thisPort^.visRgn);
       end;
       if thisPort<>dialogFolder then {if ftstDmgr then}
          writeln('CHGCKITM CALLED WITH PORT <> DIALOGFOLDER!!!!!!!!!!!!!!!!')
       else
       {$endc}
       with info^^ do with ckItmLst[curCkItm]^^ do
       begin
          EraseRect(itmBox);
          FrameRect(itmBox);
          {$ifc fDbgOk}
          if ftstDmgr then PxR('Erased itmBox = ', itmBox);
          {$endc}
       end;
       info^^.curCkItm := newCkItm;
       with info^^ do with info^^.ckItmLst[curCkItm]^^ do
       begin
          FillRect(itmBox,black);
          {$ifc fDbgOk}
          if ftstDmgr then PxR('Filled itmBox = ', itmBox);
          {$endc}
       end;
       (*$R+*)
    end;

(* ********************************************************************* *)

    procedure ShowCkBtn(* info: TCkBoxHnd *);
    var i: Integer;
    begin
        {$IFC sampcalls}  ALogCall;  {$ENDC}
        if info=nil then
            writeln('DMgrError--ShowCkBtn info is nil.')
        else with info^^ do begin
            (* show the button title *)
            ShowCkItm(ckItmLst[0],FALSE,FALSE);

            (* First, hide buttons that are not to be shown *)
            for i := 0 to numBtns do
                with ckItmLst[i+1]^^ do if (ord(iTitle^^[0])=0) then HideButn(i);

            (* Then, show buttons that are to be shown *)
            for i := 0 to numBtns do begin
                with ckItmLst[i+1]^^ do if (ord(iTitle^^[0])>0) then begin
                    HideButn(i);
                    PushButn(i,FALSE);
                    ShowButn(i, hitBox.left, hitBox.top, FALSE);
                end;
            end;
        end;
    end; (* ShowCkBtn *)

(* ********************************************************************* *)

    procedure ShowCkBox(* info: TCkBoxHnd *);
    var i: Integer;
        boxChecked: Boolean;
    begin
        {$IFC sampcalls}  ALogCall;  {$ENDC}
        if info=nil then
            writeln('DMgrError--ShowCkBox info is nil.')
        else with info^^ do
            for i := 0 to numCkItm do begin
                if ftstDmgr then writeln(' In ShowCkBox, the ckItm is ', i);
                boxChecked := (i=curOnOff) or (i=curCkItm);
                ShowCkItm(ckItmLst[i],i<>0,boxChecked);
            end;
    end; (* ShowCkBox *)

(* ********************************************************************* *)

    procedure ShowJust1CkBox(* info: TCkFormHnd; ckBox: Integer *);
    var
        thisPort: GrafPtr;
    begin
        {$IFC sampcalls}  ALogCall;  {$ENDC}
        {$IFC fDbgOk}
        if ftstDmgr then writeln('ShowJust1CkBox,');
       {$ENDC}
       (*$R-*)
       GetPort(thisPort);
       if ftstDmgr then
       begin
          InvertRect(thisPort^.portRect);
          {$ifc fDbgOk}
          PxR('The PortRect is ', thisPort^.portRect);
          {$endc}
          write(CHR(7), 'PortRect is inverted. Type <CR> to un-invert and invert ClipRgn:');
          readln;
          InvertRect(thisPort^.portRect);
          InvertRgn(thisPort^.clipRgn);
          write(CHR(7), 'ClipRgn is inverted. Type <CR> to un-invert and invert VisRgn:');
          readln;
          InvertRgn(thisPort^.clipRgn);
          InvertRgn(thisPort^.visRgn);
          write(CHR(7), 'VisRgn is inverted. Type <CR> to un-invert:');
          readln;
          InvertRgn(thisPort^.visRgn);
       end;
       if thisPort<>dialogFolder then {if ftstDmgr then}
          writeln('SHOWJUST1CKBOX CALLED WITH PORT <> DIALOGFOLDER!!!!!!!!!!!!!!!!')
       else with info^^, info^^.ckBoxLst[ckBox]^^ do
       begin
          FillRect(ckItmLst[0]^^.hitBox, white);
          CallShowCkBox(ckBoxLst[ckBox], DrawProc);
       end;
       (*$R+*)
    end;

(* ********************************************************************* *)

    procedure ShowCkForm(* info: TCkFormHnd *);
    var i: Integer;
        thisPort: GrafPtr;
    begin
        {$IFC sampcalls}  ALogCall;  {$ENDC}
        {OpenDBox}
        GetPort(thisPort);
        if thisPort=dialogFolder then begin
            DialogHeight(info^^.vCkForm,FALSE);
            theDBox.isOpen := TRUE;
        end;
        {$IFC fDbgOk}
        if ftstDmgr then
        begin
           writeln('On entry to ShowCkForm,');
           DebugCkForm(Pointer(Ord(theDBox.inData)));
           writeln('In ShowCkForm: ');
           DebugTheDbox;
           InvertRect(thisPort^.portRect);
           PxR('The PortRect is ', thisPort^.portRect);
           write(CHR(7), 'PortRect is inverted. Type <CR> to un-invert and invert ClipRgn:');
           readln;
           InvertRect(thisPort^.portRect);
           InvertRgn(thisPort^.clipRgn);
           write(CHR(7), 'ClipRgn is inverted. Type <CR> to un-invert and invert VisRgn:');
           readln;
           InvertRgn(thisPort^.clipRgn);
           InvertRgn(thisPort^.visRgn);
           write(CHR(7), 'VisRgn is inverted. Type <CR> to un-invert:');
           readln;
           InvertRgn(thisPort^.visRgn);
        end;
        if thisPort<>dialogFolder then
           writeln(CHR(7),
                   'SHOWCKFORM CALLED WITH PORT <> DIALOGPORT!!!!!!!!!!!!!!!!!!!!!');
        {$ENDC}
        FillRect(thisPort^.portRect,white);

        with info^^ do begin
            if (numCkBox <> -1) then for i := 0 to numCkBox do
                (*$R-*)
                CallShowCkBox(ckBoxLst[i],ckBoxLst[i]^^.DrawProc);
                (*$R+*)
        end;
    end; (* ShowCkForm *)

(* ********************************************************************* *)

    function  CkBtnHit(* info: TCkBoxHnd; pt: Point;
                        var ckItm: Integer): Boolean *);
    var pushed: TButn;
    begin
        {$IFC sampcalls}  ALogCall;  {$ENDC}
        SetPort(dialogFolder);
        CkBtnHit := ButnPushed(0,numBtns,pushed,pt);
        ckItm := pushed+1;
    end; (* CkBtnHit *)

(* ********************************************************************* *)

    function  CkBoxHit(* info: TCkBoxHnd; pt: Point;
                        var ckItm: Integer): Boolean *);
    var hit: Boolean;
        i: Integer;
    begin
        {$IFC sampcalls}  ALogCall;  {$ENDC}
        ckItm := 0;
        hit := PtInRect(pt,info^^.ckItmLst[0]^^.hitBox);
        if hit then with info^^ do begin
            for i := 1 to numCkItm do
                if PtInRect(pt,ckItmLst[i]^^.hitBox) then begin
                    ckItm := i;
                    i := numCkItm;
                end;
        end;
        CkBoxHit :=  hit;
    end; (* CkBoxHit *)

(* ********************************************************************* *)

    procedure CkFormSelect(* info: TCkFormHnd; var ckBox,ckItm: Integer *);
    var msePt: Point;
        oldCkItm,i: Integer;
        thisRect: Rect;
        thisEvent: EventRecord;
    begin
        {$IFC sampcalls}  ALogCall;  {$ENDC}
        (* assumes port is already set *)
        if fTstDmgr then
            writeln('CkFormSelect');

        repeat
            (* do nothing *)
        until not StillDown;
        GetMouse(msePt);
        (* get rid of buttonUp event *)
        if PeekEvent(thisEvent) then begin
            if thisEvent.what=buttonUp then begin
                GetEvent(thisEvent);
                msePt := thisEvent.where;
            end;
        end;
        ckBox := 0;
        with info^^ do
        if (numCkBox <> -1) then for i := 0 to numCkBox do begin
            (*$R-*)
            if CallHitTst(ckBoxLst[i],msePt,
                          ckItm,ckBoxLst[i]^^.HitTstFunc) then
            with ckBoxLst[i]^^ do begin
                ckBox := ckBoxId;
                i := numCkBox;
                if ckBox>0 then
                    if ckItm>0 then begin
                        (* figure out which item was checked *)
                        if hasOnOff and (ckItm<=2) then begin
                            oldCkItm := curOnOff;
                            curOnOff := ckItm;
                        end else begin
                            oldCkItm := curCkItm;
                            curCkItm := ckItm;
                        end;
                        (* clear old item *)
                        if oldCkItm>0 then begin
                            thisRect := ckItmLst[oldCkItm]^^.itmBox;
                            InSetRect(thisRect,1,1);
                            FillRect(thisRect,white);
                        end;
                        (* fill new item *)
                        FillRect(ckItmLst[ckItm]^^.itmBox,black);
                    end;
            end;
            (*$R+*)
        end;

        if fTstDmgr then
            writeln('    ',ckBox:2,',',ckItm:2);
    end; (* CkFormSelect *)

(* ********************************************************************* *)

    procedure GetCkHit(* thisEvent: EventRec *);
    var info: TCkFormHnd;
        ckBox,ckItm: Integer;
    begin
        {$IFC sampcalls}  ALogCall;  {$ENDC}
        if fTstDmgr then
            writeln('GetCkHit(',thisEvent.what:2,')');

        if thisEvent.what=buttonDown then with theDBox do begin
            info := Pointer(Ord(inData));
            CkFormSelect(info,ckBox,ckItm);
            if (ckBox=0) and (ckItm<>0)  then
                CallEndProc(ckItm,endProc)
        end;
    end; (* GetCkHit *)

(* ********************************************************************* *)

    procedure TrkCkBoxMse(* msePt: Point; retTime: TL *);
    var info:     TCkFormHnd;
        i,j,ckItm,dBoxv: Integer;
        inCkItm:  Boolean;
        mseMoved: Boolean;
        nHood:    Rect;
        thisPort: GrafPtr;
        arrowCrs: Boolean;
    begin
        {$IFC sampcalls}  ALogCall;  {$ENDC}
        (* assumes port is already set *)
        (* msePt is in dialog coords *)
        GetPort(thisPort);
        dBoxv := thisPort^.portRect.bottom;
        info := Pointer(Ord(theDBox.inData));
        arrowCrs := false; {We don't know what the cursor is, so we can't assume it's
                            an arrow}
        with info^^ do
        repeat
            GetMouse(msePt);
            inCkItm := FALSE;
            if (numCkBox > 0) then for i := 1 to numCkBox do
                (*$R-*)
                if CkBoxHit(ckBoxLst[i],msePt,ckItm) then begin
                    nHood := ckBoxLst[i]^^.ckItmLst[ckItm]^^.hitBox;
                    i := numCkBox;
                    inCkItm := ckItm<>0;
                    if ftstDmgr then begin
                        writeln('TrkCkBoxMse:');
                        writeln('   i = ', i);
                        writeln('   ckItm = ', ckItm);
                        writeln('   inCkItm = ', ord(inCkItm));
                        with nHood do writeln('   nHood = ', left,' ', top,' ', right,' ',
                                               bottom);
                        with msePt do writeln('   msePt = ', h,' ', v);
                        end;
                end;
                (*$R+*)
            if inCkItm then begin
                arrowCrs := false;
                SetStdCursor(iCrsCheck);
                repeat
                    GetMouse(msePt);
                    mseMoved := not PtInRect(msePt,nHood);
                until EventAvail or mseMoved or (time >= retTime);
                if mseMoved then
                    SetStdCursor(iCrsInactive);
            end
            else if not arrowCrs then begin
               SetStdCursor(iCrsInactive);
               arrowCrs := true;
               end
        until EventAvail or (msePt.v>dBoxv) or (time >= retTime);
    end; (* TrkCkBoxMse *)

(* ********************************************************************* *)

    procedure RestoreCkForm;
    var info: TCkFormHnd;
    begin
        {$IFC sampcalls}  ALogCall;  {$ENDC}
        if fTstDmgr then
            writeln('RestoreCkForm');

        info := Pointer(Ord(theDBox.inData));
        ShowCkForm(info);
    end; (* RestoreCkForm *)

(* ********************************************************************* *)

(* miscellaneous *)

(* ********************************************************************* *)


{$S LWSpInit}
    procedure InitDmgr     (* btnsLim: Integer *);
    {Init this Dmgr unit.  The buttons defined in the phrase file range from 0 to btnsLim}
    begin
        {$IFC sampcalls}  ALogCall;  {$ENDC}
       numBtns := btnsLim;
    end;


{$S LWGuess}
    procedure SetDBox(* inD,outD: QDPtr; prcP,trkP,rstP,endP: ProcPtr *);
    begin
        {$IFC sampcalls}  ALogCall;  {$ENDC}
        with theDBox do begin
            inData      := inD;
            outData     := outD;
            processProc := prcP;
            trkMseProc  := trkP;
            restoreProc := rstP;
            endProc     := endP;
            {$IFC fDbgOk}
            if ftstDmgr then
               begin
               writeln('After SetDBox,');
               DebugCkForm(Pointer(Ord(theDBox.inData)));
               end;
            {$ENDC}
        end;
    end; (* SetDBox *)

(* ********************************************************************* *)

    procedure ClearDBox;
    begin
        {$IFC sampcalls}  ALogCall;  {$ENDC}
        if fTstDmgr then
            writeln('ClearDBox.');

        with theDBox do begin
            if isOpen then
                DialogHeight(0,False);
            {
            else
                writeln('DMgrError: ClearDBox--theDBox not open.');
            }
            isOpen := FALSE;
        end;
        {$IFC fDbgOk}
        if ftstDmgr then begin
            writeln('In ClearDBox: ');
            DebugTheDbox;
            end;
        {$ENDC}

    end; (* ClearDBox *)

(* ********************************************************************* *)

    procedure RestoreDBox;
    begin
        {$IFC sampcalls}  ALogCall;  {$ENDC}
        if fTstDmgr then
            writeln('RestoreDBox.');

        with theDBox do
        if isOpen then begin
            SetPort(dialogFolder);
            CallRstProc(restoreProc);
        end;
    end; (* RestoreDBox *)

(* ********************************************************************* *)

{$S LWSpInit}
    procedure DboxPrcsProc {(thisEvent: EventRec)};
    begin
    {$IFC sampcalls}  ALogCall;  {$ENDC}
    {$IFC fDbgOk}
    if ftstDmgr then
       begin
       writeln('On entry to DboxPrcsProc,');
       DebugCkForm(Pointer(Ord(theDBox.inData)));
       end;
    {$ENDC}
    CallPrcsProc(thisEvent, theDBox.ProcessProc);
    end;

    procedure DboxTrkProc  {(msePt: Point; retTime: TL)};
    begin
        {$IFC sampcalls}  ALogCall;  {$ENDC}
        CallTrkProc(msePt, retTime, theDBox.TrkMseProc);
    end;

    procedure DboxRstProc;
    begin
        {$IFC sampcalls}  ALogCall;  {$ENDC}
        CallRstProc(theDBox.RestoreProc);
    end;

    procedure DboxEndProc  {(butnItm: Integer;)};
    begin
        {$IFC sampcalls}  ALogCall;  {$ENDC}
        CallEndProc(butnItm, theDBox.EndProc);
    end;



    procedure CallPrcsProc (* thisEvent: EventRec; ProcessProc: ProcPtr *);
        external;

    procedure CallTrkProc  (* msePt: Point; retTime: TL; TrkProc: ProcPtr *);
        external;

    procedure CallRstProc  (* RestoreProc: ProcPtr *);
        external;

    procedure CallEndProc  (* butnItm: Integer; EndProc: ProcPtr *);
        external;

    procedure CallShowCkBox(* info: TCkBoxHnd; ShowProc: ProcPtr *);
        external;

    function  CallHitTst   (* info: TCkBoxHnd; pt: Point;
                              var ckItm: Integer; HitFunc: ProcPtr): Boolean *);
        external;

END. (* unit *)


ÿ