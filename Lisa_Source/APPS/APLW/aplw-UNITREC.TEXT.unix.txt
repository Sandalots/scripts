{$I APLW/COMPFLAGS }                                            {gdf 1/16/84}
{$R-}

UNIT UnitRec;

(* Copyright 1983, 1984, Apple Computer Inc. *)

{ gdf  2/24/84 -- insert trace calls for segmentation  }
{ gdf  1/16/84 -- printing compile flag & include COMPFLAGS added }
{ tjm  3/07/83 -- check fRecDep in OrecOfPt }
{ crc  3/02/83 -- OrecConOfOboxIcs --> OrecConOfOboxIcsLp }
{ crc  3/01/83 -- check fRecDep (NOT fSelectable) in OrecOfLp }
{ tjm  2/24/83 -- check fSelectable in OrecOfLp }
{ crc 11/22/82 -- argptInch[tyxyScrn].v changed from 66 to 72 }
{ jrm 11/11/82 -- fixed OrecOfPt; defined strec.fSelectable and initialized it }
{ jrm  6/29/82 -- USES changes: LisaGraf => QuickDraw; moved USES UnitFile }
{ crc  6/03/82 -- Init argptInch[tyxyHiDotMx] }
{ crc  6/02/82 -- Conditional compilation }
{ crc  5/21/82 -- IcsConFromPane added from UnitLotus }
{ crc 04/06/82 -- integration with new font manager }
{ bas 03/04/82 -- move ConvertTyxy here from UnitTld }
{ bas 03/03/82 -- add tyxy to TArec, cascade effects }
{ bas 01/13/82 -- USES for new wm }
{ bas 12/10/81 -- integration with font manager }
{ edited by dsy 9/24/81 }
{ edits -- 1) 'PxDsp' cleaned up and exported.
           2) 'PxOrec' cleaned up.
           3) 'PxOrecCon' created; called by PxDsp.
}

INTERFACE

USES
     {$IFC sampcalls}                                               {gdf 2/24/84}
         {$U aplw/tracecalls.obj} tracecalls,
     {$ENDC}
     {$U Obj:UnitStd.Obj  } UnitStd,
     {$U Obj:UnitHz.Obj   } UnitHz,
     {$U LibOS/Syscall.Obj }   Syscall,
     {$U Obj:UnitFile.Obj } UnitFile,
     {$U Obj:Storage.Obj  } Storage,
     {$U Obj:QuickDraw.Obj } QuickDraw,
     {$U Obj:FontMgr.Obj  } FontMgr,
     {$U Obj:PmDecl       } PmDecl,
     {$IFC fPrintOld}                                               {gdf 1/16/84}
        {$U Obj:PrStd.Obj }     PrStd,
     {$ELSEC}
        {$U Obj:PrStdInfo.Obj } PrStdInfo,
          (* {$U Obj:PrPublic.Obj }  PrPublic,  unneeded ? *)
     {$ENDC}                                                        {gdf 1/16/84}
     {$U Obj:UnitFmt.Obj  } UnitFmt,
     {$U Obj:UnitCs.Obj   } UnitCs,
     {$U Obj:Wm.Events.Obj  } Events,
     {$U Obj:Wm.Folders.Obj } Folders,
     {$U Obj:WM.Menus   } Menus,
     {$U Obj:WmlStd     }    WmlStd,
     {$U Obj:AlertMgr   } AlertMgr,
     {$U ApLW/Queue.Obj    } Queue,
     {$U ApLW/UnitBox.Obj  } UnitBox;

CONST
      {$IFC NOT fDbgOk} fTstRec = FALSE; {$ENDC}
      fixedLpFst = $01;
      fixedLpLim = $02;
      fixedR     = $04;
      ioBox = 0;

TYPE TTyterm = (tytermEos, tytermOutOfWw, tytermLayoutDone);

     TFixed =  TB; {(fixedLpFst, fixedLpLim, fixedR)}
     TSetfixed = TB;  {SET OF TFixed}
     TStrec = PACKED RECORD
             fRecCon: TF;
             fValid: TF;
             fImged: TF;
             fScrollH: TF;
             fScrollV: TF;
             fFormFeed: TF;
             fKeep: TF;
             fRecDep: TF;
             fSelectable: TF;
             fParSt: TF;
             setfixed: TSetfixed;
             END;
     TFo = PACKED RECORD
             fObox:  TF;
             o:      0..MAXINT;
             END;
     TArec = RECORD
             orecSib: TC;            { used for chaining free rec's, also }
             orecSon: TC;
             tyxy: TTyxy;            { metric space of r }
             r: TR;                  { box-relative coords }
             ointDep: TC;
             foPrnt: TFo;
             strec:  TStrec;
             CASE {strec.fRecCon: } TF OF
                FALSE:  (dlpLim: TLp);
                TRUE:   (orecNxtCon: TC; tyterm: TTyterm; ointSel: TC);
             END;
     TRec = ^TArec;
     TArgarec = ARRAY [0..0] OF TArec;

     TAdsp = RECORD
             orecLim: TC;
             orecFreeFst: TC;
             orecConRoot:       TC;
             argarec: TArgarec;
             END;
     TDsp = ^TAdsp;

     TArgo = ARRAY [0..0] OF TC;
     TRgo = ^TArgo;

VAR
    {$IFC fDbgOk} fTstRec: TF; {$ENDC}
    dsp:    TDsp;
    arecStd: TArec;
    foNil:  TFo;
    argptInch: ARRAY[TTyxy] OF TPt;

{$ifc fDbgOk}
PROCEDURE PxDsp;
PROCEDURE PxOrec (orec: TC);
{$endc}
PROCEDURE InitRec      (hz: THz; irecLim: TC);
PROCEDURE ConvertTyxy  (VAR x,y: TC; tyxySrc, tyxyDst: TTyxy);
PROCEDURE ConvertRTyxy (VAR rSrc, rDst: TR; tyxySrc, tyxyDst: TTyxy);
FUNCTION  OrecAlloc    : TC;
FUNCTION  Orec1Alloc   (ics: TB; lpFst: TLp; lpLim: TLp; ptTL, ptBR: TPt;
                       tyxyRec: TTyxy; setfixed: TSetfixed; fRecCon: TF;
                       foPrntRec: TFo): TC;
FUNCTION  OrecSibFree  (orec: TC): TC;

FUNCTION  OrecOfPt     (orecFst: TC; fNearest: TF; ptScrn: TPt; fRecurse: TF): TC;
FUNCTION  OrecOfLp     (orecFst: TC; ics: TB; lp: TLp; fRecurse: TF): TC;
FUNCTION  OboxOfOrec   (orec: TC): TC;
(*
FUNCTION  FInRec       (pt: TPt; orec: TC) : TF;
*)
FUNCTION  FEqRs        (r1, r2: TR): TF;
FUNCTION  LpLimCon     (orecCon: TC): TLp;
PROCEDURE SetRgoFromOrec   (orec: TC; rgo: TRgo; ioLim: TC);
FUNCTION  OrecCreateWindow (ics: TB; lpFst: TLp; ptTL: TPt; obox: TC): TC;
FUNCTION  OrecConOfOboxIcsLp (obox: TC; ics: TB; lp: TLp): TC;
FUNCTION  IcsConFromPane (oboxPane: TC; VAR orecCon: TC) : TB;
FUNCTION  OrecConFromIcs (ics: TB): TC;


IMPLEMENTATION

{$IFC fSymOK }
{$D+ }
{$ELSEC }
{$D- }
{$ENDC }

CONST crecMore = 10;

VAR hzDsp: THz;
    cbRecMore: TC;
    argn: TArgn;


{$S LWMenu}
{------------------------------------------------------------}
FUNCTION IcsConFromPane {(oboxPane: TC; VAR orecCon: TC) : TB};
{------------------------------------------------------------}
LABEL 33;
VAR recCon:     TRec;
    pboxPane:     TPbox;
    pint:   TPint;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
pboxPane := POINTER(ORD(bdsp)+oboxPane);
orecCon := pboxPane^.refCon;
WHILE TRUE DO
    BEGIN
    recCon := POINTER(ORD(dsp)+orecCon);
    IF recCon^.orecSib = orecNil THEN GOTO 33;
    orecCon := recCon^.orecSib;
    END;
33:
pint := POINTER(ORD(lldInt)+recCon^.ointDep);
IcsConFromPane := pint^.ics;
END;


{$S LWCommand}
PROCEDURE SetRgoFromOrec {(orec: TC; rgo: TRgo; ioLim: TC)};
VAR io: TC;
    PROCEDURE ChasePrnt (orec1: TC);
    VAR rec1: TRec; fo: TFo;
    BEGIN
    {$IFC sampcalls}  ALogCall;  {$ENDC}
    IF fTstRec THEN WRITELN('ChasePrnt -- orec1:', orec1:4);
    rec1 := POINTER(ORD(dsp)+orec1);
    fo := rec1^.foPrnt;
    IF fo.fObox THEN rgo^[0] := fo.o
                ELSE  ChasePrnt(fo.o);
    IF io < ioLim THEN
         BEGIN  rgo^[io] := orec1; io := io + 1; END;
    END;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
io := 1;
ChasePrnt(orec);
END;


{$S LWInsert}
FUNCTION OboxOfOrec {(orec: TC) : TC};
VAR argo: ARRAY [0..1] OF TC;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
SetRgoFromOrec(orec, @argo, 1);
IF fTstRec THEN WRITELN('OboxOfOrec -- orec:', orec:4, '; obox:', argo[ioBox]:4);
OboxOfOrec := argo[ioBox];
END;


FUNCTION OrecConOfOboxIcsLp {(obox: TC; ics: TB; lp: TLp) : TC};
LABEL 99;
VAR orecCon: TC; rec, recCon: TRec; pint:  TPint;
    orecNxtCon: TC;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
orecCon := dsp^.orecConRoot;
WHILE orecCon <> orecNil DO
     BEGIN
     IF fTstRec THEN WRITELN('OrecConOfOboxIcsLp -- orecCon:', orecCon:4);
     rec := POINTER(ORD(dsp)+orecCon);
     orecNxtCon := rec^.orecNxtCon;
     IF (obox = oboxNil) OR (OboxOfOrec(orecCon) = obox) THEN
          BEGIN
          rec := POINTER(ORD(dsp)+orecCon);
          pint := POINTER(ORD(lldInt)+rec^.ointDep);
          IF (pint^.ics = ics) THEN
             IF (lp = lpNil) OR
                ((pint^.lpFst <= lp) AND (pint^.lpLim > lp)) THEN
                  IF LpLimCon(orecCon) > lp THEN GOTO 99;
          { this code looks uselessly redundant
          recCon := rec;
          orecCon := recCon^.orecSib;
          WHILE orecCon <> orecNil DO
               BEGIN
               recCon := POINTER(ORD(dsp) + orecCon);
               pint := POINTER(ORD(lldInt) + recCon^.ointDep);
               IF (pint^.ics = ics) THEN
                  IF (lp = lpNil) OR
                     ((pint^.lpFst <= lp) AND (pint^.lpLim > lp)) THEN
                       IF LpLimCon(orecCon) > lp THEN GOTO 99;
               orecCon := recCon^.orecSib;
               END;
          }
          END;
     orecCon := orecNxtCon;
     END;
99:
IF fTstRec THEN WRITELN('OrecConOfOboxIcsLp -- returns:', orecCon:4);
OrecConOfOboxIcsLp := orecCon;
END;


{$S LWScroll}
FUNCTION OrecConFromIcs {(ics: TB) : TC};
{ Retrun first orecCon found which contains ics }
LABEL 99;
VAR orecCon: TC; rec, recCon: TRec; pint:  TPint;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
orecCon := dsp^.orecConRoot;
WHILE orecCon <> orecNil DO
     BEGIN
     IF fTstRec THEN WRITELN('OrecConFromIcs -- orecCon:', orecCon:4);
     rec := POINTER(ORD(dsp)+orecCon);
     pint := POINTER(ORD(lldInt)+rec^.ointDep);
     IF pint^.ics = ics THEN GOTO 99;
     orecCon := rec^.orecNxtCon;
     END;
99:
IF fTstRec THEN WRITELN('OrecConFromIcs -- returns:', orecCon:4);
OrecConFromIcs := orecCon;
END;


{$S LWInsert}
FUNCTION LpLimCon {(orecCon: TC) : TLp};
LABEL 77;
VAR orecT: TC; recT: TRec; pintT: TPint; lpLim: TLp;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
recT := POINTER(ORD(dsp)+orecCon);
IF NOT recT^.strec.fRecCon THEN Break(32);
orecT := recT^.orecSon;
pintT := POINTER(ORD(lldInt)+recT^.ointDep);
lpLim := pintT^.lpFst;
WHILE orecT <> orecNil DO
     BEGIN
     recT := POINTER(ORD(dsp)+orecT);
     IF recT^.strec.fRecDep THEN GOTO 77;
     pintT := POINTER(ORD(lldInt)+recT^.ointDep);
     lpLim := pintT^.lpFst + recT^.dlpLim;
     orecT := recT^.orecSib;
     END;
77:
LpLimCon := lpLim;
END;

{$ifc fDbgOk}
{$S LWDebug}
PROCEDURE PxOrec {(orec: TC)};
VAR rec: TRec;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
rec := POINTER(ORD(dsp) + orec);
WITH rec^ DO
     BEGIN
     WRITE('orec'); IF strec.fRecCon THEN WRITE('CON') ELSE WRITE('   ');
     WRITELN(':', orec:4, '; rec: ', ORD(rec), '; orecSib:', orecSib:4,
             '; orecSon:', orecSon:4);
     WRITELN('    tyxy: ', ORD(tyxy):1, '; r: [', r.left:5, r.top:5,
             r.right:5, r.bottom:5, ']');
     WRITE('       ointDep: '); PxInt(ointDep);
     WRITELN('     foPrnt.fObox: ', ORD(foPrnt.fObox):1, '; foPrnt.o: ',
     foPrnt.o:4, '; fRecDep: ', strec.fRecDep,
             '; fKeep: ', strec.fKeep, '; fSelectable: ', strec.fSelectable);
     WRITE('       fValid: ', strec.fValid,  '; setfixed:', strec.setfixed:3,
         '; fRecCon: ', strec.fRecCon, '; fImged: ', strec.fImged,
             '; fParSt: ', strec.fParSt);
     IF strec.fRecCon
          THEN WRITELN('; orecNxtCon:', orecNxtCon:4, '; ointSel: ', ointSel:4)
          ELSE WRITELN('; dlpLim: ', dlpLim);
     END;
END;


PROCEDURE PxOrecCon (orecCon: TC);
VAR recCon, rec: TRec; orec: TC;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
PxOrec(orecCon);
recCon := POINTER(ORD(dsp) + orecCon);
IF NOT recCon^.strec.fRecCon THEN Break(35);
orec := recCon^.orecSon;
WHILE orec <> orecNil DO
     BEGIN
     PxOrec(orec);
     rec := POINTER(ORD(dsp) + orec);
     orec := rec^.orecSib;
     IF rec^.orecSon <> orecNil THEN PxOrecCon(rec^.orecSon);
     END;
END;


PROCEDURE PxDsp;
VAR orec, orecSon: TC; rec: TRec; i: TC;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
WITH dsp^ DO
     WRITELN('dsp: ', ORD(dsp), '; orecLim: ', orecLim:4,
             '; orecFreeFst: ', orecFreeFst:4, '; orecConRoot: ', orecConRoot:4);
orec := dsp^.orecConRoot;
WHILE orec <> orecNil DO
     BEGIN
     PxOrecCon(orec);
     rec := POINTER(ORD(dsp) + orec);
     orec := rec^.orecNxtCon
     END;
WRITELN('Free rec''s:');
orec := dsp^.orecFreeFst;
i := 0;
WHILE orec <> orecNil DO
     BEGIN
     WRITE(orec:4, '; ');
     i := i + 1;
     IF i MOD 8 = 0 THEN WRITELN;
     rec := POINTER(ORD(dsp) + orec);
     orec := rec^.orecSib;
     END;
IF i MOD 8 <> 0 THEN WRITELN;
END;
{$endc}

{$S LWScroll}
FUNCTION OrecPrvCon (orecCon: TC): TC;
LABEL 99;
VAR orecT: TC; rec: TRec;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
orecT := dsp^.orecConRoot;
OrecPrvCon := orecNil;
WHILE orecT <> orecCon DO
     BEGIN
     OrecPrvCon := orecT;
     IF orecT = orecNil THEN GOTO 99;
     rec := POINTER(ORD(dsp)+orecT);
     orecT := rec^.orecNxtCon;
     END;
99:
END;


{$S LWScroll}
PROCEDURE InitRecsFree (orecFst: TC);
VAR orecLst: TC; rec: TRec;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
orecLst := dsp^.orecLim-SIZEOF(TArec);
WHILE orecFst <= orecLst DO
     BEGIN
     rec := POINTER(ORD(dsp)+orecFst);
     rec^.orecSib := dsp^.orecFreeFst;
     dsp^.orecFreeFst := orecFst;
     orecFst := orecFst+SIZEOF(TArec);
     END;
END;


{$S LWCom1}
FUNCTION OrecAlloc;
VAR rec: TRec; orec, orecLimOld: TC; h: TH;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
IF dsp^.orecFreeFst = orecNil THEN
     BEGIN
     orecLimOld := dsp^.orecLim;
     IF orecLimOld >= 32000 - cbRecMore THEN
         BEGIN
         StopAlert(WPAlerts, 21);
         KillLotus;
         END;
     h := @dsp;
     ChangeSizeBk(hzDsp, h, orecLimOld+cbRecMore);
     dsp^.orecLim := orecLimOld+cbRecMore;
     InitRecsFree(orecLimOld);
     END;
orec := dsp^.orecFreeFst;
OrecAlloc := orec;
rec := POINTER(ORD(dsp)+orec);
dsp^.orecFreeFst := rec^.orecSib;
rec^ := arecStd;
IF fTstRec THEN WRITELN('OrecAlloc : ', orec);
END;


{$S LWType}
FUNCTION OrecSibFree;
LABEL 99;
VAR rec, recTmp: TRec; orecT, orecSon: TC;
    ointDep: TC; pint: TPint; fBreak: TF;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
{$ifc fDbgOk}
IF fTstRec THEN BEGIN WRITELN('OrecSibFree(', orec, ') ...'); PxOrec(orec); END;
{$endc}
rec := POINTER(ORD(dsp)+orec);
IF rec^.strec.fRecCon THEN
   IF orec = dsp^.orecConRoot THEN
        dsp^.orecConRoot := rec^.orecNxtCon
   ELSE BEGIN
        orecT := OrecPrvCon(orec);
        recTmp := POINTER(ORD(dsp)+orecT);
        recTmp^.orecNxtCon := rec^.orecNxtCon;
        END;
OrecSibFree := rec^.orecSib;
orecSon := rec^.orecSon;
ointDep := rec^.ointDep;
pint := POINTER(ORD(lldInt)+ointDep);
IF ointDep <> ointNil THEN FreeInt(pint^.ics, ointDep);
rec^.orecSib := dsp^.orecFreeFst;
dsp^.orecFreeFst := orec;
WHILE orecSon <> orecNil DO
     BEGIN
     recTmp := POINTER(ORD(dsp)+orecSon);
     fBreak := recTmp^.strec.fRecCon;
     orecSon := OrecSibFree(orecSon);
     IF fBreak THEN GOTO 99;
     END;
99:
END;


{$S LWSelect}
FUNCTION  Orec1Alloc {(ics: TB; lpFst: TLp; lpLim: TLp; ptTL, ptBR: TPt;
                      tyxyRec: TTyxy; setfixed: TSetfixed; fRecCon: TF;
                      foPrntRec: TFo): TC};
VAR orec, orecT: TC; rec: TRec; ointT:   TC;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
orec := OrecAlloc;
ointT := OintMark(ointNil, ics, lpFst, lpLim, FALSE, ointNil);
rec := POINTER(ORD(dsp)+orec);
WITH rec^ DO BEGIN
    strec.fValid := FALSE;
    strec.setfixed := setfixed;
    r.topLeft := ptTL; r.botRight := ptBR;
    tyxy := tyxyRec;
    strec.fRecCon := fRecCon;
    foPrnt := foPrntRec;
    ointDep := ointT;
    END;
IF fRecCon THEN
   BEGIN
   rec^.ointSel := ointNil;
   rec^.orecNxtCon := orecNil;
   IF dsp^.orecConRoot = orecNil THEN
        dsp^.orecConRoot := orec
   ELSE BEGIN
        orecT := OrecPrvCon(orecNil);
        rec := POINTER(ORD(dsp)+orecT);
        rec^.orecNxtCon := orec
        END;
   END;
Orec1Alloc := orec;
END;

{$S LWScroll}
FUNCTION OrecCreateWindow {(ics: TB; lpFst: TLp; ptTL: TPt; obox: TC): TC };
 { assumes ptTL is tyxyScrn }
VAR orecCon: TC; foPrnt: TFo;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
foPrnt.o := obox; foPrnt.fObox := TRUE;
orecCon := Orec1Alloc(ics, lpFst, lpFst, ptTL, ptTL, tyxyScrn,
                      0, TRUE, foPrnt);
OrecCreateWindow := orecCon;
rghcs^[ics]^^.orecConFst := orecCon;
END;


(* ---
FUNCTION FInRec { (pt:TPt; orec:TC) : TF };
VAR rec: TRec;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
rec := POINTER(ORD(dsp)+orec);
WITH rec^.r DO
    IF (topLeft.v <= pt.v) AND (topLeft.h <= pt.h) THEN
         FInRec := (pt.v < botRight.v) AND (pt.h < botRight.h)
    ELSE FInRec := FALSE ;
END;
--- *)

{$S LWInsert}
FUNCTION FEqRs;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
FEqRs := (r1.topLeft.h = r2.topLeft.h)
        AND (r1.topLeft.v = r2.topLeft.v)
        AND (r1.botRight.h = r2.botRight.h)
        AND (r1.botRight.v = r2.botRight.v);
END;


{$S LWCommand}
FUNCTION OrecOfPt  {(orecFst:TC; fNearest:TF; ptScrn:TPt; fRecurse:TF) : TC } ;
 { assumes ptScrn is a tyxyScrn }
 (* My orecNearest logic assumes no two recs on any sib chain have overlapping
    y-coordinates, i.e. none are side-by-side.  These assumptions allow me to
    check just the y-coordinates of recs to find the nearest one. JRM *)
LABEL 11;
VAR orec, orecResult, orecNearest: TC; rec: TRec; pt: TPt;
    dy, dyNearest: TC;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
IF fTstRec THEN
   WRITE('OrecOfPt - orecFst:',orecFst, ', pt: [', ptScrn.h, ptScrn.v, ']');
orec := orecFst;
orecNearest := orecNil;
dyNearest := MAXINT;
 { assume that a rec's tyxy is always the same as the tyxy of its sib. }
IF orec <> orecNil THEN
     BEGIN
     rec := POINTER(ORD(dsp)+orec);
     pt := ptScrn; ConvertTyxy(pt.h, pt.v, tyxyScrn, rec^.tyxy);
     END;
WHILE orec <> orecNil DO
     BEGIN
     rec := POINTER(ORD(dsp)+orec);
     IF rec^.strec.fSelectable AND (NOT rec^.strec.fRecDep) THEN
        WITH rec^.r DO
            IF (top <= pt.v) AND (pt.v < bottom) THEN
                BEGIN
                IF fNearest THEN
                   BEGIN
                   orecNearest := orec;
                   dyNearest := 0;
                   END;
                IF (left <= pt.h) AND (pt.h < right) THEN
                      BEGIN orecNearest := orec; GOTO 11; END;
                END
            ELSE
                BEGIN
                IF top > pt.v THEN dy := top - pt.v ELSE dy := pt.v - bottom;
                IF dy < dyNearest THEN
                   BEGIN orecNearest := orec; dyNearest := dy; END;
                END;
     orec := rec^.orecSib;
     END;
11:
IF orecNearest <> orecNil THEN
     BEGIN
     rec := POINTER(ORD(dsp)+orecNearest);
     IF fRecurse AND (rec^.orecSon <> orecNil) THEN
          BEGIN
          orecResult := OrecOfPt(rec^.orecSon, fNearest, ptScrn, fRecurse);
          IF orecResult = orecNil THEN orecResult := orecNearest;
          END
     ELSE orecResult := orecNearest;
     END
ELSE orecResult := orecNearest;
IF fTstRec THEN WRITELN(': ',orecResult);
OrecOfPt := orecResult;
END;


FUNCTION OrecOfLp{(orecFst: TC; ics: TB; lp: TLp; fRecurse: TF): TC};
LABEL 11;
VAR orec: TC; rec: TRec; pint: TPint; lpT: TLp;
    recSon : TRec; pintSon: TPint; lpSon: TLp;
    orecSon: TC;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
IF fTstRec THEN WRITE('OrecOfLp - orecFst:',orecFst,', lp:',lp);
orec := orecFst;
rec := POINTER(ORD(dsp)+orec);
WHILE orec <> orecNil DO
    BEGIN
    rec := POINTER(ORD(dsp)+orec);
    pint := POINTER(ORD(lldInt)+rec^.ointDep);
    lpT := pint^.lpFst;
    IF (ics = pint^.ics) AND (lpT <= lp) THEN
        BEGIN
        IF rec^.strec.fRecCon THEN
             lpT := pint^.lpLim
        ELSE lpT := lpT+rec^.dlpLim;
        IF (NOT rec^.strec.fRecDep) AND (lp < lpT) THEN
             BEGIN
             orecSon := rec^.orecSon;
             IF fRecurse AND (orecSon <> orecNil) THEN
                  BEGIN
                  recSon := POINTER(ORD(dsp)+orecSon);
                  pintSon := POINTER(ORD(lldInt)+recSon^.ointDep);
                  IF rec^.strec.fRecCon THEN
                       lpSon := lp
                  ELSE { we will shift streams }
                       lpSon := pintSon^.lpFst + (lp - pint^.lpFst);
                  orec := OrecOfLp(orecSon, pintSon^.ics, lpSon, TRUE);
                  END;
             GOTO 11;
             END;
        END;
    IF rec^.strec.fRecCon THEN orec := orecNil ELSE orec := rec^.orecSib;
    END;
11:
IF fTstRec THEN WRITELN(': ',orec);
OrecOfLp := orec;
END;


(* ---
PROCEDURE EraseOrec;
VAR rec: TRec;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
rec := POINTER(ORD(dsp) + orec);
WITH rec^.r DO SetRectRgn(@argn, topLeft.h, topLeft.v, botRight.h, botRight.v);
SetClipRgn(@argn);
EraseRect(argn.rgnBBox);
END;
--- *)


PROCEDURE ConvertTyxy { (VAR x,y: TC; tyxySrc, tyxyDst: TTyxy) };
VAR dxT, dyT: TL;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
IF tyxySrc = tyxyDst THEN EXIT(ConvertTyxy);
WITH argptInch[tyxyDst] DO
    BEGIN dxT := h; dyT := v; END;
WITH argptInch[tyxySrc] DO
    BEGIN
    IF x < 0 THEN
         x := -((dxT * (-x) + (h DIV 2)) DIV h)
    ELSE x := (dxT * x + (h DIV 2)) DIV h;
    IF y < 0 THEN
         y := -((dyT * (-y) + (v DIV 2)) DIV v)
    ELSE y := (dyT * y + (v DIV 2)) DIV v;
    END;
END;

{This routine truncates instead of rounding, as above}
PROCEDURE CnvrtTruncTyxy (VAR x,y: TC; tyxySrc, tyxyDst: TTyxy);
VAR dxT, dyT: TL;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
IF tyxySrc = tyxyDst THEN EXIT(CnvrtTruncTyxy);
WITH argptInch[tyxyDst] DO
    BEGIN dxT := h; dyT := v; END;
WITH argptInch[tyxySrc] DO
    BEGIN
    IF x < 0 THEN
         x := -((dxT * (-x)) DIV h)
    ELSE x := (dxT * x) DIV h;
    IF y < 0 THEN
         y := -((dyT * (-y)) DIV v)
    ELSE y := (dyT * y) DIV v;
    END;
END;


PROCEDURE ConvertRTyxy {(VAR rSrc, rDst: TR; tyxySrc, tyxyDst: TTyxy)};
{rDst.botRight is really dptWidHt for most of this routine }
{by converting the width-height with truncation we guarantee:
     A) size of rectangle is independent of position
     B) bounding boxes always remain a minimum size, hence as long as the
        fractional pixel contains white space movements and erasures will work
        correctly
}
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
rDst := rSrc;
SubPt(rDst.topLeft, rDst.botRight);
ConvertTyxy(rDst.left, rDst.top, tyxySrc, tyxyDst);
CnvrtTruncTyxy(rDst.right, rDst.bottom, tyxySrc, tyxyDst);
AddPt(rDst.topLeft, rDst.botRight);
END;


{$S LWCreate}
PROCEDURE InitRec;
VAR hT: TH; orec, orecLim: TC; irec: TC; rec: TRec;
BEGIN
{$IFC sampcalls}  ALogCall;  {$ENDC}
hzDsp := hz;
IF fTstRec THEN WRITELN('InitRec');
foNil.fObox := TRUE; foNil.o := oboxNil;
cbRecMore := crecMore*SIZEOF(TArec);
arecStd.orecSib := orecNil; arecStd.orecSon := orecNil;
arecStd.ointDep := ointNil; arecStd.strec.fValid := TRUE;
arecStd.strec.fRecCon := FALSE;
arecStd.strec.fRecDep := FALSE;
arecStd.strec.fImged := FALSE;
arecStd.strec.fKeep := FALSE;
arecStd.strec.fParSt := FALSE;
arecStd.strec.fSelectable := TRUE;
arecStd.strec.fScrollH := TRUE; arecStd.strec.fScrollV := TRUE;
arecStd.foPrnt := foNil;
hT := @dsp;
orecLim := SIZEOF(TAdsp)+irecLim*SIZEOF(TArec);
AllocBk(hzDsp, hT, orecLim, tybkStd);
dsp^.orecLim := orecLim;
dsp^.orecFreeFst := orecNil;
InitRecsFree(SIZEOF(TAdsp));
dsp^.orecConRoot := orecNil;

argptInch[tyxyScrn].h := 100;
argptInch[tyxyScrn].v := 72;
argptInch[tyxyIP].h := 120;
argptInch[tyxyIP].v := 48;
argptInch[tyxyPgFract].h := prPgFract;
argptInch[tyxyPgFract].v := prPgFract;
argptInch[tyxyMica].h := 2540;
argptInch[tyxyMica].v := 2540;
argptInch[tyxyDotMx].h := 96;
argptInch[tyxyDotMx].v := 72;
argptInch[tyxyHiDotMx].h := 160;
argptInch[tyxyHiDotMx].v := 144;
{ these landscape metrics remain equal to portrait metrics
  until someone decides what is right }
argptInch[tyxyLDotMx].h := 96;
argptInch[tyxyLDotMx].v := 72;
argptInch[tyxyLHiDotMx].h := 160;
argptInch[tyxyLHiDotMx].v := 144;

END;


END.


ÿ