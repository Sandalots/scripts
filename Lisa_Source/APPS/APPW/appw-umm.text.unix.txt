{------------------------------------------------------------------------------------------------------------}
{                  Preferences Window Configurable Driver Tree and list structures                            }
{------------------------------------------------------------------------------------------------------------}

UNIT UMM;

INTERFACE
USES
                    UnitStd,
                    UnitHz,
                    Storage,
    {$U libos/Syscall}    Syscall,
    {$U UClascal.obj}     UClascal;

{$SETC CFGDEBUG = FDBGOK}  {FDBGOK}
{$SETC CFGSYMBOLS = FSYMOK}

TYPE
    s255 = STRING[255];
    pS255 = ^s255;

    TObject = SUBCLASS OF NIL

      {Creation and Destruction}
        FUNCTION  TObject.Create(heap: THz): TObject; ABSTRACT;
        FUNCTION  TObject.Heap: THz;
        PROCEDURE TObject.FreeObject;               {frees just the object, not its contents}
        PROCEDURE TObject.Free;                     {frees the object and its contents}
        FUNCTION  TObject.Clone(itsHeap: THz): TObject;
{$IFC CFGDEBUG}
        PROCEDURE TObject.Print;
{$ENDC}
        END;


    TDynamicArray = SUBCLASS OF TObject

      {Variables}
        size:           INTEGER;
        recordBytes:    INTEGER;
        ch: PACKED ARRAY [1..1] OF CHAR;                            {Silence $R+ if apps use ch}

      {Creation and Destruction}
        FUNCTION  {TDynamicArray.}Create(obj: TObject; itsHeap: THz; initialSize: LONGINT): TDynamicArray;

      {Attributes}
        PROCEDURE {TDynamicArray.}BeSize(numElements: LONGINT);

      {Inspect elements}
        PROCEDURE {TDynamicArray.}At(i: LONGINT; pElement: Ptr);
        PROCEDURE {TDynamicArray.}First(pElement: Ptr);
        PROCEDURE {TDynamicArray.}Last(pElement: Ptr);

      {Text}
        PROCEDURE TDynamicArray.AppendText(newText: TDynamicArray);
        PROCEDURE TDynamicArray.AppendNumber(digit: INTEGER);
        PROCEDURE TDynamicArray.AppendWithParens(newText: TDynamicArray);

      {Delete elements}
        PROCEDURE {TDynamicArray.}DelAt(i: LONGINT);
        PROCEDURE {TDynamicArray.}DelManyAt(i: LONGINT; howMany: LONGINT);

        END;

    TIndexList = SUBCLASS OF TObject

      {Variables}
        size:           INTEGER;
        handleBytes:    INTEGER;
        elements:       ARRAY [1..1] OF TObject;

      {Creation and Destruction}
        FUNCTION  TIndexList.Create(obj: TObject; itsHeap: THz): TIndexList;
        PROCEDURE TIndexList.Free;  OVERRIDE;
        PROCEDURE TIndexList.BeSize(numElements: LONGINT);

      {Inspect elements}
        FUNCTION  TIndexList.At(i: LONGINT): TObject;
        FUNCTION  TIndexList.First: TObject;
        FUNCTION  TIndexList.Last: TObject;

        PROCEDURE TIndexList.InsAt(i: LONGINT; x: TObject);
        PROCEDURE TIndexList.InsLast(x: TObject);

      {Delete elements}
        PROCEDURE TIndexList.DelAll(fFree: BOOLEAN);
        PROCEDURE TIndexList.DelAt(i: LONGINT; fFree: BOOLEAN);
        PROCEDURE TIndexList.DelManyAt(i: LONGINT; howMany: LONGINT; fFree: BOOLEAN);
        PROCEDURE TIndexList.DelObject(x: TObject; free: BOOLEAN);

        END;

    TNamedObject = SUBCLASS OF TObject
        name:   TDynamicArray;
        ID:     LONGINT;

        FUNCTION  TNamedObject.Create(obj: TObject; itsHeap: THz; itsName: TDynamicArray; itsID: LONGINT): TNamedObject;

        END;

    TNamedList = SUBCLASS OF TIndexList

      {Creation and Destruction}
        FUNCTION  TNamedList.Create(obj: TObject; itsHeap: THz): TNamedList;
        FUNCTION  TNamedList.HasName(target: e_name): TNamedObject;
        FUNCTION  TNamedList.HasTextAsName(target: TDynamicArray): TNamedObject;
        FUNCTION  TNamedList.HasID(target: LONGINT): TNamedObject;
        END;

    TPrefPort = SUBCLASS OF TNamedObject
        connectedDevice:    TPrefPort;
        position:           cd_position;
        isDetached:         BOOLEAN;

        FUNCTION  TPrefPort.Create(obj: TObject; itsHeap: THz; itsName: TDynamicArray; itsId: INTEGER): TPrefPort;
        PROCEDURE TPrefPort.BuildMenu(VAR error: INTEGER);    ABSTRACT;
        PROCEDURE TPrefPort.BuildInstallDeviceMenu(VAR error: INTEGER); ABSTRACT;
    END;




    TPLint = ^LONGINT;
    THLint = ^TPLint;
    TClass = THLint;

VAR
    cDynamicArray:  TClass;
    cIndexList:     TClass;

    nullPosition:   cd_position;
    nilList:        TNamedList;

    StaticHeap: THz;
    DynamicHeap: THz;


FUNCTION  CreateTextFromString (heap: Thz; sp: pS255): TDynamicArray;
PROCEDURE CreateStringFromText (text: TDynamicArray; VAR sp: e_name);

PROCEDURE Concat2 (source1, source2: pS255; VAR result: pS255);

FUNCTION  EqualText(x,y: TDynamicArray): BOOLEAN;
PROCEDURE Free(x: TObject);
FUNCTION  NewObject(itsHeap: THz; itsClass: TClass; nBytes: INTEGER): TObject;
FUNCTION  Prompt(str: s255): BOOLEAN;
PROCEDURE ABCBreak(s: s255; errCode: LONGINT);


IMPLEMENTATION
{$R-}

{$IFC CFGSYMBOLS}
   {$D+}
{$ELSEC}
   {$D-}
{$ENDC}

{--------------------------------------------------------------------------------------------------------}
PROCEDURE XferLeft(source, dest: Ptr; nBytes: INTEGER); EXTERNAL;
PROCEDURE XferRight(source, dest: Ptr; nBytes: INTEGER); EXTERNAL;
FUNCTION  EqualBytes(source, dest: Ptr; nBytes: INTEGER): BOOLEAN; EXTERNAL;
FUNCTION  EqualChar(source, dest: Ptr; nBytes: INTEGER): BOOLEAN; EXTERNAL;
PROCEDURE EnterLisaBug; EXTERNAL;
{--------------------------------------------------------------------------------------------------------}

FUNCTION  CreateTextFromString {(heap: Thz; sp: pS255): TDynamicArray};
{function: Return a handle to a block containing the string sp. }
VAR
text: TDynamicArray;
BEGIN
   text := TDynamicArray.Create(NIL, heap, ORD(sp^[0])+1);
   XferLeft(POINTER(ORD(sp)), @text.ch, ORD(sp^[0])+1);
   CreateTextFromString := text;
END;

{--------------------------------------------------------------------------------------------------------}

PROCEDURE CreateStringFromText (text: TDynamicArray; VAR sp: e_name);
BEGIN
    XferLeft(@text.ch, @sp, text.size);
END;

{--------------------------------------------------------------------------------------------------------}

PROCEDURE Concat2 (source1, source2: pS255; VAR result: pS255);
VAR firstSize:  INTEGER;
BEGIN
    (*
    WRITELN('Entering concat2, source 1 is ',source1^,' source2 is ',source2^);
    firstSize := ORD(source1^[0]) + 1;
    XferLeft(source1, result, firstSize);
    XferLeft(POINTER(ORD(source2) + 1), POINTER(ORD(result) + firstSize), source1^[0]);
    SELF.ch[1] := CHR(firstSize + source1^[0]);
    WRITELN('Leaving concat2 the result is ',result^);
    *)
END;

{--------------------------------------------------------------------------------------------------------}

PROCEDURE Concat3 (source1, source2, source3: pS255; VAR result: pS255);
BEGIN

END;

{--------------------------------------------------------------------------------------------------------}

FUNCTION  EqualText(x,y: TDynamicArray): BOOLEAN;
BEGIN
    EqualText := FALSE;
    IF x.size = y.size THEN
        IF EqualChar(@x.ch, @y.ch, x.size) THEN
            EqualText := TRUE;
END;

{--------------------------------------------------------------------------------------------------------}

FUNCTION Prompt{(str: s255): BOOLEAN};
VAR debug: CHAR;
BEGIN
    write(str);
    read(debug);
    writeln;
    if (debug = 'y') or (debug = 'Y') then
        Prompt := true
    ELSE
        Prompt := false;
END;

{--------------------------------------------------------------------------------------------------------}

PROCEDURE ABCBreak{(s: s255; errCode: LONGINT)};
BEGIN
    WriteLn;
    Write(CHR(7), s);       {Beep}
    IF errCode <> 0 THEN
        Write(': ', errCode:1);
    WriteLn;
    EnterLisaBug;
END;


PROCEDURE Free{(x: TObject)};
BEGIN
    IF x <> NIL THEN
        x.Free;
END;

{--------------------------------------------------------------------------------------------------------}
FUNCTION  NewObject{(itsHeap: THz; itsClass: TClass; nBytes: INTEGER): TObject};
    VAR obj: TObject;
{--------------------------------------------------------------------------------------------------------}
BEGIN
    obj := POINTER(ORD(HAllocate(POINTER(ORD(itsHeap)), nBytes)));
    THLint(obj)^^ := ORD(itsClass);
    IF ORD(obj) = ORD(hNIL) THEN
       ABCBreak('NewObject: Heap full, can''t make an object of size', nBytes);
    NewObject := obj;
END;


PROCEDURE ClascalError(error: INTEGER);
BEGIN
     IF error > 0 THEN
          IF classesInitialized THEN
               Halt     {*** Fix this *** }
          ELSE
               Halt;   { and this }
END;

METHODS OF TObject;

    {----------------------------------------------------------------------------------------------------}
    PROCEDURE {TObject.}Free;
    {----------------------------------------------------------------------------------------------------}
    BEGIN
        SELF.FreeObject;
    END;


    {----------------------------------------------------------------------------------------------------}
    PROCEDURE {TObject.}FreeObject;
    {----------------------------------------------------------------------------------------------------}
        VAR heap:       THz;
            numObjects: INTEGER;
    BEGIN
        heap := SELF.Heap;
        FreeH(POINTER(ORD(heap)), POINTER(ORD(SELF)));
    END;


    {----------------------------------------------------------------------------------------------------}
    FUNCTION  {TObject.}Heap{: THz};
    {----------------------------------------------------------------------------------------------------}
    BEGIN
        Heap := POINTER(ORD(HzFromH(POINTER(ORD(SELF)))));
    END;

    FUNCTION  TObject.Clone(itsHeap: THz): TObject;
    VAR Hz:     THz;
        size:   INTEGER;
        source: TH;
        dest:   TH;
    BEGIN
        hz := POINTER(ORD(itsHeap));
        source := POINTER(ORD(SELF));
        size := CbDataOfH(itsHeap, source);
        dest := HAllocate(itsHeap, size);
        XferLeft(@source^^, @dest^^, size);
        Clone := POINTER(ORD(dest));
    END;

{$IFC CFGDEBUG}
    {----------------------------------------------------------------------------------------------------}
    PROCEDURE  {TObject.}Print;
    {----------------------------------------------------------------------------------------------------}
    BEGIN
        WRITELN('Print not implemented in this class');
    END;
{$ENDC}

BEGIN
IF pleaseInitClascal THEN
     InitClascal(ClascalError);
END;


PROCEDURE ResizeIndexList(l: TIndexList; deltaSize: LONGINT);
    VAR hz: THz;
        h: TH;
        newBytes: LONGINT;
BEGIN
    h := POINTER(ORD(l));
    hz := HzFromH(h);
    WITH l DO
        BEGIN
        size := size + deltaSize;
        newBytes := SIZEOF(TIndexList) + (handleBytes * size);  { handleBytes bytes per handle }
        END;
    IF (newBytes < 0) OR (newBytes > (MAXINT-20)) THEN
        ABCBreak('Create size must lie between 0 and 32K-20, not', newBytes);
    ChangeSizeH(hz, h, newBytes);
    IF CbDataOfH(hz, h) < newBytes THEN
        BEGIN
        WriteLn;
        WriteLn('*** ERROR: Unable to grow by ', deltaSize:1, ' to ', l.size:1);
        ABCBreak('ResizeIndexList', 0);
        END;
END;


PROCEDURE InsDynamicArray(l: TIndexList; i, howMany: LONGINT);
BEGIN
    ResizeIndexList(l, howMany);
    WITH l DO
{$H-}   XferRight(POINTER(ORD(@elements) + (i-1)*handleBytes),
                  POINTER(ORD(@elements) + (i-1+howMany)*handleBytes),
                  (size-(i-1+howMany)) * handleBytes); {$H+}
END;


PROCEDURE DelDynamicArray(l: TIndexList; i, howMany: LONGINT);
BEGIN
    WITH l DO
{$H-}   XferLeft(POINTER(ORD(@elements) + (i-1+howMany)*handleBytes),
                  POINTER(ORD(@elements) + (i-1)*handleBytes),
                  (size-(i-1+howMany)) * l.handleBytes); {$H+}
    ResizeIndexList(l, -howMany);
END;


METHODS OF TDynamicArray;


    FUNCTION {TDynamicArray.}Create{(obj: TObject; itsHeap: THz; initialSize: INTEGER): TDynamicArray};
    BEGIN
        IF obj = NIL THEN
            obj := NewObject(itsHeap, ThisClass, SIZEOF(TDynamicArray) + initialSize);
        SELF := TDynamicArray(obj);
        WITH SELF DO
            BEGIN
            size := initialSize;
            recordBytes := 1;
            END;
    END;

{$IFC CFGDEBUG}
    PROCEDURE TDynamicArray.Print;
    VAR ptrStr: ^e_name;
    BEGIN
        ptrStr := @SELF.ch;
        WRITELN(ptrStr^);
    END;
{$ENDC}

    PROCEDURE {TDynamicArray.}At{(i: LONGINT; pElement: Ptr)};
    BEGIN
        WITH SELF DO
  {$H-}     XferLeft(POINTER(ORD(@ch) + (i-1)*recordBytes), pElement, recordBytes); {$H+}
    END;


    PROCEDURE {TDynamicArray.}BeSize{(numElements: LONGINT)};
    BEGIN
        ResizeIndexList(POINTER(ORD(SELF)), numElements - SELF.size);
    END;


    PROCEDURE {TDynamicArray.}DelAt{(i: LONGINT)};
    BEGIN
        DelDynamicArray(POINTER(ORD(SELF)), i, 1);
    END;


    PROCEDURE {TDynamicArray.}DelManyAt{(i: LONGINT; howMany: LONGINT)};
    BEGIN
        DelDynamicArray(POINTER(ORD(SELF)), i, howMany);
    END;


    PROCEDURE {TDynamicArray.}First{(pElement: Ptr)};
    BEGIN
        WITH SELF DO
  {$H-}     XferLeft(@ch, pElement, recordBytes); {$H+}
    END;


    PROCEDURE {TDynamicArray.}Last{(pElement: Ptr)};
    BEGIN
        WITH SELF DO
  {$H-}     XferLeft(POINTER(ORD(@ch) + (size-1)*recordBytes), pElement, recordBytes); {$H+}
    END;

    PROCEDURE TDynamicArray.AppendText(newText: TDynamicArray);
    VAR origSize:    INTEGER;
    BEGIN
        origSize := SELF.size;
        SELF.BeSize(origSize + newText.size);
        XferLeft(@newText.ch, POINTER(ORD(@SELF.ch) + origSize), newText.Size);
        SELF.ch[1] := CHR(origSize + newText.size - 1);
        SELF.ch[origSize + 1] := ' ';
    END;

    PROCEDURE TDynamicArray.AppendNumber(digit: INTEGER);
    VAR origSize:   INTEGER;
    BEGIN
        origSize := SELF.size;
        SELF.BeSize(origSize + 2);
        SELF.ch[origSize + 1] := ' ';
        SELF.ch[origSize + 2] := CHR(ORD('0') + digit);
        SELF.ch[1] := CHR(origSize + 1);
    END;

    PROCEDURE TDynamicArray.AppendWithParens(newText: TDynamicArray);
    VAR origSize:   INTEGER;
        newSize:    INTEGER;
    BEGIN
        origSize := SELF.size;
        newSize := newText.size;
        SELF.BeSize(origSize + newSize + 2);
        XferLeft(@newText.ch, POINTER(ORD(@SELF.ch) + origSize + 1), newSize);
        SELF.ch[origSize + 1] := ' ';
        SELF.ch[origSize + 2] := '(';
        SELF.ch[SELF.size] := ')';
        SELF.ch[1] := CHR(origSize + newText.size + 1);
    END;

BEGIN
    cDynamicArray := ThisClass;
END;


METHODS OF TIndexList;


    FUNCTION TIndexList.Create(obj: TObject; itsHeap: THz): TIndexList;
    BEGIN
        IF obj = NIL THEN
            obj := NewObject(itsHeap, ThisClass, SIZEOF(SELF));
        SELF := TIndexList(obj);
        SELF.size := 0;
        SELF.handleBytes := 4;
    END;


    PROCEDURE TIndexList.Free;
        VAR j: INTEGER;
    BEGIN
        FOR j := 1 TO SELF.size DO
            Free(SELF.elements[j]);
        SELF.FreeObject;
    END;


    FUNCTION TIndexList.At(i: LONGINT): TObject;
    BEGIN
        At := SELF.elements[i];
    END;


    PROCEDURE TIndexList.BeSize(numElements: LONGINT);
    BEGIN
        ResizeIndexList(SELF, numElements - SELF.size);
    END;

    PROCEDURE TIndexList.DelAt(i: LONGINT; fFree: BOOLEAN);
    BEGIN
        IF fFree THEN
            Free(SELF.elements[i]);
        DelDynamicArray(SELF, i, 1);
    END;

    PROCEDURE TIndexList.DelAll(fFree: BOOLEAN);
    VAR i:  INTEGER;
    BEGIN
        IF SELF.size > 0 THEN
            BEGIN
            IF fFree THEN
                FOR i := 1 TO SELF.size DO
                    Free(SELF.elements[i]);
            DelDynamicArray(SELF, 1, SELF.size);
            END;
    END;

    PROCEDURE TIndexList.DelObject(x: TObject; free: BOOLEAN);
    VAR i:  INTEGER;
    BEGIN
        FOR i := 1 TO SELF.size DO
            IF x = SELF.elements[i] THEN
                BEGIN
                SELF.DelAt(i, free);
                EXIT(DelObject);
                END;
    END;

    PROCEDURE TIndexList.DelManyAt(i: LONGINT;
                                      howMany: LONGINT; fFree: BOOLEAN);
        VAR j: INTEGER;
    BEGIN
        IF fFree THEN
            FOR j := i TO i+howMany-1 DO
                Free(SELF.elements[j]);
        DelDynamicArray(SELF, i, howMany);
    END;

    FUNCTION TIndexList.First: TObject;
    BEGIN
        First := SELF.elements[1];
    END;

    PROCEDURE TIndexList.InsAt(i: LONGINT; x: TObject);
    BEGIN
        InsDynamicArray(SELF, i, 1);
        SELF.elements[i] := x;
    END;

    PROCEDURE TIndexList.InsLast(x: TObject);
    BEGIN
        SELF.InsAt(SELF.size + 1, x);
    END;

    FUNCTION TIndexList.Last: TObject;
    BEGIN
        Last := SELF.elements[SELF.size];
    END;

{$IFC CFGDEBUG}
    PROCEDURE TIndexList.Print;
    VAR   i:   INTEGER;

    BEGIN
        FOR i := 1 TO SELF.size DO
            SELF.elements[i].Print;
    END;
{$ENDC}

BEGIN
    cIndexList := ThisClass;
END;

METHODS OF TNamedObject

    FUNCTION TNamedObject.Create(obj: TObject; itsHeap: THz; itsName: TDynamicArray;
                                                                              itsID: LONGINT): TNamedObject;
    BEGIN
        IF obj = NIL THEN
            obj := NewObject(itsHeap, ThisClass, SIZEOF(SELF));
        SELF := TNamedObject(obj);
        SELF.name := itsName;
        SELF.ID := itsID;
    END;

    PROCEDURE TNamedObject.Free;
    BEGIN
        SELF.name.Free;
        SUPERSELF.Free;
    END;

{$IFC CFGDEBUG}
    PROCEDURE TNamedObject.Print;
    VAR stringPtr:  ^e_name;
    BEGIN
        stringPtr := @SELF.name.ch;
        WRITELN(stringPtr^,'  ID:  ',SELF.id:1);
    END;
{$ENDC}

END;

METHODS OF TNamedList;


    FUNCTION TNamedList.Create(obj: TObject; itsHeap: THz): TNamedList;
    BEGIN
        IF obj = NIL THEN
            obj := NewObject(itsHeap, ThisClass, SIZEOF(SELF));
        SELF := TNamedList(TIndexList.Create(obj, itsHeap));
    END;

    FUNCTION TNamedList.HasName(target: e_name): TNamedObject;
        VAR i:          INTEGER;
            namedObject: TNamedObject;
    BEGIN
        HasName := NIL;
        FOR i := 1 TO SELF.size DO
            BEGIN
            namedObject := TNamedObject(SELF.elements[i]);
            IF LENGTH(target)+1 = namedObject.name.size THEN
                IF EqualChar(@namedObject.name.ch, @target, LENGTH(target)+1) THEN
                    HasName := namedObject;
            END;
    END;

    FUNCTION TNamedList.HasTextAsName(target: TDynamicArray): TNamedObject;
        VAR i:          INTEGER;
            namedObject: TNamedObject;
    BEGIN
        HasTextAsName := NIL;
        FOR i := 1 TO SELF.size DO
            BEGIN
            namedObject := TNamedObject(SELF.elements[i]);
            IF EqualChar(@namedObject.name.ch, @target.ch, target.size) THEN
                HasTextAsName := namedObject;
            END;
    END;


    FUNCTION TNamedList.HasID(target: LONGINT): TNamedObject;
        VAR i:          INTEGER;
            namedObject: TNamedObject;
    BEGIN
        HasID := NIL;
        FOR i := 1 TO SELF.size DO
            BEGIN
            namedObject := TNamedObject(SELF.elements[i]);
            IF namedObject.id = target THEN
                HasID := namedObject;
            END;
    END;

{$IFC CFGDEBUG}
    PROCEDURE TNamedList.Print;
        VAR i:          INTEGER;
    BEGIN
        FOR i := 1 TO SELF.size DO
            SELF.elements[i].Print;
    END;
{$ENDC}

END;

METHODS OF TPrefPort

    FUNCTION TPrefPort.Create(obj: TObject; itsHeap: THz; itsName: TDynamicArray; itsId: INTEGER): TPrefPort;
    BEGIN
        IF obj = NIL THEN
            obj := NewObject(itsHeap, ThisClass, SIZEOF(SELF));
        SELF := TPrefPort(TNamedObject.Create(obj, itsHeap, itsName, itsID));
        WITH SELF DO
            BEGIN
            connectedDevice := NIL;
            position := nullPosition;
            isDetached := FALSE;
            END;
    END;

    PROCEDURE TPrefPort.Free;
    BEGIN
        Free(SELF.connectedDevice);
        SUPERSELF.Free;
    END;


END;
{$S InitPref}

END.
ÿ