program createbtn;
{ This program reads in the a button file "btnfile", in the format described in
  segment named t11 buttons.
  CreateBtn uses unit check for delarations of the structures it stores the data
  into.
  All terms, structures, and other good stuff, are defined in the Preferences
  Window document. }

USES {$U libos/SysCall  }  SysCall,
     {$U Obj:UnitStd    }  UnitStd,
     {$U Obj:UnitHz     }  UnitHz,

     {$U Obj:quickdraw  }  quickdraw,
     {$U Obj:FontMgr    }  FontMgr,
     {$U Obj:Storage    }  Storage,
     {$U Obj:FMgrUtil   }  FMgrUtil,

     {$U appw/UMM       }  UMM,

     {$U Obj:WM.Events  }  Events,
     {$U obj:WM.Menus   }  Menus,
     {$U Obj:WM.Folders }  Folders,
     {$U Obj:dbenv      }  dbenv,
     {$U appw/check     }  checkbox;

{$D+}

const
   HeapSize = 30000;
   cfgHeapSize = 5000;
   btnheapsize = 3000;
   btnLDSN = 9;
   btnheapldsn = 8;

var
   menu:         TMenu;
   DataHandle:   ^hcheckmenus;
   test:         TH;
   SegName:      PathName;
   btnAddr:      TL;
   Addr:         TL;
   cError:       TC;
   intrefnum,i,j:TC;
   btnrefnum:    TC;
   btnfile:      text;
   segSize:      LONGINT;
   btndbg:       boolean;

   rBtnLst:     Rect;
   excepName:    T_Ex_Name;
   error:       INTEGER;
{$R-}

PROCEDURE ReadBtnFile (var btnfile: Text; form: TForm);
forward;
FUNCTION  MenuRead  (VAR ckFile: Text): TMenu;
forward;
FUNCTION  BtnRead  (VAR ckFile: Text): TButton;
forward;


{ ---------------------------------------------------------------------- }

FUNCTION BtnRead {VAR ckFile: Text): TButton};
{Function: Read from ckFile a button discirptor in the form
 Format: hhh vvv fam BIT Label}

VAR sp:  TSp;
    text: TDynamicArray;
    lx:  TLooks;
    r:   Rect;
    startPoint: Point;
    chRel, chB, chI, chQd, chAct, chTyp, ch: CHAR;
    btn:    TButton;

BEGIN
 { read a button from the file }
    IF btndbg THEN writeln('     entering readbutton');
   btnRead := NIL;
   READ(ckFile, startPoint.h);

        IF startPoint.h = -1 THEN
            READLN(ckFile)
        ELSE
            BEGIN
            READLN (ckFile, startPoint.v, ch, chB, chAct, ch, sp);

            IF sp = '' THEN
               text := emptyText
            ELSE
               text := CreateTextFromString (StaticHeap, @sp);

          { Set the face of the button }
            lx.fB   := (chB   ='B'); {Use 'b' for not bold.}

          { Set the type of the button - label or check box }
            CASE chAct OF
               'I','i': lx.ButtonType := boxlabel;
               'C','c': lx.ButtonType := ckbox;
            END;

            btn := TButton.Create(NIL, StaticHeap, lx, text, startPoint, FALSE);

            btnRead := btn;
            END;
END;

{ ---------------------------------------------------------------------- }


FUNCTION MenuRead {VAR ckFile: Text): TMenu};

{Function: Read from ckFile a menu descriptor in the form
 Format: menu id, count of buttons in this menu, count of ghost button}

VAR idMu:       INTEGER;
    menu:       TMenu;
    btn:        TButton;
    spComment:  Str255;
    dummy:      menuinfo;
    sp:         Tsp;
    staticEnd:  BOOLEAN;

BEGIN
   IF btndbg THEN
      begin
      writeln;
      writeln;
      WRITE ('Entering menu read');
      end;

    READLN (ckFile, idMu, spComment);

    IF btndbg THEN
        WRITELN(': - idmu is',idmu:1);

    MenuRead := NIL;

    IF idMu <> 999 THEN
        BEGIN
    { allocate menu }
        menu := TMenu.Create(NIL, StaticHeap, idMu);

    { Read in the buttons for this menu}
        REPEAT
            btn := BtnRead (ckFile);
            IF btn <> NIL THEN
                menu.InsLast(btn);
        UNTIL btn = NIL;

        menu.staticButtons := menu.btnArray.size;

    {$IFC CFGDEBUG}
        IF btnDbg THEN
            menu.Print;
    {$ENDC}

     { skip the comment }
        READLN (ckFile, spComment);

        MenuRead := menu;
        END;
END;

{ ---------------------------------------------------------------------- }

FUNCTION TextRead (VAR ckFile: Text): TDynamicArray;
VAR sp:     Tsp;
    Text:   TDynamicArray;
BEGIN
    TextRead := NIL;
    READLN (ckFile, sp);
    TextRead := CreateTextFromString(staticHeap, @sp);
END;

{ ---------------------------------------------------------------------- }

PROCEDURE ReadBtnFile {var btnfile: Text; form: TForm};
{function: }

VAR err: integer;
    cAlerts: TC;
    cghostbtn, cMenu: TC;
    i: TB;
    temp: TMenu;
    textObject: TDynamicArray;
    spComment:  Str255;

BEGIN
   if btndbg then
      writeln('entering ReadBtnFile');

{ Set up an empty string to use as the text for all check boxes without text }
    emptyText := TDynamicArray.Create(NIL, staticHeap, 1);
    emptyText.ch[1] := CHR(0);

  { Read in the random text at the top of the button file }
    if btndbg then writeln('Reading the random text');
    sCnctTo := TextRead(btnFile);
    sConnector := TextRead(btnFile);
    FOR i := 1 TO 3 DO
        sSlots[i] := TextRead(btnFile);
    sSerialA := TextRead(btnFile);
    sSerialB := TextRead(btnFile);
    sParallel := TextRead(btnFile);
    sNothing := TextRead(btnFile);
    sWidget := TextRead(btnFile);
    sSony := TextRead(btnFile);
    sBuiltIn := TextRead(btnFile);
    sInUse := TextRead(btnFile);
    sUnknown := TextRead(btnFile);
    sDetached := TextRead(btnFile);
    sLower := TextRead(btnFile);
    sMiddle := TextRead(btnFile);
    sUpper := TextRead(btnFile);

  { skip the blank line }
    READLN (btnfile, spComment);

{ Allocate the menu list that holds all the menus }
   menuMaster := TMenuList.Create(NIL, StaticHeap);

{ Read in all the menus from button file }
   if btndbg then writeln('reading in master form');
   REPEAT
   { this little reassignment thingie is here because it works this way }
      temp := MenuRead(btnfile);
      IF temp <> NIL THEN
        menuMaster.InsLast(temp);
   UNTIL temp = NIL;

   if btndbg then
      writeln('finished reading menus');

{ initialize the empty form }
   temp := TMenu(menuMaster.first);
   form.menuArray.InsLast(temp);  { Put root menu in form }

   if btndbg then
      writeln('exititng ReadBtnFile');
END;

{ ---------------------------------------------------------------------- }

FUNCTION Heap1Error(hz: Thz; bytesNeeded: TC): TC;
   VAR expansion,
       newdiscsize,
       newmemsize : longint;
       status : integer;
       dsinfo: dsinforec;

    BEGIN
    WRITELN('The button heap is full.-- Expanding   ');

    IF bytesneeded < 2*512 then expansion := 2*512
    else expansion := bytesneeded;

    Info_dataseg(Status,btnRefNum,dsinfo);
    size_dataseg(status,btnRefNum,expansion,newmemsize,expansion,newdiscsize);
    if status > 0 then
       BEGIN
       WRITELN('Expansion Failed  Status = ', status);
       Heap1Error := 0;
       END

    ELSE
       BEGIN
       if status < 0 then writeln('HeapError warning ',status);
       Heap1Error := newmemsize - dsinfo.mem_size;
       WRITELN('button read Tool heap expanded to ',newmemsize);
       END;
    END;


FUNCTION Heap2Error(hz: Thz; bytesNeeded: TC): TC;
   VAR expansion,
       newdiscsize,
       newmemsize : longint;
       status : integer;
       dsinfo: dsinforec;

    BEGIN
    writeln('the internal heap is full. -- Expanding ');

    IF bytesneeded < 2*512 then expansion := 2*512
    else expansion := bytesneeded;

    Info_dataseg(Status,intrefnum,dsinfo);
    size_dataseg(status,intrefnum,expansion,newmemsize,expansion,newdiscsize);
    if status > 0 then
       BEGIN
       WRITELN('Expansion Failed  Status = ', status);
       Heap2Error := 0;
       END

    ELSE
       BEGIN
       if status < 0 then writeln('HeapError warning ',status);
       Heap2Error := newmemsize - dsinfo.mem_size;
       WRITELN('button read Tool heap expanded to ',newmemsize);
       END;
    END;


PROCEDURE LastWishes(environPtr : P_env_blk; excepDataPtr : P_ex_data);
VAR error:  INTEGER;
BEGIN
WRITELN;
Writeln('The Preferences Window Button Pre-Processor is comming down');
END;

begin
writeln('Welcome to the ALL NEW Preferences Window Button Pre-Processor');

{ do the basic init stuff }
InitGraf(@theport);
OpenWM;
btndbg := false;

   excepName := 'SYS_TERMINATE';
   Declare_Excep_Hdl (error, excepName, @LastWishes);

{ Create data segment and heap for buttons }
Segname := '{T11}buttons';
Kill_DataSeg (cError, SegName);
if cError > 0 then
   writeln('cant kill T11 - error is: ', cError);
segSize := heapsize + cfgheapsize + 100;
Make_DataSeg(cError, SegName, segSize, segSize, btnRefNum,btnAddr, btnLDSN, ds_shared);
if cError > 0 then
    BEGIN
    writeln('were in trouble making data segment, tried to make size ', segsize,' - error is: ', cerror);
    HALT;
    END;

{ make the heap }
StaticHeap := hzinit (pointer(btnAddr + 12),pointer(btnAddr + heapsize),
                                                pointer(btnAddr + 4),100,0,@heap1error,nil,nil,nil);

setheap(StaticHeap);

{ save off address of space at beginning of segment }
datahandle := pointer(ord(btnAddr));


{ create data segment and heap for internal use }
Segname := 'StaticHeap';
Open_DataSeg (cError, SegName, intRefnum, Addr, btnheapLDSN);
if cError <= 0 then
   SetAccess_DataSeg(cError, intRefNum, False)  {Access R/W}
else
   begin
   Kill_DataSeg (cError, SegName);
   Make_DataSeg(cError, SegName,btnheapsize, btnheapsize,intRefNum,Addr, btnheapLDSN, ds_shared);
   if cError > 0 then
      writeln('were in trouble on internal heap error is ', cerror);
   end;

InitHeap(pointer(addr),pointer(addr + btnheapsize),@heap2error);

{ open the button file }
RESET(btnfile,'appw/btnfile.text');
i := IOResult;
IF i>0 THEN
   BEGIN
   WRITELN('cant find btnfile - error is: ',i:1);
   HALT;
   END;

{ Allocate a Check Form }
mainForm := TForm.Create(NIL, StaticHeap);

{ Allocate the record that hold the handles }
handleHolder := POINTER(ORD(NewObject(StaticHeap, NIL, SIZEOF(tcheckmenus))));

btndbg := Prompt('Do you want to watch the buttons go by?');

{$IFC CFGDEBUG}
CheckDebug(btnDbg);
{$ENDC}

{ read the buttons }
readbtnfile (btnfile, mainForm);

StuffHandles;

{ write to the data segment }
    datahandle^ := handleHolder;

{********************************************************}

{$IFC CFGDEBUG}
IF Prompt('Do you want to check the buttons?') THEN
    BEGIN
{ retrieve the handles }
    datahandle := pointer(ord(btnAddr));
    handleHolder := datahandle^;

{ Stuff the handles from button file }
    with handleHolder^^ do
        begin
        mainForm := stuffform;
        menuMaster := stuffMenu;
        end;

    PrintRandomText;

    writeln('**** printing master menu ****');
    menuMaster.Print;
    writeln('*** printing form ****');
    mainForm.Print;
    END;
{$ENDC}
end.
ÿ