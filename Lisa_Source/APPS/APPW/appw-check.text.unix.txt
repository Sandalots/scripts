UNIT CheckBox;

{ This unit provides the primitives to select, modify, and draw buttons and menus.
}

INTERFACE

USES
     {$U libos/SysCall  }  SysCall,
     {$U Obj:UnitStd    }  UnitStd,
     {$U Obj:UnitHz     }  UnitHz,

     {$U Obj:quickdraw  }  quickdraw,
     {$U Obj:FontMgr    }  FontMgr,
     {$U Obj:Storage    }  Storage,
     {$U Obj:FMgrUtil   }  FMgrUtil,

     {$U appw/UMM       }  UMM,
     {$U libpm/PmDecl   }  PmDecl,
     {$U PrStdInfo      }  PrStdInfo,

     {$U Obj:WM.Events  }  Events,
     {$U obj:WM.Menus   }  Menus,
     {$U Obj:WM.Folders }  Folders,
     {$U Obj:WmlStd     }  WmlStd,
     {$U Obj:WmlCrs     }  WmlCrs,
     {$U obj:Wmlsb      }  Wmlsb;


{ ---------------------------------------------------------------------- }

CONST

   BtnNil = -1;                { nil button }

   dhMarg  = 6; dvMarg  = 4;   { standard hortizontal and vertical spacing }
   buttonSpace = 12;           { Minimum space between buttons }
   dv = 10;                    { Standard button height }

 {---------------------- Menus -------------------------}
 { These constants correspond to check form menus in the menu file }

{ The main check box menu and it's buttons }
    main = 0;
    btnConven = 1;
    btnDflt = 2;
    btnAttach = 3;
    btnInstall = 4;
    btnRemove = 5;

    cNothingBtn = 2;


{ The menus that form the Convenience Setting screen }
   muDfltConven = 1;                     { Default menu }
   NrmCont = 2;   NrmContBtns = 19;   { Normal Contrast menu has 19 buttons }
   DmCont = 3;    DimContBtns = 18;   { Dim Contrast menu has 18 buttons }
   Bpvol = 4;                      { Beep Volume }
   FdDelay = 5;                    { Fade Delay }
   Bgnrepeat = 6;                  { Initial Repeat Delay }
   Sbrepeat = 7;                   { Subsequent Repeat Delay }
   DblClick = 8;                   { Double Click Delay }

{  The device configuration menus }
   cRootMenu = 16;                { The root device configuratio menu }
   cCnctToSlot = 9;              { A list of cards for a slot }
   cInstall = 10;               { A list of cards to Install }
   cCardMenu = 11;              { A list of connectors of a card and what is currently attached to them }
   cCncToConnector = 12;             { A list of devices that can go on a connector }
   cDefaultPrinter = 14;            { list of printers }
   cBootFrom = 15;                  { list of bootable disks }
   cRemoveDriver = 18;              { list of all drivers to remove }
   cTestMemory = 17;

TYPE

   Tbtntype  = (boxlabel, ckbox); { there are two types of buttons }
                                  { 1)  a check box with a label }
                                  { 2)  just a label }

   Tlooks = PACKED RECORD    { this record should be inline in the  button record }
      fB:    TF;     { true => bolding }
      buttonType:  Tbtntype;  { button type }
   END;

    TButton  = SUBCLASS OF TObject   { a button record }
        boundr: Rect;           { Rectangle of bounding box of check box }
        btnr:   Rect;           { Rectangle of visual box in screen }
        looks:  Tlooks;         { face and font information }
        text:   TDynamicArray;  { name }

        FUNCTION  TButton.Create(obj: TObject; itsHeap: THz; itsLooks: Tlooks;
                                        text: TDynamicArray; VAR startPoint: Point; doWrap: BOOLEAN): TButton;

      { Drawing }
        PROCEDURE TButton.FillButton  (scrnLp: INTEGER; fill: TF);
        PROCEDURE TButton.Bindlooks;
        PROCEDURE TButton.Draw(ScrnLp: INTEGER);
        PROCEDURE TButton.Erase(ScrnLp: INTEGER);

      { Positioning }
        PROCEDURE TButton.ComputeBoundingBox;
        PROCEDURE TButton.TextStartPos  (VAR ptSp: Point; VAR dhSp: INTEGER);

      { Name Changing }
        PROCEDURE TButton.AppendText(newText: TDynamicArray);
        PROCEDURE TButton.MakeString(newText: e_name);
        PROCEDURE TButton.MakeText(newText: TDynamicArray);
        PROCEDURE TButton.AppendNumber(digit: INTEGER);
        PROCEDURE TButton.AppendDash;
        PROCEDURE TButton.AppendWithParens(newText: TDynamicArray);

    END;

    TDriverButton  = SUBCLASS OF TButton   { a button record }
        driver: TObject;

        FUNCTION  TDriverButton.Create(obj: TObject; itsHeap: THz; itsLooks: Tlooks; text: TDynamicArray;
                                 VAR startPoint: Point; doWrap: BOOLEAN; itsDriver: TObject): TDriverButton;
        END;

    TMenu        = SUBCLASS OF TObject    { a menu record }
        port:       TPrefPort;  { The port that this menu is displaying - if we are in the configuration screen }
        MuLp:       TC;     { the horizontal position of this menu on the screen }
        idMu:       TB;     { menu id. Read from button file }
        iBtn:       TB;     { the selected button in this menu, may be btnnil }
        oldBtn:     TB;     { The last button that was selected for restoration }
        boundRect:  Rect;   { the bounding rectangle of all the buttons }
        staticButtons: INTEGER;
        btnArray:   TIndexList;  { array of buttons }

        FUNCTION  TMenu.Create(obj: TObject; itsHeap: THz; Id: INTEGER): TMenu;
        PROCEDURE TMenu.ChangeButton(newText: TDynamicArray; newName: pS255);
        PROCEDURE TMenu.Draw;
        PROCEDURE TMenu.DelManyAt(i: LONGINT; howMany: LONGINT; fFree: BOOLEAN);
        PROCEDURE TMenu.Hit(iBtnNew: INTEGER);
        PROCEDURE TMenu.InsLast(btn: TButton);
        PROCEDURE TMenu.RestoreButton;
   END;

    TMenuList      = SUBCLASS OF TIndexList

        FUNCTION  TMenuList.Create(obj: TObject; itsHeap: THz): TMenuList;
        FUNCTION  TMenuList.GetMenu(id: INTEGER): TMenu;
        FUNCTION  TMenuList.GetIndex(menu: TMenu): INTEGER;
    END;

    TForm    = SUBCLASS OF TObject    { form record }
        iMuKey:     TMenu;        { selected menu }
        iBtnKey:    TB;           { selected button }
        menuArray:  TMenuList;    { array of handles to menus }

        FUNCTION  TForm.Create(obj: TObject; itsHeap: THz): TForm;
        PROCEDURE TForm.AddMenu(menuIndex, btn: INTEGER);
        PROCEDURE TForm.Draw;
        PROCEDURE TForm.InsLast(Menu: TMenu);
        PROCEDURE TForm.MakeEndAfter(menu: TMenu);
        PROCEDURE TForm.Reset(Menu: TMenu);
    END;

Tcheckmenus = record          { uppermost structure in heap - used to get at other structures }
    stuffCnctTo:    TDynamicArray;
    stuffConnector: TDynamicArray;
    stuffSlot:      ARRAY [1..3] OF TDynamicArray;
    stuffASerial:   TDynamicArray;
    stuffBSerial:   TDynamicArray;
    stuffParallel:  TDynamicArray;
    stuffNothing:   TDynamicArray;
    stuffBuiltIn:   TDynamicArray;
    stuffWidget:    TDynamicArray;
    stuffSony:      TDynamicArray;
    stuffInUse:     TDynamicArray;
    stuffunknown:   TDynamicArray;
    stuffDetached:  TDynamicArray;
    stuffLower:     TDynamicArray;
    stuffMiddle:    TDynamicArray;
    stuffUpper:     TDynamicArray;
    stuffMenu:      TMenuList;   { handle to menu array }
    stuffForm:      Tform;       { handle to empty form }
end;
pcheckmenus = ^Tcheckmenus;
hcheckmenus = ^pcheckmenus;

VAR

    cButton:    TClass;
    cMenu:      TClass;
    cMenuList:  TClass;
    cForm:      TClass;

    oldlx:      Tlooks;         { used to determine if the face or font has changed }

    menuMaster: TMenuList;     { Contains all the menus }

    handleHolder: hcheckmenus;

    sCnctTo:    TDynamicArray;
    sConnector: TDynamicArray;
    sNothing:   TDynamicArray;
    sWidget:    TDynamicArray;
    sBuiltIn:   TDynamicArray;
    sSony:      TDynamicArray;
    sSlots:     Array [1..3] OF TDynamicArray;
    sSerialA:   TDynamicArray;
    sSerialB:   TDynamicArray;
    sParallel:  TDynamicArray;
    sInuse:     TDynamicArray;
    sunknown:   TDynamicArray;
    sDetached:  TDynamicArray;
    sLower:     TDynamicArray;
    sMiddle:    TDynamicArray;
    sUpper:     TDynamicArray;

{ emptyText is a pointer to an empty string - not nil }
   emptyText:   TDynamicArray;

   FntInfo:     FontInfo;

   mywindow:    WindowPtr;

 { Current screen structure }
    mainForm:       TForm;
    theMainMenu:    TMenu;

    isPepsi:        BOOLEAN;

{ ---------------------------------------------------------------------- }


{$IFC CFGDEBUG }
PROCEDURE CheckDebug (f: TF);
PROCEDURE PrintRandomText;
{$ENDC}

FUNCTION  FindButton  (menu: TMenu; ptMse: Point; VAR iBtnHit: TC): TF;
FUNCTION  FindMenuAndButton(form: TForm; ptMse: Point; VAR menu: TMenu; VAR button: INTEGER): TF;
FUNCTION  MousePress(form: TForm; ptMse: Point; VAR menu: TMenu; VAR button: INTEGER): TF;
PROCEDURE TransFormPointers;
PROCEDURE StuffHandles;
PROCEDURE UnStuffHandles;
FUNCTION  NoText: TDynamicArray;
{ ---------------------------------------------------------------------- }

IMPLEMENTATION


{$S ckUtil }

{$IFC NOT CFGDEBUG}
CONST
   checkdbg = FALSE;
{$ENDC}

VAR

{$IFC CFGDEBUG }
   checkdbg: TF;
{$ENDC}

   hzCfg:       THz;

{--------------------------------------------------------------------------------------------------------}
PROCEDURE XferLeft(source, dest: Ptr; nBytes: INTEGER); EXTERNAL;
PROCEDURE XferRight(source, dest: Ptr; nBytes: INTEGER); EXTERNAL;
{--------------------------------------------------------------------------------------------------------}

{$IFC CFGDEBUG}
PROCEDURE PrintRandomText;
BEGIN
    WRITE('The Connect To is ');
    sCnctTo.Print;
    WRITE('The Connector is ');
    sConnectTo.Print;
    WRITE('The sony is ');
    sSony.Print;
    WRITE('The widget is ');
    sWidget.Print;
    WRITE('The BuiltIn is ');
    sBuiltIn.Print;
    WRITE('The Nothing is ');
    sNothing.Print;
    WRITE('The in use is ');
    sInUse.Print;
    WRITE('The unknown is ');
    sunknown.Print;
    WRITE('The detached is ');
    sDetached.Print;
    WRITE('The lower is ');
    sLower.Print;
    WRITE('The middle is ');
    sMiddle.Print;
    WRITE('The upper is ');
    sUpper.Print;
    WRITE('The slots are ');
    sSlots[1].Print;
    sSlots[2].Print;
    sSlots[3].Print;
    WRITE('The Connectors are ');
    sSerialA.Print;
    sSerialB.Print;
    IF isPepsi THEN
        sParallel.Print;
END;
{$ENDC}

PROCEDURE StuffHandles;
VAR i:  INTEGER;
BEGIN
{ Stuff the record }
with handleHolder^^ do
   begin
   stuffCnctTo := sCnctTo;
   stuffConnector := sConnector;
   stuffBuiltIn := sBuiltIn;
   stuffNothing := sNothing;
   stuffWidget := sWidget;
   stuffSony := sSony;
   stuffInuse := sInUse;
   stuffunknown := sunKnown;
   stuffdetached := sDetached;
   stuffLower := sLower;
   stuffMiddle := sMiddle;
   stuffUpper := sUpper;
   stuffMenu := menuMaster;
   stuffForm := mainForm;
   FOR i := 1 TO 3 DO
    stuffSlot[i] := sSlots[i];
   stuffASerial := sSerialA;
   stuffBSerial := sSerialB;
   stuffParallel := sParallel;
   end;
END;

PROCEDURE UnStuffHandles;
VAR i:  INTEGER;
BEGIN
{ Stuff the handles from button file }
    WITH handleHolder^^ DO
        BEGIN
        sCnctTo := stuffCnctTo;
        sConnector := stuffConnector;
        FOR i := 1 TO 3 DO
            sSlots[i] := stuffSlot[i];
        sSerialA := stuffASerial;
        sSerialB := stuffBSerial;
        sParallel := stuffParallel;
        sNothing := stuffNothing;
        sBuiltIn := stuffBuiltIn;
        sSony := stuffSony;
        sWidget := stuffWidget;
        sInuse := stuffInUse;
        sunknown := stuffunknown;
        sDetached := stuffDetached;
        sLower := stuffLower;
        sMiddle := stuffMiddle;
        sUpper := stuffUpper;
        mainForm := stuffform;
        menuMaster := stuffMenu;
      END;
END;

METHODS OF TButton

    FUNCTION TButton.Create(obj: TObject; itsHeap: THz; itsLooks: TLooks; text: TDynamicArray;
                                                              VAR startPoint: Point; doWrap: BOOLEAN): TButton;
    VAR r:   Rect;
        strlngth: TC;
        i, btnh, btnv, dh: TC;
        ptrstr: ^Tsp;
    BEGIN
        IF obj = NIL THEN
            obj := NewObject(itsHeap, ThisClass, SIZEOF(SELF));
        SELF := TButton(obj);

        SELF.looks := itsLooks;
        SELF.text := text;

            { Set the size of the check box }
        dh := 0;
        if itsLooks.ButtonType = ckbox then
           dh := 10;

      { bind the font and face }
        SELF.bindLooks;

        ptrstr := @text.ch;

        strlngth := Stringwidth(ptrstr^);

      { adjust the button position for the margins }
        WITH SELF.boundr DO
            BEGIN
            IF doWrap THEN
                IF startPoint.h + dh + strlngth > myWindow^.portRect.right - dhsBox + 1 THEN
                    BEGIN
                    startPoint.h := 0;
                    startPoint.v := startPoint.v + 15;
                    END;
            left := startPoint.h + dhMarg;
            top := startPoint.v + dvMarg;
  {$H-}     SetRect (SELF.btnr, left, top, left+dh, top+dv); {$H+}
            END;

        SELF.ComputeBoundingBox;
    END;

    PROCEDURE TButton.Free;
    BEGIN
        SELF.text.Free;
        SUPERSELF.Free;
    END;

    {$IFC CFGDEBUG}
    PROCEDURE TButton.Print;
    {function: prints a button }
    VAR
        ptrstr: ^Tsp;
    BEGIN
    WITH SELF DO
        BEGIN
        ptrstr := @text.ch;
        WRITELN('         string is ',ptrstr^);
        IF looks.buttonType = boxLabel THEN
            WRITELN('         Label')
        ELSE
            WRITELN('         check box');
        writeln('      bounding box is: top h', boundr.topleft.h,' bot h',boundr.botright.h);
        writeln('                       top v', boundr.topleft.v,' bot v',boundr.botright.v);
        END;
    end;
    {$ENDC}

    PROCEDURE TButton.BindLooks;
    {Function: If font or face information have changed make the appropriate
               QuickDraw calls. }
    VAR fOk:      TF;
        cErr:     TC;
        lfntid:   TLfntid;
    BEGIN
       if not SELF.looks.fb = oldlx.fb then
          begin
          lfntid.fam      := 0;
          lfntid.seteface := [];
          lfntid.dev      := devScreen;
          IF SELF.looks.fB THEN lfntid.seteface := lfntid.seteface+[Bold];

          fok := FMFontMetrics(lfntid, FntInfo, cErr);
          if checkdbg then
             if cErr > 0 then
                writeln('FMFontMetrics error in TButton.BindLooks');

          TextFont (0);
          TextFace (lfntid.seteface);
          TextMode ( srcOr );
          end;
       oldlx := SELF.looks;
    END;

    PROCEDURE TButton.ComputeBoundingBox;
    VAR strLength, dh:   INTEGER;
        ptrStr:          ^s255;
    BEGIN
     { Compute the check box bounding rectangle from the box and string length }
        ptrstr := @SELF.text.ch;
        strlength := Stringwidth(ptrstr^);

        dh := 0;
        if SELF.looks.ButtonType = ckbox then
           dh := 10;

        WITH SELF.boundr DO
    {$H-}   SetRect(SELF.Boundr, left, top, left + dh + strlength,top + dv); {$H+}
    END;

    PROCEDURE TButton.Draw(ScrnLp: INTEGER);
{Function: draw the button vertically relative to ScrnLp and horizontally relative
           to 0 }
    VAR r: Rect;
        ptSp: Point;
        dhSp, dhOv, dvOv: TC;
        ptrstr: ^Tsp;

    BEGIN
       if checkdbg then WRITELN('Entering DrawButton ');
       SELF.BindLooks;

       SELF.TextStartPos(ptSp, dhSp);

       ptSp.v := ptSp.v + scrnlp;

    { if this is a check box draw the box }
       IF SELF.looks.buttonType = ckbox THEN
          BEGIN
          r := SELF.btnr;
 {$H-}    OffsetRect(r, 0, scrnLp); {$H+}
          FrameRect (r);
          END;

    { draw the string }
       MoveTo (ptSp.h, ptSp.v);
       Ptrstr := @SELF.text.ch;
       Drawstring (Ptrstr^);
    END;

    PROCEDURE TButton.Erase(ScrnLp: INTEGER);
    VAR r:  Rect;
{Function: Erase the buttons bounding box }
    BEGIN
        if checkdbg then WRITELN('Entering TButton.Erase ');
        r := SELF.boundr;
 {$H-}  OffsetRect(r, 0, scrnLp); {$H+}
        r.bottom := r.bottom + 3;
        EraseRect(r);
    END;

    PROCEDURE TButton.FillButton (scrnLp: INTEGER;fill: TF);
    {Function: Select button BTN in menu MENU. If fill is true then fill
               the button with black else fill it with white.}

    VAR r: Rect;

    BEGIN
       IF checkdbg THEN WRITELN('Entering FillButton ');
       SELF.BindLooks;

       r := SELF.btnr;
       r.top := r.top + scrnLp;
       r.bottom := r.bottom + scrnLp;

    { Only fill the inside of the box }
       InSetRect  (r, 1, 1);

       IF fill THEN
          fillrect (r, black)
       ELSE
          FillRect (r, white);

    END;

    PROCEDURE TButton.MakeString(newText: e_name);
    VAR size:   INTEGER;
    BEGIN
        size := LENGTH(newText) + 1;
        SELF.text.BeSize(size);
        XferLeft(@newText, @SELF.text.ch, size);
        SELF.ComputeBoundingBox;
    END;

    PROCEDURE TButton.MakeText(newText: TDynamicArray);
    VAR size:   INTEGER;
    BEGIN
        size := newText.size;
        SELF.text.BeSize(size);
        XferLeft(@newText.ch, @SELF.text.ch, size);
        SELF.ComputeBoundingBox;
    END;

    PROCEDURE TButton.AppendText(newText: TDynamicArray);
    BEGIN
        SELF.text.AppendText(newText);
        SELF.ComputeBoundingBox;
    END;

    PROCEDURE TButton.AppendNumber(digit: INTEGER);
    BEGIN
        SELF.text.AppendNumber(digit);
        SELF.ComputeBoundingBox;
    END;

    PROCEDURE TButton.AppendDash;
    VAR thisText:   TDynamicArray;
        origSize:   INTEGER;
    BEGIN
        thisText := SELF.text;
        origSize := thisText.size;
        thisText.BeSize(origSize + 3);
        thisText.ch[origSize + 1] := ' ';
        thisText.ch[origSize + 2] := '-';
        thisText.ch[origSize + 3] := ' ';
        SELF.text.ch[1] := CHR(origSize + 3);
        SELF.ComputeBoundingBox;
    END;


    PROCEDURE TButton.AppendWithParens(newText: TDynamicArray);
    BEGIN
        SELF.text.AppendWithParens(newText);
        SELF.ComputeBoundingBox;
    END;

    PROCEDURE TButton.TextStartPos  (VAR ptSp: Point; VAR dhSp: INTEGER);
    {Function: Given a button determine the point where the text starts }
    VAR
      ptrstr: ^Tsp;
    BEGIN
    WITH SELF DO
       BEGIN
       IF looks.buttonType <>   boxlabel THEN
          ptSp.h := btnr.right + dvMarg
       ELSE
          ptSp.h := btnr.left;

       WITH FntInfo DO
          ptSp.v := (btnr.bottom + btnr.top + Ascent) DIV 2;
       END;
    END;

BEGIN
    cButton := thisClass;
END;

{ ---------------------------------------------------------------------- }

METHODS OF TDriverButton

    FUNCTION TDriverButton.Create(obj: TObject; itsHeap: THz; itsLooks: TLooks; text: TDynamicArray;
                                VAR startPoint: Point; doWrap: BOOLEAN; itsDriver: TObject): TDriverButton;
    BEGIN
        IF obj = NIL THEN
            obj := NewObject(itsHeap, ThisClass, SIZEOF(SELF));
        SELF := TDriverButton(TButton.Create(obj, itsHeap, itsLooks, text, startPoint, doWrap));
        SELF.driver := itsDriver;
    END;
END;

{ ---------------------------------------------------------------------- }

METHODS OF TMenu

    FUNCTION TMenu.Create(obj: TObject; itsHeap: THz; Id: INTEGER): TMenu;
  { Allocate button list, set current button to nil, set bounding rect to zero }
    VAR tempList:   TIndexList;
    BEGIN
        IF obj = NIL THEN
            obj := NewObject(itsHeap, ThisClass, SIZEOF(SELF));
        SELF := TMenu(obj);
  {$H-} SetRect(SELF.boundRect, 0, 0, 0, 0); {$H+}
        tempList := TIndexList.Create(NIL, itsHeap);
        WITH SELF DO
            BEGIN
            ibtn := btnNil;
            oldBtn := btnNil;
            idMu := Id;
            staticButtons := 0;
            muLp := 0;
            btnArray := tempList;
            END;
    END;

    PROCEDURE TMenu.ChangeButton(newText: TDynamicArray; newName: pS255);
    VAR button: TButton;
    BEGIN
        IF checkdbg THEN WRITELN('TButton.ChangeButton ');
        IF newText = NIL THEN
            newText := CreateTextFromString(dynamicHeap, newName);
        button := TButton(SELF.btnArray.elements[SELF.ibtn + 1]);
        button.Erase(SELF.muLp);
        button.makeText(newText);
        button.draw(SELF.muLp);
    END;


    PROCEDURE TMenu.Draw;
    {Function: Display  menu .}

    VAR i: TC;
    dummy: grafptr;

    BEGIN
    IF checkdbg THEN WRITELN('Entering TMenu.Draw ',SELF.idMu);

    FOR i := 1 TO SELF.btnArray.size DO    { for all the buttons in the menu }
       BEGIN
       if checkdbg then writeln('drawing button -',i,' at ',SELF.muLp);
       TButton(SELF.btnArray.elements[i]).Draw(SELF.muLp);
       END;

     { push the button }
       IF SELF.iBtn<>BtnNil THEN
          TButton(SELF.btnArray.elements[SELF.iBtn]).FillButton (SELF.muLp, TRUE);
    END;

    PROCEDURE TMenu.Hit(iBtnNew: INTEGER);
    {Function: a button has been pushed in menu fill it.}
    BEGIN
        IF checkdbg THEN WRITELN('Entering TMenu.Hit - new button is ', ibtnNew);

        WITH SELF DO
            IF iBtnNew<>iBtn THEN
                BEGIN
                IF iBtn<>BtnNil THEN
              {$H-} TButton(btnArray.elements[iBtn]).FillButton (muLp, false); {$H+}
                oldBtn := iBtn;
                iBtn := iBtnNew;
                IF iBtn<>BtnNil THEN
              {$H-} TButton(btnArray.elements[iBtn]).FillButton (muLp, true); {$H+}
                END;
    END;

    PROCEDURE  TMenu.InsLast(btn: TButton);
    BEGIN
        SELF.btnArray.InsLast(btn);

      { Recompute the bounding box }
        IF SELF.btnArray.size = 1 THEN
            SELF.boundRect := btn.boundr
        ELSE
{$H-}       UnionRect(SELF.boundRect, btn.boundr, SELF.boundRect); {$H+}
    END;

    PROCEDURE  TMenu.RestoreButton;
    BEGIN
        SELF.Hit(SELF.oldBtn);
    END;

    PROCEDURE TMenu.DelManyAt(i: LONGINT; howMany: LONGINT; fFree: BOOLEAN);
    VAR j:  INTEGER;
    BEGIN
        IF howMany > 0 THEN
            BEGIN
            SELF.btnArray.DelManyAt(i, howMany, fFree);

          { Recompute the bounding box }
            SELF.boundRect := TButton(SELF.btnArray.elements[1]).boundr;
            FOR j := 2 TO SELF.btnArray.size DO
{$H-}           UnionRect(SELF.boundRect, TButton(SELF.btnArray.elements[j]).boundr, SELF.boundRect); {$H+}
            END;
    END;

    {$IFC CFGDEBUG}
    PROCEDURE TMenu.Print;
    {function: prints a menu given a handle }
    VAR i:      integer;
        btn:    TButton;
        ptrstr: ^Tsp;
    BEGIN
        WITH SELF DO
            BEGIN
            writeln('    menu ',idmu,' is');
            WRITELN('    static count is ', staticButtons:1,' total count is ', btnArray.size:1);
            writeln('    mulp is ',mulp:1);
            WITH boundRect DO
                writeln('    boundRect is:', top, left, bottom, right);
            for i := 1 to btnArray.size do
                BEGIN
                btn := TButton(btnArray.elements[i]);
                ptrStr := @btn.text.ch;
                WRITE('          ');
                if ibtn = i then
                    write('*')
                else
                    IF oldBtn = i THEN
                        WRITE('&');
                write(ptrStr^);
                END;
            writeln;
            end;
        writeln('------------------------------------------------------');
    end;
    {$ENDC}
BEGIN
    cMenu := ThisClass;
END;

{ ---------------------------------------------------------------------- }

METHODS OF TMenuList

    FUNCTION  {TMenuList.}Create{(obj: TObject; itsHeap: THz): TMenuList};
    BEGIN
        IF obj = NIL THEN
            obj := NewObject(itsHeap, ThisClass, SIZEOF(SELF));
        SELF := TMenuList(TIndexList.Create(obj, itsHeap));
    END;

    FUNCTION {TMenuList.}GetMenu{(id: INTEGER): TMenu};
        VAR i:      INTEGER;
            menu:   TMenu;
    BEGIN
        GetMenu := NIL;
        FOR i := 1 TO SELF.size DO
            BEGIN
            menu := TMenu(SELF.elements[i]);
            IF menu.idMu = id THEN
                BEGIN
                GetMenu := menu;
                EXIT(GetMenu);
                END;
            END;
        IF checkDbg THEN
            ABCBreak('GetMenu cant find menu ',id);
    END;

    FUNCTION {TMenuList.}GetIndex{(menu: TMenu): INTEGER};
        VAR i:      INTEGER;
            index:      TMenu;
    BEGIN
        GetIndex := -1;
        FOR i := 1 to SELF.size DO
            BEGIN
            index := TMenu(SELF.elements[i]);
            IF menu = index THEN
                BEGIN
                GetIndex := i;
                EXIT(GetIndex);
                END;
            END;
        IF checkDbg THEN
            ABCBreak('GetIndex cant find the menu',i);
    END;

BEGIN
    cMenuList := thisClass;
END;

{ ---------------------------------------------------------------------- }

METHODS OF TForm

    FUNCTION {TForm.}Create{(obj: TObject; itsHeap: THz): TForm};
  { Allocate button list, set current button to nil, set bounding rect to zero }
    VAR tempList:   TMenuList;
    BEGIN
        IF obj = NIL THEN
            obj := NewObject(itsHeap, ThisClass, SIZEOF(SELF));
        SELF := TForm(obj);
        tempList := TMenuList.Create(NIL, itsHeap);
        WITH SELF DO
            BEGIN
            imuKey := NIL;
            iBtnKey := btnNil;
            menuArray := tempList;
            END;
    END;

    PROCEDURE TForm.Draw;
    VAR
        i:  INTEGER;
    BEGIN
        WITH SELF.menuArray DO
            FOR i := 1 TO size DO
    {$H-}       TMenu(elements[i]).Draw; {$H+}
    END;

    PROCEDURE TForm.AddMenu(menuIndex, btn: INTEGER);
    VAR menu: TMenu;
    {Function: Places menu MENU at the end of the check form  and selects
               button BTN. }
    begin
        menu := menuMaster.GetMenu(menuIndex);
        WITH SELF DO
            BEGIN
            if (btn > 0) and (btn <= menu.btnArray.size) then
                menu.ibtn := btn
            else
                menu.ibtn := btnnil;
            END;
        if checkdbg then
            writeln ('adding menu ',menu.idMu:1, ' btn is ',btn:1, ' at form slot ', SELF.menuArray.size);
        SELF.InsLast(menu);
    end;

    PROCEDURE TForm.InsLast(Menu: TMenu);
    VAR previousMenu: TMenu;
        bottom:     INTEGER;
    BEGIN
        bottom := SELF.menuArray.size;
        SELF.menuArray.InsLast(menu);
        with SELF do
           begin
           previousMenu := TMenu(menuArray.elements[bottom]);  { since we set bottom before calling InsLast
                                                                 it now points at one element before the end }
           menu.mulp := previousMenu.mulp + previousMenu.boundRect.botright.v;
           IF checkDbg THEN
                WRITELN('previousMenu.muLp is ',previousMenu.muLp);
           end;
    END;

    PROCEDURE TForm.MakeEndAt(menu: TMenu);
    VAR indexOfMenu, indexOfLastMenu:   INTEGER;
    BEGIN
        IF checkDbg THEN
            WRITELN('entering MakeEndAfter');
        indexOfMenu := SELF.menuArray.GetIndex(menu);
        indexOfLastMenu := SELF.menuArray.size;
        IF indexOfLastMenu > indexOfMenu THEN
            SELF.menuArray.DelManyAt(indexOfMenu + 1, indexOfLastMenu - indexOfMenu, FALSE);
    END;

    Procedure TForm.Reset(menu: TMenu);
    {Function: Erase from the bottom of menu MENU down to the last menu in the form and remove the
               erased menus from FORM.}
    var
       TempRect: rect;
       lastMenu: TMenu;
    BEGIN
        IF checkDbg THEN
            WRITELN('Entering ResetForm');
        if checkdbg then
           writeln('Clearing screen from pixel ',menu.mulp + menu.boundRect.botright.v  + 3,'down. ');

        lastMenu := TMenu(SELF.menuArray.last);
        SetRect(TempRect, 0, menu.mulp + menu.boundRect.botright.v  + 3,
                                                            700, lastMenu.mulp + lastMenu.boundRect.botright.v + 2);

        eraserect(temprect);

        SELF.MakeEndAt(menu);
    END;

    {$IFC CFGDEBUG}
    PROCEDURE TForm.Print;
    VAR i:      integer;
        menu:   TMenu;
    BEGIN
        WITH SELF DO
            BEGIN
            IF iMuKey <> NIL THEN
                writeln('Current Menu is ',iMuKey.idMu);
            writeln('Current button is ',iBtnKey);
            writeln('Current size is ', menuArray.size);
            writeln('Menus are');
      {$H-} menuArray.Print; {$H+}
            end;
    END;
    {$ENDC}

BEGIN
    cForm := thisClass;
END;

{ ---------------------------------------------------------------------- }

FUNCTION  NoText: TDynamicArray;
VAR text: TDynamicArray;
BEGIN
    text := TDynamicArray.Create(NIL, dynamicHeap, 1);
    text.ch[1] := CHR(0);
    NoText := text;
END;

{ ---------------------------------------------------------------------- }

PROCEDURE TransformPointers;
{ Trim the form back to only the static buttons. }
VAR i,j:    INTEGER;
    menu:   TMenu;
    button: TButton;
BEGIN
    IF checkDbg THEN
        WRITELN('converting the pointers');

  { Convert the random Text }
    THLint(sCnctTo)^^ := ORD(cDynamicArray);
    THLint(sConnector)^^ := ORD(cDynamicArray);
    THLint(sBuiltIn)^^ := ORD(cDynamicArray);
    THLint(sSony)^^ := ORD(cDynamicArray);
    THLint(sWidget)^^ := ORD(cDynamicArray);
    THLint(sNothing)^^ := ORD(cDynamicArray);
    THLint(sSerialA)^^ := ORD(cDynamicArray);
    THLint(sSerialB)^^ := ORD(cDynamicArray);
    THLint(sParallel)^^ := ORD(cDynamicArray);
    THLint(sInUse)^^ := ORD(cDynamicArray);
    THLint(sUnKnown)^^ := ORD(cDynamicArray);
    THLint(sLower)^^ := ORD(cDynamicArray);
    THLint(sMiddle)^^ := ORD(cDynamicArray);
    THLint(sUpper)^^ := ORD(cDynamicArray);
    THLint(sDetached)^^ := ORD(cDynamicArray);

    FOR i := 1 TO 3 DO
        THLint(sSlots[i])^^ := ORD(cDynamicArray);

  { Convert the form, there is no need to convert its members. They are also in menuMaster }
    THLint(mainForm)^^ := ORD(cForm);

  { Convert menu master and all its members }
    THLint(mainForm.menuArray)^^ := ORD(cMenuList);
    THLint(menuMaster)^^ := ORD(cMenuList);
    FOR i := 1 TO menuMaster.size DO
        BEGIN
        menu := TMenu(menuMaster.elements[i]);
        THLint(menu)^^ := ORD(cMenu);
        THLint(menu.btnArray)^^ := ORD(cIndexList);
        FOR j := 1 TO menu.staticButtons DO
            BEGIN
            button := TButton(menu.btnArray.elements[j]);
            THLint(button)^^ := ORD(cButton);
            THLint(button.text)^^ := ORD(cDynamicArray);
            END;
        menu.btnArray.BeSize(menu.staticButtons);
        END;
END;


{ ---------------------------------------------------------------------- }
FUNCTION  FindButton {menu: TMenu; ptMse: Point; VAR iBtnHit: TC): TF };
{Function: find the button in menu that was hit and return it in iBtnHit. }

VAR i:      INTEGER;
    BB:     rect;
    MuLp:   INTEGER;
    btn:    TButton;

BEGIN

FindButton := FALSE;
MuLp := menu.MuLp;

WITH mywindow^.portrect DO
{ Are we inside the scroll bars? }
 if (ptmse.v < bottom -dvsbox + 1) and (ptmse.h < right - dhsbox + 1) then
   begin

{ adjust the bounding box of the menu }
   BB := menu.boundRect;
   BB.topleft.v := menu.boundRect.topleft.v + MuLp;
   BB.botright.v := menu.boundRect.botright.v + MuLp;

{$H-} IF PtInRect (ptMse, BB) THEN  {$H+}   { are we in the menu }
      WITH menu DO
         FOR i := 1 TO menu.btnArray.size DO     { for all the buttons in the menu }
            BEGIN
            btn := TButton(btnArray.elements[i]);
            IF btn.looks.buttonType <> boxlabel THEN   { if this is a check box }
               BEGIN

          { adjust the selectable area for this button, i.e., box and adjoining text }
               BB := btn.boundr;
               BB.topleft.v := btn.boundr.topleft.v + MuLp;
               BB.botright.v := btn.boundr.botright.v + MuLp;
        {$H-}  IF PtInRect (ptMse, BB) THEN  {$H+}
                  BEGIN
                  FindButton := TRUE;
                  iBtnHit := i;
                  EXIT (FindButton);
                  END;
               END;
           END;
   end;
END;

{ ---------------------------------------------------------------------- }

FUNCTION FindMenuAndButton{form: TForm; ptMse: Point; VAR menu: TMenu; VAR button: TC): TF};
{function: For form form and point ptMse find the menu and button hit }
VAR i: TC;
    tempMenu:   TMenu;
BEGIN
   FindMenuAndButton := FALSE;
   WITH form DO
      FOR i := 1 TO menuArray.size DO
         BEGIN
         tempMenu := TMenu(menuArray.elements[i]);
  {$H-}  IF FindButton (tempMenu, ptMse, button) THEN {$H+}
            BEGIN
            menu := tempMenu;
            FindMenuAndButton := TRUE;
            EXIT (FindMenuAndButton);
            END;
         END;
END;

{ ---------------------------------------------------------------------- }

FUNCTION  MousePress {form: TForm; ptMse: Point; VAR menu: TMenu; VAR button: INTEGER): TF};
{Function: Find the menu and button that were selected. Used for cursor tracking. }
VAR
    icrs:   INTEGER;


BEGIN
   IF checkdbg THEN writeln('entering Mousepress: looking at ',ptmse.h,ptmse.v);

   MousePress := FALSE;

   WITH theMainMenu DO
       oldBtn := iBtn;

   WITH form DO
{$H-}   IF FindMenuAndButton(form, ptmse, menu, button) THEN {$H+}
            BEGIN
            IF checkDbg THEN writeln('found a hit in menu ',menu.idMu:1,' at button ',button:1);
            icrs := icrscheck;
            MousePress := TRUE;
            form.iMukey := menu;
  {$H-}     menu.Hit(button); {$H+}
            END
        ELSE
            icrs := icrsInactive;
    SetStdCursor(icrs);

    IF checkdbg THEN
        writeln('Leaving MousePress');
END;

{ ---------------------------------------------------------------------- }
{                               Debug                                    }
{ ---------------------------------------------------------------------- }
   {$IFC CFGDEBUG }

PROCEDURE CheckDebug { f: TF };
BEGIN
      checkdbg := f;
END;
   {$ENDC}
{$S InitPref}

END.

ÿ