{$S InitPref}
    PROCEDURE TranslateBootInfo;
    VAR bootInfo:   TBootStuff;
    BEGIN
        PMReadBoot(bootInfo);
        bootPos := nullPosition;
        WITH bootPos DO
            CASE bootInfo.bootVol OF
                0:  slot := 12;
                1:  slot := 13;
                2:  slot := 11;
                3:  BEGIN
                    slot := 1;
                    chan := 1;
                    END;
                4:  BEGIN
                    slot := 1;
                    chan := 2;
                    END;
                6:  BEGIN
                    slot := 2;
                    chan := 1;
                    END;
                7:  BEGIN
                    slot := 2;
                    chan := 2;
                    END;
                9:  BEGIN
                    slot := 3;
                    chan := 1;
                    END;
                10: BEGIN
                    slot := 3;
                    chan := 2;
                    END;
            END;
    END;

PROCEDURE InitPref(VAR error: integer);
VAR configStatus:   TConfigStatus;
    i:              INTEGER;
    alertDigit:     e_name;
BEGIN
    IF uPrefDbg THEN
        WRITELN('Calling InitPref');

    emptyString := '';
    TransLateBootInfo;

    InitCd(error);
    IF error > 0 THEN
        EXIT(initPref);

    root := TRoot.Create(NIL, dynamicHeap);
    IF uPrefDbg THEN
        WRITELN('Calling TRoot.Init');
    root.Init(configStatus);

    BuildRootMenu;

    WITH configStatus DO
        BEGIN
        FOR i := 1 TO 3 DO
            BEGIN
            alertDigit[0] := CHR(1);  { This string is always one character long }
            IF (inSlot[i] > 0) AND NOT hasDriver[i] THEN
                BEGIN
                alertDigit[1] := CHR(ORD('0') + i);
                PrefParamAlert(@alertDigit, @emptyString);
                NoteAlert(prefAlert, MapAlert(cardButNoDriver));
                END;
            IF hasDriver[i] AND (inSlot[i] = 0) THEN
                BEGIN
                alertDigit[1] := CHR(ORD('0') + i);
                PrefParamAlert(@alertDigit, @emptyString);
                NoteAlert(prefAlert, MapAlert(driverButNoCard));
                END;
            END;
        IF driversWithNoDevices THEN
            NoteAlert(prefAlert, MapAlert(pmEntryButNoDriver));
        END;

END;

PROCEDURE BuildInstallMenu;
VAR error:      INTEGER;
    menu:       TMenu;
    instList:   TIndexList;
    button:     TButton;
    btnText:    TDynamicArray;
    looks:      TLooks;
    startPoint: Point;
    i:          INTEGER;

BEGIN
    instList := InstWhat(error);
    IF error > 0 THEN
        BEGIN
        EndWaitAlert;  { The searching sony alert may still be up }
        CASE error OF
            cdNoSony: NoteAlert(prefAlert, MapAlert(putInSony));
            cdNoDrivers: NoteAlert(prefAlert, MapAlert(noDrivers));
            OTHERWISE NoteAlert(prefAlert, MapAlert(SonyCantRead));
            END;
        mainForm.Reset(theMainMenu);
        EXIT(BuildInstallMenu);
        END;

    menu := menuMaster.GetMenu(cInstall);
    menu.btnArray.delManyAt(menu.staticButons + 1, menu.btnArray.size-menu.staticButons, FALSE);

    WITH looks DO
        BEGIN
        fb := FALSE;
        buttonType := ckbox;
        END;

    button := TButton(menu.btnArray.First);
    SetPt(startPoint, deltaH, button.boundr.bottom + deltaV);
    FOR i := 1 TO instList.size DO
        BEGIN
        button := TButton.Create(NIL, dynamicHeap, looks, TDynamicArray(instList.elements[i]), startPoint, TRUE);
        startPoint.v := startPoint.v + 15;
        menu.InsLast(button);
        END;

    menu.port := NIL;
    menu.ibtn := btnNIL;
    mainForm.InsLast(menu);
    instList.FreeObject;
END;

{$S PrefHot}
PROCEDURE BuildRemoveMenu;
VAR menu:       TMenu;
    button:     TButton;
    drvrButton: TDriverButton;
    looks:      TLooks;
    startPoint: Point;
    driver:     TChObject;
    i:          INTEGER;

    PROCEDURE CrunchList(thisList: TChList);
    VAR
        i:          INTEGER;
        console:    e_name;
        tempString: e_name;
        thisText:   TDynamicArray;
    BEGIN
        FOR i := 1 TO thisList.size DO
            BEGIN
            driver := TChObject(thisList.elements[i]);
        { Explicit checks for the sony and console drivers !!!!! This is very hard coded !!!!!! }
            IF (driver.typeDevice <> disk) OR (driver.id <> 36) THEN
            IF driver.Reveal THEN
                BEGIN
              { Create the left hand device button }
                startPoint.h := dhMarg;
                looks.buttonType := ckBox;
                tempString := driver.name;
                thisText := CreateTextFromString(dynamicHeap, @tempString);
                drvrButton := TDriverButton.Create(NIL, dynamicHeap, looks, thisText, startPoint, FALSE,
                                                                                                    driver);
                menu.InsLast(drvrButton);

              { Create the right hand active button }
                IF driver.InUse THEN
                    BEGIN
                    startPoint.h := 200;
                    looks.buttonType := boxLabel;
                    button := TButton.Create(NIL, dynamicHeap, looks, NoText, startPoint, FALSE);
                    button.MakeText(sInUse);
                    menu.InsLast(button);
                    END;
                startPoint.v := startPoint.v + 15;
                END;
            END;
    END;

BEGIN
    menu := menuMaster.GetMenu(cRemoveDriver);

    menu.DelManyAt(2, menu.btnArray.size - 1, TRUE);
    looks.fb := FALSE;

    SetPt(startPoint, dhMarg, TButton(menu.btnArray.elements[1]).boundr.bottom + dvMarg);

    CrunchList(driverList);

    menu.ibtn := btnNIL;
    mainForm.InsLast(menu);
END;

{$S PrefHot}
PROCEDURE ConsistencyCheck(VAR configStatus: TConfigStatus);
VAR i:              INTEGER;
    error:          INTEGER;
    pmObject:       TPmObject;
BEGIN
    Cards_Equipped(error, configStatus.inSlot);

    WITH configStatus DO
        BEGIN
        FOR i := 1 TO 3 DO
            HasDriver[i] := FALSE;
        driversWithNoDevices := FALSE;
        END;

    FOR i := 1 TO list_pm.size DO  { For all entries in parameter memory }
        BEGIN
        pmObject := TPmObject(list_pm.elements[i]);

        WITH pmObject.config.pos DO
            BEGIN
            IF (slot < 4) AND (chan = 0) THEN   { If this is a card then set the card flag }
                configStaus.HasDriver[slot] := TRUE;

             IF (slot <> 10) AND (chan <> 0) THEN
        {$H-}   IF driverList.HasID(pmObject.config.driverid) = NIL THEN  {$H+}
                    configStatus.driversWithNoDevices := TRUE;
            END;
        END;

END;

METHODS OF TPrefDevice
    FUNCTION  TPrefDevice.Create(obj: TObject; itsHeap: THz): TPrefDevice;
    BEGIN
    END;
END;

METHODS OF TDevicePort

    PROCEDURE TDevicePort.RemoveConnectedDevice(VAR error: INTEGER);
    VAR chObject:   TChObject;
        device:     TPrefPort;
    BEGIN
        device := SELF.connectedDevice;
        IF device = NIL THEN
            BEGIN
            error := 0;
            EXIT(RemoveConnectedDevice);
            END;

        chObject := GetDescriptor(TDevice(device));


        chObject.PosDetach(error, SELF.position);
        IF error > 0 THEN
            EXIT(RemoveConnectedDevice);

      { If this was a printer then set the flag, reset the type device, and decrement the cdd's
        active count. }
        IF chObject.typeDevice <> card THEN
            IF TDevice(device).typeDevice = printer THEN
                BEGIN
                printerChanged := TRUE;
                TDevice(device).typeDevice := other;

              { Get the lowlevel driver and decrement its active count }
                chObject := GetDescriptor(TDevice(device));
                chObject.active := chObject.active - 1;
                END;

        device.Free;
        SELF.connectedDevice := NIL;
    END;

    PROCEDURE TDevicePort.DefferConnectedDevice(VAR error: INTEGER);
    BEGIN
        Detach(error, SELF.position, TRUE);
        IF (error = 0) OR (error = 752) OR (error = 753) THEN
            BEGIN
            TDevice(SELF.connectedDevice).isDetached := TRUE;
            error := 0;
            END
        ELSE
            error := errorDetaching;
    END;

END;

PROCEDURE BuildConnectorName(chanCount, chan: INTEGER; text: TDynamicArray);
BEGIN
    CASE chanCount OF
     2: BEGIN
        CASE chan OF
            1: text.AppendText(sLower);
            2: text.AppendText(sUpper);
            END;
        text.AppendText(sConnector);
        END;
     3: BEGIN
        CASE chan OF
            1: text.AppendText(sLower);
            2: text.AppendText(sMiddle);
            3: text.AppendText(sUpper);
            END;
        text.AppendText(sConnector);
        END;
  OTHERWISE BEGIN
            text.AppendText(sConnector);
            text.AppendNumber(chan);
            END;
        END;
END;

FUNCTION BuildLocationName(pmObject: TPmObject): TDynamicArray;
VAR text: TDynamicArray;
    size: INTEGER;
    pos:  cd_position;
BEGIN
    pos := pmObject.config.pos;
    CASE pos.slot OF
        10: IF pos.chan = 1 THEN
                text := TDynamicArray(sSerialA.Clone(dynamicHeap))
            ELSE
                IF pos.chan = 2 THEN
                    text := TDynamicArray(sSerialB.Clone(dynamicHeap));
        11: text := TDynamicArray(sParallel.Clone(dynamicHeap));
        1,2,3:  BEGIN
                text := TDynamicArray(sSlots[pos.slot].Clone(dynamicHeap));
                text.AppendWithParens(root.slots[pos.slot].connectedDevice.name);
                size := TCard(TSlot(root.slots[pos.slot]).connectedDevice).connectors.size;
                BuildConnectorName(size, pos.chan, text);
                END;
        END;
    BuildLocationName := text;
END;

METHODS OF TRoot;


    FUNCTION TRoot.Create(obj: TObject; itsHeap: THz): TRoot;
    BEGIN
        IF uPrefDbg THEN
            WRITELN('Entering TRoot.Create');
        IF obj = NIL THEN
            obj := NewObject(itsHeap, ThisClass, SIZEOF(SELF));
        SELF := TRoot(TPrefPort.Create(obj, itsHeap, NIL, 0));
    END;

{$IFC CFGDEBUG}
    PROCEDURE TRoot.Print;
    VAR i:  INTEGER;
    BEGIN
        WITH SELF DO
            BEGIN
      {$H-} FOR i := 1 TO 3 DO
                slots[i].Print;
            serialA.Print;
            serialB.Print;
            IF NOT isPepsi THEN
                parallel.Print; {$H+}
            END;
    END;
{$ENDC}

    PROCEDURE TRoot.Init(VAR configStatus: TConfigStatus);

    VAR tempSlot:       TSlot;
        tempConnector:  TConnector;
        i:              INTEGER;
        temp:           e_name;

    BEGIN
        IF uPrefDbg THEN
            WRITELN('Entering TRoot.InitRoot');

      { Allocate the 3 slots }
        FOR i := 1 TO 3 DO
            BEGIN
            tempSlot := TSlot.Create(NIL, dynamicHeap, sSlots[i], i);
            SELF.slots[i] := tempSlot;
            END;

      { Allocate the 2 or 3 connectors }
        temp := 'lisaSerial';
        tempConnector := TConnector.Create(NIL, dynamicHeap,
                                                    CreateTextFromString(dynamicHeap, @temp), NIL);
        WITH tempConnector.position DO
            BEGIN
            slot := 10;
            chan := 1
            END;
        SELF.serialA := tempConnector;

        tempConnector := TConnector.Create(NIL, dynamicHeap,
                                                    CreateTextFromString(dynamicHeap, @temp), NIL);
        WITH tempConnector.position DO
            BEGIN
            slot := 10;
            chan := 2
            END;
        SELF.serialB := tempConnector;

        IF NOT isPepsi THEN
            BEGIN
            temp := 'lisaParallel';
            tempConnector := TConnector.Create(NIL, dynamicHeap,
                                                    CreateTextFromString(dynamicHeap, @temp), NIL);
            tempConnector.position.slot := 11;
            SELF.parallel := tempConnector;
            END;

    {$IFC CFGDEBUG}
        IF uPrefDbg THEN
            BEGIN
            IF Prompt('Print the Card list?') THEN
                BEGIN
                WRITELN;
                WRITELN;
                WRITELN('***** Here is the card list *****');
                cardList.Print;
                WRITELN;
                END;
            IF Prompt('Print the device list? ') THEN
                BEGIN
                WRITELN;
                WRITELN;
                WRITELN('***** Here is the device list *****');
                deviceList.Print;
                WRITELN;
                END;
            END;
    {$ENDC}

      { Build current configuration tree from PM and check against available drivers in lists }
        ConsistencyCheck(configStaus);
        SELF.BuildConfiguration;
    END;

    PROCEDURE TRoot.BuildConfiguration;
    VAR card:           TCard;
        device:         TDevice;
        connector:      TConnector;
        i:              INTEGER;
        pmRec:          TPmObject;
        pmList:         TNamedList;
        pos:            cd_position;
        cdd:            TCddObject;
        prd:            TPrdObject;
        ch:             TChObject;

    BEGIN
        IF uPrefDbg THEN
            WRITELN('Entering TRoot.BuildConfiguration');
        pmList := list_pm;
    {$IFC CFGDEBUG}
        IF uprefDbg THEN
            IF Prompt('Print the pmList? ') THEN
                pmList.Print;
    {$ENDC}
        FOR i := 1 TO pmList.size DO
            BEGIN
            pmRec := TPmObject(pmList.elements[i]);
            pos := pmRec.config.pos;
            IF (pos.slot < 4) AND (pos.chan = 0)THEN
                BEGIN
                cdd := TCddObject(cardList.HasID(pmRec.config.driverId));
                IF cdd = NIL THEN
                    cdd := unknownCdd;
                card := TCard.Create(NIL, dynamicHeap, cdd, SELF.slots[pos.slot], pos);
                SELF.slots[pos.slot].connectedDevice := card;
                cdd.active := cdd.active + 1;
                END;
            END;

        FOR i := 1 TO pmList.size DO
            BEGIN
          { Get the connector the device is attached to }
            pmRec := TPmObject(pmList.elements[i]);
            IF pmRec.config.driverID <> 40 THEN
                BEGIN
                connector := PosToConnector(pmRec.config.pos);

              { Place the device on the connector }
                IF connector <> NIL THEN
                    BEGIN
                    cdd := NIL;
                    prd := NIL;
                    cdd := TCddObject(deviceList.HasId(pmRec.config.driverId));
                    IF cdd = NIL THEN
                        cdd := unknownCdd;
                    ch := cdd;
      { IF this is a printer then find the printer with the same id as in PM and pass it to Create }
                    IF pmRec.printerId > 0 THEN
                        BEGIN
                        IF cdd <> unknownCdd THEN
                            prd := TPrdObject(processList.HasId(pmRec.printerId));
                        IF prd = NIL THEN
                            prd := unknownPrd;
                        cdd.active := cdd.active + 1;
                        ch := prd;
                        END;


                    device := TDevice.Create(NIL, dynamicHeap, ch, pos, cdd.id, pmRec.printerId);
                    connector.connectedDevice := TPrefPort(device);
                    ch.active := ch.active + 1;
                    END;
                END;
            END;
    END;

{$S PrefHot}
    PROCEDURE TRoot.BuildMenu(VAR error: INTEGER);
  { The menu this method generates is in two columns. The one on the left is the position and the one on
    the left is the device attached to that position. All the buttons were pre-allocated. The text for the
    positions was read in from the button file, but the text for the device buttons is supplied here. We are
    therefore only modifying the even buttons - thus the 2, 4, and i*2 + 4. }
    VAR menu:       TMenu;
        i:          INTEGER;
        slotsStart: INTEGER;

        PROCEDURE MakeButton(port: TPrefPort; buttonIndex: INTEGER);
        VAR
            text:       TDynamicArray;
            button:     TButton;
        BEGIN
            button := TButton(menu.btnArray.elements[buttonIndex]);
            IF port.connectedDevice <> NIL THEN
                text := port.connectedDevice.name
            ELSE
                text := sNothing;
            button.MakeText(text);
            IF port.connectedDevice <> NIL THEN
                IF port.connectedDevice.isDetached THEN
                    button.AppendText(sDetached);
        END;

    BEGIN
        IF uPrefDbg THEN
            WRITELN('Entering TRoot.BuildMenu');
        error := 0;
        menu := menuMaster.GetMenu(cRootMenu);
        { Create and stuff the menu with the appropriate buttons }

      { Serial A }
        MakeButton(SELF.serialA, 2);

      { Serial B }
        MakeButton(SELF.serialB, 4);

      { Parallel }
        IF NOT isPepsi THEN
            BEGIN
            MakeButton(SELF.parallel, 6);
            slotsStart := 6
            END
        ELSE
            slotsStart := 4;

      { Slots }
        FOR i := 1 TO 3 DO
            MakeButton(SELF.slots[i], i * 2 + slotsStart);

        menu.port := SELF;
        menu.iBtn := btnNil;
        mainForm.InsLast(menu);
    END;

    PROCEDURE TRoot.BuildDefaultPrinterMenu;
    VAR i:              INTEGER;
        button:         TButton;
        drvrButton:     TDriverButton;
        menu:           TMenu;
        currentButton:  INTEGER;
        pmList:         TNamedList;
        looks:          TLooks;
        startPoint:     Point;
        device:         TDevice;
        btnText:        TdynamicArray;
        prdObject:      TPrdObject;
        pmObject:       TPmObject;
        tempString:     e_name;

  { Find all currently configured printers }
    BEGIN
        IF uPrefDbg THEN
            WRITELN('Entering TRoot.BuildDefaultPrinterMenu');
        pmList := list_pm;

        menu := menuMaster.GetMenu(cDefaultPrinter);
        menu.DelManyAt(2, menu.btnArray.size -1, TRUE);

        looks.fb := FALSE;

        currentButton := btnNil;
        startPoint.v := TButton(menu.btnArray.elements[1]).boundr.bottom + dvMarg;
        FOR i := 1 TO pmList.size DO
            BEGIN
            pmObject := TPmObject(pmList.elements[i]);
            IF pmObject.printerID > 0 THEN
                BEGIN
                prdObject := TPrdObject(processList.HasId(pmObject.printerID));
                IF prdObject <> NIL THEN
                    BEGIN
                {$IFC CFGDEBUG}
                    IF uPrefDbg THEN
                        BEGIN
                        WRITELN('Found printer ');
                        prdObject.print;
                        END;
                {$ENDC}
                    startPoint.h := 5;
                    looks.buttonType := ckBox;
                    tempString := prdObject.name;
                    drvrButton := TDriverButton.Create(NIL, dynamicHeap, looks,
                              CreateTextFromString(dynamicHeap, @tempString), startPoint, FALSE, pmObject);
                    menu.Inslast(drvrButton);

                    IF pmObject.isDefault THEN
                        currentButton := menu.btnArray.size;

                    startPoint.h := 200;
                    looks.buttonType := boxLabel;
                    button := TButton.Create(NIL, dynamicHeap, looks, BuildLocationName(pmObject),
                                                                                     startPoint, FALSE);
                    menu.Inslast(Button);
                    startPoint.v := startPoint.v + 15;
                    END;
                END;
            END;

        menu.port := NIL;
        menu.iBtn := currentButton;
        IF menu.btnArray.size > 1 THEN
            mainForm.InsLast(menu);
    END;

    PROCEDURE TRoot.BuildBootFromMenu;
    VAR i:              INTEGER;
        button:         TButton;
        drvrButton:     TDriverButton;
        menu:           TMenu;
        currentButton:  INTEGER;
        pmList:         TNamedList;
        pmObject:       TPmObject;
        looks:          TLooks;
        startPoint:     Point;
        device:         TCddObject;
        leftText:       TDynamicArray;
        rightText:      TDynamicArray;
        deviceIsDisk:   BOOLEAN;
        tempString:     e_name;

  { Find all currently configured disks }
    BEGIN
        IF uPrefDbg THEN
            WRITELN('Entering TRoot.BuildBootFromMenu');

        TranslateBootinfo;
        pmList := list_pm;
        menu := menuMaster.GetMenu(cBootFrom);
        menu.DelManyAt(2, menu.btnArray.size -1, TRUE);

        looks.fb := FALSE;

        currentButton := btnNil;

        startPoint.v := TButton(menu.btnArray.elements[1]).boundr.bottom + dvMarg;
        FOR i := 1 TO pmList.size DO
            BEGIN
            deviceIsDisk := FALSE;
            pmObject := TPmObject(pmList.elements[i]);
            IF pmObject.config.devName = '#12' THEN
                BEGIN
                deviceIsDisk := TRUE;
                leftText := TDynamicArray(sWidget.Clone(dynamicHeap));
                rightText := NoText;
                END
  { sony }  ELSE
                IF pmObject.config.devName = '#13' THEN
                    BEGIN
                    deviceIsDisk := TRUE;
                    leftText := TDynamicArray(sSony.Clone(dynamicHeap));
                    rightText := NoText;
                    END
                ELSE
                    BEGIN
                  { Find the device in the device list }
                    device := TCddObject(deviceList.HasID(pmObject.config.driverID));
                    IF device <> NIL THEN
                        IF (device.typeDevice = disk) AND (device.bootable) THEN
                            BEGIN
                            deviceIsDisk := TRUE;
                            rightText := BuildLocationName(pmObject);
                            tempString := device.name;
                            leftText := CreateTextFromString(dynamicHeap, @tempString);
                            END
                    END;

          { Now make the button }
            IF deviceIsDisk THEN
                BEGIN
                startPoint.h := dhMarg;
                looks.buttonType := ckBox;
                drvrButton := TDriverButton.Create(NIL, dynamicHeap, looks, leftText, startPoint, FALSE,
                                                                                                pmObject);
                menu.Inslast(drvrButton);
                IF EqualBytes(@pmObject.config.pos, @bootPos, 3) THEN
                    currentButton := menu.btnArray.size;

                startPoint.h := 200;
                looks.buttonType := boxLabel;
                button := TButton.Create(NIL, dynamicHeap, looks, rightText, startPoint, FALSE);
                startPoint.v := startPoint.v + 15;
                menu.Inslast(button);
                END;
            END;

        menu.port := NIL;
        menu.iBtn := currentButton;
        IF menu.btnArray.size > 1 THEN
            mainForm.InsLast(menu);
    END;

    PROCEDURE TRoot.TraverseConnectors(PROCEDURE Operator(connector: TPrefPort));
    VAR
        slot:       TSlot;
        card:       TCard;
        i, j:       INTEGER;

    BEGIN
      { Try to detach all defered detached devices }
        FOR i := 1 TO 3 DO
            BEGIN
            slot := TSlot(SELF.slots[i]);
            card := TCard(slot.connectedDevice);
            IF card <> NIL THEN
                BEGIN
                FOR j := 1 TO card.connectors.size DO
                    Operator(TConnector(card.connectors.elements[j]));
                Operator(card);
                END;
            END;

        Operator(SELF.serialA);

        Operator(SELF.serialB);

        IF NOT isPepsi THEN
            Operator(SELF.parallel);
    END;

END;

METHODS OF TSlot;


    FUNCTION TSlot.Create(obj: TObject; itsHeap: THz; itsName: TDynamicArray; itsSlot: INTEGER): TSlot;
    BEGIN
        IF uPrefDbg THEN
            WRITELN('Entering TSlot.Create');
        IF obj = NIL THEN
            obj := NewObject(itsHeap, ThisClass, SIZEOF(SELF));

        SELF := TSlot(TPrefPort.Create(obj, itsHeap, itsName, 0));
        SELF.cardList := cardList;
        SELF.position.slot := itsSlot;
    END;

{$IFC CFGDEBUG}
    PROCEDURE TSlot.Print;
    BEGIN
        SUPERSELF.Print;
        IF SELF.connectedDevice <> NIL THEN
            BEGIN
            IF NOT SELF.connectedDevice.isDetached THEN
                WRITELN('  has attached to it ')
            ELSE
                WRITELN('  has deffered device attached to it');
            WRITE('    ');
            SELF.connectedDevice.Print;
            END
        ELSE
            WRITELN('  has nothing attached');
    END;
{$ENDC}

    PROCEDURE TSlot.BuildMenu(VAR error: INTEGER);
    VAR menu:           TMenu;
        button:         TButton;
        driverButton:   TDriverButton;
        size:           INTEGER;
        looks:          TLooks;
        text:           TDynamicArray;
        startPoint:     Point;
        arraySize, i:   INTEGER;
        currentButton:  INTEGER;
        card:           TCddObject;
        dummy:          INTEGER;
        device:         TCard;
        pmObject:       TPmObject;
        tempString:     e_name;

    BEGIN
        IF uPrefDbg THEN
            WRITELN('Entering TSlot.BuildMenu');

        error := 0;

        menu := menuMaster.GetMenu(cCnctToSlot);
        arraySize := menu.btnArray.size;
        button := TButton(menu.btnArray.first);
        button.MakeText(sCnctTo);
        button.AppendText(SELF.name);

        menu.DelManyAt(menu.staticButons + 1, arraySize - menu.staticButons, FALSE);
        WITH looks DO
            BEGIN
            fb := FALSE;
            buttonType := ckbox;
            END;

        button := TButton(menu.BtnArray.elements[menu.staticButons]);
        SetPt(startPoint, button.boundr.right + buttonSpace, button.boundr.top - dvMarg); { Location of the "Install a New Device" button }

        currentButton := 0;
        WITH SELF DO
            BEGIN
            IF connectedDevice = NIL THEN
                currentButton := 2
            ELSE
                IF connectedDevice.id = unknownDevice THEN
                    currentButton := btnNIL;
            END;

        FOR i := 1 TO SELF.cardList.size DO
            BEGIN
            card := TCddObject(SELF.cardList.elements[i]);
            tempString := card.name;
            driverButton := TDriverButton.Create(NIL, SELF.heap, looks, CreateTextFromString(dynamicHeap,
                                                                    @tempString), startPoint, TRUE, card);
            startPoint.h := driverButton.boundr.right + buttonSpace;
            menu.InsLast(driverButton);
            IF currentButton = 0 THEN
                IF SELF.connectedDevice.id = card.id THEN
                    currentButton := i + 2;
            END;

        IF currentButton = 0 THEN
            currentButton := 2;
        menu.ibtn := currentButton;
        menu.port := SELF;
        mainForm.InsLast(menu);
        IF currentButton > 2 THEN
            SELF.connectedDevice.BuildMenu(error);

    END;

END;

METHODS OF TCard;

    FUNCTION TCard.Create(obj: TObject; itsHeap: THz; cd: TCddObject; itsSlot: TSlot; position: cd_position): TCard;
    VAR i:          INTEGER;
        thisList:   TNamedList;
        connector:  TConnector;
        cnctName:   TDynamicArray;
        name:       TDynamicArray;
        tempString: e_name;
    BEGIN
      { Allocate card }
        IF uPrefDbg THEN
            WRITELN('Entering TCard.Create');
        IF obj = NIL THEN
            obj := NewObject(itsHeap, ThisClass, SIZEOF(SELF));
        tempString := cd.name;
        name := CreateTextFromString(dynamicHeap, @tempString);
        SELF := TCard(TPrefPort.Create(obj, itsHeap, Name, cd.id));

        thisList := TNamedList.Create(NIL, itsHeap);
        WITH SELF DO
            BEGIN
            connector := thisList;
            slot := itsSlot;
            typeDevice := card;
            IF itsSlot <> NIL THEN
                position := itsSlot.position;
            END;

      { Allocate the connector descriptors }
        IF cd.connectors = NIL THEN
            SELF.connectors := NIL
        ELSE
            FOR i := 1 TO cd.connectors.size DO
                BEGIN
                cnctName := TDynamicArray(TNamedObject(cd.connectors.elements[i]).name.Clone(dynamicHeap));
                connector := TConnector.Create(NIL, itsHeap, cnctName, SELF);
                connector.position := SELF.position;
                connector.position.chan := i;
                SELF.connectors.InsLast(connector);
                END;
    END;

    PROCEDURE TCard.Free;
    BEGIN
        Free(SELF.connectors);
        SUPERSELF.Free;
    END;

{$IFC CFGDEBUG}
    PROCEDURE TCard.Print;
    BEGIN
        SUPERSELF.Print;
        IF SELF.connectors <> NIL THEN
            SELF.Connectors.Print;
    END;
{$ENDC}

    PROCEDURE TCard.BuildMenu(VAR error: INTEGER);
    VAR i:          INTEGER;
        looks:      TLooks;
        card:       TCard;
        button:     TButton;
        drvrButton: TDriverButton;
        text:       TDynamicArray;
        menu:       TMenu;
        connector:  TConnector;
        startPoint: Point;
        connecCount: INTEGER;

    BEGIN
        IF uPrefDbg THEN
            WRITELN('Entering TCard.BuildMenu');

        error := 0;
        IF SELF.connectors.size = 0 THEN
            EXIT(BuildMenu);

        menu := TMenu(menuMaster.GetMenu(cCardMenu));

      { Remove all the old buttons }
        menu.DelManyAt(2, menu.btnArray.Size - 1, TRUE);

      { Generate the menu title - which consists of the slot, a dash, and the card name }
        WITH looks DO
            BEGIN
            fb := TRUE;
            buttonType := boxLabel;
            END;

        button := TButton(menu.btnArray.First);
        button.MakeText(SELF.slot.name);
        button.AppendDash;
        button.AppendText(SELF.name);

      { Create a two column format menu with connectors on the left and current device on the right }
        looks.fb := FALSE;

        startPoint.v := button.boundr.bottom + dvMarg;

        connecCount := SELF.connectors.size;
        FOR i := connecCount DOWNTO 1 DO
            BEGIN
            connector := TConnector(SELF.connectors.elements[i]);

          { Build the left hand button }
            startPoint.h := 5;
            looks.buttonType := ckbox;
            drvrButton := TDriverButton.Create(NIL, SELF.heap, looks, NoText, startPoint, FALSE,
                                                                                connector);
            BuildConnectorName(connecCount, i, drvrButton.text);
            drvrButton.ComputeBoundingBox;
            menu.InsLast(drvrButton);

          { Build the right hand button }
            startPoint.h := 200;
            looks.buttonType := boxLabel;
            button := TButton.Create(NIL, SELF.heap, looks, NoText, startPoint, FALSE);
            IF connector.connectedDevice <> NIL THEN
                BEGIN
                button.MakeText(connector.connectedDevice.name);
                IF connector.connectedDevice.isDetached THEN
                    button.AppendText(sDetached);
                END
            ELSE
                button.MakeText(sNothing);

            menu.InsLast(button);
            startPoint.v := startPoint.v + 15;
            END;

        menu.iBtn := btnNIL;
        menu.port := SELF;
        mainForm.InsLast(menu);
    END;

END;

METHODS OF TConnector;


    FUNCTION TConnector.Create(obj: TObject; itsHeap: THz; itsName: TDynamicArray; itsCard: TCard): TConnector;

    BEGIN
        IF uPrefDbg THEN
            WRITELN('Entering TConnector.Create');
        IF obj = NIL THEN
            obj := NewObject(itsHeap, ThisClass, SIZEOF(SELF));
        SELF := TConnector(TPrefPort.Create(obj, itsHeap, itsName, 0));

        SELF.card := itsCard;
    END;

{$IFC CFGDEBUG}
    PROCEDURE TConnector.Print;
    BEGIN
        SUPERSELF.Print;
        IF SELF.connectedDevice <> NIL THEN
            BEGIN
            IF NOT SELF.connectedDevice.isDetached THEN
                WRITELN('  has attached to it ')
            ELSE
                WRITELN('  has deffered device attached to it');
            WRITE('    ');
            SELF.connectedDevice.Print;
            END
        ELSE
            WRITELN('  has nothing attached');
    END;
{$ENDC}

    PROCEDURE TConnector.BuildMenu(VAR error: INTEGER);
    VAR menu:           TMenu;
        drvrButton:     TDriverButton;
        button:         TButton;
        size:           INTEGER;
        looks:          TLooks;
        text:           TDynamicArray;
        startPoint:     Point;
        previousButton: TButton;
        i:              INTEGER;
        driver:         TCddObject;
        process:        TPrdObject;
        currentButton:  INTEGER;
        device:         TDevice;
        pmObject:       TPmObject;

        PROCEDURE MakeButton(driver: TChObject);
        VAR
            tempString: e_name;
        BEGIN
            tempString := driver.name;
            drvrButton := TDriverButton.Create(NIL, SELF.heap, looks,
                                   CreateTextFromString(dynamicHeap, @tempString), startPoint, TRUE, driver);

            startPoint.h := drvrButton.boundr.right + buttonSpace;
            menu.InsLast(drvrButton);

        END;

    BEGIN
        IF uPrefDbg THEN
            WRITELN('Entering TConnector.BuildMenu');
        error := 0;

        menu := menuMaster.GetMenu(cCncToConnector);

        button := TButton(menu.btnArray.First);
        button.MakeText(sCnctTo);
        IF SELF = root.serialA THEN
            button.AppendText(sSerialA)
        ELSE
            IF SELF = root.serialB THEN
                button.AppendText(sSerialB)
            ELSE
                IF SELF = root.parallel THEN
                    button.AppendText(sParallel)
                ELSE
                    BEGIN
                    BuildConnectorName(SELF.card.connectors.size, SELF.position.chan, button.text);
                    button.ComputeBoundingBox;
                    END;

        menu.btnArray.DelManyAt(menu.staticButons + 1, menu.btnArray.size - menu.staticButons, FALSE);

        WITH looks DO
            BEGIN
            fb := FALSE;
            buttonType := ckbox;
            END;

        currentButton := 0;
        WITH SELF DO
            BEGIN
            IF connectedDevice = NIL THEN
                currentButton := 2
            ELSE
                IF connectedDevice.id = unknownDevice THEN
                    currentButton := btnNIL;
            END;

        button := TButton(menu.BtnArray.elements[menu.staticButtons]);
        SetPt(startPoint, button.boundr.right + buttonSpace, button.boundr.top - dvMarg); { Location of the "Install a New Device" button }

        device := TDevice(SELF.connectedDevice);
        FOR i := 1 TO deviceList.size DO
            BEGIN
            driver := TCddObject(deviceList.elements[i]);
            IF driver.attachment.HasTextAsName(SELF.name) <> NIL THEN
                IF driver.reveal THEN
                    BEGIN
                    MakeButton(driver);
                    IF device <> NIL THEN
                        IF (device.typeDevice <> printer) AND (device.id = driver.id) THEN
                            currentButton := menu.btnArray.size;
                    END;
            END;

        device := TDevice(SELF.connectedDevice);
        FOR i := 1 TO processList.size DO
            BEGIN
            process := TPrdObject(processList.elements[i]);
            IF process.GetGoodDriver(SELF.name, '') <> NIL THEN
                BEGIN
                MakeButton(process);
                IF device <> NIL THEN
                    IF (device.typeDevice = printer) AND (device.printerID = process.id) THEN
                        currentButton := menu.btnArray.size;
                END;
            END;

        IF currentButton = 0 THEN
            currentButton := 2;

        menu.ibtn := currentButton;
        menu.port := SELF;
        mainForm.InsLast(menu);
    END;

END;

METHODS OF TDevice;

    FUNCTION TDevice.Create(obj: TObject; itsHeap: THz; ch: TChObject; itsPosition: cd_position;
                                                                itsID, itsPrinterID: INTEGER): TDevice;
    VAR i,j:            INTEGER;
        card:           TCard;
        attach:         TNamedObject;
        devName:        TDynamicArray;
        tempList:       TNamedList;
        tempString:     e_name;

    BEGIN
        IF uPrefDbg THEN
            WRITELN('Entering TDevice.Create');
        IF obj = NIL THEN
            obj := NewObject(itsHeap, ThisClass, SIZEOF(SELF));
        tempString := ch.name;
        devName := CreateTextFromString(dynamicHeap, @tempString);
        SELF := TDevice(TPrefPort.Create(obj, itsHeap, devName, itsID));

        WITH SELF DO
            BEGIN
            position := itsPosition;
            typeDevice := ch.typeDevice;
            PrinterID := itsPrinterID;
            IF typeDevice = disk THEN
                bootable := TCddObject(ch).bootable
            ELSE
                BEGIN
                bootable := FALSE;
                IF ch.typeDevice = process THEN
                    SELF.typeDevice := printer;
                END
            END;

    END;

{$IFC CFGDEBUG}
    PROCEDURE TDevice.Print;
    BEGIN
        SUPERSELF.Print;
        CASE SELF.typeDevice OF
            disk:       WRITELN('  disk');
            printer:    WRITELN('  printer with id: ', SELF.printerID);
            process:    WRITELN('  process');
            card:       WRITELN('  card');
            OTHERWISE   WRITELN('  unkown');
            END;
    END;
 {$ENDC}

END;

{$S InitPref}

END. {of UNIT}
ÿ