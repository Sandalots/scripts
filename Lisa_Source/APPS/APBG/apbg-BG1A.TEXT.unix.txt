
Unit Bg1A;                              {'Copyright 1983, 1984, Apple Computer Inc.'}

INTERFACE

uses (*$U UnitStd.obj *)                UnitStd,
     (*$U UnitHz.obj *)                 UnitHz,
     (*$U Storage.obj *)                Storage,
     (*$U QuickDraw.obj *)              QuickDraw,
     (*$U Fontmgr.obj *)                FontMgr,
     (*$U dbenv.obj *)                  dbenv,
     (*$U FEdec.obj *)                  FEdec,
     (*$U fld.obj *)                    fieldedit,
     (*$U teenv.obj *)                  teenv,

     (*$U libos/SysCall.obj    *)       Syscall,
     (*$U FPlib.obj *)                  FPlib,
     (*$U PMDecl *)                     PMDecl,
     (*$U PrStdInfo *)                  PrStdInfo,
     (*$U PrPublic.obj *)               PrPublic,
     (*$U aplc/mm/matmgr.obj *)         matmgr,

     (*$U wm.events.obj *)              events,
     (*$U wm.folders.obj *)             folders,
     (*$U wm.menus.obj *)               menus,
     (*$U AlertMgr.obj *)               AlertMgr,

     (*$U WMLstd.obj  *)                WMLstd,
     (*$U WMlsb.obj   *)                WMlsb,
     (*$U tm.obj      *)                tm,

     (*$U aplc/mm/BGenv.obj *)          BGenv,
     (*$U apbg/Graf2D.obj *)            Graf2D,
     (*$U apbg/ttl.obj *)               ttl,

     (*$U aplc/feglobals.obj *)         feglobals,
     (*$U aplc/comgr.obj *)             coMgr,
     (*$U aplc/TEexec.obj *)            TEexec,
     (*$U aplc/LCfexec.obj    *)        LCfexec,
     (*$U Mathlib.obj *)                Mathlib;

(* This Unit contains the data handling routines for Business Graphics. *)
Type
   tRgrsRec = Record
      RSlope  : Real;
      Intrcpt : Real;
      RSquare : Real;
   end;

VAR
   RgrsArray : Array[1..YListMax] of tRgrsRec;

Procedure GetValReal(rowid, colid: Integer; NANResult: cnSymkind;
                     VAR Val: Real);
Function  BGXAllNumbers: Boolean;
Procedure ResetData;
Procedure SortData(VAR Status: Integer);
Procedure UnsortData(VAR Status: Integer);
Function  GetDataPtr(J: Integer): DataRecHnd;
Function  AdjDataLists(aRg: Range; VAR Status: Integer): Boolean;
Function  XPosCalc(XDataVal: Real): Real;
Function  YPosCalc(YDataVal: Real): Real;
Procedure GetNextPoint(J: Integer; VAR XPos, YPos: Real; VAR Done: Boolean);
Procedure BGCkXCol(aRg: Range; VAR Status: Integer);
Procedure AdjTblLimits;

Procedure SetDataDflt;
Procedure SetAxes;
Procedure SetAxesDflt;
Procedure SetPlotSize;
Procedure SetPlotRect;
Procedure SetPlotBndry;
Procedure InitState;
Procedure ReCalcXAxis;
Procedure ReCalcYAxis;
Procedure CkAxes(aRg: Range);
Procedure BGYAxMenu;
Procedure BGXAxMenu;
Procedure RealToBytes(RealVal: Real; pByteData: ptrData; byteMax: Integer;
                      Before, After: Integer;
                      VAR Len: Integer; VAR Error: boolean);


Procedure SetType   (x: GraphTypes);
Procedure SetPgSize (XPageSize, YPageSize: Real);
Procedure SetBGGrid (XGrid, YGrid: Boolean);
Procedure SetBGLegends(XLegendFlag: Boolean);
Procedure SetProportion( newGraphProportion: TGraphProportion);
Procedure SetPreview(newBGPreview: Boolean);
Procedure SetBGMark(newBGMark: Boolean);


IMPLEMENTATION

(*$R- *)

(*$IFC teSym *)
(*$D+ *)
(*$ELSEC *)
(*$D- *)
(*$ENDC *)


{*** External routines ***}
{ SDY addition 12/4/81. BGYAxCK and BGXAxCk are in file apbg/BG1.0 in unit BG1 }
procedure BGYAxCk( NewMin, NewOrg, NewMax : Real; NewIncr: Real;
              NewMinSet, NewOrgSet, NewMaxSet, NewincrSet: boolean); external;
procedure BGXAxCk( NewMin, NewOrg, NewMax : Real; NewIncr: Real;
              NewMinSet, NewOrgSet, NewMaxSet, NewIncrSet: boolean); external;


{*** forward routines ***}
Procedure CkXAxisRange; forward;
Procedure CkYAxisRange(aRg: Range); forward;




{$S     }
Procedure RealToBytes{(RealVal: Real; pByteData: ptrData; byteMax: Integer;
                      Before, After: Integer;
                      VAR Len: Integer; VAR Error: boolean)};
        { RealtoBytes converts RealVal into byte data and puts it at the
          location pted at by pByteData.

          byteMax = max length of the byte array at pByteData;
          Before  = number of digits to left of decimal place
          After   = number of digits to right of decimal place
          Len     = returned length of the byte string
          Error   = same error returned by the x80 conversion routine }

VAR
   myLongReal: Longreal;
   myString  : resultStr;
   numDigits : Integer;
   Scientific: boolean;  (* set true if Rstr returns scientific notation *)
   myFormat  : byte;
   I         : Integer;
   myBefore  : Integer;
   xNumber   : extended;

Begin
   (* Special case for 0. Returned as '0' if After <= 0; else returned as
      '.00000' with After number of 0's *)
   If (RealVal = 0.0) Then Begin
      If (After <= 0) then Begin
         Len := 1;
         pByteData^[0] := ord('0');
      end
      else begin
         Len := After + 1;
         pByteData^[0] := ord(DataFormat.DecPt);
         For I := 1 to Len-1 do Begin
            pByteData^[I] := ord('0');
         end;
      end;
   end

   Else Begin
      (* Convert the real into a double *)
      S2X(RealVal, xNumber);

      (* Ask for integer format or decimal format according to number of digits
         requested after the decimal place *)
      If (After = 0) Then myFormat := 13
                     Else myFormat := 3;

      (* Call aplc/Lex/RStr to convert it into a string *)
      numDigits := before + after + 2;  (* add 2 for sign and decimal point *)
      RStr(xNumber, myString, After, myFormat, numDigits, Scientific);

      (* Get the length of the string and move the string to the byte array *)
      Len := Length(myString);
      StoB(myString, pByteData, Len);
   end;

   Error := False;
end;


(*$S     *)
Procedure GetValReal(* rowid, colid: Integer; NANResult: cnSymkind;
                     VAR Val: Real*);
(* GetValReal is the interface to the MatMgr routine GetValue. GetValReal
always returns Val as Real, even though the Matmgr routine returns
a longreal, an extended real, or whatever. Basically, this routine does
whatever kluges are necessary to return a Real value in Val *)

VAR
   RealKluge: valueType;
   sgn      : integer;
   myClass  : numClass;
Begin
   (* Call GetValue to get the LongReal number in the matrix *)
   GetValue(RowId, ColId, false, RealKluge);

   (* Translate the longreal into a single real *)
   ClearXcps;
   X2S(RealKluge.val.xrealvar, val);       (* this may set the overflow bit *)
   myClass := ClassX(RealKluge.val.xrealvar, sgn);

   If (myClass = QNAN) or (myClass = SNAN) Then Val := 0
   Else If (myClass = infinite) OR testxcp(overflow) Then Begin
      If sgn = 1 Then Val := -1E38
                 Else Val := +1E38;
   End;
End;


(*$S     *)
Function BGXAllNumbers (*: Boolean *);
(* Returns True if the X column has one or more numbers, and all the rest
   are blanks *)

CONST
   ColConst = 1;

VAR
   TextFound  : Boolean;
   NumberFound: Boolean;
   Result: CellKind;   (* type of cell as returned by the Matmgr routine *)
   I     : Integer;
   Answer: Boolean;    (* working value to be returned                   *)

Begin
   I := 1;
   TextFound := False;
   NumberFound := False;

   (* Keep looking until you find a text item or end of list *)
   While (I <= BGData.XListTot) AND (not TextFound) do Begin
      (* Get the cell type using the routine in file UM (Matmgr file ) *)
      CellType(I, ColConst, Result);
      NumberFound := NumberFound OR (Result = tNumber);
      TextFound   := TextFound   OR (Result = tText);
      I := I + 1;
   End; (* While *)
   BGXAllNumbers := (NumberFound AND (not TextFound));
End;



(*$S     *)
Procedure ResetData;
(* Reset data resets the 'Unread' ptrs to point to the first elt
in each data list *)

VAR
   I:Integer;

Begin
   With BGData do Begin
      Unread := First;
      (* Bump the Unread ptrs past the first sentinel record *)
      For I := 1 to YListMax Do Begin
         If (Unread[I] <> Nil) Then Unread[I] := Unread[I]^^.Next;
      end;
   End; (* With *)
End;


(*$S     *)
Procedure BGInsert(PtrXRec, PtrNewX: DataRecHnd);
(* BGInsert inserts a new X data record in sorted order into the linked
list headed by PtrXRec. BGInsert assumes that there is a minimum X
record at the start of the list, and a maximum X record at the end of
the list. *)

VAR
   myPtr,                     (* ptr to the potential record to insert after*)
   nextPtr:   DataRecHnd;     (* ptr to the comparing record in linked list *)
   NewXVal:   Real;           (* X value of the new X record                *)

Begin
   (* init some things *)
   NewXVal := PtrNewX^^.XVal;
   myPtr   := PtrXRec;
   If (myPtr = Nil) Then exit(BGInsert)
                    else nextPtr := myPtr^^.next;

   (* insert just before the one that's bigger *)
   While (nextPtr <> Nil) Do Begin
      If (NewXVal <= nextPtr^^.XVal) Then Begin
         (* insert the new one before nextPtr. Make the current one point to
         the new one, and the new one point to the next one *)
         myPtr^^.Next   := PtrNewX;
         PtrNewX^^.Next := nextPtr;
         nextPtr        := Nil;
      End (* If *)

      Else Begin
         myPtr    := nextPtr;
         nextPtr  := myPtr^^.next;
      End; (* Else *)
   End; (* While *)
End; (* BGInsert *)


(*$S     *)
Function BGDelete(PtrXRec: DataRecHnd; OldXIndex: Integer): Boolean;
(* BGDelete deletes the first record in the linked list headed by
PtrXRec that has an XIndex of OldIndex. If no record has an index of
OldIndex it acts as a no-op and returns false.
BGDelete assumes that PtrXRec points at a legitimate X record
(i.e. is not Nil )                                                *)

VAR
   myPtr,                 (* ptr to the current record in the data list *)
   nextPtr: DataRecHnd;   (* ptr to the next record in the data list *)

Begin
   (* Assume it's not found *)
   BGDelete := False;

   myPtr := PtrXRec;
   If (myPtr = Nil) Then exit(BGDelete)           (* not enough memory error condition *)
                    Else nextPtr := myPtr^^.next;

   (* Search for and delete the record having an Index of OldIndex *)
   While (nextPtr <> Nil) Do Begin
      If (nextPtr^^.XIndex = OldXIndex) Then Begin
         (* found the one to delete. Delete it by making the previous one
         point around it *)
         myPtr^^.next := nextPtr^^.next;
         FreeH( hzFromH(pointer(ord(nextPtr))), pointer(ord(nextPtr)) ); (* 3/18/83 DisposeHandle -> FreeH *)
         nextPtr := Nil;
         BGDelete := True;
      End (* If *)
      Else Begin
         myPtr   := nextPtr;
         nextPtr := myPtr^^.next;
      End; (* Else *)
   End; (* While *)
End; (* BGDelete *)


(*$S     *)
Procedure NewDataRec(VAR XPtr: DataRecHnd; VAR Status: Integer);
(* Allocates heap storage using the standard storage mgr and returns
a pointer to the storage in XPtr *)

Begin
   (* Using Bill's stuff *)
   XPtr := pointer(ord(AllocBlock(Sizeof(DataRec)))); (* 3/18/83 NewHandle -> AllocBlock *)
   If (XPtr = pointer(ord(Nil)))  Then Status := OutOfMem;
End;



(*$S     *)
Procedure DisposDataPtrs;
(* DisposDataPtrs disposes of all of the data records by returning all
the storage to the heap *)

VAR
   I : Integer;
   CurDataPtr,               (* pointer to current data record *)
   NextDataPtr: DataRecHnd;  (* ptr to the next data record *)

Begin
   For I := 1 to YListMax do Begin
      (* Get the first ptr out of each list *)
      CurDataPtr := BGData.First[I];

      (* Dispose of all the records in this list *)
      While (CurDataPtr <> Nil) Do Begin
         NextDataPtr := CurDataPtr^^.Next;
         FreeH( hzFromH(pointer(ord(CurDataPtr))), pointer(ord(CurDataPtr)) ); (* 3/18/83 DisposeHandle -> FreeH *)
         CurDataPtr := NextDataPtr;
      End; (* While *)

      (* Reset the first data ptr to Nil *)
      BGData.First[I] := Nil;
   End; (* For *)
End; (* DisposDataPtrs *)


(*$S     *)
Procedure InitDataPtrs(VAR Status: Integer);
(* InitDataPtrs inits all the 'sentinel' records at the beginning and end
of the sorted data lists used in plotting out the line chart. Each list
looks like this after initialization:
                           ---------                ---------
                           ! -1    !                ! -2    !  XIndex
                   -----   --------!                --------!
BGData.First[I]--->! --!-->!-BigNum!                !+BigNum!  XVal
                   -----   ---------    -------     --------!
                           !  -----!--->!  ---!---->! Nil   !  Next
                           ---------    -------     ---------

The XVal is sorted in increasing order for linegraph and scatter so they
go from -BigNum to +BigNum.
                                                                   *)

VAR
   XPtr1,                     (* Ptr to the first sentinel record *)
   XPtr2  : DataRecHnd;       (* Ptr to the second sentinel record*)
   I,J    : Integer;

Begin
   For I := 1 to YListMax (* defined in apbg/BG2 *) Do Begin
      NewDataRec(XPtr1, Status);
      If (Status = OK) Then Begin
         (* Make the head of the list pt at the first sentinel record *)
         BGData.First[I] := XPtr1;

         (* Set up the first sentinel record. BigNum is a global Var in BG2,
            initted in BG1A/InitFltPt *)
         With XPtr1^^ Do Begin
            XIndex := -1;
            XVal   := -BigNum;
            Next   := Nil;
         End; (* With *)

         (* Set up the second sentinel record *)
         NewDataRec(XPtr2, Status);
         If (Status = OK) Then Begin
            XPtr1^^.Next := XPtr2;
            With XPtr2^^ DO Begin
               XIndex := -2;
               XVal   := +BigNum;
               Next   := Nil;
            End; (* With *)
         end;
      end

      (* not enough room to allocate the first sentinel record *)
      else Begin
         BGData.First[I] := Nil;
      end;
   End; (* For *)
End; (* InitDataPtrs *)


(*$S     *)
Procedure SortData(* VAR Status: Integer *);
(* SortData sorts the data records according to increasing X value.
The Line chart and scattergram use this *)

VAR
   I,                     (* Index to current data list     *)
   J         : Integer;
   XPtr      : DataRecHnd;(* Ptr to a new X record          *)
   NANResult : cnSymKind; (* tests for Not A Number (NAN)   *)

Begin
   (* Reinit all the ptrs and the sentinel characters. This should
   do a dispose or something to return the memory to the OS  *)
   DisposDataPtrs;
   InitDataPtrs(Status);

   (* Go down the X data list *)
   For J := 1 to BGData.XListTot Do Begin
      (* If there's an X value there then ck all the Y values *)
      If (CellHasValue(J, 1)) Then Begin

         For I := 1 to BGData.RepNum do Begin
            (* If there's a Y value then insert it into the Ith data list.
            Use I+1 here since must skip the X data in the first column *)
            If (CellHasValue(J, I+1)) Then Begin

               (* Build a new X record *)
               NewDataRec(XPtr, Status);
               If (Status = OK) Then Begin
                  With XPtr^^ Do Begin
                     XIndex := J;
                     GetValReal(J, 1, NANResult, XVal);
                     Next   := Nil;
                  End; (* With *)

                  (* Insert the X record into the Ith data list *)
                  BGInsert(BGData.First[I], XPtr);
               End; (* If on Status *)
            End; (* If *)
         End; (* For *)
      End; (* If *)
   End; (* For on J *)
End; (* SortData *)


(*$S     *)
Procedure UnsortData(* VAR Status: Integer *);
(* UnsortData gets the data values straight from the Table mgr in
their order in the table mgr. Thus the data records are unsorted *)
Begin
   DisposDataPtrs;
End;


(*$S     *)
Function GetDataPtr(*J:Integer):DataRecHnd*);
(* GetDataPtr returns a ptr to the next data record in data list J.
If there are no more it returns NIL. It bumps the Unread ptr to
point to the next elt in the Jth data list *)

VAR
   DataPtr: DataRecHnd;

Begin
   (* Return the ptr to the next unread data record. Bump the
   unread ptr if you're not at the end of the list *)
   With BGData Do Begin
      DataPtr := Unread[J];

      (* Check for error condition i.e. not enough memory to even set up sentinel records *)
      If (DataPtr = Nil) Then GetDataPtr := Nil

      (* Return Nil if you're pointing at the sentinel record *)
      else If (DataPtr^^.Next = Nil) Then GetDataPtr := Nil

      Else Begin
         (* Return the pointer to an actual data record, bump the unread ptr *)
         GetDataPtr := DataPtr;
         Unread[J]  := DataPtr^^.Next;
      End; (* Else *)
   End; (* With *)
End; (* GetDataPtr *)


(*$S     *)
Function AdjLineList(aRg: Range; VAR Status: Integer): Boolean;
(* AdjLineList adjusts the linked list of the X data records
for a line chart or scattergram according to the range aRg that has
just changed. It returns true if the graph should be replotted.
Basically it changes the linked list so that the points will be
plotted in the proper order the next time the graph is redrawn *)

VAR
   YList      : Integer;         (* index into the Y data lists     *)
   XPtr       : DataRecHnd;      (* ptr to a new X data record      *)
   NANResult  : cnSymKind;       (* Not A Number result, see aplc/mm/Calc *)
   I          : Integer;
   myBool     : Boolean;

Begin (* AdjLineLists *)
   (* Assume no replot till we find otherwise *)
   myBool := False;

   With aRg Do Begin  (* aRg type declared in libte/teEnv *)
      Case rKind of

        aNullRg : Begin (* Do Nothing *) End;

        a1CellRg: Begin
           (* if it's an X cell, delete the old X data record
           from all the active linked lists, then insert the
           new X record into all the active linked lists *)

           If (loCol = 1) Then Begin

              (* For each list, delete the record with the old XIndex,
                build a new record, insert it into the list *)
              For I := 1 to BGData.RepNum do Begin
                 (* Delete the record with the old XIndex *)
                 myBool := BGDelete(BGData.First[I], loRow) OR myBool;

                 (* Insert a new record if X and Y both have a value *)
                 If (CellHasValue(lorow, 1) AND CellHasValue(lorow, I+1)) Then Begin
                    myBool := True;

                    (* Build up a new X data record. The XVal is in table column one *)
                    NewDataRec(XPtr, Status);
                    If (Status = OK) Then Begin
                       With XPtr^^ Do Begin
                          XIndex := lorow;
                          GetValReal(lorow, 1, NANResult, XVal);
                          Next   := Nil;
                       End; (* With *)

                       (* Insert the new X record into the Ith data list *)
                       BGInsert(BGData.First[I], XPtr);
                    end; (* if on status *)
                 End; (* If *)
              End; (* For *)
           End (* If *)

           (* if it's a Y cell, delete the old X data record from
           that Y list only, and insert the new one into the list *)

           Else If ((2 <= loCol) and (loCol <= 9)) Then Begin
              myBool := BGDelete(BGData.First[loCol-1], loRow);

              (* Insert a new record if the X and Y cell both have a value *)
              If (CellHasValue(lorow, 1) AND CellHasValue(lorow, locol)) Then Begin
                 myBool := True;

                 (* build a new X data record. Get the XVal out of the
                    X data column i.e. column one *)
                 NewDataRec(XPtr, Status);
                 If (Status = OK) Then Begin
                    With XPtr^^ do Begin
                       XIndex := loRow;
                       GetValReal(loRow, 1, NanResult, XVal);
                       Next   := Nil;
                    End; (* With *)

                    BGInsert(BGData.First[loCol-1], XPtr);
                 end; (* if on status *)
              End; (* If *)
           End; (* Else If *)
        End; (* a1CellRg *)

        aRectRg: Begin
           (* Sort the data and replot only if the changed table data is within Column J *)
           If (locol <= YListMax + 1) Then Begin
              SortData(Status);
              myBool := True;
           end;
        end;

        Otherwise Begin
           SortData(Status);
           myBool := True;
        End; (* Otherwise *)

     End; (* Case *)
  End; (* With *)

  AdjLineList := myBool;
End; (* AdjLineLists *)


(*$S  BGPie  *)
Function AdjPieList(aRg: Range; VAR Status: Integer): Boolean;
(* AdjPieList adjusts the linked list for a pie chart. This could
optimize the code here for when aRg is only a 1-cell range. Returns
True if the graph needs to be redrawn. *)
VAR
   Replot : Boolean;

Begin
  With aRg Do Begin  (* aRg type declared in libte/teEnv *)
     Case rKind of

        aNullRg : Replot := False;

        (* Replot if the user entered a new value into the second column,
        OR if he entered a new value into the 1st column and it makes a data pair *)
        a1CellRg: Replot := (HiCol = 2) OR ((HiCol = 1) AND CellHasValue(loRow, 2));

        aRectRg : Replot := (locol <= 2);

        Otherwise Replot := True;
     end;
  end;

  (* Sort the data if we're going to replot it *)
  AdjPieList := Replot;
End;


(*$S     *)
Function AdjDataLists(* aRg: Range; VAR Status: Integer): Boolean *);
(* Adjusts the data lists according to the current graph type. Returns
   True if graph should be redrawn. *)

Begin
   Case GType of
      Bar, BarLine, BarScat, StackBar,
      SolidBar, TextAreaGraph: Begin
         Case aRg.rKind of
            aNullRg : AdjDataList := False;
            a1CellRg,
            aRectRg : AdjDataList := (aRg.loCol <= YListMax + 1);
            otherwise AdjDataList := True;
         End;
      End;

      LineGraph, Scatter, NumbAreaGraph: Begin
         AdjDataList := AdjLineList(aRg, status);
      End;

      Pie: Begin
         AdjDataLists := AdjPieList(aRg, status);
      End;
   End; (* Case *)
End; (* AdjDataLists *)


(*$S     *)
Function XPosCalc(*XDataVal:Real):Real*);
(* XPosCalc calculates the world coordinate X position of the
X data point in XDataVal *)

VAR
   XDataDiff: Real;  (* difference between XDataVal and min val on X Axis *)
   XDataTot : Real;  (* difference in data value along entire X axis      *)

Begin
   With XAxis Do Begin
      XDataDiff:= XDataVal-Min;
      XDataTot := Max-Min;

      (* The world coord position is proportional to the data value's
         distance along the entire length of the X axis  *)
      XPosCalc := XPos1 + Delta*(XDataDiff/XDataTot);
   End; (* With *)
End; (* XPosCalc *)


(*$S     *)
Function YPosCalc(*YDataVal:Real):Real*);
(* YPosCalc calculates the world coordinate Y position of the
Y data point passed in in YDataVal *)

VAR
   YDataDiff: Real;  (* difference between YDataVal and min val on Y Axis *)
   YDataTot : Real;  (* difference in data value along entire Y axis      *)

Begin
   With YAxis Do Begin
      YDataDiff:= YDataVal-Min;
      YDataTot := Max-Min;

      (* The world coord position equals YPos1 - (the proportional
         height along the Y axis). It's negative since -Y goes up the page *)

      YPosCalc := YPos1 - Delta*(YDataDiff/YDataTot);
   End; (* With *)
End; (* YPosCalc *)



(*$S     *)
Procedure GetNextPoint(*J: Integer; VAR XPos, YPos: Real; VAR Done: BOOLEAN *);
(* GetNextPoint returns the world coordinate position (XPos,YPos)
of the next data point in the Jth data list *)

VAR
   DataPtr : DataRecHnd;  (* ptr to the next data record in the Jth list *)
   XDataVal: Real;        (* X value out of the data record              *)
   YDataVal: Real;        (* Y value out of the data record              *)
   I       : Integer;     (* Dummy variable                              *)

   Rowid,
   ColId   : Integer;     (* indices into the matrix                     *)
   NANResult: cnSymKind;  (* see if the value is Not A Number - NAN      *)
   Status  : Integer;

Begin

   (* Each data list contains a set of data records that looks like this:

             !---------!                  !--------!
             !XIndex   !                  !XIndex  !
             !---------!        !----!    !--------!
             ! :--------------->!  --!--->! :----------------->
             !---------!        !----!    !--------!

     where XIndex is a row index into the X column in the matrix and signifies
     that that X-Y data pair belongs in the data list.
     GetDataPtr returns the pointer to the next data record in data list J *)


   DataPtr:= GetDataPtr(J);
   Done   := (DataPtr = Nil);

   If (Not Done) Then Begin
      (* Get the X and Y data values *)
      XDataVal := DataPtr^^.XVal;

      (* The Y value is in the XIndex row, the Jth+1 column. (Remember that
      the X column is the first one, Y1 is the second, Y2 is the third ... *)
      Rowid := DataPtr^^.XIndex;
      ColId := J+1;
      GetValReal(RowId, ColId, NANResult, YDataVal);

      (* Now calculate the X and Y position of these data values *)
      XPos := XPosCalc(XDataVal);
      YPos := YPosCalc(YDataVal);
      Done := False;
   End ;
End; (* GetNextPoint *)


(*$S     *)
Procedure BGCkXCol(* aRg: Range; VAR Status: Integer *);
(* Procedure to check and adjust the graph type if the X column contains
a strange value e.g. if it's a line chart needing all numbers and the X
column contains a text string. The GType is then changed to BarLine. *)

Begin
   (* only do something if the range contains an X value *)
   If (aRg.loCol = 1) OR (aRg.rKind = aWTBoxRg) Then Begin

      Case GType of

         BarLine: Begin
            (* BarLine assumes some text in X column so change to a line graph
               if its all numbers *)
            If (BGXAllNumbers) Then Begin
               GType := LineGraph;
               SortData(Status);     (* sorts on increasing X. See file BG1A *)
               CkXAxisRange; (* set data min and max, adjust X axis. In BG1A *)
            End;
         End;

         LineGraph: Begin
            (* LineGraph assumes all numbers in X column *)
            If (Not BGXAllNumbers) Then Begin
               UnSortData(Status);
               GType := BarLine;
            end;
         End;

         BarScat: Begin
            (* BarScat assumes there's some text in X column so change to a
               scattergram if it's all numbers *)
            If (BGXAllNumbers) Then Begin
               GType := Scatter;
               SortData(Status);
               CkXAxisRange;
            end;
         end;

         Scatter: Begin
            (* Scatter assumes all numbers in X so change to a BarScat if
               there's some text *)
            If (not BGXAllNumbers) Then Begin
               UnSortData(Status);
               GType := BarScat;
            end;
         end;

         NumbAreaGraph: Begin
            {* NumbAreaGraph assumes all numbers in X }
            If (not BGXAllNumbers) Then Begin
               UnsortData(Status);
               GType := TextAreaGraph;
            end;
         end;

         TextAreaGraph: Begin
            { TextAreaGraph assumes there's at least one text entry in X }
            If (BGXAllNumbers) Then Begin
               SortData(Status);
               GType := NumbAreaGraph;
            end;
         end;
      end; { Case }
   End; (* If *)
End;


(*$S     *)
Procedure AdjTblLimits;
(* AdjTblLimits sets BGData.XListTot to the row number of the last
row in the data table containing a data value. It also sets
BGData.RepNum to the (column number - 1) of the last column in the data
table containing a data value. *)

VAR
   FinalRow: IdType;
   FinalCol: IdType;


   Function RowBlank( Row : IdType; FinalCol : IdType) : Boolean;
   (* RowBlank returns True iff all the cells in the row up to FinalCol are blank *)

   VAR
      AllBlank: Boolean;
      CurCol  : IdType;

   Begin
      (* Assume they're all blank till you find a full cell *)
      AllBlank := True;
      RowBlank := True;

      CurCol   := 1;

      While ((AllBlank) and (CurCol <= FinalCol)) Do Begin
         If (CellBlank(Row, CurCol)) Then Begin
            (* Cell is blank so look at the next one *)
            CurCol := CurCol + 1;
         End
         Else Begin
            (* Cell is full so the row is not blank *)
            RowBlank := False;
            AllBlank := False;
         End;
      End; (* While *)
   End; (* RowBlank *)


   Function ColBlank( FinalRow : IdType; Col : IdType) : Boolean;
   (* ColBlank returns True iff all the cells in the Col up to FinalRow are blank *)

   VAR
      AllBlank: Boolean;
      CurRow  : IdType;

   Begin
      (* Assume they're all blank till you find a full cell *)
      AllBlank := True;
      ColBlank := True;

      CurRow   := 1;

      While ((AllBlank) and (CurRow <= FinalRow)) Do Begin
         If (CellBlank(CurRow,Col)) Then Begin
            (* Cell is blank so look at the next one *)
            CurRow := CurRow + 1;
         End
         Else Begin
            (* Cell is full so the row is not blank *)
            ColBlank := False;
            AllBlank := False;
         End;
      End; (* While *)
   End; (* ColBlank *)

Begin (* AdjTblLimits *)

   (* Get the final snips as stored in the matrix mgr. These may include
   Cells with no data in them that just include formatting information *)
   FinalRow := FinalSnip(RowKind);
   FinalCol := FInalSnip(ColKind);

   (* ignore any data past the 9th column i.e. 1 col for X data and 8 cols
      for Y data *)
   If (FinalCol > 9) Then FinalCol := 9;

   (* start peeling back rows that have no data values in their cells *)
   While ((FinalRow >= 1) and (RowBlank(FinalRow, FinalCol))) Do Begin
      FinalRow := FinalRow - 1;
   End;

   (* Start peeling back columns that have no data values in their cells *)
   While ((FinalCol >= 1) and (ColBlank(FinalRow, FinalCol))) Do Begin
      FinalCol := FinalCol - 1;
   End;

   (* Subtract 1 from the number of columns to get the number of Y lists
   since the X list is in the first column *)
   With BGdata do Begin
      RepNum   := FinalCol - 1;
      XListTot := FinalRow;

      If (RepNum > 8) Then RepNum := 8;
   End; (* With *)
End; (* AdjTblLimits *)

(*$I apbg/BG1B.text*)
ÿ