unit lmprint;
{Copyright 1983,1984, Apple Computer Inc.}

interface
{$SETC ForOS := TRUE }
{$DECL IsIntrinsic}
{$SETC IsIntrinsic := TRUE}
{$DECL UTProblems}
{$SETC UTProblems := FALSE}
{$DECL WithUObject}
{$SETC WithUObject := FALSE}

{$IFC NOT WithUObject}
    {$SETC LibraryVersion := 30 } { 10 = 1.0 libraries; 13 = 1.3 libraries; 10 = Pepsi, 30 = Spring, etc. }
{$ENDC}


uses {$U  UnitStd  }  UnitStd,
     {$U  UnitHz   }  UnitHz,
     {$U  Storage  }  Storage,
     {$U  QuickDraw}  QuickDraw,
     {$U  fontmgr }   fontmgr,
     {$U  dbEnv}     dbEnv,
     {$U  fedec}     feDec,
     {$U  fld}       fieldEdit,
     {$U  teEnv}     teEnv,
     {$U  dbdecl1.Obj} dbdecl1,
     {$U  libos/syscall.Obj} syscall,
     {$U  wm.events.obj} events,
     {$U  wm.folders.obj} folders,
     {$U  wm.menus.obj} menus,
     {$U  alertmgr} alertmgr,
     {$U  apll/dbconvert} dbconvert,

     {$U  lowlevel.Obj} lowlevel,
     {$U  pmdecl}   PmDecl,
     {$U  PrStdInfo  }  PrStdInfo,
     {$U  PrPublic}  PrPublic,
     {$U  unitFile.obj}    unitFile,
     {$U  unitFmt.obj}     unitFmt,
     {$U  unitCs.obj}      UnitCs,
     {$U  unitFF.obj}      unitFF,
     {$U  uunivtext}       UUniversalText,
{$IFC SAMPCALLS}
     {$U  apll/matmgr}    lmmat,
     {$U  apll/tracecalls.obj} tracecalls;
{$ELSEC}
     {$U  apll/matmgr}    lmmat;
{$ENDC}


procedure PrintList;

implementation

{$R-}

{$IFC MMSYMBOLS}
{$D+}
{$ELSEC }
{$D-}
{$ENDC}

{$S lmprint }

const
        zero = 0;
        LstPrint = 'lmprint ';

        maxString = 130;
        nHedLns =       2;              {how many lines before first row}
        nFootLns = 0;                   {number of footer lines}
        npgoffsetlns = 0;               {number of rows before title}
        leftmargin = 4;                 {number of chars left margin}
        printldsn = 1;                  {ldsn for printing}
                           {number of linex before page end}

type
        RCSnip = (rowSnip, colSnip);
        axis = VHSelect { =(v, h) };

var
   sKindOfAxis:    array[axis] of snipKind; {lexicon}

   fstTbl:         array[RCSnip] of idType;{initial    tbl snip to print}
   limTbl:         array[RCSnip] of idType;{beyond last   "   "    " }

   scrChD:         array[axis] of integer; {Screen Character width or ht }
   ttyChD:         array[axis] of integer; {Printer  "         "   "     }
   ttyPgD:         array[axis] of integer; {Printer  Page      "   "     }

   pgChars:        array[axis] of integer; {num of chars fitting accross
                                                or down a print page}

   offset:         array[axis] of integer; {origin offsets for printable page}

   chBetween:      array[axis] of integer; {num of blanks between cells}
   DotsBetween:    array[axis] of integer; {distance         "      "  }

   pageNum:        integer;                {current page number}

   sfontid,                                {screen font id}
   pfontid : tlfntid;                      {printer font id}


   PrintPreference : TPrRec;               {documents print prefernce}


PROCEDURE CalcPgLimits (currow:idtype; curcol:idtype;
                       var nextcol:idtype; var width:integer);forward;
procedure initPrint    (VAR sfontid,pfontid:tlfntid); forward;
procedure printBlanks (count: integer); forward;
procedure printCell   (row,col:idtype;hh,vv:integer); forward;
procedure printHed    (hh,vv,width:integer); forward;
procedure printHRule  (hh,vv,length:integer;row,col:idtype); forward;
procedure printPage   (startrow,startcol,endcol:idtype;
                       var nextrow:idtype;width:integer;pagenum:integer;
                       var numrows:integer);forward;
procedure printRow    (currow:idtype;startcol : idtype; var nextcol:idtype;
                        vv:integer); forward;
procedure printVRule  (hh,vv,length:integer;row,col:idtype); forward;

procedure prBotRow(currow : idtype; startcol:idtype; nextcol:idtype; vv:integer);
                        forward;
procedure prBotCell(row,col:idtype;hh,vv:integer);forward;
function  snipChars   (aSnip: idType; u: axis): integer; forward;
procedure skipPage   (startrow,startcol,endcol:idtype;
                       var nextrow:idtype;width:integer;pagenum:integer;
                       var numrows:integer);forward;




PROCEDURE CalcPgLimits{currow:idtype; curcol:idtype;
                        var nextcol:idtype; var width:integer};
     {Calculate the startcol, the col past the last col to be printed on this
       page, and calculate the width of the table in dots}
     var
        lineFull:       boolean;
        col : idtype;
        cellhchars: integer;
        celldots : integer;
        cumchars : integer;
        cumDots : integer;

     BEGIN
{$IFC SAMPCALLS}  ALogCall;  {$ENDC}
     if tracelmprint then writeln('CalcPgLimits');
     lineFull := False;
     col := curcol;
     cumchars := 0;
     cumdots := offset[h] + (leftmargin*ttychd[h]);

     {skip row headers if in list or search table}
     if ((curTable = tlist)OR (CurTable = tsearch)) and (curcol = minfsnip) then
        getsnipid(colkind,mmnext,0,minfsnip, col);

     while NOT((col = limTbl[colSnip]) OR  lineFull) do
        begin
        cellhchars := snipChars(col, h);
        if tracelmprint then writeln('cellhchars',cellhchars:4);
        cellDots := cellhChars*ttyChD[h];
        if (cumdots + (cellhChars*ttychd[h]) + (chBetween[h]*ttychd[h])) <
                 ((pgChars[h]*ttychd[h]) + (leftmargin * ttychd[h])) then
           begin
           getsnipid(colkind,mmnext,0,col,col);
           cumchars := cumchars + cellhchars + chBetween[h];
           cumDots := cumDots + cellDots + (chBetween[h]*ttyChD[h]);
           end
        else
           begin
           lineFull := True;
           end;
        end;
     cumchars := cumchars - chBetween[h];
     cumDots := CumDots - ((chBetween[h] * ttyChD[h]) div 2);
     width := cumDots + (dotsbetween[h] div 2) - (leftmargin*ttychd[h])- offset[h];
     nextcol := col;


     END;

procedure initPrint{VAR sfontid,pfontid:tlfntid};
        {init  our data structures}
     var
        scAscent, scDescent:     integer;
        finfo : fontinfo;
        fmetreturns : boolean;
        ferror : integer;
        tempfontid : tlfntid;

     begin
{$IFC SAMPCALLS}  ALogCall;  {$ENDC}

     sfontid := mmfontinfo.font;
     pfontid := mmfontinfo.DMPfont;

     PrintPreference := mmprintpreference;

     {Tell font manager not to substitute a font????}

     if tracelmprint then with PrintPreference,pfontid do
        begin
        writeln('pfontid: ');
        writeln('fam wid face', ord(fam),
          bold in seteface,
          italic in seteface,
          shadow in seteface);
        writeln('resolution h,v ',prinfo.hres:5,prinfo.vres:4);
        with prinfo.rpaper do
        writeln('rpaper',left,top,right,bottom);
        with prinfo.rprintable do
        writeln('rprintable',left,top,right,bottom);

        end;

     sKindOfAxis[v] := rowKind;
     sKindOfAxis[h] := colKind;

     fmetreturns := fmfontmetrics(sfontid,finfo,ferror);
     if mmtrap1 then if ferror <> 0 then writeln('fmfontmetrics returns',ferror);
     if tracelmprint then with finfo do
        begin
        writeln(' ascent,descent,widmax,leading ',ascent:3, descent:3,widmax:3,leading:3);

        end;

     with finfo do
       begin
       scascent := ascent;
       scdescent := descent;
       scrchd[v] := ascent+descent;
       scrchd[h] := widmax;
       end;

     prlfntid(printpreference,pfontid.fam,[],tempfontid);
     if fmfontmetrics(tempfontid,finfo,ferror) then ;
     pfontid := tempfontid;

     with pfontid do
       begin
       seteface := [];
       dev := printpreference.prinfo.adev.dev;
       end;

     if tracelmprint then with finfo do
        begin
        writeln( 'a,d,w,l ', ascent:3, descent:3,widmax:3,leading:3);
        end;

     ttyChD[v] := finfo.ascent+finfo.descent;
     ttyChD[h]:= finfo.widmax;
     with PrintPreference.prinfo.rprintable do
        begin
        ttyPgD[v] := bottom - top;
        ttyPgD[h] := right - left;
        offset[h] := left;
        offset[v] := top;
        end;
     chBetween[h] := 1;
     chBetween[v] := 0;
     DotsBetween[h] := chBetween[h]*ttyChD[h];
     with finfo do
     DotsBetween[v] :=  (*leading*) cmax(((ascent+descent+leading) div 6),leading);
     if tracelmprint then writeln ('dotsbetween[v] ',dotsbetween[v]:3);



     pgChars[h] := (ttyPgD[h] div ttyChD[h]) - leftmargin;
     pgChars[v] := (ttyPgD[v] div (ttyChD[v]+dotsbetween[v])) - nFootLns
                     - npgoffsetlns;

     if tracelmprint then writeln('chars per row/column = ',
                                     pgChars[h], pgChars[v]);

     {determine which rectangular range of cells to print}
     fstTbl[rowSnip] := mInfSnip;
     fstTbl[colSnip] := mInfSnip;
     limTbl[colSnip]:= PInfSnip;
     limTbl[rowSnip] := PInfSnip;;


     end;


procedure PrintList;

   label 10;

   const LMP = 'PrintList';

   var
       currow,
       curcol,
       nextrow,
       nextcol : idtype;
       ncol : idtype;
       rpg : rect;
       width : integer;
       nextprintrow,
       row         : idtype;
       samepage : boolean;
       numrows : integer;
       i : integer;
       status : integer;
       PrPort : TPrPort;

   begin
{$IFC SAMPCALLS}  ALogCall;  {$ENDC}

   (*FbackGround := TRUE;*)

   if tracelmprint then writeln(LMP,'; PrDocStart');

   {initialize lmprint stuff}
   initPrint(sfontid,pfontid);

   if tracelmprint then with PrintPreference do
      begin
      writeln('PrVersion : ',prversion);
      writeln('PrLDSN : ',PrLDSN);
      writeln('PrJobInfo -');
      with PrJobInfo do
        begin
        writeln('   DocPgFst,DocPgLst : ',DocPgFst:8,DocPgLst:8);
        writeln('   SpooledPages : ',SpooledPages);
        writeln('   Copies : ',copies);
        writeln('   Immediate : ',immediate);
        end;
      writeln('PrInfo -');
      with PrInfo do
        begin
        writeln('   PrinterID : ',PrinterID);
        writeln('   NumColor : ',NumColor);
        writeln('   HRes,Vres : ',Hres:8,Vres:8);
        with rpaper do
          writeln('   rpaper',left,top,right,bottom);
        with rprintable do
          writeln('   rprintable',left,top,right,bottom);
        end;
      end;

   {Open Printer}
   if PrDocStart(PrintPreference,PrPort,PrintLDSN,TRUE) then
     begin

     currow := fstTbl[rowsnip];
     curcol := fstTbl[colsnip];
     pageNum := 1;
     while currow <> limTbl[rowSnip] do
       begin
       NextPrintRow := curRow;
       samepage := false;
       while curcol <> limTbl[colSnip] do
         begin
         if tracelmprint then writeln(lmp,'; currow, curcol : ',currow:8,curcol:8);
         CalcPgLimits(currow,curcol,nextcol,width);

         if tracelmprint then writeln(LMP,'; PrStartPage');
         if PrPageStart(PrintPreference,PrPort) then
           begin
           TextFont ( pfontid.fam );

           {now make sure  that the cache is ok }
           if samepage then
              begin {back up to top of page}
              row := nextPrintrow;
              i := 1;
              while i <= numrows do
                begin
                getsnipid(rowkind,mmprior,0,row,row);
                i := i+1;
                end;
              curRow := row;
              end;

           if tracelmprint then writeln(lmp,'; PrintPage');
           PrintPage(curRow,Curcol,nextcol,nextrow,width,
                    pagenum,numrows);
           end
         else       {PrStartPage FALSE}
           begin
           if samepage then
              begin {back up to top of page}
              row := nextPrintrow;
              i := 1;
              while i <= numrows do
                begin
                getsnipid(rowkind,mmprior,0,row,row);
                i := i+1;
                end;
              curRow := row;
              end;

           if tracelmprint then writeln(LMP,'; SkipPage');
           SkipPage(curRow,Curcol,nextcol,nextrow,width,
                    pagenum,numrows);
           end;     {PrStartPage FALSE}

         samepage := true;
         nextPrintRow := nextrow;

         if PrPageEnd(PrintPreference,PrPort) then goto 10;
         if tracelmprint then writeln(LMP,'; PrPageEnd FALSE');

         curCol := NextCol;
         pageNum := pageNum + 1;
         end; {cols}

       samepage := false;
       curRow := nextPrintRow;
       curCol := fstTbl[colsnip];
       end; {rows}

       end;
10:
   if tracelmprint then writeln(LMP,'; PrPageEnd TRUE; at PrDocEnd');
   PrDocEnd(PrintPreference,PrPort);

   end;





procedure printBlanks {count: integer;var newh:integer};
        {print count blanks}

      begin
{$IFC SAMPCALLS}  ALogCall;  {$ENDC}
      (*if tracelmprint then writeln('printblanks');*)
      move( count*ttychd[h],0);
      end;


procedure printCell{row,col:idtype;hh,vv:integer};
        {print the cell identified by row,col at hh,vv}
     var
        cellLen:                integer;
        cellhchars,
        cellvchars    : integer;
        cellStr:        array[0..maxString] of byte;
        cInfo:                  CellRecord;
        status:                 integer;
        preSpace:               integer;

     begin
{$IFC SAMPCALLS}  ALogCall;  {$ENDC}
     (*if tracelmprint then writeln('printcell');*)
     {init}
     if row <> minfsnip then
        begin
        if curtable <> tlist then
          moveto(hh+(dotsbetween[h] div 2), vv+ttychd[v]+(dotsbetween[v] div 2))
        else
          moveto(hh+(dotsbetween[h] div 2), vv+ttychd[v]+dotsbetween[v] );

        end
     else
        begin {center title}
        moveto (hh+(dotsbetween[h] div 2),vv+ttychd[v] + ttychd[v] div 2);
        end;

     cellhchars := snipchars(col,h);

     if (row <> mInfSnip) or (col <> mInfSnip) then
        begin
        {print the value and formula}
        cellLen := MaxString;
        cellValue(mmRead, row, col,
                @cellStr, False, cellLen, status);
        IF CellLen > CellhChars THEN CellLen := CellhChars;

        cellInfo(CRecord, mmRead, row, col, cInfo);
        case cInfo.align of
           left:        preSpace := 0;
           center:      preSpace := (cellhChars - cellLen) div 2;
           right:       preSpace := cellhChars - cellLen;
           end;

        printBlanks(preSpace);
        if (row = minfsnip) or (col = minfsnip) then
                       pfontid.seteface := [bold]
        else pfontid.seteface := [];
        textface(pfontid.seteface);
        DrawText(@cellStr, 0, cellLen);

        end;

    {Print a line under the headers only}
    If (row = MInfSnip) THEN
       printHRule(hh,vv+(2*ttychd[v])+(dotsbetween[v] div 2),
                  cellhchars*ttychD[h]+DotsBetween[h],row,col);

    IF (CurTable = tcolspecs) AND (Row <> MinfSnip) THEN
       printHRule(hh,vv+ttychd[v]+(2*dotsbetween[v]),
                  cellhchars*ttychD[h]+DotsBetween[h],row,col);

    {Always print the column separator}
    cellvchars := snipchars(row,v);
    if (curtable = tcolspecs)  then
       printVRule(HH,VV,
                  cellvchars*(ttychD[v]+dotsbetween[v])+dotsbetween[v],
                  row,col)
    else
       printVRule(HH,VV,cellvchars*(ttychD[v]+dotsbetween[v]),row,col);
    end;



procedure printHed{hh,vv,width:integer};
   {print the header}

   var hedTxt: string[255];
       pgTxt:  string[255];
       digTxt: string[10];
       titlelen : integer;
       charwidth : integer;
       prespace : integer;
       pagelen : integer;
       bptr : ptrdata;
       titlesize : integer;

   begin
{$IFC SAMPCALLS}  ALogCall;  {$ENDC}
   (*if TraceLMPrint then writeln('pr printHed');*)
   charwidth := width div ttychD[h];
   if tracelmprint then writeln('charwidth ' , charwidth);

   hedTxt := ListTitle;
   if HedTxt = '' then hedtxt := lfname
   else
      begin
      {not the list so tack on from filename }
      {for createing or redefining substitute col specs string}

      if curtable = tColSpecs then getalert(lmalerts,coldescr,@hedtxt);
      getalert(lmalerts,fromstr,@pgtxt);
      if tracelmprint then writeln(length(lfname),lfname);

      hedtxt := concat(hedtxt,' ',pgtxt,' ',lfname);
      if tracelmprint then writeln(length(hedtxt),hedtxt);
      end;

   GetAlert(LMALERTS,pagestr,@pgTxt);
   pgTxt := Concat(pgtxt,' ');

   titlelen := Length(HedTxt);
   titlesize := length(hedtxt)+length(pgtxt) + 4;

   if (charwidth > titlelen) then
      begin
      prespace := (charwidth - titlelen) div 2;
      if (titlesize+prespace > pgchars[h]) then
         begin
         prespace := 0;
         end
      else
         begin
         PrintBlanks(prespace);
         end;
      end
   else
      begin
      prespace := 0;
      end;

   if (titlesize >= pgchars[h]) then                 {#lm191}
      begin
      {truncate name}
      hedtxt := copy(hedtxt,1,length(hedtxt) -(titlesize-pgchars[h])-2 );
      if tracelmprint then writeln('hedtxt len', length(hedtxt), titlesize);
      end;
   pfontid.seteface := [bold];
   textface(pfontid.seteface);
   DrawText(@hedTxt, 1, length(hedtxt));
   pfontid.seteface := [];
   textface(pfontid.seteface);

   moveto(hh+(pgchars[h]-length(pgtxt)-5)*ttychd[h],vv);
   DrawText(@pgTxt, 1, Length(pgtxt));

   if tracelmprint then
      begin
      writeln('pgchars[h],prespace,titlelen,length(pgtxt)');
      writeln(pgchars[h]:4,prespace:3,titlelen:3,length(pgtxt):3);
      writeln('pgcharsexpr',pgchars[h]-prespace-titlelen-length(pgtxt) -10 );
      end;

   pagelen := 0;
   bptr := @digtxt;
   inttoascii(@bptr^[1],pagelen,pagenum,blankfill,4);

   DrawText(@digTxt, 1, pagelen);
   end;

procedure printHRule{hh,vv,length:integer;row,col:idtype};
        {print rule under current cell; beware pixel truncation errors}
     begin
{$IFC SAMPCALLS}  ALogCall;  {$ENDC}
     moveto(hh,vv);
     Line(length,0);
     end;


procedure printPage{startrow,startcol,endcol:idtype;
                    var nextrow:itdtype;width:integer;pagenum:integer;
                    numrows:integer};
        {print the page starting with startrow,startcol; and ending just
         before endcol is printed. Return the next row to be printed}
     var
        rowLines:       integer;
        pageFull:       boolean;
        currow,
        prevrow : idtype;
        hh,vv : integer;
        cellvchars : integer;

     begin
{$IFC SAMPCALLS}  ALogCall;  {$ENDC}
     (*if TraceLMPrint then writeln('pr printPage: ', pageNum);*)
     hh :=  offset[h]+ (leftmargin*ttychd[h]);
     vv := offset[v]+ ttychd[v] + dotsbetween[v]+
           npgoffsetlns*(ttychd[v]+dotsbetween[v]);
     numrows := 0;

     {print a header}
     MoveTo(hh,vv);
     printHed(hh,vv,width);
     vv := vv + nHedlns*(ttychd[v]+dotsbetween[v]);
     MoveTo (hh,vv);
     Line(width,0);

     {Print the col titles}
     curRow := startrow;
     cellvChars := snipChars(minfsnip, v);
     Printrow(minfsnip,startcol,endcol,vv);
     vv := vv + cellvchars*(ttychd[v]+DotsBetween[v])   ;
     if (startrow = minfsnip) then getsnipid(rowkind,mmnext,0,minfsnip,currow);

     pageFull := False;
     while NOT ((curRow = limTbl[rowSnip]) OR  pageFull) do
        begin
        cellvChars := snipChars(curRow, v);
        if vv + cellvChars*(ttychd[v] + dotsbetween[v]) + (2*dotsbetween[v])<=
               pgChars[v]*(ttychd[v]+dotsbetween[v]) then
           begin
           printRow(currow,startcol,endcol,vv);
           numrows := numrows + 1;
           prevrow := currow;
           vv := vv + cellvchars*ttychd[v]+DotsBetween[v];

           {add space for a grid if in col specs}
           if (curTable = tcolspecs)  then vv := vv + dotsbetween[v] ;

           getsnipid(rowkind,mmnext,0,curRow,CurRow);
           end
        else pageFull := True;
        end;
     nextRow := CurRow;

     {put a last line on the table}
     if NOT (curTable = tcolspecs) then
        begin
        if pagefull OR (curTable = tsearch)then
           begin
           prBotRow(prevrow,startcol,endcol,vv);
           vv := vv+ 2*dotsbetween[v];
           end;
        MoveTo(hh,vv);
        Line(width,0);
        end;

     end;


procedure printRow{currow : idtype; startcol:idtype;  nextcol:idtype; vv:integer};
        {print currow from startcol to just before nextcol at vv}
     var
        colChars:       integer;
        lineFull:       boolean;
        col : idtype;
        lh : integer;
        newh : integer;
        cellhchars,
        cellvchars   : integer;
     begin
{$IFC SAMPCALLS}  ALogCall;  {$ENDC}
     (*if tracelmprint then writeln('printrow');*)

     lh := offset[h]+(leftmargin * ttychd[h]);
     cellvchars := snipchars(currow,v);
     Moveto(lh,vv);
     IF  (Curtable = tcolspecs) then
         Line(0,cellvchars*(ttychD[v]+dotsbetween[v])+dotsbetween[v])
     ELSE  Line(0,cellvchars*(ttychD[v]+dotsbetween[v]));


     lineFull := False;

     {skip row headers if in the list or search table}
     if ((curTable = tlist) OR (CurTable = tsearch))and (startcol = minfsnip) then
        getsnipid(colkind,mmnext,0,minfsnip,startcol);



     col := startcol;
     while (col <> nextcol)  do
        begin
        cellhChars := snipChars(col, h);
        if (lh + (cellhChars*ttychd[h]) + (chBetween[h]*ttychd[h])) <
                 ((pgChars[h]*ttychd[h]) + (leftmargin * ttychd[h])) then
           begin
           printCell(CurRow,Col,lh,vv);
           getsnipid(colkind,mmnext,0,col,col);
           lh := lh + cellhchars*ttychd[h] +chBetween[h]*ttychd[h];
           end;
        end;

     end;


procedure printVRule{hh,vv,length:integer;row,col:idtype};
        {print rule to right of current cell}
     var
         cellhchars : integer;
     begin
{$IFC SAMPCALLS}  ALogCall;  {$ENDC}
     (*if tracelmprint then writeln('printvrule');*)
     cellhchars := snipchars(col,h);
     moveto(hh + cellhchars*ttychd[h] + DotsBetween[h], vv  );
     Line(0,length);

     end;


procedure prBotRow{currow : idtype; startcol:idtype;  nextcol:idtype; vv:integer};
        {print bottom row  from startcol to just before nextcol at vv}
     var
        colChars:       integer;
        lineFull:       boolean;
        col : idtype;
        lh : integer;
        newh : integer;
        cellhchars,
        cellvchars   : integer;
     begin
{$IFC SAMPCALLS}  ALogCall;  {$ENDC}
     (*if tracelmprint then writeln('prbotrow');*)

     lh := offset[h]+(leftmargin * ttychd[h]);
     {subtract ttychd[v] so rule lenght can be at lease 1char high}
     Moveto(lh,vv-ttychd[v]  );
     Line(0,ttychd[v]+2*dotsbetween[v]);


     lineFull := False;

     {skip row headers if in the list or search table}
     if ((curTable = tlist) OR (CurTable = tsearch))and (startcol = minfsnip) then
        getsnipid(colkind,mmnext,0,minfsnip,startcol);



     col := startcol;
     while (col <> nextcol)  do
        begin
        cellhChars := snipChars(col, h);
        if (lh + (cellhChars*ttychd[h]) + (chBetween[h]*ttychd[h]))<
                 ((pgChars[h]*ttychd[h]) + (leftmargin * ttychd[h])) then
           begin
           prBotCell(CurRow,Col,lh,vv);
           getsnipid(colkind,mmnext,0,col,col);
           lh := lh + cellhchars*ttychd[h] +chBetween[h]*ttychd[h];
           end;
        end;

     end;

procedure prBotCell{row,col:idtype;hh,vv:integer};
        {print the bottom cell identified by row,col at hh,vv}
     var
        cellLen:                integer;
        cellhchars,
        cellvchars    : integer;
        cellStr:        array[0..maxString] of byte;
        cInfo:                  CellRecord;
        status:                 integer;
        preSpace:               integer;

     begin
{$IFC SAMPCALLS}  ALogCall;  {$ENDC}
     (*if tracelmprint then writeln('printcell');*)

     {subtrace a ttychd[v] so vrule is atleast one char in height}

     printVRule(HH,VV-ttychd[v],ttychD[v]+2*dotsbetween[v],row,col);
     end;


procedure skipPage{startrow,startcol,endcol:idtype;
                    var nextrow:itdtype;width:integer;pagenum:integer;
                    numrows:integer};
        {skip the page starting with startrow,startcol; and ending just
         before endcol is printed. Return the next row to be printed/skippd}
     var
        rowLines:       integer;
        pageFull:       boolean;
        currow,
        prevrow : idtype;
        hh,vv : integer;
        cellvchars : integer;

     begin
{$IFC SAMPCALLS}  ALogCall;  {$ENDC}
     (*if TraceLMPrint then writeln('pr skipPage: ', pageNum);*)
     hh :=  offset[h]+ (leftmargin*ttychd[h]);
     vv := offset[v]+ ttychd[v] + dotsbetween[v]+
           npgoffsetlns*(ttychd[v]+dotsbetween[v]);
     numrows := 0;

     {skip a header}
     vv := vv + nHedlns*(ttychd[v]+dotsbetween[v]);

     {skip the col titles}
     curRow := startrow;
     cellvChars := snipChars(minfsnip, v);
     vv := vv + cellvchars*(ttychd[v]+DotsBetween[v])   ;
     if (startrow = minfsnip) then getsnipid(rowkind,mmnext,0,minfsnip,currow);

     pageFull := False;
     while NOT ((curRow = limTbl[rowSnip]) OR  pageFull) do
        begin
        cellvChars := snipChars(curRow, v);
        if vv + cellvChars*(ttychd[v] + dotsbetween[v]) + (2*dotsbetween[v]) <=
               pgChars[v]*(ttychd[v]+dotsbetween[v]) then
           begin
           numrows := numrows + 1;
           prevrow := currow;
           vv := vv + cellvchars*ttychd[v]+DotsBetween[v];

           {add space for a grid if in col specs}
           if (curTable = tcolspecs)  then vv := vv + dotsbetween[v] ;

           getsnipid(rowkind,mmnext,0,curRow,CurRow);
           end
        else pageFull := True;
        end;
     nextRow := CurRow;

     {put a last line on the table}
     if NOT (curTable = tcolspecs) then
        begin
        if pagefull OR (curTable = tsearch)then
           begin
           vv := vv+ 2*dotsbetween[v];
           end;
        end;

     end;


function  snipChars {(aSnip: idType; u: axis): integer};
        {how many characters (in u direction) fit in aSnip on the screen?}
     var i : integer;
     begin
{$IFC SAMPCALLS}  ALogCall;  {$ENDC}
     i := (SnipWidth(sKindOfAxis[u], aSnip) + (scrChD[u] div 2)) div scrChD[u];
     if (asnip = minfsnip)  and (u = v) then i:= 2;   {titles always 2 char high}
     {restrict size of snip to page size - 2}                      {#179}
     i := cmin(pgchars[u]-2,i);
     snipChars := i;
     end;

{$S lminit}

end .
ÿ