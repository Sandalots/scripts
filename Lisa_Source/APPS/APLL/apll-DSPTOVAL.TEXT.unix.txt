(*****
L. Rosenstein 4/12/84: In AsciiToTime, changed WHILE condition from
    "... and (dbf^.editcheck[cii] MOD 8 <> 0) and ..." to
    "... and (dbf^.editcheck[cii] MOD 8 > noComp) and(dbf^.editcheck[cii] MOD 8 < ampmComp) and ..." to
*****)

{$S DBCPack}
function KeywordLookup (kw:integer; chDsp:ptrfstring; strtDsp:integer;
                        endDsp:integer; kwPart:integer): boolean;

{******************************************************************************

Function: Checks for the keyword identified by keyword[kw], starting at strtDsp,
          and returns true if it finds it.  KwPart is the amount of the keyword
          to check (e.g., month abbreviations are the first three characters
          of the corresponding month names).  The check is case insensitive.

          Does nothing but return FALSE if kwPart is <= 0.

******************************************************************************}

begin
{$IFC SAMPCALLS}  ALogCall;  {$ENDC}
if dbcTest then
  writeln('Entering KeywordLookup, kwPart=',kwPart:1);

if ((1 + endDsp) - strtDsp < kwPart) or (kwPart<=0) then
   { not enough chars left to match OR trying to match a null string }
    KeywordLookup := false

else { there are enough characters for a match }
    begin
    KeywordLookup := CompChrIdentity(POINTER(ORD(@keyword[kw]) + 1),
                                        POINTER(ORD(chDsp) + strtDsp),
                                        kwPart, kwPart, CompVeryWeak);
    end; { of action if there are enough character for a match }
end; { of KeywordLookup }


{$S DBCPack}
function CheckCurrent (chDsp:ptrfstring; strtDsp:integer; endDsp:integer;
    var kwLen:integer): boolean;

{ looks for current keyword as a date/time component, and sets kwLen to
  the length of what was found }

begin
{$IFC SAMPCALLS}  ALogCall;  {$ENDC}
if KeywordLookup (kwCurrent, chDsp, strtDsp, endDsp,
                  length(keyword[kwCurrent]))
    then
    begin
    CheckCurrent := true;
    kwLen := length(keyword[kwCurrent]);
    end
else if KeywordLookup (kwC, chDsp, strtDsp, endDsp,
                       length(keyword[kwC])) then
    begin
    CheckCurrent := true;
    kwLen := length(keyword[kwC]);
    end
else
    begin
    CheckCurrent := false;
    kwLen := 0;
    end;
if dbcTest then
    writeln ('Exiting CheckCurrent; kwLen: ', kwLen);
end; { of CheckCurrent }


{$S DBCPack}
function CheckNumber (chDsp:ptrfstring; strtDsp:integer; endDsp:integer;
    var kwLen:integer): boolean;

{ looks for number as a date/time component, and sets kwLen to
  the length of what was found }
var done: boolean;
    ch:char;

begin
{$IFC SAMPCALLS}  ALogCall;  {$ENDC}
if strtDsp <= endDsp then
    begin
    kwLen := 0;
    done := false;
    while NOT done and (strtDsp <= endDsp) do
        begin
        ch := chdsp^[strtdsp];
        if ('0' <= ch) and (ch <= '9') then kwLen := kwLen + 1
        else done := TRUE;
        strtDsp := strtDsp + 1;
        end;

    CheckNumber := kwLen > 0;
    end
else
    CheckNumber := FALSE;

if dbcTest then
    writeln ('Exiting CheckNumber; kwLen: ', kwLen:1);
end; { CheckNumber }

(*
{$S DBCPack}
function CheckChar (b:byte; checkO:boolean; var errNum:integer): boolean;

{ looks for a non-numeric character when a numeric component is being
  processed; returns true if a non-numeric character is found; returns error
  17 if an L is found instead of a 1 and error 18 if an o is found instead
  of a 0; o instead of 0 is checked only if checkO is true }

var c: char;

begin
{$IFC SAMPCALLS}  ALogCall;  {$ENDC}
IF b < 0 THEN
    b := b + 256;
DownChrShift(@b, 1);
c := chr(b);

{ check for an L instead of a 1 }
if (c = 'l') then
    begin
    errNum := 17; { Letter L entered instead of number 1 }
    CheckChar := true;
    end
{ check for an o instead of a 0 }
else if checkO and (c = 'o') then
    begin
    errNum := 18; { Letter o entered instead of number 0 }
    CheckChar := true;
    end
{ check for other non-digit }
else if not (c in ['0'..'9']) then
    CheckChar := true;
end; { of CheckChar }
*)

{$S DBCPack}
function AsciiToInt (num:ptrdata; numlen:integer; var errnum:integer): integer;

{******************************************************************************

    Function: Converts a numeric string (in an array of bytes) to an integer.

    Errors:   17 - Letter L entered instead of number 1
              18 - Letter o entered instead of number 0
              19 - Alphabetic character not allowed
              80 - Digit expected

******************************************************************************}

var     total: integer;
        i:     integer;
        c:     char;

begin
{$IFC SAMPCALLS}  ALogCall;  {$ENDC}
if dbcTest then writeln ('Entering AsciiToInt');
total := 0;
i := 0;
while i < numlen do
    begin
    c := chr(num^[i]);
    if ('0' <= c) and (c <= '9') then
        total := total * 10 + num^[i] - ord('0')
    else
        begin
        if (c = 'L') or (c = 'l') then
            errNum := 17   { Letter L entered instead of number 1 }
        else if (c = 'o') or (c = 'O') then
            errNum := 18   { Letter o entered instead of number 0 }
        else if (ord(c) in alphaSet) then
            errNum := 19   { Alphabetic character not allowed }
        else errnum := 80; { Digit expected }
        end;
    i := i+1;
    end;

AsciiToInt := total;
end; { of AsciiToInt }


{$S DBCPack}
procedure AsciiToDate (dbf:ptrdbfield; dsp:ptrdata; strtDsp:integer;
    endDsp:integer; var d:dateValues; var errnum:integer);

{******************************************************************************

Function: Convert an ASCII date into a date record.  dateValues components of
          -1 => Null (no value entered).

Assumes: The date field is not null.

Calls: AsciiToInt
       KeywordLookup

Errors:  17 - Letter L entered instead of number 1
         18 - Letter o entered instead of number 0
         75 - Unknown date component
         78 - Unrecognized month name
         80 - Digit expected in date

******************************************************************************}

label 100;    { error exit }

const
        noComp  =   0;
        monComp =   1;
        dayComp =   2;
        yrComp  =   3;

var     cii:         integer;    { index for editcheck checkinfo }
        chDsp:       ptrfstring; { to get characters out as characters }
        dspi:        integer;    { index through display data }
        i:           integer;    { index }
        token:       ptrdata;    { beginning of a date component }
        tokenlen:    integer;    { length of a date component }
        done:        boolean;
        compType:    integer;    { type of component in editcheck }
        curDate:     dateValues; { current date in db format }
        osGmtTime:   time_rec;   { OS Greenwich Mean Time }
        osCurTime:   time_rec;   { OS current time }
        dayInc:      integer;    { used for computing month and day from OS time }
        osErr:       integer;    { OS error status }
        checkO:      boolean;    { => whether to check for o instead of 0 }
        c:           char;       { temp storage }
        useCurrent:  boolean;    { => put current in specified components }
        ecp:         ^longStr;   { pointer to edit check string }
        eci:         integer;    { index on edit check string }

begin { of AsciiToDate }
{$IFC SAMPCALLS}  ALogCall;  {$ENDC}
if dbcTest then writeln ('Entering AsciiToDate; start: ', strtDsp:2, ' end: ',
    endDsp:2);

chDsp := pointer(ord(dsp));

{ initialize the current date }
Get_Time (osErr, osGmtTime);
if dbcTest then writeln ('After Get_Time; endDsp is: ', endDsp);
Convert_Time (osErr, osGmtTime, osCurTime, false);
if dbcTest then writeln ('After Convert_Time; endDsp is: ', endDsp);


curDate[yrComp] := osCurTime.year;
{ calculate the month and day from the day value in osCurTime }
curDate[monComp] := 0;
i := 0;
while i < osCurTime.day do
    begin
    curDate[monComp] := curDate[monComp] + 1;
    curDate[dayComp] := osCurTime.day - i;
    { "30 days hath September, April, June, and November..." }
    dayInc := 31;
    case curDate[monComp] of
        4, 6, 9, 11:
            dayInc := 30;
        2:  begin
            dayInc := 28;
            if (curDate[yrComp] mod 4 = 0) then
                dayInc := 29;
            if (curDate[yrComp] mod 100 = 0) and
               (curDate[yrComp] mod 400 <> 0) then
                dayInc := 28;
            end; { of action for figuring days in February }
        end; { of case }
    i := i + dayInc;
    end; { of while for determining month and day values }

{ initialize returned date to null }
i := 1;
while i <= 3 do
  begin
  d[i] := -1;
  i := i+1;
  end;

{ look for the 'Date' keyword }
{ if dbcTest then
    writeln ('Looking for date; start/end is: ', strtDsp, endDsp,
        ' calculated length is ', endDsp-strtDsp+1); }
tokenLen := 0;
if KeywordLookup (kwDate, chDsp, strtDsp, endDsp,
                    length(keyword[kwDate])) then
    tokenLen := length(keyword[kwDate])

else if CheckCurrent (chDsp, strtDsp, endDsp, tokenLen) then;

if dbcTest then
    writeln('keyword length ', tokenLen:1);

{ now put in current values if they were requested }
if (tokenLen>0) and (tokenLen=endDsp-strtDsp+1) then
    begin
    if dbcTest then
        writeln ('Using current');
    cii := 1;
    while cii <= 3 do
        begin
        i := dbf^.editCheck[cii] MOD 8;
        if (monComp <= i) and (i <= yrcomp) then d[i] := curDate[i];
        cii := cii+1;
        end;
    goto 100;
    end; { of action for putting current in appropriate components }

{ neither keyword was used standalone, so do 'normal' processing of components }
if dbcTest then
    writeln ('About to process components; strtDsp is ', strtDsp:2, ' endDsp is ',
        endDsp:2);

errnum := 0;
ecp := pointer(ord(dbf) + dbf^.display);
eci := 1;
dspi := strtDsp; { index through displayed string }
cii := 1; { index for editcheck checkinfo array }
while (cii < 4) and (dbf^.editcheck[cii] MOD 8 <> 0) and (dspi <= endDsp) and
    (errnum = 0) do
    begin

    compType := dbf^.editCheck[cii] MOD 8;
    if dbcTest then
        writeln ('date component=',compType);

    { check for current  }
    if CheckCurrent(chDsp, dspi, endDsp, tokenLen)then
        if ord(chDsp^[dspi+tokenLen]) in alphaSet then
            tokenLen := 0 {wasn't really Current}
        else
            begin
            d[compType] := curDate[compType];
            if dbcTest then
                writeln('got current, tokenLen=', tokenLen);
            end;

    if tokenLen = 0 then
        begin
        { check for number }

        { first set i to potential start of number:
            allow one blank before the number }
        i := dspi;
        if (chDsp^[i]=' ') or (chDsp^[i]=reqSpace) then
            i := i + 1;

        { allow multiple blanks before the year }
        if compType = yrComp then
            begin
            done := false;
            while not done and (i <= endDsp) do
                if (chDsp^[i]=' ') or (chDsp^[i]=reqSpace) then
                    i := i + 1
                else
                    done := true;
            end;

        if (chDsp^[i]=' ') or (chDsp^[i]=reqSpace) then
            begin { must be null number }
            tokenLen := i - dspi + 1;
            { the d array already had been set to null components }
            end

        else if CheckNumber(chDsp, i, endDsp, tokenLen) then
            begin
            { accept the skipped over spaces }
            dspi := i;

            {allow users to enter the date digits without any punctuation}
{u}      (* if ODD(tokenLen) then
                tokenLen := 1
            else
                tokenLen := 2; *)

            if CompType = yrComp
            then tokenlen := cmin(tokenlen,dbf^.editCheck[4])
            else tokenlen := cmin(tokenlen,2);

            { get the number }
            token := pointer(ord(chDsp) + dspi);
            i := AsciiToInt(token, tokenLen, errnum);

            { ??? check for 'l', 'O', and 'o' ??? }

            d[compType] := i;
            if dbcTest then
                writeln('got number=', i, ', tokenLen=',tokenLen);
            end

        { check for month name }
        else if compType = monComp then
            begin
            i := kwJanuary;
            done := false;

            while (i <= kwDecember) and not done do

                { first look for spelled out month name }
                if KeywordLookup (i, chDsp, dspi, endDsp,
                                    length(keyword[i])) then
                    begin
                    done := true;
                    tokenLen := length(keyword[i]);
                    end

                { now look for abbreviated month name }
                else if KeyMatch (pointer(ord(chDsp)),
                                    dspi, endDsp, monthAbbr[i]) then
                    begin
                    done := true;
                    tokenLen := length(monthAbbr[i]);
                    end

                else { neither the spelled out nor the abbreviated form matched }
                    i := i + 1;

            if done and not(ord(chDsp^[dspi+tokenLen]) in alphaSet) then
                begin
                d[monComp] := i;
                if dbcTest then
                    writeln('got month name=', i, ', tokenLen=',tokenLen);
                end

            else if ord(chDsp^[dspi]) in alphaSet then
                errNum := 78; { Unrecognized month name }
            end;

        end; {did not find current component}

    if errnum = 0 then
        begin { finish processing component }

        dspi := dspi + tokenLen;
        if dbcTest then
            writeln('finish component, dspi=', dspi);

        { set i to end of current component in ec string }
        i := eci + (dbf^.editcheck[cii] DIV 16) MOD 32;

        { set eci to start of next component (if any) }
        eci := length(ecp^) + 1;
        if cii < 3 then
            if dbf^.editcheck[cii+1] <> 0 then
                eci := dbf^.editcheck[cii+1] DIV 512 + 1; { editcheck position is
                                                            relative to 0 }

        { scan away at most as many delimiters as in edit check;
            i is the number in editcheck }
        i := eci {start of next component} - i {end of current component};
        if dbcTest then
            writeln('try to scan away ', i:1, ' delimiters, eci=', eci);

        done := false;
        while not done and (i>0) and (dspi<=endDsp) do
            begin
            c := chDsp^[dspi];
            if (ord(c) in spcl) then
                begin
                dspi := dspi+1;
                i := i-1;
                end
            else
                done := true;
            end;

         end; {finishing up after component}

    if dbcTest then
        writeln('ready for next component, dspi=', dspi);

    cii := cii + 1;
    end; { of while on checkinfo components }


{ check for extra parts }
if (errnum = 0) and (dspi < endDsp + 1) then
    errnum := 75; { unknown date component }

100: if dbcTest then
    writeln ('Date: ', d[monComp]:2, '/', d[dayComp]:2, '/', d[yrComp]:4)

end; { of AsciiToDate }


{$S DBCPack}
procedure AsciiToTime (dbf:ptrdbfield; dsp:ptrdata; strtDsp:integer;
    endDsp:integer; var tval:timeValues; var errnum:integer);

{*****************************************************************************

Function: Converts an entered time value into a set of three integer time
          components. tVal value of -1 => null (no value entered).

Calls: AsciiToInt

Errors: 17 - Letter L entered instead of number 1
        18 - Letter o entered instead of number 0
        62 - Component in wrong place
        69 - Unknown time component
        71 - Too many time components
        73 - AM/PM allowed only with hour from 0 to 23

*****************************************************************************}

label  50, { exit if current value wanted }
      100; { error exit }

const   noComp=0;
        hrComp=1;
        minComp=2;
        secComp=3;
        ampmComp=4;
        makeLower = 32; { ord('a')-ord('A') }

var     cii:         integer;    { index for editcheck checkinfo }
        chDsp:       ptrfstring; { to get chars out as chars }
        dspi:        integer;    { index through display data }
        i:           integer;    { index }
        token:       ptrdata;    { beginning of a time component }
        tokenlen:    integer;    { length of a time component }
        done:        boolean;
        compType:    integer;    { type of component in editcheck }
        osGmtTime:   time_rec;   { OS Greenwich Mean Time }
        osCurTime:   time_rec;   { OS current time }
        osErr:       integer;    { OS error status }
        checkO:      boolean;    { => whether to check for o instead of 0 }
        c:           char;       { temp storage }
        useCurrent:  boolean;    { => put current in specified components }
        ecp:         ^longStr;   { pointer to edit check string }
        eci:         integer;    { index on edit check string }
        curTime:     timeValues; { temp storage for current time }
        tokens:      array[1..3] of integer;
                                 { token values, in order encountered; -1 =>
                                     null; -2 => current }
        amToken:     integer;    { 0:none, -1:am, 1:pm }
        component:   dbccompstr; {component string}

begin
{$IFC SAMPCALLS}  ALogCall;  {$ENDC}

if dbctest then writeln ('Entering AsciiToTime');

chDsp := pointer(ord(dsp));

 { initialize the current time }
Get_Time (osErr, osGmtTime);
Convert_Time (osErr, osGmtTime, osCurTime, false);

curTime[1] := osCurTime.hour;
curTime[2] := osCurTime.minute;
curTime[3] := osCurTime.second;

{ initialize the returned time to null }
i := 1;
while i <= 3 do
    begin
    tokens[i] := -1;
    tVal[i] := -1;
    i := i+1;
    end;

{ look for the 'Time' keyword }
if dbcTest then
    writeln ('About to look for time keyword; strtDsp is ', strtDsp:2,
        ' endDsp is ', endDsp:2);

tokenLen := 0;
if KeywordLookup (kwTime, chDsp, strtDsp, endDsp,
                  length(keyword[kwTime])) then
    tokenLen := length(keyword[kwTime])
else if CheckCurrent (chDsp, strtDsp, endDsp, tokenLen) then;

if dbcTest then
    writeln('keyword length ', tokenLen:1);

if (tokenLen>0) and (tokenLen=endDsp-strtDsp+1) then
    begin
    if dbcTest then
        writeln('using current');
    comptype := hrcomp;
    while comptype <= seccomp do
      begin
      tval[compType] := -2;
      comptype := comptype+1;
      end;
    goto 50;
    end; (* if matched a time token *)

{  neither keyword was used standalone, so do 'normal' processing of components }
if dbcTest then
    writeln ('About to process components; strtDsp: ', strtDsp:2, ' endDsp: ',
             endDsp:2);


amToken := 0;
errnum := 0;
ecp := pointer(ord(dbf) + dbf^.display);
eci := 1;
dspi := strtDsp; { index through displayed string }
cii := 1; { index for editcheck checkinfo array }

(*****
L. Rosenstein 4/12/84: changed condition from "... and (dbf^.editcheck[cii] MOD 8 <> 0) and ..."
*****)
while (cii < 5) and (dbf^.editcheck[cii] MOD 8 > noComp) and (dbf^.editcheck[cii] MOD 8 < ampmComp) and
            (dspi <= endDsp) and
            (errnum = 0) do
    begin
    if dbcTest then
        writeln('looking for token; dspi=',dspi:1,' cii=',cii:1);

    compType := dbf^.editCheck[cii] MOD 8;
    if dbcTest then
        writeln ('time component=',compType);

    { check for current  }
    if CheckCurrent(chDsp, dspi, endDsp, tokenLen) then
        if ord(chDsp^[dspi+tokenLen]) in alphaSet then
            tokenLen := 0 {wasn't really Current}
        else
            begin
            tval[compType] := -2;
            if dbcTest then
                writeln('got current, tokenLen=', tokenLen);
            end;

    if tokenLen = 0 then
        begin
        { check for number }

        { first set i to potential start of number:
            allow one blank before the number }
        i := dspi;
        if (chDsp^[i]=' ') or (chDsp^[i]=reqSpace) then
            i := i + 1;

        if (chDsp^[i]=' ') or (chDsp^[i]=reqSpace) then
            begin { it's a null number }
            tokenLen := i - dspi + 1;
            { the tval array already has been set to a null component }
            end

        else if CheckNumber(chDsp, i, endDsp, tokenLen) then
            begin
            {accept the skipped spaces}
            dspi := i;

            {allow users to enter the time digits without any punctuation}
            if ODD(tokenLen) then
                tokenLen := 1
            else
                tokenLen := 2;

            token := pointer(ord(chDsp) + dspi);
            i := AsciiToInt(token, tokenLen, errnum);

            { ??? check for 'l', 'O', and 'o' ??? }

            tval[compType] := i;
            if dbcTest then
                writeln('got number=', i, ', tokenLen=',tokenLen);
            end

        {try am}
        else if KeywordLookup (kwAm, chDsp, dspi, endDsp,
                                      length(keyword[kwAm])) then
            begin
            amToken := -1;
            if dbcTest then
                writeln('am token');
            tokenLen := length(keyword[kwAm]);
            end

        { now try pm }
        else if KeywordLookup (kwPm, chDsp, dspi, endDsp,
                           length(keyword[kwPm])) then
        begin
        amToken := 1;
        if dbcTest then
            writeln('pm token');
        tokenLen := length(keyword[kwPm]);
        end

        else if ord(chDsp^[dspi]) in alphaSet then
            begin
            errnum := 69; { unknown time component }
            goto 100;
            end;

        end; { if didn't find current component }

    if errnum = 0 then
        begin { finish processing component }

        dspi := dspi + tokenLen;
        if dbcTest then
            writeln('finish component, dspi=', dspi);

        { set i to end of current component in ec string }
        i := eci + (dbf^.editcheck[cii] DIV 16) MOD 32;

        { set eci to start of next component (if any) }
        eci := length(ecp^) + 1;
        if cii <= 3 then
            if dbf^.editcheck[cii+1] <> 0 then
                eci := dbf^.editcheck[cii+1] DIV 512 + 1; { editcheck position is
                                                            relative to 0 }

        { scan away at most as many delimiters as in edit check (BUT at least 1);
            i is the number in the editcheck }
        i := eci {start of next component} - i {end of current component};
        if i = 0 then
            i := 1;

        if dbcTest then
            writeln('try to scan away ', i:1, ' delimiters, eci=', eci);

        done := false;
        while not done and (i>0) and (dspi<=endDsp) do
            begin
            c := chDsp^[dspi];

            component := TimeDelims[CompType];
            if KeyMatch(pointer(ord(chdsp)),dspi,dspi+i-1,component)
            then begin
                 dspi := dspi+length(component);
                 i := i-length(component);
                 end
            else if (ord(c) in alphaset)        {test for time delimiter}
                 then begin
                      component := TimeDelims[4];
                      if KeyMatch(pointer(ord(chdsp)),dspi,dspi+i-1,component)
                      then begin
                           dspi := dspi+length(component);
                           cii := 5;    {found string; stop looking for components}
                           end;

                      done := TRUE;
                      end
            else if (ord(c) in spcl) then {remember there can
                                            be alphabetic delimiters}
                begin
                dspi := dspi+1;
                i := i-1;
                end
            else
                done := true;
            end;

         end; {finishing up after component}

    if dbcTest then
        writeln('ready for next component, dspi=', dspi);

    cii := cii + 1;
    end; { of while on checkinfo components }


{ see if we got them all }
if (errnum = 0) and (dspi <= endDsp) then
    begin
    errNum := 71; { too many time components }
    goto 100;
    end;

50:

{ insert current values if wanted;
  adjust hours according to am/pm or default }

for compType := hrComp to secComp do
    begin
    if compType = hrComp then
        begin
        i := tval[compType];

        { maybe fix up the hours }

        if i >= 0 then {not current or null}
            begin
            if (amToken<0) and (i=12) then { am wanted and hour is pm }
              i := i - 12
            else

            if (amToken>0) and (i<12) then { pm wanted and hour is am }
                i := i + 12

            else if (amToken=0) and                 { no am/pm in input }
                    (dbf^.editcheck[5]=0) and       { am/pm expected }
                    (i>=1) and                      { user entered a value (not CURRENT) }
                    (i<=6) then                     { from 1 to 6 }
                i := i+12;                          { default value appropriately }

            tval[compType] := i;
            end;
        end;

    if tval[compType] = -2 then
        tval[compType] := curTime[compType];

    end; { processing found components }

{ substitute 0 as a value for null low order components }
done := false;
for compType := secComp downto hrComp do
    if tval[compType] <> -1 then
        done := true
    else if not done then
        tval[compType] := 0;

if (tVal[1]=-1) and (amToken<>0) then    { null hour and am/pm is an error }
    errnum := 73; { am/pm allowed only with hour between 0 and 23 }

100:
if dbcTest then
    writeln ('Exiting AsciiToTime; time parts are ', tVal[1], tVal[2], tVal[3]);
end; { of AsciiToTime }

ÿ