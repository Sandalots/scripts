{$S lmcolspec }
PROCEDURE ChangeColSpecs{VAR CStatus: INTEGER};

   CONST CCSpecs = ' ChangeColSpecs ';

   VAR ListColSnip: PtrColSnip;
       ListColId: IdType;
       SpecColSnip: PtrColSnip;
       SpecColId: IdType;
       Column: IdType;
       FieldPtr: PtrDbField;
       DataPtr: PtrData;
       BytePtr: PtrData;
       Len: INTEGER;
       Status: INTEGER;
       ErrNum: INTEGER;
       ByteArray: ARRAY[0..255] OF BYTE;
       EditString: ECStr;
       DType: INTEGER;
       ColumnCount: INTEGER;
       Row: IdType;
       Col: IdType;
       CurPt: Point;
       VisRg: Range;
       NewCol: BOOLEAN;
       TempList: CutColRecord;
       NextCol: IdType;

   BEGIN
{$IFC SAMPCALLS}  ALogCall;  {$ENDC}
   IF TraceMMGR then WriteLn(MxMgr,CCSpecs);

   {Make sure column spec table is current}
   IF MMTrap1 THEN IF CurTable <> TColSpec THEN
      BEGIN
      Writeln(Bell,'***Colspec table is not current');
      END;

   {Any new blank columns should be removed. These may exist if the list was being recreated
   or created. However, in the case of being created, the blank columns are removed later,
   so we won't bother to do it here.}
   IF RedefiningTable THEN
      BEGIN
      Column := TableData.FirstCol;
      WHILE Column <> NullSnip DO
         BEGIN
         SetSnipPtr(ColKind,Column);
         NextCol := ColSnip^.NextSnip;
         IF ColSnip^.NewColumn THEN
            BEGIN
            IF CellBlank(MInfSnip,Column) THEN
               BEGIN
               WITH TableData DO
                  BEGIN
                  IF Trace2MMgr THEN WriteLn('Removing blank column');
                  CutColSnips(FirstCol,LastCol,Column,Column);
                  FreSnipBlock(Column);
                  ColCount := ColCount - 1;
                  END;
               END; {CellBlank}
            END; {New column}
         Column := NextCol;
         END;
      END; {RedefiningTable}

   {Make sure there are no cut columns if not creating or redefining}
   IF ColumnsCut AND NOT (CreatingTable OR RedefiningTable) THEN
      BEGIN
      IF CautionAlert(LMAlerts,MustPasteCol) THEN;
      CStatus := MustPasteCol;
      EXIT(ChangeColSpecs);
      END;

   {Get rid of any cut or deleted columns. Once this is done, it will no
   longer be possible to pack data into the row snips, because some of the
   field information is missing. However, for those columns which remain,
   it will be possible to read information out of the row snips, since the
   offsets for the remaining fields are still valid.}
   IF ColumnsCut THEN RelScrap; {This puts cut columns on deleted list}
   IF ColumnsDeleted THEN WITH TableData DO
      BEGIN
      CutColSnips(FirstCol,LastCol,DelCol.FirstCol,DelCol.LastCol);
      ColumnsDeleted := FALSE;
      ReOrderColumns(FirstCol,ColCount);
      END;

   {Remember the horizontal scroll position}
   IF CreatingTable OR RedefiningTable THEN HScrollId := NullSnip
   ELSE BEGIN
        {Get point in current view}
        TMCurVwPt(CurPt);
        IF Trace2MMgr THEN WriteLn('H: ',CurPt.H,' V: ',CurPt.V);
        {Get currently visible range}
        VisRgOfView(CurPt,VisRg);
        {Remember it only if it is a valid snipid}
        WITH VisRg DO
           BEGIN
           CASE LoCol OF
              MInfSnip,PInfSnip,NullSnip: HScrollId := NullSnip; {Error case}
              OTHERWISE BEGIN
                        SetSnipPtr(ColKind,LoCol);
                        HScrollId := ColSnip^.ListSnip;
                        END;
              END; {CASE}
           END; {WITH}
        END;

   {Loop through all of the col spec snips and transfer information to list.
   Must remember that any operation which changes the value in a snip could
   cause all snips to relocate, consequently the snip pointers must constantly
   be reset.}
   WITH TempList DO
      BEGIN
      FirstCol := NullSnip;
      LastCol := NullSnip;
      ColCount := 0;
      END;

   Column := TableData.FirstCol;
   ColumnCount := 0;
   WHILE Column <> NullSnip DO
      BEGIN
      ColumnCount := ColumnCount + 1;
      SpecColId := Column;
      SetSnipPtr(ColKind,SpecColId);
      WITH ColSnip^ DO
         BEGIN
         ListColId := ListSnip;
         NewCol := NewColumn;
         END;

      {If the list snip is null, it means that this column was added after
      the colspec table was displayed. Create a list snip to go with it}
      IF ListColId = NullSnip THEN
         BEGIN
         {Create a default list snip}
         NewListCol(ListColId,ListTable.ColCount + 1); {Order should not matter}

         {Link into the list table snips}
         WITH ListTable DO
            BEGIN
            InsertColSnips(FirstCol,LastCol,LastCol,ListColId,ListColId);
            ColCount := ColCount + 1;
            END;
         END;


      {First move over the column name. The name may be changed at any time.}
      Len := 255;
      CellV(MMRead,MInfSnip,SpecColId,@ByteArray,NIL,TRUE,Len,Status);
      IF MMTrap1 THEN IF Status <> 0 THEN
         BEGIN
         WriteLn(Bell,'***CellV read status: ',status);
         END;
      CellV(MMWrite,MInfSnip,ListColId,@ByteArray,NIL,TRUE,Len,Status);
      IF MMTrap1 THEN IF Status <> 0 THEN
         BEGIN
         WriteLn(Bell,'***CellV read status: ',status);
         END;


      {If we are creating or redefining the table, and this is a new column,
      move over the data type. At First release, data type may not be
      changed after creation.}
      IF NewCol THEN
         BEGIN
         {Get the field type}
         SetSnipPtr(ColKind,SpecColId);
         FieldPtr := @ColSnip^.FieldInfo;
         SetSnipPtr(RowKind,TypeRow);
         DataPtr := POINTER(ORD(RowSnip) + RowSnip^.Data);

         UnPack(FieldPtr,@ByteArray,20,Len,DataPtr,ErrNum);
         IF MMTrap1 THEN IF Errnum <> 0 THEN
            BEGIN
            WriteLn(Bell,'***Data type UnPack status: ',ErrNum);
            END;

         CheckDataType(DType,@ByteArray,Len,Status);
         IF MMTrap1 THEN IF Status <> 0 THEN
            BEGIN
            WriteLn(Bell,'***CheckDataType #',DType,' ; status: ',status);
            END;

         {Compare with the original data type}
         SetSnipPtr(ColKind,ListColId);
         IF DType <> ColSnip^.FieldInfo.FldType THEN
            BEGIN
            IF Trace2MMGR then WriteLn(MxMgr,CCSpecs,'Data type change');
            ColSnip^.FieldInfo.FldType := DType;
            END;
         END;

      {Move over edit check}
      SetSnipPtr(ColKind,SpecColId);
      FieldPtr := @ColSnip^.FieldInfo;
      SetSnipPtr(RowKind,EditCRow);
      DataPtr := POINTER(ORD(RowSnip) + RowSnip^.Data);
      BytePtr := @EditString;

      {get the edit string out of the row snip}
      UnPack(FieldPtr,@BytePtr^[1],40,Len,DataPtr,ErrNum);
      IF MMTrap1 THEN IF Errnum <> 0 THEN
         BEGIN
         WRiteLn(Bell,'***UnPack status: ',ErrNum);
         END;
      BytePtr^[0] := Len;

      {Make sure its legal, and generate the checkinfo array}
      SetSnipPtr(ColKind,ListColId);
      FieldPtr := @ColSnip^.FieldInfo;
      LegalEditCheck(EditString,FieldPtr^.FldType,FieldPtr^.EditCheck,ErrNum);
      IF MMTrap1 THEN IF ErrNum <> 0 THEN
         BEGIN
         WriteLn(Bell,'***LegalEditCheck status: ',ErrNum);
         END;

      {Now put the edit string into the field information record}
      SetFieldAttr(ListColId,1,BytePtr,Len+1);



      {Transfer the column width}
      SetSnipPtr(ColKind,SpecColId);
      SpecColSnip := ColSnip;
      SetSnipPtr(ColKind,ListColId);
      ListColSnip := ColSnip;
      ListColSnip^.Width := SpecColSnip^.Width;


      {Transfer over the column order - Since the column order is specified by
      the linked list, the order is transferred over by relinking the list
      column snips in the same order as the spec column snips.}

      {First cut the column out of the list}
      WITH ListTable DO
         BEGIN
         CutColSnips(FirstCol,LastCol,ListColId,ListColId);
         ColCount := ColCount - 1;
         END;

      {Now paste into the temporary list}
      WITH TempList DO
         BEGIN
         InsertColSnips(FirstCol,LastCol,LastCol,ListColId,ListColId);
         ColCount := ColCount + 1;
         END;

      {If the sort and visibility rows are present, transfer the information.}
      IF NOT (CreatingTable OR ReDefiningTable) THEN
         BEGIN
         {Transfer over the sort information}
         SetSnipPtr(ColKind,SpecColId);
         SpecColSnip := ColSnip;
         SetSnipPtr(ColKind,ListColId);
         ListColSnip := ColSnip;
         with ListColSnip^ do
           begin
           SortOrder := SpecColSnip^.SortOrder;
           SortDirection := SpecColSnip^.SortDirection;
           end;

         {Move over Visibility}
         SetSnipPtr(RowKind,VisibleRow);
         DataPtr := POINTER(ORD(RowSnip) + RowSnip^.Data);
         FieldPtr := @SpecColSnip^.FieldInfo;
         UnPack(FieldPtr,@ByteArray,10,Len,DataPtr,ErrNum);
         IF MMTrap1 THEN IF ErrNum <> 0 THEN
            BEGIN
            WriteLn(Bell,'***Visibility UnPack status: ',ErrNum);
            END;

         {Set Visibility flag}
         CheckVisible(ListColSnip^.Visible,@ByteArray,Len,Status);
         IF MMTrap1 THEN IF Status <> 0 THEN
            BEGIN
            WriteLn(Bell,'***CheckVisible status: ',Status);
            END;
         END;

      {continue to loop}
      SetSnipPtr(ColKind,SpecColId);
      Column := ColSnip^.NextSnip;
      END;

   {If we are not creating the table, the ticket column should be left.
   It is left because it is the only column
   which does not have a corresponding colspec snip, so that when the list
   snips were reordered by looping through the colspec snips, it was left
   out. Now is the time to link it back in.}
   IF NOT CreatingTable THEN
      BEGIN
      {Find the ticket snip}
      Column := ListTable.FirstCol;
      WHILE Column <> NullSnip DO
         BEGIN
         SetSnipPtr(ColKind,Column);
         IF ColSnip^.FieldInfo.FldType = Tick THEN
            BEGIN
            IF Trace2MMgr THEN WriteLn('Found ticket');
            WITH ListTable DO
               BEGIN
               CutColSnips(FirstCol,LastCol,Column,Column);
               ColCount := ColCount - 1;
               END;

            {Paste into templist. Position should not matter, since always invisible.}
            WITH TempList DO
               BEGIN
               InsertColSnips(FirstCol,LastCol,NullSnip,Column,Column);
               ColCount := ColCount + 1;
               END;

            Column := NullSnip;
            END

         ELSE BEGIN {Not ticket column}
              Column := ColSnip^.NextSnip;
              IF MMTrap1 THEN IF Column = NullSnip THEN
              WriteLn(Bell,'***Failed to find ticket snip');
              END;

         END; {Column <> NullSnip}
      END; {Not creatingtable}


   {Now that all of the snips have been relinked in their proper order,
   set the order fields to correspond.}
   WITH TempList DO ReOrderColumns(FirstCol,ColCount);


   {If any other columns are left on the main list, presumably it is because
   their colspec counterparts were cut. Get rid of them.}
   IF ListTable.FirstCol <> NullSnip THEN
      BEGIN
      IF MMTrap1 THEN IF NOT (CreatingTable OR RedefiningTable)
      THEN WriteLn(Bell,'***Unexpected columns left on ListTable');

      ReleaseSnips(ColKind,ListTable.FirstCol);
      END;

   {Now we can take the list of reordered column snips and place them back
   on the ListTable chain.}
   WITH ListTable DO
      BEGIN
      FirstCol := TempList.FirstCol;
      LastCol := TempList.LastCol;
      ColCount := TempList.ColCount;
      END;

   {Transfer the total sort key count.}
   NSKeys := CSKeys;

   {Get rid of the colspec row and column snips now}
   ReleaseRowSnips;
   ReleaseColSnips;

   CurTable := TNone;
   CStatus := 0;
   END;



{$S lmsearch }
PROCEDURE ChangeSrch{var csstatus : integer; var row,column : idtype} ;

   Label 100,999;

   CONST CSrch = ' ChangeSearch ';
         bufsize = 200;

   VAR ListColSnip: PtrColSnip;
       SpecColSnip: PtrColSnip;
       Len: INTEGER;
       Status: INTEGER;
       realid : Integer;
       SpecColId : Idtype;
       ListColId : Idtype;
       srchNull : boolean;
       countcomp : integer;
       Rarray: Array [0..maxrun] of run;
       eptr : ptrexpr;
       foffset : integer;
       voffset : integer;
       tripleIndex : integer;
       cumsize : integer;
       cumvsize : integer;
       lasttriple : integer;
       lastlasttriple : integer;
       tottriples : integer;
       ch,ch2 : char;
       i:integer;
       hdata : hnddata;
       numruns : integer;
       numstr : integer;
       len1,len2 : integer;
       sindex1,sindex2 : integer;
       isbold : boolean;
       runptr : ptrruns;
       result : integer;
       oldtrpindx : integer;
       Col: IdType;
       CurPt: Point;
       VisRg: Range;
       BufferSnip : idtype;
       bufhnd : hnddata;


   BEGIN
{$IFC SAMPCALLS}  ALogCall;  {$ENDC}
   IF TraceMMGR then WriteLn(MxMgr,CSrch);
   row := nullsnip;
   column := nullsnip;

   {Make sure search table is current}
   IF MMTrap1 THEN IF CurTable <> TSearch THEN
      BEGIN
      WriteLn(Bell,'***Col spec table not current');
      END;

   GetSnipBlock(BufferSnip,BufSize,Status);
   bufhnd := POINTER(ord(snipToPtr(buffersnip)));

   {Remember the horizontal scroll position}
   {Get point in current view}
   TMCurVwPt(CurPt);
   IF Trace2MMgr THEN WriteLn('H: ',CurPt.H,' V: ',CurPt.V);
   {Get currently visible range}
   VisRgOfView(CurPt,VisRg);
   SetSnipPtr(ColKind,VisRg.LoCol);
   HScrollId := ColSnip^.ListSnip;

   IF numTriples = 0 then srchNull := True
   Else srchNull := False;

   {loop thru the rows, then for each row, loop thru each cell counting  }
   {the number of comparisons}

   countcomp := 0;
   cumsize := 0;
   cumvsize := 0;

   GetSnipId(rowkind,MMFIRST,0,Row,Row);

   WHILE Row <> PinfSnip do
      BEGIN

      {Loop through all of the col spec snips and transfer information to list.
      Must remember that any operation which changes the value in a snip could
      cause all snips to relocate, consequently the snip pointers must constantly
      be reset.}
      Column := TableData.FirstCol;
      WHILE Column <> NullSnip DO
         BEGIN
         IF Trace2MMGR then
            BEGIN
            WRITELN('row, col: ', row:4, column:4);
            END;

         SpecColId := Column;
         SetSnipPtr(ColKind,SpecColId);
         ListColId := ColSnip^.ListSnip;
         SpecColSnip := ColSnip;
         SetSnipPtr(ColKind,ListColId);
         ListColSnip := ColSnip;

               {Transfer the column width}
         ListColSnip^.Width := SpecColSnip^.Width;

         IF NOT CellBlank(Row,Column) then
            BEGIN
            len := BufSize;
            CellValue(MMREAD,Row,Column,bufhnd^,true,len,status);
            if Status= cvnotenuff then
               begin
               ExpSnipBlock(buffersnip,len,Status);
               CellValue(MMREAD,Row,Column,bufhnd^,true,len,status);
               IF MMTrap1 THEN if status <> ok then MMFatalErr(Status,CSrch,'CellValue 1');
               SetSnipPtr(ColKind,SpecColId);
               SpecColSnip := ColSnip;
               SetSnipPtr(ColKind,ListColId);
               ListColSnip := ColSnip;
               end

            else IF MMTrap1 THEN if status <> ok then MMFatalErr(Status,CSrch,'CellValue 2 ');
            numruns := maxrun;
            CellRun(MMREAD,row,column,@Rarray,numruns,status);
            IF MMTrap1 THEN
              begin
              if status <> ok then mmfatalErr(status,CSrch,'CellRun');
              if numruns > 3 then mmfatalerr(LogicErr,CSrch,'Cellrun');
              end;
            parsesrch(bufhnd^,len,@rarray,numruns,numstr,sIndex1,sindex2,
                      len1,len2,status);
            if status = badSrchOp then
               begin
               csstatus := badsrchop;
               goto 999;
               end;

            with listcolsnip^.fieldinfo do
              begin
              countcomp := countcomp+ numstr;
              cumsize := cumsize + (numstr*size);
              IF (reptype = ftypvstr) OR
                 (reptype = ftypvnull) then
                 cumvsize := cumvsize + len1 + len2;
              end;
            END;

         {continue to loop}
         Column := SpecColSnip^.NextSnip;

         END; {while column}


      GetSnipId(rowkind,MMNext,0,Row,Row);
      END;{row while}

   if Trace2MMGR then
      writeln('countcomp,cumsize,cumvsize ',countcomp:3,cumsize:3,cumvsize:3);

   {Now that we know how many comparisons, lets allocate the tuples and
    constant arrays}

   {3/21 if no search specs then just leave}
   if countcomp = 0 then goto 100;
   tottriples := countcomp*3 + (countcomp-1);

   if Trace2MMGR then writeln('before exprhnd stuff');
   IF ExprHnd = nil then getsnipblock(exprsnip,tottriples*sizeOf(triple),Status)
   ELSE ExpSnipBlock(exprsnip,tottriples*sizeOf(triple),Status);
   ExprHnd := POINTER(ORD(SnipToPtr(exprsnip)));


   if Trace2MMGR then writeln('before consthnd stuff');
   IF ConstHnd = nil then getsnipblock(constsnip,cumsize+cumvsize+2*countcomp,Status)
   ELSE ExpSnipBlock(constsnip,cumsize + cumvsize + 2 * countcomp,Status);
   ConstHnd := POINTER(ORD(SnipToPtr(constsnip)));


   tripleindex := tottriples -1;
   voffset := cumsize + countcomp - 1;
   foffset := 0;
   constsize := voffset  ;
   LastTriple := -1;
   LastLastTriple := -1;

   if Trace2MMGR then writeln('ready to begin second loop');

   {Now loop again thru rows and columns assembling tuples and constants.}

   GetSnipId(rowkind,MMFIRST,0,Row,Row);

   WHILE Row <> PinfSnip do
      BEGIN

      {Loop through all of the col spec snips and transfer information to list.
      Must remember that any operation which changes the value in a snip could
      cause all snips to relocate, consequently the snip pointers must constantly
      be reset.}
      Column := TableData.FirstCol;
      WHILE Column <> NullSnip DO
         BEGIN
         IF Trace2MMGR then
            BEGIN
            WRITELN('row, col: ', row:4, column:4);
            END;

         SpecColId := Column;
         SetSnipPtr(ColKind,SpecColId);
         ListColId := ColSnip^.ListSnip;
         SpecColSnip := ColSnip;
         SetSnipPtr(ColKind,ListColId);
         ListColSnip := ColSnip;

         IF NOT CellBlank(Row,Column) then
            BEGIN
            IF Trace2MMGR then
               BEGIN
               WRITELN('cellblank = false');
               END;

            len := BufSize;
            CellValue(MMREAD,Row,Column,bufhnd^,true,len,status);
            if Status= cvnotenuff then
               begin
               ExpSnipBlock(buffersnip,len,Status);
               CellValue(MMREAD,Row,Column,bufhnd^,true,len,status);
               IF MMTrap1 THEN if status <> ok then MMFatalErr(Status,CSrch,'CellValue 3');
               SetSnipPtr(ColKind,SpecColId);
               SpecColSnip := ColSnip;
               SetSnipPtr(ColKind,ListColId);
               ListColSnip := ColSnip;
               end

            else IF MMTrap1 THEN if status <> ok then MMFatalErr(Status,CSrch,'CellValue 4 ');

            if Trace2MMGR then writeln('len ',len:3);
            numruns := maxrun;
            CellRun(MMREAD,row,column,@Rarray,numruns,status);
            IF MMTrap1 THEN if status <> ok then mmfatalErr(status,CSrch,'CellRun');
            IF MMTrap1 THEN if numruns > 3 then mmfatalerr(LogicErr,CSrch,'Cellrun');
            parsesrch(bufhnd^,len,@rarray,numruns,numstr,sIndex1,sindex2,
                      len1,len2,status);
            if status = badSrchOp then
               begin
               csstatus := badsrchop;
               goto 999;
               end;

            if numstr = 1 then
               begin
               runptr := @Rarray;
               if (numruns = 2) and (sindex1>= runptr^[1].lpfst) and (
                 bold in   runptr^[0].face )then  isbold := true
               else isbold := false;
               setuptriple(tripleindex,bufhnd^,len,isbold,listcolid,speccolid,row,countcomp,
                           foffset,voffset,cumsize,cumvsize,csstatus);
               if csstatus <> 0 then                                                             {#183}
                  begin
                  csstatus := badsrchop;
                  goto 999;
                  end;

               {update ptrs}
               lastlasttriple := lasttriple;
               lasttriple := tripleindex -2 ;
               tripleIndex := tripleIndex - 3;

               IF (lastlasttriple <> -1 ) AND (tripleIndex >= 0 )  then
                  BEGIN {generate an AND node}
                  SetUpAndTriple(tripleindex,lasttriple,lastlasttriple);
                  tripleIndex := tripleIndex -1;
                  lastlasttriple := lasttriple;
                  lasttriple := tripleindex;
                  END;
               end

            else if numstr = 2 then
               begin
               setuptriple(tripleindex,@bufhnd^^[sindex1],len1,false,
                           listcolid,speccolid,row,countcomp,foffset,voffset,
                           cumsize,cumvsize,csstatus);
               if csstatus <> 0 then                                                             {#183}
                  begin
                  csstatus := badsrchop;
                  goto 999;
                  end;
               oldtrpindx := tripleindex ;

               {update ptrs}
               lastlasttriple := lasttriple;
               lasttriple := tripleindex -2 ;
               tripleIndex := tripleIndex - 3;

               IF (lastlasttriple <> -1 ) AND (tripleIndex >= 0 )  then
                  BEGIN {generate an AND node}
                  SetUpAndTriple(tripleindex,lasttriple,lastlasttriple);
                  tripleIndex := tripleIndex -1;
                  lastlasttriple := lasttriple;
                  lasttriple := tripleindex;
                  END;

               {now second comparison}
               setuptriple(tripleindex,@bufhnd^^[sindex2],len2,false,listcolid,
                           speccolid,row,countcomp,foffset,voffset,cumsize,
                           cumvsize,csstatus);
               if csstatus <> 0 then                                                             {#183}
                  begin
                  csstatus := badsrchop;
                  goto 999;
                  end;

               {fix up comparands}
               result := Compare(@ExprHnd^^[oldtrpindx].desc,
                                 @ExprHnd^^[tripleindex].desc,
                                 1,@consthnd^^,@consthnd^^) ;
               if Trace2MMGR then writeln('compare yields ',result:3);
               if result = lt then
                  begin
                  ExprHnd^^[oldtrpindx-2].action := ex_ge;
                  ExprHnd^^[tripleindex -2].action := ex_le;
                  end
               else
                  begin
                  ExprHnd^^[oldtrpindx-2].action := ex_le;
                  ExprHnd^^[tripleindex -2].action := ex_ge;
                  end;


               {update ptrs}
               lastlasttriple := lasttriple;
               lasttriple := tripleindex -2 ;
               tripleIndex := tripleIndex - 3;

               IF (lastlasttriple <> -1 ) AND (tripleIndex >= 0 )  then
                  BEGIN {generate an AND node}
                  SetUpAndTriple(tripleindex,lasttriple,lastlasttriple);
                  tripleIndex := tripleIndex -1;
                  lastlasttriple := lasttriple;
                  lasttriple := tripleindex;
                  END;


               END;

            if Trace2MMGR then writeln('foffset, voffset ',foffset:3,voffset:3);

            IF MMTrap1 THEN
               BEGIN
               IF ((ORD(ConstHnd^) + foffset) > (ORD(ConstHnd^)+ cumsize + countcomp)) OR
                  ((ORD(ConstHnd^) + voffset) > (ORD(ConstHnd^)+ cumsize + countcomp + cumvsize))
                  then BEGIN
                       WriteLn(Bell,'***No constant space');
                       MMFatalErr(LogicErr,CSrch,'');
                       END;
               END;

            END;

         {continue to loop}
         SetSnipPtr(ColKind,SpecColId);
         Column := ColSnip^.NextSnip;

         END; {while column}


      GetSnipId(rowkind,MMNext,0,Row,Row);
      END;{row while}

   if Trace2MMGR then writeln('constsize', constsize);
100:
   IF countcomp = 0 then
      BEGIN
      constsize := 0;
      NumTriples := 0;
      END
   ELSE
      BEGIN
      NumTriples := tottriples;
      END;


   {Free up the run row}
   FreSnipBlock(RunRow);
   FreSnipBlock(Buffersnip);

   {Get rid of the colspec row and column snips now}
   ReleaseRowSnips;
   ReleaseColSnips;
   CurTable := TNone;
   csstatus := 0;

999:
 END;







{$S lmcolspec }
PROCEDURE CheckDataType{VAR DataType: INTEGER;
                           ValuePtr: PtrData;
                           Len: INTEGER;
                           VAR Status: INTEGER};

      {This procedure accepts a pointer to an array which contains an ascii
      string specifying a data type, and a length. If the string is a valid
      data type, the integer representation of the data type is returned.
      Status is set non zero if the array does not match a supported data type.}

      CONST CDT = ' CheckDataType ';

      VAR TypeString: STRING[20];
          DTString: STRING[20];
          TypePtr:ptrdata;
          StrPtr:CompStrP;

      BEGIN
{$IFC SAMPCALLS}  ALogCall;  {$ENDC}
      IF TraceMMGR then WriteLn(MxMgr,CDT);
      StrPtr := @TypeString;
      TypePtr := @TypeString;
      Status := 0;

      IF Len > 20 THEN Len := 20;
      MoveLeft(ValuePtr^,TypePtr^[1],Len);

      {Strip out any blanks}
      StripBlanks(@TypePtr^[1],Len);

      {reset length of string if necessary}
      TypePtr^[0] := Len;

      {Upshift the characters before comparing}
      UpStrShift(StrPtr);
      StrPtr := @DTString;

      DTString := DTTextString;
      UpStrShift(StrPtr);

      IF (Pos(DTString,TypeString) = 1) AND (Len = length(DTTextString)) THEN DataType := Lets
      ELSE
        BEGIN
        DTString := DTNumbString;
        UpStrShift(StrPtr);


        IF (Pos(DTString,TypeString) = 1) AND (Len = length(DTNumbString))
        THEN DataType := Num
        ELSE
          BEGIN
          DTString := DTDateString;
          UpStrShift(StrPtr);


          IF (Pos(DTString,TypeString) = 1) AND (Len = length(DTDateString))
          THEN DataType := Dt
          ELSE
            BEGIN
            DTString := DTSocSString;
            UpStrShift(StrPtr);


            IF (Pos(DTString,TypeString) = 1) AND (Len = length(DTSocSString))
            THEN DataType := SS
            ELSE
              BEGIN
              DTString := DTTeleString;
              UpStrShift(StrPtr);


              IF (Pos(DTString,TypeString) = 1) AND (Len = length(DTTeleString))
              THEN DataType := PH
              ELSE
                BEGIN
                DTString := DTTimeString;
                UpStrShift(StrPtr);


                IF (Pos(DTString,TypeString) = 1) AND (Len = length(DTTimeString))
                THEN DataType := Tim
                ELSE
                  BEGIN
                  DTString := DTZipCString;
                  UpStrShift(StrPtr);


                  IF (Pos(DTString,TypeString) = 1) AND (Len = length(DTZipCString))
                  THEN DataType := Zip
                  ELSE
                    BEGIN
                    DTString := DTMonyString;
                    UpStrShift(StrPtr);


                    IF (Pos(DTString,TypeString) = 1) AND (Len = length(DTMonyString))
                    THEN DataType := Dol
                    ELSE
                      BEGIN
                      DTString := DTTcktString;
                      UpStrShift(StrPtr);


                      IF (Pos(DTString,TypeString) = 1) AND (Len = length(DTTcktString))
                      THEN DataType := tick
                      ELSE Status := InvDTypeErr;
                      END;
                    END;
                  END;
                END;
              END;
            END;
          END;
        END;

      END;



{$IFC MMDEBUG }
{$S lmres  }
PROCEDURE CHECKSNIPID{SnipType: SnipKind;
                      SnipId: IdType};

   {This procedure checks a snip identifier to make sure it is valid and of
   the indicated kind - row or column. A fatal error occurs if any of the
   tests fail.}

   CONST chksid = ' CheckSnipId ';

   VAR h : TH;
       cp : ^ColSnipRecord;
       FSnip: IdType;

   BEGIN
{$IFC SAMPCALLS}  ALogCall;  {$ENDC}
   IF Trace2MMGR then WriteLn(mxMgr,chksid,'Sniptype: ',ORD(sniptype):3,' Snipid: ', snipid:4);

   IF MMTrap1 THEN IF (SnipId <= 0 ) OR (SnipId > MaxSnips) THEN
      Begin
      Writeln(Bell,'***SnipId <= 0 or > MaxSnips');
      MMFatalErr(LogicErr,ChkSID,'');
      End;

   {Make sure snip is not on free list}
   if MMTrap2 then
      begin
      FSnip := FreeSnipList;
      WHILE FSnip <> NullSnip DO
         BEGIN
         IF FSnip = SnipId THEN
            BEGIN
            WriteLn(Bell,'***Snip is on free list');
            MMFatalErr(LogicErr,ChkSID,'');
            END;
          FSnip := SnipMap[FSnip];
         END;
      end;


   h := SnipToPtr(SnipId);
   IF MMTrap1 THEN IF (ORD(h) < ORD(@SegPtr^))
   OR (ORD(h) > (Ord(@SegPtr^) + ZoneSize)) THEN
         BEGIN
         WriteLn(Bell,'***Snip pointer does not point within zone');
         WriteLn('SnipZone: ',Ord(SnipZone));
         WriteLn('SnipMap[SnipId]: ',SnipMap[SnipId]);
         WriteLn('Ord(h):',Ord(h));
         WriteLn('Ord(@SegPtr^: ',Ord(@SegPtr^));
         WriteLn('ZoneSize: ',ZoneSize);
         WriteLn('(Ord(@SegPtr^) + ZoneSize): ',(Ord(@SegPtr^) + ZoneSize));
         MMFatalErr(LogicErr,ChkSID,'');
         END;

   cp := @h^^;
   IF MMTRAP1 THEN if odd(ord(cp)) then
      BEGIN
      writeln(Bell,'***checksnips cp^ is odd');
      MMFatalErr(LogicErr,ChkSId,'');
      END;

   IF MMTrap1 THEN IF cp^.snipid <> SnipId Then
      begin
      writeln(Bell,'***Snip id does not match .SnipId field in record');
      MMFatalErr(LogicErr,ChkSID,'');
      end;

   IF MMTrap1 THEN IF cp^.sniptype <> SnipType Then
      begin
      writeln(Bell,'***Sniptype does not match .SnipType field in record');
      MMFatalErr(LogicErr,ChkSID,'');
      end;


   END;

{$ENDC }



{$S lmcolspec }
PROCEDURE CheckSort{VAR Order: INTEGER;
                       VAR Direction: INTEGER;
                       ValuePtr: PtrData;
                       Len: INTEGER;
                       VAR Status: INTEGER};

      {This procedures accepts a length and a pointer to a byte array which
      contains the ascii representation of the sort direction and order. It
      converts it to the internal representation if valid.}

      LABEL 999;

      CONST CSort = ' CheckSort ';

      VAR SortString: STRING[20];
          UpSrtAsc: STRING[20];
          UpSrtDes: STRING[20];
          SortData: ptrdata;
          StrPtr:CompStrP;
          Position: INTEGER;
          ParsePt: INTEGER;
          Total: INTEGER;

      BEGIN
{$IFC SAMPCALLS}  ALogCall;  {$ENDC}
      IF TraceMMGR then WriteLn(MxMgr,CSort);

      Status := InvSortErr;

      IF Len = 0 THEN
         BEGIN
         Order := 0;
         Direction := 0;
         END

      ELSE BEGIN
           SortData := @SortString;
           StrPtr := @SortString;
           IF Len > 20 THEN Len := 20;
           MoveLeft(ValuePtr^,SortData^[1],Len);

           {Strip out any blanks}
           StripBlanks(@SortData^[1],Len);

           {Reset string length if necessary}
           SortData^[0] := Len;

           {Upshift the characters before comparing}
           UpStrShift(StrPtr);

           {Upshift sort ascending string before comparing}
           StrPtr := @UpSrtAsc;
           UpSrtAsc := SrtAscString;
           UpStrShift(StrPtr);

           {Upshift sort descending string before comparing}
           StrPtr := @UpSrtDes;
           UpSrtDes := SrtDesString;
           UpStrShift(StrPtr);

           Position := Pos(UpSrtAsc,SortString);
           IF Position = 1 THEN
              BEGIN
              ParsePt := length(UpSrtAsc)+1;
              Direction := 0;
              END

           ELSE BEGIN
                Position := Pos(UpSrtDes, SortString);
                IF Position = 1 THEN
                   BEGIN
                   ParsePt := length(UpSrtDes)+1;
                   Direction := 1;
                   END

                ELSE GOTO 999; { Not a valid sort string}
                END;

           {Now figure out the order. If there are no parenthesis, then the
           order is assumed to be 1. Otherwise, there had better be some.}
           IF ParsePt > Len THEN
              BEGIN
              Order := 1;
              END

           ELSE BEGIN
                IF SortString[ParsePt] <> '(' THEN GOTO 999;
                ParsePt := ParsePt + 1;

                Total := 0;
                WHILE SortData^[ParsePt] IN [Ord('0')..Ord('9')] DO
                   BEGIN
                   Total := Total * 10 + SortData^[ParsePt] - Ord('0');
                   ParsePt := ParsePt + 1;
                   IF Total > 200 THEN GOTO 999;
                   END;

                IF SortData^[ParsePt] <> Ord(')') THEN GOTO 999;

                Order := Total;
                END;
           END;

      Status := 0;
999:  END;



{$S lmcolspec }
PROCEDURE CheckVisible{VAR Vis: BOOLEAN;
                          ValuePtr: PtrData;
                          Len: INTEGER;
                          VAR Status};

   CONST CV = ' CheckVisible ';

   VAR VisString: STRING[20];
       UpVisYes: STRING[20];
       UpVisNo: STRING[20];
       VisPtr: ptrdata;
       StrPtr : compstrp;

   BEGIN
{$IFC SAMPCALLS}  ALogCall;  {$ENDC}
   IF TraceMMgr THEN WriteLn(MxMgr,CV);
   Status := 0;
   VisPtr := @VisString;
   StrPtr := @VisString;
   IF Len > 20 THEN Len := 20;
   MoveLeft(ValuePtr^,VisPtr^[1],Len);

   {Reset string length}
   VisPtr^[0] := Len;

   {Upshift the characters before comparing}
   UpStrShift(StrPtr);

   {Strip out blanks}
   StripBlanks(@VisPtr^[1],Len);

   {Upshift visibility YES string before comparing}
   StrPtr := @UpVisYes;
   UpVisYes := VisYesString;
   UpStrShift(StrPtr);

   {Upshift visibility NO string before comparing}
   StrPtr := @UpVisNo;
   UpVisNo := VisNoString;
   UpStrShift(StrPtr);

   IF (Pos(UpVisNo,VisString) = 1) AND (Len = length(UpVisNo)) THEN Vis := FALSE
   ELSE IF (Pos(UpVisYes,VisString) = 1) AND (Len = length(UpVisYes)) THEN Vis := TRUE
   ELSE Status := InvVisErr;
   END;
ÿ