
{$S DBCUnPack}
procedure PutChar(c:byte; dsp:ptrdata; var dsplen:integer);

{****************************************************************************

Function: Stuff a character into the display output array.

Assumes: There is room in the display area for the character.

****************************************************************************}

begin
{$IFC SAMPCALLS}  ALogCall;  {$ENDC}
dsp^[dsplen] := c;
dsplen := dsplen + 1;
end; { of PutChar }


{$S DBCUnPack}
procedure IntToAscii {dsp:ptrdata; var dsplen:integer; value:integer;
    fill:filltype; digits:integer};

{***************************************************************************

Function: Format an integer into the display buffer at the current end of the
          display, updating the display length.  Digits is the number of output
          digits to generate if filltype is not nofill.  The number of digits
          necessary to display the correct number will always be generated.  If
          filltype is zerofill or blankfill, the generated digits will be padded
          with the appropriate character (0 or blank) up to the number specified
          in digits.  Value is the integer to be converted.  If value is -1,
          only fill characters are generated.

Assumes:  There is room in the display buffer for all digits generated.

***************************************************************************}

var     digbuf:     array[0..25] of byte;
        ndigits:    integer;    { number of display digits in number }
        nfill:      integer;    { number of fill digits in number }
        rest:       integer;    { remaining digits }
        fillchar:   byte;       { 0 or space for fill digits }
        i:          integer;

begin
{$IFC SAMPCALLS}  ALogCall;  {$ENDC}
{ put digits into dummy buffer in reverse order }
ndigits := 0; { number of display digits }
if value >= 0 then
    begin
    rest := value;
    repeat
        digbuf[ndigits] := ord('0') + rest mod 10; { least significant digit };
        ndigits := ndigits + 1;
        rest := rest div 10; { remaining digits }
        until (rest = 0) or ((ndigits = digits) and (digits <> 0));
    end; { of action for zero or greater value }

{ put as many fill characters into the output array as are needed }
if fill <> nofill then
    begin
    if fill = blankfill then
        fillchar := ord(' ')
    else
        fillchar := ord('0');
    nfill := ndigits;
    while nfill < digits do
        begin
        PutChar(fillchar, dsp, dsplen);
        nfill := nfill + 1;
        end;
    end;

{ now put the actual digits into the array }
if ndigits > 0 then
  begin
  i := ndigits-1;
  while i >= 0 do
    begin
    PutChar(digbuf[i], dsp, dsplen);
    i := i-1;
    end;
  end;
end; { of IntToAscii }


{$S DBCUnPack}
function ExtractBcd (dbf:ptrDbField; db:ptrData; digitOffset:integer):
                     integer;

{******************************************************************************

Function: Extract the value from the digitOffset'th 4-bit bucket of
          the field described by dbf.

Assumes:  digitOffset >= 0.

******************************************************************************}

var wordOffset: integer;
    dbp:        ptrData;
    bytePtr:    integer;
    halfByte:   integer;

begin
{$IFC SAMPCALLS}  ALogCall;  {$ENDC}
{ find the word offset of the beginning of the field }
if odd (dbf^.offset) then
    begin
    wordOffset := dbf^.offset - 1;
    bytePtr := 2;
    end
else
    begin
    wordOffset := dbf^.offset;
    bytePtr := 0;
    end;

{ now create a pointer to the word within the field }
wordOffset := wordOffset + (2 * ((digitOffset + bytePtr) div 4));
dbp := pointer(ord(db) + wordOffset);

{ now set up the bit pointer within the word }
halfByte := 12 - (((digitOffset + bytePtr) mod 4) * 4);

{ finally, we can deposit the data }
ExtractBcd := Extract (dbp, 4, halfByte);
end; { of ExtractBcd }


{$S DBCUnPack}
procedure DateOut (dbf:ptrdbfield; dsp:ptrdata; maxdsplen:integer;
    var dsplen:integer; db:ptrdata; var errnum:integer);

{*****************************************************************************

Function: Format a date onto the screen.  The general scheme is to format a
          value, skip the corresponding example value in the edit check string,
          then output punctuation from the edit check string.  If there isn't
          enough room for the largest possible date string, dsplen gives the
          amount of space required.  The month is output in numeric, abbreviated
          or spelled out format.  If either of the latter two is used, the
          capitalization in the editcheck string is used.

Assumes: Errnum is 0 on entry.
         The monthNames array has been initialized.
         The editcheck string is valid.

Errors: dspTooSmall - not enough room in dsp array

Calls: IntToAscii
       PutChar
       MonthOut
       ExtractBcd

*****************************************************************************}

label 100;      { exit for null value }

var     i:         integer;    { index }
        j:         integer;    { maximum month length }
        null:      boolean;
        ecp:       ptrLongStr; { alias for editcheck string }
        eci:       integer;    { index on ecp }
        eclen:     integer;    { length of editcheck string }
        cii:       integer;    { index on checkinfo array }
        ciVal:     INTEGER;    { value from checkinfo array }
        d:         integer;    { temporary storage for extracted values }
        done:      boolean;
        fill:      fillType;   { blank or zero padding }
        genDig:    boolean;    { => output zeroes and not blanks }
        monStr:    keyStr;     { month to display }


    procedure PutUpperLowerByte (b:byte; ecIndex:integer);

    { sets the capitalization of the byte b to
        that if the ecIndex component of the edit check string and moves it
        to the output array; assumes b starts off in lower case }

    begin
{$IFC SAMPCALLS}  ALogCall;  {$ENDC}
    if (ORD(ecp^[ecIndex]) in UCalpha) then
        UpChrShift(@b, 1);
    PutChar (b, dsp, dspLen);
    end; { of PutUpperLowerChar }


begin
{$IFC SAMPCALLS}  ALogCall;  {$ENDC}
if dbctest then writeln ('Entering DateOut');

{ find the edit check string }
ecp := pointer(ord(dbf) + dbf^.display);
eclen := length(ecp^);

{ see if the value is actually the null value (all zeros) }
null := true;
with dbf^ do
  begin
  i := offset;
  j := offset+size;
  end;
while i < j do
  begin
  if db^[i] <> 0 then null := false;
  i := i+1;
  end;
if null then goto 100;

{ compute maximum output size }
dsplen := eclen; { start here }
cii := 1;
while cii <= 3 do
  begin
  IF dbf^.editcheck[cii] > 0 THEN
      BEGIN
      { subtract length of component in format }
      dsplen := dsplen-((dbf^.editcheck[cii] DIV 16) MOD 32);

      { add length of output value for component }
      CASE dbf^.editcheck[cii] MOD 8 OF
          1:  { month }
              CASE dbf^.editcheck[5] OF
                  1:  { numeric month }
                      dsplen := dsplen+2;
                  2:  { abbr month }
                      dsplen := dsplen+maxAbbr;
                  3:  { full month }
                      dsplen := dsplen+maxMon;
                  END;
          2:  { day }
              dsplen := dsplen+2;
          3:  { year }
              dsplen := dsplen+dbf^.editcheck[4];
          END;
      END;
  cii := cii+1;
  end;

{ check for enough room in the output array - return dsplen = amount of room
  needed if there isn't enough }
{u}
if dsplen > maxdsplen then errnum := dspTooSmall;
{    begin
    errnum := dspTooSmall;
    goto 100;
    end;}

{ advance through the data and editcheck, putting bytes into the display data
    array }
dsplen := 0;
cii := 1; { index on editcheck checkinfo }
eci := 1; { index on editcheck string }
while (cii <= 3) and (dbf^.editcheck[cii] <> 0) do
    begin
    ciVal := dbf^.editcheck[cii];

    { output any punctuation before component }
    i := (ciVal div 512) + 1;
    IF dbcTest THEN
        WRITELN('i=',i:1, '  eci=',eci:1);
    WHILE eci < i DO
        BEGIN
        dsp^[dsplen] := ord(ecp^[eci]);
        eci := eci+1;
        dsplen := dsplen+1;
        END;
    IF dbcTest THEN
        WRITELN('after outputting punctuation, i=',i:1, '  eci=',eci:1);

    { Output the current field value }
    CASE ciVal MOD 8 OF
        1:  { month }
            begin
            d := ExtractBcd (dbf, db, 4) - 1;
            if d >= 0 then
                d := d * 10 + (ExtractBcd (dbf, db, 5) - 1);

            case dbf^.editCheck[5] of
                0:  { Numeric month }
                    begin
                    IF ODD(ciVal DIV 8) THEN
                        fill := blankFill
                    else
                        fill := zeroFill;
                    IntToAscii (dsp, dsplen, d, fill, 2);
                    end;
                1,2:  { month spelled out }
                    begin
                    { figure out the max length of the month }
                    if dbf^.editCheck[5] = 1 {abbr} then
                        j := maxAbbr
                    else
                        j := maxMon;

                    if (d > 0) then
                        begin
                        { get the month to display }
                        if (dbf^.editCheck[5]=1) { abbreviation } and
                           (monthAbbr[d]<>'') then
                          monStr := monthAbbr[d]
                        else
                          monStr := keyword[d];

                        { insert the padding }
                        i := 1;
                        while i <= j-length(monstr) do
                          begin
                          PutChar(ord(' '), dsp, dsplen);
                          i := i+1;
                          end;

                        { insert the month }
                        PutUpperLowerByte(ord(monStr[1]),eci);
                        i := 2;
                        while i <= length(monstr) do
                           begin
                           PutUpperLowerByte(ord(monstr[i]),eci+1);
                           i := i+1;
                           end;
                        end
                    else
                        IntToAscii (dsp, dspLen, d, blankFill, j);
                    end; {spelled out month case}
                end; { of case }
            end; { of action for month }

        2:  { day }
            begin
            d := ExtractBcd (dbf, db, 6) - 1;
            if d >= 0 then
                begin
                d := d * 10 + (ExtractBcd (dbf, db, 7) - 1);
                if ODD(ciVal DIV 8) then
                    fill := blankFill
                else
                    fill := zeroFill;
                end
            else
                fill := blankFill;
            IntToAscii (dsp, dsplen, d, fill, 2);
            { 0 digits => only put in the number of digits which actually
                exist - don't fill }
            end;

        3: { year }
            begin
            genDig := false;
            if (dbf^.editCheck[4] > 0) then
                i := 5-dbf^.editcheck[4];
                while i <= 4 do
                    begin
                    if (ecp^[eci] = '0') then
                        genDig := true;
                    d := ExtractBcd (dbf, db, i-1) - 1;
                    if (d > 0) or
                       ((d = 0) and genDig) then { output year digit }
                        begin
                        PutChar(d + {ord('0')} 48, dsp, dspLen);
                        genDig := true;
                        end
                    else { null or blank-filled year digit }
                        PutChar (ord(' '), dsp, dspLen);
                    i := i+1;
                    end;
            end;
        END; { CASE }

    eci := eci+((ciVal DIV 16) MOD 32);

    cii := cii + 1;
    END; { of while }

{ output trailing punctuation }
while eci<=eclen do
  begin
  dsp^[dsplen] := ord(ecp^[eci]);
  eci := eci + 1;
  dsplen := dsplen + 1;
  end;

100: end; { of DateOut }


{$S DBCUnPkStr}
Procedure CharOut(dbf:ptrdbfield; dsp:ptrdata; maxdsplen:integer;
    var dsplen:integer; db:ptrdata; var errnum:integer);

{*****************************************************************************

Function: Outputs to dsp the contents of the character field described by dbf
          in the record pointed to by db.  If the output array is too small,
          maxDspLen characters are moved into dsp and the amount of space
          required for the whole string is returned in dspLen.

Errors: display value array too small (dspTooSmall)

*****************************************************************************}

label 100; { error exit }

var     dspi:     integer;
        i:        integer;
        endDsp:   integer;
        eci:      integer;
        eclen:    integer;
        start:    integer;
        vf:       ptrvfld;
        moveLen:  integer;
        ecp:      ^longstr;
        ce:       char;

begin
{$IFC SAMPCALLS}  ALogCall;  {$ENDC}
if dbctest then writeln ('Entering CharOut');

{ find the start and length of the actual text }
if dbf^.reptype = ftypstr then { fixed-length text string }
    begin
    start := dbf^.offset;
    if db^[start] = 0 then { field is null }
        dsplen := 0
    else
        begin
        dsplen := dbf^.size;
        while (db^[start+dsplen-1] = ord(' ')) and (dsplen > 0) do
            dsplen := dsplen - 1;
        end;

    endDsp := start + dsplen;
    if dbctest then
        writeln('fixed length: dsplen=', dsplen:1);
    end

else { variable-length text string }
    begin
    vf := pointer(ord(db) + dbf^.offset);
    start := vf^.offset;
    dsplen := vf^.size;
    endDsp := start + dsplen;
    if dbctest then
        writeln('variable length: dsplen=', dsplen:1);
    end;

if (dbf^.editcheck[4]<>0) and (dsplen>0) then
    begin
    ecp := pointer(ord(dbf)+dbf^.display);
    eclen := ORD(ecp^[0]) - 1; {-1 to allow for close quote}
    dsplen := dsplen + Length(ecp^) - 2{the quotes} - dbf^.editcheck[4];
                       {number of punctuation characters in pattern}
    if dbctest then
        writeln('pattern: dsplen=', dsplen:1);
    end;

{ check for error }
if dsplen > maxdsplen then
    begin
    errnum := dspTooSmall;
    moveLen := maxDspLen;
    end
else
    moveLen := dspLen;

{ move the characters into the display value array }
if dbf^.editcheck[4]<>0 then {pattern}
  begin
  i := 0;
  eci := 2; {0th is string length; 1st is open quote}

  i := 0;
  while i < movelen do
      BEGIN
      if eci > eclen then
          ce := wild1Char {if too few pattern chars, output db char}
      else
          ce := ecp^[eci];

      if dbcTest then
        writeln(ce,' ',ord(ce):1,' ',ord(ce) in spcl);

      if (ce in ['0'..'9']) or (ce=wild1Char) or (ce=wild2Char) or
         (ord(ce) in alphaSet) then
        begin
        if start >= endDsp then
            begin
            if dbctest then
              writeln('early termination; i=', i:1);
            dsplen := i; {the actual length}
            goto 100;
            end;

        dsp^[i] := db^[start];    { get next character from db field }
        start := start+1;
        end

      else
        dsp^[i] := ord(ce);       { get punctuation from ec }

      eci := eci+1;
      i := i+1;
      end;
  end

else if moveLen > 0 then MoveLeft(db^[start], dsp^[0], moveLen);

100: end; { of CharOut }


{$S DBCUnPack}

procedure NumbOut (dbf:ptrdbfield; dsp:ptrdata; maxDspLen:integer;
              var dsplen:integer; db:ptrdata; var errNum:integer);

{****************************************************************************

Function: Output a number.  This can be a REAL pain, but the edit check will
          "show us the way . . .".  Dsplen will be set to the length of the
          output string.

Assumes: Value may be null.
         Dsplen is 0 on entry.
         The editcheck may have been changed via ReplaceEditCheck, but the
             repType hasn't changed.

Errors:  display value area is too small (dspTooSmall)

Calls: Quad int arithmetic procedures

****************************************************************************}

label   100;    { exit for null case }

const   noPad = 'x';

        { error numbers }
        dspTooSmall = 3100;

        { this should be the size of the maximum length of the output
            value; it should be the maximum size of an edit check
            plus about 20 extra, because this routine can insert a
            sign or digits that do not appear in the format }
        maxOutputLength = 60;

var     i:           integer;      { byte or integer number }
        l:           longint;      { long integer number }
        qnum:        quadint;      { quad arithmetic accumulator }
        nptr:        nptrrec;      { pointer for aliasing for right type }
        ntype:       integer;      { low-level representation type }
        ip:          ^integer;     { pointer for aliasing }
        lp:          ^longint;     { pointer for aliasing }
        negative:    boolean;
        signChar:    char;         { char to output for the sign }
        crdrComp:    keystr;

        e:           boolean;      { results of quad arithmetic operations }

        digbuf:      array [1..21] of 0..9;
        diglen:      integer;      { number of digits in output number }
        digi:        integer;      { index on digbut }
        dig:         integer;
        digleft:     integer;      { number of digits positions left
                                     in edit check string }
        curpad:      char;         { current type of padding }

        ecinfo:      checkinfo;
        ecp:         ^longstr;     { alias for editcheck string }
        eci:         integer;      { index into editcheck string }
        eclen:       integer;      { length of editcheck string }
        outbuf:      array[1..maxOutputLength] of byte;
        outi:        integer;

        done:        boolean;
        c:           char;
        c2:          char;

        dollarOut:   boolean;
        signOut:     boolean;

    procedure InsertSign;
        { if needed, put sign in output and also get any spaces after
            sign char from ec; call this at all places where the sign
            might want to be output
          this routine handle only +, -, <, and ( sign characters,
            not CR/DR  }
    var x:      INTEGER;
    begin
{$IFC SAMPCALLS}  ALogCall;  {$ENDC}
        if dbcTest then
            writeln('signOut=',signOut,
                    '  eci=',eci:1,
                    '  ecinfo[1]=',ecinfo[1]:1);
        if not signOut and
           ((eci > ecinfo[1]) { we already passed the sign in the ec} or
            (ecinfo[1] = 0))  { there is no sign } then
            begin
            signOut := true;

            outbuf[outi] := ord(signChar);
            outi := outi + 1;
            if ecinfo[1] > 0 then
                begin
                x := ecinfo[1] + 1;
                while (x <= eclen) do
                    begin
                    if (ecp^[x]=' ') or (ecp^[x]=reqSpace) then
                        begin
                        outbuf[outi] := ord(ecp^[x]);
                        outi := outi+1;
                        end
                    else
                        EXIT(InsertSign);
                    x := x + 1;
                    end;
                end;
            end;
    end;
begin
{$IFC SAMPCALLS}  ALogCall;  {$ENDC}
if dbctest then writeln ('Entering NumbOut');

{$IFC DEBUG}
{ make sure that we have enough space in the output array }
if maxOutputLength < ecstrBnd + 20 then
    writeln('WARNING: maxOutputLength < ecstrBnd + 20 in NumbOut');
{$ENDC}

CLRQ (qnum);

{ Extract the value into a quad int array }
ntype := dbf^.reptype;
nptr.ip := pointer(ord(db) + dbf^.offset);

if ntype = ftypint1 then { 8-bit integer }
        begin
        if nptr.bp^[0] = nullint1 then goto 100;
        i := nptr.bp^[0];
        LDQI (qnum, i);
        end

else if ntype = ftypint2 then { 16-bit integer }
        begin
        if nptr.ip^ = nullint2 then goto 100;
        LDQI(qnum, nptr.ip^);
        end

else if ntype = ftypint4 then { 32-bit integer }
        begin
        l := nptr.lp^;
        if l = nullint4 then goto 100;
        ip := @l;
        if l < 0 then
            begin
            l := -l;
            qnum[2] := ip^; { assumes qnum = array[0..3] of integer }
            ip := pointer(ord(ip) + 2);
            qnum[3] := ip^;
            e := NEGQ (qnum);
            end
        else
            begin
            qnum[2] := ip^;
            ip := pointer(ord(ip) + 2);
            qnum[3] := ip^;
            end;
        end

else if ntype = ftypint8 then { 64-bit integer }
        begin
        lp := pointer(ord(db) + dbf^.offset + 4);
        if (nptr.lp^ = nullint4) and (lp^ = 0) then goto 100;
        qnum := nptr.qp^;
        end

else { ticket (48-bit integer) }
        begin
        qnum[1] := nptr.ip^;
        ip := pointer(ord(nptr.ip) + 2);
        qnum[2] := ip^;
        ip := pointer(ord(nptr.ip) + 4);
        qnum[3] := ip^;
        if (qNum[1] = 0) and (qNum[2] = 0) and (qNum[3] = 0) then
            goto 100;
        end;

if dbctest then writeln ('Retrieved number: ', qnum[0], qnum[1], qnum[2],
    qnum[3]);

{ Now generate all the digits into a byte buffer }
negative := false;
if TSTQ(qnum) < 0 then
    { remember to output the sign, but print the digits as if they were positive }
    begin
    negative := true;
    e := NEGQ(qnum);
    end;

{ find the individual digits and put them into a buffer }
diglen := 0; { invariant: # digits in the digbuf so far }
repeat
    e := DIVQIR(qnum, 10, dig);
    diglen := diglen + 1;
    digbuf[diglen] := dig;
until TSTQ(qnum) = 0;

if dbctest then
    begin
    writeln ('Generated digits: ');
    i := 1;
    while i <= diglen do
      begin
      write (digbuf[i]:3);
      i := i+1;
      end;
    writeln;
    end;

ecp := pointer(ord(dbf) + dbf^.display);
eclen := length(ecp^);
ecinfo := dbf^.EditCheck;

dollarOut := false;
signOut := false;

if negative then
    signChar := '-'
else
    signChar := ' ';
curPad := noPad;

{ Now spin through the edit check and output the value. }
outi := 1; { index through outbuf }
eci := 1;  { index through editcheck }
digi := diglen; { index through digits (backwards; leftmost digit first) }
                { digi indicates the nubmer of digits left in the digbuf }
digleft := ecinfo[3]+ecinfo[5];

while eci <= eclen do
    begin

    c := ecp^[eci];
    case c of

      '0', '1', '2', '3', '4', '5', '6', '7', '8', '9', '#',
      '$', sterling, yen, '*': begin
        { note: digleft must be >0 if we get here }

        if dollarOut or ((c<>'$') and
                         (c<>sterling) and
                         (c<>yen)) then
          begin { first dollar/sterling/... is NOT a digit }

          if digleft>digi
          then begin      { not enough digits in digbuf }
            if (curPad='0') or (c='0') then
              begin
              insertsign;
              outbuf[outi]:=ord('0');
              curPad:='0';
              end
            else if (curPad=' ') or ((c>='1') and (c<='9')) then
              begin
              { no InsertSign since sign should float to right past spaces }
              outbuf[outi]:=ord(' ');
              curPad:=' ';
              end
            else if (curPad='*') or (c='*') then
              begin
              InsertSign;
              outbuf[outi]:=ord('*');
              curPad:='*';
              end
            else curPad:=noPad;
            if curPad<>noPad then
              outi := outi+1;
            end

          else
            begin      { enough digits left in digbuf }
            InsertSign;
            i := digi;
            while i >= digleft do
              begin
              outbuf[outi] := digbuf[i] + ord('0');
              outi := outi+1;
              i := i-1;
              end;
            digi := digleft-1;
            curPad := '0';
            end;

          digleft:=digleft-1; { one less digit in edit check }

          end  { of case of an actual digit position in edit check }

        else
            begin { found first dollar/sterling/... so not a digit position }
            InsertSign;
            outbuf[outi] := ord(c);
            outi := outi+1;
            dollarOut:=true;
            end;

        end; { of case for possible digit-generating characters }

      '(', '<', '-', '+': begin { sign indicators }
        if (c='+') then
            if negative then
                signChar := '-'
            else
                signChar := '+'
        else if negative then
            signChar := c
        else
            signChar := ' ';

        if digleft=0 then
            begin
            { can insert sign now because we are after all the digits }
            outbuf[outi] := ord(signChar);
            outi := outi+1;
            signOut := true;
            end
        else
            { the sign will be output before the first digit, the
                    decimal separator, etc. };
        end;

      ')', '>': begin { closing sign indicators }
        if negative then outbuf[outi] := ord(c)     { output the character }
        else outbuf[outi] := ord(' ');
        outi := outi+1;
        end;

      ',', '.', ' ', reqSpace: begin { comma or period or space }

        if (eci=ecinfo[4]) then    { this is the fraction char }
          begin

          { maybe output sign before fraction part }
          InsertSign;

          { may have to output digits now, if there are none to left
            of fraction char in edit check }
          while digi>digleft do
            begin
            outbuf[outi] := digbuf[digi] + ord('0');
            outi := outi+1;
            digi:=digi-1;
            end;

          outbuf[outi] := ord(c);
          outi:=outi+1;

          curPad := '0'; {zero-pad after decimal point}
          end

        else if (curPad='0')         { already put out a digit } or
           (((c=' ') or (c=reqSpace)    { this is a space } or
             (c='.'))                   { a period } and
            ((digleft=ecinfo[3]+ecinfo[5])      { it is before } or
             (digleft=0))) then                 { after }
          begin                         { all digit places in edit check string }
          outbuf[outi] := ord(c);       { output the character }
          outi := outi+1;
          end

        else if curPad<>noPad then      { else output the padding }
          begin
          outbuf[outi] := ord(curPad);
          outi := outi+1;
          end;

        end;

      otherwise begin
        if eci=ecinfo[1] then { this is the sign (cr/dr);
                                    it must be after the number }
          begin
          if negative then
            crdrComp := keyword[kwCR]
          else if keyMatch(POINTER(ORD(ecp)), eci, eclen, keyword[kwDR])
               then crdrComp := keyword[kwDR]
               else crdrComp := ' ';

          outbuf[outi] := ord(crdrComp[1]);
          if dbctest then
            writeln('char 1: ord(c)=',ord(c):1);

          if (ord(c) in UCalpha) then
            begin
            if dbctest then
                writeln('before upshift=', outbuf[outi]:1);
            UpChrShift(POINTER(ORD(@outbuf[outi])), 1);
            if dbctest then
                writeln('after  upshift=', outbuf[outi]:1);
            end;

          outi := outi+1;

          c := ecp^[eci+1];
          if dbctest then
            writeln('char 2: ord(c)=',ord(c):1);

          i := 2;
          while i <= length(crdrcomp) do
            begin
            outbuf[outi] := ord(crdrComp[i]);

            if (ord(c) in UCalpha) then
                begin
                if dbctest then
                    writeln('before upshift=', outbuf[outi]:1);
                UpChrShift(POINTER(ORD(@outbuf[outi])), 1);
                if dbctest then
                    writeln('after  upshift=', outbuf[outi]:1);
                end;

            outi := outi+1;
            i := i+1;
            end;

          i := length(crdrcomp);
          while i <= maxcrdr do
            begin { pad with spaces on the right }
            outbuf[outi] := ord(' ');
            outi := outi+1;
            i := i+1;
            end;

          if keyMatch(POINTER(ORD(ecp)), eci, eclen, keyword[kwCR]) then
            eci := eci + length(keyword[kwCR])
          else
            eci := eci + length(keyword[kwDR]);
          end

        else begin { not CR/DR, must be units symbol }
          insertsign;
          outbuf[outi] := ord(c);
          outi := outi+1;
          end;

        end; { alphabetics case }
      end; { of case }

    eci := eci + 1;
    end; { of spinning through the edit check }

if dbctest then
    begin
    writeln ('Generated bytes: ');
    i := 1;
    while i <= (outi-1) do
      begin
      write (outbuf[i]);
      if ((i+1) mod 10)=0
      then writeln;
      i := i+1;
      end;
    writeln;
    end;

if (outi - 1) > maxDspLen then
    begin
    errNum := dspTooSmall;
{u}
{   dspLen := outi - 1;}
    end;
{else}
    i := 1;
    while i < outi do
      begin
      putchar(outbuf[i], dsp, dspLen);
      i := i+1;
      end;

100: end; { of NumbOut }

{$S DBCUnPack}
procedure BcdOut (dbf:ptrdbfield; dsp:ptrdata; maxdsplen:integer;
    var dsplen:integer; db:ptrdata; var errnum:integer);

label 50;

var dbi:    integer;    { index on db }
    dbx:    integer;
    null:   boolean;
    ecp:    ptrLongStr; { alias for editcheck string }
    eci:    integer;    { index on ecp }
    ecLen:  integer;    { length of editcheck string }

begin
{$IFC SAMPCALLS}  ALogCall;  {$ENDC}
{ see if the value is actually the null value (all zeros) }
null := true;
with dbf^ do
  begin
  dbi := offset;
  dbx := offset+size-1;
  end;
while dbi <= dbx do
  begin
  if db^[dbi] <> 0 then null := false;
  dbi := dbi+1;
  end;
if null then goto 50;

{ find the edit check string }
if (dbf^.display = fnilval) then
    ecLen := 0
else
    begin
    ecp := pointer(ord(dbf) + dbf^.display);
    eclen := length(ecp^);
    end;

if (ecLen > maxDspLen) then
    begin
    errNum := dspTooSmall;
{u}
{   dspLen := ecLen;
    goto 50;}
    end;

{ advance through the data and editcheck, putting bytes into the display
    data array }
dbi := 0;
{ if this is a phone number field and there is no area code, start after
    the first three digits }
if (dbf^.fldtype = ph) and (dbf^.editcheck[5] <> 0) then
    dbi := 3;

{ set up a pointer to word containing first byte }
eci := 1;  { editcheck index }
while eci <= eclen do
    begin
    if ecp^[eci] in ['0' .. '9'] then { place for a number from the data }
        begin
        dsp^[dsplen] := 47 + ExtractBcd (dbf, db, dbi);
        if dsp^[dsplen] = 47 {null digit} then
          dsp^[dsplen] := 32 {space};
        dbi := dbi + 1;
        end { of action for outputting a numeric digit }
    else { it's a punctuation digit }
        begin
        dsp^[dsplen] := ord(ecp^[eci]);
        end; { of action for outputting a punctuation digit }
    dsplen := dsplen + 1;
    eci := eci + 1;
    end; { of while loop }
50: end; { of BcdOut }


{$S DBCUnPack}
procedure TimeOut (dbf:ptrdbfield; dsp:ptrdata; maxdsplen:integer;
    var dsplen:integer; db:ptrdata; var errnum:integer);

LABEL 50;

CONST oSpace=32;  { ord(' ') }
      makeLower=32; { ord('a')-ord('A') }

VAR tvals:      timevalues;
    pm:         BOOLEAN;
    null:       BOOLEAN;
    dbi:        INTEGER;       { index on db }
    ecp:        ptrLongStr;    { alias for editcheck string }
    eci:        INTEGER;       { index on ecp }
    ecc:        CHAR;
    ecLen:      INTEGER;       { length of editcheck string }
    i:          INTEGER;
    v:          INTEGER;       { value of time component }
    comp:       compStr;
    cii:        INTEGER;       { index on checkinfo array }
    fill:       filltype;
    timetype:   INTEGER;      { type of time component }
    ciVal:      INTEGER;       { value from checkinfor array }

BEGIN
{$IFC SAMPCALLS}  ALogCall;  {$ENDC}
{ see if the value is actually the null value (all zeros) }
null := true;
with dbf^ do
  begin
  dbi := offset;
  i := offset+size-1;
  end;
while dbi <= i do
  begin
  if db^[dbi] <> 0 then null := false;
  dbi := dbi+1;
  end;
if null then goto 50;

{ find the edit check string }
if (dbf^.display = fnilval) then
    ecLen := 0
else
    begin
    ecp := pointer(ord(dbf) + dbf^.display);
    eclen := length(ecp^);
    end;

{ compute maximum length of output }
dsplen := eclen; { start with this }
cii := 1;
while cii <= 4 do
  begin
  IF dbf^.editcheck[cii] > 0 THEN
      BEGIN
      { subtract length of component in format }
      dsplen := dsplen-((dbf^.editcheck[cii] DIV 16) MOD 32);

      { add length of output value for component }
      IF dbf^.editcheck[cii] MOD 8 <> 4 THEN
          dsplen := dsplen+2
      ELSE
          dsplen := dsplen+maxAMPM;
      END;
  cii := cii+1;
  end;

if (dsplen > maxDspLen) then
    begin
    errNum := dspTooSmall;
{u}
{   goto 50;}
    end;

{ extract hours, minutes, seconds }
dbi:=0;
i := 1;
while i <= 3 do
    begin
    v:=ExtractBcd(dbf,db,dbi)-1;
    if v>=0 then
        tvals[i]:=10*v+ExtractBcd(dbf,db,dbi+1)-1
    else
        tvals[i]:=-1;
    dbi:=dbi+2;
    i := i+1;
    end;

pm:=false;
if (dbf^.editcheck[5]=0) then { am/pm wanted }
    BEGIN
    if (tvals[1]>=12) then
        begin
        tvals[1]:=tvals[1]-12;
        pm:=true;
        end;

    if tvals[1]=0 then
        tvals[1]:=12;
    END;

if dbcTest then
  writeln('hour:',tvals[1],' minute:',tvals[2],
         ' second:',tvals[3],' pm:',pm);

{ advance through the data and editcheck, putting bytes into the display
    data array }
dsplen := 0;
cii := 1;  { index on editcheck checkinfo, which gives the existence and
    order of components }
eci := 1;  { editcheck index }

while (cii <= 4) and (dbf^.editcheck[cii] <> 0) do
    { editcheck = 0 => no more components }
    begin
    ciVal := dbf^.editcheck[cii];

    { output any punctuation before component }
    i := (ciVal div 512) + 1; { plus 1 because ec is a string }
    IF dbcTest THEN
        WRITELN('i=',i:1, '  eci=',eci:1);
    WHILE eci < i DO
        BEGIN
        dsp^[dsplen] := ord(ecp^[eci]);
        eci := eci+1;
        dsplen := dsplen+1;
        END;

    { see if zero- or blank-filled }
    IF ODD(ciVal DIV 8) THEN
        fill := blankFill
    ELSE
        fill := zeroFill;

    { output the current field value }
    timetype := ciVal MOD 8;

    case timetype of
    1,2,3:
      begin
      if tvals[timetype]<0 then { null component }
            fill:=blankFill;
      IntToAscii (dsp, dsplen, tvals[timetype], fill, 2);
      end;

    4:begin
        if tvals[1] < 0 then
            comp := ''          { no am/pm at all if no hour }
        else if pm then
            comp := timeComps[tkwPM]
        else
            comp := timeComps[tkwAM];

        i := 1;
        while i <= length(comp) do
            BEGIN
                { set case of ampm according to case of format }
            IF i=1 THEN
                ecc := ecp^[eci]
            ELSE
                ecc := ecp^[eci+1];

            dsp^[dsplen] := ord(comp[i]);
            IF ORD(ecc) in UCalpha THEN
                UpChrShift(POINTER(ORD(dsp) + dsplen), 1);

            dsplen := dsplen+1;
            i := i+1;
            END;

        {pad am/pm to maximum length}
        i := 1;
        v := maxampm-length(comp);
        while i <= v do
            BEGIN
            dsp^[dsplen] := oSpace;
            dsplen := dsplen + 1;
            i := i+1;
            END;
      end; { am/pm }
    end; { case }

    { skip the current field in the edit check }
    eci := eci + ((ciVal DIV 16) MOD 32);
    cii := cii + 1;
    end; { of while for outputting components }

{ output trailing punctuation }
while eci<=eclen do
  begin
  dsp^[dsplen] := ord(ecp^[eci]);
  eci := eci + 1;
  dsplen := dsplen + 1;
  end;

50:
end; { of TimeOut }


{$S DBCUnPack}
procedure UnPack {dbf:ptrdbfield; dsp:ptrdata; maxdsplen:integer;
    var dsplen:integer; db:ptrdata; var errnum:integer};

{******************************************************************************

Function: Converts the value in the record pointed to by db of the field
          described by dbf to a displayable string in dsp.  The displayed length
          (dsplen) is returned.  If there isn't enough room in the display
          array, the length needed is returned in dsplen.

Assumes: The editcheck has been checked for legality.
         The editcheck may have been modified via ReplaceEditCheck, but the
             repType never changes.

Errors: not enough room in display-string array (dspTooSmall)

Calls: IntToAscii
       DateOut
       CharOut
       NumbOut

******************************************************************************}

label 100; { error and early exit point }

begin
{$IFC SAMPCALLS}  ALogCall;  {$ENDC}
if dbctest then writeln ('Entering Unpack');
errnum := 0;

{ initialize the size of the displayed data }
dsplen := 0;

case dbf^.fldtype of
    zip,
    ss,
    ph:  { BCD values, all of them (stored on byte boundaries).  The strategy
             is to copy out the BCD digits one at a time, inserting punctuation
             from the edit check string as encountered. }
         BcdOut (dbf, dsp, maxdsplen, dsplen, db, errnum);

    tim: { Time value.  The time is stored as 6 BCD digits, but we must
             extract them in the order called for by the edit check. }
         TimeOut (dbf, dsp, maxdsplen, dsplen, db, errnum);

    dt:  { Dates are stored as a simple record, but allow many variations on
             output }
         DateOut (dbf, dsp, maxdsplen, dsplen, db, errnum);

    lets:{ Output character field }
         CharOut (dbf, dsp, maxdsplen, dsplen, db, errnum);

    dol, num, tick:
         { Output number field (may be monetary as well) }
         NumbOut (dbf, dsp, maxDspLen, dspLen, db, errNum);

    end; { of case for data types }

if dbctest then writeln ('Exiting UnPack');

100: end; { of UnPack }

ÿ