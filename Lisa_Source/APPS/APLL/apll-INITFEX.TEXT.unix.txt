
                  {ERROR HANDLERS AND GROWPROCS }

{$S lminit}
FUNCTION HeapError(hz: Thz; bytesNeeded: INTEGER): INTEGER;
   VAR memexpansion,
       discexpansion,
       newdiscsize,
       newmemsize : longint;
       status : integer;

    BEGIN
{$IFC SAMPCALLS}  ALogCall;  {$ENDC}
    {always ensure that there is sufficient disk space so QuickDraw won't crap}
    if PrintProcs then writeln('heaperror');

    {grow in 1k increments}
    IF bytesneeded < {2*512} 1024 then memexpansion := {2*512} 1024
    else memexpansion := bytesneeded;
    IF ((lmheapsize + memexpansion) < lmdiscsize ) then discexpansion := 0
    ELSE discExpansion := memexpansion;
    if mmtrap1 then
       BEGIN

       WRITELN('**** LM heap full.  Expanding memory from ',
               lmheapsize,' to ',lmheapsize+memexpansion);
       IF discExpansion <> 0 then WRITELN(chr(chbell),
                       '****    Expanding disc from ',
                       lmdiscsize,' to ',lmdiscsize+discexpansion)
       ELSE WRITELN('DiscSize ',lmdiscsize);
       END;

    size_dataseg(status,lmheaprefnum,memexpansion,newmemsize,discexpansion,newdiscsize);
    if status > 0 then
       Begin
       {this throws us into term exception handler}
       MMFatalErr(0,'heaperror','outofspace - theheap');
       END
    else if status < 0 then
       begin
       if mmtrap1 then
         writeln('warning from sizedataseg status is ', status);
       end
    else
       begin
       HeapError := newmemsize-lmheapsize;
       lmheapsize := newmemsize;
       if mmtrap1 then WRITELN('LMheap expanded to ',newmemsize,
                                            ' disc ',newdiscsize);
       end;
    END;



{$S lmfiler}
Procedure TermListMgr;
   BEGIN
{$IFC SAMPCALLS}  ALogCall;  {$ENDC}
   {Terminate Exception handler}
   if PrintProcs then writeln('termListManager');
   ImDying;
   if not fileclosed then AbortCloseDocument;
   END;

{$S lminit}
procedure mygrafproc(error:integer);
   Begin
{$IFC SAMPCALLS}  ALogCall;  {$ENDC}
   {Graf Error procedure just ignore errors - insure that they dont happen
    with grow proc}
   if mmtrap1 then
      begin
      writeln('quickdraw error ',error);
      end;
   end;


{$S lminit}
Procedure initfexec(var istatus:Freason);
{ONE time initialization code}
Label
      999;

{$IFC NOT fexecdebug}
const
     traceinit = false;
{$ENDC}
var
     I : integer;
     Status:        INTEGER;             {temporary used for return codes from proced.}
     temppt : point;
     segname : pathname;
     toolpath:pathname;
     thsegptr : longint;
     thldsn : integer;
     refnum : integer;
     exceptname : T_Ex_Name;
     pinfo : procinforec;
     LMmcString : stringtype;
     DBCmcString : stringtype;
     DataPtr : PtrData;
     LMMarketCode : integer;
     DBCMarketCode : integer;

{$IFC fexecdebug}

     traceinit : boolean;
{$ENDC }

     hsbh,
     hsbv  : thsb;

     mfile : text;
     tempstr : string[255];






BEGIN
{$IFC SAMPCALLS}  ALogCall;  {$ENDC}

{$IFC fexecDebug }
Writeln;Writeln;
Writeln('                    List Manager  ');
WRiteln;Writeln;
{$ENDC }
tempstr := 'Copyright 1983,1984, Apple Computer Inc.';

{initialize internation compare flag}
VeryWeak := TRUE;
IF traceInit then writeln('international; veryweak : ',veryweak);

PictureHdl := NIL;

Sched_Class(status,true);

{$IFC fexecDebug }
PrintProcs := FALSE;
tracing := FALSE;
traceinit := true;
{$ENDC }


{file is closed -- if we barf in this routine,dont call closedoc}
fileclosed := true;

IF traceInit then Write('InitGraf ');
InitGraf(@theport);
IF traceInit then Writeln('*');

IF traceInit then Write('OpenWm ');
OpenWM;
setport(deskport);
IF traceInit then Writeln('*');

exceptname := 'SYS_TERMINATE';
Declare_Except_Hdl(status,exceptname,@TermListMgr);
IF MMTRAP1 then if  (status > 0) THEN
   BEGIN
   Writeln('exception handler init error ', status:4);
   istatus := internalerror;
   exit(initfexec);
   end;
(*
{aw}
{set up universal text procedure pointers}
InitLLCutProcs
  (@CellBlank,
   @CellInfo,
   @CellValue,
   @FinalDataSnip,
   @GetNxtVisCol,
   @GetPrvVisCol,
   @GetSnipId,
   @RgInScrap,
   @SnipWidth,
   @SwitchTable
  );
*)
{Set up flags for tracing.  initialize them to false}

{$IFC TEDebug }
traceFMGR := FALSE;
{$ENDC }
{$IFC MMDebug }
traceMMGR := FALSE;
trace2MMGR := FALSE;
TraceLMPrint := FALSE;
{$ENDC }
{$IFC tmdebug }
traceTMGR := FALSE;
traceXTMGR := FALSE;
traceSMGR := FALSE;
{$ENDC }

setdbctest( false );

{initialize debug flags}
{$IFC PRERELEASE}
lmdebug := false ;
dbdebug := FALSE;
{$ENDC }



skipnextmouseup := false;


{get tool path name }
INFO_process(status,My_Id,pinfo);
if mmtrap1 then if status <> 0 then writeln('info_process status ', status);
toolpath := pinfo.progpathname;
i := length(toolpath);
while (toolpath[i] <>'-') and (i>0) do i := i - 1;
toolpath := copy(toolpath,1,i);

{open a dataseg for lisagraf etc. }
segname := 'lmheap';
thldsn := LMHeapLDSN;
LMHeapsize :=10*1024;
lmdiscsize := 16*1024;
{make sure disk size is worst case because we dont want to fail to allocate
 space for the heap used by quickdraw}
IF traceInit then Write('makedataseg ');

Sched_Class(i,false);
make_dataseg(status,segname,LMheapsize,LMdiscsize,LMHeaprefnum,
                thsegptr,thldsn,ds_private);
Sched_Class(i,true);

if status > 0 then
   begin
   if mmtrap1 then writeln('not enough disk space for data segments');
   IF status = 309 then
      begin
      {try on the skill disk}
      segname := concat(toolpath,'lmheap');
      make_dataseg(status,segname,LMheapsize,LMdiscsize,LMHeaprefnum,
                thsegptr,thldsn,ds_private);

      IF status > 0 then
         begin
         IF status = 309 then istatus := noDiskSpace
         ELSE if status = 315 then istatus := noMemory
         else istatus := internalerror;
         exit(initfexec);
         end
      ELSE if status < 0 then
         begin
         if mmtrap1 then writeln('makedataseg status: ', status);
         end;


      end {status = 309}
   else if (status <> 309) and (status > 0 ) then
      begin
      if status = 315 then istatus := noMemory
      else  istatus := internalerror;
      exit(initfexec);
      end
   else if mmtrap1 then if status < 0 then writeln('makedataseg warning ',status);
   end
else if status < 0 then
   begin
   if mmtrap1 then
      writeln(' makedataseg warning status',status);
   end;
IF traceInit then Writeln('*');


IF traceInit then Write('InitHeap ');
InitHeap(POINTER(thsegptr),POINTER(thsegptr+lmheapsize),@HeapError);
IF traceInit then Writeln('*');

(*
if mmtrap1 then theheap^.fscramble := TRUE;
if mmtrap2 then theheap^.fcheck := TRUE;
*)


docsopen := 0;

Scrolling := false;
QuitRequest := FALSE;                 {Quit menu not hit}

IF traceInit then Write('InitMP; init LMphrase and menu file ');
InitMP(theHeap,concat(toolpath,'{t5}lm'),nil,lmversion,@mymenus[1],LMALERTS,alerterror);
if alerterror > 0 then
   begin
   if mmtrap1 then writeln('alerterror ',alerterror);
   istatus := internalError;
   exit(initfexec);
   end
else if alerterror < 0 then
   begin
   if mmtrap1 then writeln('alert warning ',alerterror);
   end;
IF traceInit then Writeln('*');

{init phrase file stuff}

OpenPhraseFile(refnum,concat(toolpath,'{t5}dbc'));
if alerterror > 0 then
   begin
   if mmtrap1 then writeln('alerterror ',alerterror);
   istatus := internalError;
   exit(initfexec);
   end;
DBCALERTS := ReadAlerts(refnum,dbcversion);
if alerterror > 0 then
   begin
   if mmtrap1 then writeln('alerterror ',alerterror);
   istatus := internalError;
   exit(initfexec);
   end
else if alerterror < 0 then
   begin
   if mmtrap1 then writeln('alert warning ',alerterror);
   end;
IF traceInit then Writeln('*');

{Get the marketcode from each of the alert files, compare and save}
GetAlert(LMALERTS,LMMktCode,@LMmcString);
GetAlert(DBCALERTS,DBCMktCode,@DBCmcString);

LMMarketCode := 0;
DataPtr := pointer(ord(@LMmcString)+1);
i := 0;
while i < length(LMmcString) do
  begin
  LMMarketCode := LMMarketCode * 10 + dataptr^[i] - ord('0');
  i := i+1;
  end;

DBCMarketCode := 0;
DataPtr := pointer(ord(@DBCmcString)+1);
i := 0;
while i < length(DBCmcString) do
  begin
  DBCMarketCode := DBCMarketCode * 10 + dataptr^[i] - ord('0');
  i := i+1;
  end;

if mmtrap1 then writeln('LM,DBC MarketCodes : ',LMMarketCode:4,DBCMarketCode:4);

if LMMarketCode <> DBCMarketCode then
  begin
  if mmtrap1 then writeln('alertfile MktCodes not equal : ',LMMarketCode:4,DBCMarketCode:4);
  istatus := internalError;
  exit(initfexec);
  end
else ToolMarketCode := LMMarketCode;

IF traceInit then Write('Prmgrinit ');

PrMgrInit;
SetPort(deskPort);
IF traceInit then Writeln('*');


somenuup := false;
dtmenuup := false;
cvmenuup := false;
TrceMenuVis := False;
Bldmenuvis := FALSE;
{$IFC fexecdebug}
VerMenuVis := FALSE;
{$ENDC}


IF tracing then writeln('initwml');
{Init cursors, scroll boxes }

IF traceInit then Write('InitWml... ');
Initwmlsb;
InitwmlCrs(status);
if status > 0 then
   begin
   istatus := internalError;
   exit(initfexec);
   end
else if mmtrap1 then if status < 0 then
    writeln('imitwmlcrs warning status',status);
setstdcursor(icrsHrGlass);
InitSbList(sblist,theheap);
setpt(temppt,0,0);
hsbv := sbcreate(sblist,hsbnil,tyvhv,temppt,0);
hsbh := sbcreate(sblist,hsbnil,tyvhh,temppt,0);
IF traceInit then Writeln('*');

IF traceInit then Write('MMInit ');
{Initialize the matrix manager}
MMInit(DBCALERTS, Status);
IF traceInit then Writeln('*');

IF status > 0  then
   begin
   if (status = 309) or (status = 3709) then istatus := noDiskSpace
   ELSE if status = 315 then istatus := noMemory
   ELSE istatus := internalError;
   if mmtrap1 then
      begin
      writeln('mminitfiles failed -- insufficient disc space');
      writeln('mminit status',status:5);
      end;
   exit(initfexec);
   end
ELSE if mmtrap1 then if status < 0 then
   writeln('mminit warning  status ',status);

{Initialize the field editing stuff}

IF traceInit then Write('FeInit ');
Getalert(lmalerts, fetab, @tempstr);
FEinit(theHeap,@tempstr,status);
if status <> 0 then
   begin
   istatus := internalError;
   exit(initfexec);
   end;
{$IFC flddebug }
setfldTest (FALSE);
{$ENDC }
IF traceInit then Writeln('*');

{Initialize the panels and selection manager}

IF traceInit then Write('InitPnls ');
InitPnls(status);
TitlePnl := cTitlePnl;
allocpnl(cTitlePnl);

if mmtrap1 then if status <> 0  then mmfatalerr(-1,'initfex','initpnls');
{ put up the Folder}
IF traceInit then Writeln('*');

IF traceInit then Write('InitSelMgr ');
InitSelMgr(true,maxstring,maxstring,maxrun);
IF traceInit then Writeln('*');

IF traceInit then Write('createtitlefield ');
CellError := False;
CreateTitleField;
IF traceInit then Writeln('*');


SetRect(nullNbrhood,0,0,0,0);

{Ungray the trace flag menu items}


IF traceInit then Write('InitMspecs ');
initMspecs;
IF traceInit then Writeln('*');

IF traceInit then Write('tminit ');
tminit;
IF traceInit then Writeln('*');

IF traceInit then Write('initprocinfo ');
InitProcInfo
  (
   @CellInfo,
   @CellRun,
   @CellValue,
   @FoldedFormula,
   @GetSnipId,
   @mmBreakSnip,
   @mmMapSnip,
   @SetSnipWidth,
   @SnipLT,
   @SnipWidth,
   @TEFatalErr
  );

IF traceInit then Writeln('*');

{do some initialization so journaling will work}
ActivePnl := nullPnl;
CurTimeout := dflttimeout;
TimeoutTime := dflttimeout;
Check2or3Click := FALSE;
FndDouble := FALSE;
FndTriple := FALSE;
MouseHasMoved := FALSE;

NullSel;
NullCr;
GetCr(curRange);

openrequested := false;
susdoccalled := false;



IF tracing then writeln('exiting initfex');
IF traceInit then Writeln('InitFex - Initialization complete');
istatus := allok;

999:


END;

{$S  lmopendoc }
Procedure OpentheList(opentype:docactiontype;var ostatus : integer);
   LABEL
          999;
   {$IFC NOT fexecdebug }
   CONST traceopen = false;
   {$ENDC }
   TYPE
      ia = array [0..0] of Integer;
      pia = ^ia;
   VAR
        status:integer;
        deltap:point;
        tempport:grafptr;
        row1id,col1id : idtype;
        frect:rect;
        folderport:grafptr;
        startrg:range;
        hndsusdata: hnddata;
        suslen : integer;
        offset : integer;
        ptri : pia;
        finfo:fontinfo;
        ferror : integer;
        oldstyleitem: integer;
        oldfontitem: integer;
        sysfntid : tlfntid;
        opdocstatus: integer;
        itemstring : str255;
        what : freply;
        FilerStatus : FReason;
        rgnh : rgnhandle;
        flqp,propwheel : boolean;
   {$IFC fexecdebug }
        traceopen : boolean;
   {$ENDC }



   BEGIN
{$IFC SAMPCALLS}  ALogCall;  {$ENDC}
   if PrintProcs then writeln('openthelist');

   ldtmgr;

   {$IFC fexecdebug }
   traceopen := true;
   {$ENDC }

   if traceopen then writeln('opening the list');


   {      set up the default panels for the ports    }

   setPort(folder);
   setorigin(0,0);
   GetPort(tempPort);
   SetPt(deltap,0,0);
   folderport := tempport;
   {$IFC tedebug}
   if tracing then
      begin
      writeln('folder portrect');
      prntrect(folder^.portrect);
      end;
   {$ENDC}

   TitlePnl := cTitlePnl;

   IF traceOpen then Write('setup panels ');

   SetUpPnl(folderpnl,tempPort,tempport^.portrect.right-dhsbox +1,
                      tempport^.portrect.bottom-dvsbox+1,deltap);

   SetUpPnl(DialogPnl,tempPort,720,0,deltap );


   SetPt(deltap,0,0);
   SetUpPnl(scrapPnl,Scrapfolder,Scrapfolder^.portrect.right-dhsbox+1,
                              Scrapfolder^.portrect.bottom-dvsbox+1,deltap );

   IF traceopen then Writeln('*');



      {Load up a real file}


   IF traceopen then Write('OpenDocument ');
   OpenDocument(@PFName,opentype,OpDocStatus);
   IF traceopen then Writeln('*');


   IF opdocStatus <> 0 THEN GOTO 999;
   fileclosed := false;

   IF traceopen then Write('Loading Fonts ');

   if not fmfontmetrics(mmfontinfo.dmpfont,finfo,ferror) then if mmtrap1 then
      begin
      writeln('bad call to fmfontmetrics substituting p12tile');
      end;

   FntIDToItem(mmfontinfo.DMPfont,oldstyleitem,oldfontitem);
   checkitem(mymenus[menufont],oldstyleitem,true);
   checkitem(mymenus[menufont],oldfontitem,true);
   menustyle := oldstyleitem;   {initialize}


   with mmfontinfo do
      begin
      checkitem(mymenus[menufont],mactual,printfont in fontoptions);
      checkitem(mymenus[menufont],msmall,smallfont in fontoptions);
      checkitem(mymenus[menufont],mmedium,mediumfont in fontoptions);
      checkitem(mymenus[menufont],mlarge,largefont in fontoptions );
      end;




   IF traceopen then Writeln('*');

   with finfo do
   wavheight := ascent+descent+leading+4;

   with sysfntid do
      begin
      fam := systext;
      seteface := [];
      dev := devscreen;
      end;

   if not fmfontmetrics(sysfntid,finfo,ferror) then
        if mmtrap1 then writeln('fmfontmetrics is false');

   with finfo do
   titleheight := ascent+descent+leading+4;



   IF traceOpen then Write(' more setup panels ');
   {set up table panel and wide panel}
   GetPnlRect(FolderPnl,frect);
   WITH frect do
      BEGIN
      setpt(deltap,0,titleheight+wavheight+2);
      SetUpPnl(TblPnl,Folderport,right-left, bottom-top-(titleheight+wavheight+2),
               deltap );
      SetPt(deltap,0,0);
      SetUpPnl(widePnl,Folderport,right-left,wavheight,deltap);
      SetPt(deltap,0,wavheight+1);
      SetUpPnl(titlePnl,Folderport,right-left,titleheight,deltap);

      END;
   IF traceOpen then Writeln('*');


   {Set up Timeouts and Neighborhoods}
   {For Now, No Timeout except as necessary to find double and triple clicks}

   {Flags for finding and maintaining info on double and triple Clicks}

   TimeoutTime := time;
   Check2or3Click := FALSE;
   FndDouble := FALSE;
   FndTriple := FALSE;
   MouseHasMoved := FALSE;
   GetPnlRect(FolderPnl,dfltNbrhood);

   {save open time}

   GMTPostTime := DocScrapId;

   if DocSuspended then
      BEGIN
      if mmtrap1 then writeln('reading in suspend data');
      GetSusData(HndSusData,suslen);

      ptri := POINTER(ORD(hndsusdata^));
      offset := ptri^[0];
      UnpackSusData(HndSusData,offset);

      ptri := POINTER(ORD(hndsusdata^));
      offset := ptri^[1];
      UnpackSMGR(HndSusData,offset);

      FreeH(hzfromH(POINTER(ORD(hndsusdata))),POINTER(ORD(hndsusdata)));

      {draw image expecially into off screen bitmap}

      if mmtrap1 then writeln('restoring curRange: ',ORD(curRange.rkind));
      IF (CurRange.rkind = a1CellRg) or (CurRange.rkind = aColHedRg) OR
         (CurRange.rkind = aRowHedRg) then
         begin
         redrawSel(true);
         end;

      {adjust panels}

      setport(folder);
      setorigin(0,0);
      adjpnls(folder^.portrect.botright);
      rgnh := newrgn;
      RectRgn(rgnh,folder^.portrect);
      fldrupdateok := true;
      redrawfldr(rgnh,true,true,false);
      disposergn(rgnh);


      if creatingtable then
         BEGIN
         GetItem(mymenus[menusubst],msnew,@itemstring);
         SetItem(mymenus[menushow],mslist,@itemstring);
         END
      Else if redefining then
         BEGIN
         GetItem(mymenus[menusubst],msmodified,@itemstring);
         SetItem(mymenus[menushow],mslist,@itemstring);
         END
      ELSE
         BEGIN
         GetItem(mymenus[menusubst],msentire,@itemstring);
         SetItem(mymenus[menushow],mslist,@itemstring);
         END;

      CheckItem(mymenus[menushow],lastshowitem,true);

      {cancel pending update}
      setPnlPort(folderpnl);
      getpnlrect(folderpnl,frect);
      validrect(frect);

      END
   ELSE
      BEGIN
      wholeselected := FALSE;
      ActivePnl := nullpnl;
      curTimeout := dfltTimeout;
      curNbrhood := dfltNbrhood;

      {Load up the list}
      IF traceopen then Write('LoadList/LoadColspecs ');
      IF CreatingTable then
         BEGIN
         LoadColSpecs(TblPars) ;
         GetItem(mymenus[menusubst],msnew,@itemstring);
         SetItem(mymenus[menushow],mslist,@itemstring);
         CheckItem(mymenus[menushow],mscolspecs,true);
         lastshowitem := mscolspecs;
         END
      ELSE
         BEGIN
         LoadList(TblPars,false,FALSE,false);
         GetItem(mymenus[menusubst],msentire,@itemstring);
         SetItem(mymenus[menushow],mslist,@itemstring);
         checkitem(mymenus[menushow],mslist,true);
         lastshowitem := mslist;
         END;
      IF traceopen then Writeln('*');

      {Get the current range }

      NullSel;
      NullCr;

      GetCr(curRange);

      {Get the First row and column coordinates for the first cell to be displayed}

      IF traceopen then Write('GetSNipIds');
      GetSnipId(rowkind,MMfirst,0,Row1Id,Row1Id);
      GetSnipId(colkind,MMfirst,0,Col1Id,Col1Id);
      IF traceopen then Writeln('*');

      SetRg(startRg,a1CellRg,Row1Id,Col1Id,Row1Id,Col1Id);

      if tracing then Writeln('fexec','initfexec','ready to call rebldtbl');

      {put up the table}


      {$IFC tedebug}
      IF tracing then prntRg('startRg : ',StartRg );
      {$ENDC}

      IF traceopen then Write('RebldTbl ');
      ReBldTbl(startRg);
      IF traceopen then Writeln('*');

      {draw lines}
      drawPnllines;
      ShoTitle;


      IF traceopen then Write('tmshonewtbl ');
      tmshonewtbl;
      IF traceopen then Writeln('*');

      {cancel pending update}
      setPnlPort(folderpnl);
      getpnlrect(folderpnl,frect);
      validrect(frect);

      IF CreatingTable THEN
         BEGIN
         SetRg(CurRange,aColHedRg,minfSnip,startrg.locol,minfSnip,startrg.locol);
         ActivePnl := TblPnl;
         LastPnl := NullPnl;
         NullSel;
         NullCr;
         DoSelCR(CurRAnge,CurTimeout,SelectContents);
         END
      ELSE
         BEGIN
         curtimeout := dflttimeout;
         ActivePnl := TblPnl;
         LastPnl := NullPnl;
         NullSel;
         NullCr;
         GetCr(CurRange);

         END;
      END;  {doc not suspended}


   ostatus := 0;
   fldrupdateok := true;
   fixmenus;


   susDocCalled := false;


   if traceopen then writeln('exiting openTheList');
   exit(OpentheList);

999:
   ostatus := -1;
   fileclosed := true;
   docsopen := docsopen -1;
   fldrupdateok := false;
   activepnl := nullpnl;
   if mmtrap1 then writeln('openthelist status : ',OpDocstatus);
   what := docclosd;
   case OpDocStatus of

      MMBadData:    FilerStatus := badData;
      MMNoDisc:     FilerStatus := noDiskSpace;
      MMNoMem:      FilerStatus := noMemory;
      MMIntErr:     FilerStatus := internalError;
      MMTooNewDoc:  FilerStatus := newerdoc;
      MMTooOldDoc:  FilerStatus := CantConvert;
      MMNeedToCvt:  FilerStatus := NeedToConvert;
      MMCvtErr:     FilerStatus := badData;
      MMBadPassWd:  begin
                    FilerStatus := wrongPassWord;
                    what := docClosd;
                    end;
      Otherwise     FilerStatus := internalError;

      end;
   TellFiler(status,what,FilerStatus,folder);
   if mmtrap1 then if status <> 0 then
      writeln('TellFiler status: ',status:4);


   END;



ÿ