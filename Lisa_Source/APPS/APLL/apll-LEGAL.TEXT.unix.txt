
{$S DBCPkStr}
procedure CharOk (dbf:ptrdbfield; dsp:ptrdata; strtDsp:integer; endDsp:integer;
    var errnum:integer);

{******************************************************************************

Verifies a field of type text (lets) and returns true if the contents
  are legal.

Calls inpset.

Errors:
     16 - extra characters at the end
     91 - letter not allowed
     93 - digit not allowed
     94 - punctuation not allowed
    212 - too few characters

******************************************************************************}

label 50; { escape for bad character found }

var i: integer;
    p: ptrdata;
    c,ce: char;
    ecp:^longstr;
    eci,endEC:integer;
    dspCh:ptrfstring;   { to get characters out as characters }
    dspi:integer;
    done:boolean;

begin
{$IFC SAMPCALLS}  ALogCall;  {$ENDC}
errnum := 0;
dspCh := pointer(ord(dsp));

if (dbf^.editcheck[4] = 0) then
  begin
  if (dbf^.editcheck[3] <> 15) then
    begin
    p := @dbf^.editcheck[3];
    for i := strtDsp to endDsp do
      begin
      c := dspCh^[i];
      if dbcTest then
        begin
        writeln('ord(c)=',ord(c):1);
        writeln('alphaSet=', (ord(c) in alphaSet),
                '  digit=', (c in ['0'..'9']),
                '  spcl=', (ord(c) in spcl));
        end;

      if (ord(c) in alphaSet)
          and (not (inpset(p, 0) or inpset(p, 1))) then
        begin
        errnum := 91; { character not allowed }
        goto 50;
        end
      else if (c in ['0'..'9']) and (not inpset(p, 2)) then
        begin
        errnum := 93; { number not allowed }
        goto 50;
        end
      else if (ord(c) in spcl) and (not inpset(p, 3)) then
        begin
        errnum := 94; { punctuation character not allowed }
        goto 50;
        end;
      end; { of for loop through string }
     end; { of action if not all character types are legal }
  end { if edit check specifies allowed types of chars }
else
  begin
  ecp := pointer(ord(dbf)+dbf^.display);
  endEC := length(ecp^)-1;      { last significant char }
  eci := 2;                     { first char is double quote }
  dspi := strtDsp;

  while ((dspi<=endDsp) or (eci<=endEC)) and (errnum=0) do
    begin

    { skip to next non-punct in input }
    done := false;
    while not done and (dspi<=endDsp) do
      begin
      c := dspCh^[dspi];       { get next char of input }
      if (ord(c) in spcl) then
        dspi := dspi+1
      else
        done := true;
      end;
    if dbcTest then
      if dspi<=endDsp then
        writeln('got char ',c,' at ',dspi:1)
      else
        writeln('end of input');

    { skip to next non-punct in editcheck }
    done := false;
    while not done and (eci<=endEC) do
      begin
      ce := ecp^[eci];
      if (ce<>wild1Char) and
         (ce<>wild2Char) and
         (ord(ce) in spcl) then
        eci := eci+1
      else
        done := true;
      end;
    if dbcTest then
      if eci<=endEC then
        writeln('got echar ',ce,' at ',eci:1)
      else
        writeln('end of echars');

    if (eci<=endEC) = (dspi<=endDsp) then
      begin
      if eci<=endEC then
        begin
        dspi := dspi+1;
        eci := eci+1;

        if (ce<>wild1Char) and (ce<>wild2Char) then
          if (ord(c) in alphaSet) and not (ord(ce) in alphaSet) then
            errnum := 91  { character not allowed }
          else if (c in ['0'..'9']) and not (ce in ['0'..'9']) then
            errnum := 93; { digit not allowed }
        end;
      end
    else if eci>endEC then
      errnum := 16  { extra characters at end }
    else
      errnum := 212 { too few characters }
    end; (* while something to do *)

  end; { if edit check contains a pattern string }

50:
end; { of CharOk }


{$S DBCPack}
procedure BcdOk (dbf:ptrdbfield; dsp:ptrdata; strtDsp:integer; endDsp:integer;
    var errnum:integer);

{***************************************************************************

Function: Checks bcd types (zip code, phone number, and social security
          number) for legal characters and the right number of digits.
          number of digits:
          zip: 5 or 9;
          phone: 7 or 10;
          ss: 9

Errors:    8 - wrong number of digits
          17 - letter L entered instead of number 1
          18 - letter o entered instead of number 0
          19 - alphabetic character not allowed

***************************************************************************}

label 50; { error exit }

var i: integer;
    scount: integer;
    c:   char;

begin
{$IFC SAMPCALLS}  ALogCall;  {$ENDC}

scount := 0;
for i := strtDsp to endDsp do
    begin
    c := chr(dsp^[i]);
    if c in ['0' .. '9'] then
        scount := scount + 1
    else if (c = 'L') or (c = 'l') then
        begin
        errNum := 17; { letter L entered instead of number 1 }
        goto 50;
        end
    else if (c = 'o') or (c = 'O') then
        begin
        errNum := 18; { letter o entered instead of number 0 }
        goto 50;
        end
    else if ord(c) in alphaSet then
        begin
        errNum := 19; { alphabetic character not allowed }
        goto 50;
        end;
    end; { of loop through entered characters }

case dbf^.fldtype of
  ph:  if (scount<>7) and (scount<>10) then
         errnum := 8; { wrong number of digits }
  zip: if (scount<>5) and (scount<>9) then
         errnum := 8; { wrong number of digits }
  ss:  if (scount<>9) then
         errnum := 8; { wrong number of digits }
  end;

50:
end; { of BcdOk }


{$S DBCPack}
procedure NumOk (dbf:ptrdbfield; dsp:ptrdata; strtDsp:integer; sigLen:integer;
   var errnum:integer);

{***************************************************************************

Function: Verifies a numeric value for legality.

Calls: VerifyNum

Errors (in addition to those returned by VerifyNum):
       10 - sign not allowed
       40 - too many digits left of decimal point
       41 - too many digits right of decimal point
       42 - too many digits
      206 - too many digits left of decimal comma
      207 - too many digits right of decimal comma
      215 - units in input does not match units in format

***************************************************************************}

var seq:        checkinfo;
    doltype:    byte;
    actualDsp:  ptrData;
    fract:      boolean;    { true if '.' is decimal char in edit check }
    ecp:        ^LongStr;
    q:          quadint;    { arg to VerifyNum }
    actULen:    INTEGER;    { length of units in actual string }
begin
{$IFC SAMPCALLS}  ALogCall;  {$ENDC}
case dbf^.fldtype of
    tick: doltype := 0;
    num:  doltype := 0;
    dol:  doltype := 1;
    end; { of case }
actualDsp := pointer(ord(dsp) + strtDsp);
ecp := pointer(ord(dbf) + dbf^.display);

{ determine preferred fraction char from edit check }
if dbf^.editCheck[4]=0 then
  fract := prefPeriod           { default }
else
  begin
  if ecp^[dbf^.editCheck[4]]='.' then
    fract := true               { edit check uses '.' }
  else
    fract := false;             { edit check uses ',' }
  end;

if VerifyNum (actualDsp, sigLen, FALSE, doltype,
              fract, seq, q, errnum) then
    begin
    if (seq[1] <> 0) and (dbf^.editCheck[1] = 0) then
        errNum := 10 { sign not allowed }
    else if (seq[3] > dbf^.editCheck[3]) and (dbf^.editCheck[5] = 0) then
        errNum := 42 { too many digits }
    else if seq[3] > dbf^.editCheck[3] then
        if fract then
          errnum := 40   { too many digits left of decimal point }
        else
          errnum := 206  { too many digit left of decimal comma }
    else if seq[5] > dbf^.editCheck[5] then
        if fract then
          errNum := 41   { too many digits right of decimal point }
        else
          errnum := 207 { too many digits right of decimal comma }
    else if seq[2] <> 0 then
        begin { check the units }
        actUlen := seq[2] MOD 32; {actual (user's input) units length}

        errnum := 215; { assume the worst }
        if actUlen = dbf^.editcheck[2] MOD 32 THEN
            begin
            IF CompChrIdentity(POINTER(ORD(ecp) + dbf^.editcheck[2] DIV 32 + 1),
                                            {ecp^[0] is length}
                               POINTER(ORD(actualDsp) + seq[2] DIV 32),
                               actUlen, actUlen, CompVeryWeak) THEN
                errnum := 0;
            end;
        end;
    end;
end; { of NumOk }


{$S DBCPack}
procedure DateOk (dbf:ptrdbfield; dsp:ptrdata; strtDsp:integer; endDsp:integer;
   var errnum:integer);

{*****************************************************************************

Function: Validates a date value.

Calls:    AsciiToDate

Errors    (in addition to those returned by AsciiToDate):
          76 - day out of range
          77 - month out of range
          79 - year out of range

*****************************************************************************}

var d: dateValues;
    i: integer;

begin
{$IFC SAMPCALLS}  ALogCall;  {$ENDC}
AsciiToDate (dbf, dsp, strtDsp, endDsp, d, errnum);

{ check out the date values }
if errnum = 0 then
    begin
    if (d[1] <> -1) and ((d[1] < 1) or (d[1] > 12)) then
        errnum := 77 { month out of range }
    else if (d[3] < -1) or (d[3] > 9999) then
        errnum := 79 { year out of range }
    else { check day validity }
        begin
        { "30 days hath September, April, June, and November..." }
        i := 31;
        case d[1] of
            kwSeptember, kwApril, kwJune, kwNovember:
                i := 30;
            kwFebruary:  begin
                i := 28;
                if (d[3] mod 4 = 0) then
                    i := 29;
                if (d[3] mod 100 = 0) and (d[3] mod 400 <> 0) then
                    i := 28;
                end; { of action for figuring days in February }
            end; { of case }
        if (d[2] <> -1) and ((d[2] < 1) or (d[2] > i)) then
            errnum := 76; { day out of range }
        end; { of action to check day validity }
    end; { of checking date values }
end; { of DateOk }


{$S DBCPack}
procedure TimeOk (dbf:ptrdbfield; dsp:ptrdata; strtDsp:integer; endDsp:integer;
   var errnum:integer);

{******************************************************************************

Function: Validates a time string.

Calls: AsciiToTime

Errors (in addition to those returned by AsciiToTime):
       65 - hours out of range
       66 - minutes out of range
       67 - seconds out of range

******************************************************************************}

var tval: timeValues;

begin
{$IFC SAMPCALLS}  ALogCall;  {$ENDC}
AsciiToTime (dbf, dsp, strtDsp, endDsp, tval, errnum);

{ if no errors found yet, check out the time values }
if errnum = 0 then
    begin
    if (tval[1] < -1) or (tval[1] > 23) then
        errnum := 65 { hours out of range }
    else if (tval[2] < -1) or (tval[2] > 59) then
        errnum := 66 { minutes out of range }
    else if (tval[3] < -1) or (tval[3] > 59) then
        errnum := 67; { seconds out of range }
    end;

end; { of TimeOk }


{$S DBCPkStr}
procedure Legal {dbf:ptrdbfield; dsp:ptrdata; dsplen:integer;
    var errnum:integer};

{******************************************************************************

Function: Checks legality of data entered into the typed field described by
          dbf and its corresponding edit check string.  Returns an error number
          and the value false if the data in dsp is not legal.

Assumes: Formatting characters may or may not have been entered.

Errors:   all the ones listed the following routines

Calls: CharOk
       BcdOk
       NumOk
       DateOk
       TimeOk
       Inpset
       VerifyNum
       AsciiToDate
       AsciiToTime

******************************************************************************}

var strtDsp: integer;
    endDsp:  integer;
    sigLen:  integer;
    endLen:  integer;

begin
{$IFC SAMPCALLS}  ALogCall;  {$ENDC}
if dbctest then writeln ('Entering Legal');

errnum := 0;

{ find the display string without its leading and trailing blanks }
strtDsp := 0;
endDsp := dspLen - 1;
sigLen := dspLen;
if endDsp >= 0 then
    while (endDsp >= 0) and
          ((chr(dsp^[endDsp]) = ' ') or
           (chr(dsp^[endDsp]) = reqSpace)) do
        begin
        endDsp := endDsp - 1;
        sigLen := sigLen - 1;
        end;
endLen := sigLen;
if strtDsp < endLen then
    while (strtDsp < endLen) and
          ((chr(dsp^[strtDsp]) = ' ') or
           (chr(dsp^[strtDsp]) = reqSpace)) do
        begin
        strtDsp := strtDsp + 1;
        sigLen := sigLen - 1;
        end;
if dbcTest then
    writeln ('Start: ', strtDsp, ' End: ', endDsp, ' Length less blanks: ',
        sigLen);

if sigLen > 0 then
    case dbf^.fldtype of

        lets: { character field }
            CharOk (dbf, dsp, strtDsp, endDsp, errnum);

        ss, zip, ph: { bcd values }
            BcdOk (dbf, dsp, strtDsp, endDsp, errnum);

        dol, num, tick: { numeric values }
            NumOk (dbf, dsp, strtDsp, sigLen, errnum);

        dt: { date }
            DateOk (dbf, dsp, strtDsp, endDsp, errnum);

        tim: { time }
            TimeOk (dbf, dsp, strtDsp, endDsp, errnum);

        end; { of case }

if dbctest then writeln ('Exiting Legal');
end; { of Legal }

ÿ