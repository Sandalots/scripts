unit coMgr;                            {'Copyright 1983, 1984, Apple Computer Inc.'}
     intrinsic;

interface

uses (*$U UnitStd.obj *)                UnitStd,
     (*$U UnitHz.obj *)                 UnitHz,
     (*$U Storage.obj *)                Storage,
     (*$U QuickDraw.obj *)              QuickDraw,
     (*$U Fontmgr.obj *)                FontMgr,
     (*$U dbenv.obj *)                  dbenv,
     (*$U FEdec.obj *)                  FEdec,
     (*$U fld.obj *)                    fieldedit,
     (*$U teenv.obj *)                  teenv,

     (*$U libos/SysCall.obj *)          Syscall,
     (*$U FPlib.obj *)                  FPlib,
     (*$U PMDecl *)                     PMDecl,
     (*$U PrStdInfo *)                  PrStdInfo,
     (*$U PrPublic.obj *)               PrPublic,
     (*$U aplc/mm/matmgr.obj *)         matmgr,

     (*$U wm.events.obj *)              events,
     (*$U wm.folders.obj *)             folders,
     (*$U wm.menus.obj *)               menus,
     (*$U AlertMgr.obj *)               AlertMgr,

     (*$U WMLstd.obj *)                 WMLstd,
     (*$U WMlsb.obj *)                  WMlsb,
     (*$U WMLgrow.obj *)                WMLgrow,
     (*$U tm.obj *)                     tm,
     (*$U aplc/feglobals.obj *)         feglobals,
     (*$U scrap.obj *)                  scrap,
     (*$U MathLib.obj *)                MathLib;


const
       maxCoLen         = 12;           {maximum no. of chars in coordinates display minus 1}
       maxFormLen       = 113;          {maximum no. of chars in formula display minus 1 - with p20tile (94 with p15tile)}
       hintsDiSize      = 275;          {initial size of the function hints dialog box}

type

        lblType    = (lMenu, lNull);

        lblFld     = record  {A label field for a dialog box}
                        lblBox    : rect;           {The box associated with and enclosing the string}
                        lblH      : integer;
                        lblV      : integer;        {Where the string should start}
                        assocFldH : hndField;       {A fldSel field that should receive a selection
                                                     if someone clicks in this lblBox}
                        assocFSH  : hndFstate;      {The corresponding fstate}
                        case lbl : lblType of
                           lMenu : (lMenuNum  : integer;
                                    lMenuItem : integer
                                   );
                           lNull : (lStr : PtrStrType)
                     end;

        lblFldX    = ^lblFld;

        ChkRow     = record {Defines a row of check boxes in the Dialog Box}
                        chkFldH : hndField; {Handle to the field (ala Field Editor) that describes the check box row}
                        procX   : procPtr; {The procedure to be called whenever a mouse down in a position
                                            within this field is detected.  The procedure has two parameters:
                                            chkFldH and an interval that gives the positions within the
                                            field that have been dragged over (with mouse button down)
                                           }
                     end;

        ChkRowX    = ^ChkRow;

        pbutton    = record {Defines an application-specific button in the Dialog Box}
                        buttonNum  : TButn;    {The number of the button in the phrase file}
                        environ    : rect;     {If the mouse goes down in this rect, then track button pushing}
                        buttonLeft : integer;
                        buttonTop  : integer;  {top/left coords of the button}
                        procX      : procPtr;  {The procedure to be called whenever this button has been
                                                pushed.  It has no parameters.
                                               }
                     end;

        buttonX    = ^pbutton;

        editType   = (pnlPaste, pnlClear, pnlCut, pnlCopy);

        {Kinds of dialog boxes for LisaCalc and Business Graphics}
        diBoxKind  = (diColWid, diPrecision, diReplicate, diXAxis, diYAxis, diFuncHints, diStatus, diPrint, diFind, diCircle,
                      diInsert
                     );

        {Set of responses to give to questionable actions while the dialog box is up}
        diResponse = (OkAction, IgnoreAction, DiGone);

        tAxisType  = (XAXisType, YAxisType); (* SDY addition 12/2/81 *)
        fldKind    = (plainTxt, noSel, BtnArea, aChkRow);


var coFldH      : hndField;             {text editing Field for coordinates display}
    coFSH       : hndFState;            {text editing Field state for coordinates display}
    formFldH    : hndField;             {text editing field for formula display}
    formFSH     : hndFstate;            {text editing field state for form display}
    ScoFldH     : hndField;             {place to store coordinate field during a redraw of the scrap}
    ScoFSH      : hndFState;            {place to store coordinate field state during a redraw of the scrap}
    editForm    : boolean;              {true when formula display editing is enabled}
    curDiHeight : integer;              {Height in pixels of current dialog box}
    curDiBox    : diBoxKind;            {Kind of last dialog box}

    lblA        : lblFld;
    lblB        : lblFld;
    lblC        : lblFld;
    lblD        : lblFld;
    lblE        : lblFld;               {Label fields used for messages in the dialog box}

    {field and fieldstate vars for dialog box field}
    diHfld      : array[1..4] of HndField;
    diHfs       : array[1..4] of hndFstate;

    inPutGraphics : boolean;
    finishChkBoxs : boolean;


{**************************************     Appdibox      **************************************}

procedure clrUndo;

procedure DiBoxScroll(where : point; myhsbHit : Thsb; myIconHit : TIcon);

procedure disMissDi(menu: integer; menuIndex: Integer; var response: diResponse);

procedure initDiFlds;

{$IFC fDebug }
procedure prntLblFld(lblX: lblFldX);
{$ENDC }

procedure PutCircleBox;

procedure putCWidPrecBox (diBox : diBoxKind; initStr : resultStr);

procedure PutFindBox;

procedure PutFuncHints(InitBoxSize : integer);

procedure PutPrintBox;

procedure PutStatusBox;

procedure putRepBox;

procedure removeDiBox;


{*******************************    CoMgr    **************************************}

function  ButLeftEdge : integer;

procedure CoMgStuff ( mode:  FIOKind ; lcVer: integer; VAR status: integer );

procedure drawCo;

procedure emptyCoDisp;

procedure getCoRg (var coRg: range; var status: integer;
                   var errRec: errRecord);

procedure getRgName (rg: range; rgNameX: ptrData; var numBytes: integer);

procedure initCo;

procedure reDrawCo;

procedure shoNewCo (aRg: range);

{*******************************    DiMgr    **************************************}

procedure addDiBox(height: integer; DoItProcX, GoAwayProcX: procPtr; addScrollBars, checkboxes : boolean);

procedure findDiPart(var pt: point; var fKind: fldKind; var pnlFldH: hndField;
                     var pnlFSH: hndFstate; var specProcX: procPtr);

procedure initDi;

procedure putDiFld(fldH: hndField; fsH: hndFstate; var status: integer);

procedure putDiLbl(lblX: lblFldX);

Procedure PutGraphics(GrafProcX: ProcPtr);

procedure reDrawDi;

procedure rmvDiBox;

procedure setDiPnl;


{*******************************    FormMgr    ************************************}

procedure drawForm;

procedure endRgInsert;

procedure enterForm (rowId, colId: idType; var status: integer;
                     var errRec: errRecord);

procedure emptyFormDisp;

procedure initForm;

procedure newFormRg (formRg: range; var status: integer);

procedure reDrawForm;

procedure shoNewForm (rowId, colId: idType);


{*******************************    Lcsmgr    **************************************}

procedure doPnlAct (Activate: boolean);

procedure doPnlSel (pnl: pnlIndex; pnlPoint: point; var nbh: rect;
                var timeout: integer);

procedure endOtherSel (pnl: pnlIndex);

procedure endPnlSel;

procedure GetPnlFldKind (var theFldKind: fldKind);

procedure jumpAField
  (dir         : moveDir;
   var timeout : integer
  );

procedure LCSMStuff
  ( mode       : FIOKind ;
    lcVer      : integer;
    VAR status : integer   );

procedure nullPnlSel;

procedure nullSelIn (pnl: pnlIndex);

procedure newNbhPnl (pnlPoint: point; var nbh: rect; var timeout: integer);

procedure peekAtNewSel (pnl: pnlIndex; pnlPoint: point; var fKind: fldKind);

procedure pnlEdit
  (editKind   : editType;
   var status : integer
  );

procedure pnlKey (ch: char; shiftflag: boolean; var timeout: integer;
                  var status: integer);

procedure pnlMseUp (pnlPoint: point; var timeout: integer;
                    var newSel: boolean);

procedure pnlSelAll (pnl: pnlIndex; var timeout: integer);

procedure pnlSelFld (var timeout: integer);

procedure pnlTimOut (var timeout: integer);

procedure pnlUndo (pnl: pnlIndex; var status: integer);

procedure pnlWSel (P: point; var nbh: rect; var timeout: integer);

procedure shoPnlErr (pnl: pnlIndex; status: integer; errRec: errRecord;
                     var timeout: integer);

procedure doFinds(var newCR : boolean);
{************************************************}

implementation

(*$R- *)

(*$IFC teSym *)
(*$D+ *)
(*$ELSEC *)
(*$D- *)
(*$ENDC *)

const   maxLbls         = 19;   {max no. of label fields (-1) in dialog box at one time}
        maxDiFlds       = 4;    {max no. of fldSel fields (-1) in dialog box at one time}
        maxDiStrng      = PRStrLength;  {max length of an editable field in the dialog box - PR is the longest}
        maxButtons      = 3;    {max no. of button fields (-1) in dialog box at one time}
        maxChkRows      = 1;    {max no. of ChkRow fields (-1) in dialog box at one time}
        nullLp          = 5000; {a null Logical Position.  If the l.chkIntrvl is nullLp/nullLp, then there is no interval.}
        nullIntrvl      = 9000; {If the lpFst field of rgIntrvl = nullIntrvl then there is no interval for insertion of a range name}
        ButPadRH        = 10;   {padding for the buttons from the right edge}
        FormLblNum      = 2;            {index into diChkArray for the formula in a paste relative dialog}

        {Replicate dialog box constants}
        BDistFromTop    = 5;            {distance from the top of the dialog box to the top of the next/prev buttons}
        FormLeft        = 8;            {Left edge of "Formula:"}
        RepLblH         = 4;            {Start the label 4 pixels to the right of the left edge of the label box}
        repBtnLeft      = 0;            {Environ for Next and Previous buttons}
        repBtnTop       = 0;
        repBtnBot       = 32;

        PrintDiSize      = 70;          {initial size of the printing dialog box}
        StatDiSize       = 125;         {initial size of the status dialog box}
        FindDiSize       = 65;          {initial size of the find dialog box}
        FindCircleSize   = 85;          {initial size of the circle dialog box}

type
     boxVariant =  record
                      case boolean of
                         true  : (b : packed array [1..32] of boolean);
                         false : (l : longint)
                      end;

var diFldArray   : array[0..maxDiFlds] of hndField;       {handles to active fldSel fields for current dialog box}
    diFSArray    : array[0..maxDiFlds] of hndFstate;      {handles to field states, corresponding to fields}
    diLblArray   : array[0..maxLbls] of lblFldX;          {pointers to label field descriptors for dialog box}
    diBtnArray   : array[0..maxButtons] of buttonX;       {pointers to current button descriptors}
    diChkArray   : array[0..maxChkRows] of ChkRowX;       {pointers to ChkRow descriptors}

    numDiFlds    : integer;
    numLbls      : integer;
    numButtons   : integer;
    numChkRows   : integer;                               {current number of each of these fields}
    diPnlPtr     : pnlHandle;                             {panelPtr for dialog panel}
    grafixX      : procPtr;                               {Ptr to client's procedure for drawing graphics in the current dialog box}
    DoItBtn      : pButton;
    GoAwayBtn    : pButton;                               {Button descriptors for DoIt and GoAway}

    l            : record          {Collection of all LCSMgr variables that need to be saved/restored on close/open}
                      lastPnl      : pnlIndex;   {the last LC-specific panel that hada mouse down in it or a new selection}
                      pnlFldKind   : fldKind;    {field kind for lastPnl}
                      prevPnl      : pnlIndex;   {the previous LC-specific panel that had a mouse down in it or a new selection}
                      prevFldKind  : fldKind;    {field kind for prevPnl}
                      chkIntrvl    : interval;   {Interval being checked in current chkRow}
                      lastLpFst    : integer;    {Low end of interval for last check box hit}
                      newSelection : boolean     {true iff the last mouse action caused a new selection}
                   end;

   { these 6 vars moved out from L-record }
    pnlFldH      : hndField;  {handle to field data structure for lastPnl, assuming pnlFldKind=plainTxt}
    pnlFSH       : hndFstate; {handle to field state data structure for lastPnl, assuming pnlFldKind=plainTxt}
    pnlProcX     : procPtr;   {Pointer to procedure to call on button pushed or chkBox pressed}
    prevFldH     : hndField;  {handle to field data structure for prevPnl, assuming prevFldKind=plainTxt}
    prevFSH      : hndFstate; {handle to field state data structure for prevPnl, assuming prevFldKind=plainTxt}
    prevProcX    : procPtr;   {pnlProcX for prevPnl.}

    nextBtn      : pbutton;
    prevBtn      : pbutton;

    chkRowA      : ChkRow;
    chkRowR      : ChkRow;

    rgIntrvl     : interval;             {the interval of the range name just inserted into the formula}
    lastFormRg   : range;                {the last formRg passed to newFormRg }
    DoItButTop   : integer;              {the top of the do it button}
    tempLabel    : str40;                {temporary label used in aplc/appdibox for performanace}
    formLabel    : str255;

    numChkBytes  : integer;
    scrollArea   : rect;
    HintsPicture : PicHandle;
    HintsOffset  : point;
    chkBoxInfo   : record
                      hasCheckBoxes: boolean;
                      pt    : point;
                      boxes : array [boolean] of boxVariant
                   end;


{**************************************     Forward Declarations     ************************************}

procedure DrawDialog; forward;

procedure endChking; forward;

procedure findChkBox(pnlPoint : point); forward;

procedure findCoFld (var fKind: fldKind; var pnlFldH: hndfield;
                var pnlFSH: hndfstate); forward;

procedure findFormFld (var fKind: fldKind; var pnlFldH: hndfield;
                var pnlFSH: hndfstate); forward;

procedure findNxtDiFld
  (theDir    : idModeType;
   var fKind : fldKind;
   var fldH  : hndField;
   var fsh   : hndFstate
  ); forward;

procedure findPnlFld (pnl: pnlIndex; pnlPoint: point; var fKind: fldKind;
                      var pnlFldH: hndField; var pnlFSH: hndFstate;
                      var specProcX: procPtr); forward;

procedure nilArcProc(verb : grafVerb; r : rect; startAngle, arcAngle : integer); forward;

procedure nilBitsProc(var srcBits : BitMap; var srcRect, dstRect : rect; mode : integer; maskRgn : rgnHandle); forward;

procedure nilLineProc(newPt : point); forward;

procedure nilOvalProc(verb : grafVerb; r : rect); forward;

procedure nilPolyProc(verb : grafVerb; poly : polyHandle); forward;

procedure nilRgnProc(verb : grafVerb; ploy : rgnHandle); forward;

procedure nilRRectProc(verb : grafVerb; r : rect; ovalWidth, ovalHeight : integer); forward;

procedure nilTextProc(count : integer; textAddr : QDPtr; numer, denom : point); forward;

procedure PutButton(aButtonX : ButtonX; visible : boolean); forward;

procedure putCellCo(rowId, colId : idType; var rgNameX : ptrData;
                     var numBytes : integer); forward;

procedure putCoChars(theChars : resultStr; var rgNameX : ptrData;
                      var numBytes: integer); forward;

procedure putNewRep(whichBox: idModeType; newBtns: boolean; var done: boolean); forward;

procedure putSnipCo(aKind : snipKind; snipId : idType; var rgNameX : ptrData;
                     var numBytes : integer); forward;

Procedure ReDrawChks(aChkRowX : ChkRowX); forward;

procedure repGrafix; forward;

procedure restoreLCS; forward;

procedure saveLCS; forward;

procedure setDiSize(height: integer); forward;

procedure shoButton(aButtonX: ButtonX; visible: boolean); forward;

procedure StdRectProc(verb : grafVerb; r : rect); forward;

procedure StdTextProc(count : integer; textAddr : Ptr; numer, denom : point); forward;

procedure pnlSetPortClip(aPnl : pnlIndex; aFldH : hndField); forward;

procedure whichBoxHit(verb : grafVerb; r : rect); forward;

{**************************************     External Declarations     ************************************}

procedure chgStatPnl (keepFormula: boolean); external;                          {in aplc/LCexecsubs - UNIT LCfexec}
procedure doGoTo (newRange: range); external;                                   {in aplc/LCexecsubs - UNIT LCfexec}
procedure DrawGrafix (grafxX : procPtr); external;                              {in aplc/mm/lcasm - assembly language}
procedure enterCalc(firstEnter: boolean; reSelCR: boolean;
                    calcOnEnterOnly: boolean; delayCalc: boolean;
                    var status: integer); external;                             {in aplc/TEexec - UNIT TEEXEC}
procedure enterReSelect (var status: integer); external;                        {in aplc/TEexec - UNIT TEEXEC}
procedure forceCalc(reSelCR: boolean); external;                                {in aplc/Teexec - UNIT TEEXEC}
procedure IBtnProc(procX : procPtr); external;                                  {in aplc/mm/lcasm - assembly language}
procedure IChkProc(fldH: hndField; chkInt: interval; procX: procPtr); external; {in aplc/mm/lcasm - assembly language}
procedure InsertSnip(numSnips : integer; var newCR : boolean); external;        {in aplc/Teexec - UNIT TEEXEC}
procedure setNewFormat (numPlaces: integer); external;                          {in aplc/LCmenus2 - UNIT TEEXEC}
procedure selectRg(newRg: range; hilite: cellseltype); external;                {in aplc/Teexec - UNIT TEEXEC}
function  tblEditPnl (aPnl: pnlIndex): boolean; external;                       {in aplc/LCexecsubs - UNIT LCfexec}
procedure updFolder(forceUpdate : boolean); external;                           {in aplc/LCfexec  - UNIT LCfexec}


(*$IFC fDebug *)
function wRg(rg : range) : char; external;                                      {in aplc/mm/stubs - UNIT stubs}
function wRect(aRect : rect) : char; external;                                  {in aplc/mm/stubs - UNIT stubs}
function wClip : char; external;                                                {in aplc/mm/stubs - UNIT stubs}
(*$ENDC *)


(*$I aplc/diMgr.text *)
(*$I aplc/lcsmgr.text *)
(*$I aplc/formmgr.text *)
(*$I aplc/appdibox.text *)


{$S OpenClose}
procedure CoMgStuff { mode:  FIOKind ; lcVer: integer; VAR status: integer };
 { 18Aug82:  this version stuffs/unstuffs the field/fieldstate for the Formula Panel only,
              and does so iff it a suspend or unsuspend AND the active Panel is the formula panel }
 { called by FXLCStuff to package/unpackage vars owned by this unit }
 const stuffLen = 152 ;
 var DataLen       :  integer ;  { for use with FBufStuff }
     kipini        :  hndData ;
     lenInstalled  :  integer ;
     bufLen        :  integer ;
     begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

(*$IFC fDebug *)
        if traceCoMgr then lcTraceMess ( 221 )  ;
(*$ENDC *)
        if (lcVer <> 0) and (g.activePnl = formPnl) and ((mode=mmSuspend) or (mode=mmUnSuspend)) then
           begin { we want to stash/unstash the formula-panel's field structures }
              { allocate heap space }
              kipini := pointer(ord(HAllocate(TheHeap, stuffLen) )) ;

              if mode = mmUnSuspend then  { reading data in }
                 begin
                    dataLen := sizeOf(integer);   { read stuff length }
                    fBufStuff ( fRawData , @lenInstalled , dataLen, status ) ;
                    if status <> 0 then exit ( coMgStuff );

                    if stuffLen < lenInstalled then ChangeSizeH(TheHeap, POINTER(ORD(kipini)), LenInstalled);

                    dataLen := lenInstalled ;
                    fBufStuff ( fRawData , kipini^ , dataLen , status ) ;
                    if status <> 0 then exit ( coMgStuff );

                    {packed version of field is now hung on handle 'kipini'}

                    unPackField( formFldH, formFSH , kipini )
                      { refresh formula field/fieldstate from packed information }
                 end
              else  { writing }
                 begin
                    bufLen := stuffLen ;
                    PackField ( formFldH , formFSH , kipini , bufLen , lenInstalled ) ;

                    dataLen := sizeOf ( integer) ;
                    fBufStuff ( fRawData, @lenInstalled , dataLen , status ) ;
                    { write out length to be packed }

                    dataLen := lenInStalled ;
                    fBufStuff ( fRawData , kipini^, dataLen , status )
                 end;

              freeH ( theHeap , pointer(ord(kipini)) )    { release heap space }

        end { writing, or reading modern file }


     end {CoMgStuff} ;

{************************************************}


{$S statmgr}
procedure drawCo;
        {Just draw the coordinates display, according to the current coFldH and coFSH}
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   pnlSetPortClip(coPnl, coFldH);
   DrawField(coFldH, coFSH, true, true);
end;


{$S statmgr}
procedure emptyCoDisp;
        {Empty the coordinates display, ie, clear all text}
var errNum: integer;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug }
   if traceCoMgr then LCtraceMess(12);
{$ENDC }
   ClearField(coFldH, coFSH, errNum);
   ExitField(coFldH, coFSH);
   initFstate(coFldH, coFSH);
   coFldH^^.curlen := 0;
end;


{$S statmgr}
procedure findCoFld {var fKind: fldKind; var pnlFldH: hndfield;
                var pnlFSH: hndfstate};
        {Return pointers to the Coordinates Display Field data structures
        and set fKind to plainTxt}
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug }
   if traceCoMgr then LCtraceMess(13);
{$ENDC }
   fKind := plainTxt;
   pnlFldH := coFldH;
   pnlFSH := coFSH;
end;


{$S statmgr}
procedure getCoRg {var coRg: range; var status: integer;
                   var errRec: errRecord};
        {Return the range corresponding to the current Coordinates Display.
        Assume selection has been ended.  If status <> 0 then coRg is invalid.
        Return the status and errRec that matMgr returns.}
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug }
   if traceCoMgr then LCtraceMess(14);
{$ENDC }
   CellRange(coFldH^^.curvalue^, coFldH^^.curlen, coRg, status, errRec);
end;


{$S statmgr}
procedure getRgName {rg: range; rgNameX: ptrData; var numBytes: integer};
        {Convert the range rg to a byte array describing the range name.
         Put the byte array into rgNameX^ and return the number of bytes in
         numBytes.}
var textForAll : str40;
      begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

        numBytes := 0;
        case rg.rKind of
           aRowRg,
           aColRg : ;
           otherwise if printState.preview then exit(getRgName)
        end;
        with rg do begin
           case rg.rKind of
             {aNullRg:      BEGIN END;}
              a1CellRg:     putCellCo(loRow, loCol, rgNameX, numBytes);
              aRectRg:      begin
                               putCellCo(loRow, loCol, rgNameX, numBytes);
                               putCoChars(':', rgNameX, numBytes);
                               putCellCo(hiRow, hiCol, rgNameX, numBytes);
                               end;
              aRowRg:       begin
                               putSnipCo(rowKind, loRow, rgNameX, numBytes);
                               if (hiRow <> loRow) then begin
                                  putCoChars(':', rgNameX, numBytes);
                                  putSnipCo(rowKind, hiRow, rgNameX, numBytes);
                                  end;
                               end;
              aColRg:       begin
                               putSnipCo(colKind, loCol, rgNameX, numBytes);
                               if (hiCol <> loCol) then begin
                                  putCoChars(':', rgNameX, numBytes);
                                  putSnipCo(colKind, hiCol, rgNameX, numBytes);
                                  end;
                               end;
              aRowGrid:     begin
                               putSnipCo(rowKind, loRow, rgNameX, numBytes);
                               putCoChars('|', rgNameX, numBytes);
                               putSnipCo(rowKind, hiRow, rgNameX, numBytes);
                               end;
              aColGrid:     begin
                               putSnipCo(colKind, loCol, rgNameX, numBytes);
                               putCoChars('|', rgNameX, numBytes);
                               putSnipCo(colKind, hiCol, rgNameX, numBytes);
                               end;
              aWTBoxRg:     begin
                               WordfromLexeme(ALLop, 40, @textForAll);
                               putCoChars(textForAll, rgNameX, numBytes)
                            end;
             {otherwise     begin end;}
              end
           end
        end;


{$S initial}
procedure initCo;
        {Set up the Coordinates Display data structures and initialize to
        no string and no selection, then display.}
var coRect: rect;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug }
   if traceCoMgr then LCtraceMess(15);
{$ENDC }
   SetRect(coRect, 0, 0, 0, 0);
   coFldH := CreateField(coRect, maxCoLen + 1, 0, left, 4, 1, 0, TblFntId.Fam, false);
   coFSH  := pointer(ord(newHandle(sizeof(fieldstate))));
   InitFstate(coFldH, coFSH);

   ScoFldH := CreateField(coRect, maxCoLen + 1, 0, left, 4, 1, 0, TblFntId.Fam, false);
   ScoFSH  := pointer(ord(newHandle(sizeof(fieldstate))));
   InitFstate(ScoFldH, ScoFSH)
end;


{$S statmgr}
{sets the graf port to the current panel and sets the clipping to the intersection of aPnl's
 rectangle and then rectangle "coords" within aFld
}
procedure pnlSetPortClip{aPnl : pnlIndex; aFldH : hndField};
var pnlRect  : rect;
    scrnRect : rect;
    nonNull  : boolean;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug}
   if tracing then LCtraceMess(1);
{$ENDC }
    SetPnlPort(aPnl);
    GetPnlRect(aPnl, pnlRect);
    nonNull := SectRect(pnlRect, aFldH^^.coords, scrnRect);
    clipRect(scrnRect)
end;


{$S statmgr}
procedure putCellCo{rowId, colId: idType; var rgNameX: ptrData; var numBytes: integer};
        {Insert the characters for colId's header followed by rowId's header
        into curvalue^^, updating curlen in coFldH^^}
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

(*$IFC fDebug *)
   if traceCoMgr then LCtraceMess(16);
(*$ENDC *)
   putSnipCo(colKind, colId, rgNameX, numBytes);
   putSnipCo(rowKind, rowId, rgNameX, numBytes);
end;


{$S statmgr}
procedure putCoChars {theChars: resultStr; var rgNameX: ptrData;
                      var numBytes: integer};
        {Insert the characters in theChars into rgNameX^, starting at byte
         numBytes, updating the number of characters in numBytes.}
var i,n: integer;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

(*$IFC fDebug *)
   if traceCoMgr then LCtraceMess(17);
(*$ENDC *)
   n := length(theChars);
   StoB(theChars, @rgNameX^[numBytes], n);
   numBytes := numBytes + n
end;


{$S statmgr}
procedure putSnipCo {aKind: snipKind; snipId: idType; var rgNameX: ptrData;
                     var numBytes: integer};
        {Insert the characters for snipId's header into curvalue^^, updating
        curlen in coFldH}
var hedString : string[8];
    status    : integer;
    len       : integer;
    rowId     : idType;
    colId     : idType;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

(*$IFC fDebug *)
   if traceCoMgr then LCtraceMess(18);
(*$ENDC *)
   len   := 8;
   rowId := mInfSnip;
   colId := mInfSnip;
   if aKind = rowKind then rowId := snipId
   else colId := snipId;
   CellValue(mmRead, rowId, colId, pointer(ord(@hedstring) + 1), false, len, status);
   hedString[0] := chr(len);
   putCoChars(hedString, rgNameX, numBytes)
end;


{$S fexec}
procedure reDrawCo;
        {Redraw the Coordinates Display, according to the current data structures.
        Only the panel rectangle may have changed.}
var coRect : rect;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug }
   if traceCoMgr then LCtraceMess(19);
{$ENDC }
   getPnlRect(coPnl, coRect);
   SetFCoords(coFldH, coFSH, coRect);
   SetFCoords(ScoFldH, ScoFSH, coRect);
   drawCo
end;


{$S statmgr}
procedure shoNewCo {aRg: range};
        {Display aRg within the Coordinates Display}
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug }
   if traceCoMgr then begin
      LCtraceMess(20);
      writeln('Range passed is:', wRg(aRg))
   end;
{$ENDC }
   emptyCoDisp;
   redrawCo;
   getRgName(aRg, coFldH^^.curvalue^, coFldH^^.curlen);
   initFstate(coFldH, coFSH);
   drawCo
end;

(*$S initial *)
END.
ÿ