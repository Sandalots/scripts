
                            {'Copyright 1983, 1984, Apple Computer Inc.'}

(*********** aplc/lcmenus3 - second include file for LCMenus - unit teexec    **************)
(*************************** contains procedures relating to setting of fonts **************)

{ The procedures in this file are:

     FontCommand         Called when any item in the TypeStyle menu is selected; hands control
                                on to one of the following two procedures, depending on which
                                portion of the TypeStyle menu was was selected.
     SetTblFont          Obtains new font metrics, etc., for a new Table Font, and informs
                                the Matrix Manager of the change
     ChgTblFont          Does everything necessary to react to a change in Table Font, including
                                calling SetTblFont, and repainting the entire Folder

     ChgTypeStyle        Sets up the TypeStyle menu when BG Graph panel is NOT the active panel
     ChgBGTypeStyle      Sets up the TypeStyle menu when BG Graph panel IS the active panel  }


{$S FontChanging}
procedure setTblFont{NewFontFam: TFam};
        {Change the table font to NewFontFam ;
         determine needed font parameters (like tblFInfo and tblCharWid
         and pnlLineDv), and tell the Matrix Mgr about them .}
     var
        fontOk:          boolean;
        ferror:          integer;
     begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

        { Set up the new table font }
        TextFont(NewFontFam);
        TextFace([]);

        TblFntId.Fam  := NewFontFam;

        { Get the table font information }
        fontOk := FMFontMetrics(TblFntId, tblFInfo, ferror);
{$IFC fDebug}
        if traceLCPR then
           begin
              writeln('SetTblFont; new fam=',NewFontFam:1 );
              if ferror<0 then
                       writeln('setTblFont==tblFont subst; newfam=',TblFntID.fam:1 )
           end;
        if not fontOK then PrintMess(37);
{$ENDC }
        tblCharWid := tblFInfo.WidMax;

        { The new dividing line information is based on the sys fonts }
        if sysFInfo.ascent + sysFInfo.descent > tblFInfo.ascent + tblFInfo.descent then LrgFInfo := sysFInfo
        else LrgFInfo := tblFInfo;
        with sysFInfo do
           pnlLineDv := ascent + descent + leading + 2;

        { tell matmgr about it (++um2++) }
        SetCharInfo(TblFntId)
     end;

{ - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - }

{$S FontChanging}
procedure ChgTblFont{NewFontFam: TFam};
        {Change the table font to NewFontFam, then redraw the table using the new table font
         -- but if table font is ALREADY newFontFam, does nothing  }

var
    errNum :    integer ;
    fRect  :    rect;
    aWTRg  :    range; { whole table range to update the graph }

begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

(*$IFC fDebug *)
   if traceLCexec or tracelcpr or traceOC then writeln('ChgTblFont to font #',newFontFam:2) ;
(*$ENDC *)

   if (newFontFam <> TblFntId.Fam)  then { a change is evident... }
      begin
         { Set up the new table font }
         SetTblFont ( NewFontFam ) ;

         setPnlPort(FolderPnl);                  { Clear }
         getPnlRect(FolderPnl, fRect);            { out   }
         BackPat(white);                           { the    }
         EraseRect(fRect);                          { Folder }

         changeCellFont;
         SetApp2Pnls ;
         DividPnls   ;

         restoreRGFG(g.curTimeOut);    { restore range-feedback }

         { now set the new font information into the formula and coords fields }
         if g.LCFlag then SetRunFormat(formFldH, nil, tblFntId.Fam, setFont, errNum);
         SetRunFormat(coFldH, nil, tblFntId.Fam, setFont, errNum);

         if g.curRange.rKind = a1CellRg then begin
            resizeflds;        { set revised font info into tbl-pnl fields }
            reDrawSel(true)    {  ... and draw them in the new font        }
         end;
{
         if printState.preview then begin
            lcReDoBreaks;
            mmSetWids(tPreview)
         end;
}

         tmReDoViews;                  { redraw the table       }

         if g.BGFlag then
             begin          {Redraw the graph}
                lcBGSetGrfx;
                aWTRg.rKind := aWTBoxRg;
                FixRange(aWTRg);
                fxRgChged(aWTRg);
                fxNewRgImg
             end
         else reDrawForm;
         reDrawCo;

        { Draw the scroll bars }
         If g.LCFlag then LCDrawScrolls
         else BGDrawScrolls

      end
end;

{ - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - }

{$S FontChanging}
procedure SetFontFam(doPrinter : boolean);
var NewFontFam : TFam;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

  { adjust Table font, if required }
   case g.TabFontMode of
      FMPrinter : if doPrinter then newFontFam := FamFromIndex(g.curPrFont)
                  else newFontFam := 0;      { nothing to do, because this case was handled by "resolveFont" }
      FMSmall   : newFontFam := p20tile;     { for small, we always use Tile}
      otherwise   if g.TabFontMode = FMMedium then begin
                     if g.ModernFont then newFontFam := p12tile
                     else newFontFam := p12cent
                  end
                  else if g.ModernFont then newFontfam := p10tile
                  else newFontFam := p10cent
   end;
   if newFontFam <> 0 then ChgTblFont(newFontFam)
end;


{ - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - }
{$S FontChanging}
procedure fontCommand ;
    { undoing      : boolean;     --  true iff called from doUndo
      VAR newCR    : boolean )    --  always comes back FALSE     }

{ Called when user selects an item in the Font Menu, or when the user selects UNDO and most recent
  action was selection of an item in the Font Menu. }

var
   status:    integer;
   newIndex:  integer;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

(*$IFC fDebug *)
   if traceLCexec or traceLCPR then LCTraceMess ( 225 ) ;
(*$ENDC *)

   NewCR := false;
   if undoing then begin
      if prevFontMenuItem <= NPrinterFonts then newIndex := prevPrFontIndex { item changed printer font }
      else newIndex := PrevTblFontMode ;{ item had changed table font mode }
   end
   else case g.ItemIndex of
           FTClassic : begin
                          g.ModernFont := false;
                          newIndex     := g.CurPrFont;
                          case newIndex of
                             FT12 : newIndex := FC12;
                             FT10 : newIndex := FC10
                          end
                       end;
           FTModern  : begin
                          g.ModernFont := true;
                          newIndex     := g.CurPrFont;
                          case newIndex of
                             FC12 : newIndex := FT12;
                             FC10 : newIndex := FT10
                          end
                       end;
           fnt20     : newIndex := FT20;
           fnt15     : newIndex := FT15;
           fnt12     : if g.ModernFont then newIndex := FT12
                       else newIndex := FC12;
           fnt10     : if g.ModernFont then newIndex := FT10
                       else newIndex := FC10;
           otherwise   newIndex := g.ItemIndex
        end;

   PrevPrFontIndex   :=  g.CurPrFont;               { set state for possible UNDO }
   PrevTblFontMode   :=  g.TabFontMode;
   PrevFontMenuIndex :=  newIndex;

   docDirty := true;
   ClrUndo;
   if (g.TabFontMode <> FMPrinter) and ((g.ItemIndex <= FTClassic) and not undoing) then SetFontFam(true)
   else if newIndex <= NPrinterFonts then begin { a particular printer font selected }
          { Accomplishes a change in the current 'Printer Font', in response to user's  selection of a printer
            font from the TypeStyle menu; this includes:  resolving possible conflicts between the Print Preference
            and the selected Font; Recomputation & redisplay of page breaks; and redetermination of the Table Font
           }
           if g.CurPrFont <> newIndex then begin     { there really is a change }
              g.CurPrFont := newIndex;               { set into global record }
              resolveFont;                           {sort out conflicts with print pref - in aplc/lcpr - UNIT Feglobals.
                                                      May result in change in fam }
              SetFontFam(false);
              lcReDoBreaks;     { recompute page breaks }
              tmShoBreaks       { ... and show them     }
           end
        end
   else if newIndex <> g.TabFontMode then  begin { a change in table font mode selected - there's something to do }
           g.TabFontMode := newIndex;
           SetFontFam(true)
        end;
   setUndo(uFontChg, tblPnl)

end;  { fontCommand }

{ - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - }


{$S fexec}
procedure ChgTypeStyle;       { Control will never come here if BGPnl is active}

{ Checks and grays the BG and LC TypeStyle menus }

var PItem    : Integer ;
    MenuT    : Integer ;   { menuindex for BG or LC typestyle menu           }
    BGIncr   : Integer ;   { amount to add in to get to BG's font menu items }
    newIndex : integer;

    (*$S BGspecific *)
    procedure doBGstuff;
    var x : integer;
    begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

       { delete both old menus that might be in the typestyle position }
       DeleteMenu(BGTypeStyle);
       DeleteMenu(MenuBGLCTypestyle);

       { Since TblPnl is active, install the LCTypeStyle menu containing only the fixed fonts.
         Overwrite the position of the old TypeStyle menu.
       }
       MenuT := MenuBGLCTypeStyle;
       SetMenuNum(MenuBGLCTypeStyle);
       InsertMenu(myMenus[MenuBGLCTypeStyle], BGTypeStyle + 1);
       BGIncr := 7;
       ChgGray(7, True);     { Disable the null item }

       { Disable the BG formatting items since they don't apply to the table }
       for x := BGPlainText to BGShadow do ChgGray(x, True)
    end;

(*$S fexec *)
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

(*$IFC fDebug *)
   if traceLCexec then LCtraceMess(104);
(*$ENDC *)
   if g.LCFlag then begin
      MenuT  := LCTypeStyle;
      BGIncr := 0;
      SetMenuNum(LCTypeStyle)
   end
   else DoBGstuff;
   case g.curPrFont of
      FT20 : newIndex := fnt20;
      FT15 : newIndex := fnt15;
      FT10,
      FC10 : newIndex := fnt10;
      FT12,
      FC12 : newIndex := fnt12
   end;
   { uncheck all items in Table area of Font Menu }
   for pItem := 1 to FMLast do begin
      ChkItem(pItem + BGIncr, (pItem = newIndex));
      ChgGray(pItem + BGIncr, (pItem = 3) or (pItem = 8) or ((pItem <= NPrinterFonts) and printState.preview))
   end;
   ChkItem(FTClassic - ord(g.ModernFont) + BGIncr, true);

   if g.TabFontMode <> 0 then                   {if could not recognize printer family, check nothing}
      chkItem(g.TabFontMode + BGIncr, true)
end;


(*$S BGspecific *)
Procedure ChgBGTypeStyle;
{ checks and grays the BG typestyle menu when BGPnl is active }

var PItem,
    myMenuFont     : Integer;
    fGray,
    myPlain        : Boolean;
    myRun          : Run;
    myInterval     : Interval;

begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   { delete both old menus that might be in the typestyle position. Install the BGPnl one
   that lists ALL the font styles. }
   SetMenuNum(BGTypeStyle);
   DeleteMenu(BGTypeStyle);
   DeleteMenu(MenuBGLCTypestyle);
   InsertMenu(myMenus[BGTypeStyle], BGTypeStyle + 1);

   {*****************************************************************}
   { Properly gray all the items in the format menu  }
   fGray := not BGPreview;
   for PItem := BGPlainText to BG24Point do begin
      ChgGray(PItem, FGray);
      ChkItem(PItem, false)
   end;

   { Gray the null item }
   ChgGray(7, True);
   ChgGray(10, True);

   ChkItem(BGClassic - ord(g.ModernFont), true);

   {*********************************************************************}
   { Figure out which items to check }
   If (BGFld <> Nil) Then Begin
      myInterval := hFldSt^^.select.int;
      myInterval.LPLim := myInterval.LPLim - 1;
      FmtFromInt(BGFld, hFldSt, myInterval, myPlain, myRun);
   end
   else with myRun do Begin
           font    := -1;
           face    := [];
           myPlain := False;
        end;

   With myRun do Begin
      { Check the formatting items }
      ChkItem(BGPlainText, myPlain);
      ChkItem(BGUnderLine, (UnderLine in face));
      ChkItem(BGBold     , (Bold      in face));
      ChkItem(BGItalic   , (Italic    in face));
      ChkItem(BGHollow   , (Outline   in face));
      ChkItem(BGShadow   , (Shadow    in face));

      { Check the font items }
      ChkItem(BG20Pitch,      (font = p20Tile));
      ChkItem(BG15Pitch,      (font = p15Tile));
      ChkItem(BG12Pitch,      (font = p12Tile) or (font = p12Cent));
      ChkItem(BG10Pitch,      (font = p10Tile) or (font = p10Cent));
      ChkItem(BG12Point,      (font = Tile12)  or (font = Cent12));
      ChkItem(BG14Point,      (font = 15)  or (font = 16));
      ChkItem(BG18Point,      (font = Tile18)  or (font = Cent18));
      ChkItem(BG24Point,      (font = Tile24)  or (font = Cent24));
   end
end;


ÿ