
                            {'Copyright 1983, 1984, Apple Computer Inc.'}

(*********** aplc/LCsmgr - lisacalc selection manager - unit comgr **************)

(***************************************.*******.********************************************************
   checkBox - The mouse is down at pt over the chkRow described by pnlFldH and
              pnlProcX.  Determine the new check interval(l.chkIntrvl), pass it to
              pnlProcX, then return the new neighborhood.
****************************************.*******.*******************************************************)
{$S PRdialog}
procedure checkBox(pt : point; var nbh : rect);
var newChar : interval;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   with newChar do begin
      lpFst := LpFromX(pt.h, pnlFldH, nil, tySelWd);
      lpLim := lpFst + 1;
      if lpFst <> l.lastLpFst then begin
         if (lpFst < l.chkIntrvl.lpFst) or (l.chkIntrvl.lpFst = nullLp) then l.chkIntrvl.lpFst := lpFst;
         if (lpLim > l.chkIntrvl.lpLim) or (l.chkIntrvl.lpLim = nullLp) then l.chkIntrvl.lpLim := lpLim;
         IChkProc(pnlFldH, l.chkIntrvl, pnlProcX);
         l.lastLpFst := lpFst
      end
   end;
   nbh := pnlFldH^^.coords;
   nbh.left  := pt.h - 1;
   nbh.right := pt.h + 1
end;


(***************************************.*******.********************************************************
   doPnlAct - If Activate is true, then re-activate any selection that lcsMgr is
              managing, otherwise, de-activate the selection.
****************************************.*******.*******************************************************)
(*$S ActDeActCode *)
procedure doPnlAct {Activate: boolean};
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug }
   if traceLCSMgr or traceOC then begin
      if Activate then LCtraceMess(47)
      else LCtraceMess(48);
   end;
{$ENDC }
   if l.pnlFldKind = plainTxt then begin
      pnlSetPortClip(l.lastPnl, pnlFldH);
      ChangeSelHilight(pnlFldH, pnlFSH, not Activate)
   end
end;


(***************************************.*******.********************************************************
   doPnlSel - Initiate a new selection at point pnlPoint, relative to the upper
              left of panel pnl.  Change l.lastPnl to pnl, update l.lastPnl's other
              data structures, and show the selection on the screen.  Assume that
              any previous selections have already been ended.  Before doing anything,
              save the current LisaCalc-Selection-Manager selection information.
****************************************.*******.*******************************************************)
{$S statmgr}
procedure doPnlSel {pnl: pnlIndex; pnlPoint: point; var nbh: rect;
                var timeout: integer};
var theButn : TButn;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug }
   if traceLCSMgr then LCtraceMess(49);
{$ENDC }
   l.newSelection := false;  {Init to false until mouse comes up}
   saveLCS;
   l.lastPnl := pnl;
   timeout := dflttimeout;
   findPnlFld(pnl, pnlPoint, l.pnlFldKind, pnlFldH, pnlFSH, pnlProcX);
   case l.pnlFldKind of
      plainTxt : begin
                    pnlSetPortClip(l.lastPnl, pnlFldH);
                    Select(pnlPoint, pnlFldH, pnlFSH, nbh, timeout)
                 end;
      noSel    : begin
                    if chkBoxInfo.hasCheckBoxes and (pnl = cDialogPnl) then findChkBox(pnlPoint);
                    nbh := nullNbrhood;
                    endChking
                 end;
      aChkRow  : begin
                    endChking;
                    checkBox(pnlPoint, nbh)
                 end;
      BtnArea  : begin
                    SetPnlPort(l.lastPnl);
                    RestoreLCS;
                    if ButnPushed(firstBtn, lastBtn, theButn, pnlPoint) then begin
                       buttonWasPushed(theButn);
                       if l.newSelection then l.newSelection := false
                       else begin
                               l.newSelection := true;
                               l.PnlFldKind := BtnArea
                             end
                    end;
                    nbh := nullNbrhood;
                    endChking
                 end
   end
end;


(***************************************.*******.********************************************************
   drawPnlFld - Draw the field in l.lastPnl with pnlFld and PnlFS
****************************************.*******.*******************************************************)
{$S statmgr}
procedure drawPnlFld;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug }
   if traceLCSMgr then begin
      LCtraceMess(50);
      writeln('drawPnlFld: l.lastPnl is ', ord(l.lastPnl):1);
   end;
{$ENDC }
   pnlSetPortClip(l.lastPnl, pnlFldH);
   DrawField(pnlFldH, pnlFSH, true, true)
end;


(***************************************.*******.********************************************************
   endChking - Reset the l.chkIntrvl to a null interval.  Perhaps, the mouse has come
               up after being down in a check row.  Or, we could be initializing aplc/LCSMgr.
****************************************.*******.*******************************************************)
{$S statmgr}
procedure endChking;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   l.chkIntrvl.lpFst := nullLp;
   l.chkIntrvl.lpLim := nullLp;
   l.lastLpFst := nullLp
end;


(***************************************.*******.********************************************************
   endChking - End the selection in pnl (which is not l.lastPnl), without changing
               l.lastPnl or the info set up about it
****************************************.*******.*******************************************************)
{$S statmgr}
procedure endOtherSel {pnl: pnlIndex};
var tlastPnl   : pnlIndex;
    tpnlFldKind : fldKind;
    tpnlFldH    : hndField;
    tpnlFSH     : hndFstate;
    tpnlProcX   : procPtr;
    pnlPoint    : point;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug }
   if traceLCSMgr then LCtraceMess(51);
{$ENDC }
   tlastPnl    := l.lastPnl;
   tpnlFldKind := l.pnlFldKind;
   tpnlFldH    := pnlFldH;
   tpnlFSH     := pnlFSH;
   tpnlProcX   := pnlProcX;
   pnlPoint.h  := -1;
   findPnlFld(pnl, pnlPoint, l.pnlFldKind, pnlFldH, pnlFSH, pnlProcX);
   l.lastPnl   := pnl;
   endPnlSel;
   nullPnlSel;
   l.lastPnl    := tlastPnl;
   l.pnlFldKind := tpnlFldKind;
   pnlFldH      := tpnlFldH;
   pnlFSH       := tpnlFSH;
   pnlProcX     := tpnlProcX
end;


(***************************************.*******.********************************************************
   endPnlSel - if a text selection exists in one of the LisaCalc-specific panels, end
               it and remove the screen feedback.  Retain the data structures describing
               the field.
****************************************.*******.*******************************************************)
{$S statmgr}
procedure endPnlSel;
        {if a text selection exists in one of the LisaCalc-specific panels, end
        it and remove the screen feedback.  Retain the data structures describing
        the field.}
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug }
   if traceLCSMgr then LCtraceMess(52);
{$ENDC }
   if l.pnlFldKind = plainTxt then begin
      pnlSetPortClip(l.lastPnl, pnlFldH);
      if not pnlFSH^^.valid then DrawField(pnlFldH, pnlFSH, false, true);
      ExitField(pnlFldH, pnlFSH);
      InitFstate(pnlFldH, pnlFSH)
   end
end;


(***************************************.*******.********************************************************
   findChkBox -
****************************************.*******.*******************************************************)
(*$S specialDiSeg *)
procedure findChkBox{pnlPoint : point};
var value       : longint;
    nullChecks  : boolean;
    myGrafProcs : QDProcs;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   case curDiBox of
      diStatus,
      diCircle : begin
                    value := 0;
                    nullChecks := false
                 end;
      otherwise  begin
                    value := chkBoxInfo.boxes[true].l;
                    nullChecks := true
                 end
   end;
   chkBoxInfo.boxes[false].l := value;
   chkBoxInfo.pt := pnlPoint;
   finishChkBoxs := false;
   SetPnlPort(DialogPnl);
   SetStdProcs(myGrafProcs);
   with myGrafProcs do begin
      rectProc  := @whichBoxHit;
      textProc  := @nilTextProc;
      rRectProc := @nilRRectProc;
      lineProc  := @nilLineProc;
      ovalProc  := @nilOvalProc;
      arcProc   := @nilArcProc;
      polyProc  := @nilPolyProc;
      rgnProc   := @nilRgnProc;
      bitsProc  := @nilBitsProc
   end;
   Dialogfolder^.grafProcs := @myGrafProcs;
   drawPicture(HintsPicture, HintsPicture^^.picFrame);
   if (chkBoxInfo.boxes[false].l <> 0) or nullChecks then begin
      chkBoxInfo.boxes[true].l := chkBoxInfo.boxes[false].l;
      finishChkBoxs := true;
      drawPicture(HintsPicture, HintsPicture^^.picFrame)
   end;
   Dialogfolder^.grafProcs := nil
end;


(***************************************.*******.********************************************************
   findPnlFld - Return information about the field that pnlPoint is in: 1)fKind, the
                kind of field (eg, plainTxt or noSel), 2)pnlFldH and pnlFSH, handles
                to the field data structures if fKind=plainTxt, and 3)specProcX,
                the procedure to call when a checkBox is pressed.  pnlPoint
                is within pnl and is relative to pnl's upper-left corner.  If pnlPoint
                has negative coordinates, then return the information for the default
                field, ie, the one given in response to Select All (APPLE-F or APPLE-G).
                Assume that the field state is not uninitialized.
****************************************.*******.*******************************************************)
{$S statmgr}
procedure findPnlFld {pnl: pnlIndex; pnlPoint: point; var fKind: fldKind;
                var pnlFldH: hndField; var pnlFSH: hndFstate;
                var specProcX: procPtr};
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug }
   if traceLCSMgr then LCtraceMess(53);
{$ENDC }
   case pnl of
      ccoPnl     : findCoFld(fKind, pnlFldH, pnlFSH);
      cformPnl   : findFormFld(fKind, pnlFldH, pnlFSH);
      cdialogPnl : findDiPart(pnlPoint, fKind, pnlFldH, pnlFSH, specProcX)
   end;
{$IFC fDebug }
   if traceLCSMgr then begin
      write('findPnlFld: ');
      case fKind of
         plaintxt:
            begin
            writeln('plaintxt Field info set up by findPnlFld');
            end;
         NoSel:
            writeln('No selection');
         BtnArea:
            writeln('Button Area');
         aChkRow:
            begin
            writeln('In the Check Box Row');
            writeln('specProcX is ', ord(specProcX));
            end;
        otherwise writeln('Other fKind');
         end;
      end;
{$ENDC }
end;


(***************************************.*******.********************************************************
   GetPnlFldKind - Return the current fldKind
****************************************.*******.*******************************************************)
{$S statmgr}
procedure GetPnlFldKind {var theFldKind: fldKind};
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   theFldKind := l.pnlFldKind
end;


(***********************************************************************************
   jumpAField - the current selection is in the Dialog box and the user has hit a
                move-key to move in the DIR direction.  Assume that the user wants
                to move to the next field if DIR is MvRight, MvDown, MvNewRow or
                MvStay.  Assume also that the user wants to move to the previous
                field if DIR is MvLeft, MvUp or MvNewCol.  If the current selection
                is plainTxt, then end the selection feedback and, if there is a
                next/prior field to move to, select all of the field.  Return the
                new timeout.
************************************************************************************)
(*$S DiSeg *)
procedure jumpAField
  (* dir         : moveDir;
     var timeout : integer
  *);
var fldDir : idModeType;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug }
   if traceLCSMgr then LCtraceMess(54);
{$ENDC }
   endPnlSel;
   timeout := -1;
   case dir of
      MvNewCol,
      MvLeft,
      MvUp    : fldDir := mmPrior;
      otherwise fldDir := mmNext
   end;
   findNxtDiFld(fldDir, l.pnlFldKind, pnlFldH, pnlFsh);

   if l.pnlFldKind = plainTxt then begin
      pnlSetPortClip(l.lastPnl, pnlFldH);
      SelectAll(pnlFldH, pnlFsh, timeout)
   end
   else nullPnlSel
end;


(***************************************.*******.********************************************************
   LCSMStuff - called at open/close time to package or unpackage LCSMgr information from disk
               In the current implementation, does nothing on output, and on input restores
               LCSMgr structures appropriately
****************************************.*******.*******************************************************)
{$S OpenClose}
procedure LCSMStuff { mode:  FIOKind ; lcVer: integer; VAR status: integer };
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   if ((mode = mmUnSuspend) and (lcVer>4) and (g.activePnl = formPnl)) then begin
{$IFC fDebug}
      if TraceLCSMgr or traceOC then writeln('LcsmStuff--restore Form info') ;
{$ENDC}
      l.LastPnl     := formPnl;
      PnlFldH       := formFldH;
      PnlFSH        := formFSH;
      l.PnlfldKind  := plainTxt;
      PnlProcX      := nil;
      saveLCS   { inits 'prev' entries }
   end         { unsuspending file which has formPnl as active panel }
   else if (mode = mmOpen) or (mode = mmUnSuspend) then begin
           nullPnlSel;
           saveLCS     { inits 'prev' entries }
        end
end; { LCSMStuff }


(***************************************.*******.********************************************************
   nilTextProc - prevents and text drawing during a drawpicture.
****************************************.*******.*******************************************************)
(*$S specialDiSeg *)
procedure nilArcProc{verb : grafVerb; r : rect; startAngle, arcAngle : integer};
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)
end;

(*$S specialDiSeg *)
procedure nilBitsProc{var srcBits : BitMap; var srcRect, dstRect : rect; mode : integer; maskRgn : rgnHandle};
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)
end;

(*$S specialDiSeg *)
procedure nilLineProc{newPt : point};
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)
end;

(*$S specialDiSeg *)
procedure nilOvalProc{verb : grafVerb; r : rect};
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)
end;

(*$S specialDiSeg *)
procedure nilPolyProc{verb : grafVerb; poly : polyHandle};
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)
end;

(*$S specialDiSeg *)
procedure nilRgnProc{verb : grafVerb; ploy : rgnHandle};
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)
end;

(*$S specialDiSeg *)
procedure nilRRectProc{verb : grafVerb; r : rect; ovalWidth, ovalHeight : integer};
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)
end;

(*$S specialDiSeg *)
procedure nilTextProc{count : integer; textAddr : QDPtr; numer, denom : point};
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)
end;


(***************************************.*******.********************************************************
   newNbhPnl - The mouse has moved out of the last neighborhood set.  Extend the
               selection to the new point pnlPoint, which is relative to the upper-left
               of l.lastPnl, the LisaCalc-specific panel that the mouse went down in.
               Change l.lastPnl's data structures and give screen feedback.
****************************************.*******.*******************************************************)
{$S statmgr}
procedure newNbhPnl {pnlPoint: point; var nbh: rect; var timeout: integer};
var overFKind : fldKind;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug }
   if traceLCSMgr then LCtraceMess(57);
{$ENDC }
   timeout := dfltTimeOut;
   case l.pnlFldKind of
      plainTxt : begin
                    pnlSetPortClip(l.lastPnl, pnlFldH);
                    GrowSel(pnlPoint, pnlFldH, pnlFSH, nbh)
                 end;
      noSel    : begin
                    peekAtNewSel(l.lastPnl, pnlPoint, overFKind);
                    if overFKind = aChkRow then begin
                       findPnlFld(l.lastPnl, pnlPoint, l.pnlFldKind, pnlFldH, pnlFSH, pnlProcX);
                       CheckBox(pnlPoint, nbh)
                    end
                    else nbh := nullNbrhood
                 end;
      aChkRow  : CheckBox(pnlPoint, nbh)
   end
end;


(***************************************.*******.********************************************************
   nullPnlSel - Initialize all LCSMgr data structures to no l.lastPnl and no selection
****************************************.*******.*******************************************************)
{$S statmgr}
procedure nullPnlSel;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug }
   if traceLCSMgr then LCtraceMess(55);
{$ENDC }
   l.lastPnl    := nullPnl;
   l.pnlFldKind := noSel;
   pnlFldH      := nil;
   pnlFSH       := nil;
   pnlProcX     := nil
end;


(***************************************.*******.********************************************************
   nullSelIn - If the current selection is in pnl, then do a nullPnlSel
****************************************.*******.*******************************************************)
(*$S statmgr *)
procedure nullSelIn {pnl: pnlIndex};
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug }
   if traceLCSMgr then LCtraceMess(56);
{$ENDC }
   if l.lastPnl = pnl then nullPnlSel
end;


(***************************************.*******.********************************************************
   peekAtNewSel - If the mouse were to go down at pnlPoint in pnl, what would be the
                  fKind of the new "selection"?
****************************************.*******.*******************************************************)
{$S statmgr}
procedure peekAtNewSel {pnl: pnlIndex; pnlPoint: point; var fKind: fldKind};
var fh     : hndField;
    fsh    : hndFstate;
    tProcX : procPtr;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug }
   if traceLCSMgr then LCtraceMess(58);
{$ENDC }
   findPnlFld(pnl, pnlPoint, fKind, fh, fsh, tProcX)
end;


(***************************************.*******.********************************************************
   pnlEdit - calls the correct routine to do the cut/paste/copy/clear operations
             on a field in an application specific panel.  The opertation to be
             done is passed via EDITKIND.  With EDITKIND set to:

                pnlPaste - Paste text from the scrap to the current selection,
                           if there is one
                pnlCut
                pnlCopy  - Cut or copy the current text selection, if there is one,
                           into the scrap
                pnlClear - Clear the text field that currently has or is the selection.
                           Do nothing if there is no text selection.
****************************************.*******.*******************************************************)
{$S statmgr}
procedure pnlEdit
  (* editKind   : editType;
     var status : integer
  *);
var errNum : integer;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug }
   if traceLCSMgr then LCtraceMess(57);
{$ENDC }

   if l.pnlFldKind = plainTxt then begin
      case editKind of
         pnlCut,
         pnlCopy  : CutCopyField(pnlFldH, pnlFSH, editKind = pnlCut, true, errNum);
         pnlClear : ClearField(pnlFldH, pnlFSH, errNum);
         pnlPaste : PasteField(pnlFldH, pnlFSH, errNum, false, pnlFldH^^.maxlen)
      end;
      status := errNum;
      drawPnlFld
   end
   else status := 0

{$IFC fDebug }
    ;if status <> 0 then
       writeln('pnlEdit: errNum: ', errNum:1,'  operation: ',ord(editKind):1)
{$ENDC }
end;


{$S statmgr}
procedure pnlKey {ch: char; shiftflag: boolean; var timeout: integer;
                 var status: integer};
        {A key, ch, has been typed.  Insert it into the field data structures
        for the panel we are editing.  Don't redraw the display yet.}
var errNum : integer;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug }
   if traceLCSMgr then LCtraceMess(62);
{$ENDC }
   if l.pnlFldKind = plainTxt then begin
      pnlSetPortClip(l.lastPnl, pnlFldH);
      CaretOn(pnlFldH, pnlFSH); {Stop blinking the caret}
      case ord(ch) of
         chBack    : if shiftFlag then Forwardspace(pnlFldH, pnlFSH, errNum)
                     else Backspace(pnlFldH, pnlFSH, errNum);
         otherwise   InsCh(ch, pnlFldH, pnlFSH, errNum)
      end;
      status  := errNum;
      timeout := nullTimeout
   end
   else status := 0
end;


{$S statmgr}
procedure pnlMseUp {pnlPoint: point; var timeout: integer; var newSel: boolean};
        {The mouse has gone up at pnlPoint, relative to the same panel that it
         went down in.  Also if the mouse action did not make a
        new selection, then restore the selection state variables to what they
        were before the mouse down.  Set newSel to true iff a new selection was made
        or a button was successfully pressed.}
var nowFldKind : fldKind;
    nowProcX   : procPtr;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug }
   if traceLCSMgr then LCtraceMess(63);
{$ENDC }
   case l.pnlFldKind of
      plainTxt : begin
                      StartBlink(timeout);
                      obscureCursor;
                      newSel := true
                  end;
      BtnArea : newSel := l.newSelection;
      noSel   : begin
                   restoreLCS;
                   newSel := false
                end;
      aChkRow : begin
                   newSel := false;
                   EndChking;
                end
   end
end;


(***************************************.*******.********************************************************
   pnlSelAll - Select all text in pnl.  Set l.lastPnl to pnl, set up l.lastPnl's other
               data structures, and show the screen feedback.
****************************************.*******.*******************************************************)
{$S statmgr}
procedure pnlSelAll {pnl: pnlIndex; var timeout: integer};
var pnlPoint : point;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug }
   if traceLCSMgr then LCtraceMess(64);
{$ENDC }
   l.lastPnl  := pnl;
   pnlPoint.h := -1;
   findPnlFld(pnl, pnlPoint, l.pnlFldKind, pnlFldH, pnlFSH, pnlProcX);
   pnlSelFld(timeout)
end;


(***************************************.*******.********************************************************
   pnlSelFld - The third click of a triple click has just come in.  Select all text in
               l.lastPnl's current field and return a new timeout.
****************************************.*******.*******************************************************)
{$S statmgr}
procedure pnlSelFld {var timeout: integer};
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug }
   if traceLCSMgr then LCtraceMess(65);
{$ENDC }
   if l.pnlFldKind = plainTxt then begin
      pnlSetPortClip(l.lastPnl, pnlFldH);
      ExitField(pnlFldH, pnlFSH);
      SelectAll(pnlFldH, pnlFSH, timeout)
   end
end;


(***************************************.*******.********************************************************
   pnlTimOut - A timeout event has been received.  Do what's necessary to the display
              and data structures for the last panel we selected in (eg, blink caret
              or draw field with all typed characters inserted), then return a new
              timeout value.
****************************************.*******.*******************************************************)
{$S statmgr}
procedure pnlTimOut {var timeout: integer};
var errNum : integer;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug }
   if traceLCSMgr then LCtraceMess(66);
{$ENDC }
   if l.pnlFldKind = plainTxt then
      if not pnlFSH^^.valid then begin
         drawPnlFld;
         StartBlink(timeout)
      end
      else begin
              pnlSetPortClip(l.lastPnl, pnlFldH);
              BlinkCaret(pnlFldH, pnlFSH, timeout)
           end
end;


(***************************************.*******.********************************************************
   pnlUndo - Undo the last edit.
****************************************.*******.*******************************************************)
{$S undoCode}
procedure pnlUndo{pnl: pnlIndex; var status: integer};
var pnlPoint : point;
    errnum   : integer;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug }
   if traceLCSMgr then LCtraceMess(67);
{$ENDC }

   l.lastPnl  := pnl;
   pnlPoint.h := -1;
   findPnlFld(pnl, pnlPoint, l.pnlFldKind, pnlFldH, pnlFSH, pnlProcX);
   if l.pnlFldKind = plainTxt then begin
      undoEdit(pnlFldH, pnlFSH, errnum);
      status := errnum;
      drawPnlFld
   end
end;


(***************************************.*******.********************************************************
   pnlWSel - The second click of a double-click has just come in.  Select the
             whole word that the selection started in.
****************************************.*******.*******************************************************)
{$S statmgr}
procedure pnlWSel {P: point; var nbh: rect; var timeout: integer};
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug }
   if traceLCSMgr then LCtraceMess(68);
{$ENDC }
   if l.pnlFldKind = plainTxt then begin
      pnlSetPortClip(l.lastPnl, pnlFldH);
      SelectWord(P, pnlFldH, pnlFSH, nbh, timeout)
   end
end;


(***************************************.*******.********************************************************
   restoreLCS - Restore the LisaCalc-Selection-Mgr state saved by the last call to saveLCS.
****************************************.*******.*******************************************************)
{$S statmgr}
procedure restoreLCS;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug }
   if traceLCSMgr then begin
      LCtraceMess(69);
      writeln('l.prevPnl = ', ord(l.prevPnl):1);
      writeln('l.prevFldKind = ', ord(l.prevFldKind):1);
      writeln('prevFldH = ', ord(prevFldH):1);
   end;
{$ENDC }
   l.lastPnl    := l.prevPnl;
   l.pnlFldKind := l.prevFldKind;
   pnlFldH      := prevFldH;
   pnlFSH       := prevFSH;
   pnlProcX     := prevProcX
end;


(***************************************.*******.********************************************************
   saveLCS - Save the LisaCalc-Selection-Mgr state
****************************************.*******.*******************************************************)
{$S statmgr}
procedure saveLCS;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug }
   if traceLCSMgr then begin
      LCtraceMess(70);
      writeln('l.lastPnl = ', ord(l.lastPnl):1);
      writeln('l.pnlFldKind = ', ord(l.pnlFldKind):1);
      writeln('pnlFldH = ', ord(pnlFldH):1)
   end;
{$ENDC }
   l.prevPnl     := l.lastPnl;
   l.prevFldKind := l.pnlFldKind;
   prevFldH      := pnlFldH;
   prevFSH       := pnlFSH;
   prevProcX     := pnlProcX
end;


(***************************************.*******.********************************************************
   shoPnlErr - Print out the error message in errRec in the Dialog Box,
               and highlight the interval specified (in errRec) within pnl.
               For now, also write out the status on the Soroc.
****************************************.*******.*******************************************************)
{$S DiSeg}
procedure shoPnlErr {pnl: pnlIndex; status: integer; errRec: errRecord;
                     var timeout: integer};
var i        : integer;
    pnlPoint : point;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug }
   if traceLCSMgr then LCtraceMess(71);
{$ENDC }
   l.lastPnl := pnl;
   findPnlFld(pnl, pnlPoint, l.pnlFldKind, pnlFldH, pnlFSH, pnlProcX);
   if l.pnlFldKind = plainTxt then begin
      pnlSetPortClip(l.lastPnl, pnlFldH);
      ErrorSelect(pnlFldH, pnlFSH, errRec.errSpot.LpFst, errRec.errSpot.LpLim, timeout)
   end;
{$IFC fDebug }
   if TempCode then writeln('status returned from matMgr = ', status:1);
{$ENDC }
   ParamAlert(errRec.mess1, errRec.mess2, '');
   doStopAlert(errRec.status)
end;


(***************************************.*******.********************************************************
   whichBoxHit -
****************************************.*******.*******************************************************)
(*$S specialDiSeg *)
procedure whichBoxHit{verb : grafVerb; r : rect};
var box   : integer;
    state : boolean;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   if verb = fill then begin
      box := ChkBoxNum;
      if box > 0 then begin
         state := chkBoxInfo.boxes[false].b[box];
         if PtInRect(chkBoxInfo.pt, r) and not finishChkBoxs then begin
            case curDiBox of
               diPrint,
               diFind  : state := not state;
               otherwise state := true
            end;
            chkBoxInfo.boxes[false].b[box] := state
         end;
         with DialogFolder^ do begin
            fillPat := white;
            if state then fillPat := black
         end
      end
   end;
   if finishChkBoxs then
      case verb of
         fill,
         frame : StdRect(verb, r)
      end
end;


ÿ