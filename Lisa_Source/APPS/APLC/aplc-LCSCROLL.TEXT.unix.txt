
                            {'Copyright 1983, 1984, Apple Computer Inc.'}

(*********** aplc/lcscroll - lisacalc scroll bar code - unit lcfexec **************)

{$S Scrolling}
procedure GetSBMidPt(myHSB: THSB; VAR myPoint: Point);
{ Helper routine to return the midpoint of a scroll box in TblPnl coords }
VAR
   myRect   : Rect;   { Scroll box rect in folder coords }
   myFPoint : Point;  { Folder relative midpoint of rect }
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   { calc a point within the scroll bar }
   GetSBRect(myHSB, myRect);
   With myRect Do begin
      myFPoint.h := (left + right) div 2;
      myFPoint.v := (top + bottom) div 2;
   end;
   { adjust it to the table panel }
   AdjToPnl(TblPnl, myFPoint, myPoint)
end;


{$S growCode} {& splitCode }
procedure LCInitSBRect;
var rH,rV,rV2  : Rect;
   statpanebot : Integer;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   { Call the library routine to get the scroll bar rects }
   SetPnlPort(FolderPnl);
   FlushRects(rH, rV);
   rV2 := rV;

   { Split the vertical scroll bar at the bottom of the status panel }
   StatPaneBot := (1 + ord(g.LCFlag)) * pnlLinedV + 1;
   rV.bottom := StatPanebot;
   rV2.top := statpanebot + 1;
   SetSbRect(LChsbH, rH);
   SetSBRect(LChsbGray, rV);
   SetSbRect(LChsbV, rV2)
end;


{$S Scrolling}
procedure LCSetThumb(myHSB : THSB; myNumb : longint; doSetThumb : boolean);
{ Draws and sets the thumb in its new position for scroll box myHSB }
var lastSnip : integer;
    snip     : snipKind;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   snip := colKind;
   if tyvhofSb(myHSB) = v then snip := rowKind;
   GetSnipId(snip, mmLast, 0, 0, lastSnip);
   if myNumb = pinfSnip then myNumb := lastSnip
   else if myNumb = minfSnip then myNumb := 0;
   myNumb := ((myNumb - 1) * 1000) div lastSnip;

   if doSetThumb then SetThumb(myHSB, myNumb)
   else begin
           SetUpMvThumb(myHSB);
           MoveThumb(myNumb)
        end
end;


(*$S BGspecific *)
procedure BGSetThumb;
{ Sets the BG thumb positions. Doesn't repaint the scroll bars }
VAR
   hThumb,
   vThumb: TC;  { Thumb positions }

begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   SetPnlPort(FolderPnl);
   iBGGetThumb(hThumb, vThumb, BGProcInfo.BGGetThumb);
   SetUpMvThumb(BGHsbH);
   MoveThumb(hThumb);
   SetUpMvThumb(BGHsbV);
   MoveThumb(vThumb)
end;


{$S fexec}
procedure LCSplitX( HsbOld: THsb; VAR HsbNew: THsb; myTMBand: TMBand);
{ Splits the HsbOld scroll bar into two parts according to the info in myTMBand. myTMBand
describes where HSBNew should start }

var mypt   : Point;
    pt     : integer;
    myRect : Rect;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   { Convert the pixel to a folder relative point }
   myPt.h := myTMBand.tmblop - dhSkewer;
   myPt.v := myTMBand.tmblop - dvSkewer;

   With myRect do begin
      { stuff the point into the upper left of the rect }
      left := myPt.h;
      top := myPt.v;
      right := left + 1;
      bottom := top + 1;

      AdjRect(TblPnl, myRect);

      { Stuff the upper left point of the rect into the point }
      myPt.h := left;
      myPt.v := top
   end;

   { Split the scroll bar at that point }
   pt := myPt.V;
   if tyvhofSb(HsbOld) = H then pt := myPt.H;
   SplitSB(LCSBList, HsbOld, HsbNew, pt);

   { Set the thumb position of the new scroll box }
   SetThumb(HSBNew, 0);
   SetSBIcons(HSBNew, [iconSkewer, iconArwA, iconPagA, iconThumb, iconPagB, iconArwB] );
end;


{$S Scrolling}
procedure ReThumbLC(init : boolean);
{ Proc to set and draw all the LC thumb positions based on the band information }

VAR
   myTMBand: TMBand; { see libte/teenv }
   myHsb   : THsb;   { hnd to a scroll box, always runs to end of scroll bar }
   myRect  : Rect;
   myFract : Fract;
   newHSB  : THsb;   { new HSB formed after a split }

begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   SetPnlPort(FolderPnl);
   { in libte/tm4 - UNIT TM, init call to prepare getting the bands }
   TMSaveBands;

   { get the first row band and set the thumb position }
   TMSaveNextBand(myTMBand);
   myHSB := LCHSBV;
   LCSetThumb(myHSB, myTMBand.tmbloid, init);

   { now do each of the other row bands }
   TMSaveNextBand(myTMBand);
   while myTMBAnd.tmbKind = rowkind do begin
      if init then begin
         LCSplitX(myHSB, NewHSB, myTMBand);
         myHSB := NewHSB
      end
      else myHSB := HSBNext(myHSB);
      LCSetThumb(myHSB, myTMBand.tmbloid, init);
      TMSaveNextBand(myTMBand)
   end; { While }

   { myTMBand now contains the first column band }
   myHSB := LCHSBH;
   LCSetThumb(myHSB, myTMBand.tmbloid, init);

   { Get the next band and do each column band }
   TMSaveNextBand(myTMBand);
   while myTMBand.tmBloid <> nullsnip do begin
      if init then begin
         LCSplitX(myHSB, NewHSB, myTMBand);
         myHSB := NewHSB
      end
      else myHSB := HSBNext(myHSB);
      LCSetThumb(myHSB, myTMBand.tmbloid, init);
      TMSaveNextBand(myTMBand)
   end; { While }
end;


{$S growCode} {& splitCode }
procedure SetLCScroll;
{ Proc to set all the scroll box rects and thumb positions based on the band information }
VAR
   myTMBand: TMBand; { see libte/TEEnv }
   myHsb   : THsb;   { hnd to a scroll box, always runs to end of scroll bar }
   myRect  : Rect;
   myFract : Fract;
   newHSB  : THsb;   { new HSB formed after a split }

begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   { kill all the scroll boxes except the heads }
   myHSB := HsbFirst(LCSBList);
   while (myHsb <> hsbNil) do begin
      if FSBarHead(myHSB) then NewHSB := HSBNext(myHSB)
      else begin
              { Save a ptr to the next one and delete the current one }
              newHSB := HSBNext(myHSB);
              KillSB(LCSBList, myHSB)
           end;
      myHSB := newHSB
   end;

   { set all the initial SB rects }
   LCInitSBRect;
   ReThumbLC(true);
end;


(*$S BGspecific *)
procedure SetBGScroll;
{ sets all the rects for all the BG scroll boxes }
VAR
   rH, rV: Rect;
   myTblRect,
   myFolderRect: Rect;
   myPt        : Point;

begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   { Set all the LC Scroll bars(LCHsbV, LCHsbH, LCHsbGray) to LC positions.
     Then put the BG SBs right on top of the LC ones. }
   LCInitSBRect;

   GetSBRect(LCHsbV, rV);
   SetSBRect(BGHsbV, rV);
   GetSBRect(LCHsbH, rH);
   SetSBRect(BGHsbH, rH);

   { Get the TblPnl coords in Folder relative units and move the LC vertical
     SB to right of the TblPnl }
   GetPnlRect(TblPnl, myTblRect);
   AdjRect(TblPnl, myTblRect);

   With myPt do begin
      h := myTblRect.Right;
      v := myTblRect. top;
   end;
   MoveSB(LCHsbV, myPt);

   { shorten the LC and BG horizontal scroll bars to fit under the panels }
   { *** Note: SHould maybe use AdjSBCorner *** }
   GetSBRect(LCHsbH, rH);
   rH.right := myPt.h;
   SetSBRect(LCHsbH, rH);

   GetSBRect(BGHsbH, rH);
   rH.left  := myPt.h + dhSBox - dhSkewer;
   SetSBRect(BGHsbH, rH);
end;


(*$S initial *)
procedure SetUpSBars;
Const LCBotRt = 100;
      BGBotRt = 0;
var LCDummy : point;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   InitWmlSb;

   { init an empty scroll bar list on theHeap }
   InitSBList(LCSbList, theHeap);
   SetPt(LCDummy, 0, 0);

   { Get 3 new scroll bars. This order puts horiz 1st, gray 2nd, vert 3rd }
   LChsbV    := SbCreate(LCSbList, hsbNil, tyvhV, LCDummy, LCBotRt);
   LChsbgray := SbCreate(LCSbList, hsbNil, tyvhV, LCDummy, LCBotRt);
   LChsbH    := SbCreate(LCSbList, hsbNil, tyvhH, LCDummy, LCBotRt);

   { Init the scrap scroll bars on their own list }
   InitSBList(ScrapSBList, theHeap);
   hsbVScrap := SbCreate(ScrapSbList, hsbNil, tyvhV, LCDummy, LCBotRt);
   hsbHScrap := SbCreate(ScrapSbList, hsbNil, tyvhH, LCDummy, LCBotRt);

   if g.BGflag then begin
      BGHsbH := SbCreate(LCSbList, LCHsbV, tyvhH, LCDummy, BGBotRt);
      BGHsbV := SbCreate(LCSbList, BGHsbH, tyvhV, LCDummy, BGBotRt)
   end
   else begin
           InitSBList(DiagSBList, theHeap);
           hsbVDiag := SbCreate(DiagSBList, hsbNil, tyvhV, LCDummy, LCBotRt);
           hsbHDiag := SbCreate(DiagSBList, hsbNil, tyvhH, LCDummy, LCBotRt)
        end
end;


{$S OpenClose}
procedure LCSBInit;
{ inits all the scroll and grow box information for Lisacalc }
var rh, rv : TR;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   { Set the initial SB rects }
   LCInitSBRect;

   SetLCScroll;

   { Add in the appropriate icons and the thumb positions}
   SetSBIcons( LChsbV, [iconSkewer, iconArwA, iconPagA, iconThumb, iconPagB, iconArwB]);
   SetSBIcons( LChsbH, [iconSkewer, iconArwA, iconPagA, iconThumb, iconPagB, iconArwB]);

   SetSBIcons(hsbVScrap, []);
   SetSBIcons(hsbHScrap, []);

   SetPort(ScrapFolder);
   FlushRects(rH, rV);
   SetSBRect(hsbHScrap, rH);
   SetSBRect(hsbVScrap, rV)
end;


{$S OpenClose}
procedure BGSBInit;
{ inits all the scroll and grow box information for Business Graphics}
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   { init the LC Scroll bars, then the BG ones }
   LCSBInit;

   { set the appropriate icons for the LC scroll bars. }
   SetSBIcons( LChsbV, [iconArwA, iconPagA, iconThumb, iconPagB, iconArwB]);
   SetSBIcons( LChsbH, [iconArwA, iconPagA, iconThumb, iconPagB, iconArwB]);

   { Set the bg Scroll bar rects }
   SetBGScroll
end;


(*$S BGspecific *)
procedure ClearBGscroll;
{ Whites out the scroll bars }
var rH : TR;  { Rects around the perimeter of the folder }
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   GetSBRect(LCHsbV, rH);
   rh.left   := Folder^.portrect.Left;
   rh.bottom := Folder^.portrect.bottom;
   rh.right  := rh.right - 1;
   HideSBorder(rH);

   { Fixes BG bug 105 on Forgotten Line 4/5/83 }
   GetSBRect(LCHsbV, rH);
   with rH do begin
      MoveTo(Right-1, Top);
      LineTo(Right-1, Bottom-1)
   end
end;


(*$S fexec *)
procedure ClearScroll;
{ Whites out the scroll bars }
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   SetPnlPort(FolderPnl);

   { Get and white out the two scroll bars }
   HideSBorder(Folder^.portrect);

   if g.BGFlag then ClearBGscroll
end;


(*$S fexec *)
procedure LCDrawScrolls;
{ Positions and draws the LC scroll bars }
var rH, rV : TR;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   SetPnlPort(FolderPnl);
   { Paint the entire horizontal and vertical bars }
   FlushRects(rH, rV);
   EraseRect(rV);
   if g.BGflag then EraseRect(rH);

   PaintSBar(LChsbH);
   PaintSBar(LChsbgray);
   PaintSBar(LChsbV);
   PaintGrow
end;


(*$S BGspecific *)
procedure BGDrawScrolls;
{ Positions and draws the BG scroll bars }
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   SetPnlPort(FolderPnl);

   { Redraw the LC Scroll bars, then the BG scroll bars }
   LCDrawScrolls;
   PaintSBar(BGhsbH);
   PaintSBar(BGhsbV)
end;


(*$S growCode *)
(***************************************.*******.************************************
   InvalidScrBars - invalidates the regions taken up by the scroll bars for the
                    currently set graf port.
****************************************.*******.***********************************)
procedure InvalidScrBars(Fld : GrafPtr);
var rH, rV : TR;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   SetPort(Fld);
   FlushRects(rH, rV);
   InvalRect(rH);
   InvalRect(rV);
   GetGrowRect(rV);
   InvalRect(rV)
end;


(***************************************.*******.************************************
   LCCkGrow -  Returns True iff the user mousedowned in the grow box. As a side
               effect, it handles all the events for the user dragging the grow
               box to resize the folder or the scrap.  Checks for legal size,
               then resizes the scrap or main folder and all the panels in them to
               the new size.  Follows example on p. 17 of the 11/6/81 document by
               Brad Silverberg on the WM library routines
****************************************.*******.***********************************)
(*$S fexec *)
function LCCkGrow : boolean;
var ptNewBR    : TPt;              { new lower right corner set by the user }
    thePnl     : integer;


   {$S funcHints}
    procedure CheckDiBox;
    begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

       SetPort(dialogFolder);
       if FGrowHit(WMevent.Where) then begin
          LCCkGrow := true;
          DragFrame(WMevent.where, True, ptNewBr);
          PutFuncHints(ptNewBr.v)
       end
    end;


    (*$S BGspecific *)
    procedure FinishBGstuff;
    begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

       lcBGSetGrfx;
       SetBGScroll;                 { Reset the scroll bar rects }
       BGSetThumb;                  { Set the thumb positions }

       GetSBrect(LCHsbV, RefreshRect);
       SetPort(Folder);
       InvalRect(RefreshRect);

       SetPnlPort(BGPnl);
       GetPnlRect(BGPnl, RefreshRect);
       RectRgn(BGRefreshRgn, RefreshRect);
       InvalidateRgn(WMEvent.who, BGRefreshRgn)
    end;


    (*$S growCode *)
    procedure FinishGrow;
    var oldrect    : rect;      { old botright of the folder                 }
        temp       : rect;      { new scroll bar rectangles, also Scrap field rect }
        pnlHeight  : integer;
        sKind      : scrapKind;
    begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

       InvalidScrBars(WMEvent.who);

       { get the new size for the folder }
       if WMEvent.who = ScrapFolder then begin
          DrwScrScrollBars(false);
          pnlHeight  := 1;
          GetPnlRect(cScrapPnl, oldrect)
       end
       else begin
               ClearScroll;
               pnlHeight  := 1 + ord(g.LCflag);
               GetPnlRect(FolderPnl, oldrect)
            end;
       ptNewBR.v := CMax(ptNewBR.v, pnlHeight * pnlLineDv + dvsBox);
       SubPt(oldrect.topleft, ptNewBR);

       FolderSize(WMEvent.who, ptNewBR.h, ptNewBR.v, true);
       ptNewBR     := WMEvent.who^.portrect.botRight;
       ScreenChged := true;

       InvalidScrBars(WMEvent.who);

       { Reset all the panel sizes }
       if WMEvent.who = ScrapFolder then begin
          Size2Scrap(ptNewBR);  { in aplc/LCExecsubs - UNIT LCfexec}
          GetScrKind(sKind);
          if sKind = txtKind then begin
             SetPort(ScrapFolder);
             GetPnlrect(cScrapPnl, temp);
             InvalRect(temp)
          end
       end
       else begin
               ChgPnlSize(FolderPnl, ptNewBR.h, ptNewBR.v);
               SetApp2Pnls;        { in LCExecSubs }
               if g.ActivePnl <> cBGPnl then ReturnRGFB(g.curTimeOut); {get the hilite right}
               if g.curRange.rKind = a1CellRg then reSizeFlds;

               { Set all the panel sizes }
               if g.LCFlag Then SetLCScroll
               else FinishBGstuff
            end
    end;


(*$S fexec *)
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug}
   if tracing then LCTraceMess(8);
{$ENDC }
   LCCkGrow := False;
   with WMEvent do
      if (who = Folder) or (who = ScrapFolder) then begin
         if who = Folder then thePnl := FolderPnl
         else thePnl := cScrapPnl;
         SetPnlPort(thePnl);

         if FGrowHit(Where) then begin
            LCCkGrow := True;
            DragFrame(where, True, ptNewBr);
            FinishGrow;
         end
      end
      else if (who = DialogFolder) and (curDiBox = diFuncHints) then CheckDiBox
end; { LCCkGrow }


(*$S fexec *)
procedure LCSkewer(where: Point; myhsbHit: Thsb; myIconHit: TIcon);
{ Handles the events after the user has downed on a skewer. Where is
  folder relative }

var myRect     : Rect;    { Rect within which the skewer can move }
    ptSkewerUp : TPt;     { pt where skewer comes up              }

   (*$S splitCode *)
   procedure finishSplit;
   var newHSB     : THSB;    { New HSB after the split               }
       myPnlPt    : Point;   { mouse up point relative to TblPnl     }
       didsplit   : boolean; { true if split makes new band          }
       myNewP     : pixel;       { TblPnl pixel where Skewer came up }
       myResult   : BGrowResult;
       prevPnlPt  : Point;   { Point relative to TblPnl within the previous
                               band. Needed since TM associates a skewer with
                               previous band, but WMLSB associates a skewer with
                               the following band }
   begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

      adjtoPnl(TblPnl, ptSkewerUp, myPnlPt);

      { If it's the head then you're opening a new pnl, else resizing a pnl }
      if FSBarHead(myHsbHit) then begin

         { set up a new scroll bar and a new LC band }
         If tyvhofSb(myHSBHit) = H then SplitBand(colKind, myPnlPt.H, didSplit) { Split the H scroll box }
         else SplitBand(rowKind, myPnlPt.V, didSplit)    { Split the V scroll box }
      end

      else begin
              { Get a TblPnl point in the previous band. Needed since TM includes the
              split at the end of the previous band, and WM includes the split at the
              beginning of the next band. }
              AdjToPnl(tblPnl, where, prevPnlPt);
              with PrevPnlPt do begin
                 h := h - 2 * dhSkewer;
                 v := v - 2 * dvSkewer
              end;
              { adjust the size of a LC band. myPnlpt is where the skewer came up }
              If tyvhofSb(myHSBHit) = H Then growBand(colKind, prevPnlPt.h, myPnlPt.h, myResult)
              else growBand(rowkind, prevPnlpt.v, myPnlPt.v, myResult)
           end;

      { Redraw the new scroll bars }
      SetPnlPort(FolderPnl);
      SetLCScroll;
      LCDrawScrolls;
      docDirty := true
   end;


(*$S fexec *)
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   { Get the rect in panel coords, then adjust to folder coords }
   GetPnlRect(TblPnl, myRect);
   AdjRect(TblPnl, myRect);
   { Make sure the vertical skewer starts above the panel }
   If (where.v < myRect.Top) Then myRect.Top := where.v;

   { Drag the skewer around within myRect }
   DragSkewer(myhsbHit, where, myRect, ptSkewerUp);
   finishSplit
end;


(*$S BGspecific *)
procedure BGSkewer(where: Point; myhsbHit: Thsb; myIconHit: TIcon);
{ Handles the events after the user has downed on a skewer in BG. "Where"
  is folder relative. Note: the only skewer in BG is the resize skewer
  between the table and the graph panes. }

VAR
   Width     : TC;     { width of the new TblPnl }
   FolderRect: Rect;
   TblRect   : Rect;
   ptSkewerUp: point;

begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   SetPnlPort(FolderPnl);
   GetPnlRect(TblPnl, TblRect);
   GetPnlRect(FolderPnl, FolderRect);

   { Drag the skewer around within FolderRect, leaving room for the rightmost scroll bar }
   With FolderRect do Right := Right - dHsBox;
   DragSkewer(myhsbHit, where, FolderRect, ptSkewerUp);

   { Resize only if the user dragged the skewer a certain minimum distance }
   If (Abs(ptSkewerUp.h - where.h) > dHSkewer) Then Begin

      { New Tblpnl width goes from Folderleft to (SkewerUp - width of scroll box) }
      Width := ptSkewerUp.h - FolderRect.Left - dHSBox;
      with TblRect do ChgPnlSize(TblPnl, width, Bottom-Top);

      { Set the rest of the panels }
      SetApp2Pnls;        { in file LCExecSubs }
      If (g.ActivePnl <> cBGPnl) Then ReturnRGFB(g.curTimeOut); {get the hilite right}
      TmShoNewTbl;

      { Reset all the grfx parameters for the graph, erase and redraw }
      lcBGSetGrfx;
      GetPnlRect(BGPnl, RefreshRect);
      RectRgn(BGRefreshRgn, RefreshRect);
      iEraseBG(BGProcInfo.EraseBG);
      iDrawGraph(BGProcInfo.DrawGraph);

      { Redraw the new scroll bars }
      SetPnlPort(FolderPnl);
      SetBGScroll;
      BGDrawScrolls;

      { Reset the BG thumbs }
      BGSetThumb;
   end;
end;


(*$S fexec *)
procedure LCBGThumb(where: point; myhsbHit: Thsb; myIconHit: TIcon);
{ Handles the events after the user has downed on the thumb in LC & BG }

var NewThumb : TC;    { new thumb position in thousandths }
    myPoint  : point; { point within the scroll bar containing the thumb }
    didMove  : boolean;
    temp     : integer;
    snip     : snipKind;

    (*$S BGspecific *)
    procedure BGThumb;
    var direc : THorV;
    begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

       if (myHsbHit = BGHsbH) or (myHsbHit = BGHsbV) then begin
          SetPnlPort(BGPnl);             { Scroll the graph }
          direc := BGVert;
          if myHsbHit = BGHsbH then direc := BGHoriz;
          iBGScroll(direc, JmpScroll, scrollfwd, NewThumb, BGProcInfo.BGScroll);
          exit(LCBGThumb)
       end
    end;

(*$S fexec *)
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   { let user drag the thumb }
   SetPnlPort(FolderPnl);
   DragThumb(myhsbHit, where, NewThumb);
   SetThumb(myHSBHit, NewThumb);

   if g.BGflag then BGThumb;

   GetSBMidPoint(myhsbHit, myPoint);
   snip := rowKind;
   temp := myPoint.V;
   if tyvhofSb(myhsbHit) = H then begin
      snip := colKind;
      temp := myPoint.H
   end;
   ScrBand(snip, temp, jmpScroll, ScrollFwd, NewThumb, didMove)
end; { LCThumb }


(*$S Scrolling *)
procedure LCBGGryArrow(myhsbHit: Thsb; myIconHit: TIcon);
{ Handles the events after the user has downed on the gray area & scroll arrow in BG }
var myScrDir    : ScrollDir;
    myScrType   : ScrollType;
    iconHitType : TIcon;
    direc       : THorV;
    myPoint     : point;
    thumbH      : TC;
    thumbV      : TC;
    didMove     : boolean;
    doLCscroll  : boolean;

    (*$S BGspecific *)
    procedure doBGscroll;
    var cNewThumb : TC;
    begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

        cNewThumb := CThumbPos(myhsbHit);
        direc     := BGVert;
        if myHsbHit = BGHsbH then direc := BGHoriz;

        repeat
           SetPnlPort(BGPnl);
           iBGScroll(direc, myscrType, myscrDir, cNewThumb, BGProcInfo.BGScroll);

           { set up the new thumb position }
           SetPnlPort(FolderPnl);
           iBGGetThumb(thumbH, thumbV, BGProcInfo.BGGetThumb);
           cNewThumb := thumbV;
           if myHSBHit = BGHsbH then cNewThumb := thumbH;
           MoveThumb(cNewThumb)
        until not stilldown
     end;


(*$S Scrolling *)
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   { init moving the thumb then track it in the tight loop }
   SetPnlPort(FolderPnl);
   SetupMvThumb(myHsbHit);
   PaintArw(myHSBHit, myIconHit, true);
   case myIconHit of
      iconArwA,
      iconArwB : begin
                    GetSBMidPt(myHSBHit, myPoint);
                    myscrType   := IncrScroll;
                    iconHitType := iconArwA
                 end;
      otherwise  begin
                    myscrType   := PageScroll;
                    iconHitType := iconPagA
                 end
   end;

   { Set the scroll direction to display more of the top (left) of the graph
     if iconArwA is hit.  Otherwise display more of the bottom (right) of the graph }
   myscrDir := ScrollFwd;
   if myIconHit = iconHitType then myscrDir := ScrollBak;

   doLCscroll := true;
   if g.BGflag then doLCscroll := (myHSBHit <> BGHsbH) and (myHSBHit <> BGHsbV);

   if doLCscroll then
      repeat
         { Jump table horiz'ly or vertically according to type of scroll bar }
         SetPnlPort(FolderPnl);
         if myScrType <> IncrScroll then GetSBMidPt(myHSBHit, myPoint);
         If TyvhOfSb(myhsbHit) = H Then ScrBand(colKind, myPoint.h, myScrType, myScrDir, myPoint.h, DidMove)
         else ScrBand(rowKind, myPoint.v, myScrType, myScrDir, myPoint.v, DidMove);
         ReThumbLC(false);
      until not stilldown
   else doBGscroll;
   SetPnlPort(FolderPnl);
   PaintArw(myHSBHit, myIconHit, false)
end;


{ Returns true iff the user mousedowned in one of the scroll boxes. As a side
effect, it handles all the events as the user drags etc. up to and including
the mouseup event }
(*$S fexec *)
Function LCCkScroll: Boolean;
var myhsbHit  : Thsb;  { scroll box that the user hit             }
    myIconHit : TIcon; { icon in the scroll box that the user hit }

   {$S funcHints}
    procedure CheckDiBox;
    begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

       SetPort(dialogFolder);
       if FSBHit(DiagSBList, WMEvent.where, myhsbHit, myIconHit) then begin
          LCCkScroll := true;
          DiBoxScroll(WMEvent.where, myhsbHit, myIconHit)
       end
    end;


(*$S fexec *)
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   LCCkScroll := False;
   with WMEvent do begin

      { do these if its LisaCalc }
      if who = Folder then begin
         SetPnlPort(FolderPnl);
         { use the library routine to see if the user hit a scroll box }
         if FSBHit(LCSbList, where, myhsbHit, myIconHit) then begin
            LCCkScroll  := true;
            ScreenChged := true;
            case myIconHit of

               IconSkewer: if g.LCflag then LCSkewer(where, myhsbHit, myIconHit)
                           else BGSkewer(where, myhsbHit, myIconHit);

               IconThumb : LCBGThumb(where, myhsbHit, myIconHit);

               IconArwA,
               IConArwB,
               IconPagA,
               IconPagB  : LCBGGryArrow(myHsbHit, myIconHit)
            end
         end
      end
      else if (who = DialogFolder) and (curDiBox = diFuncHints) then CheckDiBox
   end
end;

ÿ