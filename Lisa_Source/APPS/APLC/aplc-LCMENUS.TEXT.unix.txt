
                            {'Copyright 1983, 1984, Apple Computer Inc.'}

(*********** aplc/lcmenus - lisacalc menu manager - unit teexec **************)


{$S fexec}
procedure ChgGray(menuItem: Integer; FGray: Boolean);
{ grays/ungrays one menu item according to the FGray boolean}
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   if FGray Then DisableItem(myMenus[menuXXX], menuItem)
            Else EnableItem(myMenus[menuXXX], menuItem)
end;


(*$S fexec *)
procedure ChkItem(MenuItem : integer; FCheck : boolean);
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   CheckItem(myMenus[menuXXX], MenuItem, FCheck)
end;


{$S fexec}
procedure SetMenuNum(menuItem : integer);
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   menuXXX := menuItem
end;


{$S fexec}
procedure ChgFilePrint;
var titleTab : str255;
    closeStr : str255;
    tempStr  : str40;
    MenuT    : integer;

    (*$S BGspecific *)
    procedure doBGFilePrint;
    var x : integer;
    begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

       GetItem(myMenus[MenuStat], PrintWord, @CloseStr);
       x := GraphWord;
       if (g.ActivePnl <> cBGPnl) and (g.curRange.rKind <> aNullRg) then x := TableWord;
       GetItem(myMenus[MenuStat], x, @tempStr);
       CloseStr := concat(CloseStr, ' ',tempStr);
       GetItem(myMenus[MenuStat], AsIsWord, @tempStr);
       CloseStr := concat(CloseStr, ' ',tempStr);
       SetItem(myMenus[MenuT], BGPrintDoc, @CloseStr)
    end;

(*$S fexec *)
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   GetFldrTitle(ActiveFolder, titleTab);

   { disable the null items }
   if ScrapFolder = ActiveFolder then begin
      MenuT := MenuScrap;
      SetMenuNum(MenuScrap)
   end
   else begin
           if g.BGflag then MenuT := BGFilePrint
           else MenuT := LCFilePrint;
           SetMenuNum(MenuT);
           ChgGray(7, True);
           if g.LCflag then begin
              ChgGray(LCPrSelection, (tableState <> tValues) or (g.curRange.rkind = aNullRg));
              ChgGray(LCPrAsIs, printState.preview);
              ChgGray(LCPrDialog, printState.preview)
           end
           else doBgFilePrint
        end;
   ChgGray(3, True);

   GetItem(myMenus[MenuBuzz], CloseMsg, @CloseStr);
   CloseStr := concat(CloseStr, ' "',titleTab,'"');
   SetItem(myMenus[MenuT], 2, @CloseStr)
end;


{$S fexec}
procedure ChgLCBGEdit;
{ disables the LC and BG edit items according to the current range. See the old
  LCMenus/chgEditMenu. }
var GraySet1,                                   { LC gray set }
    GraySet2  : Set of BGUndoLast.. SelecGraph; { BG gray set }
    PItem     : Integer;
    theSelKind: SelKind;
    InsertStr : Str255;
    sKind     : scrapKind;
    scrapCont : TB;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   { make the insert item say InsertRow/Col. Gets reset below if needed }
   GetItem(myMenus[MenuBuzz], CantInsert, @InsertStr);
   if g.BGflag then begin
      SetMenuNum(BGEdits);
      SetItem(myMenus[BGEdits], BGInsrtCol, @InsertStr)
   end
   else begin
           SetMenuNum(MenuEdits);
           SetItem(myMenus[MenuEdits], LCInsrtCol, @InsertStr)
        end;

   GetSelKind(theSelKind);
   if (g.ActivePnl = nullPnl) or TblPars.shoformulas or printState.preview then begin
      { Gray everything except undo and restore }
      GraySet1:= [feCopy, cut, pasteM, pasteR, pasteV, clr, LCInsrtCol];
      GraySet2:= [BGCopy, BGcut, BGpaste, BGclr, BGInsrtCol];
   end

   else if g.BGFlag and BGAllFlag then begin
           { Whole graph is selected. Enable only Copy, Select Table, and Select Graph  }
           { it's BG so GraySet1 doesnt matter }
           GraySet2 := [BGRstrEntry, BGCut, BGPaste, BGClr, BGInsrtCol];
        end

   else if g.ActivePnl <> ctblPnl then begin
           GraySet1 := [PasteR, PasteV, LCInsrtCol];
           GraySet2 := [BGInsrtCol];
        end

   else begin

           { it's a straight text selection }
           if theSelKind = aCellTxtSl then begin
              GraySet1 := [PasteR, PasteV, LCInsrtCol];
              GraySet2 := [BGInsrtCol];
           end

           else case g.curRange.rKind of
                   a1CellRg,
                   aRectRg,
                   aColRg,
                   aRowRg  : begin
                                GraySet1 := [LCInsrtCol];
                                GraySet2 := [BGInsrtCol];
                             end;

                   aWTBoxRg: begin
                                GraySet1 := [pasteR, LCInsrtCol];
                                GraySet2 := [BGPaste, BGInsrtCol];
                             end;

                   aColGrid,
                   aRowGrid: begin
                                GraySet1 := [feCopy, cut, clr];
                                GraySet2 := [BGCopy, BGCut, BGClr];

                                PItem := InsertCol;
                                if g.curRange.rKind = aRowGrid then PItem := InsertRow;

                                { make the Insert menu item say InsertRow }
                                GetItem(myMenus[MenuBuzz] , PItem , @InsertStr);
                                if g.BGflag then SetItem(myMenus[BGEdits],   BGInsrtCol, @InsertStr)
                                else SetItem(myMenus[MenuEdits], LCInsrtCol, @InsertStr);
                             end;

                   aRowHedRg,
                   aColHedRg: begin
                                 GraySet1 := [cut, pasteM, pasteR, pasteV, clr, LCInsrtCol];
                                 GraySet2 := [BGcut, BGPaste, BGClr, BGInsrtCol];
                              end;

                   otherwise begin
                                GraySet1 := [feCopy, cut, pasteM, pasteR, pasteV, clr, LCInsrtCol];
                                GraySet2 := [BGCopy, BGCut, BGpaste, BGClr, BGInsrtCol];
                             end
                end
        end;

   {disable paste if there is nothing on the scrap we know how to paste}
   GetCsScrap(ScrapCont);               {Check universal text stream}
   GetScrKind(sKind);
   if (ScrapCont = 0) or (sKind <> cellsKind) then begin          {no universal text, check if it is a scrap type we know about}
      if sKind <> cellsKind then begin
         GraySet1 := GraySet1 + [pasteR, pasteV];
         if (ScrapCont = 0) and (sKind <> txtKind) then begin
            GraySet1 := GraySet1 + [pasteM];
            GraySet2 := GraySet2 + [BGpaste]
         end
      end
   end;

   case g.pnlwithChg of
      cTblPnl,
      cWidePnl : if theSelKind <> aCellTxtSl then begin
                    GraySet1 := GraySet1 + [restOrig];
                    GraySet2 := GraySet2 + [BGRstrEntry]
                 end;
      cformPnl : ;
      otherwise  begin
                    GraySet1 := GraySet1 + [restOrig];
                    GraySet2 := GraySet2 + [BGRstrEntry]
                 end
   end;

   { Disable the dots }
   GraySet1 := GraySet1 + [3, 9, 11, 13];
   GraySet2 := GraySet2 + [3, 7,  9, 11];

   { Gray the items in the grayset }
   if g.LCFlag then
      for PItem := UndoItem to SelecAll do
         ChgGray(PItem, (PItem in GraySet1))
   else for PItem := BGUndoLast to SelecGraph do
           ChgGray(PItem, (PItem in GraySet2))
end;


{$S LCfexec}
procedure ChgLCPageLayout;
{ cks the page layout items according to the current range }

var FGray    : Boolean;
   { string to give proper wording for InsertCol or InsertRow }
    insertStr: str255;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   SetMenuNum(LCPageLayout);
   with g.CurRange do begin

      { enable the column width item }
      ChgGray(cWidth, ((rKind <> aColRg) and (rKind <> aWTBoxRg)) or printState.preview);

      { Gray the null item }
      ChgGray(2, True);
      ChgGray(5, True);
      ChgGray(8, True);

      ChgGray(DocFPrint, printState.preview);

      ChkItem(DoPreview, printState.preview);
      ChkItem(DoNoPreview, not printState.preview);

      { gray the PageBreak items according to whether the user has selected a grid handle or not }
      ChgGray(DoSetBreaks, ((rKind <> aRowGrid) and (rKind <> aColGrid)) or printState.preview);

      { Enable the clrbreaks item when the selection is one of these }
      case rKind of
         aWTBoxRg,
         aColRg,
         aRowRg,
         aColGrid,
         aRowGrid : FGray := false;
         otherwise  FGray := true
      end;

      { Gray it if its not enabled }
      ChgGray(DoClrBreaks, FGray or printState.preview)

   end { with }
end;


{$S fexec}
procedure ChgLCformat;
{ Cks the format menu for LC and for BG when the TblPnl is the active panel.
  if a1cellRg, it cks the current format items, otherwise it uncks them
  all }

var PItem     : Integer;
    MenuT     : Integer; { Menu title for either BG or LC }
    fGray     : Boolean;
    Decr      : Integer; { decrement for the lower half of the LC menu to
                          account for the extra menu item of fill pattern }
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   MenuT := BGformat;
   Decr  := 1;
   if g.LCflag Then begin
      MenuT := LCformat;
      Decr  := 0
   end;
   SetMenuNum(MenuT);

   with g.CurRange do begin

      { gray all items for these ranges. BG has two fewer item
        which is why you subtract out the decr }
      case rKind of
         aColGrid,
         aRowGrid,
         aNullRg : fGray := true;
         otherwise fGray := printState.preview
      end;

      for PItem := AlStandard to MoneyPlain-Decr do ChgGray(PItem, fGray);

      { Now check the appropriate items }
      if (rKind = a1CellRg) and not printState.preview then begin
         CellAttr(g.CurRange.loRow, g.CurRange.loCol, g.Attributes);

         with g.Attributes do begin
            { do the alignment items }
            ChkItem(alStandard, (attalign = aStandard) or (attalign = aInVis));
            ChkItem(alLeft,     (attalign = aLeft));
            ChkItem(alCenter,   (attalign = aCenter));
            ChkItem(alRight,    (attalign = aRight));

            if g.LCFlag then ChkItem(alRepeat, (attalign = aRepeating));

            { do the number format items }
            ChkItem(FMdefault -Decr,  (attformat = fStandard));
            ChkItem(FMInteger -Decr,  (attformat = fInteger));
            ChkItem(FMDecimals-Decr,  (attformat = fDecimal));
            ChkItem(FMScientific-Decr,(attformat = fScientific));

            { Unck all the money items }
            for PItem := MoneyDCP-Decr to MoneyPlain-Decr do ChkItem(PItem, False);

            { if money format then ck the right one }
            if (Attformat = fMoney) Then begin
               PItem := MoneyPlain - Decr;
               if (mCents in AttMoney)  Then PItem := PItem - 4;
               if (mComma in AttMoney)  Then PItem := Pitem - 2;
               if (mDollar in AttMoney) Then PItem := PItem - 1;
               ChkItem(pItem, True)
            end
         end  { with }
      end { if on a1cellrg }

      else for PItem := ALStandard to MoneyPlain-Decr do ChkItem(PItem, False)
   end; { with }

   { Gray the null item }
   ChgGray(6 - Decr, True)
end;


{$S LCfexec}
procedure ChgLCProtect;
{ if selection is a1cellRg then ck its protection mode, otherwise
  unck everything }

var PItem : Integer;
    fGray : Boolean;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   SetMenuNum(MenuProtect);
   with g.CurRange do begin
      if (rKind = a1cellRg) and not printState.preview Then begin
         CellAttr(g.CurRange.loRow, g.CurRange.loCol, g.Attributes);
         with g.Attributes do begin
            ChkItem(pUnProtect,  (attProt = ProtNone));
            ChkItem(pProtect,    (attProt = ProtAll));
            ChkItem(pRulesOnly,  (attProt = ProtRules));
            ChkItem(alInvisible, (attalign = aInVis));
            ChkItem(alVisible,   (attalign <> aInVis))
         end;
      end

      else for PItem := pUnProtect to alVisible do   {uncheck everything}
              ChkItem(pItem, False);

      { Gray it for a grid handle selection or null rg, else enable it }
      case rKind of
         aRowGrid,
         aColGrid,
         aNullRg : fGray := true;
         otherwise fGray := printState.preview
      end;
      for PItem := pUnProtect to alVisible do ChgGray(PItem, fGray);
      ChgGray(4, true)
   end
end;


{$S LCfexec}
procedure ChgLCCalculate;
{ ck the items in the calculation menu according to the current state vars }

var PItem      : integer;
    theSelKind : selKind;
    state      : boolean;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   SetMenuNum(LCCalculate);

   { Do the show formula menu items }
   ChkItem(doShowforms, TblPars.shoformulas);
   ChkItem(doShowVals, not TblPars.shoformulas);

   for pItem := CalcAuto to FuncHints do ChgGray(pItem, printState.preview);

   { Gray the null items }
   ChgGray(4, true);
   ChgGray(7, true);
   ChgGray(10, true);
   ChgGray(13, true);

   { do the calculation menu items. CalcNow is always enabled. }
   ChkItem(CalcAuto, g.AutoCalc);
   ChkItem(CalcMan, not g.AutoCalc);

   { Do the circle menu items }

   if not printState.preview then begin
      ChgGray(DoFindCircles, (circleState = cNone) or (tableState <> tValues));
      ChgGray(DoFindSame, (length(StrToFind) = 0) or (tableState <> tValues) or not(FindInVals or FindInForms));

      case g.pnlWithChg of
         ctblPnl  : begin
                       GetSelKind(theSelKind);
                       state := (theSelKind = aCellTxtSl)
                    end;
         cWidePnl,
         ccoPnl,
         cFormPnl : state := true;
         nullPnl  : state := (g.activePnl = dialogpnl) or ((g.activePnl <> cTblPnl) and (g.activePnl <> nullPnl));
         otherwise  state := (g.activePnl <> cTblPnl) or (curDiHeight > 0)
      end;
      ChgGray(funchints, state)
   end
end;


{$S fexec}
procedure ChgMenuLC;
{ Enables/Disables and Checks/Unchecks the menus for LC }
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

(*$IFC fDebug *)
   if traceLCexec then LCtraceMess(101);
(*$ENDC *)

   ChgFilePrint;
   ChgLCBGEdit;
   ChgTypeStyle;
   ChgLCPageLayout;
   ChgLCformat;
   ChgLCProtect;
   ChgLCCalculate;
end;


{*************************************************************************}
{ These routines handle cking and enabling the BG menu items              }


{$S BGspecific}
procedure ChgBGPageLayout;
{ Cks and enables the proper items in the BG page layout menu }
var fGray : Boolean;
    PItem : Integer;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   SetMenuNum(MenuBGPageLayout);
   { enable the column width item }
   fGray := not(TblEditPnl(G.activePnl) AND
                ((g.CurRange.rKind = aColRg) OR (g.CurRange.rKind = aWTBoxRG)));
   ChgGray(cWidth, fGray);

   { Uncheck all the graph size items }
   for PItem := BGFull to BGQuarter do ChkItem(PItem, False);

   { Check the proper graph size }
   ChkItem(BGFull + ord(GraphProportion), True);

   { Check the reduced/actual size items }
   ChkItem(BGActual, BGPreview);
   ChkItem(BGReduced, (NOT BGPreview));

   { Gray the null items }
   ChgGray(2, True);
   ChgGray(7, True);
end;


{$S BGspecific}
procedure ChgBGformat;
{ Checks the format menu items }
var PItem : Integer;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   SetMenuNum(BGformat);
   if (g.ActivePnl = BGPnl) Then begin

      { Gray and uncheck all the items }
      for PItem := BGStndrd to BGPlainMoney do begin
         ChgGray(PItem, True);
         ChkItem(PItem, False)
      end;

      { Check off the proper alignment item }
      if BGFld <> Nil Then begin
         with BGFld^^ do begin
            ChkItem(BGLeft,   (Align = Left));
            ChkItem(BGCenter, (Align = Center));
            ChkItem(BGRight,  (Align = Right))
         end
      end
   end

   else ChgLCformat;
end;


{$S BGspecific}
procedure ChgGType;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   SetMenuNum(MenuBGType);
   ChkItem(BGBar, (GType = Bar));
   ChkItem(BGLine, (GType = LineGraph) OR (GType = BarLine));
   ChkItem(BGScatter, (GType = Scatter) OR (GType = BarScat));
   ChkItem(BGPie, (GType = Pie));
   ChkItem(BGStackBar, (GType = StackBar));
   ChkItem(BGSolidBar, (GType = SolidBar));
   ChkItem(BGAreaGraph,(GType = TextAreaGraph) OR (GType = NumbAreaGraph));
end;


(*$S BGspecific *)
procedure ChgBGOptions;
{ Checks and enables the proper items in the options menu }
VAR
   EnableFlag,          { True when menu items are enabled           }
   ColFlag,             { True when user has selected a column range }
   myBool  : Boolean;
   I       : Integer;

begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   SetMenuNum(MenuBGOptions);
   ChkItem(BGGridOn, XAxis.Grid);
   ChkItem(BGGridOff, (NOT XAxis.Grid));

   ChkItem(BGLegOn, Legends.LegendFlag);
   ChkItem(BGLegOff, Not Legends.LegendFlag);

   { gray these items if its a pie chart }
   myBool := (GType = Pie);
   ChgGray(BGGridOn,  myBool);
   ChgGray(BGGridOff, myBool);
   ChgGray(BGLegOn,   myBool);
   ChgGray(BGLegOff,  myBool);
   ChgGray(BGYAxSpec, myBool);

   { enable the XAxSpec for graphs with numeric X axis }
   enableFlag := (GType = LineGraph) OR (GType = Scatter) OR (GType = NumbAreaGraph);
   ChgGray(BGXAxSpec, (NOT enableFlag));

   { Gray and check the Show as Line/Bar and Regression items }
   With g.CurRange do Begin
      { see if its a column range }
      ColFlag    := (rKind = aColRg) AND (2 <= LoCol) AND (LoCol <= 9);

      { Gray and check the Show as Bar/Line menu items }
      enableFlag := ColFlag AND ((GType = Bar) OR (GType = StackBar) OR (GType = SolidBar));
      ChgGray(BGShowBar , (not enableFlag));
      ChgGray(BGShowLine, (not enableFlag));

      myBool := enableFlag and (loCol = hiCol);
      ChkItem(BGShowBar , myBool AND BarBoolean[locol-1]);
      ChkItem(BGShowLine, myBool AND (not BarBoolean[loCol-1]));

      { Gray and check the regression menu items }
      enableFlag := ColFlag AND (GType <> Pie);
      ChgGray(BGRgrsOn , (not enableFlag));
      ChgGray(BGRgrsOff, (not enableFlag));

      myBool := enableFlag and (loCol = hiCol);
      ChkItem(BGRgrsOn , myBool and RgrsBoolean[loCol - 1]);
      ChkItem(BGRgrsOff, myBool and (NOT RgrsBoolean[loCol - 1]));
   end;

   ChkItem(BGMarkOn , BGMarkFlag);
   ChkItem(BGMarkOff, NOT BGMarkFlag);

   { Gray the null items }
   ChgGray( 3, True);
   ChgGray( 6, True);
   ChgGray( 9, True);
   ChgGray(12, True);
   ChgGray(15, True);
end;


(*$S BGspecific *)
procedure ChgMenuBG;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

(*$IFC fDebug *)
   if traceLCexec then LCtraceMess(100);
(*$ENDC *)

   ChgFilePrint;
   ChgLCBGEdit;
   If (g.ActivePnl = BGPnl) Then ChgBGTypeStyle { both in L:LCMenus3. Changes BG when BGPnl active. }
                            else ChgTypeStyle;  { Changes LC; and BG when Tblpnl active }
   ChgBGPageLayout;
   ChgBGformat;
   ChgGType;
   ChgBGOptions;
end;


(*$S scrapCode *)
procedure ChgMenuScrap;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   ChgFilePrint
end;
ÿ