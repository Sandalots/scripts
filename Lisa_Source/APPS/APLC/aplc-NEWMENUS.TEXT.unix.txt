
                            {'Copyright 1983, 1984, Apple Computer Inc.'}

(*********** aplc/newmenus - lisacalc menu management code - unit lcfexec **************)

{********************************************************************}

(*$S RWscrapCode *)
procedure LCutCellValue
  (mode         : IOModeType;        {reading/writing mode flag}
   rowId, colId : idType;
   value        : ptrData;           {ptr to where chrs are to go}
   wideFlg      : boolean;           {format for wide angle display? }
   var numBytes,                     {max # of chrs /actual # chrs returned}
       status   : integer            {error status number}
  );
var errRec : errRecord;
begin
   CellFormula(mode, rowId, colId, value, numBytes, status, errRec)
end;


(*$S RWscrapCode *)
function SaveScrap(forced : boolean; alertNum : integer) : boolean;
var sKind    : scrapKind;
    errNum   : integer;
    titleTab : str255;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   SaveScrap := true;       {assume we want to abort and save the scrap}
   GetScrKind(sKind);
   FEterminate;
   if ((WMEvent.who = ScrapFolder) or (WMEvent.who = Folder) or forced) and (ScrapProcess = My_ID) and
      (sKind = cellsKind) and dumpToScrap and (numDocsOpen > 0) then begin

      {this is saying:  if I own the scrap, and it is of type matrix and the scrap information has changed
       then I might need to take my private information about the scrap and make it public.  This only
       needs to be done I am about to activate someone besides myself & the scrap (that I own).
       Therefore, if the folder about to be activated is not the scrap or something I own then go ahead
       and make the scrap data public.
      }
      if forced or ((WMEvent.fromFolder <> ScrapFolder) and (WMEvent.fromProcess <> My_ID)) then begin
         GetFldrTitle(Folder, titleTab);
         ParamAlert(titleTab, '', '');
         WriteMMscrap(errNum);
         if errNum <> 0 then begin
            if forced then begin
               if doCautionAlert(alertNum) then begin
                  AcceptInheritScrap;
                  PutScrap(ScrapNil, nil, errNum);
                  WMKillPicture(ScrapFolder);

                  {force update to the image in the scrap}
                  SetPort(ScrapFolder);
                  InvalRect(ScrapFolder^.portrect)
               end
               else exit(SaveScrap)
            end
            else if false then doNoteAlert(alertNum)
         end
         else begin
                 if IsCopyCells then InitUTProcs(@LCutCellValue);
                 WriteLCUT(tblCharWid, Tblfntid, errNum);
                 if IsCopyCells then begin
                    InitUTProcs(@CellValue);
                    IsCopyCells := false
                 end;
                 if (errNum <> LCUTAllOK) and (errNum <> LCUTNoData) then begin
                    alertNum := 0;
                    if forced then begin
                       if errNum = LCUTNoHeap then alertNum := 155
                       else alertNum := 182
                    end
                    else if false then alertNum := 150;
                    if alertNum <> 0 then doNoteAlert(alertNum)
                 end
              end;
         dumpToScrap := false
      end
   end;
   SaveScrap := false
end;


{       The handlers for the File/Print menu start here               }
{ see the old LCfexec/MenuCommand code, case MenuDesk. }
(*$S OpenClose *)
procedure DoLCFilePrint(var newCR : boolean);
var item        : integer;
    status      : integer;
    message     : str255;
    titleTab    : str255;
    filerReason : FReason;
    nullPoint   : point;
    putBack     : boolean;

    (*$S OpenClose *)
    procedure doSavePutBack;
    var doTellFiler : boolean;
        filerError  : integer;
        filerReply  : FReply;
        fileOp      : FIOKind;
        sKind       : scrapKind;
    begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

       if saveScrap(true, 153) then exit(doSavePutBack);
       if doingMove then
          if doCautionalert(110) then begin
             doNoteAlert(103);
             ClrFormErrors;
             forceCalc(true)
          end
          else exit(doSavePutBack);
       if not throwAwayDoc then begin
          ParamAlert(titleTab, '', '');
          if docDirty then doWaitAlert(138)
       end;
       CloseFile(docName, true, not putBack, fileOp, filerReason, filerReply, true, putBack);

       EndWaitAlertz;
       doTellFiler := false;
       newCR := (filerReason = allOk);
       if putBack then begin
          if filerReason in [noMemory, allOk] then doTellFiler := true
       end
       else if filerReason = noMemory then begin
               ParamAlert(titleTab, '', '');
               doNoteAlert(132);
               dotellFiler := true
            end;
       if doTellFiler then begin
          if throwAwayDoc then filerReason := badData
          else filerReason := docPutBack;
          TellFiler(filerError, docClosd, filerReason, Folder);
          newCR       := false;
          Folder      := nil;
          NumDocsOpen := NumDocsOpen - 1;
          if g.BGflag then iFxBgFree(BGProcInfo.FxBgFree)
       end
       else matState := mmOpen
    end;


    (*$S OpenClose *)
    procedure doFileRestore;
    var reload       : boolean;
        loadName     : Pathname;
        errNum       : integer;
        FileAttr     : FS_Info;
        whenString   : TParamAlert;
        susFileFound : boolean;
    begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

       reload := findSDoc(true, loadName, FileAttr);
       if loadName <> '' then begin
          doDTAlert(FileAttr.DTM, whenString);
          ParamAlert(titleTab, whenString, '');
          reload := doCautionAlert(111)
       end
       else begin
               ParamAlert(titleTab, '', '');
               reload := doCautionAlert(112)
            end;
       if reload then begin
          if SaveScrap(true, 151) then exit(doFileRestore);
          if loadName <> '' then begin
             ParamAlert(titleTab, whenString, '');
             doWaitAlert(149)
          end;

          InitDisplay(Folder, filerReason, false, true, susFileFound);
          EndWaitAlert;
          FinishInitDisplay;

          { Draw the appropriate scroll bars }
          if g.LCFlag then LCDrawScrolls
          else BGDrawScrolls
       end
    end;


(*$S OpenClose *)
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   {Determine if the document currently being edited}
   GetFldrTitle(Folder, titleTab);
   case g.itemIndex of
      LCSavePutBack,
      LCSaveContinue : begin
                          status := docConsistCheck(item);
                          if status <> ok then begin
                             GetItem(myMenus[MenuBuzz], item, @message);
                             ParamAlert(titleTab, message, '');
                             if not doCautionAlert(135) then begin
                                ParamAlert(message, '', '');
                                doNoteAlert(136)
                             end
                             else begin
                                     status := ok;
                                     nullPoint.h := 0;
                                     nullPoint.v := 0;
                                     endLastSel(nullPoint);
                                     g.pnlWithChg := nullPnl
                                  end;
                          end
                       end;
      otherwise        status := ok
   end;

   filerReason := allOk;
   ParamAlert(titleTab, '', '');
   if status = ok then begin
      case g.ItemIndex of
         LCCloseAll     : DoFilingCmd(cmdClosAll);
         LCCloseOne     : DoFilingCmd(cmdClose);
         LCSavePutBack,
         LCSaveContinue : begin
                             putBack := (g.itemIndex = LCSavePutBack);
                             if docDirty or putBack then doSavePutBack
                             else doNoteAlert(133)
                          end;
         LCPrFormat     : lcprFmt;                               { 'format for printer'  }
         LCPrDialog     : LcPrint(false, ePRnormal) ;            {  print worksheet      }
         LCPrAsIs       : LcPrint(false, ePRDialogSuppress) ;    {  print worksheet without dialog }
         LCPrSelection  : LcPrint(true, ePRDialogSuppress) ;     {  print selection      }
         LCPrProgress   : PrBgdDlg;
         LCFileRestore  : if docDirty then doFileRestore
                          else doNoteAlert(133);
        {otherwise        do nothing}
      end { case }
   end;

   case filerReason of
      docPutBack,
      allOk,
      okButNoMore : ;
      otherwise     if Folder <> nil then begin
                       ParamAlert(titleTab, '', '');
                       case filerReason of
                          cantWrite   : item := 143;
                          noDiskSpace : item := 141;
                          aUserAbort  : item := 144;
                          otherwise     item := 142
                       end;
                       if filerReason = aUserAbort then doNoteAlert(item)
                       else doStopAlert(item)
                    end
   end
end;


{ Dispatches to the individual items in the LC edits menu }
(*$S fexec2 *)
procedure DoMenuEdits(var newCR : boolean);
var status     : Integer;
    txtChg     : boolean;
    continue   : boolean;
    doDialog   : boolean;
    whTblRg    : Range;
    theSelKind : selKind;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   newCR := false;
   case g.ItemIndex of
      UndoItem   : DoUndo;      { in aplc/LCMenus - UNIT TEexec}
      RestOrig   : TErestore;   { in aplc/TEExec - UNIT TEexec }
      LCInsrtCol : if g.lcFlag then PutCWidPrecBox(diInsert, '1')
                   else InsertSnip(1, newCR);
      feCopy,
      Cut,
      PasteM,
      Paster,
      Pastev,
      Clr        : begin
                      continue := true;
                      case g.ItemIndex of
                         feCopy,
                         Cut,
                         Clr : if doingMove then begin
                                  doDialog := true;
                                  if g.itemIndex = Clr then
                                     if (theSelKind = aCellTxtSl) or (g.activePnl <> tblPnl) then doDialog := false;
                                  if doDialog then begin
                                     continue := doCautionAlert(110);
                                     if continue then begin
                                        doNoteAlert(103);
                                        ClrFormErrors;
                                        forceCalc(true)
                                     end
                                  end
                               end
                      end;
                      if continue then begin
                         if TblEditPnl(g.ActivePnl) then TEedits(status, txtchg, newCR)         { in aplc/TEexec - UNIT TEexec}
                         else appEdits(status, txtchg);            { in aplc/LCExecsubs - UNIT LCfexec }
                         {lets remember if the scrap has changed due to a cut/copy of a part of the matrix.
                          This will be used later to avoid writing the scrap data to the public scrap
                          if the scrap has not been changed.
                         }
                         case undoRec.undoOp of
                            uCutRg,
                            ucopyRg : dumpToScrap := true
                         end;

                         { Inform other mgrs in case the text has changed }
                         if txtchg then EditTried(status)          { in aplc/LCExecsubs - UNIT LCfexec }
                      end
                   end;
      Selecall   : if forceEnter(status) = 0 then begin
                      whTblRg.rKind := aWTBoxRg;
                      FixRange(whTblRg);
                      doGoTo(whTblRg);
                      newCR := true
                   end
   end
end;


(*$S ColWidth *)
procedure DoCWidth;
{ changes the width of a set of columns. (See the old LCMenus/DoTblMenu) }
var xNumber   : extended;
    firstCol  : idType;
    initCWid  : resultStr;
    scient    : Boolean;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   ClrUndo;
   if g.curRange.loCol = mInfSnip then GetSnipId(colKind, mmFirst, 0, firstCol, firstCol)
   else firstCol := g.curRange.loCol;
   I2X(SnipWidth(colKind, firstCol) div tblCharWid, xNumber);
   rstr(xNumber, initCWid, 0, 0, 3, scient);
   putCWidPrecBox(diColWid, initCWid)
end;


(*$S showForms *)
procedure doShows(tblStyle : tblMode; var newCR : boolean);
var inPreview : boolean;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   ClrUndo;
   inPreview := (tblStyle = tPreview);
   mmSetWids(tblStyle);

   { reset everything in the table }
   if tblStyle <> tPreview then lcReDoBreaks;
   tmResize;
   tmReDoViews;
   newCR := true;
   if (tblStyle = tPreview) or (g.curRange.rKind = a1CellRg) then begin
      g.CurRange.rKind := aNullRg;
      FixRange(g.curRange);
      if inPreview or (tblStyle = tPreview) then begin
         { Redraw the new scroll bars }
         SetPnlPort(FolderPnl);
         SetLCScroll;
         LCDrawScrolls
      end
   end
   else SelectRg(g.curRange, selectCell)
end;


(*$S LCfexec *)
procedure DoLCCalculate(var newCR : boolean);
var status   : integer;
    continue : boolean;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   case g.ItemIndex of
      FuncHints,
      DoFindCircles,
      DoFindSame : ;
      otherwise    docDirty := true
   end;
   case g.ItemIndex of
      DoFindCircles,
      DoFindSame,
      DoFindDialog,
      DoCircles,
      DoShowForms,
      DoShowVals : continue := (forceEnter(status) = 0);
      otherwise    continue := true
   end;
   if continue then
      case g.ItemIndex of
         CalcMan      : begin
                           g.autoCalc := false;
                           SetReCalc(mmCalcMan)
                        end;
         CalcAuto     : begin
                           g.autoCalc := true;
                           SetReCalc(mmCalcAuto);
                           enterCalc(true, true, false, false, status)
                        end;
         CalcNow      : begin
                           doWaitAlert(131);
                           SetReCalc(mmCalcNow);
                           enterCalc(true, true, false, false, status);
                           if status = ok then begin
                              EndWaitAlert;
                              updFolder(true)
                           end;
                           if g.autoCalc then SetReCalc(mmCalcAuto)
                           else SetReCalc(mmCalcMan)
                        end;
         DoFindSame,
         DoFindCircles: doFinds(newCR);
         DoFindDialog : PutFindBox;
         DoCircles    : PutCircleBox;
         DoShowForms  : doShows(tFormulas, newCR);
         DoShowVals   : doShows(tvalues, newCR);
         FuncHints    : putFuncHints(HintsDiSize)
      end
end;


(*$S BGspecific *)
procedure doBGmenus(var newCR : boolean);
var doit : boolean;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   case g.MenuIndex of
      BGFilePrint  : begin
                        doit := false;
                        case g.itemIndex of
                           2,7,
                           BGCloseAll,
                           BGCloseOne,
                           BGSaveContinue,
                           BGSavePutBack : doit := true;
                           BGForPrntr    : LcPrFmt;      { in aplc/LCPR - UNIT FEglobals}
                           BGPrintTable  : LCPrint(false, ePRnormal);      { in aplc/LCPR - UNIT FEglobals }
                           BGPrintDoc    : if (g.ActivePnl <> cBGPnl) and (g.curRange.rKind <> aNullRg) then LCPrint(false, ePRDialogSuppress)      { in aplc/LCPR - UNIT FEglobals }
                                           else iBGMenuCommand(g.MenuIndex, g.ItemIndex, ePRDialogSuppress, BGProcInfo.BGMenuCommand);
                           BGPrProgress  : PrBgdDlg;
                           BGPrintGraph  : iBGMenuCommand(g.MenuIndex, g.ItemIndex, ePRnormal, BGProcInfo.BGMenuCommand);
                           BGFileRestore : begin
                                              doit := true;
                                              g.itemIndex := LCFileRestore
                                           end;
                           otherwise       doit := false
                        end;
                        if doit then DoLCFilePrint(newCR)
                     end;


      BGEdits      : Begin

         { special code to select the whole table }
         if (g.ItemIndex = SelecTable) then begin
            iBGExit(BGProcInfo.BGExit);
            g.ItemIndex := SelecAll;
            doMenuEdits(newCR);
         end


         else if (g.ActivePnl = BGPnl) or (g.ItemIndex = BGUndoLast)
            or (g.ItemIndex = SelecGraph)  then begin

            {if the menu command is an "undo" and is for LC, "BGmenuCommand"
             will eventually call the LC routine "doUndo" to handle the undo.}
            iBGMenuCommand(g.MenuIndex, g.ItemIndex, ePRnormal, BGProcInfo.BGMenuCommand);
         end

         else begin
            { if needed, translate the item to the item LC expects }
            case g.ItemIndex of
               BGClr      : g.ItemIndex := Clr;
               BGInsrtCol : g.ItemIndex := LCInsrtCol;
               SelecTable : g.ItemIndex := SelecAll
            end;

            { Call the LC handler to do it }
            doMenuEdits(newCR);
         end;
      end;

      { The BGTypeStyle menu gets displayed only when BGPnl is active, so it only handles BG stuff.
        MenuBGLCTypeStyle handles the LC stuff for BG when another panel is active }

      BGTypeStyle  : iBGMenuCommand(g.MenuIndex, g.ItemIndex, ePRnormal, BGProcInfo.BGMenuCommand);


      BGFormat     : if g.ActivePnl = BGPnl then iBGMenuCommand(g.MenuIndex, g.ItemIndex,ePRnormal,  BGProcInfo.BGMenuCommand)
                     else case g.ItemIndex of
                             BGStndrd,
                             BGLeft,
                             BGCenter,
                             BGRight : NewAlignProt(aAlign);    { in aplc/LCMenus2 - UNIT TEexec }

                             otherwise begin
                                          { BG omits menu item Repeat }
                                          g.ItemIndex := g.ItemIndex + 1;
                                          NewFormat;  { in aplc/LCMenus2 - UNIT TEexec }
                                       end
                          end;

         MenuBGType   : iBGMenuCommand(g.MenuIndex, g.ItemIndex,ePRnormal,  BGProcInfo.BGMenuCommand);

         MenuBGOptions: iBGMenuCommand(g.MenuIndex, g.ItemIndex, ePRnormal, BGProcInfo.BGMenuCommand);
      MenuBGPageLayout : if g.ItemIndex = 1 then doCWidth
                         else iBGMenuCommand(g.MenuIndex, g.ItemIndex, ePRnormal, BGProcInfo.BGMenuCommand)
   end
end;


(***************************************.*******.************************************
   MenuCommand - responds to the selection of a menu item (for both LC/BG menus) by
                 calling the corresponding routines.  The filer manages all of the
                 menu items in the first menu and the 4 of the items in the second
                 menu.  The other menu items are handled by LisaCalc/Business Graphics
                 routines.  The information about which menu & item were selected
                 is found in g.MenuIndex & g.ItemIndex.
*)
(*$S fexec2 *)
procedure MenuCommand;
var newCR      : boolean;
    response   : diResponse;
    alertIndex : integer;
    status     : integer;
    startTime  : longint;
    field      : attrType;

    (*$S pBreakCode *)
    procedure doPageBreaks;
    var x          : integer;
        foundBreak : boolean;
        breakRG    : range;
    begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

       ClrUndo;
       mmSetBreak(true, g.itemIndex = doSetBreaks, g.curRange);
       setUndo(uBreaks, tblPnl);
       lcReDoBreaks;
       tmShoBreaks;
       breakRG := g.curRange;
       foundBreak := (breakRG.rkind = aWTBoxRg);
       case breakRG.rkind of
          aColRg,
          aColGrid : begin
                        if breakRG.rKind = aColGrid then
                           breakRG.loCol := breakRG.hiCol;
                        for x := breakRG.loCol to breakRG.hiCol do
                           foundBreak := foundBreak or mmBreakSnip(colKind, x)
                     end;
          aRowRg,
          aRowGrid : begin
                        if breakRG.rKind = aRowGrid then
                           breakRG.loRow := breakRG.hiROw;
                        for x := breakRG.loRow to breakRG.hiRow do
                           foundBreak := foundBreak or mmBreakSnip(rowKind, x)
                     end
       end;
       if foundBreak and (g.itemIndex = DoClrBreaks) then doNoteAlert(157)
    end;


(*$S fexec2 *)
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   startTime := time;

   { tell the alertmgr what message to display when we need one }
   case g.MenuIndex of
      MenuStat : WouldAlert(myMenus[MenuStat], g.MenuIndex);
      otherwise  begin
                    if (g.MenuIndex >= BGFilePrint) and (g.MenuIndex <= MenuBGOptions) then
                       alertIndex := g.MenuIndex - BGFilePrint + LCFilePrint
                    else alertIndex := g.MenuIndex;
                    WouldAlert(myMenus[MenuSummary], alertIndex - 1)
                 end
   end;

   newCR    := false;
   response := OkAction;
   if curDiHeight > 0 then disMissDi(g.MenuIndex, g.ItemIndex, response);
   if response <> IgnoreAction then begin
      case g.MenuIndex of
         BGFilePrint,
         BGEdits,
         BGTypeStyle,
         MenuBGPageLayout,
         BGFormat,
         MenuBGOptions,
         MenuBGtype   : doBGmenus(newCR);

         { MenuBGLCTypeStyle gets displayed in BG when the tblpnl is active, so it handles all the
           LC typestyle commands for BG }

         MenuBGLCTypeStyle: Begin
                               { translate it into LC menu terms and call the LC handler }
                               g.ItemIndex := g.ItemIndex - 7;
                               fontCommand(false, NewCR);
                            end;

         LCFilePrint  : DoLCFilePrint(newCR);
         MenuEdits    : DoMenuEdits(newCR);
         LCTypeStyle  : fontCommand (false, newCR);
         LCPageLayout : case g.ItemIndex of
                           CWidth      : DoCWidth;
                           DoPreview   : if tableState <> tPreview then
                                            if forceEnter(status) = 0 then begin
                                               lastTblStyle := tableState;
                                               doShows(tPreview, newCR)
                                            end;
                           DoNoPreview : if tableState = tPreview then doShows(lastTblStyle, newCR);
                           DoClrBreaks,
                           DoSetBreaks : doPageBreak;
                           DocStatus   : if forceEnter(status) = 0 then PutStatusBox;
                           DocFPrint   : if forceEnter(status) = 0 then PutPrintBox
                        end;
         LCFormat     : case g.ItemIndex of
                           AlStandard,
                           AlLeft,
                           AlCenter,
                           AlRight,
                           AlRepeat : NewAlignProt(aAlign);    { in aplc/LCMenus2 - UNIT TEexec }
                           otherwise  NewFormat
                        end;
         MenuProtect  : begin
                           case g.ItemIndex of
                              alVisible,
                              alInvisible : field := aAlign;
                              otherwise     field := aProtect
                           end;
                           NewAlignProt(field)
                        end;
         LCCalculate  : DoLCCalculate(newCR);
         { MenuStat does the apple-F to select the formula, and the apple-G to goto a new cell }
         MenuStat     : case g.ItemIndex of
                           SelForm  : if g.LCFlag then selecForm(newCR);
                           SelCoord : selecCoord(newCR)
                        end;
         MenuScrap    : case g.ItemIndex OF
                           SCloseAll : DoFilingCmd(cmdClosAll);
                           SCloseOne : DoFilingCmd(cmdClose);
                           SMonitor  : PrBgdDlg;
                        end;
(*$IFC fdebug *)
         MenuTrace    : FlipTrace;      { in aplc/LCMenus2 - UNIT TEexec}
         MenuRod      : rodFlags;
         otherwise Writeln('MenuCommand has an illegal MenuIndex')
(*$ENDC *)
      end;
   end;

   if (Folder <> nil) or (ActiveFolder = ScrapFolder) then begin
      if (ActiveFolder <> ScrapFolder) and newCR then chgStatPnl(false);
      repeat until time > StartTime + 20;
      HiLiteMenu(0)
   end
end;


ÿ