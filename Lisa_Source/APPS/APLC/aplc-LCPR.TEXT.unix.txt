
                            {'Copyright 1983, 1984, Apple Computer Inc.'}


(*$IFC FDEBUG *)
    function wRg
      (rg : range
      ) : char; external;
    function  wSNIP
      (value : snipKind
      ) : char; external;
   function  RodStuff
      (menuItem: integer
      ): boolean; external;

procedure dumpbreaks;
var y : integer;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   writeln('PageBreaks for rows (1-30)');
   for y := 1 to 30 do begin
      if mmBreakSnip(rowKind, y) then write('T ')
      else write('F ');
      if y mod 10 = 0 then write('  ');
   end;
   writeln;
   writeln('PageBreaks for cols (1-30)');
   for y := 1 to 30 do begin
      if mmBreakSnip(colKind, y) then write('T ')
      else write('F ');
      if y mod 10 = 0 then write('  ');
   end;
   writeln;
end;
(*$ENDC *)


(*********** aplc/lcpr - lisacalc printing code - unit feglobals **************)

{     This unit handles page-break calculations and all printing-related issues, as well as
      printing itself, for LisaCalc and for the table portion of Business Graphics.


Note on tracing:
   If TraceLCPR is set then the exhaustive trace information on ReDoBreaks is still
      not printed out unless debugging switch 4 is set }


{ Switches[4], in conjunction with TraceLCPR, establishes break-tracing }

{--------------------------------->>>>>>>>>>>>>>> + <<<<<<<<<<<<<<<---------------------------------}

{$S LPr }
function FamFromIndex { index:  integer ) :  TFam  } ;
   { given an index into font menu, produces the corresonding font family }
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   (* if traceLCPR then writeln('lcpr.famFromIndex: index=',index:1);   *)
   case index of
      FC10: FamFromIndex := p10cent;
      FC12: FamFromIndex := p12cent;
      FT10: FamFromIndex := p10tile;
      FT12: FamFromIndex := p12tile;
      FT15: FamFromIndex := p15tile;
      FT20: FamFromIndex := p20tile
   end {case};
end ; { FamFromIndex }

{--------------------------------->>>>>>>>>>>>>>> + <<<<<<<<<<<<<<<---------------------------------}

{$S LPr}
function FinalDataSnip { aKind: snipKind ) :  idtype };

{ returns the ID of the final snip of the requested kind which has actual contents }
{    returns MInfSnip if entire table is empty                                     }

var SearchIsAtEnd : boolean;
    isBlank       : boolean;
    TrialSnip     : idtype;
    FirstSnip     : idtype;
    FirstOrth     : idtype;
    LastOrth      : idtype;
    JOrth         : idtype;

    {$S LPr}
    procedure GetLastSnip(snip : snipKind; var theFirstSnip, theLastSnip : idType);
    begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

       GetSnipId(snip, mmFirst, 0, mInfSnip, theFirstSnip);             { snip # 1 of requested type }
       if printSelection then begin
          theLastSnip := printRange.hiCol - printRange.loCol + 1;
          if snip = rowKind then theLastSnip := printRange.hiRow - printRange.loRow + 1
       end
       else theLastSnip := FinalSnip(snip)                              { snip proposed as fn value  }
    end;


begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   GetLastSnip(aKind, FirstSnip, TrialSnip);
   GetLastSnip(oppKind[aKind], FirstOrth, LastOrth);

   SearchIsAtEnd := false;

(*$IFC fDebug *)
   if traceLCpr then begin
      writeln('firstSnip, trialSnip: ',firstSnip:1,',',trialSnip:1);
      writeln('FirstOrth, LastOrth: ',FirstOrth:1,',',LastOrth:1);
      writeln('FinalDataSnip: printRange: ',wrg(printRange))
   end;
(*$ENDC *)

   if (TrialSnip > 0) and (LastOrth > 0) then
      repeat
         jOrth := FirstOrth;
         repeat
            if aKind = rowKind then isBlank := CellBlank(TrialSnip + printRange.loRow - 1, jOrth + printRange.loCol - 1)
            else isBlank := CellBlank(jOrth + printRange.loRow - 1, TrialSnip + printRange.loCol - 1);
            If not isBlank then SearchIsAtEnd := true
            else jOrth := jOrth + 1
         until SearchIsAtEnd or (jOrth > LastOrth);

         if not SearchIsAtEnd then TrialSnip := TrialSnip - 1    { eliminate this snip }
      until (TrialSnip = 0) or SearchIsAtEnd;

  FinalDataSnip := TrialSnip;
  if TrialSnip <= 0 then FinalDataSnip := mInfSnip

(*$IFC fDebug *)
  ;if traceLCpr then writeln('FinalDataSnip: snip: ',wSnip(akind),'  trialSnip: ',trialSnip:1);
(*$ENDC *)
end; { FinalDataSnip }

{--------------------------------->>>>>>>>>>>>>>> + <<<<<<<<<<<<<<<---------------------------------}

{$S LPr }
procedure LcPrFmt;
  { called from FolderExec when user requests 'format for printing'; calls the
    print manager's print-preference dialogue, and if  necessary, integrates
    the style-change into the matrix and the table }
var StyleChanged : boolean;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$ifc fDebug}
   if TraceLcPr then lcTraceMess(236) ;
{$endc }

   PrPrfDlg(CurConfig, StyleChanged, true);
   if StyleChanged then begin
      resolveFont;
      if g.BGFlag then LCBGReconfig        { call BG proc to tell it about change in style }
   end
end; {LcPrFmt}

{--------------------------------->>>>>>>>>>>>>>> + <<<<<<<<<<<<<<<---------------------------------}

{$S initial  }
procedure LcPrInit { VAR initErr:  TC }  ;
  { initializes the printing unit -- call once only for the process }
var cItem : integer;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$ifc fDebug}
   if traceLCPR or traceOC then LCTraceMess ( 231 ) ;

   traceBreaks := false;
{$endc }

   PrMgrInit;

   { now set up unchanging variables once and for all }

   sKindOfAxis[v] := rowKind;
   sKindOfAxis[h] := colKind;

   axisOfSKind[rowKind] := v;
   axisOfSKind[colKind] := h;

  {set (arbitrary) margins}
   pgChLeadMargin[h]  := 2;   { left   }
   pgChTrailMargin[h] := 2;   { right  }
   pgChLeadMargin[v]  := 1;   { top    }
   pgChTrailMargin[v] := 1;   { bottom }

   nHedLns[v] := 4;         {header, preceding first row}
   nHedLns[h] := 0;         {no "heading" on left}

   GetItem(MyMenus[MenuBuzz], PageWord, @PageText);    { the word 'Page' }
   PageText := concat(PageText, ' ');                  { get the word "page" into variable PageText }

   printSelection   := false;
   printRange.rKind := a1CellRg;
   printRange.loCol := 1;
   printRange.loRow := 1;
   printRange.hiCol := 1;
   printRange.hiRow := 1
end; { one-time initialization }


{--------------------------------->>>>>>>>>>>>>>> + <<<<<<<<<<<<<<<---------------------------------}


{$S LPr }
procedure lcPrint{selOnly : boolean; thePRmode : PrMenuSuppress} ;
        { call "print instance" dialogue, and then print table or selection via printWS }

var IndeedPrint : boolean;
    IsNewStyle  : boolean;
    myKeys      : KeySet;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$ifc fDebug}
   if traceLcPr then lcTraceMess ( 238 ) ;
{$endc }

   GetKeys(myKeys);
   checkErrors := false;
   if g.LCflag then
      if (78 in myKeys) or (124 in myKeys) then
         if 126 in mykeys then checkErrors := doCautionAlert(189);

   IndeedPrint := FPrInsDlg(CurConfig, IsNewStyle,  thePrMode);

   printSelection := selOnly;
   if IsNewStyle then begin                { style changed during print-instance dialogue }
      resolveFont;
      if g.BGFlag then LCBGReconfig        { let BG know so it can refigure graph }
   end;

   if indeedPrint then begin
      UpdFolder(true);     {refresh the screen before printing such that a white area from the
                            dialog box is not left}
      {redo the page breaks for print selection.  With the global `printSelection` set to true,
       all user set page breaks will disappear leaving only the automatic ones.
      }
      if printSelection then begin
         printRange := g.CurRange;
         expandRange(printRange, false);
{$ifc fDebug}
         if traceLcPr then writeln('lcprint: printRange: ',wRg(printRange));
{$endc }
         LcReDoBreaks
      end;
      printWS;
   end;

   if printSelection then begin
      printSelection := false;
      printRange.rKind := a1CellRg;
      printRange.loCol := 1;
      printRange.loRow := 1;
      printRange.hiCol := 1;
      printRange.hiRow := 1;
      {redo the page breaks for print selection.  With the global `printSelection` set to false,
       all user set page breaks will reappear abd the automatic ones will be recomuted.
      }
      if indeedPrint then LcReDoBreaks
   end

end;  { lcprint }

{--------------------------------->>>>>>>>>>>>>>> + <<<<<<<<<<<<<<<---------------------------------}

{$S pBreakCode }
procedure LCReDoBreaks;
        {recompute all default page breaks}
var x        : integer;
    axisLoop : axis;
    jAxis    : axis;            { axis iterator corresponding to jKind }
    jKind    : snipKind;        { iterator }
    pageFull : boolean;
    pState   : boolean;
    jFstTbl  : idType;
    jLimTbl  : idType;
    CurSnip  : idType;
    curRg    : range;
    myFInfo  : fontInfo;
    myFntId  : TLFntId;
    fError   : integer;
    fontOk   : boolean;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$ifc fDebug}
   if traceLcPr then lcTraceMess(239) ;
   traceBreaks := traceLCPR AND debug[1] ;   { see note above re this flag }
{$endc }

   ChWall[v] := 0;
   chWall[h] := ord(printState.pGrids);     { 1 character between matrix columns if showing grids }

  {set screen-related parameters }
   with TblFInfo do scrChD[v] := ascent + descent + Leading;
   scrChD[h] := TblFinfo.widmax;
   PrLfntid(CurConFig, FamFromIndex(g.curPrFont), [], myFntId);
   fontOk := FMFontMetrics(myFntId, myFinfo, ferror);

   with myFinfo do begin
      pseudoLeading := (ascent + descent + leading) div 6;              {ed's heuristic, modified}
      pseudoLeading := pseudoLeading + ord(odd(pseudoLeading));
      if pseudoLeading < leading then pseudoLeading := leading;         {bulletproofing}
      ttyChD[v] := ascent + descent + pseudoLeading;                    { height of printer char, in pixels }
      ttyChD[h] := widmax                                               { width of printer character, in pixels }
   end;

   with curConfig.prinfo.rPrintable do begin
      ttyPgD[v] := bottom - top;                { height of printable printer page, in pixels }
      ttyPgD[h] := right - left                 { width of printable printer page, in pixels }
   end;

   for x := 1 to 2 do begin
      axisLoop := h;
      if x = 1 then axisLoop := v;

      { h = number of pixels padding between adjacent matrix columns }
      { v = number of pixels padding between adjacent matrix rows (should equal 0) }
      wallD[axisLoop] := chWall[axisLoop] * ttyChD[axisLoop];

      pgChLim[axisLoop] := (ttyPgD[axisLoop] div ttyChD[axisLoop]) - pgChLeadMargin[axisLoop] - pgChTrailMargin[axisLoop]
   end;

  {determine lower limit of snips of interest}
   getSnipId(colKind, mmFirst, 0, mInfSnip, fstTbl[colKind]);
   getSnipId(rowKind, mmFirst, 0, mInfSnip, fstTbl[rowKind]);

  {$ifc fDebug}
   if TraceLCPR then begin
      writeln('pr: printer params:');
      writeln('  chDh,chDv,pgDh,pgDv,charsH,charsV,resH,resV,wallDh,wallDv');
      writeln(' ',ttyChD[h]:5,ttyChD[v]:5, ttyPgD[h]:5,ttyPgD[v]:5,
                  pgChLim[h]:7,pgChLim[v]:7,curConfig.prinfo.hRes:5,curConfig.prinfo.vRes:5,wallD[h]:7, wallD[v]:7);
      writeln('setPgBrks: printRange: ',wrg(printRange));
   end;
{$endc }

   pState := printState.preview;
   printState.preview := false;
 {determine upper limit of snips of interest}
   if pState then begin
      limTbl[colKind] := printRange.hiCol - printRange.loCol + 1;
      limTbl[rowKind] := printRange.hiRow - printRange.loRow + 1
   end
   else begin
           getSnipId(colKind, mmLast, 0, mInfSnip, limTbl[colKind]);
           getSnipId(rowKind, mmLast, 0, mInfSnip, limTbl[rowKind])
        end;

  {clear current automatic breaks: row grids, then col grids}
   setRg(curRg, aRowRg, fstTbl[rowKind] , mInfSnip, limTbl[rowKind] , pInfSnip);
   mmSetBreak(False, False, curRg);

   setRg(curRg, aColRg, mInfSnip, fstTbl[colKind] , pInfSnip, limTbl[colKind]);
   mmSetBreak(False, False, curRg);

   for jKind := rowKind to colKind do  begin
      jAxis := axisOfSKind[jKind];

      getSnipId(jKind, mmFirst, 0, mInfSnip, jFstTbl);  {set jFstTbl to first snip, i.e. to '1'}
      jLimTbl := pInfSnip;                              {set jLimTbl to 1 past final snip }

      fstPg[jKind]:= jFstTbl;    { to start out the loop, the first snip on the current page is the
                                   first snip in the table }
{$IFC fDebug}
      if TraceBreaks then begin
         writeln('   starting pass: ',ord(jKind));
         writeln('   ','ttyChPos  fstPg  CurSnip  pgChLim  break')
      end;
{$ENDC }

      while fstPg[jKind] <> jLimTbl do begin { process another page in the current direction }
         ttyChPos[jAxis] := pgChLeadMargin[jAxis] + nHedLns[jAxis]; { top or left margin }
         pageFull := False;
         if printState.pColHeadings then CurSnip := mInfSnip                  { if printing headings, start at the heading snip }
         else CurSnip := fstPg[jKind];             { ... otherwise start at the first data snip }

 {$IFC fDebug}
         if TraceBreaks then
            writeln('   ',ttyChPos[jAxis],fstPg[jKind],CurSnip,pgChLim[jAxis], mmBreakSnip(jKind, CurSnip));
 {$ENDC }

         while (CurSnip <> jLimTbl) and (not pageFull) do begin
            cellChars[jAxis] := snipChars(CurSnip, jAxis);
            if ( (ttyChPos[jAxis] + cellChars[jAxis] + chWall[jAxis]) < pgChLim[jAxis] )
                                           OR
                (curSnip = mInfSnip)  or         (curSnip = fstPg[jKind])
                    { a heading       OR    the first data snip for this page }

         { NB: we force heading & first data snip to live on current page, whether they
           appear to fit or not -- THIS IS BULLETPROOFING against a variety of pernicious
                                   LOOPS that can arise from bad print metrics OR
                                   bad Data from Font manager }
           then begin
                   CurSnip := succSnip(jKind, CurSnip); {move to next snip}
                   if CurSnip <> fstPg[jKind] then       { ... if this new snip has a break, }
                      pageFull := mmBreakSnip(jKind, CurSnip);         {then break BEFORE it }
                   ttyChPos[jAxis] := ttyChPos[jAxis] + cellChars[jAxis] + chWall[jAxis]
                end {that snip fit; its successor now in CurSnip, which may or may not also
                     fit on the current page, depending on the setting of PageFull }

           else pageFull := True  {there's not room, force a break}

        end; { one page full}

{$ifc fDebug}
        if TraceBreaks then
           writeln('***',ttyChPos[jAxis],fstPg[jKind],CurSnip,pgChLim[jAxis], mmBreakSnip(jKind, CurSnip));
{$endc }

         { if CurSnip does not already have a user-specified page break, then give it
           an 'automatic' one }

        if CurSnip <> jLimTbl then
           if not mmBreakSnip(jKind, CurSnip) then begin
{$ifc fDebug}
              if TraceBreaks then writeln('setPgBrks:  mmSetBreak: ',wSnip(jKind), 'CurSnip: ',CurSnip:1);
{$endc }
              if jKind = rowKind then setRg(curRg, aRowRg, CurSnip, mInfSnip, CurSnip, pInfSnip)
              else setRg(curRg, aColRg, mInfSnip , CurSnip, pInfSnip , CurSnip );
              mmSetBreak(False, True, curRg)
          end;
          fstPg[jKind] := CurSnip;
       end     {jKind breaks now all correct}
    end;    {rowKind and colKind both handled}
    printState.preview := pState;
{$ifc fDebug}
    if traceLcpr then dumpbreaks;
{$endc}

end;

{--------------------------------->>>>>>>>>>>>>>> + <<<<<<<<<<<<<<<---------------------------------}

{$S LPr }
procedure printCell;
        {the printer is postioned at ttyChPos[], and we can use (of the page) cellChars[h/v] origined there;
         print the cell: (CurRow, CurCol); restore ttyChPos[v] on exit, but advance ttyChPos[h] past wall[h],
         aligning for the next cell;}
var  cellLen    : integer;
     status     : integer;
     preSpace   : integer;
     savVPos    : integer;
     i          : integer;
     dRow, dCol : integer;
     tmpRect    : rect;
     cellStr    : array[0..maxString] of byte;
     cInfo      : CellRecord;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

  {init}

   savVPos := ttyChPos[v];
   if (CurRow <> mInfSnip) or (CurCol <> mInfSnip) then begin

     {print the formula and value}

     {first print any required rules (i.e., grid lines)}
      if printState.pGrids then begin
         with ttyCRect do begin
            penNormal;
            moveTo(right, top);                 { start at top-right }
            Line(0, bottom - top) ;               { draw right-hand vertical rule }
            Line(-(right - left + 1), 0);         { draw bottom horizontal rule   }

            if (((curRow = mInfSnip) or not printState.pColHeadings) and (curCol = fstPg[colKind]))      { column-A heading  }
                                 OR                                     {     or           }
                        (curCol = mInfSnip)                             { any row heading }
            then Line(0, -(bottom - top + 1));                            { draw left-hand rule, bottom-to-top }

            if (((curCol = mInfSnip) or not printState.pColHeadings) and (CurRow = fstPg[rowKind]))      {row-1 heading}
                                     OR                                 {     or     }
                          (curRow = mInfSnip)                           { any column heading }
            then begin
                    moveTo(left, top - 1);
                    Line(right - left, 0)      { draw top rule }
                 end

         end;
         setTTY    {put tty back at cell origin}
      end;

     dRow := 0;
     dCol := 0;
     if CurRow <> mInfSnip then dRow := printRange.loRow - 1;
     if CurCol <> mInfSnip then dCol := printRange.loCol - 1;

     {next print formula if appropriate}
     if tblPars.shoFormulas and (CurRow <> mInfSnip) and (CurCol <> mInfSnip) then begin
        cellLen := maxString;
        FoldedFormula(CurRow + dRow, CurCol + dCol, @cellStr, cellLen);
        for i := 0 to cellLen-1 do
           if cellStr[i] = 13 then begin
              ttyChPos[v] := ttyChPos[v] + 1;
              setTTY
           end
           else DrawChar(chr(cellStr[i]));
        setTTY         {put tty back at cell origin}
     end;              {print formula}

     {print value}
      cellLen := cellChars[h];
      cellValue(mmRead, CurRow + dRow, CurCol + dCol, @cellStr, False, cellLen, status);
      if checkErrors then
         if isErrorCell then cellLen := 0;

      cellInfo(CRecord, mmRead, CurRow + dRow, CurCol + dCol, cInfo);
      preSpace := cellChars[h] - cellLen;
      case cInfo.align of
         left   : preSpace := 0;
         center : preSpace := preSpace div 2;
        {right  : preSpace := cellChars[h] - cellLen;}
      end;

      while preSpace > 0 do begin
         drawChar (' ');
         preSpace := preSpace - 1
      end;

      DrawText(@cellStr, 0, cellLen);

      if cInfo.hilite then begin        {circle the cell}
         tmpRect := ttyCRect;  {use of tmpRect allows future (as well as past) tuning of the rectangle... }
         PenNormal;
         if printState.pGrids then PenSize( 2, 2 ); {use a heavier line if printing grids also}
         FrameRoundRect(tmpRect, 15, 10)
      end

   end; {print value and formula}

   ttyChPos[v] := savVPos
end;

{--------------------------------->>>>>>>>>>>>>>> + <<<<<<<<<<<<<<<---------------------------------}

{$S LPr }
procedure printHed;
        {printer is positioned at page topleft: print nHedLns[v] of header,
        leaving printer positioned at 1+nHedLns[h & v]

        }
 var digTxt   : resultStr;
     scient   : boolean;
     xNumber  : extended;
     title    : str255;
 begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$ifc fDebug}
    if traceLcPr then lcTraceMess ( 240 ) ;
{$endc }
    GetFldrTitle(Folder, title);   { Set up folder name for use in title }

    DrawText(@Title, 1, cMin(length(title), pgChLim[h] - length(PageText) - 4 ));    { truncate folder title so it will fit }

    ttyChPos[h] := pgChLim[h] - length(PageText) - 3 ;
    setTTy;

   { print the word 'page' }
    DrawText(@PageText, 1, length(PageText) );

   { print page number, if applicable }
    I2X(pageNumber, xNumber);
    rstr(xNumber, digTxt, 0, 0, 3, scient);
    DrawText(@digTxt, 1, length(digTxt) );

    ttyChPos[h] := pgChLeadMargin[h]  + nHedLns[h];
    ttyChPos[v] := pgChLeadMargin[v]  + nHedLns[v];
    setTTY
 end;

{--------------------------------->>>>>>>>>>>>>>> + <<<<<<<<<<<<<<<---------------------------------}

{$S LPr }
procedure printPage;
        {The printer is physically positioned to accept a new page;
         print a page whose upper left hand cell is fstPg[row/colKind];
         when done, leave CurRow and CurCol at first found page break}
var pageFull : boolean;
    myFinfo  : FontInfo;
    myFntId  : TLfntId;
    fontOk   : boolean;
    fError   : integer;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$ifc fDebug}
   if traceLcPr then
      writeLn('printPage; on entry, fstPg = [',fstPg[rowKind]:4,',',fstPg[colKind]:4,']');
{$endc }
   ttyChPos[h] := pgChLeadMargin[h];      { set pen to upper-left corner of page, }
   ttyChPos[v] := pgChLeadMargin[v];      { inset by top and left margins...      }
   setTTY;

   if printState.pPageHeader then printHed;                      { print page headings }
   pageFull := False;
   if printState.pColHeadings then CurRow := mInfSnip                  { print row headings if showing headings }
   else CurRow := fstPg[rowKind];
   PrLfntid(CurConFig, FamFromIndex(g.curPrFont), [], myFntId);
   fontOk := FMFontMetrics(myFntId, myFinfo, ferror);

   while (CurRow <> limTbl[rowKind]) and not pageFull do begin
      cellChars[v]    := snipChars(CurRow, v);
      cellD[v]        := cellChars[v] * ttyChD[v];
      ttyCRect.top    := curPxy[v] - myFinfo.ascent - ( pseudoLeading div 2 ) + 1 ;
      ttyCRect.bottom := ttyCRect.top + cellD[v] - 1 ;
      printRow;
      ttyChPos[v] := ttyChPos[v] + cellChars[v];
      setTTY;
      CurRow := succSnip(rowKind, CurRow);
      if CurRow <> fstPg[rowKind] then
         if mmBreakSnip(rowKind, CurRow) then begin
{$ifc fDebug}
               if traceBreaks then writeln('   ',ord(rowKind),'- break found at:',CurRow);
{$endc}
            pageFull := True;
         end {break found}
   end { Processing of a Row }
{$ifc fDebug}
   ; if traceLCPR then writeln('printPage; on exit, curRow/curCol= [',curRow:1,',',curCol:1,']')
{$endc}
end;  { PrintPage }

{--------------------------------->>>>>>>>>>>>>>> + <<<<<<<<<<<<<<<---------------------------------}

{$S LPr }
procedure printRow;
        {the printer is positioned at the beginning of a blank line;
         there is room for cellChars[v] (the height of CurRow) on the
         current page; print part of the row CurRow:
            start with fstPg[colKind] and print up to the next break snip (but
            not limTbl[colKind]); leave CurCol at what we just missed.
         Leave ttyChPos at left of next available line;}
var  lineFull : boolean;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   if printState.pColHeadings then CurCol := mInfSnip
   else CurCol := fstPg[colKind];

   lineFull := False;

   while (CurCol <> limTbl[colKind]) and (not lineFull) do begin
      cellChars[h] := snipChars(CurCol, h);
      cellD[h] := cellChars[h]*ttyChD[h];
      ttyCRect.left := curPxy[h] - (wallD[h] div 2) +1  ;
      ttyCRect.right := ttyCRect.left + cellD[h] + wallD[h] -1;
      printCell;
      ttyChPos[h] := ttyChPos[h] + cellChars[h] + chWall[h];
      setTTY;
      CurCol := succSnip(colKind, CurCol);
      if CurCol <> fstPg[colKind] then
            if mmBreakSnip(colKind, CurCol) then begin
{$ifc fDebug}
         if traceBreaks then
            writeln('   ','col-break found at:',CurCol);
{$endc}
         lineFull := True;
      end
   end;

   ttyChPos[h] := pgChLeadMargin[h] + nHedLns[h];
end   { printRow } ;

{--------------------------------->>>>>>>>>>>>>>> + <<<<<<<<<<<<<<<---------------------------------}
{$S LPr}
Procedure PrintWS;
label 1;
var lastCol   : idType;
    thePrPort : TPrPort;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$ifc fDebug}
   if traceLcpr then lcTraceMess(242);
{$endc }

  {determine upper limit of snips of interest}
   lastCol := finalDataSnip(colKind);
   if lastCol = mInfSnip then begin { if entire table is empty, put up alert and exit }
      doStopAlert(190);
      exit(PrintWS)
   end;

  {-------non-Empty table-----}
   limTbl[colKind] := succSnip(colKind, lastCol);
   limTbl[rowKind] := succSnip(rowKind, finalDataSnip(rowKind));
{$ifc fDebug}
   if traceLCPR then writeln('PrintWS: limCol=',limTbl[colKind]:4,'  limRow=',limTbl[rowKind]:4);
   if traceLCPR then writeln('Calling PrDocStart');
{$endc}

   if PrDocStart(CurConfig, thePrPort, printldsn, true) then begin
      PageNumber := 0;
      fstPg[colKind]:= fstTbl[colKind];

      while fstPg[colKind] <> limTbl [colKind] do begin   { print next vertical strip of pages }
         fstPg[rowKind] := fstTbl[rowKind];
         while fstPg[rowKind] <> limTbl[rowKind] do begin  { print next page in current vertical strip }
            PageNumber := PageNumber + 1 ;
{$ifc fDebug}
            if traceLCPR then writeln('calling PrStartPage, for page #',PageNumber:1);
{$endc}
            if PrPageStart(curConfig, thePrPort) then begin
               TextFont(FamFromIndex(g.curPrFont));
               PrintPage
            end
            else begin
                    curRow := succSnip(rowKind, fstpg[rowKind]);
                    while (curRow <> limTbl[rowKind]) and (not mmBreakSnip(rowKind, curRow)) do curRow := succSnip(rowKind, curRow);
                    curCol := succSnip(colKind, fstpg[colKind]);
                    while (curCol <> limTbl[colKind]) and (not mmBreakSnip(colKind, curCol)) do curCol := succSnip(colKind, curCol);
                 end; { current page in current vertical strip }

            if PrPageEnd(curConfig, thePrPort) then goto 1;
            fstPg[rowKind] := curRow
         end;
         fstPg[colKind] := CurCol
      end
   end;  { ready for next vertical strip of pages }

1: PrDocEnd(curConfig, thePrPort);
   UpdFolder(true)     {refresh the screen before printing such that a white area from the dialog box is not left}

end;

{--------------------------------->>>>>>>>>>>>>>> + <<<<<<<<<<<<<<<---------------------------------}

{$S LPr }
procedure ResolveFont;

{ This procedure resolves the issues arising from possible mismatch between the printer-preference
  and the user's requested table font.  It is called:
       (a) when the the PrintPref has just changed ( prefChanged = true )
       (b) when the user's requested table font has just changed ( prefChanged = False )
  On entry, g.CurPrFont is assumed to have been set to the current or new index in the font menu.

  This procedure will assure that the font metrics for the printer font are read in, so that they
  need not be worried about in each call to lcredoBreaks
}
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$ifc fDebug}
   if traceLCPR then LCTraceMess(232);
{$endc }

   docDirty   := true;
   if g.TabFontMode = FMPrinter then ChgTblFont(FamFromIndex(g.CurPrFont));  {++aplc/LCMenus3++}
   lcredobreaks;    { recompute breaks  }
   tmShoBreaks       { ... and show them }
end;

{--------------------------------->>>>>>>>>>>>>>> + <<<<<<<<<<<<<<<---------------------------------}

{$S LPr }
procedure setTTY;
        {set the physical printer to the current values of ttyChPos}
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   curPxy[h]:= curConfig.prinfo.rPrintable.Left + ttyChPos[h] * ttyChD[h];
   curPxy[v]:= curConfig.prinfo.rPrintable.Top + (1 + ttyChPos[v]) * ttyChD[v];
   MoveTo(curPxy[h], curPxy[v])
end;


{--------------------------------->>>>>>>>>>>>>>> + <<<<<<<<<<<<<<<---------------------------------}

{$S fexec }
function  snipChars {(aSnip: idType; u: axis): integer};
        {how many characters (in u direction) fit in aSnip on the screen?}
var aDisp : integer;
    snip  : snipKind;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   snip  := sKindOfAxis[u];
   aDisp := 0;
   if aSnip <> mInfSnip then
      if snip = rowKind then aDisp :=  printRange.loRow - 1
      else aDisp := printRange.loCol - 1;
   snipChars := (snipWidth(snip, aSnip + aDisp) + 2) div scrChD[u]
end;

{--------------------------------->>>>>>>>>>>>>>> + <<<<<<<<<<<<<<<---------------------------------}

{$S fexec }
function  succSnip {(aKind: snipKind; aSnip: idType): idType};
        {return the successor snip of aSnip: "current" first snip follows any title}
var bSnip : idType;
    bDisp : integer;

    (*$S showForms *)
    procedure doPreview;
    begin
       bDisp := printRange.hiRow - printRange.loRow + 1;
       if aKind = colKind then bDisp := printRange.hiCol - printRange.loCol + 1;
       if bSnip > bDisp then bSnip := pInfSnip
    end;

(*$S fexec *)
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   if aSnip = mInfSnip then bSnip := fstPg[aKind]
   else begin
           getSnipId(aKind, mmNext, 0, aSnip, bSnip);
           if printState.preview then doPreview
        end;
   succSnip := bSnip
end {succSnip};


ÿ