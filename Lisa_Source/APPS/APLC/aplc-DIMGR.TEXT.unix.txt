
                            {'Copyright 1983, 1984, Apple Computer Inc.'}

(*********** aplc/dimgr - lisacalc dialog box manager - unit comgr **************)

{Dialog Box Manager}

{$S DiSeg}
procedure addDiBox{height: integer; DoItProcX, GoAwayProcX: procPtr; addScrollBars, checkBoxes : boolean};
        {Put up a new dialog box, showing only: a DoIt button, if DoItProcX
        is not nil, and a GoAway button if GoAwayProcX is not nil.
        Assume that this call was preceeded by initDi or rmvDiBox}
     var
        diPnlHnd   : pnlHandle;
        ButHeight  : integer;
        ButWidth   : integer;
        ButTopEdge : integer;
        leftEdge   : integer;
     begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug }
        if traceDi then LCtraceMess(28);
{$ENDC }
        curDiHeight := height;
        chkBoxInfo.hasCheckBoxes := checkBoxes;
        DialogHeight(height, false);
        setDiSize(height);
        ButWidth   := CalcButWidth(0, 1, ButHeight);
        ButTopEdge := ButHeight div 2;
        leftEdge   := ButLeftEdge - ord(addScrollBars) * dhsBox;
        if (DoItProcX <> nil) then
           with DoItBtn do begin
              SetRect(environ, leftEdge, 0, DialogFolder^.portrect.right, curDiHeight);
              procX      := DoItProcX;
              buttonNum  := BtnDoIt;
              buttonLeft := leftEdge;
              if addScrollBars then buttonTop := Height - ButHeight - dvsBox - 2
              else buttonTop := Height - ButHeight * 3 div 2;
              DoItButTop := buttonTop - 4;
              PutButton(@DoItBtn, true)
           end;
        if (GoAwayProcX <> nil) then
           with GoAwayBtn do begin
              SetRect(environ, leftEdge, 0, DialogFolder^.portrect.right, curDiHeight);
              procX      := GoAwayProcX;
              buttonNum  := BtnGoAway;
              buttonLeft := leftEdge;
              buttonTop  := ButTopEdge;
              PutButton(@GoAwayBtn, true)
           end
     end;


(*$S DiSeg *)
function ButLeftEdge(* : integer *);
var ButHeight : integer;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   ButLeftEdge := DialogFolder^.portrect.right - CalcButWidth(0, 1, ButHeight) - ButPadRH
end;


{$S DiSeg}
procedure buttonWasPushed(theButn : TButn);
        {The button numbered theButn in the Phrase File was pushed.  Invoke
         the procedure that responds to it.}
var i : integer;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug }
   if traceDi then LCtraceMess(27);
{$ENDC }
   i := 0;
   while i < numButtons do begin
      with diBtnArray[i]^ do if (buttonNum = theButn) then begin
{$IFC fDebug }
         if traceDi then writeln('Button ', buttonNum, ' was pushed.  ProcX is ', ord(procX));
{$ENDC }
         IBtnProc(procX);
      end;
      i := i + 1
   end
end;


{$S specialDiSeg}
function  ChkBoxNum : integer;
type mypat = record
              case boolean of
                 true  : (p : pattern);
                 false : (high : longint;
                          low  : longint
                         )
              end;
var x : myPat;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   x.p := DialogFolder^.fillPat;
   ChkBoxNum := 0;
   if (x.high = $01020408) then if (x.low = $10204080) then ChkBoxNum := 1;
   if (x.high = $83070E1C) then if (x.low = $3870E0C1) then ChkBoxNum := 2;
   if (x.high = $22448800) then if (x.low = $22448800) then ChkBoxNum := 3;
   if (x.high = $11224488) then if (x.low = $11224488) then ChkBoxNum := 4;
   if (x.high = $3366CC99) then if (x.low = $3366CC99) then ChkBoxNum := 5;
   if (x.high = $40A00000) then if (x.low = $040A0000) then ChkBoxNum := 6;
   if (x.high = $88808808) then if (x.low = $88808808) then ChkBoxNum := 7;
   if (x.high = $88888888) then if (x.low = $88888888) then ChkBoxNum := 8;
{
   if (x.high = $03844830) then if (x.low = $0C020101) then ChkBoxNum := 9;
   if (x.high = $006C9292) then if (x.low = $82442810) then ChkBoxNum := 10;
   if (x.high = $FF808080) then if (x.low = $80808080) then ChkBoxNum := 11;
   if (x.high = $82422810) then if (x.low = $28448201) then ChkBoxNum := 12;
}
end;


{$S DiSeg}
procedure clrLbl(lblX: lblFldX);
        {Clear the box enclosing this label}
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug }
   if traceDi then LCtraceMess(29);
{$ENDC }
   SetPnlPort(dialogPnl);
   clipRect(lblX^.lblBox);
   BackPat(white);
   EraseRect(lblX^.lblBox);
end;


{$S DiSeg}
procedure drawLbl(lblX: lblFldX);
        {Draw the string in the label field.}
var myLabel : str80;
    theFont : integer;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug }
   if traceDi then LCtraceMess(30);
{$ENDC }
   SetPnlPort(dialogPnl);
   with lblX^ do begin
      clipRect(lblBox);
      theFont := sysText;
      if curDiBox = diReplicate then
         if lblX = diLblArray[FormLblNum] then theFont := tblFntId.Fam;
      TextFont(theFont);
      MoveTo(lblH, lblV);
      if lbl = lNull then DrawString(lStr^)
      else begin
              GetItem(myMenus[lMenuNum], lMenuItem, @myLabel);
              DrawString(myLabel)
           end
   end
end;


{$S DiSeg}
procedure findDiPart{var pt: point; var fKind: fldKind; var pnlFldH: hndField;
                     var pnlFSH: hndFstate; var specProcX: procPtr};
        {Return info about where pt is within the dialog box.  fKind is the
        fldKind, defined in feGlobals.  If fKind = plainTxt or aChkRow, then set
        pnlFldH and pnlFSH to the field and fstate handles. Note that this routine
        may change pt.  If fKind = aChkRow then set specProcX to the
        client's routine that may need to be called by LCSMgr.}
     var
        i: integer;
        lblX: lblFldX;
        btnRect: rect;
     begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug }
        if traceDi then LCtraceMess(31);
        if TempCode then
           if traceDi then begin
              writeln('Point passed to findDiPart is h/v : ', pt.h:1,'/', pt.v:1);
           end;
{$ENDC }
        fKind := noSel;
        if (pt.h < 0) or (pt.v < 0) then begin
           if numDiFlds <= 0 then begin
{$IFC fDebug }
              if TempCode then PrintMess(22);
{$ENDC }
              end
           else begin
              fKind := plainTxt;
              pnlFldH := diFldArray[0];
              pnlFSH := diFSArray[0];
              end;
           end;
        i := 0;
        while (i < numDiFlds) and (fKind = noSel) do begin
           if ptInRect(pt, diFldArray[i]^^.coords) then begin
              fKind := plainTxt;
              pnlFldH := diFldArray[i];
              pnlFSH := diFSArray[i];
              end;
           i := i + 1;
           end;
        i := 0;
        while (i < numChkRows) and (fKind = noSel) do begin
           with diChkArray[i]^ do begin
              if ptInRect(pt, chkFldH^^.coords) then begin
                 fKind := aChkRow;
                 pnlFldH := chkFldH;
                 pnlFSH := nil;
                 specProcX := procX;
                 end;
              end;
              i := i + 1;
           end;
        i := 0;
        while (i < numButtons) and (fKind = noSel) do begin
           with diBtnArray[i]^ do begin
              if ptInRect(pt, environ) then begin
                 fKind := BtnArea;
                 end;
              end;
              i := i + 1;
              end;
        i := 0;
        while (i < numLbls) and (fKind = noSel) do begin
           lblX := diLblArray[i];
           with lblX^ do
              if ptInRect(pt, lblBox) then
                 if assocFldH <> nil then begin
                    fKind := plainTxt;
                    pnlFldH := assocFldH;
                    pnlFSH := assocFSH;
                    with pnlFldH^^.coords do begin
                       if topleft.h > pt.h then pt.h := topleft.h;
                       if botRight.h < pt.h then pt.h := botRight.h;
                       if topleft.v > pt.v then pt.v := topleft.v;
                       if botRight.v < pt.v then pt.v := botRight.v
                    end
                 end;
                 i := i + 1;
        end;
{$IFC fDebug }
        if TempCode then if traceDi then writeln('fKind = ', ord(fKind):1);
{$ENDC }
     end;


(***********************************************************************************
   findNxtDiFld - the current selection in the Dialog Box is of kind FKIND.  If
                  FKIND <> plainTxt then do nothing.  If FKIND = plainTxt then
                  the handles for the field that has the current selection are
                  FLDH & FSH.  In this case, if the direction is to move to the
                  next field (theDir = mmNext), the FLDH & FSH are returned for
                  the next field in the Dialog Box,  if there is a next field;
                  otherwise FKIND is returned with noSel.  Similarily, if the
                  direction is to move to the previous field (theDIr = mmPrior),
                  do the same except for the previous field.
***********************************************************************************)
(*$S DiSeg *)
procedure findNxtDiFld
  (* theDir    : idModeType;
     var fKind : fldKind;
     var fldH  : hndField;
     var fsh   : hndFstate
   *);
var fldNum : integer;
    done   : boolean;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug }
   if traceDi then LCtraceMess(32);
{$ENDC }
   if fKind = plainTxt then begin
{$IFC fDebug }
      if TempCode then
         if numDiFlds = 0 then PrintMess(24);
{$ENDC }

      fldNum := 0;
      done   := false;
      while not done and (fldNum < numDiFlds) do
         if diFldArray[fldNum] <> fldH then fldNum := fldNum + 1
         else done := true;

{$IFC fDebug }
      if TempCode then
         if fldNum = numDiFlds then PrintMess(26);
{$ENDC }

      if theDir = mmNext then fldNum := fldNum + 1
      else fldNum := fldNum - 1;
      if (fldNum >= 0) and (fldNum < numDIFlds) then begin
         fldH := diFldArray[fldNum];
         fsh  := diFSArray[fldNum]
      end
      else fKind := noSel
   end
end;


{$S DiSeg}
procedure initDi;
        {Initialize Dialog Box Manager}
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug }
   if traceDi then LCtraceMess(33);
{$ENDC }
   numDiFlds     := 0;
   numLbls       := 0;
   numChkRows    := 0;
   numButtons    := 0;
   grafixX       := nil;
   chkBoxInfo.hasCheckBoxes := false;
   inPutGraphics := false
end;


{$S DiSeg}
Procedure PutButton{aButtonX : ButtonX; visible : boolean};
        {Draw the button defined by aButtonX^ and remember aButtonX.
         If visible is false, then hide the button, but still remember
         it's location and other parameters.}
var btnRect: rect;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug }
   if traceDi then LCtraceMess(36);
{$ENDC }
   diBtnArray[numButtons] := aButtonX;
   numButtons := numButtons + 1;
   shoButton(aButtonX, visible)
end;


{$S PRdialog}
Procedure PutChkRow(aChkRowX : ChkRowX);
        {Draw the check box row defined by aChkRowX^ and remember aChkRowX}
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug }
   if traceDi then LCtraceMess(35);
{$ENDC }
   diChkArray[numChkRows] := aChkRowX;
   numChkRows := numChkRows + 1;
   ReDrawChks(aChkRowX);
end;


{$S DiSeg}
procedure putDiFld{fldH: hndField; fsH: hndFstate; var status: integer};
        {Draw the field described by fldH and fsH, and add the field to the
        dialog box's current description (ie, remember fldH and fsH).  Be Sure that
        calls to "putDoFld" for a particular dialog box are in the correct order
        so that the move-keys will move among the fields in that same order}
var errNum : integer;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug }
   if traceDi then begin
      LCtraceMess(37);
   end;
{$ENDC }
   diFldArray[numDiFlds] := fldH;
   diFSArray[numDiFlds]  := fsH;
   SetRunFormat(diFldArray[numDiFlds], nil, sysFntId.Fam, setFont, errNum);
   numDiFlds := numDiFlds + 1;
   pnlSetPortClip(dialogPnl, fldH);
   DrawField(fldH, fsH, true, true);
   status := 0;
end;


{$S DiSeg}
procedure putDiLbl{lblX: lblFldX};
        {Draw the label described by lblX^, and add the label to the dialog
        box's current description (ie, remember lblX)}
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug }
   if traceDi then LCtraceMess(38);
{$ENDC }
   diLblArray[numLbls] := lblX;
   numLbls := numLbls + 1;
   drawLbl(lblX)
end;


{$S DiSeg}
Procedure PutGraphics{GrafProcX: ProcPtr};
        {Remember GrafProcX and call GrafProcX^ now and whenever the Dialog Box
         is redrawn.  GrafProcX^ draws graphics, like lines, that can't be
         drawn by diMgr.  Replace any previous GrafProcX with this
         one.  If GrafProcX is nil, then there's no procedure to call.
         When a Dialog Box is removed, GrafProcX is reset to nil.}
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug }
   if traceDi then LCtraceMess(39);
{$ENDC }
   inPutGraphics := true;
   grafixX := GrafProcX;
   if grafixX <> nil then DrawGrafix(grafixX);
   inPutGraphics := false
end;


{$S PRdialog}
procedure ReDrawChks{aChkRowX : ChkRowX};
        {Erase ChkRowX^, then redraw it according to aChkRowX^}
var BoxPosition : integer;
    x           : integer;
    dh, dv      : integer;
    box         : rect;
    BoxType     : boolean;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug }
   if traceDi then LCtraceMess(40);
{$ENDC }
   with aChkRowX^ do begin
      pnlSetPortClip(dialogPnl, chkFldH);
{$IFC fDebug }
      if traceDi then begin
         writeln('Clipping before checks drawn is: ',wRect(chkFldH^^.coords));
      end;
{$ENDC }

      dh := diLblArray[FormLblNum]^.lblH;
      with chkFldH^^ do begin
         with fmtInfo^^[0] do begin
            TextFont(font);
            TextFace(face)
         end;
         box.bottom := coords.bottom;
         EraseRect(coords);
         for x := 0 to curlen - 1 do
            if curValue^^[x] <> blkChrCode then begin
               BoxPosition := TextWidth(@diLblArray[FormLblNum]^.lStr^, 1, x);
               BoxType     := (curValue^^[x] = chkChrCode);
               box.left    := dh + BoxPosition;
               box.right   := box.left + tblFinfo.WidMax - 2;
               box.top     := box.bottom - tblfinfo.ascent;
               if BoxType then FillRect(box, black)
               else FrameRect(box)
            end
      end
   end
end;


{$S DiSeg}
procedure reDrawDi;
        {ReDraw the entire Dialog Box, showing all current buttons, fields, and
        labels}
var ButHeight : integer;
    ButWidth  : integer;
    i         : integer;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug }
   if traceDi then LCtraceMess(41);
{$ENDC }
   if grafixX <> nil then DrawGrafix(grafixX);
   for i := 0 to numLbls - 1 do drawLbl(diLblArray[i]);
   for i := 0 to numDiFlds - 1 do begin
      pnlSetPortClip(dialogPnl, diFldArray[i]);
      DrawField(diFldArray[i], diFSArray[i], true, true);
   end;
   for i := 0 to numChkRows - 1 do reDrawChks(diChkArray[i]);
   SetPnlPort(dialogPnl);
   ButWidth := CalcButWidth(0, 1, ButHeight);
   SetButWidths(ButWidth);
   for i := 0 to numButtons - 1 do
      ReshowButn(diBtnArray[i]^.buttonNum);
   if curDiBox = diFuncHints then begin
      PaintSBar(hsbHDiag);
      PaintSBar(hsbVDiag);
      PaintGrow
   end
end;


{$S DiSeg}
procedure rmvDiBox;
        {Remove the Dialog Box from the screen and reinitialize all Dialog Box
        data structures to empty.  Defaults the active panel to the table panel}
var x : integer;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug }
   if traceDi then LCtraceMess(42);
{$ENDC }
   g.ActivePnl := tblPnl;
   setDiSize(0);
   curDiHeight := 0;
   DialogHeight(0, false);
   for x := 0 to numButtons - 1 do HideButn(diBtnArray[x]^.buttonNum);
   initDi;
   nullSelIn(dialogPnl);
   updFolder(true)
end;


{$S OpenClose}
procedure setDiPnl;
        {Do equivalent of SetUpPnl(dialogPnl) and initialize dialogPnl to
        zero-height.}
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug }
   if traceDi then LCtraceMess(43);
{$ENDC }
   curDiHeight := 0;
end;


{$S DiSeg}
procedure setDiSize{height: integer};
        {Change (or initialize) the size of the dialogPnl to "height".  Note
        that dialogPnl is always full screen width and located at the very top
        of the Lisa screen.  This should all change or go away with the real
        Window Mgr.}
var tempPort : GrafPtr;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug }
   if traceDi then begin
      LCtraceMess(44);
      writeln('Height is ', height:1)
   end;
{$ENDC }

   curDiHeight := height;
   chgPnlSize(DialogPnl, 720, height);
   setPnlPort(dialogPnl);
   EraseRect(DialogFolder^.portRect);

{$IFC fDebug }
   if traceDi then writeln(wClip);
{$ENDC }
end;



{$S DiSeg}
procedure shoButton{aButtonX: ButtonX; visible: boolean};
        {If visible is true, then make aButtonX^ visible, otherwise hide it.}
var ButHeight : integer;
    ButWidth  : integer;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug }
   if traceDi then begin
      if visible then LCtraceMess(45)
      else LCtraceMess(46);
      writeln('Clipping after SetPnlPort(dialogPnl) is');
   end;
{$ENDC }
   SetPnlPort(dialogPnl);
{$IFC fDebug }
   if traceDi then writeln(wClip);
{$ENDC }
   ButWidth := CalcButWidth(0, 1, ButHeight);
   SetButWidths(ButWidth);
   with aButtonX^ do begin
      HideButn(buttonNum);
      PushButn(buttonNum, false);
      if visible then ShowButn(buttonNum, buttonLeft, buttonTop, false);
   end;
end;
ÿ