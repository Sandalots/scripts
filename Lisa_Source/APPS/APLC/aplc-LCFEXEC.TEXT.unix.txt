
(*$F-+ *)
Unit LCFexec;                            {'Copyright 1983, 1984, Apple Computer Inc.'}
     intrinsic;

INTERFACE

uses (*$U UnitStd.obj *)                UnitStd,
     (*$U UnitHz.obj *)                 UnitHz,
     (*$U Storage.obj *)                Storage,
     (*$U QuickDraw.obj *)              QuickDraw,
     (*$U Fontmgr.obj *)                FontMgr,
     (*$U dbenv.obj *)                  dbenv,
     (*$U FEdec.obj *)                  FEdec,
     (*$U fld.obj *)                    fieldedit,
     (*$U teenv.obj *)                  teenv,

     (*$U libos/SysCall.obj *)          Syscall,
     (*$U libos/Psyscall.obj *)         Psyscall,
     (*$U FPlib.obj *)                  FPlib,
     (*$U PMDecl *)                     PMDecl,
     (*$U PrStdInfo *)                  PrStdInfo,
     (*$U PrPublic.obj *)               PrPublic,
     (*$U aplc/mm/matmgr.obj *)         matmgr,
     (*$U wm.events.obj *)              events,
     (*$U wm.folders.obj *)             folders,
     (*$U wm.menus.obj *)               menus,
     (*$U AlertMgr.obj *)               AlertMgr,

     (*$U WMLstd.obj  *)                WMLstd,
     (*$U WMlCrs.obj  *)                WMlCrs,
     (*$U WMlsb.obj   *)                WMlsb,
     (*$U WMLgrow.obj *)                WMLgrow,
     (*$U tm.obj      *)                tm,

     (*$U aplc/feglobals.obj *)         feglobals,
     (*$U aplc/comgr.obj     *)         coMgr,
     (*$U aplc/TEexec.obj    *)         TEexec,
     (*$U aplc/mm/BGenv.obj     *)      BGenv,

     (*$U scrap.obj *)                  scrap,
     (*$U lcut.obj *)                   lcut,
     (*$U FilerComm.obj *)              filerComm;

const lcDate   = '2/23/84';
      verTitle = 'Spring Release';

{$IFC fDebug }
var rodFlag   : boolean;
    relFlag   : boolean;
    myOwnKeys : keySet;
    upTime    : longint;
{$ENDC}


{Highest level control routines - in file LCfexec}
procedure InitBGprocs
  (AnotSelect     { parameters} : ProcPtr;
   BGDown         { parameters} : ProcPtr;
   BGExit         { parameters} : ProcPtr;
   BGGetThumb     { parameters} : ProcPtr;
   BGKey          { parameters} : ProcPtr;
   BGMenuCommand  { parameters} : ProcPtr;
   BGMove         { parameters} : ProcPtr;
   BGNewRgImg     { parameters} : ProcPtr;
   BGScroll       { parameters} : ProcPtr;
   BGSetGrfx      { parameters} : ProcPtr;
   BGTmOut        { parameters} : ProcPtr;
   BGUp           { parameters} : ProcPtr;
   DOBGAct        { parameters} : ProcPtr;
   DrawGraph      { parameters} : ProcPtr;
   EraseBG        { parameters} : ProcPtr;
   FxBGStuff      { parameters} : ProcPtr;
   InitBGPnl      { parameters} : ProcPtr;
   InitBGStorage  { parameters} : ProcPtr;
   TtlSelect      { parameters} : ProcPtr;
   BGReConFig     { parameters} : ProcPtr;
   BGrgChged      { parameters} : ProcPtr;
   FxBgFree       { parameters} : ProcPtr
  );
procedure lcGrafErrProc(errNum : integer);
procedure LastWishes(environPtr : P_env_blk; excepDataPtr : P_ex_data);
procedure lcBGReConFig;
procedure lcBGSetGrfx;
procedure LCBGmainloop;
procedure NewInitFexec (var initStatus: integer);
procedure ProcessEvent;

{Second level control routines - in file LCexecSubs}
procedure chgStatPnl(keepFormula: boolean);
procedure chkScrapSize;
procedure dividPnls;
procedure doGoTo (newRange: range);
procedure editTried(status : integer);
procedure fxNewRgImg;
procedure fxRgChged (rg: range);
procedure setApp2Pnls;
procedure shoNewRx (oldRg: range; newRg: range; just: rgJust);

{ in file LCSCroll }
procedure BGSetThumb;
procedure BGDrawScrolls;
procedure LCDrawScrolls;
procedure updFolder(forceUpdate : boolean);

IMPLEMENTATION

(*$R- *)

(*$IFC teSym *)
(*$D+ *)
(*$ELSEC *)
(*$D- *)
(*$ENDC *)

const
     LCHeapMax   = 17360;        {Initial heap size for 'theHeap'}
     BGHeapMax   = 16360;
     chkHeapTime = 1000;         {check the heap sizes after 10 seconds of idle time}

     alertVerNum = 1001;
     initTblDh   = 200;         { width of the table on the left side of the folder }
     pnlSplitDv  = 1;           {Height of line splitting the panels}

     SegExtension= 'Data Segment';
     SFextension = '$S';
     Textension  = '*';

var
     downNewPnl   : boolean;    {true iff the last mouse down actually was in
                                 a real panel (as opposed to the menu bar or
                                 a scroll bar).}

     toldToDie    : boolean;
     throwAwayDoc : boolean;
     ProcsInited  : boolean;
     preserving   : boolean;
     finishOpen   : boolean;
     dumpToScrap  : boolean;
     procsDying   : boolean;
     docName      : PathName;
     tWideStr     : str40;
     resultRgnH   : rgnHandle;
     lastTblStyle : tblMode;

     formRg       : range;      {Range pointed to (via option-click) to insert into the formula}
     justPointing : boolean;    {True iff in the middle of pointing to a range to insert in the formula.}
     pendingNote  : integer;    {Pending note alert to be displayed}
     tCoDh        : integer;    {width of tCoPnl}
     tWideDh      : integer;    {width of tWidePnl}
     tFormDh      : integer;    {width of tFormPnl}

     ScreenChged  : boolean;    {This flag is used to speed up typing.  If true, then the screen has changed
                                 and the current cell where text is being entered will need to be scrolled
                                 back to be viewable.  Otherwise, do not bother
                                }
     BGProcInfo   : record
                       AnotSelect     : ProcPtr;
                       BGDown         : ProcPtr;
                       BGExit         : ProcPtr;
                       BGGetThumb     : ProcPtr;
                       BGKey          : ProcPtr;
                       BGMenuCommand  : ProcPtr;
                       BGMove         : ProcPtr;
                       BGNewRgImg     : ProcPtr;
                       BGScroll       : ProcPtr;
                       BGSetGrfx      : ProcPtr;
                       BGTmOut        : ProcPtr;
                       BGUp           : ProcPtr;
                       DOBGAct        : ProcPtr;
                       DrawGraph      : ProcPtr;
                       EraseBG        : ProcPtr;
                       FxBGStuff      : ProcPtr;
                       InitBGPnl      : ProcPtr;
                       InitBGStorage  : ProcPtr;
                       TtlSelect      : ProcPtr;
                       BGReConfig     : ProcPtr;
                       BGrgChged      : ProcPtr;
                       FxBgFree       : ProcPtr
                    end;

(***********************   Externally Declared Rouitnes   ***************************)
(*$IFC fDebug *)
function  RodStuff(menuItem: integer): boolean; external;

function wRg(rg : range) : char; external;

procedure StatusMess(status : integer); external;

(*$ENDC *)

function  iAnotSelect                                   {in aplc/mm/LCasm - assembly language}
  (mouseLoc     : point;
   VAR HAnotRec : THAnotRec;
   procParam    : ProcPtr
  ): BOOLEAN; external;

procedure iBGDown                                       {in aplc/mm/LCasm - assembly language}
  (mouseloc  : point;
   procParam : ProcPtr
  ); external;

procedure iBGExit                                       {in aplc/mm/LCasm - assembly language}
  (procParam : ProcPtr
  ); external;

procedure iBGGetThumb                                   {in aplc/mm/LCasm - assembly language}
  (var XPerCent, YPerCent : Fract;
   procParam              : ProcPtr
  ); external;

procedure iBGKey                                        {in aplc/mm/LCasm - assembly language}
  (MyEvent   : EventRec;
   procParam : ProcPtr
  ); external;

procedure iBGMenuCommand                                {in aplc/mm/LCasm - assembly language}
  (MenuIndex,MenuItem : Integer;
   thePrMode          : PrMenuSuppress;
   procParam          : ProcPtr
  ); external;

procedure iBGMove                                       {in aplc/mm/LCasm - assembly language}
  (mouseloc  : point;
   procParam : ProcPtr
  ); external;

procedure iBGNewRgImg                                   {in aplc/mm/LCasm - assembly language}
  (procParam : ProcPtr
  ); external;

procedure iBGReConfig                                   {in aplc/mm/LCasm - assembly language}
   (procParam : ProcPtr
   ); external;

procedure iBGRgChged                                    {in aplc/mm/LCasm - assembly language}
  (aRg       : Range;
   procParam : ProcPtr
  ); external;

procedure iBGScroll                                     {in aplc/mm/LCasm - assembly language}
  (HorV         : THorV;
   myScrollTYpe : ScrollType;
   theScrollDir : ScrollDir;
   VAR Dest     : Fract;
   procParam    : ProcPtr
  ); external;

procedure iBGSetGrfx                                    {in aplc/mm/LCasm - assembly language}
  (procParam : ProcPtr
  ); external;

procedure iBGTmOut                                      {in aplc/mm/LCasm - assembly language}
  (MyEvent   : EventRec;
   procParam : ProcPtr
  ); external;

procedure iBGUp                                         {in aplc/mm/LCasm - assembly language}
  (myEvent   : EventRec;
   procParam : ProcPtr
  ); external;

procedure iDoBGAct                                      {in aplc/mm/LCasm - assembly language}
  (Activate  : Boolean;
   procParam : ProcPtr
  ); external;

procedure iDrawGraph                                    {in aplc/mm/LCasm - assembly language}
  (procParam : ProcPtr
  ); external;

procedure iEraseBG                                      {in aplc/mm/LCasm - assembly language}
  (procParam : ProcPtr
  ); external;

procedure iFxBGstuff                                    {in aplc/mm/LCasm - assembly language}
  (mode       : FIOkind;
   var status : integer;
   procParam  : ProcPtr
  ); external;

procedure iFxBgFree                                     {in aplc/mm/LCasm - assembly language}
  (procParam : ProcPtr
  ); external;

procedure iInitBGPnl                                    {in aplc/mm/LCasm - assembly language}
  (procParam : ProcPtr
  ); external;

procedure iInitBGstorage                                {in aplc/mm/LCasm - assembly language}
  (procParam : ProcPtr
  ); external;

function  iTtlSelect                                    {in aplc/mm/LCasm - assembly language}
  (mouseloc    : point;
   VAR TtlName : TitleNames;
   procParam   : ProcPtr
  ): BOOLEAN; external;

(***********************   Forward Declared Rouitnes       ***************************)
procedure doActEvent(gotEvent : boolean); forward;
procedure doDeActEvent; forward;
procedure doDownEvent; forward;
procedure doFilerEvent(forceSuspend : boolean); forward;
procedure doKeyEvent; forward;
procedure doMenuAct; forward;
procedure doTimeEvent; forward;
procedure doUpEvent; forward;
procedure drawTWide(newStr : str40); forward;
procedure goToRg (var status: integer); forward;
procedure MenuCommand; forward;
procedure size2Scrap(ptNewBR: TPt); forward;
procedure updateCursor; forward;


{$I aplc/LCScroll.text}
{$I aplc/LCexecSubs.text}
{$I aplc/LCfiler.text }
{$I aplc/NewMenus.text}


(*$S specialCase *)
procedure lcGrafErrProc{ errNum : integer };
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

(*$IFC fDebug *)
   writeln('Received a GrafError: ',errNum:1);
(*$ENDC *)
   halt
end;


{$S fexec}
procedure groSelect(mouseLoc: point);
        {The mouse has moved to mouseLoc, relative to the folder.  It
        began in g.ActivePnl.  Grow the selection.}
     var newCR    : boolean;
         mouseRel : point;
         lastRg   : range;
     begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug}
        if tracing then LCtraceMess(93);
{$ENDC }
        AdjToPnl(g.ActivePnl, mouseLoc, mouseRel);
{$IFC fDebug}
        if tracing then begin
           writeln('Mouse down at ', mouseRel.h:1, '/', mouseRel.v:1, ', relative to g.ActivePnl.');
           PrintMess(14);
        end;
{$ENDC }
        c.mouseHasMoved := true;
        lastRg        := g.curRange;
        case g.ActivePnl of
           ctblPnl  : begin
                         tblMseMvd(mouseRel, newCR);
                         if newCR then begin
                            if lastRg.rKind <> g.curRange.rKind then chgStatPnl(false)
                            else shoNewCo(g.curRange);
                            updateCursor
                         end
                      end;
           cwidePnl : wideMseMvd(mouseRel);
           cBGPnl   : iBGMove(mouserel, BGProcInfo.BGMove);
           otherwise  if (g.ActivePnl <> nullPnl) then begin
                         newNbhPnl(mouseRel, g.curNbrhood, g.curTimeout);
                         adjRect(g.ActivePnl, g.curNbrhood);
                      end;
        end;

     end;


{$S fexec}
procedure startSelect(mouseLoc: point);
               {The mouse has gone down at mouseLoc, relative to the folder.
               g.ActivePnl and g.lastActPnl are set up.  Start the selection.}
var mouseRel : point;
    newCR    : boolean;

    (*$S BGspecific *)
    procedure doBGstuff;
    var BGstatus : integer;
    begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

       if g.lastActPnl <> cBGPnl then begin
          { Close down the last selection }
          BGStatus := forceEnter(BGStatus);
{$IFC fDebug}
          if TempCode then
             if BGStatus <> 0 then
                Writeln('StartSelect: msedwn err ',BGStatus:1);
{$ENDC }
          chgStatPnl(true)
       end;

       { Handle the down in the BGPnl }
       iBGDown(mouserel, BGProcInfo.BGDown)
    end;


{$S fexec}
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug}
   if tracing then LCtraceMess(80);
{$ENDC }
   AdjToPnl(g.ActivePnl, mouseLoc, mouseRel);
{$IFC fDebug}
   if tracing then
      writeln('Mouse down at ',mouseRel.h:1,',',mouseRel.v:1,' relative to g.ActivePnl');
{$ENDC }
   c.preDownTime   := g.curTimeout;
   c.mouseHasMoved := false;
   if ((WMEvent.when - c.lastUpTime) < clickDelay) and c.check2or3Click and (g.ActivePnl = c.lastMsePnl)
      and PtInRect(WMEvent.where, c.clkRect) then begin

      {it's double or triple click}
      if not (c.FndDouble or c.FndTriple) then c.FndDouble := true
      else begin
              {it's a triple click}
              c.FndDouble := false;
              c.FndTriple := true
           end
   end
   else begin
           c.FndDouble := false;
           c.FndTriple := false;
           c.check2or3Click := false
      end;
   c.lastMsePnl := g.ActivePnl;

   if not (c.FndDouble or c.FndTriple) then endLastSel(mouseRel);
   g.curTimeout := dfltTimeout;
   case g.ActivePnl of
      ctblPnl  : begin
                    tblMseDn(mouseRel, newCR);
                    if newCR then chgStatPnl(false)
                 end;
      cwidePnl : wideMseDn(mouseRel);
      cBGPnl   : doBGstuff;
      otherwise  MseDnApp(mouseRel)   { in aplc/LCExecsubs - UNIT LCfexec}
   end;

   {remember the current neighborhood for double-click detection}
   c.clkRect := g.curNbrhood
end;


{$S fexec}
procedure EventDispatch;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   if NumDocsOpen > 0 then setPnlPort(folderPnl);
   GetEvent(WMEvent);
   ProcessEvent;
{$IFC fDebug}
   if tracing then printMess(1)
{$ENDC }
end;


{$S fexec}
procedure LCBGmainloop;
        {The main loop for LisaCalc or Business Graphics.}
var timeoutTime : longInt;
    CPtime      : longint;
    idleTime    : longint;
    status      : integer;
    chkHeap     : boolean;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug}
   if tracing then LCtraceMess(86);
{$ENDC }
   chkHeap := true;
   if g.curTimeOut = -1 then timeoutTime := -1 { all cases except some unSuspends  }
   else timeoutTime  := time + g.curTimeout ;    { assures cursor blink on unSuspend }
   repeat
      while not ImActive and not quitRequest do EventDispatch;
      while ImActive and not quitRequest do begin
         if pendingNote <> ok then begin
            {any parameters for the alerts should have been set up already}
            if pendingNote <> 0 then doNoteAlert(pendingNote);
            pendingNote := ok
         end;
         updateCursor;
         if EventAvail then begin
            chkHeap  := true;
            CPtime   := -1;
            idleTime := -1;
            EventDispatch;
            if g.curTimeout = -1 then timeoutTime := -1
            else timeoutTime := time + g.curTimeout;
         end
         else begin
                 if ActiveFolder = Folder then begin

                    {if there is a recalc/redraw pending, wait for a second before doing the
                     recalc/redraw.  The wait will be extended to 4 seconds if the document is
                     not consistent.  This will allow the user a breather without triggering the recalc!
                    }
                    if CalcPending then
                       if CPtime < 0 then CPtime := time
                       else if time - CPtime > 150 + ord(docConsistCheck(status) <> ok) * 100 then begin
                               CPtime   := -1;
                               EnterCalc(false, false, false, false, status);
                               idleTime := time;
                            end;

{$IFC fDebug}
                    getkeys(myOwnKeys);
                    if (78 in myOwnKeys) and (126 in myOwnKeys) and (127 in myOwnKeys) then begin
                       writeln('MxHz ok: ',FcheckHzOK(mxHz, status),'  # blocks: ',status:1);
                       writeln('theHeap ok: ',FcheckHzOK(theHeap, status),'  # blocks: ',status:1);
                       if 125 in myOwnKeys then begin
                          PxHz(mxHz); pxHz(theHeap)
                       end
                    end;
{$ENDC }
                    {check to see if we have been idle for CHKHEAPTIME.  If so, see if the data segments
                     can be reduced in size.  This will be done only once per idle period.
                    }
                    if idleTime < 0 then idleTime := time
                    else if chkHeap and (time > idleTime + chkHeapTime) then begin
                            ShrinkSegMemory(mxHz, true, status);
{$IFC fDebug}
                            if status <> 0 then writeln('ShrinkSegMemory: mxHz - error: ',status:1);
{$ENDC }
                            ShrinkSegMemory(theHeap, true, status);
{$IFC fDebug}
                            if status <> 0 then writeln('ShrinkSegMemory: theHeap - error: ',status:1);
{$ENDC }
                            chkHeap := false
                         end;

                    if (timeoutTime <> -1) and (time > timeoutTime) then begin
                       doTimeEvent;
{$IFC fDebug}
                       if tracing then printMess(2);
{$ENDC }
                       if (g.curTimeout = -1) then timeoutTime := -1
                       else timeoutTime := time + g.curTimeout
                    end
                 end;
                 LetOthersRun
              end
      end
   until quitRequest;
end;


{$S fexec}
procedure ProcessEvent;
        {GetEvent has returned WMEvent.  Process it.}
var status   : integer;

    (*$S ActDeActCode *)
    procedure notActiveYet;
    var titleTab : str255;
    begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

       BeginUpdate(WMevent.who);
       EndUpdate(WMEvent.who);
       GetFldrTitle(WMevent.who, titleTab);
       ParamAlert(titleTab, '', '');
       doPnlDrawAlert(134, FolderPnl)
    end;


{$S fexec}
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug}
   if tracing then LCtraceMess(88);
   if PrintEvents or tracing then begin
      if not tracing then LCtraceMess(88);
      printMess(3)
   end;
{$ENDC }

   with WMEvent do
      if who <> AlertFolder then begin
         if ((what <> KeyDown) or AppleKey) and CalcPending then
            enterCalc(false, false, false, false, status);
         case what of
            filerEvent       : doFilerEvent(false);
            ButtonUp         : doUpEvent;
            ButtonDown       : doDownEvent;     {includes Menu command }
            KeyDown          : doKeyEvent;
            FolderUpdate     : if finishOpen then notActiveYet
                               else updFolder(false);
            FolderActivate   : doActEvent(true);
            FolderDeActivate : doDeActEvent;
{$IFC fDebug}
            abortEvent       : if PrintEvents or tracing then printMess(7);
            FolderMoved      : if PrintEvents or tracing then printMess(5);
            nilEvent         : if PrintEvents or tracing then printMess(6);
            otherwise          if TempCode then printMess(7)
{$ENDC }
         end {case};
      end
{$IFC fDebug}
      else begin
              if tracing then printMess(4);
           end
{$ENDC }
end;


{$S OpenClose}
function doFinishOpen : boolean;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   doFinishOpen := false;
   if pendingNote = -156 then begin
      if not doCautionAlert(156) then begin
         throwAwayDoc := true;
         docDirty     := false;
         if g.LCflag then begin
            g.ItemIndex := LCSavePutBack;
            g.MenuIndex := LCFilePrint
         end
         else begin
                 g.ItemIndex := BGSavePutBack;
                 g.MenuIndex := BGFilePrint
              end;
         MenuCommand;
         doFinishOpen := true;
         exit(doFinishOpen)
      end;
      pendingNote := ok
   end;
   FinishInitDisplay
end;


{$S ActDeActCode}
procedure doActEvent{gotEvent : boolean};
        {A FolderActivate event has come through.  Respond to it.}
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug}
   if tracing or TraceOC then LCtraceMess(89);
   if PrintEvents or tracing then PrintMess(9);
{$ENDC }

   if WMEvent.who = Folder then begin

      { Call Unit Folders routine to take control }
      if gotEvent then TakeControl(WMEvent, false, false);

      if finishOpen then
          if doFinishOpen then exit(doActEvent);

      tmFAct(true);                     { Restore the selection }
      DoSelAct(true);                   { in aplc/TMSProcs - UNIT teenv}
      DoPnlAct(true);                   { in aplc/LCSMgr - UNIT Comgr }
      if g.BGFlag then iDoBGAct(true, BGProcInfo.DoBGAct);

      { Restore the dialog box, scroll bars, and menus }
      DialogHeight(curDiHeight, true);

      If g.LCFlag then LCDrawScrolls     { Draw the appropriate scroll bars }
      else BGDrawScrolls;
      putUpMenus;
      if doingMove and (ScrapProcess <> my_id) then begin
         doNoteAlert(103);
         ClrFormErrors;
         forceCalc(true)
      end;
      ClrUndo
{$IFC fDebug}
;writeln('Process activated: ',time - uptime);
{$ENDC }
   end
   else if WMEvent.who = ScrapFolder then begin
           TakeControl(WMEvent, false, false);
           SetPnlPort(ScrapPnl);
           DrwScrScrollBars(true);
           putUpMenus
        end
end;


{$S ActDeActCodex:}
procedure doDeActEvent;
        {a FolderDeActivate event has come through.  Respond to it.}
var sKind  : scrapKind;
    status : integer;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug}
   if tracing or TraceOC then LCtraceMess(85);
   if PrintEvents or tracing then PrintMess(8);
{$ENDC }

   status := ord(SaveScrap(false, 147));

   if WMEvent.who = Folder then begin
      if doingMove then if false then doNoteAlert(145);
      { Dim the selection and clear the scroll bars }
      tmFAct(false);
      DoSelAct(false);                    { in aplc/tmsprocs - UNIT teenv}
      DoPnlAct(false);                    { in aplc/LCSMgr - UNIT comgr }

      if g.BGFlag then iDoBGAct(false, BGProcInfo.DoBGAct);
      ClearScroll;                        { in aplc/LCScroll - UNIT LCfexec }
      FeTerminate
   end
   else if WMEvent.who = ScrapFolder then begin
{$IFC fDebug}
           if tracing then PrintMess(10);
{$ENDC }
           SetPnlPort(ScrapPnl);
           DrwScrScrollBars(false)
        end; { else if }

   {Generate the current document's picture so that the window manager's can update
    the document's image without bothering me.  This image only needs to be done
    if the document about to be activated does not belong to me (i.e., the scrap).
   }
   if WMEvent.fromProcess <> My_ID then begin

      if NumDocsOpen > 0 then begin
         ShrinkSegMemory(mxHz, false, status);
{$IFC fDebug}
         if status <> 0 then writeln('ShrinkSegMemory: mxHz - error: ',status:1);
{$ENDC }
         ShrinkSegMemory(theHeap, false, status);
{$IFC fDebug}
         if status <> 0 then writeln('ShrinkSegMemory: theHeap - error: ',status:1);
{$ENDC }
      end;

      if Folder <> nil then begin
         SetPnlPort(FolderPnl);
         upDfolder(true)
      end;

      GetScrKind(sKind);
      if (sKind <> cellsKind) and (ScrapProcess = My_ID) then begin
         SetPnlPort(cScrapPnl);
         upDfolder(true)
      end
   end
   else if ActiveFolder = ScrapFolder then begin
           SetPnlPort(cScrapPnl);
           upDfolder(true)
        end;

   { Call proc in unit Folders to pass control to next guy }
   GiveControl(WMEvent)
end;


(*$S fexec *)
procedure newActPnl;
           {The mouse has gone down in a new panel.  Set up the new g.ActivePnl
           and g.lastActPnl.  Set downIgnored = true iff the point is in such a
           place that the event should be ignored. HOWEVER, if a dialog box is
           up, then query whether or not to dismiss the dialog box.  Either
           dismiss it and continue on with this mouse down, or ignore this
           mouse down.}
var response  : diResponse;
    theNewPnl : pnlIndex;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug}
   if tracing then LCtraceMEss(84);
{$ENDC }
   with WMEvent do begin
      downIgnored := false;
      theNewPnl := ptInPnl(who, where, numpnls-1);
      if (curDiHeight>0) and (theNewPnl <> DialogPnl) then begin
         WouldAlert(myMenus[MenuAlert], clickOutDi);
         disMissDi(MenuAlert, clickOutDi, response);
         if (response = IgnoreAction) then downIgnored := true;
      end;
      if theNewPnl = nullPnl then begin
{$IFC fDebug}
         PrintMess(11);
{$ENDC }
         downIgnored := true;
      end;
      if ignoreEv(what, theNewPnl) then downIgnored := true;
      if not downIgnored then begin
         g.lastActPnl := g.ActivePnl;
         g.ActivePnl := theNewPnl;
      end
   end
end;


{$S fexec}
procedure doDownEvent;
        {Respond to a mouse down event.  The global boolean
         downNewPnl gets set to true if the mouse actually went down
         in a panel (as opposed to the menu bar or a scroll bar).}
label 1;
const deltaDouble = 25;   { eventually, this'll be a system constant }
var inPnl    : pnlIndex;
    mouseNow : point;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug}
   if tracing then LCtraceMess(90);
{$ENDC }
   downNewPnl := false;
   with WMEvent do begin
      if who = MenuFolder then doMenuAct

      else if LCCkGrow {in LCScroll } then Begin
              { Do nothing since LCCkScroll did it all }
           end

      else if LCCkScroll {in LCScroll} then Begin
              { Do nothing since LCCkScroll did it all }
           end

      else begin
              downNewPnl  := true;
              ScreenChged := true;
              if (who <> ScrapFolder) and (not printState.preview or (who = DialogFolder)) then begin
                 inPnl := ptInPnl(who, where, numPnls - 1);
                 MapTitlePt(inPnl, where);
                 if g.lcFlag then
                    if inPnl <> tblPnl then EndRgInsert; {in formMgr}
                 if shiftKey then begin
                    if tblEditPnl(g.ActivePnl) then
                       if (g.ActivePnl <> inPnl) and tblEditPnl(inPnl) then shiftKey := false;
                    (* Note: justPointing is true iff we're inserting a range name into a formula rather
                       than selecting.  NB: If we want to do an even better job here, we could check to
                       see whether "where" is "close" to g.ActivePnl.  If so, then don't set shiftKey
                       to false.  This is Larry's hysteresis idea for shift- click.
                    *)
                    if (g.ActivePnl <> tblPnl) and (g.ActivePnl <> inPnl)
                       and (inPnl <> nullPnl) and not justPointing then shiftKey := false;
                    if g.ActivePnl = nullPnl then shiftKey := false;
                    if shiftKey then begin
{$IFC fDebug}
                       if PrintEvents or tracing then PrintMess(12);
{$ENDC }
                       if justPointing and (inPnl <> formPnl) then doPointing(where, false) {in LCexecSubs}
                       else groSelect(where)
                    end
                 end;
                 if not shiftKey then begin
{$IFC fDebug}
                    if PrintEvents or tracing then PrintMess(13);
{$ENDC }
                    if (g.ActivePnl = formPnl) and (inPnl = tblPnl) and codeKey then doPointing(where, true)
                    else begin
                            justPointing := false;
                            newActPnl;
                            if downIgnored then goto 1
                            else startSelect(where);
                         end
                 end; {If on not shiftkey}
{$IFC fDebug}
                 if PrintEvents or tracing then PrintMess(14);
{$ENDC }
                 (* Experimental implementation of Ed's design for delaying
                    tracking drag until a certain amount of time has passed
                 *)
                 while StillDown and (abs(time-when) < DeltaDouble) do begin
                      { do nothing--wait to see whether mse comes back up within
                        deltaDouble before tracking its progress...
                      }
                 end;

                 while stillDown do begin
                    case g.ActivePnl of
                       cScrapPnl,
                       cStblPnl,
                       cScoPnl,
                       cStCoPnl   : inPnl := cScrapPnl;
                       cDialogPnl : inPnl := DialogPnl;
                       otherwise    inPnl := FolderPnl
                    end;
                    SetPnlPort(inPnl);

                    GetMouse(mouseNow);
                    if not ptInRect(mouseNow, g.curNbrhood) then
                       if justPointing then doPointing(mouseNow, false) {in LCexecSubs}
                       else groSelect(mouseNow);

                 end; {while}
                 if (g.curRange.rKind <> aColGrid) and (g.ActivePnl <> cCoPnl) and not shiftkey then
                    shoNewRx(g.curRange, g.curRange, nearJ)
              end
              else downIgnored := true
           end
   end;
1:
end; {procedure}


{$S fexec}
procedure doKeyEvent;
        {The user has hit a key.  Process it.}
var status  : integer;
    wasMove : boolean;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug}
   if tracing then LCtraceMess(83);
   if PrintEvents or tracing then PrintMess(15);
{$ENDC }
   justPointing := false;
   if g.lcFlag then endRgInsert;
   if not WMevent.shiftKey then
      case WMEvent.keycap of
         34 : WMEvent.ascii := chr(chLeft);
         35 : WMEvent.ascii := chr(chRight);
         39 : WMEvent.ascii := chr(chUp);
         43 : WMEvent.ascii := chr(chDown)
      end;

   if ord(WMEvent.ascii) = chEscape then begin
      if g.bgflag then begin
         g.menuIndex := BGEdits;
         g.ItemIndex := BGClr
      end
      else begin
              g.MenuIndex := menuEdits;
              g.ItemIndex := Clr
           end;
      MenuCommand
   end
   else if ord(WMEvent.ascii) = 0 then exit(doKeyEvent)
   else if (WMEvent.who = Folder) or (WMEvent.who = dialogFolder) then begin
           if (ord(WMEvent.ascii) <> chBack) and WMEvent.appleKey then begin
              InsertMenu(mymenus[MenuStat], 0);
              if g.LCFlag then ChgMenuLC
              else ChgMenuBG;      { both are in aplc/LCMenus - UNIT TEexec }
              MenuKey(WMEvent.ascii, g.MenuIndex, g.ItemIndex);
              DeleteMenu(MenuStat);
              if (g.MenuIndex <> 0) then begin
                 if g.MenuIndex <> MenuStat then HiLiteMenu(g.MenuIndex);
                 MenuCommand
              end
(*$IFC fDebug *)
              else if (WMEvent.shiftKey) and (WMEvent.ascii = '~') then begin
                      debug[27] := not debug[27];
                      PutUpMenus
                   end
(*$ENDC *)
              else doStopAlert(100)     {Don't recognize command key}
           end
           else begin
                   case g.ActivePnl of
                      cBGPnl   : iBGKey(WMEvent, BGProcInfo.BGKey);
                      cTblPnl,
                      cWidePnl : TEkey(WMEvent.ascii, wasMove, status);
                      otherwise  appKey(WMEvent.ascii, wasMove, status)
                   end;

                   if wasMove then begin
                      if status = 0 then chgStatPnl(false)
                   end
                   else editTried(status)
                end
       end
   else if WMEvent.who = ScrapFolder then doStopAlert(101)      {You can't type into the scrap}
end;


{$S fexec}
procedure doMenuAct;
        {The mouse has gone down in the menu bar.  Handle it from here.}
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug}
   if tracing then LCtraceMess(91);
{$ENDC }

   updateCursor;        {make sure the cursor is right}

   {Highlight the right items in the menus}
   if ScrapFolder = ActiveFolder then ChgMenuScrap
   else if g.LCFlag then ChgMenuLC
   else ChgMenuBG;      { both are in aplc/LCMenus - UNIT TEexec }

   { Select the menu item }
   MenuSelect(WMEvent.where, g.MenuIndex, g.ItemIndex);
   if (g.MenuIndex <> 0) and (g.ItemIndex <> 0) then begin
{$IFC fDebug}
      if PrintEvents or tracing then PrintMess(16);
{$ENDC }
      justPointing := false;
      if g.lcflag then endRgInsert;
      MenuCommand
   end
   else HiLiteMenu(0)
end;


{$S fexec}
procedure doTimeEvent;
        {A timeout has happened.  Respond to it.}
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug}
   if tracing then LCtraceMess(82);
   if PrintEvents or tracing then PrintMess(17);
{$ENDC }

   {Communicate the event to the proper person and set a new g.curTimeout.}
   case g.ActivePnl of
      cWidePnl,
      cTblPnl : NewTimOut(g.curTimeout);
      cBGPnl  : iBGTmOut(WMEvent, BGProcInfo.BGTmOut);
      otherwise PnlTimOut(g.curTimeout)
   end

end;


{$S fexec}
procedure doUpEvent;
        {The mouse has come up.  Finish up the selection.}
var newCR         : boolean;
    newSel        : boolean;
    restorLastPnl : boolean;
    mouseRel      : point;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug}
   if tracing then LCtraceMess(92);
   if PrintEvents or tracing then PrintMess(18);
{$ENDC }
   with WMEvent do begin
      if downNewPnl and not downIgnored then begin
         {ie, it's not down in a scroll bar, menu bar, or someplace ignored}
         AdjToPnl(g.ActivePnl, where, mouseRel);
         case g.ActivePnl of
            cWidePnl,
            cTblPnl : begin
                         TEMseUp(mouseRel, newCR);
                         if newCR then chgStatPnl(false)
                      end;
            cBGPnl  : iBGUp(WMEvent, BGProcInfo.BGUp);
            otherwise if g.ActivePnl <> scrapPnl then begin
                         pnlMseUp(mouseRel, g.curTimeout, newSel);
                         if not newSel then begin
                            g.ActivePnl  := g.lastActPnl;
                            g.curTimeout := c.preDownTime
                         end
                      end
         end;

         {do click counting on the mouse up}
         if c.mouseHasMoved or ((g.ActivePnl = tblPnl) and not g.reEntrdCell) then begin
            c.check2or3Click := false;
            c.FndDouble := false;
            c.FndTriple := false;
         end
         else c.check2or3Click := true;
         c.lastUpTime := WMEvent.when
      end
   end
end;


{$S fexec }
procedure updateCursor; { 8Nov82 17:15 sw }
        {Updates the cursor for its current location.  Sets arrow unless
          LC Cross or I-beam is appropriate.  Code to recognize presence in
          menu bar, scroll bars, etc., is preserved to facilitate future
          rethinking of cursor-shapes for all areas }
var
    relPoint   : point;         { mouse location in panel-relative coords }
    fRelMouse  : point;         { mouse location in folder-relative coords }
    fKind      : fldKind;       {  temporaries                    }
    fldH       : hndField;      {    for use                      }
    fsH        : hndFstate;     {     in calling                  }
    procX      : procPtr;       {       AppDiPart                 }
    MyHsbHit   : tHsb;          { handle to scroll box the mouse is over }
    MyIconHit  : TIcon;          { icon the mouse is over                 }
    icrs       : integer;       {  the cursor to be used }
    update     : boolean;       { whether actually to update or not  }
    myTtlName  : TitleNames;    { BG title the mouse is over               }
    myHAnotRec : THanotRec;     { BG annotation the mouse is over          }


    function MseInPnl(pnl: pnlIndex) : boolean;
  { tells whether the mouse is currently within the requested panel; sets relPoint
     the mouse's location in panel-relative coords, wherever mouse turns out to be}
    var thePnlRect : rect;
    begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

       SetPnlPort(pnl);
       GetMouse(relPoint);
       GetPnlRect(pnl, thePnlRect);
       MseInPnl := ptInRect(relPoint, thePnlRect)
    end;

begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   update := true;              { really do the update, unless order contermanded below }
   icrs   := icrsInactive;      { start off thinking cursor will be set to inactive }

   if activeFolder = Folder then begin

      SetPnlPort(FolderPnl);
      GetMouse(fRelMouse);   { get mouse location in folder-relative terms for scroll-bar checking }

      {Check the dialog box first, since it's on top}
      if MseInPnl(dialogPnl) and (curDiHeight <> 0) then begin
         findDiPart(relPoint, fKind, fldH, fsH, procX);
         if fKind = plainTxt then icrs := icrsXIbeam
      end
      else if FSBHit(LCSblist,fRelMouse,myhsbHit,myIconHit) then begin
              {scrollbar - do nothing because icrs is already set to icrsInactive }
           end
      else if FGrowHit(fRelMouse) then begin
              {grow icon - do nothing because icrs is already set to icrsInactive }
           end
      else if MseInPnl(cTblPnl) then begin
              if not PtIn1CRg(relPoint, g.curRange) then icrs := icrsLCcross         {table}
              else icrs := icrsXIbeam
           end
      else if MseInPnl(ccoPnl) then icrs := icrsXIbeam
      else if MseInPnl(cformPnl) or MseInPnl(ctFormPnl) or MseInPnl(cWidePnl) or MseInPnl(ctWidePnl) then begin       {wide-angle-view or its title}
              { only give i-beam if current-range is a 1-cell-range }
              if g.curRange.rKind = a1CellRg then icrs := icrsXibeam
           end { wide Panel / title-of-wide-panel }
      else if MseInPnl(BGPnl) then begin
              if iTtlSelect(relpoint,myTtlName,BGProcInfo.TtlSelectg) or
                 iAnotSelect(relPoint,myHAnotRec,BGProcInfo.AnotSelect) then icrs := icrsXibeam
              else icrs := icrsLCCross
           end { BG panel }
      else if MseInPnl(ctCoPnl) then icrs := icrsXibeam
      else if ptInRect(relPoint, Folder^.portrect) then update := false    {in null panel}
   end;

   if update then SetStdCursor(icrs) { assures no change on move into a null panel }

end;  { procedure UpdateCursor }


(*$S initial *)
END.
ÿ