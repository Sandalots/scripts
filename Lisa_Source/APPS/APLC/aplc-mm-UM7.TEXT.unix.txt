
                            {'Copyright 1983, 1984, Apple Computer Inc.'}

(***************************************.*******.************************************
   FBuf routines -
****************************************.*******.***********************************)
(*$S matMgrSeg *)
procedure FBufAlloc(allocSize : integer);
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   if FIOtype = oScrap then begin
      FBufDPtr := pointer(ord(HAllocate(HzOfScrap, allocSize)));
      if ord(FBufDPtr) = ord(hNil) then FBufDptr := nil
   end
   else FBufDPtr := pointer(ord(allocBlock(allocSize)))
end;


(***************************************.*******.************************************
   FBufClose - closes I/O operations to the current filebuffer.  Any cleanup that
               is required by the type of file buffer currently opened will be done.
               Subsequent calls to any FBuf routines can be made after the "FBufClose",
               however, the results will be unpredictable; only calls to "FBufStuff"
               will be disabled (see comments there).  Any STATUS passed into
               the routine will be preserved, unless otherwise noted.  Note with
               writing to any of the buffer types, Any info contained in buffers will
               be flushed before the buffer type is closed.
               The following things are done on each buffer type:

                  oDisk   - The disk file will be closed.  If the file cannot be
                            closed due to an OS error, STATUS will be set to FileErr.
                  oBuffer - this buffer type may not be closed.  Doing so will
                            generate a FatalError.
                  oScrap  - Writing to the scrap data segment will be disabled.  The
                            undo buffer pointers will be restored to point at the undo
                            buffer instead of the scrap.  In the case of writing to the
                            scrap, if STATUS is ok, the type of the scrap will be set.
                            Otherwise, the scrap type will be set to nil.
                  oMem    - writing to memory (undo buffer) will be disabled.

               The only possible STATUS values generated directly by "FBufClose" is
               FileErr.  Other values will be passed through unchanged.  Indirectly,
               STATUS will be generated by "FBufIO" & "FBufFlush" calls.

               Note:  this routine lives in matMgrSeg but is not used by the Resident
                      code.  It is used by the CutPaste, OpenClose and RWscrap Code.
****************************************.*******.***********************************)
(*$S matMgrSeg *)
procedure FBufClose(* var status : integer *);
var error : integer;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

(*$IFC DEBUGVERSION *)
   TraceMess(41);
(*$ENDC *)

   error := 0;
   if FIOType <> oBuffer then begin
      if modeOpened = mmRead then
         case FIOtype of
            oDisk  : case status of
                        OpenErr,
                        PWordErr : ;
                        otherwise  Close_Object(error, FileRefnum)
                     end;
            oScrap : FBufPtr := FBufSave
         end
      else begin
              if status = ok then FBufFlush(status);
              case FIOtype of
                 oDisk  : begin
                             Close_Object(error, FileRefnum);
                             if status <> ok then error := 0
                          end;
                 oScrap : begin
                             if status = ok then PutScrap(ScrapMtx, @FBufPtr^, error)
                             else PutScrap(ScrapNil, nil, error);
                             FBufPtr := FBufSave
                          end
              end
           end;
      if error <> 0 then status := FileErr;
      modeOpened := mmIONull
   end

(*$IFC DEBUGVERSION *)
   else FatalMess(29);
   if (FIOtype = oDisk) and (error <> 0) then OSerror('FBufClose','FileErr',error);
   TraceMess(42);
(*$ENDC *)
end;


(***************************************.*******.************************************
   FBufFlush - flushes any data in the current buffer.  In the case of mmRead from a
               buffer type, another buffer full will be read in locally.  If STATUS
               is not OK, the routine will do nothing.  No STATUS values are
               generated directly by this routine.  Other values will be passed
               through unchanged.  Indirectly, STATUS will be generated by "FBufIO"
               calls.
****************************************.*******.***********************************)
(*$S matMgrSeg *)
procedure FBufFlush
  (* var status : integer               {error status number}
  *);
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

(*$IFC DEBUGVERSION *)
   DebugMess(11);
(*$ENDC *)

   if status = ok then begin
      if modeOpened = mmRead then FBufIO(status)
      else repeat
              if FBufpt > 0 then FBufIO(status)
           until (status <> 0) or (FBufpt <= 0)

(*$IFC DEBUGVERSION *)
      ;StatusMess(status)
(*$ENDC *)

   end
(*$IFC DEBUGVERSION *)
   ;DebugMess(12);
(*$ENDC *)
end;


(***************************************.*******.************************************
   FBufInit - initializes I/O to a buffer type.
****************************************.*******.***********************************)
(*$S matMgrSeg *)
procedure FBufInit
  (* mode       : IOModeType;
     buffertype : IOtype;
     filename   : PtrStrType;
     usePassWord: boolean;             {use the user supplied password}
     var status : integer
  *);
var error : integer;

    (*$S OpenClose *)
    procedure doDiskRead;
    begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

       Open_Secure(error, FBufFile^, FileRefnum, [DRead], thePassWord);
       case error of
          -1293,                     {not password protected}
          -1173,                     {closed by OS}
          -1174,                     {open during a system crash}
          -1175 : error := 0         {reconstructed by the Scavenger}
       end;
       if error = 1294 then status := PWordErr
       else if error <> 0 then status := OpenErr
       else FBufIO(status)
    end;


    (*$S OpenClose *)
    procedure doDiskWrite;
    var sErrNum : integer;
    begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

       Sched_Class(sErrNum, false);
       if usePassWord then Make_Secure(error, FBufFile^, thePassword)
       else Make_File(error, FBufFile^, 0);
       Sched_Class(sErrNum, true);

       if error = 890 then error := 0;  {File Already Exists}
       if error = 0 then begin

          Sched_Class(sErrNum, false);
          Open_Secure(error, FBufFile^, FileRefnum, [DWrite, Private], thePassword);
          Sched_Class(sErrNum, true);

          case error of
             -1293,                     {not password protected}
             -1173,                     {closed by OS}
             -1174,                     {open during a system crash}
             -1175 : error := 0         {reconstructed by the Scavenger}
          end;
          if error = 1294 then status := PWordErr
          else if error <> 0 then status := OpenErr
       end
       else status := MakeErr
    end;


(*$S matMgrSeg *)
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

(*$IFC DEBUGVERSION *)
   DebugMess(13);
(*$ENDC *)
   FIOType    := buffertype;
   FBufpt     := 0;
   blockcnt   := 0;
   status     := ok;
   newFBuffer := true;
   modeOpened := mode;
   if filename <> nil then FBufFile := @filename^
   else FBufFile := nil;
   if modeOpened = mmRead then
      case FIOtype of
         oDisk  : doDiskRead;
         oScrap : begin
                     FBufSave := FBufPtr;
                     FBufPtr  := @FBufFile^
                  end
      end
   else case FIOtype of
           oDisk  : doDiskWrite;
           oScrap : FBufSave := FBufPtr
        end;
   FBufDPtr := FBufPtr

(*$IFC DEBUGVERSION *)
   ;if (FIOtype = oDisk) and (error <> 0) then
      case status of
         MakeErr : OSerror('FBufInit','MakeErr',error);
         OpenErr : OSerror('FBufInit','OpenErr',error);
         PWordErr: OSerror('FBufInit','PWordErr',error)
      end;
   DebugMess(14);
   StatusMess(status)
(*$ENDC *)
end;


(***************************************.*******.************************************
****************************************.*******.***********************************)
(*$S matMgrSeg *)
procedure FBufIO
  (* var status : integer               {error status number}
  *);
var numbytes   : longint;
    buffersize : longint;                       {# bytes in flush buffer}
    BlockSize  : longint;
    ptrToLInt  : ^longInt;
    FBufTemp   : THptrData;
    error      : integer;
    index      : integer;
    flushdata  : boolean;

    (*$S OpenClose *)
    procedure doDiskRead;
    var sErrNum : integer;
    begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

       Sched_Class(sErrNum, false);
       Read_Data(error, FileRefnum, ord(@FBufBuffer), numbytes, numbytes, sequential, 0);
       Sched_Class(sErrNum, true);
       if error = -603 then error := 0
    end;


    (*$S OpenClose *)
    procedure doDiskWrite;
    var sErrNum : integer;
    begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

       Sched_Class(sErrNum, false);
       Write_Data(error, FileRefnum, ord(@FBufBuffer), numbytes, numbytes, sequential, 0);
       Sched_Class(sErrNum, true)
    end;


    (*$S CutPaste *)
    {note that this code is used by the CutPaste code and also the RWscrap code}
    procedure doHandleChaining;
    begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

       {block would be larger than 32k, try increasing it to
        its 32k limit and fill it up with data from the buffer
        if the change occurs
       }
       index := CBdataOfH(HZfromH(@FBufDPtr^), @FBufDPtr^);
       ChangeBlockSize(pointer(ord(FBufDPtr)), THmaxSize, true, error);

       if error = ok then begin
           index := THdataSize - index;
           moveLfast(@FBufBuffer, pointer(ord(@FBufDPtr^^) + BlockSize), index);
           FBufPt := FBufPt - index;
           moveLfast(@FBufBuffer[index], @FBufBuffer, FBufPt);
           FBufTemp := FBufDPtr;
           FBufAlloc(buffersize);
           ptrToLInt  := @FBufTemp^^[THdataSize];
           if FBufDPtr <> nil then begin
              ptrToLInt^ := ord(FBufDPtr);
              error      := 0;
              newFBuffer := false;
              flushdata  := false;
              blockCnt   := 0
           end
           else begin
                   ptrToLInt^ := -1;
                   error := SizeErr
               end
        end
     end;


(*$S matMgrSeg *)
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

(*$IFC DEBUGVERSION *)
   DebugMess(27);
(*$ENDC *)

   if status = ok then begin
      numbytes  := FlushSize + 1;
      error     := 0;
      flushData := true;
      if modeOpened = mmRead then
         case FIOType of
            oMem,
            oScrap : begin
                        ptrToLInt := @FBufDPtr^^[THdataSize];
                        FBufDPtr  := pointer(ptrToLInt^)
(*$IFC DEBUGVERSION *)
                        ;if debug[5] or debug[24] then
                        writeln(chr(7),'Going to oh: ',ptrtolint^,'  size: ', cbdataofh(hzfromh(@FBufDPtr^), @FBufDPtr^));
(*$ENDC *)
                     end;
            oDisk  : doDiskRead
         end
      else case FIOType of
              oDisk   : doDiskWrite;
(*$IFC DEBUGVERSION *)
             {cannot flush data out when using oBuffer}
              oBuffer : FatalMess(29);
(*$ENDC *)
             {We must be writing into either the scrap or memory}
              otherwise begin
                           index      := 0;
                           buffersize := FlushSize + 1;
                           BlockSize  := blockCnt * buffersize;

                           if blockcnt <> 0 then begin

                              {try increasing the size of the current block}
                              ChangeBlockSize(pointer(ord(FBufDPtr)), BlockSize + FBufPt, false, error);
                              if error = SizeErr then doHandleChaining
                           end
                           else if newFBuffer then begin
                                   FBufAlloc(FBufPt);
                                   FBufPtr := FBufDPtr
                                end;
                           if (error = 0) and (FBufDPtr <> nil) then begin
                              if flushdata then begin
                                 moveLfast(@FBufBuffer[index], pointer(ord(@FBufDPtr^^) + Blocksize), FBufPt - index);
                                 blockcnt := blockcnt + 1
                              end
                           end
                           else numbytes := 0
                        end
           end;
      if flushdata then FBufPt := 0;
      if (numbytes <> FlushSize+1) or (error <> 0) then status := FileErr;
      if error = 848 then status := DSpaceErr;

(*$IFC DEBUGVERSION *)
      ;if (FIOtype = oDisk) and (error <> 0) then OSerror('FBufIO','FileErr/DSpaceErr',error);
      StatusMess(status)
(*$ENDC *)

   end

(*$IFC DEBUGVERSION *)
   ;DebugMess(28)
(*$ENDC *)
end;


(***************************************.*******.************************************
****************************************.*******.***********************************)
(*$S matMgrSeg *)
function  FBufMoveTo
  (Position : longint;
   StartPtr : THptrData
   ) : boolean;
var ptrToLInt : ^longint;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   FBufMoveTo := false;
   case FIOType of
      oScrap,
      oMem : if modeOpened = mmRead then begin
                FBufDPtr := StartPtr;
                while position > THdataSize do
                   if CbDataOfH(HzfromH(@FBufDPtr^), @FBufDPtr^) > THdataSize then begin
                      Position  := Position - THdataSize;
                      ptrToLInt := @FBufDPtr^^[THdataSize];
                      FBufDPtr  := pointer(ptrToLInt^);
                   end
                   else exit(FBufMoveTo);
                FBufMoveTo := true;
                FBufPt := Position
             end
   end
end;


(***************************************.*******.************************************
****************************************.*******.***********************************)
(*$S matMgrSeg *)
procedure FBufStuff
  (* sizekind    : FBufKind;            {type of information to be stuff/unstuffed}
     value       : ptrData;             {pointer to the data area}
     var length,                        {# of bytes to read/write}
         status  : integer              {error status number}
  *);
var cache   : FBufType;
    recSize : integer;

    procedure bufferIO(ptrToData : ptrData; numBytes : integer);
    var bytecnt : longint;
        diff    : longint;
        done    : boolean;
        Fsize   : longint;
    begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

       Fsize := FlushSize + 1;
       case FIOType of
          oMem,
          oScrap : if modeOpened = mmRead then Fsize := THdataSize
       end;
       bytecnt := numBytes;
       repeat
          done := false;
          if bytecnt + FBufpt > Fsize then begin
             if FIOType <> oBuffer then begin
                diff := Fsize - FBufpt;
                if sizeKind <> fNull then
                   if modeOpened = mmRead then
                      case FIOType of
                         oMem,
                         oScrap  : moveLfast(@FBufDPtr^^[FBufpt], @ptrToData^, diff);
                         otherwise moveLfast(@FBufBuffer[FBufpt], @ptrToData^, diff)
                      end
                   else moveLfast(@ptrToData^, @FBufBuffer[FBufpt], diff);
                ptrToData := pointer(ord(ptrToData) + diff);
                bytecnt   := bytecnt - diff;
                FBufPt    := FBufPt + diff;
                FBufIO(status)
             end
(*$IFC DEBUGVERSION *)
             else FatalMess(30)
(*$ENDC *)
          end
          else begin
                  done := true;
                  if sizeKind <> fNull then
                     if modeOpened = mmRead then
                        case FIOtype of
                           oDisk,
                           oBuffer : moveLfast(@FBufBuffer[FBufpt], @ptrToData^, bytecnt);
                           otherwise moveLfast(@FBufDPtr^^[FBufpt], @ptrToData^, bytecnt)
                        end
                     else moveLfast(@ptrToData^, @FBufBuffer[FBufpt], bytecnt);
                  FBufpt := FBufpt + bytecnt
               end
       until done or (status <> ok)
    end;

begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

(*$IFC DEBUGVERSION *)
   DebugMess(29);
(*$ENDC *)

   if status = ok then begin
      if modeOpened <> mmIONull then begin
         case sizekind of
            fNull,
            fRawData : recSize := length;
            fRange   : recSize := sizeof(cache.rg);
            fPCel    : recSize := sizeof(cache.pCel);
            fTmBand  : recSize := sizeof(cache.band);
            fSymbol  : begin
                          bufferIO(@length, sizeof(length));
                          recSize := length
                       end;
            fWord    : recSize := sizeof(cache.word);
            fshort   : recSize := sizeof(cache.short);
            flong    : recSize := sizeof(cache.long)
         end;
         if status = ok then begin
            if modeOpened = mmWrite then
               case sizeKind of
                 fNull,
                 fRawData,
                 fSymbol : ;
                 otherwise  begin
                               moveLfast(@value^, @cache, recSize);
                               value := @cache
                            end
               end;
            bufferIO(value, recSize)
         end
      end
      else status := openErr

(*$IFC DEBUGVERSION *)
      ;StatusMess(status)
(*$ENDC *)

   end

(*$IFC DEBUGVERSION *)
   ;DebugMess(30)
(*$ENDC *)
end;


(***************************************.*******.************************************
   scrapSize - returns the amount of space taken for the scrap
****************************************.*******.***********************************)
(*$S OpenClose *)
function scrapSize : longint;
var FBufTemp  : THptrData;
    ptrToLInt : ^longint;
    nextBuf   : longint;
    size      : longint;
    temp      : longint;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   nextBuf := ord(ScrapPtr);
   size    := 0;
   if scrapPtr <> nil then
      repeat
         FBufTemp := pointer(nextBuf);
         temp     := CbDataOfH(HzfromH(@FBufTemp^), @FBufTemp^);
         if temp > THdataSize then begin
             ptrToLInt := @FBufTemp^^[THdataSize];
             nextBuf   := ptrToLInt^
         end
         else nextBuf := -1;
         size := size + temp
      until nextBuf < 0;
   scrapSize := size
end;


(***************************************.*******.************************************
   WriteMMscrap - writes the table within the scrap to the global system data
                  segment.  Any status information will be returned through STATUS.
                  Once the data has been written to the global data segment, the
                  local information concerning the scrap will be erased.  At this
                  point, the matrix manager can be put to sleep.  The format of the
                  global data segment is as follows:

          2 bytes       - scrap identification
          26 bytes      - scrap information record
          2 bytes       - # of bytes for scrap row/col header info
          2 bytes       - type of info in the scrap
          10 bytes      - a range which describes the size of the scrap
          n * 2 bytes   - row heights, where n = # of rows present (derived from range)
          n * 2 bytes   - col widths, where n = # of cols present (derived from range)
          2 bytes       - last row with real data
          2 bytes       - last column with real data
          -----------
          2 bytes       - attributes for the cell at (rowId, colId)
          2 bytes       - type of data  13 = non-shared cell
                                        14 = blank cell with attributes set
          2 bytes       - # of bytes for the data types information, if any
          n bytes       - information for data type, if any

                          If the cell has a formula associated with it then
                          the instance of the cell will occur first.  It will be
                          followed by another  "# of bytes" word to
                          described the type of formula and the information for the
                          formula.
          -----------
                          the information between the to dashed lines will be repeated
                          for as many items there are in the range.

     The status that can be returned is:
        ok        - the scrap was written correctly
        OpenErr   - the scrap could not be opened
        FileErr   - an error occured while writing the data to the scrap
****************************************.*******.***********************************)
(*$S RWscrapCode *)
procedure WriteMMscrap
  (* var status : integer
  *);
label 1,2;
var dataLen   : integer;
    row, col  : integer;
    oRefcnt   : integer;
    PutErr    : integer;
    bufPt     : longint;
    hContents : THContents;
    forminfo  : THFmla;
    pCel      : TPCel;
    Cel       : TCel;
    ptrToLInt : ^longint;
    pCelAttr  : wordPtr;
    FBufTemp  : THptrData;
    tbufPt    : longint;
    oper      : undoType;
    lastIds   : pIdent;
    sRg       : range;

    (*$S RWscrapCode *)
    procedure stuffinteger(value : integer);
    var length : integer;
    begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

       FBufStuff(fWord, @value, length, status)
    end;


(*$IFC DEBUGVERSION *)
    (*$S debug2 *)
    procedure dumpinfo(infonum : integer);
    begin
       case infonum of
          1 : begin
                 writeln('Version #',scrapverNum:1,'  rg: ', wrg(ScrapInfo^.SnipBds),'  len: ',bufPt:1,
                         '  lastIds: ',lastIds.length:1,',',lastIds.start:1);
              end;
          2 : begin
                 writeln(row:1,',',col:1,'  attr: ',pCelAttr^:1,'  len: ',dataLen:1,
                         '  rule: ',pCel^.attributes.pkHasRule);
              end;
       end
    end;
(*$ENDC *)


(*$S RWscrapCode *)
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

(*$IFC DEBUGVERSION *)
   if traceMMgr then writeln('dumping table to the scrap');
(*$ENDC *)
   {open up the scrap buffer}
   if ScrapProcess <> My_ID then ClaimScrap;
   EraseScrapData(status);
   if status <> 0 then goto 2;
   StartPutScrap(status);
   if status <> 0 then goto 2;
   if ScrapPtr <> nil then begin
      FbufTemp := FbufPtr;
      FbufPtr  := ScrapPtr;
      ReadHeader(oper, sRg, lastIds, status);
      FBufPtr  := FBufTemp
   end;
   FBufInit(mmWrite, oScrap, nil, false, status);

   if ScrapPtr <> nil then begin

      {put in table identifier, version number, scrap info record and size of scrap}
      stuffinteger(ScrapVerNum);
      dataLen := sizeof(ScrapInfo^);
      FBufStuff(fRawData, @ScrapInfo^, dataLen, status);

      {compute the size of the scrap header and save it in the buffer}
      with ScrapInfo^.SnipBds do
         dataLen := (hiCol-loCol+1 + hiRow-loRow+1) * 2 + sizeof(range) + sizeof(integer) * 3;
      bufPt := dataLen;
      stuffinteger(dataLen);
      FBufStuff(fRawData, @ScrapPtr^^, dataLen, status);
      stuffinteger(lastIds.length);
      stuffinteger(lastIds.start);
      if status <> ok then goto 1;

(*$IFC DEBUGVERSION *)
      if traceMMgr then dumpinfo(1);
(*$ENDC *)

      with ScrapInfo^.SnipBds do
         for row := loRow to lastIds.length do
            for col := loCol to lastIds.start do begin
               tBufPt     := FBufPt;
               FBufTemp   := FBufDptr;
               modeOpened := mmRead;
               FIOtype    := oMem;
               if FBufMoveTo(bufPt, ScrapPtr) then
                  FBufStuff(fPcel, @Cel, dataLen, status)
               else status := PtrErr;
               modeOpened := mmWrite;
               FIOtype    := oScrap;
               FBufPt     := tBufPt;
               FBufDptr   := FBufTemp;
               if status <> ok then goto 1;

               bufPt := bufPt + sizeof(TPCel);
               pCel  := @Cel;
               hContents := GetHContents(pCel);
               pCelAttr  := @pCel^.attributes;
               stuffinteger(pCelAttr^);
               if hContents <> nil then
                  with hContents^^.vinfo.data do begin
                     stuffinteger(13);
                     oRefcnt := refcnt;
                     refcnt  := 1;
                     dataLen := CbDataOfH(mxHz, @hContents^);
                     FBufStuff(fSymbol, @hContents^^, dataLen, status);
                     hContents^^.vinfo.data.refcnt  := oRefcnt;
                  end
               else stuffinteger(14);
(*$IFC DEBUGVERSION *)
               if traceMMgr then dumpinfo(2);
(*$ENDC *)
               if pCel^.attributes.pkHasRule then begin
                  forminfo := pointer(MakeHnd(hcontents^^.rule.ohfmla));
                  with forminfo^^ do begin
                     if shared <> fSingle then begin
                        oRefcnt := sdata.info.refcnt;
                        sdata.info.refcnt := 1
                     end;
                     dataLen := CbDataOfH(mxHz, @forminfo^);
                     FBufStuff(fSymbol, @forminfo^^, dataLen, status);
                     if shared <> fSingle then sdata.info.refcnt := oRefcnt
                  end
               end;
               if status <> ok then goto 1
            end
   end
   else status := noScrapErr;
   goto 1;
2: UndoInheritScrap(status);
   status := OpenErr;
1: FBufClose(status);
   EndPutScrap(PutErr);
   if (status = ok) and (PutErr <> 0) then status := FileErr;

(*$IFC DEBUGVERSION *)
   ;StatusMess(status);
   traceMMgr := false
(*$ENDC *)
end;


(***************************************.*******.************************************
    The status that can be returned is:
       ok       - the scrap was read correctly
       OpenErr  - the scrap could not be opened
       FileErr  - an error occured while reading the data from the scrap
       BadFile  - a file was opened which is not a LisaCalc file or
                  is a more recent file than the current software can
                  handle
       DSpaceErr- no disk space for the data segment
       OutOfMem - the file could not be loaded because of memory problems
****************************************.*******.***********************************)
(*$S RWscrapCode *)
procedure ReadMMscrap
  (* var status : integer
  *);
label 2,3;
var dataLen   : integer;
    row, col  : integer;
    dataType  : integer;
    version   : integer;
    x         : integer;
    GetErr    : integer;
    scrapKind : scrapType;
    FBufScrap : record
                   case alias of
                      twdlDee : (h : TH);
                      twdlDye : (d : THptrData)
                   end;
    hContents : THContents;
    forminfo  : THFmla;
    Cel       : TCel;
    pCelAttr  : wordPtr;
    oper      : undoType;
    lastIds   : pIdent;
    sRg       : range;
    t2bufPt   : longint;
    tbufPt    : longint;
    FBufTemp  : THptrData;

    (*$S RWscrapCode *)
    procedure unstuffinteger(var value : integer);
    var length : integer;
    begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

       FBufStuff(fWord, @value, length, status);
       t2BufPt := t2BufPt + sizeof(value)
    end;


(*$IFC DEBUGVERSION *)
    (*$S debug2 *)
    procedure dumpinfo(infonum : integer);
    begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

       case infonum of
          1 : begin
                 writeln('Version #',scrapverNum:1,'  rg: ', wrg(ScrapInfo^.SnipBds),'  len: ',datalen:1);
              end;
          2 : begin
                 writeln(row:1,',',col:1,'  attr: ',pCelAttr^:1,'  type: ',dataType:1,
                         '  rule: ',Cel.attributes.pkHasRule);
              end;
          3 : writeln('  lastIds: ',lastIds.length:1,',',lastIds.start:1);

       end
    end;
(*$ENDC *)


(*$S RWscrapCode *)
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

(*$IFC DEBUGVERSION *)
   if traceMMgr then writeln('restoring the table from the scrap');
(*$ENDC *)

   {clear previous scrap information}
   if FBufPtr = ScrapPtr then FBufTemp := nil
   else FBufTemp := FBufPtr;
   tBufPt   := FBufPt;
   FBufPtr  := ScrapPtr;
   claimUndoBuffer(true);
   ScrapPtr := nil;
   FBufPtr  := FBufTemp;
   FBufPt   := tBufPt;

   {open up the scrap buffer}
   StartgetScrap(status);
   if status <> ok then goto 3;

   GetScrap(scrapKind, FBufScrap.h);
   if FBufScrap.h <> nil then begin
      FBufInit(mmRead, oScrap, @FBufScrap.d^, false, status);
      unstuffinteger(version);
      if version = ScrapVerNum then begin
         dataLen := sizeof(ScrapInfo^);
         FBufStuff(fRawData, @ScrapInfo^, dataLen, status);
         unstuffinteger(dataLen);

(*$IFC DEBUGVERSION *)
         if traceMMgr then dumpinfo(1);
(*$ENDC *)
         if status <> ok then goto 2;
         t2BufPt := FBufPt;
         FBufInit(mmWrite, oMem, @FBufScrap.d^, false, status);
         FBufStuff(fRawData, @FBufScrap.d^^[t2BufPt], dataLen, status);
         t2BufPt := t2BufPt + dataLen;

         {save writing state for memory}
         tBufPt   := FBufPt;
         FBufTemp := FBufDptr;

         {set up reading state for the scrap}
         modeOpened := mmRead;
         FIOtype    := oScrap;
         if not FBufMoveTo(t2BufPt, @FBufScrap.d^) then goto 2;

         unStuffInteger(row);
         lastIds.length := row;
         unStuffInteger(col);
         lastIds.start  := col;

(*$IFC DEBUGVERSION *)
         if traceMMgr then dumpinfo(3);
(*$ENDC *)

         if status = ok then begin
            pCelAttr := @Cel.attributes;
            with ScrapInfo^.SnipBds do
               for row := loRow to lastIds.length do
                  for col := loCol to lastIds.start do begin
                     unstuffinteger(pCelAttr^);
                     unstuffinteger(dataType);
                     if status <> ok then goto 2;
(*$IFC DEBUGVERSION *)
                     if traceMMgr then dumpinfo(2);
(*$ENDC *)
                     case dataType of
                        14 : hContents := @mxHz^;
                        13 : begin
                                unstuffinteger(dataLen);
                                if status <> ok then goto 2;
                                hContents := allocBlock(dataLen);
                                if hContents <> nil then begin
                                   FBufStuff(fRawData, @hContents^^, dataLen, status);
                                   t2BufPt := t2BufPt + dataLen
                                end
                                else status := OutOfMem
                             end
                     end;
                     if status = ok then begin

                        {reload writing state for memory}
                        modeOpened := mmWrite;
                        FIOtype    := oMem;
                        FBufPt     := tBufPt;
                        FBufDptr   := FBufTemp;

                        {write data to memory}
                        Cel.oh := GetOH(ord(hContents));
                        FBufStuff(fPcel, @Cel, dataLen, status);

                        {save new writing state for memory}
                        tBufPt     := FBufPt;
                        FBufTemp   := FBufDptr;

                        {reload reading state for the scrap}
                        modeOpened := mmRead;
                        FIOtype    := oScrap;
                        if not FBufMoveTo(t2BufPt, @FBufScrap.d^) then goto 2;

                        if Cel.attributes.pkHasRule then begin
                           unstuffinteger(dataLen);
                           if status <> ok then goto 2;
                           forminfo := pointer(ord(allocBlock(dataLen)));
                           if forminfo <> nil then begin
                              FBufStuff(fRawData, @formInfo^^, dataLen, status);
                              t2BufPt := t2BufPt + dataLen;
                              hContents^^.rule.ohfmla := GetOH(ord(forminfo))
                           end
                           else status := OutOfMem
                        end
                     end
                  end
         end
      end
(*$IFC DEBUGVERSION *)
      else FatalMess(25)
(*$ELSEC *)
      else status := NoScrapErr
(*$ENDC *)
   end
   else status := NoScrapErr;
   goto 2;
3: status := openErr;
2: modeOpened := mmWrite;
   FIOtype    := oMem;
   FBufPt     := tBufPt;
   FBufDptr   := FBufTemp;
   FBufClose(status);
   ScrapPtr   := FBufPtr;
   FBufPtr    := FBufSave;
   EndGetScrap(GetErr);
   if (status = ok) and (GetErr <> 0) then status := FileErr;

(*$IFC DEBUGVERSION *)
   ;StatusMess(status);
   traceMMgr := false
(*$ENDC *)
end;

ÿ