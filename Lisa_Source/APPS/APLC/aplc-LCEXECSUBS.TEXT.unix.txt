
                            {'Copyright 1983, 1984, Apple Computer Inc.'}

(*********** aplc/lcexecsubs - INCLUDE FILE FOR LCFEXEC WITH LISACALC-SPECIFIC ROUTINES - unit lcfexec **************)


{$S TxtCutPaste}
procedure appEdits (var status: integer; var txtChg: boolean);
        {An edit menu item has been selected and the g.ActivePnl is a
        LisaCalc-specific panel.  Respond to the menu item.  Return
        status = 0 iff all went well.  Return txtChg = true iff
        action was a text cut, clear, or paste}
const maxScrBytes = 100;
var skind      : scrapKind;
    ScrapCont  : TB;
    theFldKind : fldKind;
    numbytes   : integer;
    uOp        : undoOpKind;
    scrapVal   : array [0..maxScrBytes] of byte;
    i          : integer;
    timeout    : integer;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug}
   if tracing then LCtraceMess(164);
{$ENDC }
   status := -1;
   txtChg := false;
   ClrUndo;
   GetPnlFldKind(theFldKind);
   if theFldKind = plainTxt then begin
      case g.ItemIndex of
         clrTxt   : begin
                       txtChg := true;
                       pnlEdit(pnlClear, status);
                       uOp := uClrTxt
                    end;
         cut      : begin
                       txtChg := true;
                       pnlEdit(pnlCut, status);
                       uOp := uCutTxt
                    end;
         feCopy   : begin
                       pnlEdit(pnlCopy, status);
                       uOp := uCopyTxt
                    end;
         restOrig : TERestore;
         pasteM   : begin
                       GetScrKind(sKind);
                       GetCsScrap(ScrapCont);               {Check universal text stream}
                       if (sKind <> cellsKind) and (ScrapCont <> 0) then sKind := txtKind;
                       case sKind of
                          txtKind   : begin
                                         txtChg := true;
                                         pnlEdit(pnlPaste, status);
                                         uOp := uPasteTxt
                                      end;
                          cellsKind : begin
                                         numbytes := maxScrBytes + 1;
                                         if aRgInScrap(@scrapVal, true, numbytes) then begin

                                            {Paste the text from the formula of the single cell in
                                             the scrap into the current text selection and set
                                             txtChg and the undo information.
                                            }
                                            txtChg := true;
                                            for i := 0 to numbytes - 1 do
                                               pnlKey(chr(scrapVal[i]), false, timeout, status);
                                            uOp := uPasteTxt
                                         end
                                         else doStopAlert(102)        {Scrap and selection incompatible}
                                      end;
                          otherwise   doStopAlert(102)
                       end
                    end
      end;
      if status = ok then setUndo(uOp, g.ActivePnl)
   end
   else doStopAlert(104)  {Not a selection for editing}
end;


(*$S statmgr *)
procedure appKey(ch: char; var wasMove: boolean; var status: integer);
        {Called when a key (ch) has been typed and the g.ActivePnl is a
        LisaCalc-specific panel.  If the key is an enter-move key, do
        the appropriate enter and move,  set wasMove to true, and set
        status to 0 iff the enter and move happened successfully.  If
        ch is not an enter-move key, insert the character, if appropriate,
        into the value under-edit in the g.ActivePnl, set wasMove to false,
        and set status to 0 iff the key was accepted.}
var response:  diResponse;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug}
   if tracing then LCtraceMess(165);
{$ENDC }
   status  := 0;
   MoveAct := MoveAction(ch);
   if MoveAct <> noMove then begin
      wasMove := true;
      case g.ActivePnl of
         ccoPnl     : begin
                         endPnlSel;
                         nullPnlSel;
                         goToRg(status)
                      end;
         cDialogPnl : begin
                         WouldAlert(myMenus[MenuAlert], FldFldMove);
                         jumpAField(moveAct, g.curTimeOut);
                         setUndo(uFldFldMove, DialogPnl)
                      end;
         otherwise    begin
                         response := OkAction;
                         if curDiHeight > 0 then
                            disMissDi(MenuAlert, HitEnter, response);
                         if response = IgnoreAction then wasMove := false
                         else if forceEnter(status) = 0 then DoKeyedMove(MoveAct, status)
                      end
      end
   end
   else begin
           wasMove  := false;
           response := OkAction;
           if curDiHeight > 0 then disMissDi(MenuAlert, HitKey, response);
           if response <> IgnoreAction then begin
              if (tableState <> tValues) and (g.ActivePnl in [tblPnl, widePnl, formPnl])
              then doStopAlert(10004)  {Can't type if formulas are showing}
              else if g.ActivePnl = nullPnl then doStopAlert(104) {No selection for editing}
              else begin
                      if g.pnlWithChg <> g.ActivePnl then ClrUndo;
                      ObscureCursor;
                      pnlKey(ch, WMEvent.shiftKey, g.curTimeout, status);
                      if status = 0 then setUndo(uKey, g.ActivePnl);
                      editTried(status)
                   end
           end
        end
end;


{$S statmgr}
procedure chgStatPnl{keepFormula: boolean};
        {There's a new current range.  Put range-related feedback in the
        application-specific panels.  Assume the status panel has been
        emptied.  Keep the formula if we're told to and it's a RectRg. }
var theCellKind : CellKind;
    newTWide    : str40;
    item        : integer;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug}
   if tracing then LCtraceMess(167);
{$ENDC }

   if g.LCflag and not(keepFormula and (g.CurRange.rKind = aRectRg)) then begin
      emptyFormDisp;
      drawForm
   end;
   shoNewCo(g.curRange);

   if g.curRange.rKind = a1CellRg then begin
      if g.LCFlag and not printState.preview then shoNewForm(g.curRange.loRow, g.curRange.loCol);
      CellType(g.curRange.loRow, g.curRange.loCol, theCellKind);
      case theCellKind of
         tNumber:  item := ValNum;
         tText:    item := ValText;
         tDate:    item := ValDate;
         tParts:   item := ValDatePart;
         tBlank:   item := ValBlank;
         tPage :   item := ValPage
      end
   end
   else begin
           EraseWav;
           item := ValueItem
        end;
   GetItem(myMenus[MenuStat], item, @newTWide);
   drawTWide(newTWide)
end;


(*$S fexec *)
procedure chkScrapSize;
var tRect  : rect;
    tPoint : point;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   GetPnlRect(cScrapPnl, tRect);
   with ScrapFolder^.portrect do
      if (tRect.right <> right-left) or (tRect.bottom <> bottom-top) then begin
         tPoint.h := right-left;
         tPoint.v := bottom-top;
         Size2Scrap(tPoint)
      end
end;


{$S fexec}
procedure dividPnls;
        {Draw panel division lines and strings within title panels}
var
   folderDh    : pixel;
   folderDv    : pixel;
   statPaneBot : pixel;
   splitRect   : rect;
   fRect       : rect;
   pnlRect     : rect;
   theStr      : str40;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   GetPnlRect(FolderPnl, fRect);
   with fRect do begin
{$IFC fDebug}
      if tracing then LCtraceMess(168);
{$ENDC }
      { Subtract off width of scroll box as declared in unit SBoxlib }
      folderDh    := botRight.h - dhSBox;
      folderDv    := botright.v - dvSBox;
      statPaneBot := (1 + ord(g.LCflag)) * pnlLineDv + 1;

      SetPnlPort(FolderPnl);
      PenNormal;
      SetRect(splitRect, 0, statPaneBot, folderDh, statPaneBot + PnlSplitDv);
      FillRect(splitRect, black);

      MoveTo(0, pnlLineDv);
      LineTo(folderDh, pnlLineDv);
      MoveTo(coDh, 0);
      LineTo(coDh, pnlLineDv);

      {Draw title string within each title panel.}
      SetPnlPort(tCoPnl);
      GetItem(myMenus[MenuStat], CellsItem, @theStr);
      GetPnlRect(tCoPnl, pnlRect);
      with LrgFInfo, pnlRect do
         coBase := ascent + top + (((bottom - top) - (ascent + descent)) div 2);
      BackPat(white);
      EraseRect(pnlRect);
      putSysStr(4, coBase, theStr);
      drawTWide(tWideStr);
      if g.LCflag then begin
         SetPnlPort(tFormPnl);
         GetItem(myMenus[MenuStat], FormItem, @theStr);
         GetPnlRect(tFormPnl, pnlRect);
         with LrgFInfo, pnlRect do
            formBase := ascent + top + (((bottom - top) - (ascent + descent)) div 2);
         EraseRect(pnlRect);
         putSysStr(4, formBase, theStr)
      end
   end
end;


(***************************************.*******.************************************
   docConsistCheck - determines if the document is currently being edited.  Being
                     under edit implies that either a dialog box is down or a value
                     is being entered in the table/wide angle view or formula panel.
                     Status is returned which states if the document is inconsistent
                     (InConsErr) or alright (ok).  If the document is considered
                     inconsistent, additional status is returned through
                     "alertItem" which states what is causing the inconsistency.
*)
(*$S fexec2 *)
function docConsistCheck(var alertItem : integer) : integer;
var status     : integer;
    theSelKind : selKind;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   status := InConsErr;
   case g.pnlWithChg of
      ctblPnl,
      cWidePnl : begin
                    GetSelKind(theSelKind);
                    if theSelKind = aCellTxtSl then alertItem := ValMsg
                    else status := ok
                 end;
      cFormPnl : alertItem := FormMsg;
      nullPnl  : status := ok;
      otherwise  if curDiHeight > 0 then alertItem := DBoxMsg
                 else status := ok
   end;
   docConsistCheck := status
(*$IFC DebugVersion *)
   ;StatusMess(status)
(*$ENDC *)
end;


{$S fexec}
procedure doGoTo {newRange: range};
        {Scroll to show newRange.  If we're in shoValues mode, select the new
        range, set the undoRec to uGoTo, and the g.ActivePnl to tblPnl.}
var temp : rgJust;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug}
   if tracing then LCtraceMess(170);
{$ENDC }
   case newRange.rKind of
      a1CellRg,
      aRowGrid,
      aColGrid : temp := nearJ;
      otherwise  temp := nearJ
   end;
   ShoNewRx(g.curRange, newRange, temp);
   if (newRange.rkind <> a1CellRg) or (tableState = tValues) then begin
      g.curRange := newRange;
      DoSelCR(g.curRange, g.curTimeout, SelectCell);
      SetAnchorCell(g.curRange.loRow, g.curRange.loCol);
      g.ActivePnl := tblPnl
   end
end;


{$S LCfexec}
procedure doPointing(mouseLoc: point; startPointing : boolean);
        {The user started pointing at a range (the name of which to insert
         into the formula display) via an option-click, and has now extended
         that selection by dragging the mouse or by doing a shift-click.
         Change the range "name" within the formula, leaving the blinking
         cursor at the end and be prepared for possible subsequent calls to
         doPointing with a false in start pointing.  Also, set g.curnbrhood.}
var mouseRel : point;
    status   : integer;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   AdjToPnl(tblPnl, mouseLoc, mouseRel);
   if startpointing then begin
      justPointing := true;
      PointAtRg(mouseRel, formRg, g.curnbrhood) {in TmsProcs}
   end
   else MovePointer(mouseRel, formRg, g.curnbrhood); {in TmsProcs}
   AdjRect(tblPnl, g.curnbrhood);
   NewFormRg(formRg, status); {in formMgr}
   editTried(status);
end;


{$S fexec}
procedure drawTWide{newStr: str40};
        {Erase the tWidePnl, set tWideStr to newStr, then draw the
         current tWideStr in tWidePnl}
var tWideRect : rect;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug}
   if tracing then LCtraceMess(172);
{$ENDC }
   SetPnlPort(tWidePnl);
   GetPnlRect(tWidePnl, tWideRect);
   BackPat(white);
   EraseRect(tWideRect);
   tWideStr := newStr;
   putSysStr(4, coBase, tWideStr)
end;


(***************************************.*******.************************************
   editTried - The user just typed a key or did a cut, paste, or clear.  If the
               edit was successful (ie, status = 0), then handle any inter-panel
               interactions and set lastPnl to the g.ActivePnl.  If the user just
               typed a key, the screen may be scroll to insure that the cell
               receiving the text is visible.  Wheter the screen is scrolled
               depends if the screen has be changed recently (ie, scrolled, grown).
************************************************************************************)
{$S fexec}
procedure editTried {status: integer};
var ValStr : str40;
    maxNum : integer;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug}
   if tracing then begin
      LCtraceMess(173);
      writeln('Status = ', status:1)
   end;
{$ENDC }
   case status of
      0: begin
            if g.pnlWithChg <> g.ActivePnl then GetItem(myMenus[MenuStat], ValueItem, @ValStr);
            case g.ActivePnl of
               ctblPnl,
               cwidePnl   : begin
                               if g.pnlWithChg <> g.ActivePnl then begin
                                  if g.LCFlag then begin
                                     emptyFormDisp;
                                     drawForm
                                  end;
                                  DrawTWide(ValStr)
                               end;
                               if ScreenChged then ShoNewRx(g.curRange, g.curRange, nearJ)
                            end;
               cformPnl   : begin
                               if g.pnlWithChg <> cformPnl then begin
                                  ClearSel(status);
                                  ExitSel;
                                  NewRgimg(g.curRange);
                                  DrawTWide(ValStr)
                               end;
                               if ScreenChged then ShoNewRx(g.curRange, g.curRange, nearJ)
                            end;
              {cdialogPnl : begin  end;}
              {cCoPnl     : begin  end;}
              {otherwise    begin  end}
            end;
            g.pnlWithChg := g.ActivePnl
         end;
      fldProtected : doStopAlert(105); {Can''t edit this; it''s protected.}
      fldFull      : begin
                        case g.ActivePnl of
                           cFormPnl : maxNum := maxFormLen + 1;
                           cCoPnl   : maxNum := maxCoLen + 1;
                           otherwise  maxNum := maxLenWav
                        end;
                        doParamAlert(maxNum);
                        doStopAlert(106);       {Too many characters. Last edit ignored}
                     end;
      nullSelect   : doStopAlert(107);          {Selection is zero-length}
      invScrapType : doStopAlert(108);          {Scrap type can''t be dealt with}
      invOpType    : doStopAlert(109);          {Last operation can''t be undone}
{$IFC fDebug}
      noRoomForRuns: if TempCode then PrintMess(25);
{$ENDC }
   end
end;


{$S fexec}
procedure endLastSel(mouseRel: point);
        {The mouse has gone down.  End the last selection unless the current
        and the last selection were both in Table Editor panels.  Leave the
        port set to the g.ActivePnl.}
var doEnd    : boolean;
    newFKind : fldKind;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug}
   if tracing then LCtraceMess(175);
{$ENDC }
   if not tblEditPnl(g.ActivePnl) then begin
      peekAtNewSel(g.ActivePnl, mouseRel, newFKind);
      doEnd := (newFKind = plainTxt);
   end
   else doEnd := true;
   if doEnd then begin
      if tblEditPnl(g.lastActPnl) and not tblEditPnl(g.ActivePnl) then begin
         ExitSel;
         if g.curRange.rKind = a1CellRg then NewRgimg(g.curRange)
      end;
      if not tblEditPnl(g.lastActPnl) then begin
         endPnlSel;
         nullPnlSel;
         if g.lcflag then EndRgInsert; {in formMgr}
      end;

   end; { If }

   { Clean up BGPnl if you just moved out of it }
   if (g.BGFlag) then iBGExit(BGProcInfo.BGExit);
end;


{$S fexec }
(***************************************.*******.********************************************************
   EnterLastSelection - enters the data that was last selected.  A call willl be made to recalculate the
                        model once the data is entered.  "EnterLastSelection" will determine if the last
                        selection should be reselected once the calculation is done and call the
                        particular recalculation routine that will do that.  The last selection will not
                        reselected if:

                           1. the last panel that was changed was the coordinate panel.
                           2. the new panel that is being selected is the coordinate panel.

                        The reason for not reselected the selected areas in these two cases is to
                        avoid an unnecessary scroll of the screen the all indications are that the
                        user is abount to scroll the screen anyway.   `status` is returned for the
                        status of the recalculation.
****************************************.*******.*******************************************************)
procedure EnterLastSelection(var status : integer);
var reSelect : boolean;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   case g.pnlWithChg of
      nullPnl,
      cCoPnl   : reSelect := false;
      otherwise  reSelect := (g.ActivePnl <> cCoPnl)
   end;
   if reSelect then EnterReSelect(status)
   else status := ForceEnter(status)
end;


{$S fexec }
procedure fxNewRgImg;
        {Do whatever the Folder Exec must do when the matrix manager
        changes values within the matrix.
        For LisaCalc, there's nothing to do.}

        { For Business Graphics, it determines the current size of the
        data table and sets up how many data values to plot. Then it
        replots the graph accordingly. }

begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug}
   if tracing then LCtraceMess(177);
{$ENDC }

   if g.BGflag then iBGNewRgImg(BGProcInfo.BGNewRgImg)
end; (* fxNewRgImg *)


{$S fexec}
procedure fxRgChged {rg: range};
        {Stub until Steve provides real routine}
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug}
   if tracing then begin
      LCtraceMess(178);
      writeln('Range passed is ', wrg(rg))
   end;
{$ENDC }
   if g.BGflag then iBGRgChged(rg, BGProcInfo.BGrgChged)
end;


{$S fexec}
procedure goToRg {var status: integer};
        {User has changed the co display and typed an enter key.  Make the typed
        range the current range.  Set g.ActivePnl to tblPnl.  Return status = 0
        iff the new range is valid.  Give error diagnostics if not.}
var newRange : range;
    errRec   : errRecord;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug}
   if tracing then LCtraceMess(179);
{$ENDC }
   WouldAlert(myMenus[MenuAlert], HitGoTo);
   if tableState = tValues then begin
      nullCR;
      if g.curRange.rKind = a1CellRg then begin
         DumpSel;
         NewRgImg(g.curRange)
      end
   end;
   getCoRg(newRange, status, errRec);
   if status = 0 then begin
      ClrUndo;
      SetUndo(uGoto, coPnl);
      doGoTo(newRange)
   end
   else begin
           shoPnlErr(coPnl, status, errRec, g.curTimeout);
           if tableState = tValues then begin
              g.CurRange.rKind := aNullRg;
              FixRange(g.curRange)
           end
        end
end;


{$S fexec}
function ignoreEv (ev: EventKind; pnl:pnlIndex) : boolean;
        {Return true iff this event within this pnl should be totally
        ignored at this time.}
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug}
   if tracing then LCtraceMess(180);
{$ENDC }
   case ev of
      ButtonDown,
      ButtonUp:   ignoreEv := ((pnl in [formPnl, widePnl]) and (g.curRange.rKind <> a1CellRg)) or
                              (pnl in [cScrapPnl, StCoPnl, ScoPnl, StblPnl]);
      otherwise   ignoreEv := false
   end
end;


{$S OpenClose}
procedure initAppMgrs;
        {Initialize application-specific panel (and other) managers}
var errNum : integer;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug}
   if tracing or traceOC then LCtraceMess(181);
{$ENDC }
   nullPnlSel;
   SetRunFormat(coFldH, nil, tblFntId.Fam, setFont, errNum);
   emptyCoDisp;
   reDrawCo;
   if g.LCFlag then begin
      SetRunFormat(formFldH, nil, tblFntId.Fam, setFont, errNum);
      emptyFormDisp;
      reDrawForm
   end
   else iInitBGPnl(BGProcInfo.InitBGPnl)
end;


{$S statmgr}
procedure MapTitlePt (var pnl: pnlIndex; var mousePt: point);
        {The mouse went down at (folder-relative) mousePt, which is in pnl.
        If pnl is a title panel (tCoPnl, tWidePnl, or tFormPnl), then map
        the point to the left edge of the corresponding non-title panel
        (coPnl, widePnl, or formPnl, respectively) and change pnl to that
        pnl index.  Otherwise, do nothing.}
var pRect  : rect;
    newPnl : pnlIndex;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug}
   if tracing then LCtraceMess(183);
{$ENDC }
   case pnl of
      ctCoPnl   : newPnl := coPnl;
      ctWidePnl : newPnl := widePnl;
      ctFormPnl : newPnl := formPnl;
      otherwise   newPnl := pnl;
   end;
   if pnl <> newPnl then begin
      pnl := newPnl;
      GetPnlRect(pnl, pRect);
      AdjRect(pnl, pRect);
      mousePt.h := pRect.topleft.h;
   end
end;


{$S statmgr}
procedure MseDnApp(mouseRel: point);
        {The mouse has gone down in an application-specific panel.  We've
        already set double/triple-click flags and ended any previous selections.
        Do the new selection.  Also, if the new selection is in the co Pnl,
        do the implied enter, re-select of the current range, and end selection
        before continuing}
var status    : integer;
    theActPnl : pnlIndex;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug}
   if tracing then LCtraceMess(184);
{$ENDC }
   if c.FndDouble then begin
      pnlWSel(mouseRel, g.curNbrhood, g.curTimeout); { in LCSMgr }
      adjRect(g.ActivePnl, g.curNbrhood);
   end
   else if c.FndTriple then begin
           pnlSelFld(g.curTimeout);                       { in LCSMgr }
           g.curNbrhood := dfltNbrhood;
        end
   else begin
           status := 0;
           if (g.ActivePnl = coPnl) and (g.curRange.rKind = a1CellRg) then begin
              theActPnl := g.ActivePnl;

              EnterLastSelection(status);

              WouldAlert(myMenus[MenuAlert], SelEnter);
              if status = 0 then begin
                 ExitSel;
                 g.ActivePnl := theActPnl;
                 g.curRange.rKind := aNullRg;
                 FixRange(g.curRange)
              end
           end;
           if status = 0 then begin
              doPnlSel(g.ActivePnl, mouseRel, g.curNbrhood, g.curTimeout);
              adjRect(g.ActivePnl, g.curNbrhood)
           end
        end
end;


{$S fexec}
procedure selecCoord(var newCR: boolean);
        {The user has hit APPLE-G or the selecCoords menu item.  Do it.  Return
        newCR = true since the current range has changed.}
var status    : integer;
    nullPoint : point;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug}
   if tracing then LCtraceMess(186);
{$ENDC }
   nullPoint.h  := 0;
   nullPoint.v  := 0;
   g.lastActPnl := g.ActivePnl;
   endLastSel(nullPoint);
   EnterLastSelect(status);
   if tableState <> tValues then doStopAlert(10004)       {Can't type while formulas are showing}
   else begin
           if status = 0 then begin
              ExitSel;
              pnlSelAll(coPnl, g.curTimeout);
              g.ActivePnl := coPnl;
              g.curRange.rKind := aNullRg;
              FixRange(g.curRange)
           end;
           newCR := false
        end
end;


{$S LCfexec}
procedure selecForm(var newCR: boolean);
        {The user has hit APPLE-F or the SelectFormula menu item.  Do it.  Return
        newCR = false since the current range has not changed. If the selection
        is in the Coordinates Display when the APPLE-F is hit, then do a GoTo
        first.}
var status    : integer;
    nullPoint : point;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug}
   if tracing then LCtraceMess(187);
{$ENDC }
   status := 0;
   if g.ActivePnl = coPnl then begin
      endPnlSel;
      nullPnlSel;
      goToRg(status);
      if status = 0 then chgStatPnl(false)
   end;
   if tableState <> tValues then doStopAlert(10004)       {Can't type while formulas are showing}
   else begin
           SelInLeftCorner;
           if g.curRange.rKind = a1CellRg then begin
              g.lastActPnl := g.ActivePnl;
              g.ActivePnl := formPnl;
              nullPoint.h := 0;
              nullPoint.v := 0;
              endLastSel(nullPoint);
              pnlSelAll(formPnl, g.curTimeout);
              newCR := false;
           end
        end
end;


{$S OpenClose}
procedure SetPnlInfo(pnl : pnlIndex; dh, dv : integer);
var deltaP : point;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   SetPt(deltaP, dh, dv);
   SetUpPnl(pnl, Folder, 0, 0, deltaP)
end;


(***************************************.*******.************************************
   SetAppPnls - sets up the starting location for each panel.  The starting location
                is based on the font information found in the system font.
                The actual widths of the panels will be set by "SetApp2Pnls".
*)
{$S OpenClose}
procedure setAppPnls;
var folderDh    : pixel;
    folderDv    : pixel;
    statPaneBot : pixel;
    deltaP      : point;
    temp        : pixel;
    fRect       : rect;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug}
   if tracing or traceOC then LCtraceMess(188);
{$ENDC }
   GetPnlRect(FolderPnl, fRect);
   with fRect do begin
      { subtract off the scroll box width }
      folderDh    := botRight.h - dhSbox + 1;
      folderDv    := botRight.v - dvSbox + 1;
      statPaneBot := (1 + ord(g.LCflag)) * pnlLineDv + 1;

      {Set up editable portion of the coordinate panel}
      SetPnlInfo(coPnl, tCoDh + 1, 0);

      {Set up editable portion of the wide angle panel}
      SetPnlInfo(widePnl, coDh + 2 + tWideDh, 0);

      {Set up title portion of the coordinate panel}
      SetPnlInfo(tCoPnl, 0, 0);

      {Set up title portion of the wide angle panel}
      SetPnlInfo(tWidePnl, coDh + 1, 0);

      (* Set up the table panel - in the left part of the folder for BG *)
      SetPt(deltaP, 0, statPaneBot + pnlSplitDv);
      if g.LCflag then temp := folderDh
      else temp := CMin(initTblDh, folderDh div 3);
      SetUpPnl(tblPnl, Folder, temp, 0, deltaP);

      if g.BGflag then begin
         (* Set up the graphing panel *)
         (* compute the h and v positions of the upper left point of BGPnl.
            The graph panel starts at Table width + Scrollbox width *)
         SetPnlInfo(BGPnl, initTblDh + dhsBox, StatPaneBot + pnlSplitDv);

         (* Set up the null formula panel & formula title panel *)
         SetPnlInfo(formPnl, folderDh, folderDv);
         SetPnlInfo(tFormPnl, folderDh, folderDv)
      end
      else begin
              (* Set up the null BG panel *)
              SetPnlInfo(BGPnl, folderDh, folderDv);

              (* Set up editable formula panel *)
              SetPnlInfo(formPnl, tFormDh + 1, pnlLineDv + 1);

              (* Set up title formula panel *)
              SetPnlInfo(tFormPnl, 0, pnlLineDv + 1)
           end
   end;
   SetApp2Pnls
end;


(***************************************.*******.************************************
   SetApp2Pnls - This routine complements "SetAppPnls" by setting the widths for all
                 the panels based on the current FOLDERpnl size.  This routine is
                 normally used whenever FOLDERpnl is changed.  UPDATETABLE is used to
                 insure that the table portion of the folder gets updated, despite of
                 the application running.
*)
{$S growCode} {& splitCode & fontChanging }
procedure setApp2Pnls;
var folderDh,
    folderDv    : pixel;
    statPaneBot : pixel;
    offset      : point;
    temp        : integer;
    fRect       : rect;

    (*$S BGspecific *)
    procedure setBG2Pnls;
    var width      : pixel;
        BGrect     : rect;
        tblPnlRect : rect;
        deltaP     : point;
        pnlOffSet  : point;
    begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

       (* Set up the table panel in the left part of the folder. Width
          set to min of old width or (new folder size minus scroll bar width) *)
       GetPnlRect(tblPnl, tblPnlRect);
       GetPnlRect(BGPnl, BGRect);
       SetPt(pnlOffSet, 0, statPaneBot + pnlSplitDv);
       if pnlOffSet.v <> tblPnlRect.top then begin
          MovePnl(tblPnl, pnlOffSet);
          pnlOffSet.h := BGRect.left;
          MovePnl(BGPnl, pnlOffSet)
       end;
       Width := CMin(FolderDh - dhsBox, TblPnlRect.Right);
       ChgPnlSize(tblPnl, Width, folderDv - (statpaneBot + pnlSplitDv));

       (* Set up the graphing panel on right side of folder.  *)
       with DeltaP do begin
          h := width + dhsBox;
          v := StatPaneBot + pnlSplitdV;
       end;
       MovePnl(BGPnl, DeltaP);
       ChgPnlSize(BGPnl, FolderDh - DeltaP.h, FolderDv - DeltaP.v)
    end;


    {$S growCode} {& splitCode & fontChanging }
    procedure setLC2Pnls;
    var formRect  : rect;
        tblRect   : rect;
        pnlOffSet : point;
    begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

       GetPnlRect(formPnl, formRect);
       SetPt(pnlOffSet, tFormDh + 1, pnlLineDv + 1);
       if pnlOffSet.v <> formRect.top then begin
          MovePnl(formPnl, pnlOffSet);
          pnlOffSet.h := 0;
          MovePnl(tFormPnl, pnlOffSet)
       end;
       GetPnlOffset(formPnl, pnlOffSet);
       ChgPnlSize(formPnl, folderDh - pnlOffSet.h, pnlLineDv);

       GetPnlRect(tblPnl, tblRect);
       SetPt(pnlOffSet, 0, statPaneBot + pnlSplitDv);
       if pnlOffSet.v <> tblRect.top then MovePnl(tblPnl, pnlOffSet);
       ChgPnlSize(tblPnl, folderDh, folderDv - (statpaneBot + pnlSplitDv))
    end;


(*$S fexec2 *)
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug}
   if tracing or traceOC then LCtraceMess(189);
{$ENDC }
   GetPnlRect(FolderPnl, fRect);
   with fRect do begin
      { subtract off the scroll box width }
      folderDh    := botRight.h - dhSbox + 1;
      folderDv    := botRight.v - dvSbox + 1;
      statPaneBot := (1 + ord(g.LCflag)) * pnlLineDv + 1;

      {Set up panels in PnlArray}
      GetPnlOffset(CoPnl, offset);
      temp := Cmin(folderDh - offset.h, coDh - tCoDh - 1);
      ChgPnlSize(CoPnl, temp, pnlLineDv);

      GetPnlOffset(widePnl, offset);
      ChgPnlSize(widePnl, folderDh - offset.h, pnlLineDv);

      GetPnlOffset(tCoPnl, offset);
      temp := Cmin(folderDh - offset.h, tCoDh);
      ChgPnlSize(tCoPnl, temp, pnlLineDv);

      GetPnlOffset(tWidePnl, offset);
      temp := Cmin(folderDh - offset.h, tWideDh);
      ChgPnlSize(tWidePnl, temp, pnlLineDv);

      if g.LCFlag then begin
         SetLC2pnls;
         GetPnlOffset(tFormPnl, offset);
         temp := Cmin(folderDh - offset.h, tFormDh);
         ChgPnlSize(tFormPnl, temp, pnlLineDv)
      end
      else SetBG2pnls;
      if ProcsInited then TmResize
   end
end;


{$S fexec}
procedure shoNewRx {oldRg: range; newRg: range; just: rgJust};
        { calls TM:shoNewRg and sets the thumb position }
var pt      : point;
    rowVis  : boolean;
    colVis  : boolean;
    rgVis   : boolean;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug}
   if tracing then LCtraceMess(195);
{$ENDC }
   tmcurvwpt(pt);
   rowvis := snipAllInBand(rowkind, pt.v, newRg.lorow);
   colVis := snipAllInBand(colkind, pt.h, newRg.locol);

   rgVis := true;
   case newRg.rKind of
      a1CellRg,
      aRectRg  : rgVis := rowVis and colVis;
      aRowGrid,
      aRowRg   : rgVis := rowVis;
      aColGrid,
      aColRg   : rgVis := colVis
   end;
   if not rgVis then begin
      shoNewRg(oldRg, newRg, just);
      SetPnlPort(FolderPnl);
      ReThumbLC(false)
   end;
   ScreenChged := false
end;


{$S OpenClose}
procedure sizeScrap;
        {Set up the three sub-scrap panels: StCoPnl, ScoPnl, and StblPnl.
         Assume that cScrapPnl is set up}
var scrapRect : rect;
   tempPt     : point;
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug}
   if tracing then LCtraceMess(196);
{$ENDC }
   GetPnlRect(cScrapPnl, scrapRect);

   { Subtract off the scroll bars }
   With scrapRect do Begin
      right  := right  - dhsBox + 1;
      bottom := bottom - dvsBox + 1;
   end;

   SetPt(tempPt, 0, 0);
   SetUpPnl(StCoPnl, ScrapFolder, tCoDh, pnlLineDv, tempPt);
   SetPt(tempPt, tCoDh + 1, 0);
   SetUpPnl(ScoPnl, ScrapFolder, coDh - tCoDh - 1, pnlLineDv, tempPt);
   SetPt(tempPt, 0, pnlLineDv + 1);
   SetUpPnl(StblPnl, ScrapFolder, scrapRect.right, scrapRect.bottom - tempPt.v, tempPt)
end;


{$S fexec}
procedure size2Scrap{ptNewBR: TPt};
{ Procedure to resize the scrap panel and the individual scrap panels.
  ptNewBR is the new lower right corner of the scrap folder }

VAR rh, rv : TR;
Begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

   SetPort(ScrapFolder);
   FlushRects(rH, rV);
   SetSBRect(hsbHScrap, rH);
   SetSBRect(hsbVScrap, rV);

   { reset the size of the entire scrap panel }
   ChgPnlSize(cScrapPnl, ptNewBR.h, ptNewBR.v);

   { change all the subordinate scrap panels }
   { Scrap title coord panel }
   ChgPnlSize(stCoPnl, tCoDh, pnlLineDv);

   { Scrap coords panel. Sub off the title width and the scroll box width }
   ChgPnlSize(sCoPnl, coDh - (tCoDh + dhsBox + 1), pnlLineDv);

   {Scrap tblpnl. Subtract off the coords panel height and the scroll box hgt }
   ChgPnlSize(sTblPnl, ptNewBR.h - dhSBox + 1, ptNewBR.v - (pnlLineDv + dvSBox + 1) + 1);
end;


{$S fexec2}
procedure updFolder{forceUpdate : boolean};
        {An update event has come in for a folder (the main folder, the scrap,
         or the dialog box).  If it's the main folder or the scrap, redraw
         the status panel and whatever part of the tblPnl needs updating.
        }
var sKind      : scrapKind;
    gErr       : integer;
    upDatePort : grafPtr;
    done       : boolean;

    (*$S BGspecific *)
    procedure UpdateBGgraph;
    begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

       GetPnlRect(BGPnl, RefreshRect);
       RectRgn(BGRefreshRgn, RefreshRect);
       iEraseBG(BGProcInfo.EraseBG);
       iDrawGraph(BGProcInfo.DrawGraph)
    end;


(*$S fexec2 *)
begin
(*$IFC fTraceLC *) LogCall; (*$ENDC *)

{$IFC fDebug}
   if tracing then LCtraceMess(214);
{$ENDC }
   if procsDying then exit(upDfolder);
   upDatePort := WMEvent.who;
   done := true;
   if forceUpdate then
      if WMevent.what = FolderDeActivate then begin
         GetPort(upDatePort);
         WMOpenPicture(upDatePort)
      end
      else begin
              upDatePort := activeFolder;
              done       := false
           end;

   repeat
      with WMEvent do
         if upDatePort = Folder then begin

            if WMEvent.what <> FolderDeActivate then BeginUpdate(upDatePort);
            dividPnls;
            if g.curRange.rKind = a1CellRg then reDrawSel(true)
            else EraseWav;         { in file TMSProcs3 }
            reDrawCo;              { in file CoMgr }
            if g.lcFlag then reDrawForm;

            SetPnlPort(folderPnl);
            if WMEvent.what = FolderDeActivate then tmShoNewTbl
            else if SectPnl(tblPnl, upDatePort, upDatePort^.visrgn, resultRgnH) then ShoTbl(resultRgnH);

            if g.BGFlag then UpdateBGgraph;

            { Draw the scroll bars }
            if (WMEvent.what <> FolderDeActivate) and (ActiveFolder = Folder) then
               if g.LCFlag then LCDrawScrolls
               else BGDrawScrolls
            else ClearScrolls;
            if WMEvent.what <> FolderDeActivate then ValidRgn(resultRgn)
         end
         else if upDatePort = ScrapFolder then begin
                 setPnlPort(cScrapPnl);
                 if WMEvent.what <> FolderDeActivate then BeginUpdate(upDatePort);
                 getScrKind(sKind);
                 case sKind of
                    cellsKind : drwCellScrap;
                    noKind    : ClrScrapFld;
                    txtKind   : shoScrapFld;
                    graphKind : begin
                                   DrawUscrap(gErr);
                                   SetPnlPort(ScrapPnl);
                                   DrwScrScrollBars(ScrapFolder = ActiveFolder);
   {$IFC fDebug}
                                   if gErr <> 0 then PrintMess(32)
   {$ENDC }
                                end;
   {$IFC fDebug}
                    otherwise   PrintMess(40)
   {$ENDC }
                 end
              end

         else if upDatePort = DialogFolder then begin
                 if curDiHeight > 0 then begin
                    setPnlPort(dialogPnl);
                    BeginUpdate(upDatePort);
                    reDrawDi
                 end
              end
   {$IFC fDebug}
         else if upDatePort = FilerFolder then PrintMess(37)

         else if upDatePort = AlertFolder then PrintMess(38)

         else if upDatePort = MenuFolder then PrintMess(39)
         else begin
                 writeln('Update Event for who =',ord(upDatePort):1,'# docs: ',numdocsopen:1);
                 BeginUpdate(folder)
              end;
   {$ENDC }

      ;if WMEvent.what <> FolderDeActivate then EndUpdate(upDatePort)
      else begin
              SetPort(upDatePort);
              WMClosePicture
           end;

      if forceUpdate then
         if upDatePort = Folder then upDatePort := DialogFolder
         else done := true
   until done;
end;
ÿ