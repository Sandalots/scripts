{ Copyright 1983, 1984, Apple Computer Inc.

  This unit contains global declarations and common routines for LisaDesk.
}

{$E Err.text}

UNIT DmGlobals;


INTERFACE

{$SETC flrDebug   := FALSE }        { Set to FALSE for release software }
{$SETC flrJrnl    := FALSE }       { ditto }
{$SETC network    := FALSE }       { FALSE for 1st release }


USES
   {$U HwInt      } HwInt,
   {$U UnitStd    } UnitStd,
   {$U UnitHz     } UnitHz,
   {$U Storage    } Storage,
   {$U QuickDraw  } QuickDraw,
   {$U FontMgr    } FontMgr,
   {$U LibOS/SysCall  } SysCall,
   {$U WM.Events  } Events,
   {$U WM.Windows } Windows,
   {$U WM.Menus   } Menus,
   {$U AlertMgr   } AlertMgr,
   {$U WmlStd     } WmlStd,
   {$U WmlSb      } WmlSb,
   {$U dbenv      } dbenv,
   {$U dbdecl1    } dbdecl1,
   {$U PMDecl     } PMDecl,
   {$IFC flrDebug}
   {$U apdm/tracecalls.obj} tracecalls,
   {$ENDC}
   {$U PrStdInfo  } PrStdInfo,
   {$U PrPublic   } PrPublic,
   {$U FilerComm  } FilerComm;

{$SETC flrSymbols := fSymOk }      { set to false for release version }


CONST

      { tracing flags }


{$IFC flrDebug }
   dbgFiler         = TRUE;    { For user non-switchable debugging output }
{$ELSEC }
   dbgFiler         = FALSE;
   trcFiler         = FALSE;
   trcDialog        = FALSE;
   trcAttribute     = FALSE;
   trcCatalog       = FALSE;
   trcEvents        = FALSE;
   trcFDocCtrl      = FALSE;
   trcFEntry        = FALSE;
   trcFVolCtrl      = FALSE;
   testIOerr        = FALSE;
   fakeNoHeap       = FALSE;
   skipFlushing     = FALSE;
   trcCalls         = FALSE;
{$ENDC }


      { LDSN (logical data segment number) assignments }

   flrHeapLDSN    = 1;    { theHeap for filer structures }
   dBLDSN         = 4;    { DB heap & buffers }
   dfCopyLDSN     = 6;    { DocCtrl ds used to copy diskfiles, bound only during copy }

      { Some random error constants for attributes and password protection }

   AttCatError    = 1100; { error in using the catalog w/setting up attributes }
   AttOsError     = 1101; { error w/OS call in setting up attributes }
   PassWrongType  = 1102; { error returned from SetObjPwd, wrong object type to set password on }

      { screen sizes }

   screenWidth    = 720;
   screenHt       = 364;

      { logging constants - for creating Filer system.log entries }

   flrEvType      = 100;     { Identifies a Filer generated log entry }

   opOpenDoc      = 1;       { User actions }
   opPutBackDoc   = 2;
   opSuspendDoc   = 11;
   opCopyDoc      = 3;
   opMoveDoc      = 4;
   opPowerDown    = 5;
   opTwigBackup   = 6;
   opProBackup    = 7;
   opDiskBackup   = 8;
   opBootDismount = 9;
   opBootRemount  = 10;
   opOpenContainer= 12;
   opCloseContainer=13;

      { misc. constants }

   fMaxStrLen     = 63;      { max string size for catalog names }
   nullErr        = MAXINT;  { used w/ SysErr to indicate no low level err }
   iconNamFont    = tile7;   { font used for icon titles }
   labelVersion   = 2;       { addes split and totalSize fields }
   sizeSpace    =   30;             { Reserve blocks on each backup disk       }        {***Sally Rhodes***}
   saveSpace    =  '{!SomeSpace}';  { Name of temp diskfile during disk backup }        {***Sally Rhodes***}


TYPE

      { misc types }

   PtrString     = ^Str255;
   CharArray     =  PACKED ARRAY [1..MAXINT] OF CHAR;
   PtrCharArray  = ^CharArray;
   String80      =  STRING[80];
   String10      =  STRING[10];
   NumberStr     =  STRING[11];   { large enough for - max LongInt }
   TfsInfoPtr    = ^Fs_Info;
   TQInfoPtr     = ^Q_Info;
   FmaxStr       =  STRING[fMaxStrLen];
   FmaxStrPtr    = ^FmaxStr;
   FmaxStrHdl    = ^FmaxStrPtr;


     { token for catalog records }

   IDType = LONGINT;

   TcatRID =
      RECORD
      fatherId : IdType;   { parent Id.  If given, improves access time }
      uniqueId : IdType;   { the actual identifier.  Unique on a volume }
      END;


     { data layout in file label for redundant info. }

   LabelFmt =
      RECORD
      version:  INTEGER;
      name:     FmaxStr;
      kind:     INTEGER;
      toolOnly: BOOLEAN;    { tools only, TRUE => does not support documents }
      multiDoc: BOOLEAN;    { tools only, TRUE => supports many docs at a time }
      windLoc:  Rect;       { window location and size }
                       { added fields for version 2 }
      split:    INTEGER;    { Split obj sequence #, nullInt2 if not split }
      totalSize:LONGINT;    { 0 or disk blocks needed to recombine splits }
      parentID: IdType;     { id of parent folder or disk *** not used yet ***}
      END;


VAR

   {$IFC flrDebug }

      { tracing flags }

   testIOerr,
   fakeNoHeap,
   skipFlushing,
   trcCalls,
   trcDB,
   trcFiler,
   trcDialog,
   trcAttribute,
   trcPassword,
   trcCatalog,
   trcEvents,
   trcFDocCtrl,
   trcFEntry,
   trcFVolCtrl : BOOLEAN;

   {$ENDC }


      { misc. globals }

   curEvent        : EventRecord;  { last event received }
   alertRefnum     : INTEGER;      { OS file refnum for the phrase file }
   flrAlert        : TAlertFile;   { Set by ReadAlerts }
   termEventRefnum : INTEGER;      { for sys_son_term event }
   startTime       : LongInt;      { used for performance measurements }
   printProcess    : LongInt;      { id of background printing process }


PROCEDURE ArrayToStr(pArray : PtrCharArray; pStr : PtrString; numChars : INTEGER);
PROCEDURE BeginTiming(msg: FmaxStr);
PROCEDURE BuildToolName(toolID: IdType; VAR toolName: E_Name);
PROCEDURE ClipScrollBars(window: WindowPtr; setClipping: BOOLEAN);
PROCEDURE CopyStrg(srcStrg: FmaxStr; VAR dstStrg: FmaxStr);
PROCEDURE DownShift(pStr: PtrString);
PROCEDURE EndTiming;
PROCEDURE GetStrg(strHdl: FmaxStrHdl; VAR strg: FmaxStr);
PROCEDURE GetString(alertNumber: INTEGER; pStr: TpStr255);
PROCEDURE GetVersion (version: INTEGER; VAR tempStr: FmaxStr);
PROCEDURE IntToStr(number : LongInt; VAR strg : NumberStr);
PROCEDURE KillStrg(strHdl: FmaxStrHdl);
PROCEDURE LabelDefault(VAR labelRec: LabelFmt);
FUNCTION  LabelIO(writeLabel: BOOLEAN; disk: E_Name;
                     toolNum,docNum: IDtype; VAR labelRec: LabelFmt): BOOLEAN;
FUNCTION  Max(a,b : INTEGER) : INTEGER;
PROCEDURE MakePrintProcess(VAR err: INTEGER);
FUNCTION  Min(a,b : INTEGER) : INTEGER;
FUNCTION  NewHdl(byteCount: INTEGER): Handle;
FUNCTION  NewStrg(strg: FmaxStr): FmaxStrHdl;
PROCEDURE QuoteName(VAR name: FmaxStr);
FUNCTION  RandInt(min,max: INTEGER): INTEGER;
PROCEDURE SetStrg(strHdl: FmaxStrHdl; strg: FmaxStr);
PROCEDURE StrToInt(str : E_Name; start : INTEGER;
                   VAR number : LongInt; VAR numDigits : INTEGER);
PROCEDURE SysErr(err: INTEGER; lowlvlerr: INTEGER);
FUNCTION  TopWindow: WindowPtr;

{#############################################################################}

IMPLEMENTATION

{$IFC flrSymbols }
   {$D+}
{$ELSEC }
   {$D-}
{$ENDC }

{$IFC flrDebug }
   {$R+}
{$ELSEC }
   {$R-}
{$ENDC }

PROCEDURE PrintLabel(VAR labelRec: LabelFmt; bytes: LONGINT);         FORWARD;

{$S flrDm1 }
PROCEDURE ArrayToStr{pArray: PtrCharArray; pStr: PtrString; numChars: INTEGER};
{  moves chars into string and sets count }
VAR
   dstPtr : PtrCharArray;

BEGIN
{$IFC flrDebug } IF trcCalls THEN ALogCall; {$ENDC}
IF numChars < 0 THEN numChars := 0;

dstPtr := Pointer(ORD(pStr)+1);
MoveLeft(pArray^,dstPtr^,numChars);

{$R-}
pStr^[0] := CHR(numChars);

{$IFC flrDebug }
   {$R+}
{$ENDC }
END;



{$S flrDebug }
PROCEDURE BeginTiming{msg: FmaxStr};
{ displays msg and records the current time.  To be used with "EndTiming" }
BEGIN
{$IFC flrDebug } IF trcCalls THEN ALogCall; {$ENDC}
{$IFC flrDebug }
WRITE('--> Begin timing: ',msg);
startTime := Timer;
{$ENDC }
END;



{$S flrDm1 }
PROCEDURE BuildToolName {toolID: IdType; VAR toolName: E_Name};
{ constructs the O/S name for a tool from the tool number }
VAR intStr: NumberStr;
BEGIN
{$IFC flrDebug } IF trcCalls THEN ALogCall; {$ENDC}
IF trcCatalog THEN WRITELN ('BuildToolName: toolID is ',toolID:1);
IntToStr (toolID, intStr);
toolName := CONCAT ('{T',intStr,'}');
IF trcCatalog THEN WRITELN ('   exit BuildToolName: name is ',toolName);
END;



{$S flrAll }
PROCEDURE ClipScrollBars{window: WindowPtr; setClipping: BOOLEAN};
{ Sets or clears clipping on scroll bars }
VAR r: Rect;
BEGIN
{$IFC flrDebug } IF trcCalls THEN ALogCall; {$ENDC}
WITH window^ DO
  BEGIN
  r := portRect;
  IF setClipping AND (window <> filerFolder) THEN
     BEGIN
     r.right := r.right-dhSBox+1;
     r.bottom := r.bottom-dvSBox+1;
     END;
  RectRgn(clipRgn,r);
  END;
END;



{$S flrDm }
PROCEDURE CopyStrg{srcStrg: FmaxStr; VAR dstStrg: FmaxStr};
{ copies src to dst moving minimum number of chars }
BEGIN
{$IFC flrDebug } IF trcCalls THEN ALogCall; {$ENDC}
MoveLeft(srcStrg,dstStrg,LENGTH(srcStrg)+1);
END;



{$S flrOpen }
PROCEDURE DownShift{ pStr: PtrString };
{ converts all upper case chars in "s" to lower case }
VAR i: INTEGER;
BEGIN
{$IFC flrDebug } IF trcCalls THEN ALogCall; {$ENDC}
FOR i := 1 TO LENGTH(pStr^) DO
   IF (pStr^[i] <= 'Z') THEN
      IF (pStr^[i] >= 'A') THEN pStr^[i] := CHR(ORD(pStr^[i]) + 32);
END;



{$S flrDebug }
PROCEDURE EndTiming;
{ Used with "BeginTiming."  Prints time since startTime }
VAR totalTime: LongInt;
    seconds,hundredths: INTEGER;
    secStrg,hunStrg: NumberStr;
BEGIN
{$IFC flrDebug } IF trcCalls THEN ALogCall; {$ENDC}
{$IFC flrDebug }
totalTime := Timer - startTime;

seconds := totalTime DIV 1000;
hundredths := (totalTime MOD 1000) DIV 10;
IntToStr(seconds,secStrg);
IntToStr(hundredths,hunStrg);
IF hundredths < 10 THEN hunStrg := CONCAT('0',hunStrg);

WRITELN('  --- time: ',secStrg,'.',hunStrg,' seconds');
{$ENDC }
END;



{$S dmMain }
PROCEDURE GetStrg{strHdl: FmaxStrHdl; VAR strg: FmaxStr};
{ fetches the string from the heap }
BEGIN
{$IFC flrDebug } IF trcCalls THEN ALogCall; {$ENDC}
IF strHdl = NIL THEN
   strg := ''
ELSE
   MoveLeft(strHdl^^,strg,LENGTH(strHdl^^)+1);
END;



{$S dmMain }
PROCEDURE GetString{ alertNumber: INTEGER; pStr: TpStr255 };
{ fetches a string constant from the phrase file }
BEGIN
{$IFC flrDebug } IF trcCalls THEN ALogCall; {$ENDC}
GetAlert(flrAlert,alertNumber,pStr);
END;

{********************************************************************************}
{$S flrAtt}
PROCEDURE GetVersion {* version: INTEGER; VAR tempStr: FmaxStr *};
{version number is stored as three 5-bit fields in an integer, thus xx.yy.zz, where the field value
 has the range of 0-31 (2^5).  The values are stored in the integer as they appear, ie. zz is in the
 lower (least significant) bits, xx in the top, with bit 15 (top bit) being reserved (=0). }

VAR
   first, second, third : NumberStr;
   a,b,c                : INTEGER;

BEGIN
{$IFC flrDebug } IF trcCalls THEN ALogCall; {$ENDC}

IF (version < 0) OR (version > MAXINT) THEN
   tempStr := 'Invalid'
ELSE
   BEGIN
   a := version DIV 1024;
   c := version MOD 32;
   b := (version MOD 1024) - c;
   IntToStr (a, first);
   IntToStr (b, second);
   IntToStr (c, third);
   tempStr := CONCAT(first,'.',second,'.',third);
   END;

END; {of PROC GetVersion}

{********************************************************************************}
{$S flrAll }
PROCEDURE IntToStr{ number : LongInt; VAR strg : NumberStr };
{  converts an integer into a string }
VAR
   i,
   numDigits : INTEGER;
   negative  : BOOLEAN;
   bkwdStrg  : NumberStr;

BEGIN
{$IFC flrDebug } IF trcCalls THEN ALogCall; {$ENDC}
negative := (number < 0);
numDigits := 0;

REPEAT
   numDigits := numDigits + 1;
   bkwdStrg[numDigits] := CHR(ORD('0') + ABS(number MOD 10));
   number := number DIV 10
UNTIL number = 0;

IF negative THEN
   BEGIN
   numDigits := numDigits + 1;
   bkwdStrg[numDigits] := '-';
   END;

FOR i := 1 TO numDigits DO
   strg[i] := bkwdStrg[numDigits-i+1];

{$R-}
strg[0] := CHR(numDigits);

{$IFC flrDebug }
   {$R+}
{$ENDC }
END;



{$S flrAll }
PROCEDURE KillStrg{strHdl: FmaxStrHdl};
{ releases the space occupied by a string in the heap }
BEGIN
{$IFC flrDebug } IF trcCalls THEN ALogCall; {$ENDC}
IF strHdl <> NIL THEN
   DisposeHandle(Pointer(ORD(strHdl)));
END;


{$S flrDm1 }
PROCEDURE LabelDefault{VAR labelRec: LabelFmt};
BEGIN
{$IFC flrDebug } IF trcCalls THEN ALogCall; {$ENDC}
WITH labelRec DO
   BEGIN
   version  := labelVersion;
   name     := '';
   kind     := nilKind;
   multiDoc := FALSE;
   toolOnly := FALSE;
   split    := nullInt2;
   parentID := 1;   { idDisk }
   totalSize:= 0;
   END;
END;


{$S flrCopy }
FUNCTION LabelIO{writeLabel: BOOLEAN; disk: E_Name;
                 toolNum,docNum: IDtype; VAR labelRec: LabelFmt): BOOLEAN};
{ reads/writes the label for a given tool or document }
LABEL 99;
VAR actual: LongInt;
    err: INTEGER;
    docStr,toolStr: NumberStr;
    maxLabel: ARRAY [1..max_Label_Size] OF -128..127;
    prefix,path: PathName;
    toolName: E_Name;

BEGIN
IF trcCatalog THEN WRITELN('LabelIO write = ',writeLabel);

LabelIO := FALSE;
prefix := disk;
IF LENGTH(disk) <> 0 THEN
   prefix := CONCAT('-',disk,'-');

IF docNum = 0 THEN
   BEGIN
   BuildToolName(toolNum,toolName);
   path := CONCAT(prefix,toolName,'obj');
   END
ELSE
   BEGIN
   IntToStr(toolNum,toolStr);
   IntToStr(docNum,docStr);
   IF toolNum <= 0    THEN path := CONCAT(prefix,'{F',docStr,'}')
   ELSE IF docNum = 0 THEN path := CONCAT(prefix,'{T',toolStr,'}')
   ELSE                    path := CONCAT(prefix,'{D',docStr,'T',toolStr,'}');
   END;

IF trcCatalog THEN WRITELN('   path = ',path);

Read_Label(err,path,ORD(@maxLabel),max_Label_Size,actual);
IF err > 0 THEN IF writeLabel
THEN actual := SIZEOF(labelRec)        { creating a new label }
ELSE GOTO 99;                          { no label to read }

IF writeLabel THEN
   BEGIN
   MoveLeft(labelRec,maxLabel[1],SIZEOF(labelRec));
   Write_Label(err,path,ORD(@maxLabel),actual,actual);
   END
ELSE
   BEGIN
   MoveLeft(maxLabel[1],labelRec,SIZEOF(labelRec));
   IF labelRec.version < labelVersion THEN
      BEGIN                             { update the label w/ defaults for the new fields }
      { defaults for versions < 2 go here }
      IF labelRec.version < 2 THEN WITH labelRec DO
         BEGIN                          { defaults for added ver. 2 fields}
         split := nullInt2;
         totalSize := 0;
         parentID := 1;                 { idDisk }
         END;
      labelRec.version := labelVersion;
      END;
   END;


99:
LabelIO := (err <= 0);

IF trcCatalog THEN IF err <= 0 THEN
   BEGIN
   IF writeLabel
   THEN WRITELN('label written:')
   ELSE WRITELN('label read:');
   PrintLabel(labelRec,actual);
   END;
IF trcCatalog THEN WRITELN('   exit LabelIO: err is ',err:1);
END;


{$S  }
PROCEDURE MakePrintProcess {VAR err: INTEGER};

{ Starts the background printing process.  References intrinsic unit shared
  globals in the "PrStd" unit }

VAR
   setPriErr    : INTEGER;
   path         : PathName;
   entry        : NameString;

BEGIN
{$IFC flrDebug } IF trcCalls THEN ALogCall; {$ENDC}
path := PrParentProcess;    { name of code file }
Make_Process(err,printProcess,path,entry,termEventRefnum);
IF err <= 0 THEN
   SetPriority_Process(setPriErr,printProcess,120)
ELSE
   printProcess := prNilProcess;
END;



{$S flrAll }
FUNCTION Max{a,b: INTEGER): INTEGER};
{ returns maximum of 2 integers }
BEGIN
{$IFC flrDebug } IF trcCalls THEN ALogCall; {$ENDC}
IF a > b THEN Max := a ELSE Max := b;
END;



{$S flrAll }
FUNCTION Min{a,b : INTEGER) : INTEGER};
{ returns minimum of 2 integers }
BEGIN
{$IFC flrDebug } IF trcCalls THEN ALogCall; {$ENDC}
IF a <= b THEN Min := a ELSE Min := b;
END;

{$S flrDm1 }
FUNCTION NewHdl{byteCount: INTEGER): Handle};
{ cover procedure for heap allocater }
VAR hdl: Handle;
BEGIN
{$IFC flrDebug } IF trcCalls THEN ALogCall; {$ENDC}
IF fakeNoHeap THEN
   IF (ABS(Random) MOD 3) = 0 THEN
      BEGIN
      StopAlert(flrAlert,109);   { not enough room ... }
      NewHdl := NIL;
      EXIT(NewHdl);
      END;

hdl := NewHandle(byteCount);
IF hdl = hNil THEN hdl := NIL;

NewHdl := hdl;
END;



{$S flrDm }
FUNCTION NewStrg{strg: FmaxStr): FmaxStrHdl};
{ allocates space in the heap and and copies the string }
VAR strHdl: FmaxStrHdl;
BEGIN
{$IFC flrDebug } IF trcCalls THEN ALogCall; {$ENDC}
strHdl := Pointer(ORD(NewHandle(LENGTH(strg)+1)));
CopyStrg(strg,strHdl^^);
NewStrg := strHdl;
END;



{$S flrDebug }
PROCEDURE PrintLabel{VAR labelRec: LabelFmt; bytes: LONGINT};
BEGIN
{$IFC flrDebug } IF trcCalls THEN ALogCall; {$ENDC}

WITH labelRec DO
IF (version <= 0) OR (version > labelVersion) THEN
   BEGIN
   WRITELN ('Bad Label! version is reported to be ',version:1);
   EXIT(PrintLabel);
   END;

WITH labelRec DO
   BEGIN
   WRITELN('version: ',version:1);
   WRITELN('name: ',name);
   WRITELN('kind: ',kind:1);
   WRITELN('toolOnly: ',toolOnly);
   WRITELN('multiDoc: ',multiDoc);
   WRITELN('windLoc: --');
   IF version > 1 THEN
      BEGIN
      WRITELN('split: ',split:1);
      WRITELN('totalSize: ',totalSize:1);
      WRITELN('parentId: ',parentID:1);
      END;
   END;
END;



{$S dmMain }
PROCEDURE QuoteName{VAR name: FmaxStr};
{  adds quotes, handles alert mgr overflow case }
VAR maxSize: INTEGER;
BEGIN
{$IFC flrDebug } IF trcCalls THEN ALogCall; {$ENDC}
maxSize := SIZEOF(TparamAlert)-3;  { room for quotes }
IF LENGTH(name) > maxSize THEN
   name := CONCAT(COPY(name,1,maxSize-3),'...');
name := CONCAT('"',name,'"');
END;



{$S flrMisc }
FUNCTION RandInt{min,max: INTEGER): INTEGER};
{ returns a random integer x, such that: min <= x <= max }
BEGIN
{$IFC flrDebug } IF trcCalls THEN ALogCall; {$ENDC}
RandInt := min + (ABS(Random) MOD (max-min+1));
END;



{$S flrDm }
PROCEDURE SetStrg{strHdl: FmaxStrHdl; strg: FmaxStr};
{ inserts string adjusting size }
BEGIN
{$IFC flrDebug } IF trcCalls THEN ALogCall; {$ENDC}
IF strHdl <> NIL THEN
   BEGIN
   SetSize(Pointer(ORD(strHdl)),LENGTH(strg)+1);
   CopyStrg(strg,strHdl^^);
   END;
END;



{$S flrUtil }
PROCEDURE StrToInt{str : E_Name; start : INTEGER;
                   VAR number : LongInt; VAR numDigits : INTEGER};
   { converts string begining at 'start' to a LongInt }
VAR
   index : INTEGER;
   endOfNum : BOOLEAN;
BEGIN
{$IFC flrDebug } IF trcCalls THEN ALogCall; {$ENDC}
IF trcFiler THEN WRITELN('StrToInt  str = "',str,'"  start = ',start:1);
number := 0;
index := start;
endOfNum := FALSE;
WHILE (index <= LENGTH(str)) AND (NOT endOfNum) DO
   BEGIN
   IF str[index] IN ['0'..'9'] THEN
      BEGIN
      number := 10*number + (ORD(str[index]) - ORD('0'));
      index := index + 1;
      END
   ELSE
      endOfNum := TRUE;
   END;
numDigits := index - start;
IF trcFiler THEN WRITELN('   exit StrToInt.  number = ',number:1);
END;


{$S flrUtil }
PROCEDURE SysErr{err: INTEGER; lowlvlerr: INTEGER};

{ Called when the Filer has found an unexpected condition. 'err' denotes the
  calling module and a specific call within that module. 'lowlvlerr' is an
  error passed up to the Filer from low-level software e.g. the OS. If desired,
  SysErr can be called to check for lowlvlerr=0 which will cause SysErr to do
  nothing. If there is no low level error, use 'nullErr'.

  The system informs the user of the error via the alert box, waits for the user
  to respond, and then reboots the system. Optionally the user can be given the
  choice of proceeding, i.e., SysErr returns to the caller, instead or a reboot.
  This option is enabled by -err, i.e., negative errs are warnings.

  The error ranges for each module are:

  FGlobals       100-199
  FEntry         200-299
  Catalog        300-399
  FDocCtrl       800-899
  FVolCtrl       900-999
  Filer         1000-1099
}

VAR
    response:  FMaxStr;
    tempStr:   NumberStr;
    errStr:    STRING[5];
    lowLvlStr: STRING[5];

BEGIN
{$IFC flrDebug } IF trcCalls THEN ALogCall; {$ENDC}
IF dbgFiler THEN
   IF lowlvlerr < 0 THEN WRITELN ('   warning: ',lowlvlerr:1);
IF lowlvlerr <= 0 THEN EXIT (SysErr);

IntToStr (ABS(err), tempStr);            { Build alert msg params           }
errStr := tempStr;
IF lowlvlerr <> nullErr
THEN BEGIN
  IntToStr (lowlvlerr, tempStr);
  lowlvlstr := CONCAT('/',tempStr);
  END
ELSE lowlvlstr := '';
IF dbgFiler THEN WRITELN('*** SysErr ',errStr,lowlvlStr);

ParamAlert (errStr, lowlvlstr, '');

IF (err < 0) OR dbgFiler
THEN BEGIN
  IF AskAlert (flrAlert, 201) = 2
  THEN EXIT (SysErr)
  ELSE HALT;
  END
ELSE BEGIN
  StopAlert (flrAlert, 200);
  HALT;
  END
END;



{$S dmMain }
FUNCTION TopWindow{* : WindowPtr *};
{ return topmost visible window }
VAR window: WindowPeek;
BEGIN
{$IFC flrDebug } IF trcCalls THEN ALogCall; {$ENDC}
IF trcFiler THEN WRITELN('TopWindow');
window := Pointer(ORD(dialogFolder));
window := window^.nextWindow;   { point to first normal window }
WHILE window <> Nil DO
   BEGIN
   IF window^.visible THEN
      BEGIN
      TopWindow := Pointer(ORD(window));
      EXIT(TopWindow);
      END;
   window := window^.nextWindow;
   END;
TopWindow := Pointer(ORD(filerFolder));   { couldn't find one, return filer }
END;

END.

ÿ