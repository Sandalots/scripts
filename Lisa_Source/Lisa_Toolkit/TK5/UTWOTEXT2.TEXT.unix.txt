{UTwoText2}

{changed 01/11/84 1843  New compiler with "CREATION" block usually gone, THISCLASS, etc.}
{changed 01/03/83 1955  New compiler with "CREATION" excised}
{changed 12/30/83 1851  NewClass has fewer args}
{changed 12/26/83 2125  NewMainView now sent to the panel}

CONST
    tkTeam = 'Apple ToolKit/32 Team';
    testUniv = 888;
VAR drawingOK:  BOOLEAN;

METHODS OF TTwoBoxView;

    FUNCTION  {TTwoBoxView.}CREATE{(object: TObject; heap: THeap; itsPanel: TPanel; itsExtent: LRect;
                              itsPrintable: BOOLEAN): TTwoBoxView};
    BEGIN
        {$IFC fTrace}BP(11);{$ENDC}
        IF object = NIL THEN
            object := NewObject(heap, THISCLASS);
        SELF := TTwoBoxView(itsPanel.NewView(object, itsExtent, TPrintManager.CREATE(NIL, heap),
                                             stdMargins, TRUE));
        {$IFC fTrace}EP;{$ENDC}
    END;

   {$IFC fDebugMethods}
    PROCEDURE {TTwoBoxView.}Fields{(PROCEDURE Field(nameAndType: S255))};
    BEGIN
        TView.Fields(Field);
        Field('txtImg1: TTextImage');
        Field('txtImg2: TTextImage');
    END;
    {$ENDC}

    FUNCTION {TTwoBoxView.}CursorAt{(mouseLPt: LPoint): TCursor};
    BEGIN
        {$IFC fTrace}BP(10);{$ENDC}
        IF LPtInLRect(mouseLPt, SELF.txtImg1.extentLRect) THEN
            CursorAt := textCursor
        ELSE IF LPtInLRect(mouseLPt, SELF.txtImg2.extentLRect) THEN
            CursorAt := textCursor
        ELSE
            CursorAt := SUPERSELF.CursorAt(mouseLPt);
        {$IFC fTrace}EP;{$ENDC}
    END;

    PROCEDURE {TTwoBoxView.}Draw;
    VAR r:  LRect;
    BEGIN
        {$IFC fTrace}BP(10);{$ENDC}
        PenNormal;
        PenSize(1,1);
        r := SELF.txtImg1.extentLRect;
        InsetLRect(r, -4, -6);
        FrameLRect(r);
        r := SELF.txtImg2.extentLRect;
        InsetLRect(r, -4, -6);
        FrameLRect(r);
        SELF.txtImg1.Draw;
        {$IFC fTrace}EP;{$ENDC}
    END;

     PROCEDURE {TTwoBoxView.}MousePress{(mouseLPt: LPoint)};
     VAR panel:         TPanel;
         aSelection:    TSelection;

        PROCEDURE NoSelInPanels(obj: TObject);
        VAR panel:  TPanel;
        BEGIN
            panel := TPanel(obj);
            panel.Highlight(panel.selection, hOnToOff);
            aSelection := panel.Selection.FreedAndReplacedBy(panel.view.NoSelection);
            panel.Highlight(panel.selection, hOffToOn);
        END;

     BEGIN
        {$IFC fTrace}BP(11);{$ENDC}
        panel := SELF.panel;
        IF LPtInLRect(mouseLPt, SELF.txtImg1.extentLRect) THEN
            SELF.txtImg1.MousePress(mouseLpt)
        ELSE IF LPtInLRect(mouseLPt, SELF.txtImg2.extentLRect) THEN
            SELF.txtImg2.MousePress(mouseLpt)
        ELSE
            SELF.panel.window.panels.Each(NoSelInPanels);
        {$IFC fTrace}EP;{$ENDC}
    END;

    FUNCTION  TTwoBoxView.OKToDrawIn(lRectInView: LRect): BOOLEAN;
    BEGIN
        {$IFC fTrace}BP(10);{$ENDC}
        OKToDrawIn := drawingOK;
        {$IFC fTrace}EP;{$ENDC}
    END;

    FUNCTION {TTwoBoxView.}SeesSameAs{(image: TImage): BOOLEAN; DEFAULT};
    BEGIN
        {$IFC fTrace}BP(3);{$ENDC}
        SeesSameAs := (image.view = SELF) OR InClass(image.view, TOneBoxView);
        {$IFC fTrace}EP;{$ENDC}
    END;
END;


METHODS OF TOneBoxView;

    FUNCTION  {TOneBoxView.}CREATE{(object: TObject; heap: THeap; itsPanel: TPanel; itsExtent: LRect;
                              itsPrintable: BOOLEAN): TOneBoxView};
    BEGIN
        {$IFC fTrace}BP(11);{$ENDC}
        IF object = NIL THEN
            object := NewObject(heap, THISCLASS);
        SELF := TOneBoxView(itsPanel.NewView(object, itsExtent, TPrintManager.CREATE(NIL, heap),
                                             stdMargins, TRUE));
        {$IFC fTrace}EP;{$ENDC}
    END;

   {$IFC fDebugMethods}
    PROCEDURE {TOneBoxView.}Fields{(PROCEDURE Field(nameAndType: S255))};
    BEGIN
        TView.Fields(Field);
        Field('txtImg: TTextImage');
    END;
    {$ENDC}

    FUNCTION {TTwoBoxView.}CursorAt{(mouseLPt: LPoint): TCursor};
    BEGIN
        {$IFC fTrace}BP(10);{$ENDC}
        IF LPtInLRect(mouseLPt, SELF.txtImg.extentLRect) THEN
            CursorAt := textCursor
        ELSE
            CursorAt := SUPERSELF.CursorAt(mouseLPt);
        {$IFC fTrace}EP;{$ENDC}
    END;

    PROCEDURE {TOneBoxView.}Draw;
    VAR r:  LRect;
    BEGIN
        {$IFC fTrace}BP(10);{$ENDC}
        PenNormal;
        PenSize(1,1);
        r := SELF.txtImg.extentLRect;
        InsetLRect(r, -4, -6);
        FrameLRect(r);
        SELF.txtImg.Draw;
        {$IFC fTrace}EP;{$ENDC}
    END;

     PROCEDURE {TOneBoxView.}MousePress{(mouseLPt: LPoint)};
     VAR panel:         TPanel;
         aSelection:    TSelection;
        PROCEDURE NoSelInPanels(obj: TObject);
        VAR panel:  TPanel;
        BEGIN
            panel := TPanel(obj);
            panel.Highlight(panel.selection, hOnToOff);
            aSelection := panel.Selection.FreedAndReplacedBy(panel.view.NoSelection);
            panel.Highlight(panel.selection, hOffToOn);
        END;

     BEGIN
        {$IFC fTrace}BP(11);{$ENDC}
        panel := SELF.panel;
        IF LPtInLRect(mouseLPt, SELF.txtImg.extentLRect) THEN
            SELF.txtImg.MousePress(mouseLpt)
        ELSE
            SELF.panel.window.panels.Each(NoSelInPanels);
        {$IFC fTrace}EP;{$ENDC}
    END;

    FUNCTION  TOneBoxView.OKToDrawIn(lRectInView: LRect): BOOLEAN;
    BEGIN
        {$IFC fTrace}BP(10);{$ENDC}
        OKToDrawIn := drawingOK;
        {$IFC fTrace}EP;{$ENDC}
    END;

    FUNCTION {TOneBoxView.}SeesSameAs{(image: TImage): BOOLEAN; DEFAULT};
    BEGIN
        {$IFC fTrace}BP(3);{$ENDC}
        SeesSameAs := (image.view = SELF) OR InClass(image.view, TTwoBoxView);
        {$IFC fTrace}EP;{$ENDC}
    END;

BEGIN
    drawingOK := TRUE;
END;


METHODS OF TSideView;

    FUNCTION  {TSideView.}CREATE{(object: TObject; heap: THeap; itsPanel: TPanel; itsExtent: LRect;
                              itsPrintable: BOOLEAN): TSideView};
    BEGIN
        {$IFC fTrace}BP(11);{$ENDC}
        IF object = NIL THEN
            object := NewObject(heap, THISCLASS);
        SELF := TSideView(itsPanel.NewView(object, itsExtent, NIL, stdMargins, TRUE));
 {$H-}  SetLRect(SELF.r, 10, 10, 130, 150);  {$H+}
        {$IFC fTrace}EP;{$ENDC}
    END;


    PROCEDURE {TSideView.}Draw;
    BEGIN
        {$IFC fTrace}BP(10);{$ENDC}
        PenNormal;
        PenSize(1,1);
        FrameLOval(SELF.r);
        {$IFC fTrace}EP;{$ENDC}
    END;

     PROCEDURE {TSideView.}MousePress{(mouseLPt: LPoint)};
     VAR panel:         TPanel;
         aSelection:    TSelection;
        PROCEDURE NoSelInPanels(obj: TObject);
        VAR panel:  TPanel;
        BEGIN
            panel := TPanel(obj);
            panel.Highlight(panel.selection, hOnToOff);
            aSelection := panel.Selection.FreedAndReplacedBy(panel.view.NoSelection);
            panel.Highlight(panel.selection, hOffToOn);
        END;

     BEGIN
        {$IFC fTrace}BP(11);{$ENDC}
        panel := SELF.panel;
        panel.window.panels.Each(NoSelInPanels);
        IF LPtInLRect(mouseLPt, SELF.r) THEN
            aSelection := SELF.panel.selection.FreedAndReplacedBy(
                        TSideSelection.CREATE(NIL, SELF.Heap, SELF, mouseLPt));
        panel.Highlight(panel.selection, hOffToOn);
        {$IFC fTrace}EP;{$ENDC}
    END;


END;

METHODS OF TSideSelection;

    FUNCTION  {TSideSelection.}CREATE{(object: TObject; heap: THeap; itsView: TView;
                                       itsAnchorLPt: LPoint): TSideSelection};
    BEGIN
        {$IFC fTrace}BP(9);{$ENDC}
        IF object = NIL THEN
            object := NewObject(heap, THISCLASS);
        SELF := TSideSelection(TSelection.CREATE(object, heap, itsView, somethingKind, itsAnchorLPt));
        {$IFC fTrace}EP;{$ENDC}
    END;

    PROCEDURE {TSideSelection.}Highlight{(highTransit: THighTransit};
    VAR sideView:   TSideView;
    BEGIN
        {$IFC fTrace}BP(10);{$ENDC}
        SetPenState(highPen[highTransit]);
        PenSize(4,4);
        sideView := TSideView(SELF.view);
        FrameLOval(sideView.r);
        {$IFC fTrace}EP;{$ENDC}
    END;

END;

METHODS OF TMyProcess;


    FUNCTION  {TMyProcess.}CREATE{: TMyProcess};
    BEGIN
        {$IFC fTrace}BP(11);{$ENDC}
        SELF := TMyProcess(TProcess.CREATE(NewObject(mainHeap, THISCLASS), mainHeap));
        {$IFC fTrace}EP;{$ENDC}
    END;


    FUNCTION  {TMyProcess.}NewDocManager{(volumePrefix: TFilePath): TDocManager};
    BEGIN
        {$IFC fTrace}BP(11);{$ENDC}
        NewDocManager := TMyDocManager.CREATE(NIL, mainHeap, volumePrefix);
        {$IFC fTrace}EP;{$ENDC}
    END;


END;



METHODS OF TMyDocManager;


    FUNCTION  {TMyDocManager.}CREATE{(object: TObject; heap: THeap; itsPathPrefix: TFilePath): TMyDocManager};
    BEGIN
        {$IFC fTrace}BP(11);{$ENDC}
        IF object = NIL THEN
            object := NewObject(heap, THISCLASS);
        SELF := TMyDocManager(TDocManager.CREATE(object, heap, itsPathPrefix));
        {$IFC fTrace}EP;{$ENDC}
    END;


    FUNCTION  {TMyDocManager.}NewWindow{(heap: THeap; wmgrID: TWindowID):TWindow};
    BEGIN
        {$IFC fTrace}BP(11);{$ENDC}
        NewWindow := TMyWindow.CREATE(NIL, heap, wmgrID);
        {$IFC fTrace}EP;{$ENDC}
    END;


END;



METHODS OF TMyWindow;


    FUNCTION  {TMyWindow.}CREATE{(object: TObject; heap: THeap; itsWmgrID: TWindowID): TMyWindow};
    BEGIN
        {$IFC fTrace}BP(10);{$ENDC}
        IF object = NIL THEN
            object := NewObject(heap, THISCLASS);
        SELF := TMyWindow(TWindow.CREATE(object, heap, itsWmgrID, TRUE));
        {$IFC fTrace}EP;{$ENDC}
    END;


    PROCEDURE {TMyWindow.}BlankStationery;
    VAR viewLRect:      LRect;
        r:              Rect;
        panel:          TPanel;
        sidePanel:      TPanel;
        topPanel:       TPanel;
        twoBoxView:     TTwoBoxView;
        oneBoxView:     TOneBoxView;
        aSelection:     TSelection;
        txtImg1:        TTextImage;
        txtImg2:        TTextImage;
        imageLRect:     LRect;
        paraFormat:     TParaFormat;
        text:           TText;
        paragraph:      TEditPara;
        sideView:       TSideView;
        styleSheet:     TStyleSheet;

    BEGIN
        {$IFC fTrace}BP(10);{$ENDC}
        SetLRect(viewLRect, 0, 0, 5000, 3000);
                                               {v,h}
        panel := TPanel.CREATE(NIL, SELF.Heap, SELF, 20, 30, [aScroll, aSplit], [aScroll, aSplit]);
        twoBoxView := TTwoBoxView.CREATE(NIL, SELF.Heap, panel, viewLRect, TRUE);

        {CREATE TWO TEXTIMAGES WITH DIFFERENT SIZE BOXES}
        SetLRect(imageLRect, 50, 20, 250, 150);
        styleSheet := TStyleSheet.CREATE(NIL, SELF.Heap);
        SELF.styleSheet := styleSheet;
        paraFormat := TParaFormat.CREATE(NIL, SELF.Heap, styleSheet);
        WITH paraFormat DO
            BEGIN
            firstIndent := cHorizMarg*4;
            leftIndent := cHorizMarg;
            rightIndent := cHorizMarg;
            spaceBelowPara := cVertMarg*2;
            END;

        text := TText.CREATE(NIL, SELF.Heap, styleSheet);
        paragraph := TEditPara.CREATE(NIL, SELF.Heap, 0, paraFormat);
        text.paragraphs.InsLast(paragraph);

        txtImg1 := TTextImage.CREATE(NIL, SELF.Heap, twoBoxView, imageLRect, text, FALSE);
        {Put this textImage on text's textImage list.  NOTE: We do NOT put txtImg2 on the list
         since it is not a "head" text image}
        text.txtImgList.InsLast(txtImg1);

(*      {Create an initial paraimage}
        txtImg1.imageList.InsLast(txtImg1.NewParaImage(paragraph, imageLRect, 0, 0));   *)

        SetLRect(imageLRect, 350, 40, 500, 175);
        txtImg2 := TTextImage.CREATE(NIL, SELF.Heap, twoBoxView, imageLRect, text, FALSE);

        {Set up the links and back links and set the second textImage's headTxtImg to txtImg1}
        txtImg1.nextTxtImg := txtImg2;
        txtImg1.tailTxtImg := txtImg2;
        txtImg2.prevTxtImg := txtImg1;
        txtImg2.headTxtImg := txtImg1;

        twoBoxView.txtImg1 := txtImg1;
        twoBoxView.txtImg2 := txtImg2;

        txtImg1.RecomputeImages(actionNone, TRUE);


        {CREATE Side panel}
        sidePanel := panel.Divide(h, 25, percentFromEdge, [windowCanResizeIt, userCanResizeIt],
                              0, [], []);
        sideView := TSideView.CREATE(NIL, SELF.Heap, sidePanel, viewLRect, TRUE);


        {CREATE top panel}
        topPanel := panel.Divide(v, 33, percentFromEdge, [userCanResizeIt],
                              50, [aScroll, aSplit], [aScroll, aSplit]);
        oneBoxView := TOneBoxView.CREATE(NIL, SELF.Heap, topPanel, viewLRect, TRUE);

        SetLRect(imageLRect, 20, 10, 520, 70);
        txtImg1 := TTextImage.CREATE(NIL, SELF.Heap, oneBoxView, imageLRect, text, FALSE);
        {Put this textImage on text's textImage list.}
        text.txtImgList.InsLast(txtImg1);

        {Create an initial paraimage}
(*      txtImg1.imageList.InsLast(txtImg1.NewParaImage(paragraph, imageLRect, 0, 0));   *)
        txtImg1.RecomputeImages(actionNone, TRUE);
        oneBoxView.txtImg := txtImg1;


        {$IFC fTrace}EP;{$ENDC}
    END;

    FUNCTION  {TMyWindow.}CanDoCommand{(cmdNumber: TCmdNumber; VAR checkIt: BOOLEAN): BOOLEAN};
    BEGIN
        {$IFC fTrace}BP(12);{$ENDC}
        CASE cmdNumber OF
            cmdRefresh:
                CanDoCommand := TRUE;
            cmdTrUPara:
                BEGIN
                CanDoCommand := TRUE;
                checkIt := fParaTrace;
                END;
            cmdTrUText:
                BEGIN
                CanDoCommand := TRUE;
                checkIt := fTextTrace;
                END;
            uToggleOK:
                BEGIN
                CanDoCommand := TRUE;
                checkIt := drawingOK;
                END;
            testUnivText:
                CanDoCommand := TRUE;
            OTHERWISE
                CanDoCommand := SUPERSELF.CanDoCommand(cmdNumber, checkIt);
            END;
        {$IFC fTrace}EP;{$ENDC}
    END;


    FUNCTION  {TMyWindow.}NewCommand{(cmdNumber: TCmdNumber): TCommand};
    VAR twoBoxView:     TTwoBoxView;
        selection:  TSelection;
        PROCEDURE InvalAll(obj: TObject);
        BEGIN
            TPanel(obj).Invalidate;
        END;
    BEGIN
        {$IFC fTrace}BP(12);{$ENDC}
        NewCommand := NIL;
        CASE cmdNumber OF
     (*   uSelAll:
                BEGIN
                twoBoxView := SELF.selectPanel.view;
                SELF.selectPanel.Highlight(SELF.selectPanel.selection, hOnToOff);
                selection := SELF.selectPanel.selection.FreedAndReplacedBy
                                                    (twoBoxView.txtImg1.text.SelectAll(twoBoxView.txtImg1));
                END;
    *)
            cmdRefresh:
                SELF.panels.Each(InvalAll);
            cmdTrUPara:
                fParaTrace := NOT fParaTrace;
            cmdTrUText:
                fTextTrace := NOT fTextTrace;
            uToggleOK:
                drawingOK := NOT drawingOK;
            testUnivText:
                IF clipBoard.hasUniversalText THEN
                    WriteLn('The clipboard has universal text!')
                ELSE
                    WriteLn('The clipboard does NOT have universal text!');
            OTHERWISE
                NewCommand := SUPERSELF.NewCommand(cmdNumber);
            END;
        {$IFC fTrace}EP;{$ENDC}
    END;


END;

ÿ