{utility function to convert a menu command number into a TColor}
FUNCTION CvtCmdNumber(cmdNumber: TCmdNumber): TColor;
BEGIN
    CvtCmdNumber := cmdNumber - uWhite + colorWhite;
END;

METHODS OF TCheckerboard;

    FUNCTION  {TCheckerboard.}CREATE{(object: TObject; heap: THeap): TCheckerboard};
    VAR rowIndex:   INTEGER;
        colIndex:   INTEGER;
    BEGIN
        {$IFC fTrace}BP(11);{$ENDC}
        IF object = NIL THEN
            object := NewObject(heap, THISCLASS);
        SELF := TCheckerboard(object);

        { initialize to all white squares }
        WITH SELF DO
            FOR rowIndex := 1 TO numrows DO
                FOR colIndex := 1 TO numcols DO
                    boxes[rowIndex, colIndex] := colorWhite;
        {$IFC fTrace}EP;{$ENDC}
    END;

    {$IFC fDebugMethods}
    PROCEDURE {TCheckerboard.}Fields{(PROCEDURE Field(nameAndType: S255))};
    BEGIN
        SUPERSELF.Fields(Field);
        Field('boxes: ARRAY[1..8, 1..8] OF Byte');
        Field('');
    END;
    {$ENDC}

    PROCEDURE {TCheckerboard.}ColorAlternating{(evenColor, oddColor: TColor)};
    VAR rowIndex:   INTEGER;
        colIndex:   INTEGER;
    BEGIN
        {$IFC fTrace}BP(11);{$ENDC}
        FOR rowIndex := 1 TO numRows DO
            FOR colIndex := 1 TO numCols DO
                IF ODD(rowIndex+colIndex) THEN
                    SELF.SetColor(rowIndex, colIndex, oddColor)
                ELSE
                    SELF.SetColor(rowIndex, colIndex, evenColor);
        {$IFC fTrace}EP;{$ENDC}
    END;

    FUNCTION  {TCheckerboard.}GetColor{(rowIndex, colIndex: INTEGER): TColor};
    BEGIN
        {$IFC fTrace}BP(11);{$ENDC}
        GetColor := SELF.boxes[rowIndex, colIndex];
        {$IFC fTrace}EP;{$ENDC}
    END;

    PROCEDURE {TCheckerboard.}SetColor{(rowIndex, colIndex: INTEGER; color: TColor)};
    BEGIN
        {$IFC fTrace}BP(11);{$ENDC}
        SELF.boxes[rowIndex, colIndex] := color;
        {$IFC fTrace}EP;{$ENDC}
    END;

END;


METHODS OF TSamProcess;

{ Required boiler plate }
    FUNCTION  {TSamProcess.}CREATE;
    BEGIN
        {$IFC fTrace}BP(11);{$ENDC}
        SELF := TSamProcess(TProcess.CREATE(NewObject(mainHeap, THISCLASS), mainHeap));
        {$IFC fTrace}EP;{$ENDC}
    END;

{ Required boiler plate }
    FUNCTION  {TSamProcess.}NewDocManager{(volumePrefix: TFilePath; openAsTool: BOOLEAN): TDocManager};
    BEGIN
        {$IFC fTrace}BP(11);{$ENDC}
        NewDocManager := TSamDocManager.CREATE(NIL, mainHeap, volumePrefix);
        {$IFC fTrace}EP;{$ENDC}
    END;

END;


METHODS OF TSamDocManager;

{ Required boiler plate}
    FUNCTION  {TSamDocManager.}CREATE{(object: TObject; heap: THeap;
                                        itsPathPrefix: TFilePath): TSamDocManager};
    BEGIN
        {$IFC fTrace}BP(11);{$ENDC}
        IF object = NIL THEN
            object := NewObject(heap, THISCLASS);
        SELF := TSamDocManager(TDocManager.CREATE(object, heap, itsPathPrefix));
        {$IFC fTrace}EP;{$ENDC}
    END;

{ Required boiler plate}
    FUNCTION  {TSamDocManager.}NewWindow{(heap: THeap; WmgrId: TWindowId): TWindow};
    BEGIN
        {$IFC fTrace}BP(11);{$ENDC}
        NewWindow := TSamWindow.CREATE(NIL, heap, wmgrID);
        {$IFC fTrace}EP;{$ENDC}
    END;

END;


METHODS OF TSamView;

    FUNCTION  {TSamView.}CREATE{(object: TObject; heap: THeap; itsExtent: LRect;
                                    itsPanel: TPanel): TSamView};
        VAR res:        Point;
            margins:    LRect;
    BEGIN
        {$IFC fTrace}BP(11);{$ENDC}
        IF object = NIL THEN
            object := NewObject(heap, THISCLASS);

        res := screenRes;
        SetLRect(margins, res.h, res.v, res.h, res.v); {1" each, whatever the resolution}
        SELF := TSamView(TView.CREATE(object, heap, itsPanel, itsExtent,
            TPrintManager.CREATE(NIL, heap), margins, FALSE, res, TRUE));

        SELF.window := TSamWindow(itsPanel.window);
        {$IFC fTrace}EP;{$ENDC}
    END;

    {$IFC fDebugMethods}
    PROCEDURE {TSamView.}Fields{(PROCEDURE Field(nameAndType: S255))};
    BEGIN
        SUPERSELF.Fields(Field);
        Field('window: TSamWindow');
        Field('');
    END;
    {$ENDC}

{ return the first/last row/column that needs updating;
    0 <= firstRow <= numRows+1;
    0 <= firstCol <= numCols+1;
    0 <= lastRow <= numRows;
    0 <= lastCol <= numCols }
    PROCEDURE {TSamView.}GetDrawLimits{(VAR firstRow, firstCol, lastRow, lastCol: INTEGER)};
        VAR r:          LRect;
    BEGIN
        {$IFC fTrace}BP(11);{$ENDC}
        r := thePad.visLRect;

        SELF.LPtToRowCol(firstRow, firstCol, r.topLeft);

        { NOTE: in QuickDraw, r.botRight is not inside the rectangle, so move it by 1 pixel }
        WITH r DO
            SetLPt(botRight, botRight.h-1, botRight.v-1);

        SELF.LPtToRowCol(lastRow, lastCol, r.botRight);
        lastRow := Min(lastRow, numRows);
        lastCol := Min(lastCol, numCols);
        {$IFC fTrace}EP;{$ENDC}
    END;

{ just make believe it was a mouse press; the selection code does nothing if the same box is selected }
    PROCEDURE {TSamView.}MouseMove{(mouseLPt: LPoint)};
    BEGIN
        {$IFC fTrace}BP(11);{$ENDC}
        SELF.MousePress(mouseLPt);
        {$IFC fTrace}EP;{$ENDC}
    END;

{ Calculate the new selected box, and make the selection in both panels }
    PROCEDURE {TSamView.}MousePress{(mouseLPt: LPoint)};
    VAR rowIndex:   INTEGER;
        colIndex:   INTEGER;
        panel:      TPanel;
        samWindow:  TSamWindow;
    BEGIN
        {$IFC fTrace}BP(12);{$ENDC}
        panel := SELF.panel;
        samWindow := TSamWindow(panel.window);

        SELF.LPtToRowCol(rowIndex, colIndex, mouseLPt);
        IF (rowIndex < 1) OR (rowIndex > 8) OR (colIndex < 1) OR (colIndex > 8) THEN
            rowIndex := 0; { this signifies no selection }

        samWindow.Select(panel, rowIndex, colIndex);
        {$IFC fTrace}EP;{$ENDC}
    END;

END;


METHODS OF TSamCBView;

    FUNCTION  {TSamCBView.}CREATE{(object: TObject; heap: THeap; itsPanel: TPanel
                                itsBoxH, itsBoxV: INTEGER): TSamCBView};
    VAR r:          LRect;
    BEGIN
        {$IFC fTrace}BP(11);{$ENDC}
        IF object = NIL THEN
            object := NewObject(heap, THISCLASS);

                        {allow for 1 pixel all the way around}
        SetLRect(r, 0, 0, numCols*itsBoxH+2, numRows*itsBoxV+2);

        SELF := TSamCBView(TSamView.CREATE(object, heap, r, itsPanel));

        WITH SELF DO
            BEGIN
            boxH := itsBoxH;
            boxV := itsBoxV;
            allowMouseOutside := TRUE; {because of row/column headings}
            END;
        {$IFC fTrace}EP;{$ENDC}
    END;

    {$IFC fDebugMethods}
    PROCEDURE {TSamCBView.}Fields{(PROCEDURE Field(nameAndType: S255))};
    BEGIN
        SUPERSELF.Fields(Field);
        Field('boxH:    INTEGER');
        Field('boxV:    INTEGER');
        Field('');
    END;
    {$ENDC}

{ Draw all the boxes; this routine optimizes the drawing }
    PROCEDURE {TSamCBView.}Draw;
    VAR r:          LRect;
        CB:         TCheckerboard;
        rowIndex:   INTEGER;
        colIndex:   INTEGER;
        firstRow:   INTEGER;
        lastRow:    INTEGER;
        firstCol:   INTEGER;
        lastCol:    INTEGER;
    BEGIN
        {$IFC fTrace}BP(11);{$ENDC}
        { draw thin black line around whole checkerboard }
        PenNormal;
        r := SELF.extentLRect;
        FrameLRect(r);

        SELF.GetDrawLimits(firstRow, firstCol, lastRow, lastCol);

        CB := SELF.window.CB;

        { Draw the necessary squares; compare this loop with that in the text view }
        FOR rowIndex := firstRow TO lastRow DO
            FOR colIndex := firstCol TO lastCol DO  { index through the row }
                IF (rowIndex = 0) OR (colIndex = 0) THEN
                    SELF.DrawHeading(rowIndex, colIndex)
                ELSE
                    SELF.DrawBox(CB.GetColor(rowIndex, colIndex), rowIndex, colIndex);
        {$IFC fTrace}EP;{$ENDC}
    END;

{ Draw one box in a given color; this is a method so that someone can easily change the
        way in which boxes are drawn and/or colored.}
    PROCEDURE {TSamCBView.}DrawBox{(itsColor: TColor; rowIndex, colIndex: INTEGER)};
    VAR tempBox:    LRect;
        LPat:       LPattern;
    BEGIN
        {$IFC fTrace}BP(11);{$ENDC}
        { The tool kit makes no assumptions about the pen.  You set it when you want to use it. }
        PenNormal;
        PenSize(3,2);

        { Calculate the rectangle to fill }
        SELF.SetUpBox(rowIndex, colIndex,tempBox);

        IF LRectIsVisible(tempBox) THEN      { only draw boxes that need to be drawn }
            BEGIN
            CASE itsColor OF
                colorWhite:     LPat := lPatWhite;
                colorLtGray:    LPat := lPatLtGray;
                colorGray:      LPat := lPatGray;
                colorDkGray:    LPat := lPatDkGray;
                colorBlack:     LPat := lPatBlack;
                OTHERWISE       LPat := lPatWhite;  {should not happen}
                END;

            FillLRect(tempBox, LPat);
            FrameLRect(tempBox);
            END;
        {$IFC fTrace}EP;{$ENDC}
    END;

{ Draw a row or column heading.}
    PROCEDURE {TSamCBView.}DrawHeading{(rowIndex, colIndex: INTEGER)};
    VAR tempBox:    LRect;
        str:        S255;
        fInfo:      FontInfo;
        target:     LPoint;
    BEGIN
        {$IFC fTrace}BP(11);{$ENDC}
        {either rowIndex=0 or colIndex=0}
        {$IFC fDbgOK}
        IF (rowIndex <> 0) AND (colIndex <> 0) THEN
            ABCBreak('(rowIndex <> 0) AND (colIndex <> 0)', 0);
        {$ENDC}

        IF rowIndex <> colIndex THEN {if both are 0, nothing to do}
            BEGIN
            { The tool kit makes no assumptions about the pen.  You set it when you want to use it. }
            PenNormal;
            PenSize(1, 1);
            TextFont(fIDSystem);
            TextFace([]);
            GetFontInfo(fInfo);

            IntToStr(rowIndex + colIndex, @str);

            { Calculate the rectangle to fill }
            SELF.SetUpBox(rowIndex, colIndex, tempBox);

            IF LRectIsVisible(tempBox) THEN      { only draw boxes that need to be drawn }
                BEGIN
                WITH tempBox DO
                    IF rowIndex = 0 THEN
                        SetLPt(target, (left + right) DIV 2,
                                       top + ((bottom - top) DIV 4))
                    ELSE
                        SetLPt(target, left + ((right - left) DIV 4),
                                       (top + bottom) DIV 2);

                WITH target DO
                    BEGIN
                    h := h - (StringWidth(str) DIV 2);
                    v := v + ((fInfo.ascent - fInfo.descent) DIV 2);
                    END;

                MoveToL(target.h, target.v);
                DrawString(str);

                IF rowIndex = 0 THEN
                    InsetLRect(tempBox, 1, 0)
                ELSE
                    InsetLRect(tempBox, 0, 1);
                FrameLRect(tempBox);
                END;
            END;
        {$IFC fTrace}EP;{$ENDC}
    END;

{ return a new selection appropriate to this view }
    FUNCTION  {TSamCBView.}NewSelection{(rowIndex, colIndex: INTEGER): TSamSelection};
    BEGIN
        {$IFC fTrace}BP(11);{$ENDC}
        NewSelection := TSamCBSelection.CREATE(NIL, SELF.Heap, SELF, rowIndex, colIndex);
        {$IFC fTrace}EP;{$ENDC}
    END;

{ Decide what Row-Column box is indicated by this point; return FALSE iff not in checkerboard
    or in row/column headings }
    PROCEDURE {TSamCBView.}LPtToRowCol{(VAR rowIndex, colIndex: INTEGER; lPt: LPoint)};
    VAR r:  LRect;
    BEGIN
        {$IFC fTrace}BP(11);{$ENDC}
        { r is the rectangle containing the checkerboard }
        r := SELF.extentLRect;
        InsetLRect(r, -boxH, -boxV);
        LRectHaveLPt(r, lPt);

        WITH SELF DO
            BEGIN
            rowIndex := ((lPt.v+boxV-1) DIV boxV); {the -1 allows for the border}
            colIndex := ((lPt.h+boxH-1) DIV boxH);
            END;
      {$IFC fTrace}EP;{$ENDC}
    END;

{ Create a rectangle, for this Row-Column index. }
    PROCEDURE {TSamCBView.}SetUpBox{(rowIndex, colIndex: INTEGER; VAR bbox: LRect)};
    BEGIN
        {$IFC fTrace}BP(11);{$ENDC}
        WITH SELF, bbox DO
            BEGIN
            left := boxH * (colIndex - 1) + 1; {extra pixel for border}
            top := boxV * (rowIndex -1) + 1; {extra pixel for border}
            right := left + boxH;
            bottom := top + boxV;
            END;
        {$IFC fTrace}EP;{$ENDC}
    END;

END;

METHODS OF TSamTXView;

    FUNCTION  {TSamTXView.}CREATE{(object: TObject; heap: THeap; itsPanel: TPanel): TSamTXView};
    VAR longStr:    S255;
        fInfo:      FontInfo;
    BEGIN
        {$IFC fTrace}BP(11);{$ENDC}
        IF object = NIL THEN
            object := NewObject(heap, THISCLASS);

        { use a temporary extent rect, until we can calculate the correct one }
        SELF := TSaMTXView(TSamView.CREATE(object, heap, zeroLRect, itsPanel));

        PushFocus;
        itsPanel.window.Focus;
        SELF.SetPen;                                { set pen assumes that focus has been done }
        GetFontInfo(fInfo);

        SELF.GetBoxString(1, 1, MAXINT, longStr);   { use an unknown color to get longest string }

        WITH fInfo, SELF, extentLRect DO
            BEGIN
            lineHeight := ascent + descent + leading + 1;
            lineAscent := ascent + (leading DIV 2) - 1;
      {$H-} right := StringWidth(longStr) + 2*txMargin; {$H+}
            bottom := (numRows*numCols*lineHeight);
            END;

        PopFocus;
        {$IFC fTrace}EP;{$ENDC}
    END;

    {$IFC fDebugMethods}
    PROCEDURE {TSamTxView.}Fields{(PROCEDURE Field(nameAndType: S255))};
    BEGIN
        SUPERSELF.Fields(Field);
        Field('lineHeight:    INTEGER');
        Field('lineAscent:    INTEGER');
        Field('');
    END;
    {$ENDC}

{ return the string used to describe a row/col/color combination; this is a method so
    people can override it if needed }
    PROCEDURE {TSamTxView.}GetBoxString{(rowIndex, colIndex: INTEGER; color: INTEGER;
                                         VAR str: S255)};
    VAR tmpStr: S255;
    BEGIN
        {$IFC fTrace}BP(11);{$ENDC}
        IntToStr(rowIndex, @tmpStr);
        str := CONCAT('R', tmpStr, '-');

        IntToStr(colIndex, @tmpStr);
        str := CONCAT(str, 'C', tmpStr, ' ');

        CASE color OF
            colorWhite:     tmpStr := 'White';
            colorLtGray:    tmpStr := 'Light Gray';
            colorGray:      tmpStr := 'Gray';
            colorDkGray:    tmpStr := 'Dark Gray';
            colorBlack:     tmpStr := 'Black';
            OTHERWISE       tmpStr := 'Unknown Color';     {should not happen, except for
                                                             determining the longest possible string}
            END;
        str := CONCAT(str, tmpStr);
        {$IFC fTrace}EP;{$ENDC}
    END;

{ Draw all the boxes; this routine optimizes the drawing }
    PROCEDURE {TSamTXView.}Draw;
    VAR samWindow:  TSamWindow;
        rowIndex:   INTEGER;
        colIndex:   INTEGER;
        c1,c2:      INTEGER;
        firstRow:   INTEGER;
        lastRow:    INTEGER;
        firstCol:   INTEGER;
        lastCol:    INTEGER;
        color:      TColor;
    BEGIN
        {$IFC fTrace}BP(11);{$ENDC}

        SELF.GetDrawLimits(firstRow, firstCol, lastRow, lastCol);

        samWindow := SELF.window;

        { do first row }
        FOR colIndex := firstCol TO numcols DO
            BEGIN
            color := samWindow.CB.GetColor(firstRow, colIndex);
            SELF.DrawName(color, firstRow, colIndex);
            END;

        { do middle rows }
        FOR rowIndex := firstRow+1 to lastRow-1 DO
            FOR colIndex := 1 to numcols DO
                BEGIN
                color := samWindow.CB.GetColor(rowIndex, colIndex);
                SELF.DrawName(color, rowIndex, colIndex);
                END;

        { do last row }
        FOR colIndex := 1 to lastCol DO
            BEGIN
            color := samWindow.CB.GetColor(lastRow, colIndex);
            SELF.DrawName(color, lastRow, colIndex);
            END;

        {$IFC fTrace}EP;{$ENDC}
    END;

{ Draw one box as a text string. }
    PROCEDURE {TSamTXView.}DrawName{(itsColor: TColor; rowIndex, colIndex: INTEGER)};
    VAR tempBox:    LRect;
        str:        S255;
    BEGIN
        {$IFC fTrace}BP(11);{$ENDC}
        { The tool kit makes no assumptions about the pen.  You set it when you want to use it. }
        SELF.SetPen;

        { Calculate the rectangle to fill }
        SELF.SetUpBox(rowIndex, colIndex,tempBox);

        IF LRectIsVisible(tempBox) THEN      { only draw boxes that need to be drawn }
            BEGIN
            SELF.GetBoxString(rowIndex, colIndex, itsColor, str);

            MoveToL(tempBox.left + txMargin, tempBox.top + SELF.lineAscent);
            DrawString(str);
            END;
        {$IFC fTrace}EP;{$ENDC}
    END;

    FUNCTION  {TSamView.}NewSelection{(rowIndex, colIndex: INTEGER): TSamSelection};
    BEGIN
        {$IFC fTrace}BP(11);{$ENDC}
        NewSelection := TSamTxSelection.CREATE(NIL, SELF.Heap, SELF, rowIndex, colIndex);
        {$IFC fTrace}EP;{$ENDC}
    END;

{ Decide what Row-Column box is indicated by this point; return FALSE iff not in checkerboard }
    PROCEDURE {TSamTXView.}LPtToRowCol{(VAR rowIndex, colIndex: INTEGER; lPt: LPoint)};
    VAR index:  INTEGER;
    BEGIN
        {$IFC fTrace}BP(11);{$ENDC}
        WITH SELF DO
            index := (lPt.v DIV lineHeight);         { index should be in [0..numrows*numcols-1] }

        IF index < 0 THEN
            BEGIN
            rowIndex := 0;
            colIndex := 0;
            END
        ELSE IF index > (numRows*numCols)-1 THEN
            BEGIN
            rowIndex := numRows + 1;
            colIndex := numCols + 1;
            END
        ELSE
            BEGIN
            { convert index to row/col }
            rowIndex := (index DIV numcols) + 1;    { rowIndex is in [1..numrows] }
            colIndex := (index MOD numcols) + 1;    { colIndex is in [1..numcols] }
            END;
      {$IFC fTrace}EP;{$ENDC}
    END;

{ Set up the pen for drawing the characters; assumes that we are focused on the right window
        (or a pad in the right window); this is a method so it can be overridden }
    PROCEDURE {TSamTXView.}SetPen;
    BEGIN
      {$IFC fTrace}BP(10);{$ENDC}
      PenNormal;
      TextFont(fIDSystem);        { system font }
      TextFace([]);
      {$IFC fTrace}EP;{$ENDC}
    END;

{ Create a rectangle, for this Row-Column index. }
    PROCEDURE {TSamTXView.}SetUpBox{(rowIndex, colIndex: INTEGER; VAR bbox: LRect)};
    VAR index:  INTEGER;            { number of the line to draw; [0..numrow*numcols-1] }
    BEGIN
        {$IFC fTrace}BP(11);{$ENDC}
        index := numcols*(rowIndex-1) + colIndex-1; { index is in [0..numcols*numrows-1] }

        { use same left and right as view , calculate the top and bottom }
        WITH SELF, bbox DO
            BEGIN
            left := SELF.extentLRect.left;
            top := (index * lineHeight);
            right := SELF.extentLRect.right;
            bottom := top + lineHeight;
            END;
        {$IFC fTrace}EP;{$ENDC}
    END;

END;

METHODS OF TSamSelection;

    FUNCTION  {TSamSelection.}CREATE{(object: TObject; heap: THeap; itsView: TSamView;
                                   selRow, selCol: INTEGER): TSamSelection};
    BEGIN
        {$IFC fTrace}BP(12);{$ENDC}
        IF object = NIL THEN
            object := NewObject(heap, THISCLASS);

        SELF := TSamSelection(TSelection.CREATE(object, heap, itsView, samKind, zeroLPt));
        WITH SELF DO
            BEGIN
            row := selRow;
            col := selCol;
            END;

        {$H-}
        itsView.SetupBox(selRow, selCol, SELF.boundLRect);
        {$H+}
        {$IFC fTrace}EP;{$ENDC}
    END;

    {$IFC fDebugMethods}
    PROCEDURE {TSamSelection.}Fields{(PROCEDURE Field(nameAndType: S255))};
    BEGIN
        SUPERSELF.Fields(Field);
        Field('row:    INTEGER');
        Field('col:    INTEGER');
        Field('');
    END;
    {$ENDC}

{ set up the state of all the menu items before the user gets to see them }
    FUNCTION   {TSamSelection.}CanDoCommand{(cmdNumber: TCmdNumber; VAR checkIt: BOOLEAN): BOOLEAN};
    VAR enable:         BOOLEAN;
        cmd:            INTEGER;
        samSelection:   TSamSelection;
        color:          TColor;

    BEGIN
        {$IFC fTrace}BP(10);{$ENDC}
        enable := SELF.kind <> nothingKind;
        IF enable THEN
            color := TSamWindow(SELF.window).CB.GetColor(SELF.row, SELF.col);

        CASE cmdNumber OF
            uWhite, uLtGray, uGray,
            uDkGray, uBlack:
                BEGIN
                CanDoCommand := enable;
                checkIt := enable AND (CvtCmdNumber(cmdNumber) = color);
                END;

            OTHERWISE
                CanDoCommand := SUPERSELF.CanDoCommand(cmdNumber, checkIt);
            END;
        {$IFC fTrace}EP;{$ENDC}
    END;

    FUNCTION  {TSamSelection.}NewCommand{(cmdNumber: TCmdNumber): TCommand};
    VAR samWindow:  TSamWindow;
        oldColor:   TColor;
    BEGIN
        {$IFC fTrace}BP(11);{$ENDC}
        CASE cmdNumber OF
            uWhite, uLtGray, uGray, uDkGray, uBlack:
                            { This is the only Menu command that needs to be handled by this Application }
                BEGIN
                samWindow := TSamWindow(SELF.window);
                WITH SELF DO {$H-}
                    oldColor := samWindow.CB.GetColor(row, col);

                {don't do anything of old & new colors are the same}
                IF oldColor = CvtCmdNumber(cmdNumber) THEN
                    NewCommand := NIL
                ELSE
                    NewCommand := TRecolorCommand.CREATE(NIL, SELF.Heap, samWindow, SELF, cmdNumber);
                END;

            OTHERWISE     { The ABCs do the rest! }
                NewCommand := TSelection.NewCommand(cmdNumber);
            END;
        {$IFC fTrace}EP;{$ENDC}
    END;

END;


METHODS OF TSamCBSelection;

    FUNCTION  {TSamCBSelection.}CREATE{(object: TObject; heap: THeap; itsView: TView;
                                          selRow, selCol: INTEGER): TSamCBSelection};
    BEGIN
        {$IFC fTrace}BP(12);{$ENDC}
        IF object = NIL THEN
            object := NewObject(heap, THISCLASS);

        SELF := TSamCBSelection(TSamSelection.CREATE(object, heap, itsView, selRow, selCol));
        {$IFC fTrace}EP;{$ENDC}
    END;

{ Required to highlight the indication in the already-focused pad }
    PROCEDURE {TSamCBSelection.}Highlight{(highTransit: THighTransit)};
    VAR r:          LRect;
        samView:    TSamView;
    BEGIN
        {$IFC fTrace}BP(12);{$ENDC}
        IF HighTransit <> hNone THEN
            BEGIN

            { This application frames the box to indicate it is selected }

            { Make r be the rectangle to frame }
            samView := TSamView(SELF.view);
            samView.SetupBox(SELF.row, SELF.col, r);

            IF LRectIsVisible(r) THEN    { only do highlight if part of the frame is visible }
                BEGIN
                { Choose an appropriate pen mode for the highlight transition }
                { The highPen array assumes you want black for on, gray for dim, and that both are xor'ed }
                SetPenState(highPen[highTransit]);
                PenSize(1, 1);
                FrameLRect(r);
                END;
            END;
        {$IFC fTrace}EP;{$ENDC}
    END;

END;

METHODS OF TSamTXSelection;

    FUNCTION  {TSamTXSelection.}CREATE{(object: TObject; heap: THeap; itsView: TView;
                                          selRow, selCol: INTEGER): TSamSelection};
    BEGIN
        {$IFC fTrace}BP(12);{$ENDC}
        IF object = NIL THEN
            object := NewObject(heap, THISCLASS);

        SELF := TSamTXSelection(TSamSelection.CREATE(object, heap, itsView, selRow, selCol));
        {$IFC fTrace}EP;{$ENDC}
    END;

{ Required to highlight the indication in the already-focused pad }
    PROCEDURE {TSamTXSelection.}Highlight{(highTransit: THighTransit)};
    VAR r:          LRect;
        samView:    TSamView;
    BEGIN
        {$IFC fTrace}BP(12);{$ENDC}
        IF HighTransit <> HNone THEN
            BEGIN

            { This application inverts the text string to indicate it is selected; when deactivated,
                    the highlighting changes to a thin black rectangle }

            { Make r be the rectangle to invert }
            samView := TSamView(SELF.view);
            samView.SetupBox(SELF.row, SELF.col, r);

            IF LRectIsVisible(r) THEN    { only do highlighting if part of the rectangle is visible }
                BEGIN
                PenNormal;
                PenSize(1, 1);

                IF (highTransit = hOffToDim) OR (highTransit = hDimToOff) THEN
                    FrameLRect(r)
                ELSE
                    BEGIN
                    IF (highTransit = hOnToDim) OR (highTransit = hDimToOn) THEN
                        InsetLRect(r, 1, 1);
                    InvrtLRect(r);
                    END;
                END;
            END;
        {$IFC fTrace}EP;{$ENDC}
    END;

END;

METHODS OF TSamWindow;

    FUNCTION  {TSamWindow.}CREATE{(object: TObject; heap: THeap; itsWmgrId: TWindowId): TWindow};
    BEGIN
        {$IFC fTrace}BP(10);{$ENDC}
        IF object = NIL THEN
            object := NewObject(heap, THISCLASS);

        SELF := TSamWindow(TWindow.CREATE(object, heap, itsWmgrID, TRUE));
        WITH SELF DO
            BEGIN
            CB := NIL;
            showRowColHeads := FALSE;
            cbPanel := NIL;
            txPanel := NIL;
            END;
        {$IFC fTrace}EP;{$ENDC}
    END;

    {$IFC fDebugMethods}
    PROCEDURE {TSamWindow.}Fields{(PROCEDURE Field(nameAndType: S255))};
    BEGIN
        SUPERSELF.Fields(Field);
        Field('CB:    TCheckerboard');
        Field('showRowColHeads:    BOOLEAN');
        Field('cbPanel:            TPanel');
        Field('txPanel:            TPanel');
        Field('');
    END;
    {$ENDC}

{ Create a blank document }
    PROCEDURE  {TSamWindow.}BlankStationery;
    VAR board:      TCheckerboard;
        panel:      TPanel;
        aView:      TSamView;

    BEGIN
        {$IFC fTrace}BP(11);{$ENDC}
        board := TCheckerboard.CREATE(NIL, SELF.heap);
        board.ColorAlternating(colorDkGray, colorWhite);

        SELF.CB := board;

        panel := TPanel.CREATE(NIL, SELF.heap, SELF, 2*boxH, 2*boxV,
                                [aScroll, aSplit], [aScroll, aSplit]);
        SELF.cbPanel := panel;

        { ... and its view }
        aView := TSamCBView.CREATE(NIL, SELF.Heap, panel, boxH, boxV);

        { ... then the Text panel (other 3/8 of horizontal space) ... }
        panel := panel.Divide(h, -38, percentFromEdge, [userCanResizeIt, windowCanResizeIt],
                                0, [aScroll, aSplit], [aScroll]);
        SELF.txPanel := panel;

        { ... and its view ...}
        aView := TSamTxView.CREATE(NIL, SELF.Heap, panel);

        {$IFC fTrace}EP;{$ENDC}
    END;

{$IFC ColHeadings}
{ set up the state of all the menu items before the user gets to see them }
    FUNCTION   {TSamWindow.}CanDoCommand{(cmdNumber: TCmdNumber; VAR checkIt: BOOLEAN): BOOLEAN};
    BEGIN
        {$IFC fTrace}BP(10);{$ENDC}
        CASE cmdNumber OF
            uShowHeads, uHideHeads:
                BEGIN
                CanDoCommand := TRUE;
                checkIt := (cmdNumber = uShowHeads) = SELF.showRowColHeads;
                END;
            OTHERWISE
                CanDoCommand := SUPERSELF.CanDoCommand(cmdNumber, checkIt);
            END;
        {$IFC fTrace}EP;{$ENDC}
    END;
{$ENDC}

    PROCEDURE {TSamWindow.}InvalidateBox{(rowIndex, colIndex: INTEGER)};
    VAR s:          TListScanner;
        panel:      TPanel;
        samView:    TSamView;
        r:          LRect;
    BEGIN
        {$IFC fTrace}BP(12);{$ENDC}
        s := SELF.panels.Scanner;
        WHILE s.Scan(panel) DO
            BEGIN
            samView := TSamView(panel.view);
            samView.SetupBox(rowIndex, colIndex, r);
            panel.InvalLRect(r);
            END;
        {$IFC fTrace}EP;{$ENDC}
    END;

{$IFC ColHeadings}
    FUNCTION  {TSamWindow.}NewCommand{(cmdNumber: TCmdNumber): TCommand};
        VAR hSize:  INTEGER;
            vSize:  INTEGER;
    BEGIN
        {$IFC fTrace}BP(11);{$ENDC}
        CASE cmdNumber OF
            uShowHeads, uHideHeads:
                BEGIN
                NewCommand := NIL;

                IF cmdNumber = uShowHeads THEN
                    BEGIN
                    hSize := boxH DIV 2;
                    vSize := boxV DIV 2;
                    END
                ELSE
                    BEGIN
                    hSize := -1;
                    vSize := -1;
                    END;

                SELF.cbPanel.ShowSideBand(v, TRUE, vSize, -boxV);
                SELF.cbPanel.ShowSideBand(h, TRUE, hSize, -boxH);
                SELF.cbPanel.ShowSideBand(v, FALSE, vSize, -boxV);
                SELF.cbPanel.ShowSideBand(h, FALSE, hSize, -boxH);

                SELF.showRowColHeads := cmdNumber = uShowHeads;
                END;

            OTHERWISE     { The ABCs do the rest! }
                NewCommand := SUPERSELF.NewCommand(cmdNumber);
            END;
        {$IFC fTrace}EP;{$ENDC}
    END;
{$ENDC}

{ make the selection in both panels; rowSel=0 means de-select; this routine does nothing if the
    old and new selections are the same }
    PROCEDURE {TSamWindow.}Select{(selectPanel: TPanel; rowSel, colSel: INTEGER)};
    VAR needToSelect:   BOOLEAN;        { TRUE iff old and new selections are different }
        s:              TListScanner;
        panel:          TPanel;
        highTransit:    THighTransit;
        samView:        TSamView;
        aSelection:     TSelection;
    BEGIN
        {$IFC fTrace}BP(12);{$ENDC}
        aSelection := SELF.selectPanel.selection;
        WITH aSelection DO
            IF kind = nothingKind THEN
                needToSelect := rowSel<>0
            ELSE
                needToSelect := (rowSel<>TSamSelection(aSelection).row) OR
                                (colSel<>TSamSelection(aSelection).col);

        IF needToSelect THEN
            BEGIN
            s := SELF.panels.Scanner;
            WHILE s.Scan(panel) DO
                BEGIN
                { un-highlight the old selection }
                panel.Highlight(panel.selection, hOnToOff);

                samView := TSamView(panel.view);
                IF rowSel=0 THEN
                    aSelection := samView.NoSelection
                ELSE
                    aSelection := samView.NewSelection(rowSel, colSel);

                aSelection := panel.selection.FreedAndReplacedBy(aSelection);

                { highlight the new selection }
                highTransit := hOffToOn;
                panel.Highlight(panel.selection, hOffToOn);

                IF panel = selectPanel THEN
                    panel.BeSelectPanel(TRUE);
                END;
            END;
        {$IFC fTrace}EP;{$ENDC}
    END;

END;


METHODS OF TRecolorCommand;

    FUNCTION  {TRecolorCommand.}CREATE{(object: TObject; heap: THeap; itsWindow: TSamWindow;
                                     currSelection: TSamSelection;
                                     itsCmdNumber: TCmdNumber): TRecolorCommand};
    VAR oColor:     TColor;
        phase:      TCmdPhase;
    BEGIN
        {$IFC fTrace}BP(10);{$ENDC}
        IF object = NIL THEN
            object := NewObject(heap, THISCLASS);

        SELF := TRecolorCommand(TCommand.CREATE(object, heap, itsCmdNumber, NIL, TRUE, revealAll));
        WITH SELF, currSelection DO
            BEGIN
            selPanel := panel;
            rowChanged := row;
            colChanged := col;
      {$H-} newColor := CvtCmdNumber(itsCmdNumber);
            oColor := itsWindow.CB.GetColor(row, col); {$H+}
            END;

        WITH SELF DO
            BEGIN
            window := itsWindow; {be careful: currSelection has a window field also}
            oldColor := oColor;
            END;
        {$IFC fTrace}EP;{$ENDC}
    END;

    {$IFC fDebugMethods}
    PROCEDURE {TRecolorCommand.}Fields{(PROCEDURE Field(nameAndType: S255))};
    BEGIN
        SUPERSELF.Fields(Field);
        Field('window:         TSamWindow');
        Field('selPanel:       TPanel');
        Field('rowChanged:     INTEGER');
        Field('colChanged:     INTEGER');
        Field('oldColor:       Byte');
        Field('newColor:       Byte');
        Field('');
    END;
    {$ENDC}

    PROCEDURE {TRecolorCommand.}Perform{(cmdPhase: TCmdPhase)};
    VAR samWindow:      TSamWindow;
    BEGIN
        {$IFC fTrace}BP(12);{$ENDC}
        samWindow := SELF.window;

        samWindow.selectPanel.selection.MarkChanged;

        WITH SELF DO {$H-}
            samWindow.InvalidateBox(rowChanged, colChanged); {$H+}

        WITH SELF DO {$H-}
            CASE cmdPhase OF
                redoPhase, doPhase: samWindow.CB.SetColor(rowChanged, colChanged, newColor);
                undoPhase:          samWindow.CB.SetColor(rowChanged, colChanged, oldColor);
                END;
        {$IFC fTrace}EP;{$ENDC}
    END;

END;


ÿ