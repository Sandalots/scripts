EXEC(ToolName,ToolNumber,ToolVol,Force,Intrinsic,SysLibPrefix,TKLibPrefix,GotXFile,Ln,LastOBJ,temp)
    {
    Arguments:
        ToolName     -- (eg. 'SAMPLE') used to find all the other file names;
                            required; if not supplied, it is prompted for
        ToolNumber   -- Office System tool number (defaults to value read from build file -- see below)
        ToolVol      -- volume on which to put resulting tool (eg. 'NEWDISK')
                            (defaults to value read from build file -- see below)
        Force        -- if not null, force everything to happen (assembles, compiles, links, ...)
        Intrinsic    -- pathname of INTRINSIC.LIB to use (defaults to '-#BOOT-INTRINSIC.LIB')
        SysLibPrefix -- in the linker input, what to prefix IOSPASLIB and SYS1LIB by
                            (defaults to '-#BOOT-'; note that the dashes are required)
        TKLibPrefix  -- in the linker input, what to prefix TKLIB and TK2LIB by
                            (defaults to '-#BOOT-; note that the dashes are required)

    This is the EXEC file for building a Toolkit application.  Given an application 'SAMPLE'
    you will create a number of files:
        PSAMPLE -- phrase file
        USAMPLE -- primary PASCAL unit
        MSAMPLE -- PASCAL main program
        XSAMPLE -- build parameters (optional, but usually will be there)

    The XSAMPLE file describes what other files are needed by the application.  Its consists of the
    following:
        (Any lines beginning with ';' are comments.  Lines beginning with '$' mark the end of lists.
         A line beginning with '~' is read after stripping off the first '~'.)

        The first 2 lines should contain the tool number and tool volume for the tool.  These will be
            used if you do not supply either of the values when you run this exec file.
        Next is a list of files to assemble (ended by a line starting with '$', of course).  You should
            omit the .TEXT extension.
        Next is a list of other units to compile.  The 'USAMPLE' and 'MSAMPLE' files are always
            compiled & linked in, but if your application requires other Pascal units, they should
            be listed here.  You should omit the .TEXT extension.
        Next is a list of other object files to link in (ended by a '$' line).  Files you listed to be
            assembled and compiled are automatically linked in.  In addition, IOSPASLIB, SYS1LIB,
            TKLIB, and  TK2LIB are automatically linked in.  (This would be needed, for example, if
            you use the StdUnit unit, which requires that SULIB.OBJ be linked in.)  You can also
            specify any linker options here.  Lines that begin with '+' or '=' are assumed to be linker
            options, with the leading '=' changed to a '-' before passing it onto the linker.
        Finally there are some arguments to the InstallTool program (ended by a '$' line).  The exec
            file automatically supplies the tool number and tool volume.  Your files should supply
            all the answers starting with the question about your application handling documents.
            (The EXEC file does no checking to see that you supply correct values.)  The exec file
            always does the install unless: (1) there is no Desktop Catalog on the volume containing the
            tool or (2) you do not supply any install parameters in the XSAMPLE file.

    Given an assembler or compiler input file with the name 'FOO', the EXEC file assumes that 'FOO.TEXT'
        is the input file and that it may have include files 'FOO2.TEXT', 'FOO3.TEXT', ...  For
        PASCAL files, 'FOO.TEXT' is assumed to contain the interface and 'FOOxxx.TEXT' the implementation.

    The EXEC file always does the minimum possible (unless you invoke it with the Force option).
        If the object files for an assembler module exists and has a later date than the source file,
        it is not assembled.  The PASCAL files are assumed to depend on one another; if the interface
        of one unit changes, all the remaining files are compiled unconditionally.
    }

DEFAULT Intrinsic TO '-#BOOT-INTRINSIC.LIB'
DEFAULT SysLibPrefix TO '-#BOOT-'
DEFAULT TKLibPrefix TO '-#BOOT-'

REWRITE CommBufr,'TK Make'          {we will write in here all files to be linked in}

IF ToolName = '' THEN
    REQUEST ToolName WITH 'Tool Name (eg. ''SAMPLE'')? '
ENDIF

{Open the X<name> file}
IF Exists("X[ToolName].TEXT") THEN
    SET GotXFile TO 'T'
    RESET file,"X[ToolName].TEXT"
ELSE
    SET GotXFile TO 'F'
ENDIF

{Initialization}
IF GotXFile = 'T' THEN
    SET Ln TO <-#BOOT-TK/XRead
    DEFAULT ToolNumber TO Ln
    SET Ln TO <-#BOOT-TK/XRead
    DEFAULT ToolVol TO Ln
ENDIF

SET ToolName TO UpperCase(ToolName)
SET ToolNumber TO UpperCase(ToolNumber)
IF ToolVol <> '' THEN
    SET ToolVol TO "-[ToolVol]-"
ELSE
    RUN '-#BOOT-TK/WorkDir'
    SET ToolVol TO RETSTR
    CLEAR SCREEN
ENDIF
IF Force <> '' THEN
    SET Force TO '3'
ELSE
    SET Force TO '1'
ENDIF

{Recompile phrase file if needed}
IF ToolNumber <> '' THEN
    SET temp TO 'T'
    IF NOT (Exists("P[ToolName].TEXT") AND Exists("-#BOOT-TK/PABC.TEXT")) THEN
        SET temp TO 'F'
    ELSEIF Exists("[ToolVol]{T[ToolNumber]}PHRASE") THEN
        IF (Newer("[ToolVol]{T[ToolNumber]}PHRASE","P[ToolName].TEXT")) AND
           (Newer("[ToolVol]{T[ToolNumber]}PHRASE","-#BOOT-TK/PABC.TEXT")) THEN
            SET temp TO 'F'
        ENDIF
    ENDIF
    IF temp = 'T' THEN
        WRITELN "Need to generate [ToolVol]{T[ToolNumber]}PHRASE"
        $R{}-#BOOT-TK/Alert
        $P[ToolName]
        $[ToolVol]~{T[ToolNumber]~}
    ENDIF
ENDIF

{Assemble files as needed}
IF GotXFile = 'T' THEN
    REPEAT
        SET Ln TO <-#BOOT-TK/XRead
        IF Ln <> '$' THEN
            SET temp TO <-#BOOT-TK/UnitChanged([Ln],[Force])
            IF temp <> '0' THEN                         {file exists -- needs to be linked in}
                WRITELN (CommBufr) Ln
            ENDIF
            IF temp >= '2' THEN
                WRITELN "Need to assemble [Ln].TEXT"
                $A{ssemble}[Ln]
                ${No listing}
                $[Ln]
            ENDIF
        ENDIF
    UNTIL Ln = '$'
ENDIF

{Compile files as needed}
SET LastOBJ TO ''
IF GotXFile = 'T' THEN
    REPEAT
        SET Ln TO <-#BOOT-TK/XRead
        IF Ln <> '$' THEN
            SET temp TO <-#BOOT-TK/UnitChanged([Ln],[Force],[LastOBJ])
            IF temp <> '0' THEN
                WRITELN (CommBufr) Ln
                SET LastOBJ TO Ln
            ENDIF
            IF temp >= '2' THEN
                SET Force TO temp
                SUBMIT -#BOOT-TK/Comp([Ln],[Intrinsic])
            ELSEIF (Force >= '2') AND (temp <> '0') THEN
                WRITELN "Need to change write date on [Ln].OBJ"
                $R{}-#BOOT-TK/NullChange
                $[Ln].OBJ
            ENDIF
        ENDIF
    UNTIL Ln = '$'
ENDIF

{Compile U<name> as needed}
SET temp TO <-#BOOT-TK/UnitChanged(U[ToolName],[Force],[LastOBJ])
IF temp <> '0' THEN
    WRITELN (CommBufr) "U[ToolName]"
    SET LastOBJ TO "U[ToolName]"
ENDIF
IF temp >= '2' THEN
    SET Force TO temp
    SUBMIT -#BOOT-TK/Comp(U[ToolName],[Intrinsic])
ELSEIF (Force >= '2') AND (temp <> '0') THEN
    WRITELN "Need to change write date on U[ToolName].OBJ"
    $R{}-#BOOT-TK/NullChange
    $U[ToolName].OBJ
ENDIF

{Compile M<name> as needed}
SET temp TO <-#BOOT-TK/UnitChanged(M[ToolName],[Force],[LastObj])
IF temp <> '0' THEN
    WRITELN (CommBufr) "M[ToolName]"
    SET LastOBJ TO "M[ToolName]"
ENDIF
IF temp >= '2' THEN
    SET Force TO temp
    SUBMIT -#BOOT-TK/Comp(M[ToolName],[Intrinsic])
ELSEIF (Force >= '2') AND (temp <> '0') THEN
    WRITELN "Need to change write date on M[ToolName].OBJ"
    $R{}-#BOOT-TK/NullChange
    $M[ToolName].OBJ
ENDIF

{Set temp to output file name}
IF ToolNumber = '' THEN
    SET temp TO "[ToolVol][ToolName].OBJ"
ELSE
    SET temp TO "[ToolVol]{T[ToolNumber]}OBJ"
ENDIF

{If output file does not exists or is earlier than last .OBJ file, we need to link}
IF Exists("[temp]") THEN
    IF Exists("[LastOBJ].OBJ") THEN
        IF Newer("[LastOBJ].OBJ","[temp]") THEN
            SET Force TO '2'
        ENDIF
    ENDIF
ENDIF

IF NOT (Exists("[temp]")) OR (Force >= '2') THEN
    $F{ile-Mgr}D{elete}[temp]
    $Y{es}Q{uit}
    WRITELN "Need to link [temp]"

    $L{ink}?
    $+W
    $[Intrinsic]
    {See if there are any options & get other files to link in}
    IF GotXFile = 'T' THEN
        REPEAT
            SET Ln TO <-#BOOT-TK/XRead
            IF COPY(Ln,1,1)='+' THEN
                $[Ln]
             ELSEIF COPY(Ln,1,1)='=' THEN
                SET Ln TO CONCAT('-', COPY(Ln,2,Length(Ln)-1))
                $[Ln]
             ELSEIF Ln <> '$' THEN
                WRITELN (CommBufr) Ln
            ENDIF
        UNTIL Ln = '$'
    ENDIF
    $
    $[TKLibPrefix]TKLIB
    $[TKLibPrefix]TK2LIB
    $[SysLibPrefix]SYS1LIB
    $[SysLibPrefix]IOSPASLIB

    RESET CommBufr,'TK Make'
    REPEAT
        READLN (CommBufr) Ln
        IF Ln <> 'EOF' THEN
            $[Ln]
        ENDIF
    UNTIL Ln = 'EOF'

    ${No more files}
    ${No list file}
    $[temp].
ELSE
    {Did not link; still need to flush the linker input section}
    IF GotXFile = 'T' THEN
        REPEAT
            SET Ln TO <-#BOOT-TK/XRead
        UNTIL Ln = '$'
    ENDIF
ENDIF

{Install the tool; we always do this}
IF Exists("[ToolVol]{!Catalog}") THEN
    IF GotXFile = 'T' THEN
        SET Ln TO <-#BOOT-TK/XRead
        IF (Ln <> '$') AND (ToolNumber <> '') THEN
            WriteLn "Installing [ToolVol]{T[ToolNumber]}OBJ"
            $R{}InstallTool
            $Y{es, automatically overwrite}
            SET temp TO COPY(ToolVol,2,Length(ToolVol)-2)
            $[temp]
            $[ToolNumber]
            WHILE (Ln <> '$') DO
                $[Ln]
                SET Ln TO <-#BOOT-TK/XRead
            ENDWHILE
        ENDIF
    ENDIF
ENDIF

IF GotXFile = 'T' THEN
    CLOSE file
ENDIF
CLOSE CommBufr,'TK Make'
ENDEXEC
ÿ